0010 REM "CSC - Configurator Entry"
0020 REM "Program CSC.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "MAS * DCF * WA# 205 * BASIS"
0040 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 ENTER FIRM$,ITEM$,IVM01_DEV
0100 REM " --- Open Files"
0105 LET FILES=13
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="SYS-01",FILES$[2]="CSM-01",FILES$[3]="CSM-02"
0120 LET FILES$[4]="CSM-03",FILES$[5]="CSM-04",FILES$[6]="CSM-05"
0130 LET FILES$[7]="IVS-10",FILES$[8]="IVM-10",FILES$[9]="IVM-03"
0140 LET FILES$[10]="IVM-02",FILES$[11]="CSM-13"
0145 LET FILES$[12]="IVM-24",FILES$[13]="IVM-14"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],CSM01_DEV=CHANNELS[2],CSM02_DEV=CHANNELS[3]
0165 LET CSM03_DEV=CHANNELS[4],CSM04_DEV=CHANNELS[5],CSM05_DEV=CHANNELS[6]
0170 LET IVS10_DEV=CHANNELS[7],IVM10_DEV=CHANNELS[8],IVM03_DEV=CHANNELS[9]
0180 LET IVM02_DEV=CHANNELS[10],CSM13_DEV=CHANNELS[11]
0190 LET IVM24_DEV=CHANNELS[12],IVM14_DEV=CHANNELS[13]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$(1),A6$(1),A[ALL]
0220 IVM02A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0280 IVM10A: IOLIST X0$(1)
0285 GLS01A: IOLIST G0$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0290 IVS10D: IOLIST I5$,I6$(1),I7$(1)
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 IVM03: IOLIST X0$(1)
0340 CSM13H: IOLIST CSH0$,CSH1$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 DIM G[4]
0425 FIND (SYS01_DEV,KEY=FIRM$+"GL00",DOM=9800)IOL=GLS01A
0430 LET G[0]=NUM(G2$(1,2)),G[3]=NUM(G2$(7,2)),G[4]=NUM(G2$(9,2))
0435 FIND (SYS01_DEV,KEY=FIRM$+"IV00",DOM=9800)IOL=IVS01A
0500 REM " --- Initializations"
0510 LET L2=8,L9=21,L=L2
0520 DIM ITEM$(20)
0550 LET WIDTH=64,HEIGHT=8,WIN_X=3,HEADING$="Product Configurator",WIN_Y=9,LL=HEIGHT-4
0560 DIM FOOTING$(WIDTH-2); LET FOOTING$(1)="<F3>=Lookup"
0700 REM " --- Background"
0710 CALL "SYC.WA",MODE,WIDTH,HEIGHT,WIN_X,WIN_Y,HEADING$,NAME$
0740 PRINT 'SB'
0750 PRINT @(1,0),"Product",@(9),"Platform",@(18),"OS Level",@(27),"Prod Rev",@(36),"Type"
0760 PRINT 'SF'
1300 REM " --- Product"
1320 LET V0$="S",V1$="KER^",V2$=ITEM$(1,3),V3$="",V0=3,V1=3,V2=2,I0=0
1330 LET V4$="Enter a Valid Product (<F3>=Lookup)"
1340 GOSUB 7000
1350 ON V3 GOTO 1380,1380,1399,1355,3500
1355 CALL "CSC.LK",FILES$[2],V1,V2,V$,V0,30,"Product IDs"
1380 IF V$="" THEN GOTO 1300
1385 LET ITEM$(1,3)=V$
1390 FIND (CSM01_DEV,KEY=FIRM$+ITEM$(1,3),DOM=1300)*,PROD_DESC$,PROD_INF$
1395 PRINT @(V1,V2),ITEM$(1,3),@(1,4),'CL',PROD_DESC$
1399 GOTO 3200
1400 REM " --- Platform"
1420 LET V0$="S",V1$="KER^",V2$=ITEM$(4,3),V3$="",V0=3,V1=11,V2=2
1430 LET V4$="Enter a Valid Platform (<F3>=Lookup)"
1440 GOSUB 7000
1450 ON V3 GOTO 1480,1480,1499,1455,3500
1455 CALL "CSC.LL",FILES$[11],V1,V2,V$,V0,30,"Platform IDs","A"+ITEM$(1,3),FILES$[3],"",""
1480 IF V$="" THEN GOTO 1400
1485 LET ITEM$(4,3)=V$
1490 FIND (CSM02_DEV,KEY=FIRM$+ITEM$(4,3),DOM=1400)*,PLAT_DESC$
1492 LET T0$="A"+ITEM$(1,3),T1$=ITEM$(4,3); GOSUB 10000; IF FOUND$<>"YES" THEN GOTO 1400
1495 PRINT @(V1,V2),ITEM$(4,3),@(1,4),'CL',PLAT_DESC$
1499 GOTO 3200
1500 REM " --- OS Level"
1520 LET V0$="S",V1$="KER^",V2$=ITEM$(7,3),V3$="",V0=3,V1=20,V2=2
1530 LET V4$="Enter a Valid OS Level (<F3>=Lookup)"
1540 GOSUB 7000
1550 ON V3 GOTO 1580,1580,1599,1555,3500
1555 CALL "CSC.LL",FILES$[11],V1,V2,V$,V0,30,"OS Levels","B"+ITEM$(4,3),FILES$[4],FILES$[11],ITEM$(1,3)
1580 IF V$="" THEN GOTO 1500
1585 LET ITEM$(7,3)=V$
1590 FIND (CSM03_DEV,KEY=FIRM$+ITEM$(7,3),DOM=1500)*,OSLVL_DESC$,PORTID$; LET PORTID$=PORTID$(1,5)
1592 LET T0$="H"+ITEM$(7,3),T1$=ITEM$(1,3); GOSUB 10000; IF FOUND$<>"YES" THEN GOTO 1500
1593 LET T0$="B"+ITEM$(4,3),T1$=ITEM$(7,3); GOSUB 10000; IF FOUND$<>"YES" THEN GOTO 1500
1595 PRINT @(V1,V2),ITEM$(7,3),@(1,4),'CL',OSLVL_DESC$
1599 GOTO 3200
1600 REM " --- Product Revision"
1620 LET V0$="S",V1$="KER^",V2$=ITEM$(10,3),V3$="",V0=3,V1=29,V2=2
1630 LET V4$="Enter a Valid Product Revision (<F3>=Lookup)"
1640 GOSUB 7000
1650 ON V3 GOTO 1680,1680,1699,1655,3500
1655 CALL "CSC.LL",FILES$[11],V1,V2,V$,V0,30,"Product Revision","C"+ITEM$(7,3),FILES$[5],"",""
1680 IF V$="" THEN GOTO 1600
1685 LET ITEM$(10,3)=V$
1690 FIND (CSM04_DEV,KEY=FIRM$+ITEM$(10,3),DOM=1600)*,PRODREV_DESC$
1692 LET T0$="C"+ITEM$(7,3),T1$=ITEM$(10,3); GOSUB 10000; IF FOUND$<>"YES" THEN GOTO 1600
1695 PRINT @(V1,V2),ITEM$(10,3),@(1,4),'CL',PRODREV_DESC$
1699 GOTO 3200
1700 REM " --- License Type"
1720 LET V0$="S",V1$="KER^",V2$=ITEM$(13,3),V3$="",V0=3,V1=37,V2=2
1730 LET V4$="Enter a Valid License Type (<F3>=Lookup)"
1740 GOSUB 7000
1750 ON V3 GOTO 1770,1770,1799,1755,3500
1755 CALL "CSC.LL",FILES$[11],V1,V2,V$,V0,30,"License Types","E"+ITEM$(1,3),FILES$[6],"",""
1756 rem CALL "CSC.LK",FILES$[6],V1,V2,V$,V0,30,"License Types"
1770 IF V$="" THEN GOTO 1700
1775 FIND (CSM05_DEV,KEY=FIRM$+V$,DOM=1700)*,LIC_DESC$,LIC_2$
1780 IF LIC_2$(1,1)="N" THEN LET V4$="This type is not active <Enter>"; GOSUB 7000; GOTO 1700
1782 LET T0$="E"+ITEM$(1,3),T1$=V$; GOSUB 10000; IF FOUND$<>"YES" THEN GOTO 1700
1785 LET ITEM$(13,3)=V$
1795 PRINT @(V1,V2),ITEM$(13,3),@(1,4),'CL',LIC_DESC$
1799 GOTO 3200
3200 REM " --- Sequence control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3500
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1300,1400,1500,1600,1700,4000
3500 REM "EXIT without finishing
3510 LET ITEM$=""
3590 GOTO 9900
4000 REM " --- Does Item exist?"
4010 FIND (IVM01_DEV,KEY=FIRM$+ITEM$,DOM=4100)
4090 GOTO 9900
4100 REM " --- Create the item & bill?
4110 LET V0$="Y",V1$="CE^",V2$="",V3$="",V4$="Create This Item <Y/N>: ",V0=1,V2=5,V1=44
4120 GOSUB 7000
4130 IF V$="N" THEN LET ITEM$=""; GOTO 9900
4140 IF V$<>"Y" THEN GOTO 3400
4300 REM " --- Create Item"
4350 DIM A0$(22)
4370 LET A0$(1)=FIRM$+ITEM$
4400 DIM A1$(60),A2$(64),A3$(40),A4$(21),A6$(9*G[4]),A[12]
4405 DIM A5$(2); LET A5$(1,2)="YY"; REM Configured Item/Commissionable
4410 LET A1$(1)=CVS(PROD_DESC$(1,11)+" "+PLAT_DESC$(1,11)+" "+OSLVL_DESC$(1,11)+" "+PRODREV_DESC$(1,11)+" "+LIC_DESC$(1,11),32)
4412 LET V0$="",V1$="",V2$=A1$,V3$="",V4$="Description?",V0=60,V1=1,V2=4
4414 GOSUB 7000; IF V3 THEN GOTO 4410
4416 LET A1$(1)=V$; IF CVS(V$,2)="" THEN GOTO 4410
4420 DIM I6$(32),I7$(6*G[4]),X0$(32)
4430 LET A2$(4,5)="EAEAY",A4$(1)="N",A[0]=1,A2$(24,5)="IADDD",A2$(19,2)="NN"
4435 LET A2$(56,1)=PROD_INF$(5,1),A2$(57,1)=PROD_INF$(6,1); IF LIC_2$(2,1)="N" THEN LET A2$(56,1)="N"
4440 LET A3$(21)=A0$(3),A[0]=1
4450 FIND (IVS10_DEV,KEY=FIRM$+"D",DOM=4455)IOL=IVS10D
4455 LET A2$(1,11)=I6$(1,11),A2$(19,2)=I6$(12,2),A2$(21,3)=I6$(14,3)
4460 LET A2$(24,5)=I6$(17,5),A2$(53,3)=I6$(25,3)
4465 LET A6$(1,10)=I7$(1,10),A6$(11,10)=I7$(11,10),A6$(21,10)=I7$(51,10),A6$(31,10)=I7$(41,10),A6$(41,10)=I7$(21,10),A6$(51,10)=I7$(31,10)
4467 LET A2$(1,3)=ITEM$(1,3); REM Set Product Type to Configurator Product ID
4470 FIND (IVM10_DEV,KEY=FIRM$+"A"+A2$(1,3),DOM=4480)IOL=IVM10A
4475 LET A2$(18,1)=X0$(27,1)
4480 WRITE (IVM01_DEV,KEY=A0$)IOL=IVM01A
4485 REM " --- Create Port ID Synonym"
4490 DIM X$(42); LET X$(1)=A0$,X$(23)=PORTID$
4495 WRITE (IVM24_DEV,KEY=X$)X$
4497 LET X$(1)=A0$(1,2)+PORTID$,X$(23)=A0$(3)
4498 WRITE (IVM14_DEV,KEY=X$)X$
4500 REM " --- Create warehouse record
4505 LET B0$=A0$(1,2)+I3$(6,2)+A0$(3)
4510 DIM B1$(64),B[20],I5$(25)
4520 LET B1$(15,1)="N",B1$(56,5)="ADDDN"
4550 FIND (IVS10_DEV,KEY=FIRM$+"D",DOM=4560)IOL=IVS10D
4560 LET B1$(13,2)=PROD_INF$(2,2),B1$(19,3)=I6$(9,3),B1$(56,4)=I6$(18,4)
4580 WRITE (IVM02_DEV,KEY=B0$)IOL=IVM02A
4600 REM " --- Update IVM-03 Xrefs"
4605 DIM X0$(26)
4610 LET X0$(1)=A0$(1,2)+"A"+A2$(1,3)+A0$(3)
4615 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03
4620 DIM X0$(37)
4630 LET X0$(1)=B0$(1,2)+"B"+B0$(3,2)+B1$(11,2)+B1$(1,10)+B0$(5)
4635 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03
4640 DIM X0$(28)
4645 LET X0$(1)=B0$(1,2)+"C"+B0$(3,2)+A2$(1,3)+B0$(5)
4650 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03
4655 DIM X0$(37)
4660 LET X0$(1)=B0$(1,2)+"F"+B1$(19,3)+B1$(22,6)+A2$(1,3)+B0$(5)+B0$(3,2)
4665 IF A2$(24,1)="W" THEN WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03
4670 DIM X0$(25)
4675 LET X0$(1)=FIRM$+"G"+B0$(5)+B0$(3,2)
4680 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03
4685 DIM X0$(29)
4690 LET X0$(1)=FIRM$+"D"+B1$(22,6)+B0$(5)
4695 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03
4700 DIM X0$(43)
4705 LET X0$(1)=FIRM$+"E"+A1$(1,20)+A0$(3)
4710 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03
4715 DIM X0$(29)
4720 LET X0$(1)=FIRM$+"H"+B0$(5)+B1$(22,6)
4725 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03
4730 DIM X0$(43)
4735 LET X0$(1)=FIRM$+"I"+A3$(21,20)+A0$(3)
4740 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03
4745 DIM X0$(26)
4750 LET X0$(1)=FIRM$+"J"+A2$(21,3)+A0$(3)
4755 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03
4760 DIM X0$(26)
4765 LET X0$(1)=FIRM$+"T"+A2$(53,3)+A0$(3)
4770 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03
4775 DIM X0$(43)
4780 LET X0$(1)=FIRM$+"U"+A3$(1,20)+A0$(3)
4785 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03
4900 REM " --- Return
4990 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM 
9910 FOR I=1 TO FILES
9920 CLOSE (CHANNELS[I],ERR=9921)
9930 NEXT I
9950 CALL "SYC.WD",NAME$
9990 EXIT 
9999 END
10000 REM " --- Check configurator relationships
10005 LET FOUND$=""
10010 READ (CSM13_DEV,KEY=FIRM$+T0$,DOM=10020)
10020 LET K$=KEY(CSM13_DEV,END=10090)
10030 IF K$(1,6)<>FIRM$+T0$ THEN GOTO 10090
10040 READ (CSM13_DEV)IOL=CSM13H
10050 IF CSH1$(1,3)<>T1$ THEN GOTO 10020
10060 LET FOUND$="YES"
10090 RETURN 

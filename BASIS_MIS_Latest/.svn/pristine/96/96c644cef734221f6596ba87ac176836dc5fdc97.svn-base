0010 REM "SYM - Printer Listing"
0020 REM "Program SYM.GC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 SYM07A: IOLIST A0$(1),A1$(1),A2$(1)
0220 SYM17A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1)
0500 REM " --- Initializations"
0510 DIM HEADINGS$[2]
0550 LET L9=50,L=L9+1,WIDTH=80,PAGE=0,HEADINGS=2,WHEN$=F0$(7,3),CLOCK$=""
0560 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$+" Listing",START$="",FINISH$=""
0570 LET PAGEBREAK$="N",WIN_WIDTH=32,WIN_HEIGHT=6,COLUMN=19,NUMBER=0
0580 LET WIN_Y=10,WIN_X=INT((80-WIN_WIDTH)/2),TITLE$="Report Selections"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIN_WIDTH,WIN_HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT 'SB',@(1,0),"Beginning Printer",@(4,1),"Ending Printer",@(8,2),"Page Break",'SF',
0800 REM " --- Options"
0810 GOSUB 6000
0820 CALL "SYC.WD",NAME$
0900 REM " --- Position File"
0910 CALL "SYC.GA",7,1,"","",STATUS
0920 IF STATUS THEN GOTO 9900
0930 CALL "SYC.NB","Printing",2,COLUMN
0980 READ (SYM07_DEV,KEY=START$,DOM=1000)IOL=SYM07A
0990 GOTO 1100
1000 REM " --- Main Read"
1010 LET K$=KEY(SYM07_DEV,END=4000)
1020 READ (SYM07_DEV,END=4000)IOL=SYM07A
1100 REM " --- Display milestone"
1110 PRINT @(COLUMN,11),A0$,
1500 REM " --- Printer subheading"
1510 IF PAGEBREAK$="Y" THEN LET L=L9+1
1520 GOSUB 5200
1530 LET NUMBER=NUMBER+1
1900 REM " --- Position detail file"
1990 READ (SYM17_DEV,KEY=A0$,DOM=2000)
2000 REM " --- Read next detail record"
2010 LET K$=KEY(SYM17_DEV,END=3000)
2020 IF POS(A0$=K$)<>1 THEN GOTO 3000
2030 READ (SYM17_DEV)IOL=SYM17A
2500 REM " --- Print detail line"
2510 IF L+2>L9 THEN GOSUB 5200
2520 LET L=L+2,X$=" Standard"
2530 IF B3$(1,1)="C" THEN LET X$="Compressed"
2540 PRINT (7)@(1),B0$(3),@(4),B1$,@(66),X$
2550 PRINT (7)@(4),B2$
2900 REM " --- Next font"
2990 GOTO 2000
3900 REM " --- Next printer"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF L+2>L9 THEN GOSUB 5000
4020 PRINT (7)""
4030 PRINT (7)NUMBER," Printer(s)"
4090 GOTO 9900
5000 REM " --- Report heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5090 RETURN 
5200 REM " --- Printer subheading"
5210 IF L+6>L9 THEN GOSUB 5000
5220 PRINT (7)""
5230 PRINT (7)FILL(WIDTH,"-")
5240 PRINT (7)"Printer ID: ",A0$," ",A1$(1,30),@(59),"Initial Form Feed? ",A1$(31,1)
5250 PRINT (7)FILL(WIDTH,"-")
5260 PRINT (7)"Opt Description/Font Hex Codes",@(61),"Compressed/Standard"
5270 LET L=L+5
5290 RETURN 
6000 REM " --- Beginning value"
6010 LET V0$="S",V1$="C",V2$=START$,V3$=""
6020 LET V4$="<F1>=First   <F3>=Lookup",V0=2,V1=COLUMN,V2=0
6030 GOSUB 7000
6040 ON V3 GOTO 6100,6050,6000,6090,6500
6050 LET START$=""
6060 PRINT @(V1,V2),"First",'CL',
6070 GOTO 6200
6090 GOSUB 6900
6100 REM 
6110 IF V$="" THEN GOTO 6050
6120 LET START$=V$+"  ",START$=START$(1,2)
6130 PRINT @(V1,V2),START$,'CL',
6200 REM " --- Ending value"
6210 LET V0$="S",V1$="C",V2$=FINISH$,V3$=""
6220 LET V4$="<F1>=Last   <F3>=Lookup",V0=2,V1=COLUMN,V2=1
6230 GOSUB 7000
6240 ON V3 GOTO 6300,6250,6000,6290,6500
6250 LET FINISH$=""
6260 PRINT @(V1,V2),"Last",'CL',
6270 GOTO 6400
6290 GOSUB 6900
6300 REM 
6310 IF V$="" THEN GOTO 6250
6320 LET FINISH$=V$+"  ",FINISH$=FINISH$(1,2)
6340 PRINT @(V1,V2),FINISH$,'CL',
6400 REM " --- Page break?"
6410 LET V0$="Y",V1$="C",V2$=PAGEBREAK$,V3$="",V0=1,V1=COLUMN,V2=2
6420 LET V4$="Page Break Between Printers?"
6430 GOSUB 7000
6440 ON V3 GOTO 6450,6400,6200,6400,6500
6450 LET PAGEBREAK$=V$
6500 REM " --- Correct?"
6505 LET V1$="First",V2$="Last"
6510 IF FNP$(START$)<>"" THEN LET V1$=START$
6515 IF FNP$(FINISH$)<>"" THEN LET V2$=FINISH$
6520 PRINT @(COLUMN,0),V1$,'CL',@(COLUMN,1),V2$,'CL',@(COLUMN,2),PAGEBREAK$,'CL',
6525 LET V0$="Y",V1$="",V2$="Y",V3$="",V4$="Is The Above Correct (Y/N)?"
6530 LET V0=1,V1=MAX(INT((WIN_WIDTH-LEN(V4$))/2)-1,0),V2=WIN_HEIGHT-3
6535 PRINT @(V1,V2),V4$,
6540 LET V1=V1+LEN(V4$),V4$=""
6550 GOSUB 7000
6560 PRINT @(0,V2),'CL',
6570 ON V3 GOTO 6580,6000,6000,6000,9900
6580 ON POS(V$="YN") GOTO 6500,6590,6000
6590 RETURN 
6900 REM " --- Standard windowed lookup"
6910 LET RECORD$="",CHANNEL=SYM07_DEV,TITLE$="ADD+ON System Printers"
6930 LET DESCRIPTION$[0]="ID",FIELD[0]=1,POSITION[0]=1,LENGTH[0]=2
6940 LET DESCRIPTION$[1]="Description",FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6950 LET ROW=V2+2,COLUMN=79-LENGTH[0]-LENGTH[1]-NUMBER*4,V$="NO COMPANY"
6980 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

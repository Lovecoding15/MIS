0010 REM "ARE - Cash Receipts Ribbon Entry"
0020 REM "Program ARE.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "25Nov98 WGH: Delete extranenous code (1015,1220)
0040 REM 
0080 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 ARM01A: IOLIST A0$(1),A1$
0220 ARM10C: IOLIST Y0$
0230 ARE01A1: IOLIST C0$(1),C[0]
0240 ARE01A2: IOLIST W0$,W[ALL]
0250 ARE11A: IOLIST W0$,W[ALL]
0260 ARE21A: IOLIST W0$,W[ALL]
0270 ARE31A: IOLIST X0$,X[ALL]
0900 REM " --- Init File
0990 READ (ARE01_DEV,KEY=C0$(1,8),DOM=1000)
1000 REM " --- Key
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(ARE01_DEV,END=1050)
1030 IF V2$(1,8)<>C0$(1,8) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(9,P[0])
1050 LET V0$="M",V1$="KEC",V4$="Enter A Valid Customer Number (<F3>=Lookup)",V3$=P1$,V0=6,V1=15,V2=5
1060 GOSUB 7000
1070 IF V3=2 OR V3=4 THEN GOTO 9800
1080 IF V3=3 THEN GOTO 2000
1090 IF V$="" THEN GOTO 0900
1100 LET A0$(3)=V$
1200 REM " --- Find Key
1210 FIND (ARM01_DEV,KEY=A0$(1,K9),DOM=1000)IOL=ARM01A
1230 LET C0$(9,6)=V$
1300 REM " --- Display
1310 GOSUB 5000
1320 PRINT @(0,L0),'CE',
1390 GOTO 3000
2000 REM " --- Alpha Search
2040 CALL "SYC.LC",V$
2060 IF V$="" THEN GOTO 1020
2090 GOTO 1100
3000 REM " --- Trans
3010 GOSUB 6400
3015 LET C0$(15)=FILL(15),C[0]=0
3020 LET V0$="S",V1$="KCE",V2$=C0$(15,3),V0=3,V1=15,V2=6,I0=0,V4$="Enter A Valid Cash Receipt Code (<F3>=Lookup)"
3030 GOSUB 7000
3031 IF V3<>3 THEN GOTO 3035
3032 LET V4=1,V5=ARM10_DEV,V6=4,V7=3,V8=7,V9=20,V5$="C"
3033 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
3034 IF V$="" THEN GOTO 3000
3035 IF V3=2 THEN GOTO 1000
3037 IF V3=4 THEN GOTO 9800
3040 LET C0$(15,3)=V$
3050 FIND (ARM10_DEV,KEY=N0$+"C"+C0$(15,3),DOM=3000)IOL=ARM10C
3060 PRINT @(V1,V2),Y0$(4,3),@(V1+M0+1),Y0$(7,20),
3090 GOTO 3900
3100 REM " --- Check #
3120 LET V0$="S",V1$="",V2$=C0$(18,7),V0=7,V1=15,V2=7
3140 GOSUB 7000
3150 IF V3=2 THEN GOTO 3900
3160 LET C0$(18,7)=V$,C0$(25,1)=FILL(1)
3180 FIND (ARE01_DEV,KEY=C0$(1,25),DOM=3300)IOL=ARE01A1
3200 GOSUB 5200
3240 GOTO 4000
3300 REM " --- New Record
3310 CALL "SYC.YN",1,"Is This A New Record?",2,V$,V3
3340 ON POS(V$="YN") GOTO 3300,3400,3000
3350 LET C0$(26)="",C[0]=0
3400 REM " --- Cash/Check
3410 IF C0$(26,1)=" " THEN LET C0$(26,1)="C"
3420 LET V0$="S",V1$="C",V2$=C0$(26,1),V3$="",V4$="C=Check     $=Cash",V0=1,V1=46,V2=7,I0=2
3440 GOSUB 7000
3444 IF V$="" THEN GOTO 3450
3446 IF POS(V$="C$")=0 THEN GOTO 3400
3450 IF V$="" THEN LET V$="C"; PRINT @(35,7),"C",
3460 LET C0$(26,1)=V$
3470 IF C0$(26,1)="$" THEN LET C0$(27,10)=""
3490 GOTO 3900
3500 REM " --- ABA #
3510 IF C0$(26,1)="$" THEN GOTO 3900
3520 LET V0$="S",V1$="C",V2$=C0$(27,10),V3$="",V0=10,V1=O[7],V2=7
3530 LET V4$="Enter The ABA Number For This Check"
3540 GOSUB 7000
3550 ON V3 GOTO 3560,3500,3900,3500,3900
3560 LET C0$(27,10)=V$
3590 GOTO 3900
3600 REM " --- Amount
3610 IF Y0$(27,1)="N" THEN PRINT @(25,21),"No Cash Amount Can Be Entered",; GOTO 3900
3620 LET V0$="N",V1$="",V2$=STR(C[0]),V3$=M1$,V0=M1,V1=15,V2=8
3630 LET V4$="Enter The Amount Of This Transaction"
3640 GOSUB 7000
3650 ON V3 GOTO 3660,3600,3900,3600,3900
3660 LET C[0]=V
3690 GOTO 3900
3900 REM " --- Next Field
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 3000,3100,3400,3500,3600,4000
4000 REM " --- Options
4010 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)? ",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4050 ON POS(V$="YN") GOTO 4000,4100,3000
4100 REM " --- Write
4110 WRITE (ARE01_DEV,KEY=C0$(1,25))IOL=ARE01A1
4150 IF Y0$(29,1)="G" THEN RUN "ARE.AD"
4190 RUN "ARE.AC"
4200 REM " --- Delete
4210 READ (ARE21_DEV,KEY=C0$(1,25),DOM=4220)
4230 LET K$=KEY(ARE21_DEV,END=4390)
4250 IF K$(1,24)<>C0$(1,24) THEN GOTO 4390
4260 READ (ARE21_DEV)IOL=ARE21A
4270 LET X0$=C0$(1,4)+" "+C0$(9,6)+W0$(26,7)
4280 EXTRACT (ARE31_DEV,KEY=X0$,DOM=4320)IOL=ARE31A
4290 LET X[0]=X[0]-W[0],X[1]=X[1]-W[1]
4300 IF X[0]=0 THEN IF X[1]=0 THEN REMOVE (ARE31_DEV,KEY=X0$)
4310 IF X[0]<>0 OR X[1]<>0 THEN WRITE (ARE31_DEV,KEY=X0$)IOL=ARE31A
4320 REMOVE (ARE21_DEV,KEY=K$)
4330 GOTO 4230
4400 REM 
4420 READ (ARE11_DEV,KEY=C0$(1,25),DOM=4430)
4430 LET K$=KEY(ARE11_DEV,END=4590)
4450 IF K$(1,24)<>C0$(1,24) THEN GOTO 4590
4460 READ (ARE11_DEV)IOL=ARE11A
4470 LET X0$=C0$(1,4)+" "+C0$(9,6)+W0$(26,7)
4480 EXTRACT (ARE31_DEV,KEY=X0$,DOM=4520)IOL=ARE31A
4490 LET X[0]=X[0]-W[0],X[1]=X[1]-W[1]
4500 IF X[0]=0 THEN IF X[1]=0 THEN REMOVE (ARE31_DEV,KEY=X0$)
4510 IF X[0]<>0 OR X[1]<>0 THEN WRITE (ARE31_DEV,KEY=X0$)IOL=ARE31A
4520 REMOVE (ARE11_DEV,KEY=K$)
4530 GOTO 4430
4600 REM 
4620 REMOVE (ARE01_DEV,KEY=C0$(1,25),DOM=4630)
4630 LET K$=KEY(ARE01_DEV,END=4790)
4650 IF K$(1,24)<>C0$(1,24) THEN GOTO 4790
4660 READ (ARE01_DEV)IOL=ARE01A2
4670 LET X0$=C0$(1,4)+" "+C0$(9,6)+W0$(26,7)
4680 EXTRACT (ARE31_DEV,KEY=X0$,DOM=4720)IOL=ARE31A
4690 LET X[0]=X[0]-W[0],X[1]=X[1]-W[1]
4700 IF X[0]=0 THEN IF X[1]=0 THEN REMOVE (ARE31_DEV,KEY=X0$)
4710 IF X[0]<>0 OR X[1]<>0 THEN WRITE (ARE31_DEV,KEY=X0$)IOL=ARE31A
4720 REMOVE (ARE01_DEV,KEY=K$)
4730 GOTO 4630
4890 GOTO 1000
4900 REM " --- Not Correct
4910 GOSUB 6400
4920 GOTO 3000
5000 REM " --- Display
5010 PRINT @(15,5),FNF$(A0$(3,P[0]),M0$)," ",A1$(1,30),
5090 RETURN 
5200 REM " --- Display Trans
5210 PRINT @(46,7),C0$(26,1),@(O[7],7),C0$(27,10),@(15,8),C[0]:M1$,
5290 RETURN 
6200 REM " --- Continue
6210 LET V0$="S",V1$="E",V2$="",V3$="",V4$="Press <Enter> To Continue:",V0=1,V1=FNV(V4$),V2=22
6220 GOSUB 7000
6240 LET L=L0
6290 RETURN 
6400 REM " --- Clear Entry Screen
6410 PRINT @(15,6),'CL',@(15,7),FILL(8),@(46,7)," ",@(O[7],7),'CL',@(15,8),FILL(M1),@(O[7],8),'CL',
6490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Return to 1st overlay"
9890 RUN "ARE.AA"
9900 RUN "SYS.AA"
9999 END

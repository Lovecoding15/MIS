0010 REM "ARR - CUSTOMER LEDGER CARD - II"
0020 REM "Program: ARR.9B"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLISTs"
0210 ARM01A: IOLIST A0$,A1$
0250 ART01A: IOLIST E0$,E[ALL]
0340 ART11A: IOLIST X0$,X1$,X[ALL]
0500 REM " --- Init Data"
0510 LET H0=40,H1=70,H=0,H1$=N4$,WIDTH=80,CLOCK$="",HEADINGS=3
0520 LET L9=59,L=L9+1,T0=0,T1=0
0530 LET M1=LEN(M1$); IF M1>12 THEN LET M1$="########.00-"; LET M1=12
0540 DIM X[1],W[2],B[6],E[3],HEADINGS$[HEADINGS]
0545 LET W0$="  "
0550 LET H2$="Customer: "
0555 IF P5$="" THEN LET H2$=H2$+"First" ELSE LET H2$=H2$+FNF$(P5$,M0$)
0560 LET H2$=H2$+" Through "
0565 IF P6$="" THEN LET H2$=H2$+"Last" ELSE LET H2$=H2$+FNF$(P6$,M0$)
0570 LET H2$=H2$+", Dates Through "+FNB$(P7$)
0580 LET HEADINGS$[0]=N1$,HEADINGS$[1]=H1$,HEADINGS$[2]=H2$
0610 CALL "SYC.GA",7,1,"","",STATUS
0620 IF STATUS THEN GOTO 9900
0700 REM " --- Background"
0720 CALL "SYC.NB","Now Printing...",M0,COLUMN
1000 REM " --- Init Read"
1010 READ (ARM01_DEV,KEY=N0$+P5$,DOM=1011)IOL=ARM01A; GOTO 1111
1100 REM " --- Main Read
1110 READ (ARM01_DEV,END=4000)IOL=ARM01A
1120 IF A0$(1,2)>N0$ THEN GOTO 4000
1130 IF P6$<>"" THEN IF A0$(3,P[0])>P6$ THEN GOTO 4000
1150 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$),
1200 REM " --- Open Invoice"
1230 READ (ART01_DEV,KEY=N0$+W0$+A0$(3,6),DOM=1231)
1300 REM " --- Find & Print"
1310 LET K$=KEY(ART01_DEV,END=2900)
1320 IF K$(1,10)<>N0$+W0$+A0$(3,6) THEN GOTO 2900
1330 READ (ART01_DEV)IOL=ART01A
1335 IF E0$(24,3)>P7$ THEN READ (ART01_DEV,KEY=E0$(1,17)+$FF$,DOM=1310)
1337 IF T9$<>N0$+W0$+A0$(3,6) THEN GOSUB 5500
1340 IF L+1>L9 THEN GOSUB 5500
1350 PRINT (7)@(4),E0$(11,7)," ",FNC$(E0$(24,3))," ",FNC$(E0$(27,3))," ",E0$(22,2),@(30),E0$(33,1),@(80-M1*4),E[0]:M1$,@(80-M1),E[1]:M1$
1360 LET L=L+1
1370 LET W[0]=0,L1=0
1415 READ (ART11_DEV,KEY=E0$(1,17),DOM=1420)
1420 LET K$=KEY(ART11_DEV,END=1530)
1430 IF K$(1,17)<>E0$(1,17) THEN GOTO 1530
1440 READ (ART11_DEV)IOL=ART11A
1450 IF X1$="" THEN GOTO 1420
1460 GOSUB 5700
1480 IF X1$(1,1)=" " THEN EXITTO 1530
1485 IF X1$(2,3)>P7$ THEN GOTO 1510
1490 LET W[0]=W[0]+X[0]+X[1],T2=T2+X[0],T3=T3+X[1]
1520 GOTO 1420
1530 IF L1<1 THEN GOTO 1560
1535 IF L+1>L9 THEN GOSUB 5000
1540 PRINT (7)@(19),"***Total:",@(80-M1*2),E[0]+W[0]:M1$
1550 LET L=L+1
1560 LET T0=T0+E[0]+W[0],T1=T1+E[0]
1570 GOTO 1310
2900 REM " --- End Customer"
2910 IF T9$<>N0$+W0$+A0$(3,6) THEN GOTO 2960
2915 IF L+2>L9 THEN GOSUB 5000
2920 PRINT (7)@(80-M1*4)," ",J$(1,M1-1)," ",J$(1,M1-1)," ",J$(1,M1-1)," ",J$(1,M1-1)
2930 PRINT (7)@(10),"***Customer Total:",@(80-M1*4),T1:M1$,@(80-M1*3),T2:M1$,@(80-M1*2),T0:M1$,@(80-M1),T3:M1$
2940 LET L=L+2
2950 LET T4=T4+T0,T5=T5+T1,T6=T6+T2,T7=T7+T3
2960 LET T0=0,T1=0,T2=0,T3=0
2990 GOTO 1100
4000 REM " --- End"
4020 PRINT (7)'LF','LF',@(80-M1*4)," ",J$(1,M1-1)," ",J$(1,M1-1)," ",J$(1,M1-1)," ",J$(1,M1-1)
4030 PRINT (7)@(12),"***Report Total:",@(80-M1*4),T5:M1$,@(80-M1*3),T6:M1$,@(80-M1*2),T4:M1$,@(80-M1),T7:M1$
4100 RUN "SYS.AA"
5000 REM " --- Heading
5020 LET L=HEADINGS+1
5040 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,WIDTH,WHEN$,CLOCK$,STATUS
5050 IF STATUS>0 THEN EXITTO 9900
5070 PRINT (7)@(0),"Cust Inv #",@(13),"Date",@(20),"Due",@(26),"TC",@(29),"Typ",@(79-7-M1*3),"Invoice",@(79-7-M1*2),"Applied",@(79-7-M1),"Balance",@(71),"Discount"
5080 LET L=L+1
5490 RETURN 
5500 REM " --- Print Customer"
5510 IF L+2>L9 THEN GOSUB 5000
5520 PRINT (7)'LF',FNF$(A0$(3,P[0]),M0$)," ",A1$(1,30)
5530 LET L=L+2,T9$=N0$+W0$+A0$(3,6)
5540 RETURN 
5700 REM " --- Payment/Adj Detail"
5730 IF X1$(1,1)=" " THEN GOTO 5790
5735 IF X1$(2,3)>P7$ THEN GOTO 5770
5740 IF L+1>L9 THEN GOSUB 5000
5750 PRINT (7)@(12),FNC$(X1$(2,3)),@(21),X1$(5,7),@(30),X1$(1,1),@(80-3*M1),X[0]:M1$,@(80-M1),X[1]:M1$
5760 LET L=L+1,L1=L1+1
5790 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

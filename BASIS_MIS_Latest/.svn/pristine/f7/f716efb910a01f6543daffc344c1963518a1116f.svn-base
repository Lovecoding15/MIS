0010 REM "IVR - Zero Balance Inventory Report (Input Overlay)"
0020 REM "Program IVR.ZA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "15Aug97 WGH: Not processing initial report correctly (550,1020)"
0032 REM "30Oct97 KJS: Clarify prompts, <F3>=since last run, not today (840,1010,1025,1060)
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=8
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0130 LET FILES$[4]="IVM-03",FILES$[5]="IVM-10",FILES$[6]="IVS-10"
0135 LET FILES$[7]="IVT-05",FILES$[8]="APM-01"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0185 LET IVM03_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],IVS10_DEV=CHANNELS[6]
0190 LET IVT05_DEV=CHANNELS[7]
0200 REM " --- IOLists
0210 IVT05A: IOLIST A0$(1),A1$(1)
0220 IVM02A: IOLIST B0$,B1$,B2$,B[ALL]
0230 IVS10A: IOLIST C0$,C1$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0290 APS01A: IOLIST *,*,AP2$,*,*,APMSK$
0400 REM " --- Parameters"
0405 DIM P[5],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0435 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P4$(1,2)),P[2]=FNYY_YEAR(P4$(3,2))
0440 LET P[3]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2)),P[5]=NUM(P2$(13,2))
0450 CALL "SYC.VA","AP",INFO$[ALL]
0460 LET AP$=INFO$[20]
0470 IF AP$="N" THEN GOTO 0500
0480 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0490 LET AP$="Y",APLEN=NUM(AP2$(1,2))
0500 REM " --- Initializations"
0550 LET P8$="I",P9$=""
0600 REM " --- Additional file opens"
0610 IF AP$="N" THEN GOTO 0700
0620 CALL "SYC.DA",1,8,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS THEN GOTO 9900
0640 LET APM01_DEV=CHANNELS[8]
0700 REM " --- Background"
0710 FOR X=4 TO 8 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT @(5,3),'SB',"Last Zero Balance Analysis Run On:",@(1,5),"Include Items With Zero Balance Since:",@(30,7),"Sequence:",'SF',
0800 REM " --- Get Last Date Ran"
0810 LET C1$=F0$(7,3),NEVER$="Y"
0820 FIND (IVS10_DEV,KEY=N0$+"A",DOM=0900)IOL=IVS10A
0830 PRINT @(40,3),FNB$(C1$(1)),
0840 LET NEVER$="N",P9$=C1$(1,3)
0850 GOTO 1000
0900 PRINT @(40,3),"No Prior Calculations",
1000 REM " --- What to Include"
1010 LET V4$="<F1>=All Zero Balance Items, <F3>=Only Since Last Run"
1015 LET V0$="D",V1$="ECR",V2$=P9$,V3$="",V0=3,V1=40,V2=5,V3=1,I0=0
1020 IF NEVER$="Y" THEN GOTO 1100
1025 PRINT @(1,21),"    Only Items Whose Balances Went To Zero Since This Date Will Be Included",
1030 IF V2$="" THEN LET V2$=F0$(7,3)
1040 GOSUB 7000
1045 PRINT @(0,21),'CL'
1050 ON V3 GOTO 1060,1100,3900,1060,9900
1060 IF V3=3 THEN LET V$=C1$(1,3)
1070 LET P9$=V$
1080 PRINT @(V1,V2),'CL',FNB$(P9$),
1090 GOTO 3900
1100 REM " --- Include all"
1110 LET P9$=""
1120 PRINT @(V1,V2),"Print ALL 0 Balance Items",'CL',
1190 GOTO 3900
1200 REM " --- Sequence"
1210 LET V4$="I=Item Sequence     P=Product Sequence     C=Cycle/Location Sequence"
1220 LET V0$="S",V1$="C",V2$=P8$,V3$="IPC",V0=1,V1=40,V2=7
1230 GOSUB 7000
1240 ON V3 GOTO 1250,1200,3900,1200,9900
1250 IF POS(P8$="IPC")=0 THEN GOTO 1200
1255 LET P8$=V$
1260 LET SEQ$="Item",SORT$="",SORT=0,SUB$="",SUBHEAD$="",SUB=0
1265 IF P8$="P" THEN LET SEQ$="Product",SORT$="C",SORT=3,SUB$="A",SUBHEAD$="Prod: ",SUB=3
1270 IF P8$="C" THEN LET SEQ$="Cycle/Location",SORT$="B",SORT=12
1275 LET SEQ$=SEQ$+" Sequence"
1280 PRINT @(V1+V0+2,V2),SEQ$,'CL',
1290 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,4000
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4030 ON POS(V$="YN") GOTO 4000,4050,1000
4100 REM " --- Find The Zeros Here"
4105 IF P9$<>"" AND C1$(1,3)=F0$(7,3) THEN GOTO 4300
4110 DIM B[20]
4120 DIM A1$(23),C0$(3),C1$(23)
4140 LET C0$(1)=N0$+"A",C1$(1)=F0$(7,3),A1$(1)=F0$(7,3)
4160 WRITE (IVS10_DEV,KEY=C0$)IOL=IVS10A
4170 CALL "SYC.NB","Now Processing:",P[0]+3,COL
4190 READ (IVM02_DEV,KEY=N0$,DOM=4200)
4200 LET K2$=KEY(IVM02_DEV,END=4300)
4210 IF POS(N0$=K2$)<>1 THEN GOTO 4300
4220 READ (IVM02_DEV)IOL=IVM02A
4230 PRINT @(COL,11),B0$(3,2)," ",B0$(5,P[0]),
4240 IF B[0]>0 THEN REMOVE (IVT05_DEV,KEY=B0$,DOM=4290); GOTO 4290
4250 LET A0$=B0$
4260 WRITE (IVT05_DEV,KEY=A0$,DOM=4290)IOL=IVT05A
4290 GOTO 4200
4300 RUN "IVR.ZB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

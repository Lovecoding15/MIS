0010 REM "POR - Purchase Order/Requisition Printing (Plain Paper & Preprinted)
0020 REM "Program POR.BB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.6 - 15Aug99 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "27Aug96 WGH: Only print POs that are in POE-22 (1250)"
0032 REM "25Sep96 JAL: Don't print date promised/required for M or V lines (5455)"
0033 REM "14Jan97 JWK: Used wrong message code (3010)
0034 REM "15May97 WGH: Error 42 on Line 9905 when work files aren't available (105-110,410,430)"
0035 REM "10Jun98 WGH: Restore return program's maximum line counter (555,9915)"
0036 REM "15Jun 98 WGH: Empty POs don't print correctly on forms (5690,5710)"
0037 REM "01Feb99 Caj: Total and messages weren't printing in the proper places on PO forms (ie PLAIN$="N") (585,5015,5690-5720,5870)
0038 REM "13Jan99 Caj: Use the default message from parameters as required (3010) *** Requires associated SCN for POE.AB, POE.BB, POR.XA and POR.BA ***
0039 REM "07Jun00 JAL: Test BATCH against 0 (zero), not O (oh) (1140)"
0040 REM 
0045 REM "O0=0 when batch printing/O0=1 when printing on demand"
0047 REM "PLAIN$="N" when preprinted form/"Y" when plain paper
0048 REM "       It is set in calling programs.
0049 REM "       NOTE: Requistions are always PLAIN$="Y"
0050 REM 
0070 SETERR 9000
0080 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 DIM M$[3]
0110 LET FILES=2,M$[1]=M1$,M$[2]=M2$,M$[3]=M3$
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]=SORTFILE$,FILES$[2]=WORKFILE$
0135 LET OPTIONS$[1]="C",OPTIONS$[2]="C",PRINTER=UNT
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET POW02_DEV=CHANNELS[1],POW12_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 POE02A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0220 IVM01A: IOLIST B0$(1),B1$(1)
0230 APM01A: IOLIST C0$(1),C1$(1)
0240 APM05A: IOLIST D0$(1),D1$(1)
0270 APS01B: IOLIST X$,NAME$(1)
0280 ARM01A: IOLIST S0$(1),S1$(1)
0290 ARM03A: IOLIST S0$(1),S1$(1)
0300 ARE33A: IOLIST S0$(1),S1$(1)
0310 IVM05A: IOLIST R0$(1),R1$(1),R2$(1)
0320 POE12A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0330 POM02A: IOLIST Y0$(1),Y1$(1)
0340 POM04A: IOLIST H0$(1),H1$(1)
0350 POM14A: IOLIST L0$(1),L1$(1)
0360 IVM10C: IOLIST X0$(1)
0370 POW02A: IOLIST X8$(1)
0380 POW12A: IOLIST X9$(1)
0390 APM10C: IOLIST Z0$(1)
0400 REM " --- Parameters"
0410 DIM NAME$(30)
0420 FIND (SYS01_DEV,KEY=N0$+"AP02",DOM=430)IOL=APS01B
0430 LET LINE=L
0450 LET M1$=I6$,M2$=I7$,M3$=I8$,M0=LEN(M0$),WIDTH=80,K$=A0$
0500 REM " --- Initializations"
0505 PRECISION I[1]
0510 DIM HEADING$[1],BUF$(WIDTH),BAR$(WIDTH,"-")
0515 DIM A0$(15,"9"),A1$(160,"X"),A[8],Y0$(4),Y1$(32),X9$(20),H0$(5),H1$(32)
0520 DIM W0$(18,"9"),W1$(48,"X"),W2$(32),W3$(22,"X"),W4$(40,"X"),W[12]
0525 DIM L0$(7),L1$(40),C0$(8,"0"),C1$(30,"X"),Z0$(32,"X"),X8$(25),X9$(25)
0530 DIM A$(140,"X"),N$(105,"X"),S$(105,"X")
0540 LET BATCH=1,NF$="(Not On File)",SHIPFLEN=35
0550 LET T0$="",T1$="",T2$="",T3$="",T4$=""
0555 LET RETURN_L9=L9
0560 IF O0>0 THEN LET BATCH=0
0570 IF PLAIN$<>"Y" THEN GOTO 0585
0575 LET QTYMASKMAX=10,QTYMASKMIN=10,MAXFOOTERS=40,L9=59
0580 IF PLAIN$="Y" THEN GOTO 0595
0585 LET QTYMASKMAX=9,QTYMASKMIN=9,MAXFOOTERS=10,L9=52
0590 DIM FOOTER$[MAXFOOTERS]
0595 LET TITLE=LEN(TITLE$),LABEL=LEN(LABEL$),L=L9+1,FIRST=1,PAGE=0
0600 REM " --- Resize masks"
0610 CALL "SYC.RM",M1$,M1,10,10
0620 CALL "SYC.RM",M2$,M2,QTYMASKMIN,QTYMASKMAX
0630 CALL "SYC.RM",M3$,M3,10,10
0650 REM " --- Test pattern initializations"
0660 LET W[1]=9999.99,QTY=999999,EXTENSION=999999.99,TOTAL=EXTENSION
0665 LET W1$(3,6)=F0$(18,6),W1$(22,2)="EA",A1$(5,6)=F0$(18,6),Y1$(21,1)="N"
0670 LET W1$(9,12)=A1$(5,6)+W1$(5,6),SHIPFROM$="99 "+FILL(30,"X")
0700 REM " --- Open Printer "
0710 CALL "SYC.GA",PRINTER,1,"","",STATUS
0720 IF STATUS THEN GOTO 9900
0800 REM " --- Print Test Pattern?"
0810 CALL "SYC.YN",0,"Do You Want To Print A Test Pattern",3,V$,V3
0820 IF V3=4 THEN GOTO 9900
0830 ON POS(V$="YN") GOTO 800,840,900
0840 GOSUB 5000
0845 IF PLAIN$="Y" THEN LET TESTMAX=8 ELSE LET TESTMAX=6
0850 FOR X=1 TO TESTMAX
0855 GOSUB 5200
0860 NEXT X
0865 DIM FOOTER$[MAXFOOTERS]
0870 LET FOOTCNT=0
0875 IF FNP$(POMESSAGE$)<>"" THEN LET FOOTCNT=2
0880 GOSUB 5600
0890 GOTO 0800
0900 REM " --- Position files"
0910 DIM A0$(15),A1$(160),A[8],Y0$(4),Y1$(32),C0$(8),C1$(195)
0920 DIM W0$(18),W1$(48),W2$(32),W3$(22),W4$(40),W[12],Z0$(32)
0930 IF POS(SEQUENCE$="AW") THEN GOSUB 6500
0940 LET X$=N0$
0950 IF RESTART$="Y" THEN LET X$=N0$+VENDOR$
0960 IF BATCH THEN CALL "SYC.NB","Printing",M0+8,COLUMN
0970 READ (POW02_DEV,KEY=N0$,DOM=980)
0980 READ (POE22_DEV,KEY=X$,DOM=990)
0990 READ (POE32_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Branch based on SEQUENCE$"
1010 ON POS(SEQUENCE$="NRPAW") GOTO 4000,1100,1200,1200,1300,1300
1100 REM " --- Vendor number sequence"
1110 LET K$=KEY(POE22_DEV,END=4000)
1120 IF POS(N0$=K$)<>1 THEN GOTO 4000
1130 READ (POE22_DEV)
1140 IF BATCH=0 THEN LET K$=VENDOR_PO$
1190 GOTO 1400
1200 REM " --- Req/PO number sequence"
1210 LET K$=KEY(POE32_DEV,END=4000)
1220 IF POS(N0$=K$)<>1 THEN GOTO 4000
1230 READ (POE32_DEV)
1240 LET K$=K$(1,2)+K$(10)+K$(3,7)
1250 READ (POE22_DEV,KEY=K$,DOM=1200)
1290 GOTO 1400
1300 REM " --- Alternate sort or warehouse sequence"
1310 LET K$=KEY(POW02_DEV,END=4000)
1320 IF POS(N0$=K$)<>1 THEN GOTO 4000
1330 READ (POW02_DEV)
1340 LET WHSE$=K$(3,2),K$=K$(1,2)+K$(13)
1390 GOTO 1400
1400 REM " --- Read header record"
1410 FIND (POE02_DEV,KEY=K$,DOM=3900)IOL=POE02A
1420 IF BATCH THEN PRINT @(COLUMN,11),FNF$(A0$(3,VENDLEN),M0$)," ",A0$(9,7),
1500 REM " --- Level breaks?"
1510 IF A0$(3,6)<>T0$ THEN GOSUB 6000
1520 IF A0$(3,6)+A1$(3,2)<>T1$ THEN GOSUB 6100
1530 IF A1$(38,2)<>T2$ THEN GOSUB 6200
1790 READ (POE12_DEV,KEY=A0$,DOM=1800)
1800 REM " --- Sort line items by ship-to warehouse"
1810 IF SEQUENCE$="W" THEN GOTO 1900
1820 LET K$=KEY(POE12_DEV,END=1900)
1830 IF POS(A0$=K$)<>1 THEN GOTO 1900
1840 READ (POE12_DEV)IOL=POE12A
1850 LET X9$(1)=N0$+W3$(1,2)+W0$(3)
1860 WRITE (POW12_DEV,KEY=X9$)IOL=POW12A
1890 GOTO 1800
1900 REM " --- Position sort file"
1910 LET L=L9+1,FIRST=1,TOTAL=0,PAGE=0
1990 READ (POW12_DEV,KEY=N0$,DOM=2000)
2000 REM " --- Read next sort record"
2010 IF SEQUENCE$="W" THEN GOTO 2100
2020 LET K$=KEY(POW12_DEV,END=3000)
2030 IF POS(N0$=K$)<>1 THEN GOTO 3000
2040 REMOVE (POW12_DEV,KEY=K$)
2050 LET W0$(1)=N0$+K$(5)
2060 FIND (POE12_DEV,KEY=W0$,DOM=2900)IOL=POE12A
2090 GOTO 2200
2100 REM " --- Read next detail record"
2110 LET K$=KEY(POE12_DEV,END=3000)
2120 IF POS(A0$=K$)<>1 THEN GOTO 3000
2130 READ (POE12_DEV)IOL=POE12A
2140 IF W3$(1,2)<>WHSE$ THEN GOTO 2100
2200 REM " --- Level breaks?"
2210 IF W3$(1,2)<>T3$ THEN GOSUB 6300
2220 IF W1$(1,2)<>T4$ THEN GOSUB 6400
2250 REM " --- Print detail"
2260 GOSUB 6900
2270 LET TOTAL=TOTAL+EXTENSION
2280 GOSUB 5200
2300 REM " --- Print any associated message"
2310 IF FNP$(W1$(38,3))="" THEN GOTO 2500
2320 LET MSG$=H0$(3,3),H0$(1)=N0$+W1$(38,3)
2330 FIND (POM04_DEV,KEY=H0$,DOM=2500)IOL=POM04A
2340 IF POS(H1$(21,1)="B"+TYPE$)=0 THEN GOTO 2500
2390 READ (POM14_DEV,KEY=H0$,DOM=2400)
2400 REM " --- Read next message line"
2410 LET K$=KEY(POM14_DEV,END=2500)
2420 IF POS(H0$=K$)<>1 THEN GOTO 2500
2430 READ (POM14_DEV)IOL=POM14A
2440 LET X$=L1$(1,40)
2450 GOSUB 5500
2490 GOTO 2400
2500 REM " --- Done printing line message"
2510 LET H0$(3)=MSG$,D$=""
2520 IF Y1$(22,1)<>"Y" THEN GOTO 2900
2550 REM " --- Retrieve drop ship address"
2560 IF OP$="N" THEN GOTO 2600
2570 DIM S0$(15),S1$(130)
2575 LET S0$(1)=N0$+W2$(11,13)
2580 FIND (ARE33_DEV,KEY=S0$,DOM=2600)IOL=ARE33A
2585 LET D$=S1$(31,72)+FILL(48)+S1$(103,9)
2590 GOTO 2700
2600 DIM S0$(14),S1$(174)
2610 LET S0$(1)=N0$+W2$(11,6)+W2$(27,6)
2620 FIND (ARM03_DEV,KEY=S0$,DOM=2650)IOL=ARM03A
2630 LET D$=S1$(31,72)+FILL(48)+S1$(103,9)
2640 GOTO 2700
2650 DIM S0$(8),S1$(280)
2660 LET S0$(1)=N0$+W2$(11,6)
2670 FIND (ARM01_DEV,KEY=S0$,DOM=2900)IOL=ARM01A
2680 LET D$=S1$(31,72)+S1$(179,48)+S1$(103,9)
2700 REM " --- Format address and print drop ship information"
2710 DIM X$(40)
2720 IF D$="" THEN GOTO 2900
2730 CALL "SYC.AA",D$,24,5,9,40
2740 LET NEEDED=4
2750 FOR X=1 TO 200 STEP 40
2760 IF FNP$(D$(X,40))<>"" THEN LET NEEDED=NEEDED+1
2770 NEXT X
2780 IF L+NEEDED>L9-2 THEN GOSUB 5800
2790 GOSUB 5500
2800 LET X$(1)="Please Ship Line "+W0$(16,3)+" To:"
2810 GOSUB 5500
2820 LET X$(1)=""
2830 GOSUB 5500
2840 LET X$(1)=S1$(1,30)
2850 GOSUB 5500
2860 FOR X=1 TO 200 STEP 40
2870 LET X$(1)=D$(X,40)
2880 IF FNP$(X$)<>"" THEN GOSUB 5500
2890 NEXT X
2900 REM " --- Loop back for next detail"
2990 GOTO 2000
3000 REM " --- Print total"
3010 LET MSG$=DEFAULTMSG$
3020 IF FNP$(A1$(109,3))<>"" THEN LET MSG$=A1$(109,3)
3030 IF FNP$(MSG$)="" THEN GOTO 3300
3040 IF MSG$=H0$(3,3) THEN GOTO 3500
3100 REM " --- Load current message"
3110 DIM FOOTER$[MAXFOOTERS]
3120 LET H0$(1)=N0$+MSG$,FOOTCNT=0
3130 IF FNP$(POMESSAGE$)<>"" THEN LET FOOTCNT=2
3150 FIND (POM04_DEV,KEY=H0$,DOM=3300)IOL=POM04A
3160 IF POS(H1$(21,1)="B"+TYPE$)=0 THEN GOTO 3300
3190 READ (POM14_DEV,KEY=H0$,DOM=3200)
3200 REM " --- Load message lines"
3210 LET K$=KEY(POM14_DEV,END=3500)
3220 IF POS(H0$=K$)<>1 THEN GOTO 3500
3230 READ (POM14_DEV)IOL=POM14A
3240 IF FOOTCNT>MAXFOOTERS-2 THEN GOTO 3500
3250 LET FOOTCNT=FOOTCNT+1,FOOTER$[FOOTCNT]=L1$
3290 GOTO 3200
3300 REM " --- No message"
3310 DIM FOOTER$[MAXFOOTERS]
3320 LET FOOTCNT=0
3500 REM " --- Print total"
3510 GOSUB 5600
3900 REM " --- Loop back for next header"
3990 IF BATCH THEN GOTO 1000
4000 REM " --- All requisitions printed"
4010 CLOSE (PRINTER,ERR=4020)
4020 IF BATCH THEN GOTO 4100
4030 REMOVE (POE22_DEV,KEY=A0$,DOM=4090)
4090 GOTO 9900
4100 REM " --- Update print file?"
4110 LET V4$="Are You Ready To Update The "+LABEL$+" Print File"
4120 CALL "SYC.YN",0,V4$,0,V$,V3
4130 ON V3 GOTO 4140,4100,4100,4100,4900
4140 IF V$<>"YES" THEN GOTO 9900
4150 PRINT @(COLUMN-9,11),'SB',"Updating",'SF',
4190 READ (POE22_DEV,KEY=N0$,DOM=4200)
4200 REM " --- Update the print file"
4210 LET K$=KEY(POE22_DEV,END=4900)
4220 IF POS(N0$=K$)<>1 THEN GOTO 4900
4230 PRINT @(COLUMN,11),FNF$(K$(3,VENDLEN),M0$)," ",K$(9),
4240 REMOVE (POE22_DEV,KEY=K$,DOM=4900)
4290 GOTO 4200
4900 REM " --- All done"
4990 GOTO 9900
5000 REM " --- Heading"
5003 IF PLAIN$<>"Y" THEN GOTO 5010
5005 LET L=20,PAGE=PAGE+1
5010 IF PLAIN$="Y" THEN GOTO 5017
5015 LET L=26,PAGE=PAGE+1,NAME$="",TITLE$=""
5016 DIM A$(140)
5017 CALL "SYC.HA",PRINTER,HEADING$[ALL],0,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5020 IF STATUS THEN EXITTO 9900
5025 PRINT (PRINTER)'FF'
5035 PRINT (PRINTER)NAME$,@(WIDTH-TITLE),TITLE$
5040 IF PLAIN$="Y" THEN GOTO 5055
5045 PRINT (PRINTER)""
5050 PRINT (PRINTER)""
5055 PRINT (PRINTER)A$(1,35)
5060 PRINT (PRINTER)A$(36,35),@(63-LABEL),LABEL$," Number: ",A0$(9)
5065 PRINT (PRINTER)A$(71,35),@(65-LABEL),LABEL$," Date: ",FNB6$(A1$(5,6))
5070 PRINT (PRINTER)A$(106,35),@(66),"Page:",PAGE
5075 PRINT (PRINTER)""
5080 PRINT (PRINTER)"Vendor:",@(43),"Ship From:"
5085 PRINT (PRINTER)""
5090 PRINT (PRINTER)FNF$(C0$(3,VENDLEN),M0$),@(M0+1),C1$(1,30),@(77-SHIPFLEN),SHIPFROM$
5095 IF PLAIN$<>"Y" THEN PRINT (PRINTER)""
5100 PRINT (PRINTER)@(M0+1),N$(1,35),@(80-SHIPFLEN),S$(1,SHIPFLEN)
5105 PRINT (PRINTER)@(M0+1),N$(36,35),@(80-SHIPFLEN),S$(36,SHIPFLEN)
5110 PRINT (PRINTER)@(M0+1),N$(71,35),@(80-SHIPFLEN),S$(71,SHIPFLEN)
5115 IF PLAIN$="Y" THEN GOTO 5140
5120 FOR CNT=1 TO 3
5125 PRINT (PRINTER)""
5135 NEXT CNT
5140 IF PLAIN$="Y" THEN PRINT (PRINTER)BAR$ ELSE PRINT (PRINTER)""
5145 PRINT (PRINTER)"Terms: ",Z0$(6,20),"    Ship Via: ",A1$(59,15),"   FOB: ",A1$(94,15)
5150 PRINT (PRINTER)BAR$
5155 PRINT (PRINTER)"Acknowledged By Vendor Contact: ",A1$(74,20)
5160 IF PLAIN$<>"Y" THEN GOTO 5175
5165 PRINT (PRINTER)BAR$
5170 PRINT (PRINTER)"Seq| Quantity |Item And Description",@(45),"Date Req'd|     Cost |UM|Extension"
5175 IF PLAIN$="Y" THEN GOTO 5190
5180 PRINT (PRINTER)""
5185 PRINT (PRINTER)"Seq Quantity Item And Description",@(44),"Date Req'd       Cost |UM|Extension"
5190 PRINT (PRINTER)BAR$
5195 RETURN
5200 REM " --- Print Detail Line"
5210 DIM X$(40)
5215 LET NEEDED=2
5220 IF POS(Y1$(21,1)="SN") THEN LET NEEDED=3
5225 IF FNP$(W1$(9,12))<>"" THEN LET NEEDED=NEEDED+1
5230 IF L+NEEDED>L9-2 THEN GOSUB 5800
5235 IF PLAIN$="Y" THEN PRINT (PRINTER)@(3),"|",@(14),"|",@(55),"|",@(66),"|  |" ELSE PRINT (PRINTER)@(66),"|  |"
5240 LET L=L+1
5245 ON POS(Y1$(21,1)="SNVMO") GOTO 5490,5250,5325,5355,5355,5355
5250 REM " --- Print Standard Line"
5255 DIM B0$(22),B1$(60),R0$(28),R1$(32),R2$(20),X$(40)
5260 LET B0$(1)=N0$+W3$(3),B1$(1)=NF$,R0$(1)=C0$+W3$(3)
5265 FIND (IVM01_DEV,KEY=B0$,DOM=5270)IOL=IVM01A
5270 IF P3$(58,1)="Y" THEN FIND (IVM05_DEV,KEY=R0$,DOM=5275)IOL=IVM05A
5275 IF FNP$(R2$)<>"" THEN LET NEEDED=NEEDED+1
5280 IF L+NEEDED>L9-2 THEN GOSUB 5800
5285 IF PLAIN$="Y" THEN PRINT (PRINTER)W0$(16,3),"|",QTY:M2$,"|",W3$(3,I[0]),@(46),FNB6$(W1$(3,6)),@(55),"|",W[1]:M3$,"|",W1$(22,2),"|",EXTENSION:M1$
5290 IF PLAIN$<>"Y" THEN PRINT (PRINTER)W0$(16,3)," ",QTY:M2$,@(13),W3$(3,I[0]),@(45),FNB6$(W1$(3,6)),@(56),W[1]:M3$,"|",W1$(22,2),"|",EXTENSION:M1$
5295 LET L=L+1,X$(1)=FNITEM$(B1$,I[3],I[4],I[5])
5300 GOSUB 5500
5305 IF FNP$(R2$)="" THEN GOTO 5450
5310 LET X$(1)="Your Item Number "+R2$
5315 GOSUB 5500
5320 GOTO 5450
5325 REM " --- Print Non-Stock Line"
5330 IF PLAIN$="Y" THEN PRINT (PRINTER)W0$(16,3),"|",QTY:M2$,"|",W3$(3,I[0]),@(46),FNB6$(W1$(3,6)),@(55),"|",W[1]:M3$,"|",W1$(22,2),"|",EXTENSION:M1$
5335 IF PLAIN$<>"Y" THEN PRINT (PRINTER)W0$(16,3)," ",QTY:M2$,@(13),W3$(3,I[0]),@(45),FNB6$(W1$(3,6)),@(56),W[1]:M3$,"|",W1$(22,2),"|",EXTENSION:M1$
5340 LET L=L+1,X$(1)=W4$
5345 GOSUB 5500
5350 GOTO 5450
5355 REM " --- Print Vendor Part Number, Message or Other Line"
5360 IF PLAIN$<>"Y" THEN GOTO 5380
5365 IF Y1$(21,1)="V" THEN PRINT (PRINTER)W0$(16,3),"|",@(14),"|Your Item Number ",W4$(1,I[6]),@(55),"|",@(66),"|  |"
5370 IF Y1$(21,1)="M" THEN PRINT (PRINTER)W0$(16,3),"|",@(14),"|",W4$,@(55),"|",@(66),"|  |"
5375 IF Y1$(21,1)="O" THEN PRINT (PRINTER)W0$(16,3),"|",@(14),"|",W4$,@(55),"|",W[1]:M3$,@(66),"|  |",EXTENSION:M1$
5380 IF PLAIN$="Y" THEN GOTO 5400
5385 IF Y1$(21,1)="V" THEN PRINT (PRINTER)W0$(16,3),@(13),"Your Item Number ",W4$(1,I[6]),@(66),"|  |"
5390 IF Y1$(21,1)="M" THEN PRINT (PRINTER)W0$(16,3),@(13),W4$,@(66),"|  |"
5395 IF Y1$(21,1)="O" THEN PRINT (PRINTER)W0$(16,3),@(13),W4$,@(56),W[1]:M3$,@(66),"|  |",EXTENSION:M1$
5400 LET L=L+1
5450 REM " --- Date Promised or Not Before Date?"
5455 IF POS(Y1$(21,1)="VM") THEN GOTO 5490
5460 IF FNP$(W1$(9,12))="" THEN GOTO 5490
5465 LET V=1
5470 IF FNP$(W1$(9,6))<>"" THEN LET X$(1)="Promised By "+FNB6$(W1$(9,6)),V=22
5475 IF FNP$(W1$(15,6))<>"" THEN LET X$(V)="Not Before "+FNB6$(W1$(15,6))
5480 GOSUB 5500
5490 RETURN
5500 REM " --- Print text only"
5510 IF L+1>L9-2 THEN GOSUB 5800
5520 IF PLAIN$="Y" THEN PRINT (PRINTER)@(3),"|",@(14),"|",X$,@(55),"|",@(66),"|  |"
5525 IF PLAIN$<>"Y" THEN PRINT (PRINTER)@(13),X$,@(66),"|  |"
5530 LET L=L+1
5590 RETURN
5600 REM " --- Total"
5610 LET FOOTCNT=MAX(FOOTCNT,6)
5620 LET BUF$(1)=FOOTER$[1]
5630 IF FNP$(POMESSAGE$)<>"" THEN LET BUF$(1)=POMESSAGE$
5640 LET BUF$(65)="Total "+STR(TOTAL:M1$),FOOTER$[1]=BUF$
5650 LET BUF$(1)=FOOTER$[FOOTCNT-1],BUF$(48)=FILL(32,"_")
5660 LET FOOTER$[FOOTCNT-1]=BUF$,BUF$(1)=FOOTER$[FOOTCNT]
5670 LET BUF$(56)="Purchasing Agent",FOOTER$[FOOTCNT]=BUF$
5680 LET BARCNT=1; REM "One line count for the printing of BAR$
5690 IF PLAIN$<>"Y" THEN GOTO 5705
5695 IF L+FOOTCNT+BARCNT>L9 THEN GOSUB 5800
5700 LET SKIP=L9-FOOTCNT-BARCNT-L
5705 IF PLAIN$="Y" THEN GOTO 5730
5710 IF L>L9 THEN GOSUB 5800
5720 LET SKIP=L9-L
5730 IF SKIP>0 THEN GOSUB 5900
5740 IF PLAIN$="Y" THEN PRINT (PRINTER)BAR$ ELSE PRINT (PRINTER)""
5750 FOR X=1 TO FOOTCNT
5760 PRINT (PRINTER)FOOTER$[X]
5770 NEXT X
5790 RETURN
5800 REM " --- Continued"
5810 IF FIRST THEN GOTO 5880
5820 IF PLAIN$="Y" THEN GOTO 5860
5830 FOR CNT=L TO L9-1
5835 PRINT (PRINTER)@(66),"|  |"
5840 NEXT CNT
5845 PRINT (PRINTER)""
5850 GOTO 5870
5860 PRINT (PRINTER)BAR$
5870 IF PLAIN$="Y" THEN PRINT (PRINTER)@(61),"(Continued)" ELSE PRINT (PRINTER)@(59),"(Continued)"; REM "Unform needs these different print positions
5880 GOSUB 5000
5885 LET FIRST=0
5890 RETURN
5900 REM " --- Skip lines"
5910 FOR X=1 TO SKIP
5920 IF PLAIN$="Y" THEN PRINT (PRINTER)@(3),"|",@(14),"|",@(55),"|",@(66),"|  |"
5930 IF PLAIN$<>"Y" THEN PRINT (PRINTER)@(66),"|  |"
5940 NEXT X
5990 RETURN
6000 REM " --- Vendor Break"
6010 DIM C0$(8),C1$(195)
6020 LET C0$(1)=A0$,C1$(1)=NF$,T0$=C0$(3)
6030 FIND (APM01_DEV,KEY=C0$,DOM=6040)IOL=APM01A
6040 LET N$=C1$(31,72)+C1$(103,9)
6050 CALL "SYC.AA",N$,24,3,9,35
6090 RETURN
6100 REM " --- Ship-From Break"
6110 DIM D0$(10),D1$(160),S$(140)
6120 LET D0$(1)=A0$(1,8)+A1$(3,2),T1$=D0$(3),SHIPFROM$="SAME"
6130 IF FNP$(A1$(3,2))="" THEN GOTO 6190
6140 LET D1$(1)=NF$
6150 FIND (APM05_DEV,KEY=D0$,DOM=6160)IOL=APM05A
6160 LET S$=D1$(31,72)+D1$(103,9),SHIPFROM$=A1$(3,2)+" "+D1$(1,30)
6170 CALL "SYC.AA",S$,24,3,9,35
6180 LET SHIPFLEN=MAX(LEN(CVS(D1$(1,30),2)),LEN(CVS(S$(1,35),2)),LEN(CVS(S$(36,35),2)),LEN(CVS(S$(71,35),2))); REM "Max len of ship from addr block 
6185 IF PLAIN$="Y" THEN LET SHIPFLEN=35; REM "Only move block to fit preprinted forms
6190 RETURN
6200 REM " --- Terms Break"
6210 DIM Z0$(32)
6220 LET Z0$(1)=N0$+"C"+A1$(38,2)+NF$,T2$=A1$(38,2)
6230 FIND (APM10_DEV,KEY=Z0$(1,5),DOM=6290)IOL=APM10C
6290 RETURN
6300 REM " --- Ship-To Break"
6310 DIM X0$(160)
6320 LET X0$(1)=N0$+"C"+W3$(1,2)+NF$,T3$=W3$(1,2)
6330 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=6340)IOL=IVM10C
6340 LET A$=X0$(30,96)+X0$(136,9)
6350 CALL "SYC.AA",A$,24,4,9,35
6390 RETURN
6400 REM " --- Line Code Break"
6410 DIM Y0$(4),Y1$(32)
6420 LET Y0$(1)=N0$+W1$(1,2),T4$=W1$(1,2)
6430 FIND (POM02_DEV,KEY=Y0$,DOM=6490)IOL=POM02A
6490 RETURN
6500 REM " --- Sort print file by alternate sort or warehouse"
6510 CALL "SYC.NB"," Sorting",M0+8,COLUMN
6590 READ (POE22_DEV,KEY=N0$,DOM=6600)
6600 REM " --- Read next print record"
6610 LET K$=KEY(POE22_DEV,END=6800)
6620 IF POS(N0$=K$)<>1 THEN GOTO 6800
6630 PRINT @(COLUMN,11),FNF$(K$(3,VENDLEN),M0$)," ",K$(9),
6640 READ (POE22_DEV)
6650 IF SEQUENCE$="W" THEN GOTO 6680
6655 LET C0$(1)=N0$+K$(3)
6660 FIND (APM01_DEV,KEY=C0$,DOM=6600)IOL=APM01A
6665 LET X8$(1)=N0$+C1$(146,10)+K$(3)
6670 WRITE (POW02_DEV,KEY=X8$)IOL=POW02A
6675 GOTO 6600
6680 LET A0$(1)=K$
6690 READ (POE12_DEV,KEY=A0$,DOM=6700)
6700 REM " --- Must read all detail records when printing by warehouse"
6710 LET K$=KEY(POE12_DEV,END=6600)
6720 IF POS(A0$=K$)<>1 THEN GOTO 6600
6730 READ (POE12_DEV,KEY=K$,DOM=6600)IOL=POE12A
6740 LET X8$(1)=N0$+W3$(1,2)+FILL(8)+A0$(3)
6750 WRITE (POW02_DEV,KEY=X8$)IOL=POW02A
6790 GOTO 6700
6800 REM " --- Done sorting"
6880 PRINT @(COLUMN-9,11),'SB',"Printing",'SF',
6890 RETURN
6900 REM " --- Calculate EXTENSION"
6910 LET QTY=W[2]-W[3]
6920 IF TYPE$="P" THEN LET QTY=W[3]-W[7]
6930 IF Y1$(21,1)="O" THEN LET QTY=1
6960 PRECISION 2
6970 LET EXTENSION=W[1]*QTY
6980 PRECISION I[1]
6990 RETURN
8000 REM " --- Functions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 REM " --- Return to menu"
9905 LET M1$=M$[1],M2$=M$[2],M3$=M$[3]
9910 PRINT @(0,22),'CL',
9915 IF RETURN_L9 THEN LET L9=RETURN_L9
9920 CLOSE (PRINTER,ERR=9925)
9925 CLOSE (POW02_DEV,ERR=9930)
9930 CLOSE (POW12_DEV,ERR=9940)
9940 RUN O0$,ERR=9950
9950 RUN "SYS.AA"
9999 END
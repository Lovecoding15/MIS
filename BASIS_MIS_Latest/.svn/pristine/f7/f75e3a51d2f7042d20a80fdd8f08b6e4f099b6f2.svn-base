0010 REM "SYC - Vendor Lookup"
0020 REM "Program SYC.LV"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "SELECTION$: Vendor Number (returned)"
0060 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER SELECTION$
0100 REM " --- Open Files"
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SYM-06",FILES$[3]="APM-01"
0135 LET FILES$[4]="APM-04",FILES$[5]="APT-03",FILES$[6]="POE-31"
0140 LET FILES$[7]="POE-32"
0150 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],SYM06_DEV=CHANNELS[2],APM01_DEV=CHANNELS[3]
0180 LET APM04_DEV=CHANNELS[4],APT03_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 APM01A: IOLIST A0$(1),A1$(1)
0240 SYM06A: IOLIST *,COMPANY$
0260 APS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM P[2],INFO$[20]
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0425 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),SPEEDSEARCH$="N",M7$="(###)-###-####",PO$="N"
0440 LET X$=STBL("!SPEEDSEARCH",ERR=0450),N0$=X$(1,2),SPEEDSEARCH$="Y"
0450 FIND (SYM06_DEV,KEY=N0$,DOM=9800)IOL=SYM06A
0460 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0470 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(9,1))
0480 CALL "SYC.VA","PO",INFO$[ALL]
0490 LET PO$=INFO$[20]
0500 REM " --- Initializations"
0510 DIM A0$(8),A1$(256)
0540 LET WIDTH=48,HEIGHT=12,WIN_X=0,WIN_Y=6,MAX_ROW=HEIGHT-5
0550 LET SWIDTH=80-WIDTH,SHEIGHT=11,SWIN_X=WIN_X+WIDTH,SWIN_Y=WIN_Y+1
0560 LET PGM$=PGM(-2),OPTIONS=0,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3
0570 LET STITLE$="Vendor Detail",SELECTION$=""
0600 REM " --- Additional file opens"
0610 IF PO$="N" THEN GOTO 0650
0620 CALL "SYC.DA",1,6,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS THEN GOTO 9900
0640 LET POE31_DEV=CHANNELS[6],POE32_DEV=CHANNELS[7]
0650 REM " --- Window arrays"
0660 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0670 LET HEADING$(2)="Number",HEADING$(3+LEN(M0$))="Name"
0680 LET HEADING$(28+LEN(M0$))="Key",TITLE$="Vendor Lookup"
0690 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Which search option?"
0810 DIM STRINGS$[10],NUMBERS[10]
0820 LET STRINGS$[0]=PGM$,STRINGS$[1]=M0$,NUMBERS[0]=MAXROW
0830 LET NUMBERS[1]=OWIN_X,NUMBERS[2]=OWIN_Y,NUMBERS[3]=P[0]
0840 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
0850 CALL "SYC.SM",STRINGS$[ALL],NUMBERS[ALL],FKEY
0860 IF FKEY THEN GOTO 9900
0870 LET PREFIX$=STRINGS$[2],START$=STRINGS$[3],METHOD$=STRINGS$[4]
0880 LET V3$=STRINGS$[5],V0=NUMBERS[4]
0890 IF POS(PREFIX$="PR") THEN IF PO$="N" THEN GOSUB 6500; GOTO 0800
0900 REM " --- Detail Window"
0910 CALL "SYC.WA",0,SWIDTH,SHEIGHT,SWIN_X,SWIN_Y,STITLE$,SNAME$
0915 CALL "SYC.WB",NAME$,0
0920 LET HEADING$(27+LEN(M0$))=METHOD$
0925 PRINT @(0,0),'CF','SB','BR',HEADING$,'ER','SF',
0930 IF SPEEDSEARCH$<>"Y" THEN GOTO 1000
0950 REM " --- Display SpeedSearch Company"
0955 DIM X$(WIDTH+SWIDTH)
0960 LET X$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
0965 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
0970 CALL "SYC.WA",0,WIDTH+SWIDTH,3,WIN_X,WIN_Y+HEIGHT,"",CNAME$
0975 PRINT @(0,0),'BR',X$,'ER',
0980 CALL "SYC.WB",NAME$,0
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,SHEIGHT-2]
1020 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$=""
1030 IF PREFIX$="" THEN GOTO 1200
1040 IF PREFIX$="*" THEN GOTO 1300
1050 IF PREFIX$="I" THEN GOTO 1150
1060 IF PREFIX$="R" THEN LET CHANNEL=POE31_DEV
1070 IF PREFIX$="P" THEN LET CHANNEL=POE32_DEV
1080 IF POS(PREFIX$="RP") THEN GOTO 1350
1100 REM " --- Alternate Key"
1110 READ (APM04_DEV,KEY=N0$+PREFIX$+START$,DOM=1400)
1140 GOTO 1550
1150 REM " --- Invoice Number"
1160 READ (APT03_DEV,KEY=N0$+"C"+START$,DOM=1400)
1190 GOTO 1400
1200 REM " --- Vendor Number"
1210 READ (APM01_DEV,KEY=N0$+START$,DOM=1400)IOL=APM01A
1220 LET K$=A0$,X$=A0$(3)
1290 GOTO 1900
1300 REM " --- Keyword"
1310 LET START$=FNU$(START$)
1320 READ (APM01_DEV,KEY=N0$,DOM=1400)
1340 GOTO 1400
1350 REM " --- Requisition or PO Number"
1360 READ (CHANNEL,KEY=N0$+START$,DOM=1400)
1400 REM " --- Read next record"
1410 IF PREFIX$="" THEN GOTO 1800
1420 IF PREFIX$="*" THEN GOTO 1800
1430 IF PREFIX$="I" THEN GOTO 1700
1440 IF POS(PREFIX$="RP") THEN GOTO 1600
1500 REM " --- Next ARM-04 sort record"
1510 LET K$=KEY(APM04_DEV,END=3000)
1520 IF POS(N0$+PREFIX$=K$)<>1 THEN GOTO 3000
1530 READ (APM04_DEV)
1550 LET X$=K$(4,V0)
1560 IF PREFIX$="E" THEN LET X$=FNZ$(X$)
1570 IF PREFIX$="L" THEN LET X$=FNF$(X$,V3$)
1580 LET K$=N0$+K$(4+V0)
1590 GOTO 1900
1600 REM " --- Next POE-31/POE-32 record"
1610 LET K$=KEY(CHANNEL,END=3000)
1620 IF POS(N0$=K$)<>1 THEN GOTO 3000
1630 READ (CHANNEL)
1640 LET X$=K$(3,7),K$=N0$+K$(10)
1690 GOTO 1900
1700 REM " --- Next APT-03 record"
1710 LET K$=KEY(APT03_DEV,END=3000)
1720 IF POS(N0$+"C"=K$)<>1 THEN GOTO 3000
1730 READ (APT03_DEV)
1740 LET X$=K$(4,10),K$=N0$+K$(16,6)
1790 GOTO 1900
1800 REM " --- Next APM-01 record"
1810 LET K$=KEY(APM01_DEV,END=3000)
1820 IF POS(N0$=K$)<>1 THEN GOTO 3000
1830 LET X$=K$(3,P[0])
1900 REM " --- Read vendor master record"
1905 LET LOCKED=0
1910 READ (APM01_DEV,KEY=K$,ERR=1950)IOL=APM01A
1920 IF PREFIX$<>"*" THEN GOTO 2000
1930 IF START$<>"" THEN IF POS(START$=FNU$(A1$))=0 THEN GOTO 2900
1935 LET X$=START$
1940 GOTO 2000
1950 REM " --- Error reading vendor master record"
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1980 LET LOCKED=1
2000 REM " --- Display page?"
2010 IF L<MAX_ROW THEN GOTO 2400
2020 LET XMODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 0800
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2400 REM " --- Lookup window record"
2410 DIM G1$(WIDTH-2)
2415 IF LOCKED THEN GOTO 2450
2430 LET G1$(2)=FNF$(A0$(3,P[0]),M0$),G1$(3+LEN(M0$))=A1$(1,24)+" "+X$
2440 GOTO 2490
2450 LET G1$(2)=FNF$(K$(3,P[0]),M0$)+" Record In Use",A0$(1)=K$,A1$(1)=""
2490 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$(3,6)
2500 REM " --- Format address block"
2510 LET X$=A1$(31,72)+A1$(103,9),FAX$=FNP$(FNF$(A1$(186,10),M7$))
2520 CALL "SYC.AA",X$,24,3,9,30
2600 REM " --- Detail window record"
2610 DIM G2$(SWIDTH-3)
2620 LET G2$(2)=A1$(1,30),MORE$[PAGE,X1,2]=G2$,N=2
2630 FOR X=1 TO 90 STEP 30
2640 IF FNP$(X$(X,30))="" THEN GOTO 2650
2645 LET N=N+1,G2$(2)=X$(X,30),MORE$[PAGE,X1,N]=G2$
2650 NEXT X
2660 LET G2$(2)=FNF$(A1$(112,10),M7$),MORE$[PAGE,X1,N+2]=G2$
2670 IF FAX$<>"" THEN LET G2$(2)=FAX$+" fax",MORE$[PAGE,X1,N+3]=G2$
2680 LET G2$(2)=A1$(126,20),MORE$[PAGE,X1,N+4]=G2$
2685 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),LLIST$[PAGE,X1,5]=SNAME$
2690 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2910 IF LOCKED=0 THEN GOTO 1400
2920 LET LOCKED=0
2930 IF PREFIX$<>"" AND PREFIX$<>"*" THEN GOTO 1400
2940 LET K$=K$(1,LEN(K$)-1)+CHR(ASC(K$(LEN(K$),1))+1)
2950 READ (APM01_DEV,KEY=K$,DOM=1400,END=3000)IOL=APM01A
2990 GOTO 1820
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3035 LET MESSAGE$[0]="No vendors found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page"
3510 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
6500 REM " --- Error detected during processing of selection"
6510 DIM MESSAGE$[1]
6520 LET MESSAGE$[0]="Purchase Orders Is Required For This"
6530 LET MESSAGE$[1]="Selection (<Enter>=Continue)"
6540 CALL "SYC.XA",2,MESSAGE$[ALL],1,-1,-1,V$,V3
6590 RETURN 
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNU$(Q$)=CVS(Q$,4)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 FOR X=1 TO FILES
9915 CLOSE (CHANNELS[X],ERR=9916)
9920 NEXT X
9925 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9930 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
9935 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9940 IF SELECTION$="END" THEN LET SELECTION$=""
9945 IF SELECTION$="PRIOR" AND SPEEDSEARCH$="N" THEN LET SELECTION$=""
9950 EXIT 
9999 END

0010 REM "MPM - MRP Inquiry"
0020 REM "Program MPM.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "29Oct96 JJD: Prevent item# display from erasing screen mask. (5040)"
0040 REM 
0050 SETERR 9000
0070 ON O0 GOTO 0080,1400
0080 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=11
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="MPE-01",FILES$[2]="MPM-01",FILES$[3]="APM-01"
0120 LET FILES$[4]="BMM-01",FILES$[5]="SYS-01",FILES$[6]="IVM-01"
0125 LET FILES$[7]="IVM-02",FILES$[8]="IVM-05",FILES$[9]="IVM-10"
0130 LET FILES$[10]="IVM-03",FILES$[11]="IVM-12"
0150 CALL "SYC.DA",1,1,11,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9000
0160 LET MPE01_DEV=CHANNELS[1],MPM01_DEV=CHANNELS[2],APM01_DEV=CHANNELS[3]
0165 LET BMM01_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5],IVM01_DEV=CHANNELS[6]
0170 LET IVM02_DEV=CHANNELS[7],IVM05_DEV=CHANNELS[8],IVM10_DEV=CHANNELS[9]
0175 LET IVM03_DEV=CHANNELS[10],IVM12_DEV=CHANNELS[11]
0200 REM " --- IOLIST's"
0210 MPE03A: IOLIST A0$(1),A1$(1),A[ALL]
0220 MPM01A: IOLIST B0$(1),B1$(1),B2$(1),B[ALL]
0230 IVM01A: IOLIST C0$(1),C1$(1),C2$(1),C3$,C4$,C5$,C6$,C[ALL]
0240 IVM02A: IOLIST D0$(1),D1$(1),D2$(1),D[ALL]
0260 IVS01A: IOLIST X$,X$,P2$,P3$,P4$,X$,M1$,M2$,M3$
0265 APS01A: IOLIST X$,X$,R2$,X$,X$,M0$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 IVM10C: IOLIST X0$(1)
0285 IVM10F: IOLIST Y0$(1)
0290 IVS01C: IOLIST *,ABCP[ALL],FACTORS[ALL]
0295 GLS01A: IOLIST G0$,G1$,G2$,G3$
0400 REM " --- Parameters"
0410 DIM P[4],R[1],ABCP[25],FACTORS[25]
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0425 LET DAY_OF_MONTH=NUM(F0$(22,2))
0430 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N4$=F5$
0432 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=0440)IOL=GLS01A
0434 LET TOT_PERS=NUM(G2$(1,2))
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0450 FIND (SYS01_DEV,KEY=N0$+N2$+"02",DOM=9800)IOL=IVS01C
0460 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0470 LET P[0]=NUM(P2$(1,2)),IVS01_PRECISION=NUM(P2$(5,1)),P[2]=NUM(P2$(9,2))
0480 LET P[3]=NUM(P2$(11,2)),P[4]=NUM(P2$(13,2)),FIELDS=1,MP=26
0490 LET R[0]=NUM(R2$(1,2))
0495 LET CURPER$=P4$(1,2),CURYR$=P4$(3,2),CURPER=NUM(CURPER$),CURYR=FNYY_YEAR(CURYR$)
0500 REM " --- Initializations"
0510 PRECISION IVS01_PRECISION
0520 DIM J1$(30),A0$(28),A1$(MP*6),A[MP*6],B0$(5),B1$(83),B2$(MP*26)
0530 DIM B[2],C0$(22),C1$(P[2]+P[3]+P[4]),C2$(62),C[12]
0540 DIM DESCRIPTION$[FIELDS],FIELD[FIELDS],POSITION[FIELDS],LENGTH[FIELDS]
0550 DIM D0$(24),D1$(64),D2$(1),D[20],X0$(32),Y0$(32)
0560 LET FORECAST$=""
0570 LET A0$(1,2)=N0$,L0=7,L1=13,DW=80-(17+P[0]+2),M0=LEN(M0$)
0580 CALL "SYC.PA",SYS01_DEV,CURPER,CURYR,BEGPER$,ENDPER$,STATUS
0600 REM " --- Masks"
0610 CALL "SYC.RM",M1$,M1,8,11
0620 LET X=M1,M4$=M2$,M4=LEN(M4$),M5$="-##0.00%",M5=LEN(M5$)
0630 CALL "SYC.RM",M2$,M2,X,M1
0640 CALL "SYC.RM",M3$,M3,6,11
0650 CALL "SYC.RM",M4$,M4,6,10
0700 REM " --- Background"
0730 PRINT 'SB',@(2,3),"Forecast Type:",@(54),"From Period",@(69),"Through",@(1,4),"Warehouse Code:",@(11,5),"Item:",@(0,6),"Unit Of Measure:",@(21),"Product Type:",@(40),"Buyer:",'SF',
1000 REM " --- Forecast Type"
1010 PRINT 'CF',
1015 LET V0$="S",V1$="KC",V2$=FORECAST$,V3$="",V0=3,V1=17,V2=3
1020 LET V4$="Enter A Valid Forecast Type (<F3>=Lookup)"
1025 GOSUB 7000
1030 ON V3 GOTO 1040,1000,1000,1035,9900
1035 GOSUB 6000
1040 IF V$="" THEN GOTO 1000
1045 LET B0$(1)=N0$+V$,WHSE$="",ITEM$=""
1050 FIND (MPM01_DEV,KEY=B0$,DOM=1000)IOL=MPM01A
1055 LET A0$(3,3)=B0$(3,3),FORECAST$=A0$(3,3),BEGPER=1,ENDPER=B[0]
1060 PRINT @(V1,V2),B0$(3,3)," ",B1$(1,30),@(66,V2),BEGPER:"#0",@(77,V2),ENDPER:"#0",
1065 CALL "MPC.FA",MPE03_DEV,"MPE-03",N0$,FORECAST$,X$,STATUS
1070 IF STATUS THEN GOTO 1000
1075 LET K$=KEY(MPE03_DEV,END=1300)
1080 LET WHSE$=K$(6,2),ITEM$=K$(8,P[0])
1095 GOTO 1300
1100 REM " --- Beginning Period"
1110 LET V0$="N",V1$="C",V2$=STR(BEGPER),V3$="##",V0=2,V1=66,V2=3
1120 LET V4$="Enter The Beginning Period To Print (1 To "+STR(B[0])+")"
1130 GOSUB 7000
1140 ON V3 GOTO 1150,1100,1000,1100,1000
1150 IF V<1 OR V>B[0] THEN GOTO 1100
1160 LET BEGPER=V
1170 PRINT @(V1,V2),BEGPER:"#0",
1200 REM " --- Ending Period"
1205 PRINT @(17,4),'CL',
1210 LET V0$="N",V1$="C",V2$=STR(ENDPER),V3$="##",V0=2,V1=77,V2=3
1220 LET V4$="Enter The Ending Period To Print ("+STR(BEGPER)+" To "+STR(B[0])+")"
1230 GOSUB 7000
1240 ON V3 GOTO 1250,1200,1100,1200,1100
1250 IF V<BEGPER OR V>B[0] THEN GOTO 1200
1260 LET ENDPER=V
1270 PRINT @(V1,V2),ENDPER:"#0",
1300 REM " --- Warehouse"
1305 PRINT @(17,4),'CL',@(17,5),'CL',
1315 LET V0$="S",V1$="C",V2$=WHSE$,V3$="",V0=2,V1=17,V2=4
1320 IF V2$="" THEN LET V2$=P3$(6,2)
1325 LET V4$="Enter A Valid Warehouse (<F3>=Lookup)"
1330 IF FNP$(V2$)<>"" THEN LET V4$=V4$(1,LEN(V4$)-1)+"/<Enter>=Next Record)"
1335 GOSUB 7000
1340 ON V3 GOTO 1355,1300,1200,1345,9900
1345 GOSUB 6100
1350 IF V$="" THEN GOTO 1300
1355 IF V$<>WHSE$ THEN LET ITEM$=""
1360 LET X0$(1)=N0$+"C"+V$
1365 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=1300)IOL=IVM10C
1370 PRINT @(V1,V2),X0$(4,2),"  ",X0$(6,24),
1375 LET A0$(6,2)=X0$(4,2),WHSE$=X0$(4,2)
1390 READ (MPE03_DEV,KEY=A0$(1,7),DOM=1400)
1400 REM " --- Item"
1403 GOSUB 6600
1405 PRINT @(0,L0),'CE',@(17,6),"  ",@(35,6),"   ",@(47,6),'CL',
1410 LET V0$="S",V1$="KC",V3$="",V0=P[0],V1=17,V2=5
1415 LET V4$="Enter A Valid Item (<F3>=Lookup)"
1420 IF FNP$(V2$)<>"" THEN LET V4$=V4$(1,LEN(V4$)-1)+"/<Enter>=Next Record)"
1425 GOSUB 7000
1430 ON V3 GOTO 1445,1400,1300,1435,9900
1435 CALL "SYC.LI",V$
1440 IF V$="" THEN GOTO 1400
1445 LET C0$(1,22)=N0$+V$,D0$(1,24)=N0$+A0$(6,2)+V$
1450 FIND (IVM01_DEV,KEY=C0$,DOM=1400)IOL=IVM01A
1455 FIND (IVM02_DEV,KEY=D0$,DOM=1400)IOL=IVM02A
1460 LET A0$(8)=C0$(3),ITEM$=C0$(3)
1500 REM " --- Retrieve MPE-03 record"
1510 LET A0$(LEN(A0$))="A"
1520 READ (MPE03_DEV,KEY=A0$,DOM=1540)IOL=MPE03A
1530 GOTO 1590
1540 LET A0$(LEN(A0$))="B"
1550 READ (MPE03_DEV,KEY=A0$,DOM=1400)IOL=MPE03A
1590 GOSUB 5000
4000 REM " --- Run detail overlay"
4010 LET WIDTH=80,LINES=6,O0=1,NUMBER=ENDPER-BEGPER+3
4020 LET NUMCOL=INT(65/M1),BEGPER=BEGPER-1,ENDPER=ENDPER-1
4030 IF NUMCOL>NUMBER THEN LET NUMCOL=NUMBER
4040 LET X=NUMCOL*M1+15,HR=WIDTH-M4
4050 LET POSITION=INT((WIDTH-X)/2)+16,HC=HR-16-M4
4060 CALL "SYC.WC",1,0,80,0,0,L0,0
4070 CALL "SYC.WC",1,0,80,0,0,L0+5,0
4090 ON POS(A0$(LEN(A0$))="AB") GOTO 1400,4100,4200
4100 REM " --- Purchases"
4190 RUN "MPM.BB"
4200 REM " --- Production"
4290 RUN "MPM.BC"
5000 REM " --- Display item information"
5010 DIM X$(DW),Y0$(32)
5020 LET X$(1)=FNZ$(C1$,P[2],P[3],P[4]),Y0$(1)=N0$+"F"+D1$(19,3)
5030 FIND (IVM10_DEV,KEY=Y0$(1,6),DOM=5040)IOL=IVM10F
5040 PRINT @(17,5),C0$(3,P[0])," ",X$,@(17,6),C2$(4,2),@(35,6),C2$(1,3),@(47,6),Y0$(4,3)," ",Y0$(7,20),
5090 RETURN 
6000 REM " --- Forecast Lookup"
6010 LET RECORD$="",TITLE$="MRP Forecast Types"
6030 LET DESCRIPTION$[0]="Type",CHANNEL=MPM01_DEV,FIELD[1]=2
6040 LET POSITION[0]=3,LENGTH[0]=3,POSITION[1]=1,LENGTH[1]=30
6090 GOTO 6400
6100 REM " --- Warehouse Lookup"
6110 LET RECORD$="C",TITLE$="Warehouse Lookup"
6130 LET DESCRIPTION$[0]="ID",CHANNEL=IVM10_DEV,FIELD[1]=1
6140 LET POSITION[0]=4,LENGTH[0]=2,POSITION[1]=6,LENGTH[1]=24
6400 REM " --- Call Windowed Lookup"
6410 LET FIELD[0]=1,DESCRIPTION$[1]="Description"
6440 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,ROW=V2+2,V$="SKIP"
6450 CALL "SYC.LK",CHANNEL,RECORD$,FIELDS,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6490 RETURN 
6500 REM " --- Retrieve first item for forecast/whse combination"
6590 READ (MPE03_DEV,KEY=A0$(1,7),DOM=6600)
6600 REM " --- Next Item"
6610 LET V2$="",V2$=KEY(MPE03_DEV,END=6690)
6620 IF POS(A0$(1,6)=V2$)<>1 THEN LET V2$=""
6630 IF V2$<>"" THEN LET V2$=V2$(8,P[0])
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8070 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNZ$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

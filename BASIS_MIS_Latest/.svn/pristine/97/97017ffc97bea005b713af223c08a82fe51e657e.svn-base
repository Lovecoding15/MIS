0010 REM "SYC - Batch Lookup"
0020 REM "Program SYC.LF"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "SYM02_DEV : SYM-02 Channel"
0052 REM "SYM49_DEV : SYM-49 Channel"
0054 REM "SYM59_DEV : SYM-59 Channel"
0056 REM "PROCESS$  : Process to find Batches for (input)"
0058 REM "FIRM$     : Firm to find Batches for (input)"
0060 REM "SELECTION$: User selection (returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER SYM02_DEV,SYM49_DEV,SYM59_DEV,PROCESS$,FIRM$,SELECTION$
0200 REM " --- IOLIST's"
0210 SYM02A: IOLIST *,X1$(1)
0220 SYM49A: IOLIST A0$,A1$,A2$
0400 REM " --- Parameters"
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0420 IF PAGES<3 THEN GOTO 9900
0430 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0500 REM " --- Initializations"
0530 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$=""
0540 LET WIDTH=58,HEIGHT=12,WIN_X=20,WIN_Y=8,MAX_ROW=HEIGHT-5
0550 LET SWIDTH=WIDTH,SHEIGHT=4,SWIN_X=WIN_X,SWIN_Y=WIN_Y+HEIGHT
0600 REM " --- Window arrays"
0610 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0620 LET HEADING$(2)="Batch",HEADING$(8)="Description",HEADING$(39)="Opened"
0630 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0640 LET TITLE$="Batch Lookup",STITLE$="Batch Detail"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Detail Window"
0910 CALL "SYC.WA",0,SWIDTH,SHEIGHT,SWIN_X,SWIN_Y,STITLE$,SNAME$
0920 CALL "SYC.WB",NAME$,0
1000 REM " --- Initialize For Next Lookup"
1010 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$=""
1020 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,SHEIGHT-2]
1100 REM " --- Position Sort File"
1110 READ (SYM59_DEV,KEY=PROCESS$+FIRM$,DOM=1400)
1190 GOTO 1550
1500 REM " --- Next Sort Record"
1510 LET K$=KEY(SYM59_DEV,END=3000)
1520 IF POS(PROCESS$+FIRM$=K$)<>1 THEN GOTO 3000
1530 READ (SYM59_DEV,KEY=K$)
1580 LET K$=K$(13)
1900 REM " --- Read Process Batch Control Record"
1905 LET LOCKED=0
1910 READ (SYM49_DEV,KEY=K$,ERR=1950)IOL=SYM49A
1940 GOTO 2000
1950 REM " --- Error reading record"
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1980 LET LOCKED=1
2000 REM " --- Display page?"
2010 IF L<MAX_ROW THEN GOTO 2400
2020 LET XMODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 1000
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2400 REM " --- Lookup window record"
2410 DIM G1$(WIDTH-2)
2415 IF LOCKED THEN GOTO 2450
2420 IF A1$(22,2)>="12" THEN LET PM$=" PM" ELSE LET PM$=" AM"
2425 IF A1$(22,2)>"12" THEN LET HOUR$=STR(NUM(A1$(22,2))-12:"00") ELSE LET HOUR$=A1$(22,2)
2430 LET G1$(3)=A0$,G1$(8)=A2$,G1$(39)=FNB6$(A1$(16,6))+" "+HOUR$+":"+A1$(24,2)+PM$
2440 GOTO 2490
2450 LET G1$(3)=K$,G1$(8)="Record In Use"
2490 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=K$
2600 REM " --- Detail window record"
2610 DIM G2$(SWIDTH-2)
2615 IF LOCKED=1 THEN GOTO 2680
2620 DIM X1$(24)
2630 LET X1$(1)="Not On File"
2640 READ (SYM02_DEV,KEY=A1$(13,3),DOM=2650)IOL=SYM02A
2650 LET G2$(1)="{Created By:}"+X1$,MORE$[PAGE,X1,1]=G2$
2660 IF A1$(32,2)>="12" THEN LET PM$=" PM" ELSE LET PM$=" AM"
2665 IF A1$(32,2)>"12" THEN LET HOUR$=STR(NUM(A1$(32,2))-12:"00") ELSE LET HOUR$=A1$(32,2)
2670 LET G2$(1)=" {Last Used:}"+FNB6$(A1$(26,6))+" "+HOUR$+":"+A1$(34,2)+PM$,MORE$[PAGE,X1,2]=G2$
2680 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),LLIST$[PAGE,X1,5]=SNAME$
2690 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2910 IF LOCKED=0 THEN GOTO 1500
2920 LET LOCKED=0
2990 GOTO 1500
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=WIN_X+6
3035 LET MESSAGE$[0]="No Batches Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3090 GOTO 4000
3500 REM " --- Display last page"
3510 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
8000 REM " --- Functions"
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9930 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
9935 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9940 IF SELECTION$="END" THEN LET SELECTION$=""
9945 IF SELECTION$="PRIOR" THEN LET SELECTION$=""
9950 EXIT 
9999 END

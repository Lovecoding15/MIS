0010 REM "APR - Check Printing 2/Moore Form #7010A.O."
0020 REM "Program APR.EB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- OPEN/LOCK FILES"
0200 REM " --- IOLISTS"
0210 APE04A1: IOLIST A0$,A[ALL]
0220 APM01A: IOLIST B0$,B1$(1)
0230 APM03A: IOLIST B0$,B1$(1)
0240 APW01A: IOLIST D0$
0250 APT02A: IOLIST E0$,E[0],E[1]
0260 APE04A2: IOLIST W0$(1),W[ALL]
0270 APM10A: IOLIST X$
0500 REM " --- INIT DATA"
0520 LET Q6=0
1000 REM " --- INIT READ"
1010 IF P7$="Y" THEN READ (APE04_DEV1,KEY=N0$+P6$+P5$,DOM=1100)
1020 READ (APE04_DEV1,KEY=N0$,DOM=1030)
1100 REM " --- MAIN READ"
1120 LET K$=KEY(APE04_DEV1,END=4000)
1130 IF K$(1,2)<>N0$ THEN GOTO 4000
1140 READ (APE04_DEV1)IOL=APE04A1
1200 REM " --- BREAKS"
1220 IF K$(3,2)<>T0$ THEN GOSUB 6000
1240 IF K$(3,8)<>T1$ THEN GOSUB 6200
2540 GOSUB 5200
2600 REM " --- ACCUMULATE"
2620 FOR X=0 TO 2
2630 LET T[X]=T[X]+A[X]
2640 NEXT X
2900 GOTO 1100
4000 REM " --- TOTALS"
4020 LET T9$="E"
4040 GOSUB 6200
4100 GOTO 9900
5000 REM " --- CHECK STUB HEADER"
5010 LET L=0
5015 REM PRINT (7)@(H0+25),X1$,@(H0+40),"Check No: ",P8$
5020 REM PRINT (7)@(H0+25),B1$(1,30)
5025 REM PRINT (7)@(H0+25),"Check Date: ",FNB$(P9$),@(H0+50),"Account ID#: ",B1$(176,10)
5026 PRINT (7)H9$(1,3),@(H0),"Check: ",P8$,@(H0+15),FNB$(P9$),@(H0+25),B1$(1,30),@(H0+57),"Account Id#: ",B1$(176,10)
5030 PRINT (7)@(H0),"Inv Date",@(H0+11),"Inv Number",@(H0+23+M1-8),"Inv Amt",@(H0+23+2*M1-9),"Discount",@(H0+23+3*M1-9),"Amt Paid",
5040 LET H1=0
5050 IF P3$(7,1)<>"Y" THEN GOTO 5100
5060 PRINT (7)@(H0+24+3*M1),"Memo",
5070 LET H1=5
5080 IF P3$(8,1)<>"Y" THEN GOTO 5100
5090 PRINT (7)"/",
5100 IF P3$(8,1)="Y" THEN PRINT (7)@(H0+24+3*M1+H1),"Distribution",
5120 PRINT (7)""
5190 RETURN 
5200 REM " --- INVOICE DETAIL"
5210 IF L>L9 THEN GOSUB 5700
5220 PRINT (7)@(H0),FNB$(A0$(21,3)),@(H0+11),A0$(11,10),@(H0+23),A[0]:M1$,A[1]:M1$,A[0]-A[2]-A[1]:M1$,
5222 LET L1=0
5225 IF POS(" "<>A0$(40,16))=0 THEN GOTO 5258
5227 IF H0+24+3*M1+16>79 THEN LET H3=H0+24+3*M1+16-80 ELSE LET H3=0
5230 IF P3$(7,1)="Y" THEN PRINT (7)@(H0+24+3*M1),A0$(40,16-H3),
5250 IF P3$(8,1)="N" THEN GOTO 5380
5252 IF P3$(7,1)="Y" THEN PRINT (7)'LF',
5254 IF P3$(7,1)="Y" THEN LET L=L+1,L1=L1+1
5258 IF P3$(8,1)="N" THEN GOTO 5380
5260 READ (APT02_DEV,KEY=A0$(1,10)+"I"+A0$(11,10),DOM=5270)
5270 LET K1$=KEY(APT02_DEV,END=5380)
5280 IF K1$(1,21)<>A0$(1,10)+"I"+A0$(11,10) THEN GOTO 5380
5290 READ (APT02_DEV)IOL=APT02A
5300 IF L>L9 THEN GOSUB 5700
5302 LET X0=0
5304 IF A[3]<>0 THEN LET X0=E[0]*(A[0]-A[2])/A[3]
5305 LET DIMLEN=80-(H0+24+3*M1)
5306 DIM X$(DIMLEN)
5308 IF GL$<>"Y" THEN GOTO 5316
5310 LET X$(1)=FNF$(E0$(22,G[3]),G5$)
5312 LET Y$=STR(X0:M1$),Y$=Y$(POS(" "<>Y$))
5314 LET X$(DIMLEN-LEN(Y$))=" "+Y$
5316 PRINT (7)@(H0+24+3*M1),X$
5320 LET L1=L1+1,L=L+1
5330 GOTO 5270
5380 IF L1=0 THEN PRINT (7)'LF',
5385 IF L1=0 THEN LET L=L+1
5390 RETURN 
5400 REM " --- STUB TOTAL"
5420 PRINT (7)H9$(1,L9-L+1)
5430 PRINT (7)@(H0+24),T[0]:M1$,T[1]:M1$,T[0]-T[2]-T[1]:M1$
5490 RETURN 
5600 REM " --- PRINT CHECK"
5610 PRINT (7)H9$(1,8); REM "WA:200
5620 PRINT (7)@(H0+45),FNB$(P9$),@(H0+57),P8$,
5640 LET X$=STR(FPT(T[0]-T[2]-T[1])*100:"00")
5650 IF X$="00" THEN LET X$="NO"
5670 PRINT (7)@(77-M9),T[0]-T[1]-T[2]:M9$,'LF'
5675 PRINT (7)@(H0+7),"Exactly*",INT(T[0]-T[2]-T[1]):M8$,"*Dollars*And*",X$,"*Cents",'LF','LF','LF'
5680 PRINT (7)@(H0+7),B1$(1,30),'LF',@(H0+7),B1$(31,24)
5681 IF POS(" "<>B1$(55,24))>0 THEN PRINT (7)@(H0+7),B1$(55,24)
5682 PRINT (7)@(H0+7),B1$(79,24)," ",B9$
5683 PRINT (7)'FF',
5690 RETURN 
5700 REM " --- OVERFLOW"
5720 GOSUB 5800
5740 GOSUB 5000
5790 RETURN 
5800 REM " --- VOID CHECK"
5802 PRINT (7)H9$(1,L9-L+1)
5804 PRINT (7)@(H0+22+2*M1),"*Continued*"
5810 PRINT (7)H9$(1,6)
5820 PRINT (7)@(H0+69),P8$,'LF','LF'
5860 PRINT (7)@(H0+7),"***Void***Void***Void***Void***",'LF'
5870 PRINT (7)@(H0+52),FNB$(P9$),@(H0+68),"***Void***",'LF'
5880 PRINT (7)'ff';rem H9$(1,8)
5900 REM " --- WRITE VOID"
5920 LET D0$=N0$+A0$(3,2)+P8$+"V"+P9$+A0$(5,16),D$=D0$(1,11)
5930 IF P3$(1,1)<>"Y" THEN LET D$(3,2)=""
5940 WRITE (APW01_DEV,KEY=D$)IOL=APW01A
5960 GOSUB 6800
5990 RETURN 
6000 REM " --- AP TYPE BREAK"
6005 LET Q5=1
6010 LET T9$="A"
6020 GOSUB 6200
6030 LET T0$=A0$(3,2),T1$="",X$="",T9$=""
6040 IF P3$(1,1)<>"Y" THEN GOSUB 6900; RETURN 
6050 IF P3$(6,1)="N" THEN IF Q6<>0 THEN RETURN 
6060 LET Q6=1
6100 REM " --- NEW CHECKS"
6105 IF P3$(6,1)="N" THEN GOTO 6157
6130 FIND (APM10_DEV,KEY=N0$+"A"+T0$,DOM=6150)IOL=APM10A
6132 FOR V=20 TO 1 STEP -1
6134 IF X$(6+V,1)<>" " THEN EXITTO 6138
6136 NEXT V
6140 LET X$=X$(6,V+1)+" "
6145 PRINT @(40,9),T0$," ",X$,
6150 PRINT @(7,13),"Please Mount The ",X$,"Checks <Enter>: ",
6152 LET V0$="S",V1$="",V2$="",V3$="",V4$="",V0=1,V1=40+LEN(X$),V2=13
6155 GOSUB 7000
6156 PRINT @(0,13),'CL',
6157 REM " --- DATE"
6160 LET V0$="D",V1$="RC",V2$=P9$,V0=8,V1=40,V2=3,V4$="Enter Check Date For The "+X$+"Checks"
6165 GOSUB 7000
6166 LET P9$=V$
6167 IF V3=4 THEN GOTO 9900
6168 IF GL$="Y" THEN CALL "GLC.EA",P9$,"Y",PERIOD$,YEAR$,STATUS
6170 IF STATUS>99 THEN GOTO 6157
6172 REM " --- CHECK NUMBER"
6175 LET V0$="Z",V1$="RC",V0=7,V1=40,V2=5,V4$="Enter Beginning Check Number For The "+X$+"Checks",V2$=P8$
6177 GOSUB 7000
6178 IF V3=4 THEN GOTO 9900
6180 LET P8$=V$+J1$,P8$=P8$(1,7)
6181 LET V0$="Y",V1$="C",V2$="N",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=61,V2=22
6182 GOSUB 7000
6183 IF V3=4 THEN GOTO 9900
6184 IF V$="N" THEN GOTO 6160 ELSE IF V$<>"Y" THEN GOTO 6181
6194 GOSUB 6900
6195 RETURN 
6200 REM " --- NEW VENDOR"
6210 IF T1$="" THEN GOTO 6500
6220 IF T[0]-T[2]-T[1]<0 THEN GOTO 6490
6230 GOSUB 5400
6240 GOSUB 5600
6250 DIM W0$(65)
6260 LET W0$(1)=T2$,W$=W0$(1,20)
6270 IF P3$(1,1)<>"Y" THEN LET W$(3,2)=""
6280 FIND (APE04_DEV2,KEY=W$,DOM=6490)IOL=APE04A2
6285 IF POS(" "<>W0$(56,7))=0 THEN GOTO 6400
6290 LET D$=W$(1,4)+W0$(56,7)
6300 FIND (APW01_DEV,KEY=D$,DOM=6400)IOL=APW01A
6320 IF D0$(1,4)+D0$(16,16)<>W$ THEN GOTO 6400
6330 LET D0$=D0$(1,11)+"V"+P9$+T2$(5,16)
6340 WRITE (APW01_DEV,KEY=D$)IOL=APW01A
6400 LET D0$=T2$(1,4)+P8$+"C"+P9$+T2$(5,16),D$=D0$(1,11)
6410 IF P3$(1,1)<>"Y" THEN LET D$(3,2)=""
6415 IF P3$(1,1)="Y" THEN REMOVE (APW01_DEV,KEY=D$(1,2)+"  "+D$(5,7),DOM=6420)
6420 WRITE (APW01_DEV,KEY=D$)IOL=APW01A
6440 LET W0$(56,7)=P8$
6450 WRITE (APE04_DEV2,KEY=W$)IOL=APE04A2
6470 GOSUB 6800
6480 GOTO 6500
6490 GOSUB 5400
6495 GOSUB 5810
6500 IF T9$<>"" THEN RETURN 
6510 DIM X1$(M0),B1$(200),T[2]
6520 LET T2$=A0$
6530 IF A0$(5,4)="TEMP" THEN GOTO 6600
6550 FIND (APM01_DEV,KEY=N0$+A0$(5,6),DOM=6570)IOL=APM01A
6560 LET B9$=FNZ$(B1$(103))
6570 IF POS(" "<>B1$(79,24))=0 THEN LET B1$(79,24)=B9$,B9$=""
6580 LET X1$(1)=FNF$(A0$(5,P[0]),M0$),T1$=K$(3,8)
6590 GOTO 6700
6600 REM " --- TEMP NAME"
6610 LET X1$(1)="",T1$="TEMP",B9$=""
6620 FIND (APM03_DEV,KEY=A0$(1,4)+A0$(11,10),DOM=6640)IOL=APM03A
6700 GOSUB 5000
6790 RETURN 
6800 REM " --- INCREMENT CHECK #"
6820 LET P8=NUM(P8$)+1
6830 IF P8>9999999 THEN LET P8=1
6840 LET P8$=STR(P8:J0$(1,7))
6860 PRINT @(40,5),P8$,
6890 RETURN 
6900 REM " --- TEST PATTERN"
6902 LET Z0$=A0$,Z1$=B1$,Z0=A[0],Z1=A[1],Z2=A[2],Z3=A[3],Z4=T[0],Z5=T[1],Z6=T[2]
6904 CALL "SYC.YN",0,"Do You Wish To Print A Test Pattern",4,V$,V3
6910 PRINT @(0,13),'CL',
6912 IF V3=4 THEN GOTO 9900
6915 ON POS(V$="YN") GOTO 6904,6920,6980
6925 DIM B1$(200,"X"),A0$(60,"X"),T[2]
6930 LET A[0]=9999.99,A[1]=A[0],T[0]=A[0],T[1]=A[1],A0$(3,2)="",A0$(21,3)=P9$
6935 GOSUB 5000
6940 FOR X=1 TO 3
6945 GOSUB 5200
6950 NEXT X
6960 LET A0$=Z0$,B1$=Z1$
6965 GOSUB 5800
6970 GOTO 6904
6980 LET A0$=Z0$,B1$=Z1$,A[0]=Z0,A[1]=Z1,A[2]=Z2,A[3]=Z3,T[0]=Z4,T[1]=Z5,T[2]=Z6
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

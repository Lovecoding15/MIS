0010 REM "SAX - Rebuild Sales Analysis From Invoice History
0020 REM "Program SAX.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=21
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ARM-02",FILES$[3]="ARM-10"
0130 LET FILES$[4]="ART-03",FILES$[5]="ART-13",FILES$[6]="IVM-01"
0135 LET FILES$[7]="IVM-02",FILES$[8]="IVW-05",FILES$[9]="SAM-01"
0140 LET FILES$[10]="SAM-02",FILES$[11]="SAM-03",FILES$[12]="SAM-04"
0145 LET FILES$[13]="SAM-05",FILES$[14]="SAM-06",FILES$[15]="SAM-07"
0150 LET FILES$[16]="SAM-08",FILES$[17]="SAM-09",FILES$[18]="SAM-10"
0155 LET FILES$[19]="SAM-11",FILES$[20]="SAM-21",FILES$[21]="SYS-01"
0160 CALL "SYC.DA",1,1,21,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0165 IF STATUS>0 THEN GOTO 9900
0170 LET ARM01_DEV=CHANNELS[1],ARM02_DEV=CHANNELS[2],ARM10_DEV=CHANNELS[3]
0175 LET ART03_DEV=CHANNELS[4],ART13_DEV=CHANNELS[5],IVM01_DEV=CHANNELS[6]
0180 LET IVM02_DEV=CHANNELS[7],IVW05_DEV=CHANNELS[8],SAM01_DEV=CHANNELS[9]
0185 LET SAM02_DEV=CHANNELS[10],SAM03_DEV=CHANNELS[11],SAM04_DEV=CHANNELS[12]
0190 LET SAM05_DEV=CHANNELS[13],SAM06_DEV=CHANNELS[14],SAM07_DEV=CHANNELS[15]
0195 LET SAM08_DEV=CHANNELS[16],SAM09_DEV=CHANNELS[17],SAM10_DEV=CHANNELS[18]
0197 LET SAM11_DEV=CHANNELS[19],SAM21_DEV=CHANNELS[20],SYS01_DEV=CHANNELS[21]
0200 REM " --- IOLists
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 ARS01A: IOLIST U0$,CUSTIMASK$,U2$,U3$,U4$,CUSTOMASK$
0320 SAS01A: IOLIST *,OPTPARAMS$(1),LEVPARAMS$(1)
0330 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="SA",N4$=F5$
0435 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET IVPRECISION=NUM(I2$(5,1)),ITEMLEN=NUM(I2$(1,2))
0444 IF POS(I3$(3,1)="LF")>0 THEN LET LF$="Y"
0450 DIM OPTPARAMS$(11),LEVPARAMS$(11)
0460 FIND (SYS01_DEV,KEY=N0$+"SA00",DOM=9800)IOL=SAS01A
0470 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0480 LET CUSTLEN=NUM(U2$(1,2))
0500 REM " --- Initializations
0510 DIM MESSAGES$[5]
0520 LET MESSAGES$[0]="This Utility Will Generate Sales Analysis For The Current Firm In The"
0530 LET MESSAGES$[1]="Selected SA Files Using Your Existing Invoice History. It Is Recommended"
0540 LET MESSAGES$[2]="That You Purge Existing Sales Analysis Prior To Running This Program,"
0550 LET MESSAGES$[3]="Or Your Sales Analysis Data May Be Duplicated. (<Enter>=Continue)"
0560 CALL "SYC.XA",2,MESSAGES$[ALL],3,-1,-1,V$,V3
0570 IF V3=4 THEN GOTO 9900
0580 DIM OPROMPTS$[11],OPTIONS$(11)
0590 LET OPTIONS$(1)=OPTPARAMS$,I0=0
0600 REM " --- Prompts
0605 LET OPROMPTS$[1]="By Customer",OPROMPTS$[2]="By Territory"
0610 LET OPROMPTS$[3]="By Salesperson",OPROMPTS$[4]="By Customer Type"
0615 LET OPROMPTS$[5]="By Customer Ship-To",OPROMPTS$[6]="By Customer SIC"
0620 LET OPROMPTS$[7]="By Product Type",OPROMPTS$[8]="By Warehouse"
0625 LET OPROMPTS$[9]="By Stocking Vendor",OPROMPTS$[10]="By Distribution Code"
0630 LET OPROMPTS$[11]="By Non-Stock Item"
0700 REM " --- Background
0710 PRINT 'SB',@(16,4),"Rebuild For Which Sales Analysis Options:",'SF',
0720 FOR X=1 TO 11
0730 PRINT 'SB',@(35-LEN(OPROMPTS$[X]),X+5),OPROMPTS$[X],":",'SF',
0740 PRINT @(37,X+5),OPTPARAMS$(X,1),'CL',
0750 NEXT X
0790 GOTO 4000
1100 REM " --- Sales Analysis Options
1110 LET X=I0+1,V0$="Y",V1$="",V2$=OPTIONS$(X,1),V3$="",V0=1,V1=37,V2=X+5
1120 LET V4$="Rebuild Sales Analysis "+OPROMPTS$[X]+" (Y/N)?"
1130 IF OPTPARAMS$(X,1)="N" THEN GOTO 3900; REM "Can't rebuild if not using
1140 GOSUB 7000
1150 ON V3 GOTO 1160,1100,3900,1100,3900
1160 LET OPTIONS$(X,1)=V$
1170 GOTO 3900
3900 REM " --- Next Field
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1 ELSE LET V3=0
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1100,1100,1100,1100,1100,1100,1100,1100,1100,1100,1100,3990
3990 LET I0=0
4000 REM " --- Correct?
4010 LET V4$="Is The Above Information Correct (Y/N)?",I0=0
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4060,4000,4000,4000,9900
4060 IF V$="DELETE" THEN GOTO 4290
4090 ON POS(V$="YN") GOTO 4000,4100,1100
4100 REM " --- Run Overlay
4120 RUN "SAX.BB"
4190 GOTO 9900
5000 REM " --- Display
5010 FOR X=1 TO 11
5020 LET X$=""
5050 PRINT @(37,X+5),OPTPARAMS$(X,1),'CL',
5080 NEXT X
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

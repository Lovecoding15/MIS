0010 REM "POM - Standard Message Listing"
0020 REM "Program POM.DB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files "
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS>0 THEN GOTO 9900
0200 REM " --- IOLISTS "
0210 POM04A: IOLIST A0$(1),A1$(1)
0220 POM14A: IOLIST W0$(1),W1$(1)
0500 REM " --- Initialize Data "
0510 DIM HEADINGS$[2],W0$(7),W1$(40)
0550 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$+" Listing",CLOCK$="",L9=59
0560 LET WHEN$=F0$(7,3),L=L9+1,WIDTH=80,PAGE=0,NUMBER=0
0700 REM " --- Background "
0710 CALL "SYC.NB","Printing",3,COLUMN
0900 REM " --- Position file"
0990 READ (POM04_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Main Read "
1010 READ (POM04_DEV,END=4000)IOL=POM04A
1020 IF POS(N0$=A0$)<>1 THEN GOTO 4000
1030 PRINT @(COLUMN,11),A0$(3),
1200 REM " --- What type?"
1210 LET CONTINUED$="",TYPE$="Both",NUMBER=NUMBER+1
1220 IF A1$(21,1)="P" THEN LET TYPE$="PO"
1230 IF A1$(21,1)="R" THEN LET TYPE$="Req"
1240 IF A1$(21,1)="N" THEN LET TYPE$="None"
1500 REM " --- Print subheading"
1510 GOSUB 5200
1900 REM " --- Position detail file"
1990 READ (POM14_DEV,KEY=A0$,DOM=2000)
2000 REM " --- Read next detail"
2010 READ (POM14_DEV,END=3000)IOL=POM14A
2020 IF POS(A0$=W0$)<>1 THEN GOTO 3900
2100 REM " --- New page?"
2110 IF L+1<L9 THEN GOTO 2200
2120 GOSUB 5000
2130 GOSUB 5200
2200 REM " --- Print it"
2210 PRINT (7)@(4),W0$(6)," ",W1$
2220 LET L=L+1,CONTINUED$="(Continued)"
2900 REM " --- Loop back for next detail"
2990 GOTO 2000
3900 REM " --- Loop back for next header"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF L>L9 THEN GOSUB 5000
4020 PRINT (7)""
4030 PRINT (7)NUMBER," Standard Purchase Order Message(s)"
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+2
5020 CALL "SYC.HA",7,HEADINGS$[ALL],2,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)"Code   Description",@(76),"Type"
5090 RETURN 
5200 REM " --- Code subheading"
5210 IF L+4>L9 THEN GOSUB 5000
5220 PRINT (7)""
5230 PRINT (7)A0$(3),@(7),A1$(1,20)," ",CONTINUED$,@(76),TYPE$
5240 PRINT (7)""
5250 LET L=L+3
5290 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

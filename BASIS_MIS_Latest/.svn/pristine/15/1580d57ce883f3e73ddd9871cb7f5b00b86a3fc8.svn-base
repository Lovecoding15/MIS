0010 REM "SYC - Return Fiscal Period/Year"
0020 REM "Program SYC.PB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "08Jan97 Caj: Was incorrectly handling non-calendar fiscal years(2020,2090,3000-3030)
0040 REM 
0042 REM "N0$      : Firm ID (Input)"
0044 REM "SYS01_DEV: SYS-01 channel number (Input)"
0055 REM "WHEN$    : YYMMDD or YMD Packed Date (Input)"
0060 REM "PERIOD$  : Fiscal period (Returned)"
0065 REM "YEAR$    : Fiscal year (Returned)"
0075 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER N0$,SYS01_DEV,WHEN$,PERIOD$,YEAR$
0200 REM " --- IOLIST's"
0260 GLS01A: IOLIST G0$,G1$,G2$,G3$(1),G4$(1),M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 DIM G3$(325),G4$(32)
0420 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 LET PERIODS=NUM(G2$(1,2)),PERIODS$=G3$(1,PERIODS*2)
0500 REM " --- Initializations"
0550 LET NUMBERS$="01020304050607080910111213",NUMBERS$=NUMBERS$(1,PERIODS*2)
0560 LET PERIOD$="",YEAR$="",DATE$="",P=0,Y=0
1000 REM " --- Format date"
1010 IF LEN(WHEN$)=3 THEN LET DATE$=WHEN$
1020 IF LEN(WHEN$)=6 THEN LET DATE$=FND$(FNN$(WHEN$))
1090 IF DATE$="" THEN GOTO 4100
1200 REM " --- Sort period ending dates and corresponding period numbers"
1210 FOR X=2 TO PERIODS
1220 LET PRIPER=(X-2)*2+1,CURPER=(X-1)*2+1
1230 IF PERIODS$(CURPER,2)>PERIODS$(PRIPER,2) THEN GOTO 1290
1240 LET PERIODS$=PERIODS$(CURPER)+PERIODS$(1,CURPER-1)
1250 LET NUMBERS$=NUMBERS$(CURPER)+NUMBERS$(1,CURPER-1)
1270 EXITTO 2000
1290 NEXT X
2000 REM " --- Which period/year?"
2010 LET MD$=DATE$(2),P=POS(MD$<=PERIODS$,2)
2020 IF P=0 THEN GOTO 3000
2030 LET P=NUM(NUMBERS$(P,2)),Y=FNY(DATE$)
2040 IF P>1 THEN IF G3$((P-1)*2+1,2)<G3$(1,2) THEN LET Y=Y-1
2090 GOTO 4000
3000 REM " --- 'ENTERED' date is > than all period end dates
3020 LET P=NUM(NUMBERS$(PERIODS*2-1))+1,Y=FNY(DATE$)
3030 IF P>PERIODS THEN LET P=1,Y=Y+1
4000 REM " --- All done"
4010 LET PERIOD$=STR(P:"00")
4020 LET YEAR$=FNYEAR_YY21$(Y)
4100 GOTO 9900
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8070 DEF FNN$(Q$)=Q$(3,4)+Q$(1,2)
8080 DEF FNY(Q1$)=FNYY_YEAR(FNA$(Q1$(1),Q1$))
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9950 EXIT 
9999 END

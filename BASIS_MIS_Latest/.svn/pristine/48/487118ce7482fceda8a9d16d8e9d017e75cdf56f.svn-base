0010 REM "APM - Distribution Code Maintenance"
0020 REM "Program APM.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APM-10",FILES$[2]="SYS-01",FILES$[3]="GLM-01"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET APM10_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 APM10B: IOLIST A0$(1),A1$(1)
0260 APS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0290 GLM01A: IOLIST B0$(1),B1$(1)
0400 REM " --- Parameters"
0405 DIM P[1],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 LET G[3]=NUM(G2$(7,2)),G[4]=NUM(G2$(9,2))
0465 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2))
0470 CALL "SYC.VA","PO",INFO$[ALL]
0475 LET PO$=INFO$[20]
0480 CALL "SYC.VA",N2$,INFO$[ALL]
0485 LET GL$=INFO$[9]
0500 REM " --- Initializations"
0510 DIM A0$(30),A1$(5*G[4]),B0$(12),B1$(35)
0550 LET K0=1,K9=5,A0$(1,3)=N0$+"B",ROW=8
0560 LET P$="Enter A Valid GL Account Number (<F3>=Lookup)"
0570 IF P3$(3,1)="Y" THEN LET ROW=9
0600 REM " --- Additional File Opens"
0610 IF GL$<>"Y" THEN GOTO 0700
0620 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS THEN GOTO 9900
0640 LET GLM01_DEV=CHANNELS[3]
0700 REM " --- Background"
0710 PRINT 'SB',@(0,3),"Distribution Code:",@(6,4),"Description:",
0720 IF GL$<>"Y" THEN GOTO 0790
0730 PRINT @(7,5),"AP Account:",@(5,6),"Cash Account:",@(1,7),"Discount Account:",
0740 IF P3$(3,1)="Y" THEN PRINT @(0,8),"Retention Account:",
0790 PRINT 'SF',
0900 REM " --- Position file"
0990 READ (APM10_DEV,KEY=A0$(1,3),DOM=1000)
1000 REM " --- Distribution Code"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(APM10_DEV,END=1050)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4)
1050 LET V0$="S",V1$="KE",V3$="",V4$="Enter A 2 Character Distribution Code (<F3>=Lookup)",V0=2,V1=19,V2=3
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN GOSUB 6000
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(4)=V$
1200 REM "FIND KEY"
1210 LET O9=0
1220 FIND (APM10_DEV,KEY=A0$(1,K9),DOM=1400)IOL=APM10B
1300 REM "DISPLAY"
1320 GOSUB 5000
1340 GOTO 4000
1400 REM "NEW RECORD"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM "SET-UP"
1510 LET A0$(6)="",A1$(1)=""
2000 REM " --- Description"
2010 LET V0$="S",V1$="",V2$=A0$(6,20),V3$="",V0=20,V1=19,V2=4,I0=0
2020 LET V4$="Enter The Description For This Distribution Code",X0=0
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,1000,2000,3900
2050 LET A0$(6,20)=V$
2090 GOTO 3900
2200 REM " --- AP Account"
2210 LET V2$=A1$(1,G[3]),V2=5
2220 GOSUB 6200
2230 IF V3=2 OR V3=4 THEN GOTO 3900
2240 LET A1$(1,10)=V$
2290 GOTO 3900
2400 REM " --- Cash Account"
2410 LET V2$=A1$(11,G[3]),V2=6
2420 GOSUB 6200
2430 IF V3=2 OR V3=4 THEN GOTO 3900
2440 LET A1$(11,10)=V$
2490 GOTO 3900
2600 REM " --- Discount Account"
2610 LET V2$=A1$(21,G[3]),V2=7
2620 GOSUB 6200
2630 IF V3=2 OR V3=4 THEN GOTO 3900
2640 LET A1$(21,10)=V$
2690 GOTO 3900
2800 REM " --- Retention Account"
2805 IF P3$(3,1)<>"Y" THEN GOTO 3900
2810 LET V2$=A1$(31,G[3]),V2=8
2820 GOSUB 6200
2830 IF V3=2 OR V3=4 THEN GOTO 3900
2840 LET A1$(31,10)=V$
2890 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,2400,2600,2800,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM "WRITE"
4110 WRITE (APM10_DEV,KEY=A0$(1,K9))IOL=APM10B
4190 GOTO 1000
4200 REM "DELETE"
4210 REMOVE (APM10_DEV,KEY=A0$(1,K9),DOM=4220)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4600,9900
4600 REM " --- Report Initializations"
4610 LET PROGRAM$="",TOTAL$="AP Distribution Codes",HEADINGS=3,WIDTH=80
4620 LET CHANNEL=APM10_DEV,NUMBER=6,RECORD$="B"
4630 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
4640 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER],HEADINGS$[HEADINGS]
4660 LET FIELD$[0]="Code",FIELD[0]=1,POSITION[0]=4
4670 LET LENGTH[0]=2,TYPE$[0]="S",MASK$[0]=""
4680 LET FIELD$[1]="Description",FIELD[1]=1,POSITION[1]=6
4690 LET LENGTH[1]=20,TYPE$[1]="S",MASK$[1]=""
4700 IF GL$<>"Y" THEN GOTO 4900
4720 LET FIELD$[2]="AP Account",FIELD[2]=2,POSITION[2]=1,WIDTH=132
4730 LET LENGTH[2]=G[3],TYPE$[2]="M",MASK$[2]=G5$
4740 LET FIELD$[3]="Cash Account",FIELD[3]=2,POSITION[3]=1+G[4]
4750 LET LENGTH[3]=G[3],TYPE$[3]="M",MASK$[3]=G5$
4760 LET FIELD$[4]="Disc Account",FIELD[4]=2,POSITION[4]=1+G[4]*2
4770 LET LENGTH[4]=G[3],TYPE$[4]="M",MASK$[4]=G5$
4800 REM 
4810 IF P3$(3,1)<>"Y" THEN GOTO 4850
4830 LET FIELD$[5]="Retention",FIELD[5]=2,POSITION[5]=1+G[4]*3
4840 LET LENGTH[5]=G[3],TYPE$[5]="M",MASK$[5]=G5$
4900 REM 
4910 LET NUMBER=1
4920 IF GL$<>"Y" THEN GOTO 4980
4930 LET NUMBER=4
4940 IF P3$(3,1)="Y" THEN LET NUMBER=5
4980 LET HEADINGS$[0]=N1$,HEADINGS$[1]=TOTAL$+" Listing"
4990 RUN "SYR.AA"
5000 REM " --- Display"
5010 PRINT @(19,3),A0$(4,2),@(19,4),A0$(6,20),
5020 IF GL$<>"Y" THEN GOTO 5090
5030 LET V$=A1$(1,10),V1=19,V2=5
5035 GOSUB 5200
5040 LET V$=A1$(11,10),V2=6
5045 GOSUB 5200
5050 LET V$=A1$(21,10),V2=7
5055 GOSUB 5200
5060 LET V$=A1$(31,10),V2=8
5070 IF P3$(3,1)="Y" THEN GOSUB 5200
5090 RETURN 
5200 REM " --- Read/display GL account"
5210 LET B0$(1)=N0$+V$,B1$(1)="",STATUS=11
5220 FIND (GLM01_DEV,KEY=B0$,DOM=5290)IOL=GLM01A
5230 PRINT @(V1,V2),FNF$(B0$(3,G[3]),G5$)," ",B1$,
5240 LET STATUS=0
5290 RETURN 
6000 REM " --- Code Lookup"
6010 LET CHANNEL=APM10_DEV,RECORD$="B",NUMBER=1,TITLE$="AP Distribution Codes"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6050 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6060 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6070 LET V$="SKIP",ROW=V2+2
6080 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
6200 REM " --- GL Account Number Input"
6210 IF GL$<>"Y" THEN GOTO 6290
6220 LET V0$="M",V1$="RC",V3$=G1$,V4$=P$,V0=G[4],V1=19
6230 GOSUB 7000
6235 ON V3 GOTO 6250,6200,6290,6240,6290
6240 CALL "SYC.LG",V$
6250 IF V$="" THEN GOTO 6200
6260 GOSUB 5200
6270 IF STATUS THEN GOTO 6200
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

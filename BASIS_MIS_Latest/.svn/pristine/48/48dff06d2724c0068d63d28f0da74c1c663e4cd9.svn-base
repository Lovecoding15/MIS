0010 REM "APR - Aging Report"
0020 REM "Program APR.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APM-01",FILES$[2]="APM-03",FILES$[3]="APM-10"
0140 LET FILES$[4]="APT-01",FILES$[5]="APT-11",FILES$[6]="APW-02"
0150 LET FILES$[7]="SYS-01"
0155 LET OPTIONS$[6]="C"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET APM01_DEV=CHANNELS[1],APM03_DEV=CHANNELS[2],APM10_DEV=CHANNELS[3]
0185 LET APT01_DEV=CHANNELS[4],APT11_DEV=CHANNELS[5],APW02_DEV=CHANNELS[6]
0190 LET SYS01_DEV=CHANNELS[7]
0200 REM " --- IOLists
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters
0405 DIM P[2],G[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0460 FOR X=0 TO 4
0470 LET G[X]=NUM(G2$(X*2+1,2),ERR=0480)
0480 NEXT X
0485 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=FNYY_YEAR(P4$(3,2))
0500 REM " --- Init Data
0550 LET BEGDATE$=F0$(7,3),DAYS$="P",DAYS=30
0560 LET DETAIL$="D",OPTION$="I",SEQUENCE$="N",INCLUDE$="N",DISCNTS$="N"
0700 REM " --- Background
0710 FOR X=4 TO 18 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(28,3),"Aging Date:",@(24,5),"Period Or Days: ",@(24,7),"Number Of Days:",@(21,9),"Summary Or Detail:",@(26,11),"Aging Option:",@(6,13),"Include Activity Past Aging Date:",@(23,15),"Report Sequence:",@(11,17),"Include Available Discounts:",'SF',
1000 REM " --- Beginning Date
1010 PRINT 'CF',
1020 LET V0$="D",V1$="CR",V2$=BEGDATE$,V0=8,V1=40,V2=3,I0=0
1030 LET V4$="Enter The Beginning Date To Be Used For This Report"
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1060 LET BEGDATE$=V$
1090 GOTO 3900
1100 REM " --- Period
1120 LET V0$="S",V1$="CR",V2$=DAYS$,V3$="PD",V0=1,V1=40,V2=5
1130 LET V4$="Generate Report By Period Or Days (P=Period/D=Days)"
1140 GOSUB 7000
1160 LET DAYS$=V$
1190 GOTO 3900
1200 REM " --- Days
1210 IF DAYS$<>"D" THEN GOTO 3900
1220 LET V0$="N",V1$="CR",V2$=STR(DAYS),V3$="###",V0=3,V1=40,V2=7
1230 LET V4$="Enter The Number Of Days Which Each Period Should Contain"
1240 GOSUB 7000
1250 IF V<1 THEN GOTO 1200
1260 LET DAYS=V
1290 GOTO 3900
1300 REM " --- Detail/Summary
1320 LET V0$="S",V1$="CR",V2$=DETAIL$,V3$="SD",V0=1,V1=40,V2=9
1330 LET V4$="Select A Report Format (S=Summary/D=Detail)"
1340 GOSUB 7000
1360 LET DETAIL$=V$
1390 GOTO 3900
1400 REM " --- Aging Options
1420 LET V0$="S",V1$="CR",V2$=OPTION$,V3$="IAD",V0=1,V1=40,V2=11
1430 LET V4$="Select Date To Be Used For Aging Invoices (A=Accounting/D=Due/I=Invoice)"
1440 GOSUB 7000
1460 LET OPTION$=V$
1490 GOTO 3900
1500 REM " --- Include Future Invoices"
1510 LET V0$="Y",V1$="C",V2$=INCLUDE$,V3$="",V0=1,V1=40,V2=13
1520 LET V4$="Include Activity Which Occurs After "+FNB$(BEGDATE$)+" (Y/N)?"
1530 GOSUB 7000
1540 ON V3 GOTO 1550,1500,3900,1500,3900
1550 LET INCLUDE$=V$
1590 GOTO 3900
1600 REM " --- Report Sequence"
1610 LET V0$="S",V1$="RC",V2$=SEQUENCE$,V3$="AN",V0=1,V1=40,V2=15
1620 LET V4$="Select The Sequence For The Report (A=Alternate Sequence/N=Vendor Number)"
1630 GOSUB 7000
1650 ON V3 GOTO 1660,1600,3900,1600,3900
1660 LET SEQUENCE$=V$
1670 IF POS(V$="AN")=0 THEN GOTO 1600
1690 GOTO 3900
1700 REM " --- Include Available Discounts?
1705 IF OPTION$<>"D" THEN LET DISCNTS$="N"; GOTO 1790
1710 LET V0$="Y",V1$="C",V2$=DISCNTS$,V3$="",V0=1,V1=40,V2=17
1720 LET V4$=""
1730 GOSUB 7000
1740 ON V3 GOTO 1750,1700,3900,1700,3900
1750 LET DISCNTS$=V$
1790 GOTO 3900
3900 REM " --- New Field
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,1200,1300,1400,1500,1600,1700,4000,4000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Periods"
4110 IF DAYS$="D" THEN GOTO 4800
4120 LET P[2]=FNH(BEGDATE$(1,1))
4140 FOR X=1 TO G[0]
4150 IF BEGDATE$(2)>G3$(X*2-1,2) THEN GOTO 4190
4155 IF X=1 THEN IF BEGDATE$(2)>G3$(G[0]*2-1,2) THEN EXITTO 4200
4160 IF X>1 THEN IF BEGDATE$(2)>G3$((X-1)*2-1,2) THEN EXITTO 4200
4165 IF X=1 THEN IF G3$(X*2-1,2)<G3$(G[0]*2-1,2) THEN EXITTO 4200
4170 IF X>1 THEN IF G3$(X*2-1,2)<G3$((X-1)*2-1,2) THEN EXITTO 4200
4190 NEXT X
4200 REM " --- Current Period"
4205 LET V=X,V9=P[2]
4220 IF BEGDATE$(2,1)<G3$(1,1) THEN LET V9=V9-1
4230 CALL "SYC.PA",SYS01_DEV,V,V9,V0$,V9$,STATUS
4240 LET H4$=V0$,H5$=V9$
4300 REM " --- Calculate previous periods"
4310 FOR Y=1 TO 3
4320 LET V=X-Y,V9=P[2]
4330 IF V<1 THEN LET V=G[0]+V,V9=V9-1 ELSE IF BEGDATE$(2,1)<G3$(1,1) THEN LET V9=V9-1
4340 CALL "SYC.PA",SYS01_DEV,V,V9,V0$,V9$,STATUS
4350 IF V0$>H4$(1,3) THEN LET V0$(1,1)=FNI$(FNH(H4$(1,1))-1)
4360 IF V9$>H5$(1,3) THEN LET V9$(1,1)=FNI$(FNH(H5$(1,1))-1)
4370 LET H4$=H4$+V0$,H5$=H5$+V9$
4380 NEXT Y
4390 GOTO 4900
4800 REM " --- Days"
4810 LET H4$="",H5$=""
4820 FOR X=0 TO 3
4830 LET V0$=BEGDATE$,V1$="",V=(-DAYS)*(X+1)+1
4840 CALL "SYC.CA",V0$,V1$,V
4850 LET H4$=H4$+V1$,V0$=BEGDATE$,V1$="",V=(-DAYS)*X
4860 CALL "SYC.CA",V0$,V1$,V
4870 LET H5$=H5$+V1$
4890 NEXT X
4900 REM " --- Run sort overlay"
4910 LET REPORT$="APR.CB"
4990 RUN "APR.BB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNH(Q$)=ASC(Q$)-32
8090 DEF FNI$(Q)=CHR(Q+32)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

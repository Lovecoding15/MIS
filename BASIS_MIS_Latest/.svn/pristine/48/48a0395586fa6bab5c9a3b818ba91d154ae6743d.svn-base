0010 REM "APR - Vendor Cross Reference Listing"
0020 REM "Program APR.IA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM "OPEN/LOCK FILES"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APM-01",FILES$[2]="APM-04",FILES$[3]="APM-01"
0140 LET FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET APM011_DEV=CHANNELS[1],APM04_DEV=CHANNELS[2],APM012_DEV=CHANNELS[3]
0190 LET SYS01_DEV=CHANNELS[4]
0200 REM "IOLISTS"
0210 APM01A1: IOLIST A0$,A1$
0230 APM01A2: IOLIST C0$,C1$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM "PARAMETERS"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0450 DIM P[2]
0490 LET P[0]=NUM(P2$(1,2))
0500 REM "INIT DATA"
0510 DIM J$(80,"-"),W0$(2),Z0$(80),HEADINGS$[2]
0520 LET H0=80,H=0,HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,M0=LEN(M0$)
0530 LET L9=59,L=L9+1,WHEN$=F0$(7,3),CLOCK$="",HEADINGS=2,P=(H0-(31+M0))+1
0800 REM " --- Run Report?
0810 LET V4$="Do You Want To Print The "+N3$
0820 CALL "SYC.YN",1,V4$,2,V$,V3
0830 IF V3=4 THEN GOTO 9900
0840 IF V$<>"Y" THEN GOTO 9900
0900 REM 
0910 CALL "SYC.GA",7,1,"","",STATUS
0920 IF STATUS THEN GOTO 9900
0930 CALL "SYC.NB","Printing",M0,COLUMN
1000 REM "INITIAL READ"
1020 READ (APM011_DEV,KEY=N0$,DOM=1030)
1030 READ (APM04_DEV,KEY=N0$+"A",DOM=1040)
1100 REM "VENDOR READ"
1110 IF W0$(1,1)="E" THEN GOTO 2000
1120 READ (APM011_DEV,END=1900)IOL=APM01A1
1200 REM "BREAKS"
1220 IF POS(N0$=A0$)<>1 THEN GOTO 1900
1500 REM "PRINT"
1510 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$),
1520 LET Z0$(1)=FNF$(A0$(3,P[0]),M0$)+" "+A1$(1,30)
1530 LET T0=T0+1
1590 GOTO 2000
1900 REM "No more vendors"
1920 LET W0$(1,1)="E"
2000 REM "VENDOR ALPHA READ"
2010 IF W0$(2,1)="E" THEN GOTO 3000
2020 LET K$=KEY(APM04_DEV,END=2900)
2030 IF LEN(K$)<2 THEN GOTO 2900
2040 READ (APM04_DEV)
2060 IF POS(N0$+"A"=K$)<>1 THEN GOTO 2900
2400 REM "FIND VENDOR"
2420 FIND (APM012_DEV,KEY=N0$+K$(14),DOM=2000)IOL=APM01A2
2500 REM "PRINT"
2520 LET Z0$(P)=C1$(1,30)+" "+FNF$(C0$(3,P[0]),M0$)
2690 GOTO 3900
2900 REM "ALPHA END"
2920 LET W0$(2,1)="E"
3900 REM "MAIN PRINT"
3910 IF W0$="EE" THEN GOTO 4000
3940 IF L+1>L9 THEN GOSUB 5000
3950 PRINT (7)Z0$
3960 LET L=L+1,T=T+1
3980 DIM Z0$(100)
3990 GOTO 1100
4000 REM "TOTALS"
4010 PRINT (7)""
4020 PRINT (7)T0," Total Vendors"
4100 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)"Vendor Number Sequence",@(P-1),"Alternate Sequence"
5045 PRINT (7)""
5050 LET L=L+2
5090 RETURN 
8000 REM "FUNCTIONS"
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

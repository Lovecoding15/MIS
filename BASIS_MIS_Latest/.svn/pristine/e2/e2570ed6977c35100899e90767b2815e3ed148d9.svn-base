0010 REM "WOS - Closed Work Order Purge - Purge Data Files             "
0020 REM "Program:  WOU.FB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01May99 WGH: Remove references to obsolete file WOT-02 (1900-1970)"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0095 PRECISION 4
0200 REM " --- IOLISTs"
0210 WOE01A: IOLIST A0$,A1$,A[ALL]
0220 WOREQA: IOLIST W0$,W1$,W[ALL]
0500 REM " --- Init Data"
0520 DIM A[11],W[7]
0700 REM " --- Background"
0720 CALL "SYC.NB","Updating: ",28,COLUMN
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
1000 REM " --- Process Here
1010 READ (WOE01_DEV,KEY=N0$,DOM=1020)
1020 LET K$=KEY(WOE01_DEV,END=4000)
1030 IF POS(N0$=K$)<>1 THEN GOTO 4000
1040 READ (WOE01_DEV,KEY=K$)IOL=WOE01A
1050 IF A0$(15,1)<>"C" THEN GOTO 1020
1060 IF A0$(31,3)>P9$ THEN GOTO 1020
1070 PRINT @(COLUMN,11),A0$(5,7),"  ",A0$(56),
1500 REM " --- Sort Files Here
1510 LET K9$=N0$+"A"+A0$(12,2)+A0$(3,9)
1520 REMOVE (WOE03_DEV,KEY=K9$,DOM=1530)
1530 IF A0$(14,1)<>"I" THEN GOTO 1560
1540 LET K9$=N0$+"C"+A0$(54,22)+A0$(3,9)
1550 REMOVE (WOE03_DEV,KEY=K9$,DOM=1560)
1560 IF NUM(A0$(34,6))=0 THEN GOTO 1700
1570 LET K9$=N0$+"B"+A0$(34,16)+A0$(3,9)
1580 REMOVE (WOE03_DEV,KEY=K9$,DOM=1700)
1700 REM " --- Comments Here WOE-07
1710 READ (WOE07_DEV,KEY=A0$(1,11),DOM=1720)
1720 LET K7$=KEY(WOE07_DEV,END=2000)
1730 IF K7$(1,11)<>A0$(1,11) THEN GOTO 2000
1740 REMOVE (WOE07_DEV,KEY=K7$,DOM=1750)
1750 GOTO 1720
2000 REM " --- WOE-02/22/32 And WOE-03 Here
2010 LET WOREQ_DEV=WOE02_DEV
2020 READ (WOREQ_DEV,KEY=A0$(1,11),DOM=2030)
2030 LET K$=KEY(WOREQ_DEV,END=2430)
2040 IF K$(1,11)<>A0$(1,11) THEN GOTO 2430
2050 READ (WOREQ_DEV,KEY=K$)IOL=WOREQA
2060 ON POS(W0$(12,1)="ABC")-1 GOTO 2100,2200,2300
2100 REM " --- Operation Here
2110 LET K9$=N0$+"D"+W0$(3,2)+W1$(1,6)+W0$(5,7)+W0$(13,3)
2120 REMOVE (WOE03_DEV,KEY=K9$,DOM=2130)
2130 GOTO 2400
2200 REM " --- Materials Here
2210 LET K9$=N0$+"E"+W0$(3,2)+A0$(54,2)+W1$(27,20)+W0$(5,7)+W0$(13,3)
2220 REMOVE (WOE03_DEV,KEY=K9$,DOM=2230)
2230 GOTO 2400
2300 REM " --- Subcontracts Here
2310 LET K9$=N0$+"F"+W0$(3,2)+W1$(7,6)+W0$(5,7)+W0$(13,3)
2320 REMOVE (WOE03_DEV,KEY=K9$,DOM=2400)
2400 REM " --- WOE-02/22/32 Here
2410 REMOVE (WOREQ_DEV,KEY=K$,DOM=2420)
2420 GOTO 2030
2430 IF WOREQ_DEV=WOE02_DEV THEN LET WOREQ_DEV=WOE22_DEV; GOTO 2020
2440 IF WOREQ_DEV=WOE22_DEV THEN LET WOREQ_DEV=WOE32_DEV; GOTO 2020
2500 REM " --- WOT-03/23/33 And WOT-12 Here
2510 LET WOHIST_DEV=WOT03_DEV
2520 IF POS(A0$(12,2)=T0$,2)>0 THEN LET D9$=A0$(31,3) ELSE LET D9$=P9$
2530 READ (WOHIST_DEV,KEY=A0$(1,11),DOM=2540)
2540 LET K4$=KEY(WOHIST_DEV,END=2650)
2550 IF K4$(1,11)<>A0$(1,11) THEN GOTO 2650
2560 IF K4$(12,3)>D9$ THEN GOTO 2650
2570 IF POS(I3$(17,1)="LS")=0 THEN GOTO 2630
2580 READ (WOT12_DEV,KEY=K4$,DOM=2590)
2590 LET K8$=KEY(WOT12_DEV,END=2630)
2600 IF POS(K4$=K8$)<>1 THEN GOTO 2630
2610 REMOVE (WOT12_DEV,KEY=K8$,DOM=2620)
2620 GOTO 2590
2630 REMOVE (WOHIST_DEV,KEY=K4$,DOM=2540)
2640 GOTO 2540
2650 IF WOHIST_DEV=WOT03_DEV THEN LET WOHIST_DEV=WOT23_DEV; GOTO 2530
2660 IF WOHIST_DEV=WOT23_DEV THEN LET WOHIST_DEV=WOT33_DEV; GOTO 2530
2670 REMOVE (WOE01_DEV,KEY=A0$(1,11),DOM=2680)
2680 GOTO 1020
4000 REM " --- End
4090 GOTO 9900
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

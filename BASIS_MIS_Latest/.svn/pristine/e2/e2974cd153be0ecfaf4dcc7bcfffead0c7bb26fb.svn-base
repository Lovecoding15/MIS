0010 REM "SYC - Standard Input Routine (Public/Called)"
0020 REM "Program SYC.IA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0042 REM "TYPE$   : Input field type (input)"
0043 REM "          S=String  H=Hyphenated   D=Date  #=Calculator  Y=Yes/No"
0044 REM "          M=Masked  Z=Zero-Filled  T=Time  N=Numeric"
0046 REM "FLAGS$  : Input field modifier (input)"
0047 REM "          6=6 character date     C=Clear line 22 after entry"
0048 REM "          Y=2 character year     ^=Convert input to upper case"
0049 REM "          a=No cursor/No echo    E=Allow <F4> regardless of data"
0050 REM "          K=Key field            l=Lightbar input, no default/echo"
0051 REM "          R=Required field       S=Allow SPECIAL and HISTORY"
0052 REM "          M=Menu program input   I=Ignore <F5>, <F6> and <F7>"
0053 REM "          t=Use TAOS input pgm   F=Ignore all fkeys above <F4>"
0056 REM "DEFAULT$: Default value for input field (input)"
0058 REM "MASK$   : Numeric field mask or valid characters (input)"
0060 REM "MESSAGE$: Line 22 field prompt (input)"
0062 REM "PGM$    : Calling program name (input)"
0064 REM "LENGTH  : Input field length (input)"
0066 REM "COLUMN  : Column position of input field (input)"
0068 REM "ROW     : Row position of input field (input)"
0070 REM "FIELD$  : User input (returned)"
0072 REM "V       : Numeric value of input (returned)"
0074 REM "FKEY    : Function key value (returned)"
0076 REM "RESERVED: Reserved for future use"
0078 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER TYPE$,FLAGS$,DEFAULT$,MASK$,MESSAGE$,PGM$,LENGTH,COLUMN,ROW,FIELD$,V,FKEY,RESERVED
0400 REM " --- Parameters"
0410 DIM PREF$(32)
0420 LET X$=FIN(0),MAXCOL=MIN(DEC(X$(7,1)),80)-1,MAXROW=MIN(DEC(X$(8,1)),23)-1
0430 LET CASEKEY=0,CAPSKEY=0,PREF$=STBL("!SCREEN",ERR=0500)
0440 LET CASEKEY=NUM(PREF$(21,2),ERR=0450)
0450 LET CAPSKEY=NUM(PREF$(23,2),ERR=0500)
0500 REM " --- Initializations"
0510 DIM X1$(80),X3$(80,"0"),ZMASK$(LENGTH,"0")
0530 LET PAD$="_",FIELD$="",V5$="",V6$="",V8$="",V9$="",X$=""
0535 LET V=0,FKEY=0,V4=1,V5=1,D0=3,QUIT=POS("E"=FLAGS$)
0550 IF POS("Y"=TYPE$) THEN GOTO 0590
0560 IF COLUMN>77 THEN GOTO 0590
0565 IF ROW>21 THEN GOTO 0590
0570 IF MASK$="" THEN GOTO 0600
0575 IF POS(MASK$="#0.")=0 THEN GOTO 0590
0580 GOTO 0600
0590 REM LET FLAGS$=FLAGS$+"^"
0600 IF TYPE$="N" THEN IF LEN(MASK$)>0 THEN LET LENGTH=LEN(MASK$)
0605 IF TYPE$="N" THEN IF LEN(MASK$)=0 THEN IF LENGTH=0 THEN LET LENGTH=1
0610 LET FIELD=LENGTH
0615 IF QUIT AND POS("K"=FLAGS$)=0 AND FIELD<3 THEN LET FIELD=3
0620 IF COLUMN+FIELD>MAXCOL THEN LET COLUMN=MAXCOL-FIELD
0625 IF COLUMN+LENGTH>MAXCOL THEN LET LENGTH=MAXCOL-COLUMN+1
0630 IF ROW>MAXROW THEN LET ROW=MAXROW
0640 IF TYPE$<>"D" THEN GOTO 0680
0645 LET D0=3
0650 IF POS("6"=FLAGS$) THEN LET D0=6
0655 IF POS("Y"=FLAGS$) THEN LET D0=2
0660 IF POS(" "<>DEFAULT$)=0 THEN GOTO 0680
0665 IF LEN(DEFAULT$)=6 THEN IF POS("N"=FLAGS$) THEN LET DEFAULT$=FNN$(FNYY21_YY$(DEFAULT$)) ELSE LET DEFAULT$=FNN$(FNYY21_YY$(FNM$(DEFAULT$)))
0670 IF LEN(DEFAULT$)=3 THEN LET DEFAULT$=FNC$(DEFAULT$)
0675 IF POS("Y"=FLAGS$) THEN LET DEFAULT$=FNYY21_YY$(DEFAULT$)
0680 IF TYPE$="N" AND NUM(DEFAULT$,ERR=0690)=0 THEN LET DEFAULT$="0"
0690 IF LENGTH<4 OR POS(TYPE$="NYZHDT") THEN LET FLAGS$=FLAGS$+"X"
0700 REM " --- Field prompt"
0710 LET MESSAGE$=FNP$(MESSAGE$),L=LEN(MESSAGE$)
0720 IF L=0 THEN GOTO 0750
0730 IF L>MAXCOL+1 THEN LET MESSAGE$=MESSAGE$(1,MAXCOL+1),L=MAXCOL+1
0740 PRINT @(0,MAXROW),'ER','CL',@(INT((MAXCOL-L)/2)),MESSAGE$,
0750 GOSUB 6000
0800 REM " --- Display mask/default value"
0805 IF POS(TYPE$="NZ") AND MASK$="" THEN DIM MASK$(LENGTH,"#")
0810 LET FIELD=LENGTH,V2$=FNP$(DEFAULT$),X1=LEN(MASK$)
0815 IF POS(TYPE$="Y") THEN LET LENGTH=1,FIELD=LENGTH
0820 IF POS(TYPE$="SYZH") THEN LET X$=FILL(LENGTH,"_"),FIELD=LENGTH
0825 IF POS(TYPE$="MNW") THEN LET X$=MASK$,FIELD=LEN(MASK$)
0830 IF POS(TYPE$="D") THEN IF POS("Y"=FLAGS$) THEN LET X$="YY",FIELD=D0,X1=D0 ELSE LET X$="MMDDYY  ",FIELD=8,X1=8
0835 IF POS(TYPE$="T") THEN LET X$="HHMM ",FIELD=4,X1=5
0840 IF PREF$(10,1)="R" THEN LET X$='BR'+X$+'ER'
0843 IF POS("l"=FLAGS$) THEN LET X$=""
0845 IF V2$="" AND X$<>"" THEN PRINT 'SF',@(COLUMN,ROW),X$,
0850 IF POS("K"=FLAGS$) THEN GOTO 0870
0855 IF POS(TYPE$="DHNT") AND V2$<>"" THEN PRINT @(COLUMN,ROW),X1$(1,X1),@(COLUMN,ROW),DEFAULT$,
0880 IF POS(TYPE$="ZM") THEN PRINT 'SB',@(COLUMN,ROW),MASK$,
0900 REM 
0910 PRINT 'SB',
0920 LET DISPLAY=FIELD
0930 IF POS("S"=FLAGS$) AND FIELD<7 THEN LET FIELD=7,DISPLAY=7
0940 IF POS(TYPE$="N") THEN GOTO 0960
0945 IF LENGTH=0 THEN GOTO 0990
0950 IF LENGTH<2 AND POS("a"=FLAGS$) THEN GOTO 0990
0960 IF PREF$(10,1)="R" THEN GOTO 0990
0970 IF COLUMN>0 THEN PRINT @(COLUMN-1,ROW),"[",
0980 IF COLUMN+DISPLAY<80 THEN PRINT @(COLUMN+DISPLAY,ROW),"]",
0990 PRINT 'SF',
1000 REM " --- Set cursor and defaults for input"
1010 IF PREF$(10,1)="R" THEN PRINT 'BR',
1030 IF POS("a"=FLAGS$)=0 AND POS("l"=FLAGS$)=0 THEN PRINT @(COLUMN,ROW),'CURSOR'("ON"), ELSE PRINT @(COLUMN,ROW),'CURSOR'("OFF"),
1040 LET FIELD$=FNP$(DEFAULT$)
1100 REM " --- Input"
1110 LET STATUS=0,FKEY=0
1120 GOSUB 7000
1130 IF FKEY=-99 THEN GOTO 1150
1140 IF STATUS>0 THEN GOTO 1150
1145 GOTO 1200
1150 GOSUB 6000
1190 GOTO 1100
1200 REM " --- Check results of input"
1210 IF FIELD$="" THEN LET FIELD$=DEFAULT$
1220 PRINT 'CURSOR'("OFF"),'ER',
1225 IF POS("#"=TYPE$) THEN GOTO 4000
1230 IF QUIT THEN IF LEN(FIELD$)>DISPLAY AND FIELD$<>"END" THEN GOTO 1000
1240 IF FNP$(FIELD$)<>"" THEN LET FIELD$=FNP$(FIELD$)
1250 IF POS("^"=FLAGS$) THEN LET FIELD$=FNU$(FIELD$)
1260 IF POS("F"=FLAGS$) THEN GOTO 1800
1270 IF POS("I"=FLAGS$) THEN GOTO 1600
1300 REM " --- <F5> SpeedSearch"
1310 IF FKEY<>5 THEN GOTO 1400
1320 LET DEFAULT$=FIELD$
1330 CALL "SYC.QA",ERR=1400,STATUS
1350 IF STATUS>0 THEN GOTO 1380
1360 GOSUB 6000
1370 GOTO 1000
1380 LET FKEY=5
1390 GOTO 4000
1400 REM " --- <F6> Help"
1410 IF FKEY<>6 THEN GOTO 1500
1420 IF FNP$(PGM$)="" THEN GOTO 1500
1430 LET DEFAULT$=FIELD$
1450 CALL "SYC.HP",ERR=1000,PGM$,COLUMN,ROW
1490 GOTO 1000
1500 REM " --- <F7> Calculator"
1510 IF FKEY<>7 THEN GOTO 1600
1515 IF POS("N"=TYPE$) THEN LET ANSWER$=FIELD$
1520 CALL "SYC.ZC",ERR=1600,COLUMN,ANSWER$
1530 IF POS("N"=TYPE$) THEN LET FIELD$=STR(NUM(ANSWER$,ERR=1540),ERR=1540)
1540 LET DEFAULT$=FIELD$
1550 GOSUB 6000
1590 GOTO 1000
1600 REM " --- <F8> Print Screen"
1610 IF FKEY<>8 THEN GOTO 1650
1620 LET DEFAULT$=FIELD$
1630 CALL "SYC.SP",ERR=1000
1640 GOTO 1000
1650 REM " --- <F12> Move Window"
1660 IF FKEY<>12 THEN GOTO 1700
1665 LET FIN$=FIN(0)
1670 IF ASC(FIN$(10,1))=0 THEN GOTO 1700
1680 CALL "SYC.MW",ERR=1000,STATUS
1690 GOTO 1000
1700 REM " --- Case Key"
1710 IF CASEKEY=0 THEN GOTO 1750
1715 IF FKEY<>CASEKEY THEN GOTO 1750
1720 LET FIELD$=FNU$(FIELD$),FKEY=0
1725 IF LEN(FIELD$)<=1 THEN GOTO 1750
1730 FOR X=2 TO LEN(FIELD$)
1735 IF FIELD$(X-1,1)<>" " THEN LET FIELD$(X,1)=CVS(FIELD$(X,1),8)
1740 NEXT X
1750 REM " --- Caps Key"
1755 IF CAPSKEY=0 THEN GOTO 1800
1760 IF FKEY<>CAPSKEY THEN GOTO 1800
1770 LET FIELD$=FNU$(FIELD$),FKEY=0
1800 REM " --- Remaining tests"
1810 IF FKEY=-4 THEN LET FKEY=2
1820 IF QUIT AND FNU$(FIELD$)="END" THEN LET FIELD$=DEFAULT$,FKEY=4
1900 REM " --- Process input based on TYPE$"
1910 IF POS("K"=FLAGS$) AND FIELD$="" THEN LET FIELD$=DEFAULT$
1920 LET V8$=FIELD$,V4=1,V5=1,Z4$="",V5$="",V9$=""
1925 IF QUIT=0 THEN GOTO 1980
1927 IF FKEY<>4 THEN GOTO 1980
1930 IF TYPE$="N" THEN LET V8$=STR(V:MASK$)
1950 GOTO 4000
1980 IF POS("R"=FLAGS$) AND FIELD$="" AND FKEY=0 THEN GOTO 1000
1990 ON POS(TYPE$="SYZHNMDTW") GOTO 4000,2000,2100,2200,2300,2400,2500,2800,2900,3000
2000 REM " --- Type S"
2010 IF FKEY=0 AND MASK$<>"" AND FPT(LEN(MASK$)/LENGTH)=0 AND POS(FIELD$=MASK$,LENGTH)=0 THEN GOTO 1000
2020 IF FNU$(FIELD$)="YES" THEN LET FIELD$="YES"
2030 IF FNU$(FIELD$)="NO" THEN LET FIELD$="NO"
2040 LET V8$=FIELD$
2090 GOTO 4000
2100 REM " --- Type Y"
2120 IF LENGTH=1 THEN IF POS(FIELD$="YN")=0 THEN GOTO 1000
2140 IF LENGTH=3 AND (FIELD$<>"YES" AND FIELD$<>"NO") THEN GOTO 1000
2160 LET V8$=FIELD$
2190 GOTO 4000
2200 REM " --- Type Z"
2210 LET V=ABS(NUM(FIELD$,ERR=1000))
2220 IF V=0 THEN LET FIELD$=""; GOTO 2260
2230 LET FIELD$=STR(INT(V))
2240 IF LEN(FIELD$)>LENGTH THEN GOTO 1000
2250 LET FIELD$=STR(V:ZMASK$)
2260 LET V8$=FIELD$
2290 GOTO 4000
2300 REM " --- Type H"
2320 LET V=NUM(FIELD$,ERR=1000)
2340 LET FIELD$=STR(INT(V)),FIELD$=X3$(1,LENGTH-LEN(FIELD$))+FIELD$
2360 LET V8$=STR(-V:MASK$)
2390 GOTO 4000
2400 REM " --- Type N"
2410 LET X=POS(","=FIELD$)
2420 WHILE X>0
2430 LET FIELD$=FIELD$(1,X-1)+FIELD$(X+1),X=POS(","=FIELD$)
2440 WEND
2460 LET V=NUM(FIELD$,ERR=1000),V8$=STR(V:MASK$)
2490 GOTO 4000
2500 REM " --- Type M"
2505 IF FIELD$="" THEN LET V=0; GOTO 4000
2510 IF POS("("=FIELD$) OR POS(")"=FIELD$) THEN GOTO 1000
2515 LET X$=FIELD$
2520 WHILE POS("-"=X$); LET X$(POS("-"=X$),1)="0"; WEND
2525 LET X=NUM(X$,ERR=3200); REM "Masked alpha?
2530 IF V4>LEN(MASK$) THEN GOTO 2630
2535 IF MASK$(V4,1)="-" THEN GOTO 2600
2540 IF V5<=LEN(FIELD$) THEN LET V6$=FIELD$(V5,1)
2550 IF V6$=" " THEN LET V6$="0"
2560 IF V5>LEN(FIELD$) OR V6$="-" THEN LET V5$="0"+V5$
2570 IF V6$<>"-" THEN LET V5$=V5$+V6$,V5=V5+1,V6$=""
2580 LET V4=V4+1,V9$=V9$+"0"
2590 GOTO 2530
2600 LET Z4$=Z4$+V5$,V4=V4+1,V5$="",V6$="",V9$=V9$+"-"
2610 IF V5<LEN(FIELD$) THEN IF FIELD$(V5,1)="-" THEN LET V5=V5+1
2620 GOTO 2530
2630 LET Z4$=Z4$+V5$
2640 IF V5<LEN(FIELD$) THEN GOTO 1000
2650 LET V8$=STR(-NUM(Z4$,ERR=1000):V9$),FIELD$=Z4$
2660 IF LEN(FIELD$)<LENGTH THEN LET FIELD$=FIELD$+X3$(1,LENGTH-LEN(FIELD$))
2670 LET V=NUM(FIELD$,ERR=1000)
2690 GOTO 4000
2800 REM " --- Type D"
2805 IF POS("Y"=FLAGS$) THEN GOTO 3400
2810 LET FIELD$=FNP$(FIELD$)
2815 IF POS("0"<>FIELD$)=0 THEN LET FIELD$="",V8$=FILL(6)
2820 IF FIELD$="" AND POS("R"=FLAGS$)=0 THEN GOTO 4000
2825 IF LEN(FIELD$)=8 THEN IF POS("/"=FIELD$)=3 THEN LET FIELD$=FIELD$(1,2)+FIELD$(4,2)+FIELD$(7,2)
2830 IF LEN(FIELD$)<>6 THEN GOTO 1000
2835 LET FIELD$=STR(NUM(FIELD$,ERR=1000):"000000")
2840 LET MM=NUM(FIELD$(1,2)),DD=NUM(FIELD$(3,2))
2850 IF MM<1 OR DD<1 THEN GOTO 1000
2860 LET V4=JUL(1900+FNYY_YEAR(FIELD$(5,2)),MM,DD,ERR=1000)
2870 LET V8$=FIELD$(1,2)+"/"+FIELD$(3,2)+"/"+FIELD$(5,2)
2880 IF D0=3 THEN LET FIELD$=FND$(FIELD$) ELSE LET FIELD$=FNYY_YY21$(FNM$(FIELD$))
2890 GOTO 4000
2900 REM " --- Type T"
2910 LET V4=LEN(FIELD$)
2920 IF V4<4 OR V4>5 THEN GOTO 1000
2930 IF V4=5 THEN LET FIELD$=FIELD$(1,2)+FIELD$(4,2)
2940 LET V4=NUM(FIELD$(1,2),ERR=1000),V5=NUM(FIELD$(3,2),ERR=1000)
2950 IF V4<0 OR V4>24 THEN GOTO 1000
2960 IF V5<0 OR V5>59 THEN GOTO 1000
2970 IF V4=24 AND V5>0 THEN GOTO 1000
2980 LET V8$=FIELD$(1,2)+":"+FIELD$(3,2)
2990 GOTO 4000
3000 REM " --- Type W / Wildcarding"
3005 LET FIELD$=FNP$(FIELD$)
3010 IF FIELD$="" THEN GOTO 4000
3020 LET P=POS("-"=FIELD$)
3030 IF P=0 THEN GOTO 3100
3040 LET FIELD$=FIELD$(1,P-1)+FIELD$(P+1)
3050 GOTO 3020
3100 REM 
3110 LET L=LEN(MASK$),P=0,X$=""
3120 FOR X=1 TO LEN(MASK$)
3125 LET Y$="-"
3130 IF MASK$(X,1)=Y$ THEN GOTO 3170
3135 LET Y$="?",P=P+1
3140 IF P>LEN(FIELD$) THEN GOTO 3170
3145 LET Y$=FIELD$(P,1)
3150 IF Y$="-" THEN IF MASK$(X,1)=Y$ THEN GOTO 3170
3160 IF POS(Y$="?0123456789")=0 THEN EXITTO 1000
3170 LET X$=X$+Y$
3180 NEXT X
3190 LET FIELD$=X$
3195 GOTO 4000
3200 REM " --- Type M (Alpha)"
3210 LET FIELD$=CVS(FIELD$,4)
3230 IF V4>LEN(MASK$) THEN GOTO 3330
3235 IF MASK$(V4,1)="-" THEN GOTO 3300
3240 IF V5<=LEN(FIELD$) THEN LET V6$=FIELD$(V5,1)
3260 IF V5>LEN(FIELD$) OR V6$="-" THEN LET V5$=V5$+" "
3270 IF V6$<>"-" THEN LET V5$=V5$+V6$,V5=V5+1,V6$=""
3280 LET V4=V4+1,V9$=V9$+"X"
3290 GOTO 3230
3300 LET Z4$=Z4$+V5$,V4=V4+1,V5$="",V6$="",V9$=V9$+"-"
3310 IF V5<LEN(FIELD$) THEN IF FIELD$(V5,1)="-" THEN LET V5=V5+1
3320 GOTO 3230
3330 LET Z4$=Z4$+V5$
3340 IF V5<LEN(FIELD$) THEN GOTO 1000
3350 LET V8$=FNF$(Z4$,V9$),FIELD$=Z4$
3360 IF LEN(FIELD$)<LENGTH THEN LET FIELD$=FIELD$+FILL(LENGTH-LEN(FIELD$))
3390 GOTO 4000
3400 REM " --- Type D (Year)"
3410 IF POS(" "<>FIELD$)=0 THEN LET V8$="  " ELSE LET V8$=STR(ABS(NUM(FIELD$,ERR=1000)):"00",ERR=1000)
3420 LET FIELD$=FNYY_YY21$(V8$)
3490 GOTO 4000
4000 REM " --- All done"
4010 IF LENGTH<LEN(V8$) THEN LET LENGTH=LEN(V8$)
4015 IF LENGTH<FIELD THEN LET LENGTH=FIELD
4020 IF COLUMN>0 THEN PRINT @(COLUMN-1,ROW)," ",
4025 IF FIELD<LEN(V8$) THEN LET FIELD=LEN(V8$)
4030 IF POS("a"=FLAGS$)=0 THEN PRINT @(COLUMN,ROW),V8$,X1$(1,FIELD-LEN(V8$)+1),
4040 IF POS("C"=FLAGS$) THEN IF POS("a"=FLAGS$)=0 THEN PRINT @(0,MAXROW),'CL',
4050 IF POS("N"=TYPE$) THEN PRINT @(0,MAXROW),'CL',
4060 PRINT @(COLUMN,ROW),
4070 IF TYPE$="D" THEN GOSUB 6100
4080 LET FLAGS$="",MESSAGE$="",MASK$=""
4090 GOTO 9900
6000 REM " --- Update current time"
6010 LET X9$=FIN(0)
6020 IF DEC(X9$(9,2))>0 THEN GOTO 6090
6050 IF PREF$(10,1)="R" THEN PRINT 'ER',
6060 PRINT @(58,1),'SB',FNE$(Q$),'SF',
6070 IF PREF$(10,1)="R" THEN PRINT 'BR',
6090 RETURN 
6100 REM " --- Set date field"
6110 IF FKEY=0 THEN GOTO 6190
6120 IF FNP$(DEFAULT$)="" THEN GOTO 6190
6130 LET FIELD$=DEFAULT$
6140 IF POS("Y"=FLAGS$) THEN LET FIELD$=FNYY_YY21$(FIELD$); GOTO 6190
6150 IF LEN(FIELD$)<>6 THEN GOTO 6190
6160 LET FIELD$=STR(NUM(FIELD$,ERR=6190):"000000")
6170 LET V4=JUL(1900+FNYY_YEAR(FIELD$(5,2)),NUM(FIELD$(1,2)),NUM(FIELD$(3,2)),ERR=6190)
6180 IF D0=3 THEN LET FIELD$=FND$(FIELD$) ELSE LET FIELD$=FNYY_YY21$(FNM$(FIELD$))
6190 RETURN 
7000 REM " --- Input Routine Input Processing"
7005 IF POS("a"=FLAGS$) THEN GOTO 7500
7007 IF LEN(FIELD$)>FIELD THEN LET FIELD$=FIELD$(1,FIELD)
7010 INPUTE (0,TIM=180,ERR=7900)COLUMN,ROW,FIELD,PAD$,FIELD$
7020 LET FKEY=CTL
7025 IF FKEY=33 THEN LET FKEY=2
7030 IF FKEY=34 THEN IF POS("M"=FLAGS$)=0 THEN LET FKEY=0 ELSE LET FKEY=-3
7035 IF FKEY=37 THEN LET FKEY=-17
7040 IF FKEY=38 THEN LET FKEY=-16
7045 IF FKEY=39 THEN LET FKEY=-2
7050 IF FKEY=40 THEN LET FKEY=-1
7090 RETURN 
7500 REM " --- Standard SpeedSearch input"
7510 PRINT (0,ERR=7900)@(COLUMN,ROW),'EE',
7520 READ RECORD(0,TIM=180,ERR=7900,SIZ=1)FIELD$
7530 PRINT (0,ERR=7900)@(COLUMN,ROW)," ",'BE',
7540 IF FIELD$=$81$ THEN LET FKEY=1
7550 IF FIELD$=$82$ THEN LET FKEY=2
7560 IF FIELD$=$83$ THEN LET FKEY=3
7570 IF FIELD$=$84$ THEN LET FKEY=4
7580 IF FIELD$=$85$ THEN LET FKEY=5
7590 IF FIELD$=$86$ THEN LET FKEY=6
7600 IF FIELD$=$87$ THEN LET FKEY=7
7610 IF FIELD$=$88$ THEN LET FKEY=8
7620 IF FIELD$=$89$ THEN LET FKEY=9
7630 IF FIELD$=$8A$ THEN LET FKEY=10
7640 IF FIELD$=$8B$ THEN LET FKEY=11
7650 IF FIELD$=$8C$ THEN LET FKEY=12
7660 IF FIELD$=$8D$ THEN LET FKEY=13
7670 IF FIELD$=$8E$ THEN LET FKEY=14
7680 IF FIELD$=$8F$ THEN LET FKEY=15
7690 IF FIELD$=$90$ THEN LET FKEY=16
7700 IF FIELD$=$1C$ THEN LET FKEY=-1
7710 IF FIELD$=$1D$ THEN LET FKEY=-2
7720 IF FIELD$=$A1$ THEN LET FKEY=2
7730 IF FIELD$=$A2$ THEN LET FKEY=-3
7740 IF FIELD$=$A5$ THEN LET FKEY=-17
7750 IF FIELD$=$A6$ THEN LET FKEY=-16
7760 IF FIELD$=$08$ THEN LET FKEY=-2
7850 IF FKEY<>0 THEN LET DEFAULT$=""; GOTO 7890
7860 IF DEC(FIELD$)<32 OR DEC(FIELD$)>127 THEN LET FIELD$=""
7890 RETURN 
7900 REM " --- Input error"
7910 PRINT 'BE',
7920 LET STATUS=1
7990 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=DATE(0:"%hz:%mz %p")
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8070 DEF FNN$(Q$)=Q$(3,4)+Q$(1,2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNU$(Q$)=CVS(Q$,4)
8100 REM " --- FNYEAR_YY$ Un-Convert 21st Century Numeric Year to 2-Char Year"
8105 DEF FNYEAR_YY$(Q)=STR(MOD(Q,100):"00")
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Input Routine Error Processing"
9010 IF ERR=127 THEN GOTO 9500
9020 CALL "SYC.EA",ERR=9200,E0,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9090 GOTO 9500
9100 REM " --- Retry"
9110 RETRY
9200 REM " --- Exit to host"
9220 IF FIELD$="" THEN LET FIELD$=DEFAULT$,V8$=FIELD$
9230 IF LENGTH<LEN(V8$) THEN LET LENGTH=LEN(V8$)
9240 PRINT @(COLUMN,ROW),V8$,X1$(1,LENGTH+1-LEN(V8$)),
9250 IF MAXROW=22 THEN PRINT @(0,23),'CL',
9255 PRINT @(0,MAXROW),'CL',
9260 LET FKEY=999
9290 GOTO 9900
9500 REM " --- Escape requested"
9510 LET FKEY=127
9520 PRINT 'ER',
9900 REM " --- Return to calling program"
9910 LET X$=STBL("!SCREEN",ERR=9930)
9920 IF X$(25,1)="T" AND POS("SYS.AA"=PGM$)=0 THEN SETTRACE 
9930 IF FKEY<>5 AND FKEY<>6 AND FKEY<>127 AND FKEY<>999 THEN PRINT 'CURSOR'("OFF"),
9935 IF FKEY=0 THEN GOTO 9980
9940 IF TYPE$<>"D" THEN LET FIELD$=DEFAULT$
9955 IF TYPE$="Z" THEN LET FIELD$=STR(NUM(FIELD$,ERR=9956):ZMASK$)
9980 LET DEFAULT$=""
9990 EXIT 
9999 END

0010 REM "IVE - Cost Change Entry"
0020 REM "Program IVE.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=7
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0130 LET FILES$[4]="IVM-03",FILES$[5]="IVM-10",FILES$[6]="IVM-14"
0140 LET FILES$[7]="IVE-06"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0180 LET IVM03_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],IVM14_DEV=CHANNELS[6]
0190 LET IVE06_DEV=CHANNELS[7]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$,A6$,A[ALL]
0220 IVM02A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0230 IVE06A: IOLIST W0$(1),W1$,W2$,W[ALL]
0240 IVM10C: IOLIST X0$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0400 REM " --- Parameters"
0405 DIM P[5]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="IV"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 IF I3$(8,1)<>"S" THEN GOTO 4900
0450 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1)),WHSE$=I3$(6,2)
0460 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM FILES[14],PARAMS[3],O[4],B0$(24),B1$(64),B[20],X0$(32)
0520 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A[12],W0$(27),W[4]
0550 LET FILES[1]=IVM01_DEV,FILES[10]=IVM10_DEV,FILES[14]=IVM14_DEV
0555 LET PARAMS[0]=P[0],PARAMS[1]=P[3],PARAMS[2]=P[4],PARAMS[3]=P[5]
0560 LET WHEN$=F0$(7,3),L0=7,L9=21,L=L0
0600 REM " --- Resize masks and set print positions"
0610 CALL "SYC.RM",M3$,M3,13,15
0620 LET O[0]=1,O[1]=5,O[2]=O[1]+P[0]+1,O[4]=77-M3,O[3]=O[4]-M3
0630 LET DW=O[4]-O[2]-1
0650 REM " --- Set default effective date"
0660 LET K$=KEY(IVE06_DEV,END=0700)
0670 IF POS(N0$=K$)=1 THEN LET WHEN$=K$(3,3)
0700 REM " --- Background"
0710 PRINT 'SB',@(24,3),"Effective Date:",'SF',
0720 CALL "SYC.WC",1,0,80,0,0,L0-3,0
0730 PRINT 'SB',@(O[0]-1,L0-2),"Whse",@(O[1]),"Item",@(O[2]),"Description",@(O[3]+M3-13),"Current Cost",@(O[4]+M3-9),"New Cost",'SF',
0740 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0800 REM " --- Effective Date"
0805 PRINT 'CF',
0810 LET V4$="Enter The Effective Date For This Cost Change"
0820 LET V0$="D",V1$="CE",V2$=WHEN$,V3$="",V0=3,V1=40,V2=3
0830 GOSUB 7000
0840 ON V3 GOTO 0850,0800,0800,0800,9900
0850 LET WHEN$=V$,W0$(1)=N0$+WHEN$
0900 REM " --- Display any existing lines"
0910 DIM DESCRIPTION$(DW)
0920 GOSUB 5100
0930 IF Q1=0 THEN GOTO 1400
0990 GOTO 4000
1000 REM " --- Overflow"
1010 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L0),'LD',
1020 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1110 LET K$=KEY(IVE06_DEV,END=1300)
1120 IF POS(W0$(1,5)=K$)<>1 THEN GOTO 1300
1130 READ (IVE06_DEV)IOL=IVE06A
1140 LET A0$(1)=N0$+W0$(8),B0$(1)=N0$+W0$(6)
1150 FIND (IVM01_DEV,KEY=A0$,DOM=1100)IOL=IVM01A
1160 FIND (IVM02_DEV,KEY=B0$,DOM=1100)IOL=IVM02A
1170 GOSUB 5000
1180 LET I0=2
1190 GOTO 3300
1300 REM " --- EOF"
1310 LET W0$(6)=""
1400 REM " --- Warehouse"
1405 IF I3$(1,1)="Y" THEN GOTO 1420
1410 LET V$=WHSE$
1415 GOTO 1460
1420 LET V4$="Enter A Valid Warehouse ID (<F3>=Lookup)"
1425 LET V0$="S",V1$="KERC",V2$=W0$(6,2),V3$="",V0=2,V1=O[0],V2=L,I0=0
1430 GOSUB 7000
1440 IF V3=4 THEN PRINT @(V1,L),'CL',; GOTO 4000
1445 ON V3 GOTO 1460,1400,1400,1450,4000
1450 CALL "IVC.LK",3,V1,V2,V$
1460 LET X0$(1)=N0$+"C"+V$
1465 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=1400)IOL=IVM10C
1470 LET W0$(6,2)=V$
1480 PRINT @(V1,V2),W0$(6,2)
1490 GOTO 3200
1500 REM " --- Item Number"
1510 DIM ITEM$[6],ITEM[12]
1520 LET V1=O[1],V2=L,V2$=W0$(8),ACTION$="",V4$=""
1530 CALL "IVC.IA",ACTION$,V1,V2,FILES[ALL],N0$,V2$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1540 ON V3 GOTO 1550,1500,3200,1500,1550
1550 LET V$=ITEM$[0],A0$(1)=N0$+V$,B0$=N0$+W0$(6,2)+V$
1560 FIND (IVM01_DEV,KEY=A0$,DOM=1500)IOL=IVM01A
1570 FIND (IVM02_DEV,KEY=B0$,DOM=1500)IOL=IVM02A
1580 LET W0$(8)=V$
1700 REM " --- Existing record?"
1710 FIND (IVE06_DEV,KEY=W0$,DOM=1800)IOL=IVE06A
1720 IF W0$<=K9$ THEN GOSUB 5400
1730 GOSUB 5000
1740 LET I0=1
1790 GOTO 3200
1800 REM " --- New Record"
1810 DIM W[4]
1820 LET W1$="",W2$="",W[0]=B[11]
1890 GOSUB 5000
2000 REM " --- New Cost"
2010 LET V4$="Enter The New Standard Cost For This Item"
2020 LET V0$="N",V1$="C",V2$=STR(W[0]),V3$=M3$,V0=M3,V1=O[4],V2=L,I0=2
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3200,2000,3200
2050 IF V<0 THEN GOTO 2000
2060 LET W[0]=V
2070 PRINT @(V1,V2),W[0]:M3$
2090 GOTO 3200
3200 REM " --- Sequence Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,1500,2000,3300
3300 REM " --- Line Correct?"
3310 LET V4$="<Enter>=Continue     <D>=Delete     <F2>=Change"
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=W0$
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3300,3200,3300,3350
3350 IF V$="" THEN GOTO 3400
3360 IF V$="D" THEN GOTO 3700
3390 GOTO 3300
3400 REM " --- Write"
3410 WRITE (IVE06_DEV,KEY=W0$)IOL=IVE06A
3450 LET L=L+1
3460 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3700 REM " --- Remove"
3710 REMOVE (IVE06_DEV,KEY=W0$,DOM=3720)
3750 PRINT @(0,L),'CL',@(0,L+1),'CL',
3760 IF V3=4 THEN GOTO 4000
3790 GOTO 1000
4000 REM " --- Options"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete/List/Review)?"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4100
4050 IF V$="L" OR V$="LIST" THEN GOTO 4500
4070 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4080 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,4700
4100 REM " --- Return to header"
4110 GOSUB 5400
4190 GOTO 0800
4200 REM " --- Delete"
4210 LET V4$="Delete ALL Cost Change Entries For "+FNB$(WHEN$)
4220 CALL "SYC.YN",0,V4$,0,V$,V3
4230 IF V$<>"YES" THEN GOTO 0800
4240 PRINT @(16,22),"Clearing All Cost Change Entries For ",FNB$(WHEN$)," ...",
4280 LET W0$(1)=N0$+WHEN$
4290 READ (IVE06_DEV,KEY=W0$,DOM=4300)
4300 REM " --- Remove next IVE-06 record"
4310 LET K$=KEY(IVE06_DEV,END=4400)
4320 IF POS(W0$(1,5)=K$)<>1 THEN GOTO 4400
4330 REMOVE (IVE06_DEV,KEY=K$)
4390 GOTO 4300
4400 REM " --- Delete completed"
4410 LET WHEN$=F0$(7,3)
4420 READ (IVE06_DEV,KEY=N0$,DOM=4430)
4430 LET K$=KEY(IVE06_DEV,END=4100)
4440 IF POS(N0$=K$)=1 THEN LET WHEN$=K$(3,3)
4490 GOTO 4100
4500 REM " --- List"
4510 LET W0$(6)=""
4520 GOSUB 5100
4590 GOTO 4000
4600 REM " --- Review"
4610 LET W0$(6)=""
4650 REM " --- Restart"
4660 READ (IVE06_DEV,KEY=W0$(1,5),DOM=4670)
4670 GOSUB 5400
4690 GOTO 1000
4700 REM " --- Not correct"
4710 READ (IVE06_DEV,KEY=W0$(1,5)+$FF$,DOM=4790)
4790 GOTO 1000
4900 REM " --- Not standard costing"
4910 CALL "IVC.SB","S",STATUS
4990 GOTO 9900
5000 REM " --- Display"
5010 LET A0$(1)=N0$+W0$(8),A1$(1)="",B0$(1)=N0$+W0$(6),B[11]=0
5020 FIND (IVM01_DEV,KEY=A0$,DOM=5030)IOL=IVM01A
5030 FIND (IVM02_DEV,KEY=B0$,DOM=5040)IOL=IVM02A
5040 LET Q1=1,DESCRIPTION$(1)=FNITEM$(A1$,P[3],P[4],P[5])
5050 PRINT @(0,L),'CL',@(O[0]),W0$(6,2),@(O[1]),W0$(8,P[0]),@(O[2]),DESCRIPTION$,@(O[3]),B[11]:M3$,@(O[4]),W[0]:M3$,
5090 RETURN 
5100 REM " --- List"
5105 LET Q1=0
5110 GOSUB 5400
5120 READ (IVE06_DEV,KEY=W0$,DOM=5130)
5130 LET K$=KEY(IVE06_DEV,END=5300)
5140 IF POS(W0$(1,5)=K$)<>1 THEN GOTO 5300
5160 READ (IVE06_DEV)IOL=IVE06A
5190 IF L<=L9 THEN GOTO 5250
5200 REM " --- Continue"
5210 LET V4$="Press <Enter> To Continue:"
5215 LET V0$="S",V1$="CE",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5230 IF V3=4 THEN GOTO 5300
5240 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+1,K9$=W0$
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5420 PRINT @(0,L0),'CE',
5440 LET L=L0
5490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "SYC - Customer Ship-To/Vendor Purchase Address Lookup"
0020 REM "Program SYC.LT"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Oct96 WGH: Possible Error 42 Line 2685 (550)"
0032 REM "13Nov96 JWK: Error on Keyword, and none found (1205,1940)
0033 REM "02Jan97 WGH: Ship-To/Purchase Address number entry should be zero filled (1050-1070,1210-1230)"
0034 REM "13Feb97 Caj: Keyword lookup wasn't finding all records (1030)    
0040 REM 
0045 REM "ACTION$   : S=Ship To Addresses/P=Purchase Addresses (input)"
0050 REM "NUMBER$   : Customer/Vendor Number (input)"
0055 REM "SELECTION$: Ship-To Number (returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9900
0090 ENTER ACTION$,NUMBER$,SELECTION$
0100 REM " --- Open Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ARM-03",FILES$[2]="SYS-01",FILES$[3]="SYM-06"
0135 IF ACTION$="P" THEN LET FILES$[1]="APM-05"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET ARM03_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2],SYM06_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 ARM03A: IOLIST A0$(1),A1$(1)
0250 SYM06A: IOLIST *,COMPANY$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM P[2],SPEED$(128)
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0425 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0430 LET N0$=F0$(16,2),SPEEDSEARCH$="N",M7$="(###)-###-####"
0440 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=0460)
0450 LET N0$=SPEED$(1,2),SPEEDSEARCH$="Y"
0460 FIND (SYM06_DEV,KEY=N0$,DOM=9900)IOL=SYM06A
0480 IF ACTION$="S" THEN LET KEYLEN=14,NUMLEN=6,NUMPOS=9,W$="Ship-To"
0490 IF ACTION$="P" THEN LET KEYLEN=10,NUMLEN=2,NUMPOS=9,W$="Purchase"
0500 REM " --- Initializations"
0510 DIM A0$(KEYLEN),A1$(174)
0540 LET WIDTH=48,HEIGHT=12,WIN_X=1,WIN_Y=3,MAX_ROW=HEIGHT-5
0550 LET SWIDTH=30,SHEIGHT=12,SWIN_X=79-SWIDTH,SWIN_Y=WIN_Y+1
0560 LET OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3,PGM$=PGM(-2)
0570 LET TITLE$=W$+" Address Lookup",STITLE$="Detail"
0650 REM " --- Window arrays"
0670 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0680 LET HEADING$(2)="Number",HEADING$(11)="Name"
0690 LET FOOTING$(2)="F4=End  PgUp  PgDn"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Which search option?"
0810 DIM STRINGS$[10],NUMBERS[10]
0820 LET STRINGS$[0]=PGM$,STRINGS$[1]=FILL(NUMLEN,"0"),NUMBERS[0]=MAXROW
0830 LET NUMBERS[1]=OWIN_X,NUMBERS[2]=OWIN_Y,NUMBERS[3]=NUMLEN
0840 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
0850 CALL "SYC.SM",STRINGS$[ALL],NUMBERS[ALL],FKEY
0860 IF FKEY THEN GOTO 9900
0870 LET PREFIX$=STRINGS$[2],START$=STRINGS$[3],METHOD$=STRINGS$[4]
0880 LET V3$=STRINGS$[5],V0=NUMBERS[4]
0900 REM " --- Detail Window"
0910 CALL "SYC.WA",0,SWIDTH,SHEIGHT,SWIN_X,SWIN_Y,STITLE$,SNAME$
0915 CALL "SYC.WB",NAME$,0
0930 IF SPEEDSEARCH$<>"Y" THEN GOTO 1000
0950 REM " --- Display SpeedSearch Company"
0955 DIM X$(WIDTH+SWIDTH)
0960 LET X$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
0965 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
0970 CALL "SYC.WA",0,WIDTH+SWIDTH,3,WIN_X,SWIN_Y+SHEIGHT,"",CNAME$
0975 PRINT @(0,0),'BR',X$,'ER',
0980 CALL "SYC.WB",NAME$,0
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,SHEIGHT-2]
1020 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$=""
1030 IF PREFIX$="*" THEN GOTO 1200
1050 LET X=1,X=NUM(START$,ERR=1060)
1060 IF X<1 THEN LET X=1
1070 LET START$=STR(X:FILL(NUMLEN,"0"))
1200 REM " --- Position File"
1205 IF PREFIX$="*" THEN READ (ARM03_DEV,KEY=N0$+NUMBER$,DOM=1400)
1210 LET K$=N0$+NUMBER$+START$
1220 READ (ARM03_DEV,KEY=K$,DOM=1400)IOL=ARM03A
1230 GOTO 1900
1400 REM " --- Read next record"
1410 LET K$=KEY(ARM03_DEV,END=3000)
1420 IF POS(N0$+NUMBER$=K$)<>1 THEN GOTO 3000
1900 REM " --- Read customer/vendor record"
1905 LET LOCKED=0
1910 READ (ARM03_DEV,KEY=K$,ERR=1950)IOL=ARM03A
1920 IF PREFIX$<>"*" THEN GOTO 2000
1930 IF START$="" THEN GOTO 2000
1940 IF POS(FNU$(START$)=FNU$(A1$))>0 THEN GOTO 2000
1945 GOTO 2900
1950 REM " --- Error reading record"
1970 IF ERR<>0 THEN GOTO 9000
1980 LET LOCKED=1
2000 REM " --- Display page?"
2010 IF L<MAX_ROW THEN GOTO 2400
2020 LET XMODE=5
2030 GOSUB 6200
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2400 REM " --- Lookup window record"
2410 DIM G1$(WIDTH-2)
2415 IF LOCKED THEN GOTO 2450
2430 LET G1$(2)=A0$(NUMPOS,NUMLEN),G1$(11)=A1$(1,30)
2440 GOTO 2490
2450 LET G1$(2)=K$(NUMPOS,NUMLEN),G1$(11)="Record In Use",A0$(1)=K$,A1$(1)=""
2490 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$(NUMPOS,NUMLEN)
2500 REM " --- Format address block"
2510 LET X$=A1$(31,72)+A1$(103,9)
2520 CALL "SYC.AA",X$,24,3,9,30
2600 REM " --- Detail window record"
2610 DIM G2$(SWIDTH-2)
2620 LET G2$(2)=A1$(1,30),MORE$[PAGE,X1,2]=G2$,N=2
2630 FOR X=1 TO 120 STEP 30
2640 IF FNP$(X$(X,30))="" THEN GOTO 2650
2645 LET N=N+1,G2$(2)=X$(X,30),MORE$[PAGE,X1,N]=G2$
2650 NEXT X
2660 LET G2$(2)=FNF$(A1$(112,10),M7$),MORE$[PAGE,X1,N+2]=G2$
2665 LET G2$(2)=A1$(126,20),MORE$[PAGE,X1,N+3]=G2$,G2$(1)=""
2670 IF ACTION$<>"P" THEN GOTO 2685
2675 IF FNP$(A1$(146,10))<>"" THEN LET G2$(2)=FNF$(A1$(146,10),M7$)
2680 IF FNP$(G2$)<>"" THEN LET G2$(3+LEN(M7$))="fax"
2685 LET MORE$[PAGE,X1,N+4]=G2$,MORE$[PAGE,X1,0]=STR(SHEIGHT-2)
2690 LET LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2910 IF LOCKED=0 THEN GOTO 1400
2920 LET K$=K$(1,LEN(K$)-1)+CHR(ASC(K$(LEN(K$),1))+1)
2930 READ (ARM03_DEV,KEY=K$,DOM=1400,END=3000)IOL=ARM03A
2990 GOTO 1400
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET MESSAGE$[0]="No "+W$+" Addresses Found (<Enter>=Continue)"
3040 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3050 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 4000
3500 REM " --- Display last page"
3510 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
8000 REM " --- Functions" 
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNU$(Q$)=CVS(Q$,4)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 FOR X=1 TO FILES
9915 CLOSE (CHANNELS[X],ERR=9916)
9920 NEXT X
9930 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9935 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
9940 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9945 IF SELECTION$="END" THEN LET SELECTION$=""
9950 EXIT 
9999 END

0010 REM "SNM - Serial No Maintenance (Menu Processing)"
0020 REM "Program SNM.MA" 
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "MAS * DCF * WA# 208 * BASIS
0040 REM 
0045 REM ** BASIS mods @1490,1491 **
0047 REM ** BASIS - open SMC-01 (SAM/Developer Contract Info) **
0050 SETERR 9000
0055 SETESC 9000
0060 ON O0 GOTO 0070,2000,1000,2000,2000
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=49
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="SYS-01",FILES$[2]="SNM-01",FILES$[3]="SNM-02"
0120 LET FILES$[4]="SNM-03",FILES$[5]="SNM-04",FILES$[6]="SNT-01"
0125 LET FILES$[7]="SNT-02",FILES$[8]="SNT-03"
0130 LET FILES$[9]="CSM-01",FILES$[10]="CSM-02",FILES$[11]="CSM-03"
0140 LET FILES$[12]="CSM-04",FILES$[13]="CSM-05",FILES$[14]="CSM-06"
0145 LET FILES$[15]="TMM-01",FILES$[16]="ARM-01",FILES$[17]="SMC-01"
0150 CALL "SYC.DA",1,1,17,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SNM01_DEV=CHANNELS[2],SNM02_DEV=CHANNELS[3]
0165 LET SNM03_DEV=CHANNELS[4],SNM04_DEV=CHANNELS[5],SNT01_DEV=CHANNELS[6]
0170 LET SNT02_DEV=CHANNELS[7],SNT03_DEV=CHANNELS[8]
0175 LET CSM01_DEV=CHANNELS[9],CSM02_DEV=CHANNELS[10],CSM03_DEV=CHANNELS[11]
0180 LET CSM04_DEV=CHANNELS[12],CSM05_DEV=CHANNELS[13],CSM06_DEV=CHANNELS[14]
0190 LET TMM01_DEV=CHANNELS[15],ARM01_DEV=CHANNELS[16],SMC01_DEV=CHANNELS[17]
0200 REM " --- IOLIST's"
0210 SNM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A[ALL]
0220 call "templates.pgm::SNM02"
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0275 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$
0280 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$
0285 GLS01A: IOLIST G0$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0290 IVS10D: IOLIST I5$,I6$(1),I7$(1)
0295 IVS10N: IOLIST Y0$,Y[ALL]
0400 REM " --- Parameters"
0405 DIM P[8],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="IV",LF$="N",LS$="N",MF$="N",MAXROW=15
0420 LET AR$="N",AP$="N",BM$="N",GL$="N",OP$="N",PO$="N",WO$="N",DI$="N"
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 LET G[0]=NUM(G2$(1,2)),G[3]=NUM(G2$(7,2)),G[4]=NUM(G2$(9,2))
0435 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0437 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1)),P[2]=NUM(I2$(7,2))
0440 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0443 LET P[6]=NUM(I4$(1,2)),P[7]=FNYY_YEAR(I4$(3,2)),P[8]=NUM(I2$(3,2))
0445 IF POS("Y"=I2$(51,3))>0 THEN LET MF$="Y"
0450 IF POS(I3$(3,1)="LF")>0 THEN LET LF$="Y"
0455 IF POS(I3$(17,1)="LS")>0 THEN LET LS$="Y"
0460 GOSUB 6000
0465 IF AR$="N" THEN GOTO 0480
0470 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0475 LET CUSTLEN=NUM(P2$(1,2)),CUSTMASK$=M0$,DI$=P3$(11,1)
0480 IF GL$="N" THEN LET DI$="N"
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(80," "),DP[3]
0515 DIM A0$(20),A1$(60),A2$(18),A3$(35),A4$(60),A[2],Y[1]
0520 DIM OPTLIST$[2,MAXROW,6],PARAM$[2,MAXROW,3]
0530 LET L0=6,M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),M4=LEN(M4$),M5=LEN(M5$)
0545 LET A0$(1,2)=N0$,G5=LEN(G5$),DP[1]=14,MONTHS$=G3$(235,G[0]*3)
0550 IF P[4]>0 THEN LET DP[2]=DP[1]+P[3]+1
0555 IF P[5]>0 THEN LET DP[3]=DP[1]+P[3]+1+P[4]+1
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0720 PRINT 'SB',@(2,3),"Serial Nbr:",@(55),"Current Users: ",@(1,4),"Description:",'SF',@(0,L0),'CE',
0800 REM " --- Assign remaining channel numbers"
0900 REM " --- Position File"
0990 READ (SNM01_DEV,KEY="",DOM=1000)
1000 REM " --- Serial number"
1010 PRINT 'CF',@(14,4),'CL'
1015 LET V2$="",V2$=KEY(SNM01_DEV,END=1030)
1030 LET V0$="S",V1$="KCE^",V3$="",V4$="",V0=20,V1=14,V2=3,O0=1
1040 LET V4$="Enter A Valid Serial Number ("+V4$+"<F3>=Lookup)"
1045 GOSUB 7000
1050 ON V3 GOTO 1080,1080,1000,1055,9900
1055 CALL "SYC.LS",V$
1060 IF V$="" THEN GOTO 1000
1080 IF V$="" THEN GOTO 0900
1090 GOTO 1200
1200 REM " --- Find record"
1210 LET A0$(1)=V$
1220 READ (SNM01_DEV,KEY=A0$,DOM=1400)IOL=SNM01A
1230 dim snm02$:fattr(snm02$)
1240 readrecord(snm02_dev,key=a0$,dom=*next)snm02$
1300 REM " --- Display record"
1320 GOSUB 5000
1340 GOTO 2000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,1000
1490 REM ON POS(V$="YN") GOTO 1400,1500,1000
1491 ON POS(V$="YN") GOTO 1400,1000,1000; REM No adding new records!!
1500 REM " --- Initializations for new record"
1510 DIM A1$(60),A2$(18),A3$(35),A4$(60),A[2]
1540 LET O0=4
1580 GOSUB 5000
2000 REM " --- Branch to appropriate submenu or subtask"
2080 ON O0 GOTO 0070,2090,1000,2090,2100
2090 ON MENU GOTO 4000,4000,4000,4000
2100 REM " --- New Item: Load submenu"
2110 LET X$="SNMNEW",V$="",V3$="",V0=0,V1=0,V2=0,SUBTASKS=0,SUBTASK=0,O0=3
2150 CALL "SYC.OL",X$,SUBTASKS,V0,OPTLIST$[ALL],PARAM$[ALL],V$,V1,V3$,V2
2200 REM " --- New item: Run next subtask"
2210 LET SUBTASK=SUBTASK+1
2220 IF SUBTASK>SUBTASKS THEN GOTO 4000
2230 LET DESCRIPTION$=FNS$(OPTLIST$[1,SUBTASK,1])
2240 LET SELECTION$=OPTLIST$[1,SUBTASK,2]
2250 GOSUB 5400
2290 RUN SELECTION$,ERR=2200
4000 REM " --- Main submenu"
4010 DIM MENUS$[6]
4015 LET MENUS=1,MENUS$[MENUS]="SNM.MA",WIN_X=40,WIN_Y=L0+1,MENU=0
4020 LET MENUS=MENUS+1,MENUS$[MENUS]="SNM.MB"
4025 LET MENUS=MENUS+1,MENUS$[MENUS]="SNM.MC"
4030 LET MENUS=MENUS+1,MENUS$[MENUS]="SNM.MD"
4045 LET MENUS=MENUS+1,MENUS$[MENUS]="SYC.KA"
4047 LET MENUS=MENUS+1,MENUS$[MENUS]="SNM.MG"
4050 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4070 GOSUB 5400
4075 IF SELECTION$="" THEN GOTO 1000
4080 IF SELECTION$="SYC.KA" THEN GOTO 4100
4085 IF SELECTION$="SNM.MG" THEN GOTO 4200
4090 RUN SELECTION$,ERR=4000
4100 REM " --- Comments"
4110 DIM INFO$[5],INFO[5]
4120 LET X$="Serial # "+A0$+" "+FNDESC$(A1$)
4130 LET INFO$[0]="SNM-04",INFO$[1]=A0$,INFO$[2]=X$,ACTION=0,INFO[1]=L0
4150 CALL "SYC.KA",ACTION,INFO$[ALL],INFO[ALL]
4190 GOTO 4000
4200 CALL "SYC.WA",0,52,15,10,7,CVS(A0$,2),WNAME$
4210 CALL "TAOSSET::PARAM","(BBX)snvf_1.bbx",A0$
4230 CALL "SYC.WD",WNAME$
4240 GOTO 4000
5000 REM " --- Display"
5010 PRINT 'SB',@(14,3),A0$,@(70,3),A[0]:"####",@(14,4),FNDESC$(A1$),'SF',
5090 RETURN 
5400 REM " --- Clear bottom"
5410 CALL "SYC.WC",1,0,80,0,0,L0-1,0
5420 PRINT @(0,L0),'CE',
5430 IF SELECTION$<>"" THEN PRINT 'SB',@(8,L0-1)," ",DESCRIPTION$," ",'SF',
5500 REM " --- Window initializations for overlays"
5510 LET WIN_X=0,WIN_Y=5,HEIGHT=19,WIDTH=80,PAGES=MIN(INT((DSZ-5000)/4000),20)
5520 LET TITLE$=DESCRIPTION$,MAXROW=HEIGHT-5,H0=WIDTH-1
5530 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
5590 RETURN 
6000 REM " --- What other applications are installed?"
6010 CALL "SYC.VA","AR",INFO$[ALL]
6020 LET AR$=INFO$[20]
6030 CALL "SYC.VA","AP",INFO$[ALL]
6040 LET AP$=INFO$[20]
6050 CALL "SYC.VA","BM",INFO$[ALL]
6060 LET BM$=INFO$[20]
6070 CALL "SYC.VA","GL",INFO$[ALL]
6080 LET GL$=INFO$[20]
6090 CALL "SYC.VA","OP",INFO$[ALL]
6100 LET OP$=INFO$[20]
6110 CALL "SYC.VA","PO",INFO$[ALL]
6120 LET PO$=INFO$[20]
6130 CALL "SYC.VA","WO",INFO$[ALL]
6140 LET WO$=INFO$[20]
6150 CALL "SYC.VA","SA",INFO$[ALL]
6160 LET SA$=INFO$[20]
6170 IF GL$<>"Y" THEN GOTO 6190
6180 CALL "SYC.VA",N2$,INFO$[ALL]
6185 LET GL$=INFO$[9]
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNS$(Q$)=CVS(Q$,3)
8090 DEF FNDESC$(Q$)=CVS(Q$,32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "DDM - Record Layout Maintenance (Detail)"
0020 REM "Program DDM.BC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0200 REM " --- IOLists
0210 DDM01A: IOLIST X0$(1),X1$(1),X[ALL]
0230 DDM03A: IOLIST B0$(1),B1$(1),B[ALL]
0240 DDM04A: IOLIST C0$(1),C1$(1),C[ALL]
0250 DDM13A: IOLIST D0$(1),D1$(1)
0260 DDM06A: IOLIST XREF$
0280 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0500 REM " --- Initializations
0510 DIM O0$(LEN(C0$)),O1$(LEN(C1$)),O[2],X0$(12),X1$(105),X[2]
0550 LET M$="##0",M=LEN(M$),C0$(1)=B0$
0700 REM " --- Background
0750 PRINT @(0,L0-1),'CE','SB',@(1,L0-1),"Seq Data Name",@(19),"Description",@(50),"T Len Rep Occ Revised Sep",'SF',
0900 REM " --- Position file
0920 GOSUB 5100
0930 IF Q1=0 THEN GOTO 1400
0940 GOTO 4000
1000 REM " --- Overflow
1010 IF L<=L9 THEN GOTO 1100
1020 PRINT @(0,22),'CL',@(0,L0),'LD',
1040 LET L=L9
1100 REM " --- Next key
1120 LET K$=KEY(DDM04_DEV,END=1400)
1140 IF POS(B0$=K$)<>1 THEN GOTO 1400
1150 READ (DDM04_DEV)IOL=DDM04A
1190 GOTO 1485
1400 REM " --- Sequence Number
1410 LET X=NUM(C0$(8,3))+10
1420 LET V0$="Z",V1$="KERC",V2$=STR(X),V3$="",V0=3,V1=1,V2=L,I0=0
1425 LET V4$="Enter A 3 Digit Sequence Number (<F4>=End)"
1430 GOSUB 7000
1440 IF V3=4 THEN PRINT @(1,L),"   "; GOTO 4000
1450 IF V$="000" OR V$="" THEN GOTO 1400
1460 LET C0$(8,3)=V$
1480 FIND (DDM04_DEV,KEY=C0$,DOM=1600)IOL=DDM04A
1485 FIND RECORD(DDM04_DEV,KEY=C0$)OLDRECORD$
1487 LET O0$=C0$,O1$=C1$,O[0]=C[0],O[1]=C[1]
1490 IF C0$<=K9$ THEN GOSUB 5400
1500 GOSUB 5000
1520 LET I0=2
1530 GOTO 3300
1600 REM " --- Initializations for new record
1610 DIM C1$(122),C[2]
1620 LET OLDRECORD$="",C1$(47,6)=REVISION$,C1$(54,1)="N"
1630 LET O0$=C0$,O1$=C1$,O[0]=C[0],O[1]=C[1]
2000 REM " --- Data Name
2005 LET V2$=C1$(1,12)
2010 LET V0$="S",V1$="C^",V3$="",V0=12,V1=5,V2=L,I0=1
2020 LET V4$="Enter the data dictionary name for this element (<F1>=Maintenance/<F3>=Lookup)"
2030 GOSUB 7000
2040 ON V3 GOTO 2100,2050,2190,2080,1400
2050 GOSUB 6100
2060 IF V$<>"" THEN LET V2$=V$
2070 GOTO 2010
2080 GOSUB 6000
2090 IF V$="" THEN LET V$=C1$(1,12)
2100 REM 
2110 DIM X0$(12),X1$(105),X[2]
2120 LET X0$(1)=V$
2130 FIND (DDM01_DEV,KEY=X0$,DOM=2010)IOL=DDM01A
2150 IF X1$(37,1)="N" THEN LET C1$(54,1)="Y"
2160 IF X0$<>C1$(1,12) OR FNP$(C1$(17,30))="" THEN LET C1$(17,30)=X1$(1,30)
2170 LET C1$(1,12)=X0$
2180 GOSUB 5000
2190 GOTO 3200
2200 REM " --- Description
2210 LET V0$="S",V1$="C",V2$=C1$(17,30),V3$="",V0=30,V1=19,V2=L
2220 LET V4$="Enter up to "+STR(V0)+" characters describing this field"
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3200,2200,3200
2250 LET C1$(17,V0)=V$
2260 PRINT @(V1,V2),C1$(17,V0)
2290 GOTO 3200
2700 REM " --- Repetitions
2705 IF C[1]=1 THEN LET C[1]=0
2710 LET V0$="N",V1$="C",V2$=STR(C[1]),V3$=M$,V0=M,V1=56,V2=L
2720 LET V4$="Enter the repetition factor (Actual Length = Element Length * Repetitions)"
2730 GOSUB 7000
2740 ON V3 GOTO 2750,2700,3200,2700,3200
2750 IF V<0 THEN GOTO 2700
2760 LET C[1]=V
2770 PRINT @(V1,V2),C[1]:M$
2790 GOTO 3200
2800 REM " --- Occurs
2810 LET V0$="N",V1$="C",V2$=STR(C[2]),V3$=M$,V0=M,V1=60,V2=L
2820 LET V4$="Enter the number of times this field is repeated in the record layout"
2830 GOSUB 7000
2840 ON V3 GOTO 2850,2800,3200,2800,3200
2850 IF V<0 THEN GOTO 2800
2860 LET C[2]=V
2870 PRINT @(V1,V2),C[2]:M$
2890 GOTO 3200
2900 REM " --- Separator
2910 LET V0$="S",V1$="C^",V2$=C1$(54,1),V3$="",V0=1,V1=73,V2=L
2915 IF X1$(37,1)="N" THEN LET C1$(54,1)="Y"; GOTO 2980
2920 LET V4$="Separator (Y=Yes/N=No/K=Key/X=Key+Separator/E=At End)?"
2930 GOSUB 7000
2940 ON V3 GOTO 2950,2900,3200,2900,3200
2950 IF POS(V$="YNEKX")=0 THEN GOTO 2900
2960 LET C1$(54,1)=V$
2980 PRINT @(V1,V2),C1$(54,1)
2990 GOTO 3200
3200 REM " --- Sequence Control
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,2000,2200,2700,2800,2900,3300
3300 REM " --- Line Correct?
3310 LET V0$="S",V1$="^C",V2$="",V0=1,V1=78,V2=L,K9$=C0$
3320 LET V4$="<Enter>=Correct   C=Comments   D=Delete   <F1>=Additional Options"
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3800,3200,3300,3400
3350 IF V$="" THEN GOTO 3400
3360 IF V$="C" THEN GOTO 3500
3370 IF V$="D" THEN GOTO 3700
3380 GOTO 3300
3400 REM " --- Write record
3405 LET X1$(1)=""
3410 FIND (DDM01_DEV,KEY=C1$(1,12),DOM=3415)IOL=DDM01A
3415 IF C1$(17,30)=X1$(1,30) THEN LET C1$(17,30)=""
3420 WRITE (DDM04_DEV,KEY=C0$)IOL=DDM04A
3425 FIND RECORD(DDM04_DEV,KEY=C0$)NEWRECORD$
3430 IF NEWRECORD$=OLDRECORD$ THEN GOTO 3480
3435 LET C1$(47,6)=REVISION$
3440 WRITE (DDM04_DEV,KEY=C0$)IOL=DDM04A
3450 GOSUB 6800
3455 IF O0$=C0$ AND O1$=C1$ AND O[0]=C[0] AND O[1]=C[1] THEN GOTO 3480
3460 GOSUB 6200
3465 IF O0$<>C0$ OR O1$(1,12)<>C1$(1,12) THEN GOSUB 6400
3470 GOSUB 6300
3480 LET L=L+1
3485 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3500 REM " --- Run comments overlay
3510 DIM COMINFO$[2],COMINFO[5]
3520 LET COMINFO$[0]="DDM-14",COMINFO$[1]=C0$
3530 LET COMINFO$[2]="Seq "+C0$(8,3)+" "+C1$(1,12)
3540 LET COMINFO[1]=L0-3,COMINFO[3]=3,COMINFO[4]=60,COMINFO[5]=10
3550 CALL "SYC.KA",0,COMINFO$[ALL],COMINFO[ALL]
3590 GOTO 3300
3700 REM " --- Delete
3710 REMOVE (DDM04_DEV,KEY=C0$,DOM=3720)
3720 PRINT @(0,L),'CL',
3730 GOSUB 6800
3740 GOSUB 6200
3750 GOSUB 6400
3780 IF V3=4 THEN GOTO 4000
3790 GOTO 1000
3800 REM " --- <F1> Additional Options Window"
3820 CALL "DDC.BA",L0-2,C1$,DDFILES[ALL]
3890 GOTO 3300
4000 REM " --- Screen Correct?
4010 LET V4$="Is The Above Information Correct (Y/N/List/Review/Dup)?"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4060 IF V$="D" THEN GOTO 4100
4070 IF V$="L" THEN GOTO 4400
4080 IF V$="R" THEN GOTO 4600
4090 ON POS(V$="YN") GOTO 4000,4900,1000
4100 REM " --- Duplicate
4190 RUN "DDM.BE"
4400 REM " --- List
4410 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review
4610 READ (DDM04_DEV,KEY=B0$,DOM=4620)
4620 GOSUB 5400
4690 GOTO 1000
4900 REM " --- Run ribbon overlay
4910 LET O0=1
4990 RUN "DDM.BB"
5000 REM " --- Display
5010 LET T1$="",T2$="",X0$(1,12)=C1$(1)
5015 LET X1$(1)="",X[0]=0
5020 IF C[1]>1 THEN LET T1$=STR(C[1]:M$)
5030 IF C[2]>0 THEN LET T2$=STR(C[2]:M$)
5040 FIND (DDM01_DEV,KEY=X0$,DOM=5080)IOL=DDM01A
5050 LET C1$(1,12)=X0$
5060 IF FNP$(C1$(17,30))="" THEN LET C1$(17,30)=X1$(1,30)
5080 PRINT @(0,L),'CL',@(1),C0$(8),@(5),X0$,@(19),C1$(17,30),@(50),X1$(37,1),@(52),X[0]:M$,@(56),T1$,@(60),T2$,@(64),FNB6$(C1$(47,6)),@(73),C1$(54,1)
5090 RETURN 
5100 REM " --- List
5105 LET Q1=0
5110 GOSUB 5400
5120 READ (DDM04_DEV,KEY=B0$,DOM=5130)
5130 LET K$=KEY(DDM04_DEV,END=5300)
5150 IF POS(B0$=K$)<>1 THEN GOTO 5300
5160 READ (DDM04_DEV)IOL=DDM04A
5170 LET Q1=1
5200 IF L<=L9 THEN GOTO 5250
5203 LET V4$="Press <Enter> To Continue:"
5205 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5210 GOSUB 7000
5220 IF V3=4 THEN GOTO 5300
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+1,K9$=C0$
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen
5410 PRINT @(0,L0),'CE',
5420 LET L=L0
5490 RETURN 
6000 REM " --- Dictionary Lookup
6010 LET CHANNEL=DDM01_DEV,ROW=V2-14
6020 CALL "DDC.LA",CHANNEL,ROW,V$
6090 RETURN 
6100 REM " --- Data Dictionary Maintenance
6110 LET WINDOW$="YES",ROW=V2-9
6120 CALL "DDC.AA",WINDOW$,3,ROW,V$,DDFILES[ALL]
6190 RETURN 
6200 REM " --- Remove old field cross-reference
6210 LET X0$(1)=O1$(1,12),X1$(1)=""
6220 LET X1$(37,1)=O1$(53,1),X[0]=O[0]
6230 FIND (DDM01_DEV,KEY=X0$,DOM=6240)IOL=DDM01A
6240 LET LENGTH=X[0]
6250 IF O[1]>0 THEN LET LENGTH=LENGTH*O[1]
6255 IF FNP$(O1$(17,30))<>"" THEN LET X1$(1,30)=O1$(17,30)
6260 LET XREF$(1)=X0$(1,12)+X1$(1,30)+X1$(37,1)+STR(LENGTH:"0000")+O0$
6270 REMOVE (DDM06_DEV,KEY=XREF$,DOM=6280)
6290 RETURN 
6300 REM " --- Write new field cross-reference
6310 LET X0$(1)=C1$(1,12),X1$(1)=""
6320 LET X[0]=0
6330 FIND (DDM01_DEV,KEY=X0$,DOM=6340)IOL=DDM01A
6340 LET LENGTH=X[0]
6350 IF C[1]>0 THEN LET LENGTH=LENGTH*C[1]
6355 IF FNP$(C1$(17,30))<>"" THEN LET X1$(1,30)=C1$(17,30)
6360 LET XREF$(1)=X0$(1,12)+X1$(1,30)+X1$(37,1)+STR(LENGTH:"0000")+C0$
6370 WRITE (DDM06_DEV,KEY=XREF$)IOL=DDM06A
6390 RETURN 
6400 REM " --- Remove Old Data Field Comments
6410 READ (DDM14_DEV,KEY=O0$,DOM=6420)
6420 LET K$=KEY(DDM14_DEV,END=6490)
6430 IF POS(O0$=K$)<>1 THEN GOTO 6490
6440 REMOVE (DDM14_DEV,KEY=K$)
6450 GOTO 6420
6490 RETURN 
6800 REM " --- Update Last Revision Date on Header/Record ID Records
6810 LET A0$(1)=C0$(1,6),B0$(1)=C0$(1,7)
6820 FIND (SYM03_DEV,KEY=A0$)IOL=SYM03A
6830 FIND (DDM03_DEV,KEY=B0$)IOL=DDM03A
6840 LET A1$(31,6)=REVISION$,B1$(31,6)=REVISION$
6850 WRITE (SYM03_DEV,KEY=A0$)IOL=SYM03A
6860 WRITE (DDM03_DEV,KEY=B0$)IOL=DDM03A
6890 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8090 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

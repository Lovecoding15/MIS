0010 REM "DDM - Data Expansion Code Maintenance"
0020 REM "Program DDM.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="DDM-20"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],DDM20_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 DDM20A: IOLIST A0$(1),A1$(1),A2$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="DD",N4$=F5$
0500 REM " --- Initializations"
0510 DIM A0$(12),A1$(30),A2$(240)
0700 REM " --- Background"
0720 PRINT 'SB',@(4,3),"Expansion Code",@(7,4),"Description",@(3,5),"Instructions 1:",@(16,6),"2:",@(16,7),"3:",@(16,8),"4:",'SF',
0900 REM " --- Position File"
0990 READ (DDM20_DEV,KEY="",DOM=1000)
1000 REM " --- Data Expansion Code"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(DDM20_DEV,END=1050)
1050 LET V0$="S",V1$="KE",V3$="",V4$="Enter A Valid Data Expansion Code (<F3>=Lookup)",V0=12,V1=19,V2=3
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN GOSUB LOOKUP
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(1)=V$
1200 REM " --- Retrieve record"
1210 READ (DDM20_DEV,KEY=A0$,DOM=1400)IOL=DDM20A
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Initialize new record"
1510 DIM A1$(30),A2$(240)
2000 REM " --- Description"
2020 LET V0$="S",V1$="",V2$=A1$,V3$="",V0=30,V1=19,V2=4,I0=0
2030 LET V4$="Enter The Description For This Data Expansion Code"
2040 GOSUB 7000
2050 ON V3 GOTO 2060,2000,3900,2000,3900
2060 LET A1$(1)=V$
2090 GOTO 3900
2200 REM " --- Expansion Instructions Line 1"
2220 LET V0$="S",V1$="",V2$=A2$(1,60),V3$="",V0=60,V1=19,V2=5
2230 LET V4$="Enter 1st Line Of Instructions To Expansion Data"
2240 GOSUB 7000
2250 ON V3 GOTO 2260,2200,3900,2200,3900
2260 LET A2$(1,60)=V$
2290 GOTO 3900
2300 REM " --- Expansion Instructions Line 2"
2320 LET V0$="S",V1$="",V2$=A2$(61,60),V3$="",V0=60,V1=19,V2=6
2330 LET V4$="Enter 2nd Line Of Instructions To Expansion Data"
2340 GOSUB 7000
2350 ON V3 GOTO 2360,2300,3900,2300,3900
2360 LET A2$(61,60)=V$
2390 GOTO 3900
2400 REM " --- Expansion Instructions Line 3"
2420 LET V0$="S",V1$="",V2$=A2$(121,60),V3$="",V0=60,V1=19,V2=7
2430 LET V4$="Enter 3rd Line Of Instructions To Expansion Data"
2440 GOSUB 7000
2450 ON V3 GOTO 2460,2400,3900,2400,3900
2460 LET A2$(121,60)=V$
2490 GOTO 3900
2500 REM " --- Expansion Instructions Line 4"
2520 LET V0$="S",V1$="",V2$=A2$(181,60),V3$="",V0=60,V1=19,V2=8
2530 LET V4$="Enter 4th Line Of Instructions To Expansion Data"
2540 GOSUB 7000
2550 ON V3 GOTO 2560,2500,3900,2500,3900
2560 LET A2$(181,60)=V$
2590 GOTO 3900
3900 REM " --- Next Field"
3902 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,2300,2400,2500,3990
3990 LET I0=K0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record"
4110 WRITE (DDM20_DEV,KEY=A0$)IOL=DDM20A
4190 GOTO 1000
4200 REM " --- Remove record"
4210 REMOVE (DDM20_DEV,KEY=A0$,DOM=4220)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4900,9900
4900 REM " --- Report Initializations"
4990 RUN "DDM.EB"
5000 REM " --- Display"
5010 PRINT @(19,3),A0$,@(19,4),A1$,@(19,5),A2$(1,60),@(19,6),A2$(61,60),@(19,7),A2$(121,60),@(19,8),A2$(181,60),
5090 RETURN 
6200 REM " --- Standard windowed lookup"
6205 LOOKUP: 
6210 LET NUMBER=1,RECORD$="",CHANNEL=DDM20_DEV,TITLE$="Data Expansion Codes"
6220 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6230 LET DESCRIPTION$[0]="Code",FIELD[0]=1,POSITION[0]=1,LENGTH[0]=12
6240 LET DESCRIPTION$[1]="Description",FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6250 LET ROW=L0+1,COLUMN=79-LENGTH[0]-LENGTH[1]-NUMBER*4,V$="NO COMPANY"
6280 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

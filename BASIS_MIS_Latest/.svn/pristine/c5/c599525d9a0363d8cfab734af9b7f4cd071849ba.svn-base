0010 REM "ARR - Simple Invoice Register"
0020 REM "Program ARR.VA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ARE-05",FILES$[2]="ARE-15",FILES$[3]="ARM-01"
0140 LET FILES$[4]="ARM-10",FILES$[5]="SYS-01"
0150 LET OPTIONS$[1]="L",OPTIONS$[2]="L"
0160 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0185 LET ARE05_DEV=CHANNELS[1],ARE15_DEV=CHANNELS[2],ARM01_DEV=CHANNELS[3]
0190 LET ARM10_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5]
0200 REM " --- IOLists
0210 ARE05A: IOLIST A0$,A1$
0220 ARM01A: IOLIST *,X5$
0230 ARM10A: IOLIST X2$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0310 ARE15A: IOLIST W0$,W1$,W[ALL]
0330 ARM10D: IOLIST X1$,C1$
0400 REM " --- Parameters
0405 DIM P[6],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$,SOURCE$=PGM(-2)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[6]=NUM(P3$(4,1))
0455 FOR X=0 TO 4
0460 LET G[X]=NUM(G2$(X*2+1,2),ERR=0465)
0465 NEXT X
0470 LET G[2]=FNYY_YEAR(G2$(5,2))
0500 REM " --- Init Data
0520 LET H0=80,H=0,L9=59,L=L9+1,ERRORS=0,WHEN$=F0$(7,3),CLOCK$=""
0530 LET HEADINGS=2
0540 DIM HEADINGS$[HEADINGS],W[2],J$(15,"-")
0550 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,UNITS=0
0570 LET M0=LEN(M0$),M1=LEN(M1$),TOTAL=0
0600 REM " --- Additional File Opens
0650 LET GL$="N",STATUS=0
0655 CALL "GLC.BA",ERR=0660,SOURCE$,N2$,GLW11$,GL$,STATUS
0660 IF STATUS<>0 THEN GOTO 9900
0665 IF GL$<>"Y" THEN GOTO 0700
0670 LET FILES$[6]="GLM-01",FILES$[7]=GLW11$,OPTIONS$[7]="CL"
0675 CALL "SYC.DA",1,6,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS>0 THEN GOTO 9900
0690 LET GLM01_DEV=CHANNELS[6],GLW11_DEV=CHANNELS[7]
0800 REM " --- Run Report?
0810 LET V4$="Do You Want To Print The "+N3$
0820 CALL "SYC.YN",1,V4$,2,V$,V3
0830 IF V3=4 THEN GOTO 9900
0840 IF V$<>"Y" THEN GOTO 9900
0900 REM " --- File initializations"
0910 CALL "SYC.NB","Printing",7,COLUMN
0950 CALL "SYC.GA",7,1,"","",STATUS
0960 IF STATUS THEN GOTO 9900
1000 REM " --- Init File
1020 READ (ARE05_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Main Read
1120 READ (ARE05_DEV,END=4000)IOL=ARE05A
1140 IF A0$(1,2)<>N0$ THEN GOTO 4000
1150 IF A0$(10,3)<>"000" THEN GOTO 1100
1180 PRINT @(COLUMN,11),A0$(3,7),
1190 IF A1$(17,1)="V" THEN GOTO 2400
1200 REM " --- Breaks
1220 LET T0=0,T9=0
1400 REM " --- Get Customer
1410 LET X5$="*Not On File*",CUSTNAME$=X5$
1420 FIND (ARM01_DEV,KEY=N0$+A1$(7,6),DOM=1500)IOL=ARM01A
1430 LET CUSTNAME$=X5$(1,30),X5$=X5$(1,20)
1500 REM " --- Dist
1510 LET X1$="*Not On File*"
1515 DIM C1$(G[4],"0")
1520 FIND (ARM10_DEV,KEY=N0$+"D"+A1$(13,2),DOM=1600)IOL=ARM10D
1530 LET X1$=X1$(6,20)
1600 REM " --- Terms
1610 LET X2$="*Not On File*"
1620 FIND (ARM10_DEV,KEY=N0$+"A"+A1$(15,2),DOM=1700)IOL=ARM10A
1640 LET X2$=X2$(6,20)
1700 GOTO 2000
2000 REM " --- Detail
2005 READ (ARE15_DEV,KEY=A0$(1,9),DOM=2010)
2010 LET K$=KEY(ARE15_DEV,END=2200)
2015 IF LEN(K$)<9 THEN GOTO 2200
2020 IF K$(1,9)<>A0$(1,9) THEN GOTO 2200
2030 READ (ARE15_DEV)IOL=ARE15A
2040 LET T0=T0+W[2],T9=T9+W[0]
2060 GOSUB 6000
2090 GOTO 2010
2200 REM " --- Print Invoice
2210 IF L+3>L9 THEN GOSUB 5000
2215 PRINT (7)""
2220 PRINT (7)A0$(3,7),@(8),FNB6$(A1$(1,6)),@(17),FNF$(A1$(7,P[0]),M0$)," ",X5$,@(35+M0)," ",A1$(13,2)," ",X1$,@(H0-M1),T0:M1$
2230 IF A1$(17,1)="A" THEN PRINT (7)@(8),"*** Adjustment ***",
2240 PRINT (7)@(36+M0),A1$(15,2)," ",X2$
2250 LET L=L+3,TOTAL=TOTAL+T0
2280 GOSUB 6200
2300 REM " --- Check G/L
2310 LET GLSTATUS=0
2320 IF GL$<>"Y" THEN GOTO 1100
2330 CALL "GLC.EA",A1$(1,6),"N",PERIOD$,YEAR$,GLSTATUS
2340 IF GLSTATUS>0 THEN CALL "GLC.GA",7,A1$(1,6),GLSTATUS,L,ERRORS,STATUS
2390 GOTO 1100
2400 REM " --- Print Void
2420 IF L+2>L9 THEN GOSUB 5000
2430 PRINT (7)""
2440 PRINT (7)A0$(3,7),@(8),"(Voided)"
2460 LET L=L+2
2490 GOTO 1100
4000 REM " --- End
4010 LET WIDTH=H0,PAGE=H,UPDATE$="ARU.GA"
4020 IF L+2>L9 THEN GOSUB 5000
4030 PRINT (7)@(H0-M1),J$(1,M1)
4040 PRINT (7)@(H0-M1-12),"Report Total",@(H0-M1),TOTAL:M1$
4080 IF GL$="Y" THEN RUN "GLR.XA"
4090 RUN UPDATE$,ERR=9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5050 PRINT (7)"Invoice",@(10),"Date",@(16),"Customer",@(36+M0),"Distribution/Terms",@(H0-7),"Amount"
5080 LET L=L+1
5090 RETURN 
6000 REM " --- Summaries
6100 LET ACCOUNT$=W1$(1,G[4]),GLDATE$=A1$(1,6),AMOUNT=-W[2],UNITS=-W[1]
6110 LET REF1$=FNF$(A1$(7,P[0]),M0$),REF2$=A0$(3,7),REF3$=W0$(10,3)
6115 LET MEMO$=W1$(7+G[4],30)
6120 GOSUB GLRECAP
6190 RETURN 
6200 REM " --- Dist Summary
6220 LET ACCOUNT$=C1$(1,G[4]),GLDATE$=A1$(1,6),AMOUNT=T0,UNITS=T9
6230 LET MEMO$=CUSTNAME$,REF3$=""
6240 GOSUB GLRECAP
6290 RETURN 
6900 REM " --- Standard G/L Recap Routine"
6910 GLRECAP: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLW11_DEV,GLT05_DEV,ACCOUNT$,GLDATE$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

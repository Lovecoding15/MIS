0010 REM "IVU - Purge Inventory Activity"
0020 REM "Program: IVU.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="IVM-12",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET IVM12_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists"
0210 IVM12A: IOLIST A0$,A1$,A2$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0450 DIM P[7]
0460 LET P[0]=NUM(P2$(1,2)),P[6]=NUM(P4$(1,2)),P[7]=FNYY_YEAR(P4$(3,2))-1
0500 REM " --- Init Data"
0510 LET PERIOD$=STR(P[6]:"00")
0520 LET YEAR$=FNYEAR_YY21$(P[7])
0700 REM " --- Background"
0710 FOR X=4 TO 8 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(32,3),"Period:",@(34,5),"Year:",'SF',
1000 REM " --- Period"
1010 PRINT @(0,7),'CL',
1020 LET V0$="Z",V1$="REC",V2$=PERIOD$,V3$="",V4$="Enter Fiscal Period To Purge Through",V0=2,V1=40,V2=3,I0=0
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1060 IF V$<"00" OR V$>"12" THEN GOTO 1000
1070 LET PERIOD$=V$
1090 GOTO 3900
1100 REM " --- Year"
1120 LET V0$="D",V1$="RECY",V2$=YEAR$,V3$="",V4$="Enter Fiscal Year To Purge Through",V0=2,V1=40,V2=5
1140 GOSUB 7000
1150 IF V3=4 THEN GOTO 9900
1160 IF V$="" THEN GOTO 1100
1170 LET YEAR$=V$
1190 GOTO 3900
1200 REM " --- Check For Current Period"
1210 IF P4$(3,2)+P4$(1,2)>YEAR$+PERIOD$ THEN GOTO 1300
1220 CALL "SYC.PA",SYS01_DEV,NUM(P4$(1,2)),FNYY_YEAR(P4$(3,2)),V0$,CUR_DATE$,STATUS
1230 CALL "SYC.YN",0,"Current Period Ends: "+FNB$(CUR_DATE$)+". Continue",0,V$,V3
1240 IF V3=4 THEN GOTO 9900
1250 IF V$="NO" THEN GOTO 1000
1260 IF V$<>"YES" THEN GOTO 1200
3900 REM " --- Next Field
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 IF ABS(I0)>1000 THEN GOTO 3990
3960 ON I0 GOTO 1000,1100,1200,4000
4000 REM " --- Options"
4010 CALL "SYC.PA",SYS01_DEV,NUM(PERIOD$),FNYY_YEAR(YEAR$),V0$,END_DATE$,STATUS
4015 LET V4$="Continuing Will Purge Activity Records Through "+FNB$(END_DATE$)
4020 PRINT @(FNC(V4$,80),7),V4$,
4030 LET V0$="S",V1$="CE",V2$="NO",V3$="",V4$="Is The Above Information Correct (Yes/No)?",V0=3,V1=FNV(V4$),V2=22
4040 GOSUB 7000
4050 IF V3=4 THEN GOTO 9900
4060 IF V$="NO" THEN GOTO 1000
4070 IF V$<>"YES" THEN GOTO 4000
4080 REM " --- Disallow 'M'enu option in Error Routine
4090 LET EXIT_CTRL=1
4200 REM " --- Purge IVM-12 Here"
4210 READ (IVM12_DEV,KEY=N0$,DOM=4220)
4220 CALL "SYC.NB","Purging:",3+P[0],COLUMN
4300 REM " --- Main Read"
4310 READ (IVM12_DEV,END=9900)IOL=IVM12A
4320 IF A0$(1,2)<>N0$ THEN GOTO 9900
4330 IF A0$(25,4)>YEAR$+PERIOD$ THEN READ (IVM12_DEV,KEY=A0$(1,24)+$FF$,DOM=4300)
4340 PRINT @(COLUMN,11),A0$(3,2)," ",A0$(5,P[0]),
4350 REMOVE (IVM12_DEV,KEY=A0$,DOM=4351)
4390 GOTO 4300
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "POR - Purchase Requisition Register (Input Overlay)"
0020 REM "Program POR.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "21Jul97 JWK: Display Last as ending vendor default (1950)
0040 REM 
0080 BEGIN 
0085 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=22
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="SYS-01",FILES$[2]="POE-01",FILES$[3]="POE-02"
0120 LET FILES$[4]="POE-11",FILES$[5]="POE-12",FILES$[6]="POE-21"
0125 LET FILES$[7]="POE-22",FILES$[8]="POE-31",FILES$[9]="POE-32"
0130 LET FILES$[10]="POE-41",FILES$[11]="POE-42",FILES$[12]="POE-52"
0135 LET FILES$[13]="POM-02",FILES$[14]="POS-10",FILES$[15]="POW-07"
0140 LET FILES$[16]="APM-01",FILES$[17]="IVM-01",FILES$[18]="IVM-02"
0145 LET OPTIONS$[15]="CL"
0155 CALL "SYC.DA",1,1,18,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0165 LET SYS01_DEV=CHANNELS[1],POE01_DEV=CHANNELS[2],POE02_DEV=CHANNELS[3]
0170 LET POE11_DEV=CHANNELS[4],POE12_DEV=CHANNELS[5],POE21_DEV=CHANNELS[6]
0175 LET POE22_DEV=CHANNELS[7],POE31_DEV=CHANNELS[8],POE32_DEV=CHANNELS[9]
0180 LET POE41_DEV=CHANNELS[10],POE42_DEV=CHANNELS[11],POE52_DEV=CHANNELS[12]
0185 LET POM02_DEV=CHANNELS[13],POS10_DEV=CHANNELS[14],POW07_DEV=CHANNELS[15]
0190 LET APM01_DEV=CHANNELS[16],IVM01_DEV=CHANNELS[17],IVM02_DEV=CHANNELS[18]
0200 REM " --- IOLIST's"
0210 POE01A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0220 APM01A: IOLIST B0$(1),B1$(1)
0230 POW07A: IOLIST X0$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,I5$,M1$,M2$,M3$,M4$,M5$
0280 POS01A: IOLIST P0$,P1$,P2$,P3$,P4$
0290 APS01A: IOLIST X$,P1$,P2$,X$,X$,M0$
0300 ARS01A: IOLIST X$,P5$,P6$,X$,X$,P7$
0400 REM " --- Parameters"
0405 DIM I[6],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO",AP$="N",AR$="N",SF$="N"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=POS01A
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 LET VENDLEN=NUM(P2$(1,2))
0435 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1)),I[2]=NUM(I2$(7,2))
0445 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0450 LET I[6]=NUM(I2$(3,2)),M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$)
0460 CALL "SYC.VA","AR",INFO$[ALL]
0465 LET AR$=INFO$[20]
0470 CALL "SYC.VA","WO",INFO$[ALL]
0475 LET SF$=INFO$[20]
0500 REM " --- Initializations"
0510 DIM A0$(15),A1$(160),A[8],B0$(8),B1$(195),X0$(25)
0550 LET BEGREQ$="",ENDREQ$="",SEQUENCE$="V",BEGVEND$="",ENDVEND$=""
0560 LET DATETYPE$="O",BEGDATE$="",ENDDATE$="",PAGEBREAK$="N"
0600 REM " --- Additional File Opens"
0610 IF AR$="N" THEN GOTO 0630
0615 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0620 LET CUSTLEN=NUM(P6$(1,2)),FILES$[19]="ARM-01"
0630 IF SF$="N" THEN GOTO 0650
0635 LET FILES$[20]="WOE-01",FILES$[21]="WOE-22",FILES$[22]="WOE-32"
0650 CALL "SYC.DA",1,19,22,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0655 IF STATUS THEN GOTO 9900
0660 LET ARM01_DEV=CHANNELS[19],WOE01_DEV=CHANNELS[20]
0670 LET WOE22_DEV=CHANNELS[21],WOE32_DEV=CHANNELS[22]
0700 REM " --- Background
0710 FOR X=4 TO 20 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(17,3),"Beginning Requisition:",@(20,5),"Ending Requisition:",@(24,7),"Print Sequence:",@(22,9),"Beginning Vendor:",@(25,11),"Ending Vendor:",@(28,13),"Which Date:",@(24,15),"Beginning Date:",@(27,17),"Ending Date:",@(28,19),"Page Break:",'SF',
0790 GOSUB 5000
1000 REM " --- Beginning Requisition
1010 LET V4$="Enter A Valid Requisition Number (<F1>=First/<F3>=Lookup)"
1020 LET V0$="Z",V1$="KCE",V2$=BEGREQ$,V3$="",V0=7,V1=40,V2=3,I0=0
1030 GOSUB 7000
1040 ON V3 GOTO 1100,1150,1000,1050,3900
1050 CALL "POC.LB","R",V$
1060 IF LEN(V$)<15 THEN GOTO 1000
1070 LET V$=V$(9,7)
1100 IF V$="" THEN GOTO 1150
1110 LET BEGREQ$=V$
1140 GOTO 1180
1150 LET BEGREQ$="",V$="First"
1180 GOSUB 5100
1190 GOTO 3900
1200 REM " --- Ending Requisition
1210 LET V4$="Enter A Valid Requisition Number (<F1>=Last/<F3>=Lookup)"
1220 LET V0$="Z",V1$="KCE",V2$=ENDREQ$,V3$="",V0=7,V1=40,V2=5
1230 GOSUB 7000
1240 ON V3 GOTO 1300,1350,3900,1250,3900
1250 CALL "POC.LB","R",V$
1260 IF LEN(V$)<15 THEN GOTO 1200
1270 LET V$=V$(9,7)
1300 IF V$="" THEN GOTO 1350
1310 IF BEGREQ$<>"" THEN IF V$<BEGREQ$ THEN GOTO 1200
1320 LET ENDREQ$=V$
1340 GOTO 1380
1350 LET ENDREQ$="",V$="Last"
1380 GOSUB 5100
1390 GOTO 3900
1400 REM " --- Print Sequence
1410 LET V4$="A=Vendor Alternate Sequence   V=Vendor Sequence   R=Requisition Sequence"
1420 LET V0$="S",V1$="",V2$=SEQUENCE$,V3$="AVR",V0=1,V1=40,V2=7
1430 GOSUB 7000
1440 ON V3 GOTO 1450,1400,3900,1400,3900
1450 IF V$="" THEN GOTO 1400
1460 LET SEQUENCE$=V$
1480 GOSUB 5200
1490 GOTO 3900
1600 REM " --- Beginning vendor"
1610 LET V4$="Enter A Valid Vendor Number (<F1>=First/<F3>=Lookup)"
1620 LET V0$="M",V1$="KCE",V2$=BEGVEND$,V3$=M0$,V0=6,V1=40,V2=9
1630 GOSUB 7000
1640 ON V3 GOTO 1700,1750,3900,1650,3900
1650 CALL "SYC.LV",V$
1700 IF V$="" THEN GOTO 1750
1710 LET BEGVEND$=V$
1720 GOTO 1760
1750 LET BEGVEND$="",V$="",V4$="First"
1760 GOSUB 5300
1770 IF STATUS THEN GOTO 1600
1790 GOTO 3900
1800 REM " --- Ending vendor"
1810 LET V4$="Enter A Valid Vendor Number (<F1>=Last/<F3>=Lookup)"
1820 LET V0$="M",V1$="KCE",V2$=ENDVEND$,V3$=M0$,V0=6,V1=40,V2=11
1830 GOSUB 7000
1840 ON V3 GOTO 1900,1950,3900,1850,3900
1850 CALL "SYC.LV",V$
1900 IF V$="" THEN GOTO 1950
1910 IF BEGVEND$<>"" THEN IF V$<BEGVEND$ THEN GOTO 1800
1920 LET ENDVEND$=V$
1940 GOTO 1960
1950 LET ENDVEND$="",V$="",V4$="Last"
1960 GOSUB 5300
1970 IF STATUS THEN GOTO 1800
1990 GOTO 3900
2000 REM " --- Which Date?
2010 LET V4$="O=Date Ordered  P=Date Promised  R=Date Required  D=Don't Deliver Before Date"
2020 LET V0$="S",V1$="C",V2$=DATETYPE$,V3$="OPRD",V0=1,V1=40,V2=13
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 IF V$="" THEN GOTO 2000
2060 LET DATETYPE$=V$
2070 GOSUB 5400
2090 GOTO 3900
2200 REM " --- Beginning Date
2210 LET V4$="Enter The Beginning Date To Include On The Report (<F1>=First)"
2220 LET V0$="D",V1$="C6N",V2$=BEGDATE$,V3$="",V0=6,V1=40,V2=15
2230 GOSUB 7000
2240 ON V3 GOTO 2260,2250,3900,2200,3900
2250 LET V$=""
2260 LET BEGDATE$=V$,V4$="First"
2270 GOSUB 5500
2290 GOTO 3900
2400 REM " --- Ending Date
2410 LET V4$="Enter The Ending Date To Include On The Report (<F1>=Last)"
2420 LET V0$="D",V1$="C6N",V2$=ENDDATE$,V3$="",V0=6,V1=40,V2=17
2430 GOSUB 7000
2440 ON V3 GOTO 2460,2450,3900,2400,3900
2450 LET V$=""
2460 IF V$<>"" THEN IF V$<BEGDATE$ THEN GOTO 2400
2470 LET ENDDATE$=V$,V4$="Last"
2480 GOSUB 5500
2490 GOTO 3900
2600 REM " --- Page break?"
2610 LET V4$="Page Break Between "
2615 IF SEQUENCE$="R" THEN LET V4$=V4$+"Requisitions" ELSE LET V4$=V4$+"Vendors"
2620 LET V0$="Y",V1$="C",V2$=PAGEBREAK$,V3$="",V4$=V4$+" (Y/N)?",V0=1,V1=40,V2=19
2630 GOSUB 7000
2640 ON V3 GOTO 2650,2600,3900,2600,3900
2650 LET PAGEBREAK$=V$
2690 GOTO 3900
3900 REM " --- Next field"
3910 IF V3=4 THEN GOTO 3990
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,2200,2400,2600,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 IF V3=4 THEN GOTO 9900
4030 ON POS(V$="YN") GOTO 4000,4040,1000
4100 REM " --- Sort initializations"
4110 CALL "SYC.GA",7,1,"","",STATUS
4120 IF STATUS THEN GOTO 9900
4130 CALL "SYC.NB"," Sorting",18,COLUMN
4160 LET A0$(1)=N0$+BEGVEND$
4170 IF FNP$(BEGVEND$)<>"" THEN LET A0$(9)=BEGREQ$
4180 READ (POE01_DEV,KEY=A0$,DOM=4200)
4190 LET K$=KEYP(POE01_DEV,END=4900)
4195 GOTO 4220
4200 REM " --- Sort requisition file"
4210 LET K$=KEY(POE01_DEV,END=4900)
4220 IF POS(N0$=K$)<>1 THEN GOTO 4900
4230 IF ENDVEND$<>"" THEN IF K$(3,6)>ENDVEND$ THEN GOTO 4900
4240 IF ENDREQ$<>"" THEN IF K$(9,7)>ENDREQ$ THEN GOTO 4900
4250 READ (POE01_DEV,KEY=K$)IOL=POE01A; REM "This Key=K$ phrase is needed
4260 IF BEGREQ$<>"" THEN IF A0$(9,7)<BEGREQ$ THEN GOTO 4800
4270 IF A1$(35,1)="Y" THEN GOTO 4800
4280 PRINT @(COLUMN,11),FNF$(A0$(3,VENDLEN),M0$)," ",A0$(9,7),
4300 REM " --- Does record meet selection criteria?"
4310 IF BEGDATE$="" THEN IF ENDDATE$="" THEN GOTO 4400
4320 LET X=POS(DATETYPE$="OPDR"),X=11+((X-1)*6)
4330 IF FNP$(A1$(X,6))="" THEN GOTO 4400
4340 IF BEGDATE$<>"" THEN IF A1$(X,6)<BEGDATE$ THEN GOTO 4800
4350 IF ENDDATE$<>"" THEN IF A1$(X,6)>ENDDATE$ THEN GOTO 4800
4400 REM " --- Get vendor for alpha sort"
4410 IF SEQUENCE$<>"A" THEN GOTO 4500
4420 LET B0$(1)=A0$,B1$(1)=""
4430 FIND (APM01_DEV,KEY=B0$,DOM=4440)IOL=APM01A
4500 REM " --- Write sort record"
4510 LET X0$(1)=N0$
4520 IF SEQUENCE$="A" THEN LET X0$(3)=B1$(146,10)+A0$(3)
4530 IF SEQUENCE$="V" THEN LET X0$(13)=A0$(3)
4540 IF SEQUENCE$="R" THEN LET X0$(13)=A0$(9)+A0$(3,6)
4590 WRITE (POW07_DEV,KEY=X0$)IOL=POW07A
4800 REM " --- Loop back for next requisition"
4890 GOTO 4200
4900 REM " --- Run report overlay"
4910 PRINT @(COLUMN-9,11),FILL(27),@(COLUMN-9),'SB',"Printing",'SF',
4990 RUN "POR.AB"
5000 REM " --- Display"
5005 LET V1=40,V2=3,V$=BEGREQ$,V4$="First"
5010 GOSUB 5100
5015 LET V2=5,V$=ENDREQ$,V4$="Last"
5020 GOSUB 5100
5025 LET V2=7
5030 GOSUB 5200
5035 LET V2=9,V$=BEGVEND$,V4$="First",X=80-(V1+M0+1)
5040 GOSUB 5300
5045 LET V2=11,V$=ENDVEND$,V4$="Last"
5050 GOSUB 5300
5055 LET V2=13
5060 GOSUB 5400
5065 LET V2=15,V$=BEGDATE$,V4$="First"
5070 GOSUB 5500
5075 LET V2=17,V$=ENDDATE$,V4$="Last"
5080 GOSUB 5500
5085 PRINT @(V1,19),PAGEBREAK$,
5090 RETURN 
5100 REM " --- Display requisition"
5110 IF V$="" THEN LET V$=V4$
5120 PRINT @(V1,V2),V$,'CL',
5190 RETURN 
5200 REM " --- Display sequence"
5210 IF SEQUENCE$="A" THEN LET V4$="Vendor Alternate"
5220 IF SEQUENCE$="V" THEN LET V4$="Vendor"
5230 IF SEQUENCE$="R" THEN LET V4$="Requisition Number"
5240 PRINT @(V1,V2),SEQUENCE$," ",V4$," Sequence",'CL',
5290 RETURN 
5300 REM " --- Display vendor"
5310 IF V$="" THEN GOTO 5350
5315 LET B0$(1)=N0$+V$,B1$(1)="",STATUS=11
5320 FIND (APM01_DEV,KEY=B0$,DOM=5390)IOL=APM01A
5330 PRINT @(V1,V2),FNF$(B0$(3,VENDLEN),M0$)," ",B1$(1,MIN(X,30)),
5340 GOTO 5380
5350 PRINT @(V1,V2),V4$,'CL',
5380 LET STATUS=0
5390 RETURN 
5400 REM " --- Display type of date"
5410 IF DATETYPE$="O" THEN LET DATETITLE$="Date Ordered"
5420 IF DATETYPE$="P" THEN LET DATETITLE$="Date Promised"
5430 IF DATETYPE$="R" THEN LET DATETITLE$="Date Required"
5440 IF DATETYPE$="D" THEN LET DATETITLE$="Not Before Date"
5450 PRINT @(V1,V2),DATETYPE$," ",DATETITLE$,'CL',
5490 RETURN 
5500 REM " --- Display dates"
5510 IF V$="" THEN LET X$=V4$
5520 IF V$<>"" THEN LET X$=FNB6$(V$)
5530 PRINT @(V1,V2),X$,'CL',
5590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "SYC - Lookup for Op Code Sequence for a Bill of Materials"
0020 REM "Program BMC.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "SELECTION$: User selection (returned)"
0050 REM 
0080 SETERR 9000
0085 SETESC 9900
0090 ENTER SELECTION$
0100 REM " --- Open/Lock Files
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="BMM-03",FILES$[2]="BMM-08",FILES$[3]="SYM-06"
0140 LET FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0175 LET BMM03_DEV=CHANNELS[1],BMM08_DEV=CHANNELS[2],SYM06_DEV=CHANNELS[3]
0180 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLists
0210 BMM03A: IOLIST A0$(1),A1$
0220 BMM08A: IOLIST B0$
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0300 SYM06A: IOLIST *,COMPANY$
0310 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0405 DIM P[1]
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0425 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),SPEEDSEARCH$="N"
0440 LET X$=STBL("!SPEEDSEARCH",ERR=0450),N0$=X$(1,2),SPEEDSEARCH$="Y"
0450 FIND (SYM06_DEV,KEY=N0$,DOM=9800)IOL=SYM06A
0460 FIND (SYS01_DEV,KEY=N0$+"IV"+"00",DOM=9800)IOL=IVS01A
0470 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2))
0500 REM " --- Initializations
0505 LET BILL_NO$=SELECTION$,SELECTION$=""
0510 DIM OPTLIST$[2,MAXROW,5],PARAM$[2,MAXROW,3],A0$(25)
0540 LET WIDTH=P[0]+55,HEIGHT=12,WIN_X=1,WIN_Y=3,MAX_ROW=HEIGHT-5
0560 LET PGM$=PGM(-2),OPTIONS=0,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3
0570 LET OTITLE$="Method"
0620 REM " --- Window arrays
0625 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0630 LET HEADING$(2)="Seq",HEADING$(7)="Op Code",HEADING$(15)="Description"
0635 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0640 LET TITLE$="Operation Code Sequence Lookup"
0700 REM " --- Background
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Options
0810 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
0820 GOSUB 5500
0830 IF V3=4 OR V3=5 OR V3=6 THEN GOTO 9900
0890 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
0900 REM " --- Detail Window
0915 CALL "SYC.WB",NAME$,0
0925 PRINT @(0,0),'CF','SB','BR',HEADING$,'ER','SF',
0930 IF SPEEDSEARCH$<>"Y" THEN GOTO 1000
0950 REM " --- Display SpeedSearch Company
0955 DIM X$(WIDTH+SWIDTH)
0960 LET X$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
0965 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
0970 CALL "SYC.WA",0,WIDTH,3,WIN_X,WIN_Y+HEIGHT,"",CNAME$
0975 PRINT @(0,0),'BR',X$,'ER',
0980 CALL "SYC.WB",NAME$,0
1000 REM " --- Position file
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,5]
1020 LET PAGE=1,L=1,X1=1,X0=0,VENDOR$="",SELECTION$=""
1040 IF PREFIX$="*" THEN GOTO 1300
1200 REM " --- Bill Number
1210 READ (BMM03_DEV,KEY=N0$+BILL_NO$+START$,DOM=1400)IOL=BMM03A
1220 LET K$=A0$,X$=A0$(23)
1290 GOTO 1900
1300 REM " --- Keyword
1310 LET START$=FNU$(START$)
1320 READ (BMM03_DEV,KEY=N0$+BILL_NO$,DOM=1400)
1400 REM " --- Read next record
1410 LET K$=KEY(BMM03_DEV,END=3000)
1420 IF POS(N0$+BILL_NO$=K$)<>1 THEN GOTO 3000
1430 LET X$=K$(23,3)
1900 REM " --- Read bill master record
1905 LET LOCKED=0
1910 READ (BMM03_DEV,KEY=K$,ERR=1950)IOL=BMM03A
1915 IF PREFIX$<>"*" THEN GOTO 2000
1920 IF START$="" THEN GOTO 1935
1925 IF POS(START$=FNU$(A0$))>0 THEN GOTO 1935
1930 GOTO 2900
1935 LET X$=START$
1940 GOTO 2000
1950 REM " --- Error reading inventory master record
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1980 LET LOCKED=1
2000 REM " --- Display page?
2010 IF L<MAX_ROW THEN GOTO 2100
2020 LET MODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 0800
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2100 REM " --- Additional reads
2110 DIM B0$(90)
2120 READ (BMM08_DEV,KEY=N0$+A1$(1,3),DOM=2121)IOL=BMM08A
2140 IF POS(" "<>B0$(6,20))=0 THEN LET B0$(6,20)=A1$(5,60)
2400 REM " --- Lookup window record
2410 DIM G1$(WIDTH-2)
2415 IF LOCKED=1 THEN LET G1$(2)=K$(23,3)+" Record In Use"; GOTO 2470
2420 LET G1$(2)=A0$(23,3),G1$(9)=A1$(1,3),G1$(15)=B0$(6,20)
2480 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$(23,3)
2490 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record
2910 IF LOCKED=0 THEN GOTO 1400
2920 LET LOCKED=0
2930 LET K$=K$(1,LEN(K$)-1)+CHR(ASC(K$(LEN(K$),1))+1)
2940 READ (BMM03_DEV,KEY=K$,DOM=1400,END=3000)IOL=BMM03A
2990 GOTO 1420
3000 REM " --- End of file
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3035 LET MESSAGE$[0]="No Bills Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page
3510 LET MODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
4000 REM " --- All done
4090 GOTO 9900
5500 REM " --- Starting Position
5510 LET OWIDTH=10,OHEIGHT=3,OTITLE$="Seq"
5520 CALL "SYC.WA",0,OWIDTH,OHEIGHT,OWIN_X,OWIN_Y,OTITLE$,ONAME$
5540 LET V0$="Z",V1$="KCE",V2$="",V4$="",V0=3,V1=1,V2=0
5550 GOSUB 7000
5560 LET START$=V$
5590 RETURN 
6200 REM " --- Display page
6210 CALL "SYC.SA",MODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,5
6280 LET L=1,X1=1
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 CLOSE (BMM03_DEV,ERR=9911)
9920 CLOSE (SYS01_DEV,ERR=9921)
9922 CLOSE (BMM08_DEV,ERR=9923)
9925 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9930 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
9940 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9945 IF SELECTION$="END" THEN LET SELECTION$=""
9950 EXIT 
9999 END

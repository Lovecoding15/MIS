0010 REM "GLR - Budget Revision Register (Lead Overlay)"
0020 REM "Program GLR.RA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-01",FILES$[2]="GLM-08",FILES$[3]="SYS-01"
0160 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM01_DEV=CHANNELS[1],GLM08_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$(1),A1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 GLS01A: IOLIST P0$,P1$,P2$(1),P3$(1),P4$(1),M0$,M1$,M2$,M3$
0280 GLM08A: IOLIST C0$(1),C1$(1),C2$(1),C3$(1),C4$,C5$,C[ALL]
0400 REM " --- Parameters"
0410 DIM P2$(10),P3$(325),P4$(32),P[4]
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N3$=F5$
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0450 IF P4$(2,1)<>"Y" THEN GOTO 4900
0460 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),MP=13
0480 FOR X=0 TO 4
0485 LET P[X]=NUM(P2$(X*2+1,2),ERR=0490)
0490 NEXT X
0495 LET P[2]=FNYY_YEAR(P2$(5,2))
0500 REM " --- Initializations"
0510 DIM A0$(2+P[4]),A1$(40),C0$(4),C1$(64),C2$(P[4]),C3$(P[4]),C[8]
0590 LET M4$="##0.00%",OPTION$="C",ACTION$="R"
0600 REM " --- Channel Variables (Used by Overlays)"
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,7,0
0750 PRINT 'SB',@(25,3),"Revision Code:",@(22,4),"Amounts or Units:",@(27,5),"Description:",@(26,6),"Column Title:",@(14,8),"Beginning Account Number:",@(17,9),"Ending Account Number:",@(25,10),"Wildcard Mask:",@(23,11),"Rounding Factor:",@(16,12),"Update Current or Next:",'SF',
0900 REM " --- Position file"
0990 READ (GLM08_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Revision Code"
1005 PRINT 'CF',
1010 LET V2$="",V2$=KEY(GLM08_DEV,END=1040)
1020 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1030 IF V2$<>"" THEN LET C0$(3,1)=V2$(3,1),C0$(4,1)=V2$(4,1)
1040 LET V0$="S",V1$="C^",V2$=C0$(3,1),V3$="",V0=1,V1=40,V2=3,I0=0
1045 LET V4$="Enter The Revision Code (A through Z/<F3>=Lookup)"
1050 GOSUB 7000
1055 ON V3 GOTO 1080,1000,1000,1060,9900
1060 CALL "GLC.LA",ACTION$,SELECTION$,FLAGS$,DESCRIPTION$,TITLE$
1065 IF SELECTION$="" THEN GOTO 1000
1070 LET C0$(1,2)=N0$,C0$(3,1)=SELECTION$,C0$(4,1)=FLAGS$
1075 GOTO 1200
1080 IF POS(V$="ABCDEFGHIJKLMNOPQRSTUVWXYZ")=0 THEN GOTO 1000
1090 LET C0$(1,2)=N0$,C0$(3,1)=V$
1100 REM " --- Amounts or Units?"
1105 IF P4$(3,1)="Y" THEN GOTO 1120
1110 LET C0$(4,1)="A"
1115 GOTO 1200
1120 LET V4$="Amounts or Units (A/U)?"
1125 LET V0$="S",V1$="C^",V2$=C0$(4,1),V3$="",V0=1,V1=40,V2=4
1130 GOSUB 7000
1135 ON V3 GOTO 1140,1100,1000,1100,9900
1140 IF POS(V$="AU")=0 THEN GOTO 1100
1190 LET C0$(4,1)=V$
1200 REM " --- Retrieve record"
1210 FIND (GLM08_DEV,KEY=C0$,DOM=1000)IOL=GLM08A
1300 REM " --- Display record"
1310 LET OPTION$="C"
1380 GOSUB 5000
1390 GOTO 4000
2000 REM " --- Beginning Account Number"
2005 LET I0=2
2010 LET V4$="Enter A Beginning Account Number (<F1>=First/<F3>=Lookup)"
2015 LET V0$="M",V1$="C",V2$=FNP$(C2$),V3$=M0$,V0=P[4],V1=40,V2=8
2020 GOSUB 7000
2025 ON V3 GOTO 2060,2030,3900,2050,3900
2030 LET C2$(1)=""
2035 PRINT @(V1,V2),"First",'CL',
2040 GOTO 2090
2050 CALL "SYC.LG",V$
2055 IF V$="" THEN GOTO 2000
2060 IF V$="" THEN GOTO 2030
2065 GOSUB 5200
2070 IF STATUS>0 THEN GOTO 2000
2080 LET C2$(1)=A0$(3)
2085 PRINT @(V1,V2),DESCRIPTION$,'CL',
2090 GOTO 3900
2100 REM " --- Ending Account Number"
2110 LET V4$="Enter An Ending Account Number (<F1>=Last/<F3>=Lookup)"
2115 LET V0$="M",V1$="C",V2$=FNP$(C3$),V3$=M0$,V0=P[4],V1=40,V2=9
2120 GOSUB 7000
2125 ON V3 GOTO 2160,2130,3900,2150,3900
2130 LET C3$(1)=""
2135 PRINT @(V1,V2),"Last",'CL',
2140 GOTO 2190
2150 CALL "SYC.LG",V$
2155 IF V$="" THEN GOTO 2100
2160 IF V$="" THEN GOTO 2130
2163 IF V$<C2$(1) THEN GOTO 2100
2165 GOSUB 5200
2170 IF STATUS>0 THEN GOTO 2100
2180 LET C3$(1)=A0$(3)
2185 PRINT @(V1,V2),DESCRIPTION$,'CL',
2190 GOTO 3900
2200 REM " --- Wildcard Mask"
2210 LET V4$="Enter The Wildcard Mask To Be Used In Selecting Accounts For This Revision"
2215 LET V0$="W",V1$="C",V2$=C4$,V3$=M0$,V0=P[4],V1=40,V2=10
2220 GOSUB 7000
2225 ON V3 GOTO 2230,2200,3900,2200,3900
2230 IF V$<>"" THEN GOTO 2250
2235 LET C4$=""
2245 GOTO 2280
2250 LET C4$=V$
2280 PRINT @(V1,V2),C4$,'CL',
2290 GOTO 3900
2300 REM " --- Rounding Factor"
2310 LET V4$="Select The Rounding Factor To Use When Generating New Budget Amount"
2320 PRINT @(FNC(V4$,80),21),V4$,
2330 LET V4$="C=Cents   D=Dollars   H=Hundreds   T=Thousands"
2340 IF C0$(4,1)="U" THEN LET V4$="C=2 Decimal Places   D=Units   H=Hundreds   T=Thousands"
2350 LET V0$="S",V1$="C",V2$=C1$(61,1),V3$="CDHT",V0=1,V1=40,V2=11
2360 GOSUB 7000
2365 PRINT @(0,21),'CL',
2370 ON V3 GOTO 2380,2300,3900,2300,3900
2380 IF POS(V$="CDHT")=0 THEN GOTO 2300
2385 LET C1$(61,1)=V$
2390 GOTO 3900
2400 REM " --- Update Current Or Next?"
2410 LET V4$="Update Current Or Next Year's Budget (C=Current/N=Next)"
2420 LET V0$="S",V1$="C",V2$=OPTION$,V3$="CN",V0=1,V1=40,V2=12
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 IF POS(V$="CN")=0 THEN GOTO 2400
2460 LET OPTION$=V$,CURRENT$="Current Year"
2470 IF OPTION$="N" THEN LET CURRENT$="Next Year"
2480 PRINT @(V1,V2),OPTION$," ",CURRENT$,
2490 GOTO 3900
3900 REM " --- Next field"
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,2000,2100,2200,2300,2400,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V0$="S",V4$="Is The Above Information Correct (Y/N)?"
4020 LET V1$="KC",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Run report overlay"
4110 LET ACTION$="HI",PROGRAM$="GLR.IB",SOURCE$=PGM(-2),MENU$="GLU.JA"
4120 LET OFFSET=13,CHOICE$=C0$,AU$=C0$(4,1)
4125 LET BEGACCT$=FNP$(C2$),ENDACCT$=FNP$(C3$)
4130 LET ID$=C0$(3,1),BUDGET$="Budget "+$22$+ID$+$22$+" "+FNP$(C1$(1,30))
4140 IF AU$="A" THEN LET BUDGET$=BUDGET$+" (Amounts)"
4150 IF AU$="U" THEN LET BUDGET$=BUDGET$+" (Units)"
4160 CLOSE (GLM01_DEV,ERR=4170)
4170 CLOSE (GLM08_DEV,ERR=4180)
4180 CLOSE (SYS01_DEV,ERR=4190)
4190 RUN "GLR.AA"
4900 REM " --- Budgets not specified on parameter record"
4910 CALL "GLC.HA",STATUS
4990 GOTO 9900
5000 REM " --- Display"
5010 LET MASK$=M1$,BEGACCT$="First",ENDACCT$="Last"
5015 IF C1$(59,1)="P" THEN LET MASK$=M4$
5020 IF FNP$(C2$)="" THEN GOTO 5040
5025 LET V$=C2$
5030 GOSUB 5200
5035 LET BEGACCT$=DESCRIPTION$
5040 IF FNP$(C3$)="" THEN GOTO 5060
5045 LET V$=C3$
5050 GOSUB 5200
5055 LET ENDACCT$=DESCRIPTION$
5060 LET CURRENT$="Current Year"
5065 IF OPTION$="N" THEN LET CURRENT$="Next Year"
5070 PRINT @(40,3),C0$(3,1),@(40,4),C0$(4,1),@(40,5),C1$(1,30),@(40,6),C1$(31,15),@(40,8),BEGACCT$,'CL',@(40,9),ENDACCT$,'CL',@(40,10),C4$,@(40,11),C1$(61,1),@(40,12),OPTION$," ",CURRENT$,'CL',
5090 RETURN 
5200 REM " --- Retrieve and return G/L Account"
5210 DIM A1$(40),DESCRIPTION$(39)
5220 LET A0$(1)=N0$+V$,A1$(1,35)="(Not On File)"
5230 FIND (GLM01_DEV,KEY=A0$,DOM=5250)IOL=GLM01A
5250 LET DESCRIPTION$(1)=FNF$(A0$(3,P[3]),M0$)+" "+A1$(1,35)
5290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

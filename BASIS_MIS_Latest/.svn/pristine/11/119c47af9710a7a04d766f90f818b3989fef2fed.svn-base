0010 REM "IVC - Vendor Lookup By Item"
0020 REM "Program IVC.LV"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0042 REM "IVM03_DEV : IVM-03 channel (input)"
0044 REM "APM01_DEV : APM-01 channel (input)"
0046 REM "COLUMN    : Screen column (input)"
0048 REM "ROW       : Screen row (input)"
0050 REM "N0$       : Current firm (input)"
0052 REM "ITEM$     : Item number (input)"
0054 REM "VENDLEN   : Vendor number length (input)"
0056 REM "VENDMASK$ : Vendor mask (input)"
0058 REM "SELECTION$: Vendor number (Returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER IVM03_DEV,APM01_DEV,COLUMN,ROW,N0$,ITEM$,VENDLEN,VENDMASK$,SELECTION$
0200 REM " --- IOLIST's"
0210 APM01A: IOLIST A0$(1),A1$(1)
0220 IVM03H: IOLIST A0$(1),A1$(1)
0400 REM " --- Parameters"
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0500 REM " --- Initializations"
0510 DIM OPTLIST$[2,MAXROW,5],PARAM$[2,MAXROW,3],A0$(8),A1$(30)
0540 LET WIDTH=48,HEIGHT=12,WIN_X=COLUMN+2,WIN_Y=ROW+5,MAX_ROW=HEIGHT-5
0550 LET TITLE$="Vendors For Item "+FNP$(ITEM$),VENDMASK=LEN(VENDMASK$)
0560 IF WIN_X+HEIGHT>23 THEN LET WIN_X=22-HEIGHT
0570 IF WIN_Y+WIDTH>80 THEN LET WIN_Y=80-WIDTH
0600 REM " --- Window arrays"
0610 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0620 LET HEADING$(2)="Vendor",HEADING$(VENDMASK+3)="Name"
0630 LET FOOTING$(2)="F4=End  PgUp  PgDn"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,'ER','SF',
0730 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,1]
1020 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$="",FIRST$=N0$+"H"+ITEM$
1200 REM " --- Position File"
1210 READ (IVM03_DEV,KEY=FIRST$,DOM=1400)IOL=0210
1400 REM " --- Read next record"
1410 LET K$=KEY(IVM03_DEV,END=3000)
1420 IF POS(FIRST$=K$)<>1 THEN GOTO 3000
1430 READ (IVM03_DEV)
1900 REM " --- Retrieve vendor"
1910 LET A0$(1)=N0$+K$(24)
1920 FIND (APM01_DEV,KEY=A0$,DOM=2900)IOL=APM01A
2000 REM " --- Display page?"
2010 IF L<MAX_ROW THEN GOTO 2400
2020 LET MODE=5
2030 GOSUB 6200
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2400 REM " --- Lookup window record"
2410 DIM G1$(WIDTH-2)
2420 LET G1$(2)=FNF$(A0$(3,VENDLEN),VENDMASK$),G1$(VENDMASK+3)=A1$(1,30)
2430 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$(3,6)
2490 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2990 GOTO 1400
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET MESSAGE$[0]="No vendors found (<Enter>=Continue)"
3040 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3050 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 4000
3500 REM " --- Display last page"
3510 LET MODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",MODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9910 IF FKEY=4 THEN LET SELECTION$="END"
9940 CALL "SYC.WD",NAME$
9945 IF SELECTION$="END" THEN LET SELECTION$=""
9950 EXIT 
9999 END

0010 REM "DDX - Data Dictionary Resequence Detail Lines
0020 REM "Program:  DDX.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="DDM-01",FILES$[2]="DDM-03",FILES$[3]="DDM-04"
0140 LET FILES$[4]="DDM-06",FILES$[5]="DDM-14",FILES$[6]="DDW-04"
0150 LET FILES$[7]="DDW-14",FILES$[8]="SYM-03",FILES$[9]="SYS-01"
0160 LET OPTIONS$[6]="CL",OPTIONS$[7]="CL"
0170 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0175 IF STATUS>0 THEN GOTO 9900
0180 LET DDM01_DEV=CHANNELS[1],DDM03_DEV=CHANNELS[2],DDM04_DEV=CHANNELS[3]
0185 LET DDM06_DEV=CHANNELS[4],DDM14_DEV=CHANNELS[5],DDW04_DEV=CHANNELS[6]
0190 LET DDW14_DEV=CHANNELS[7],SYM03_DEV=CHANNELS[8],SYS01_DEV=CHANNELS[9]
0200 REM " --- IOLists
0230 DDM03A: IOLIST X0$,X1$(1)
0250 SYM03A: IOLIST X0$,X1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="DD",N4$=F5$
0500 REM " --- Init Data
0510 DIM X1$(63)
0700 REM " --- Background
0710 FOR X=4 TO 8 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(28,3),"File Name:",@(28,5),"Record ID:",@(24,7),"Resequence By:",'SF'
1000 REM " --- File Name
1020 LET V0$="S",V1$="C",V2$=R0$,V3$="",V4$="Enter Valid File Name, <F3>=Lookup",V0=6,V1=39,V2=3,I0=0
1030 GOSUB 7000
1040 ON V3 GOTO 1060,1000,1000,1050,9900
1050 GOSUB 6100
1060 LET V$=V$+FILL(6),V$=V$(1,6)
1070 FIND (SYM03_DEV,KEY=V$,DOM=1000)IOL=SYM03A
1080 PRINT @(V1,V2),'CL',V$,"  ",X1$(1,30),
1085 LET R0$=V$
1090 GOTO 3900
1200 REM " --- Record ID
1220 LET V0$="S",V1$="C",V2$=R1$,V3$="",V4$="Enter Valid Record ID, <F3>=Lookup",V0=1,V1=39,V2=5
1230 GOSUB 7000
1240 ON V3 GOTO 1260,1200,1290,1250,9900
1250 GOSUB 6200
1260 LET V$=V$+FILL(1),V$=V$(1,1)
1270 FIND (DDM03_DEV,KEY=R0$+V$,DOM=1200)IOL=DDM03A
1280 PRINT @(V1,V2),'CL',V$,"  ",X1$(1,30),
1285 LET R1$=V$
1290 GOTO 3900
1400 REM " --- Resequence Factor"
1410 IF R3=0 THEN LET R3=10
1420 LET V0$="N",V1$="CE",V2$=STR(R3),V3$="##0",V4$="Enter Number To Resequence By",V0=3,V1=39,V2=7
1430 GOSUB 7000
1440 ON V3 GOTO 1460,1400,1490,1400,1490
1460 IF V<1 OR MOD(V,1)<>0 THEN GOTO 1400
1470 LET R3=V
1480 PRINT @(V1,V2),'CL',R3:V3$,
1490 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,4000
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 IF V3=4 THEN GOTO 9900
4030 ON POS(V$="YN") GOTO 4000,4040,1000
4100 RUN "DDX.BB"
6100 REM " --- File Name Lookup"
6110 LET CHANNEL=SYM03_DEV,RECORD$="",NUMBER=1,TITLE$="ADD+ON System Files"
6120 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6130 LET DESCRIPTION$[0]="File",DESCRIPTION$[1]="Description"
6140 LET FIELD[0]=1,POSITION[0]=1,LENGTH[0]=6
6150 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6160 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6170 LET V$="NO COMPANY",ROW=V2+2
6180 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6190 RETURN 
6200 REM " --- Record ID Lookup"
6210 LET CHANNEL=DDM03_DEV,RECORD$=R0$,NUMBER=1,TITLE$=R0$+" Record ID's"
6220 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6230 LET DESCRIPTION$[0]="ID",DESCRIPTION$[1]="Description"
6240 LET FIELD[0]=1,POSITION[0]=7,LENGTH[0]=1
6250 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6260 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6270 LET V$="NO COMPANY & SKIP",ROW=L0
6280 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

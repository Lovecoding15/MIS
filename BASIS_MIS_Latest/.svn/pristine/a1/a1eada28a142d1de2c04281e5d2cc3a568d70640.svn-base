0010 REM "IVR - Lot/Serial Transactin History By Item Report (Input Overlay)"
0020 REM "Program IVR.7A"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=11
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="IVM-01",FILES$[2]="IVM-03",FILES$[3]="IVM-07"
0130 LET FILES$[4]="IVM-10",FILES$[5]="IVM-14",FILES$[6]="IVM-15"
0135 LET FILES$[7]="IVT-01",FILES$[8]="SYS-01"
0160 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET IVM01_DEV=CHANNELS[1],IVM03_DEV=CHANNELS[2],IVM07_DEV=CHANNELS[3]
0185 LET IVM10_DEV=CHANNELS[4],IVM14_DEV=CHANNELS[5],IVM15_DEV=CHANNELS[6]
0190 LET IVT01_DEV=CHANNELS[7],SYS01_DEV=CHANNELS[8]
0200 REM " --- IOLists
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 ARS01A: IOLIST *,*,X$,*,*,X9$
0290 APS01A: IOLIST *,*,X$,*,*,X7$
0300 GLS01A: IOLIST G0$,G1$,G2$,G3$,G4$,G5$,G6$,G7$,G8$
0400 REM " --- Parameters"
0405 DIM P[7],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N3$=F5$
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0425 IF POS(P3$(17,1)="SL")=0 THEN GOTO 4900
0430 CALL "SYC.VA",N2$,INFO$[ALL]
0435 LET GL$=INFO$[9]
0440 CALL "SYC.VA","AR",INFO$[ALL]
0445 LET AR$=INFO$[20]
0450 CALL "SYC.VA","AP",INFO$[ALL]
0455 LET AP$=INFO$[20],P[0]=NUM(P2$(1,2))
0460 LET P[1]=NUM(P2$(3,2)),P[2]=NUM(P2$(5,1)),P[3]=NUM(P2$(7,2))
0465 LET P[5]=NUM(P2$(9,2)),P[6]=NUM(P2$(11,2)),P[7]=NUM(P2$(13,2))
0470 IF AR$<>"Y" THEN GOTO 0485
0475 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0480 LET X8=NUM(X$(1,2)),X9=LEN(X9$),FILES$[10]="ARM-01"
0485 IF AP$<>"Y" THEN GOTO 0495
0487 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0490 LET X6=NUM(X$(1,2)),X7=LEN(X7$),FILES$[9]="APM-01"
0495 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0500 REM " --- Init Data"
0510 LET H0=60,H1=115,H=0,H1$=N1$,H2$=N3$
0520 LET OPEN_LIST$="Open  ClosedBoth  ",OCB$="B",ALL$="Y",TOTALS$="Y"
0530 DIM A[1],U[1],FILES[15],PARAMS$[4],OPTION$[7],TTYPE$(18)
0535 LET FILES[0]=SYS01_DEV,FILES[1]=IVM01_DEV,FILES[10]=IVM10_DEV
0540 LET FILES[14]=IVM14_DEV,FILES[15]=IVM15_DEV,PARAMS$[0]=N0$
0545 LET PARAMS$[1]=P1$,PARAMS$[2]=P2$,PARAMS$[3]=P3$,PARAMS$[4]=P4$
0550 CALL "SYC.RM",M1$,M1,10,15
0555 CALL "SYC.RM",M2$,M2,9,12
0560 CALL "SYC.RM",M3$,M3,10,12
0565 DIM INFO$[20]
0570 CALL "SYC.VA","AP",INFO$[ALL]
0575 LET AP$=INFO$[20]
0580 CALL "SYC.VA","AR",INFO$[ALL]
0585 LET AR$=INFO$[20]
0590 CALL "SYC.VA","GL",INFO$[ALL]
0595 LET GL$=INFO$[20]
0600 REM " --- Additional Opens"
0610 IF GL$="Y" THEN LET FILES$[11]="GLM-01"
0620 CALL "SYC.DA",1,9,11,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS>0 THEN GOTO 9900
0640 LET APM01_DEV=CHANNELS[9],ARM01_DEV=CHANNELS[10],GLM01_DEV=CHANNELS[11]
0700 REM " --- Background"
0710 PRINT @(24,3),'SB',"Beginning Date:",@(27,4),"Ending Date:",'SF',
0720 CALL "IVC.IB","B","D",5,FILES[ALL],PARAMS$[ALL],OPTION$[ALL],V3
0730 PRINT @(22,12),'SB',"Transaction Type:",@(18,13),"Open, Closed Or Both:",@(24,14),"Totals By Item:",'SF',
0900 REM 
0990 GOSUB 5000
1000 REM " --- Beginning Date"
1020 LET V0$="D",V1$="C",V2$=BEG_DATE$,V3$="",V4$="Enter Beginning Transaction Date (<F1>=First)",V0=8,V1=40,V2=3,I0=0
1040 GOSUB 7000
1060 IF V3=1 OR V$="" THEN LET BEG_DATE$=""; PRINT @(V1,V2),'CL',"First"; GOTO 1090
1080 LET BEG_DATE$=V$
1090 GOTO 3900
1100 REM " --- Ending Date"
1120 LET V0$="D",V1$="C",V2$=END_DATE$,V3$="",V4$="Enter Ending Transaction Date (<F1>=Last)",V0=8,V1=40,V2=4
1140 GOSUB 7000
1160 IF V3=1 OR V$="" THEN LET END_DATE$=""; PRINT @(V1,V2),'CL',"Last"; GOTO 1190
1170 LET END_DATE$=V$
1180 IF END_DATE$<>"" THEN IF END_DATE$<BEG_DATE$ THEN GOTO 1100
1190 GOTO 3900
1200 REM " --- Entry Call"
1220 CALL "IVC.IB","I","D",5,FILES[ALL],PARAMS$[ALL],OPTION$[ALL],V3
1230 IF V3=2 THEN GOTO 1280
1240 LET BEG_WHSE$=OPTION$[1],END_WHSE$=OPTION$[2],BEG_PROD$=OPTION$[3]
1250 LET END_PROD$=OPTION$[4],BEG_ITEM$=OPTION$[5],END_ITEM$=OPTION$[6]
1260 LET ITEM_TYPE$=OPTION$[7]
1270 IF V3=4 THEN GOTO 4000
1280 GOTO 3900
1300 REM " --- Type"
1305 FOR X=0 TO 8
1310 IF X=0 THEN GOSUB 6000
1320 LET TEMP$=TTYPE$(X*2+1,2)
1330 LET V0$="S",V1$="C",V2$=TEMP$,V3$="  POOPITPHTITOWIWOBM",V0=2,V1=40+(X*3),V2=12
1335 LET V4$="Enter The Transaction Type To Include On This Report (<F1>=All/<F4>=End)"
1340 GOSUB 7000
1345 IF V3=4 THEN LET V3=0,ALL$="N"; EXITTO 1385
1350 IF V3=1 THEN PRINT @(40,12),"All Transactions Selected",'CL',; DIM TTYPE$(18); EXITTO 1385
1355 IF V3=2 THEN IF X>0 THEN LET X=X-1; GOTO 1320 ELSE EXITTO 1385
1360 IF FNP$(V$)="" THEN LET TTYPE$=TTYPE$(1,X*2)+TTYPE$(X*2+3)+"  ",ALL$="N"; GOSUB 6000; GOTO 1320
1365 IF POS(V$=TTYPE$,2)<>0 AND POS(V$=TTYPE$,2)<>X*2+1 THEN GOTO 1320
1370 LET TTYPE$(X*2+1,2)=V$
1375 IF ALL$="Y" THEN PRINT @(40+(X*3)+2,12),'CL',; LET ALL$="N"
1380 NEXT X
1385 IF FNP$(TTYPE$)=TTYPE$ THEN LET ALL$="Y" ELSE IF FNP$(TTYPE$)="" THEN LET ALL$="Y"
1390 FOR X=18 TO 20; PRINT @(0,X),'CL',; NEXT X
1395 GOTO 3900
1400 REM " --- Open/Closed/Both"
1410 LET V0$="S",V1$="C",V2$=OCB$,V3$="OCB",V4$="O=Open Only   C=Closed Only   B=Both",V0=1,V1=40,V2=13
1420 GOSUB 7000
1440 PRINT @(V1+V0+1,V2),OPEN_LIST$((POS(V$="OCB")-1)*6+1,6),'CL',
1460 LET OCB$=V$
1490 GOTO 3900
1500 REM " --- Totals by Item"
1510 IF ALL$="N" THEN PRINT @(40,14),"N/A",; GOTO 1590
1520 PRINT @(40,14),'CL',
1530 LET V0$="Y",V1$="C",V2$=TOTALS$,V3$="",V4$="Y=Total By Item     N=Total By Lot/Serial Number",V0=1,V1=40,V2=14
1540 GOSUB 7000
1550 LET TOTALS$=V$
1590 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 3990
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,1200,1300,1400,1500,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4030 ON POS(V$="YN") GOTO 4000,4050,1000
4100 REM " --- Run report overlay"
4190 RUN "IVR.7B"
4900 REM " --- Lotted/Serialized Inventory not enabled"
4910 CALL "IVC.SB","L",STATUS
4990 GOTO 9900
5000 REM " --- Display Screen"
5010 IF FNP$(BEG_DATE$)<>"" THEN PRINT @(40,3),FNB$(BEG_DATE$), ELSE PRINT @(40,3),"First",'CL',
5020 IF FNP$(END_DATE$)<>"" THEN PRINT @(40,4),FNB$(END_DATE$), ELSE PRINT @(40,4),"Last",'CL',
5030 CALL "IVC.IB","D","D",5,FILES[ALL],PARAMS$[ALL],OPTION$[ALL],V3
5040 GOSUB 6040
5050 PRINT @(40,13),OCB$," ",OPEN_LIST$((POS(OCB$="OCB")-1)*6+1,6),'CL',
5060 IF ALL$="N" THEN PRINT @(40,14),"N/A", ELSE PRINT @(40,14),TOTALS$,'CL',
5090 RETURN 
6000 REM " --- Display Transaction Types"
6010 PRINT @(4,18),"OP=Order Processing     PO=Purchase Orders       IT=Inventory Transactions"
6020 PRINT @(4,19),"PH=Physical Inventory   TI=Transfers In          TO=Transfers Out"
6030 PRINT @(4,20),"WI=Work Order Issues    WO=Work Order Receipts   BM=Bill of Materials"
6040 PRINT @(40,12),'CL',
6050 IF FNP$(TTYPE$)="" THEN PRINT @(40,12),"All Transactions Selected",'CL',; GOTO 6090
6060 FOR X0=0 TO 8
6070 PRINT @(40+(X0*3),12),TTYPE$(X0*2+1,2),
6080 NEXT X0
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9060 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9120 RETRY
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "GLM - G/L Account Maintenance (Summary Inquiry)"
0020 REM "Program GLM.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0210 GLM02A: IOLIST X0$(1),A[ALL],U[ALL]
0500 REM " --- Initializations"
0510 DIM X0$(3+P[4]),A[MP],U[MP]
0550 LET HISTORY=0,I0=0,TP=P[0]+1
0560 LET L1=L0+2,WIDTH=17,OFFSET=12,FIELD=WIDTH-2
0570 LET M9$=M1$,M9=MIN(LEN(M1$),14)
0590 CALL "SYC.RM",M9$,X,M9,14
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,L0+1,0
0720 FOR X=OFFSET-1 TO WIDTH*3+(OFFSET-1) STEP WIDTH
0725 CALL "SYC.WC",1,1,0,1,X,L0,0
0730 NEXT X
0735 FOR X=OFFSET-1 TO WIDTH*3+(OFFSET-1) STEP WIDTH
0740 CALL "SYC.WC",1,1,0,15,X,L0+2,0
0745 NEXT X
0750 PRINT 'SB',@(1,L0),"Period",@(1,7),"Beginning",@(1,8+P[0]),"Ending",'SF',
0755 FOR X=1 TO P[0]
0760 LET Y=7+X
0765 PRINT 'SB',@(1,Y),X:"##-",P3$((X-1)*3+235,3),'SF',
0770 NEXT X
1000 REM " --- Retrieve and display record(s)"
1010 LET MC=LEN(FNP$(COLUMNS$))
1020 DIM VALUES[TP,MC]
1030 FOR NC=1 TO 4
1040 DIM X0$(3+P[4]),A[MP],U[MP]
1050 LET X0$(1)=A0$+COLUMNS$(NC,1)
1060 FIND (GLM02_DEV,KEY=X0$,DOM=1070)IOL=GLM02A
1080 GOSUB 5000
1090 NEXT NC
1300 REM 
1310 LET XLIMIT=TP*MC,HISTORY=0
1390 GOTO 4000
2000 REM " --- Amount"
2010 LET V=I0,NC=INT(V/TP)+1,NP=V-(NC-1)*TP,V3=0
2020 IF HISTORY>0 THEN GOTO 2100
2030 IF POS(COLUMNS$(NC,1)="024")>0 THEN GOTO 3900
2100 REM 
2110 IF TYPES$(NC,1)="A" THEN LET MASK$=M9$,MASK=LEN(MASK$)
2120 IF TYPES$(NC,1)="U" THEN LET MASK$=M2$,MASK=LEN(MASK$)
2130 LET V0$="N",V1$="C",V3$=MASK$,V4$="Enter A Valid Amount (<F1>=Next Column"
2135 IF NP=1 THEN IF POS(COLUMNS$(NC,1)="024")=0 THEN LET V4$=V4$+"/<F3>=Duplicate Period 1 Amount"
2140 LET V0=MASK,V9=(NC-1)*WIDTH+OFFSET,V1=V9+FIELD-MASK,V2=L1+NP
2145 LET V2$=STR(VALUES[NP,NC]),V4$=V4$+")"
2150 GOSUB 7000
2160 ON V3 GOTO 2170,2200,3900,2165,3900
2165 IF NP=1 THEN IF POS(COLUMNS$(NC,1)="024")=0 THEN LET VALUES[1,NC]=V; GOSUB 6000
2170 LET VALUES[NP,NC]=V
2175 IF TYPES$(NC,1)="A" THEN IF M1$<>M9$ THEN PRINT @(V9+FIELD-M1,V2),VALUES[NP,NC]:M1$,
2180 GOSUB 5200
2190 GOTO 3900
2200 REM " --- Next column"
2210 LET NP=NP+1,I0=I0+1
2220 IF NP<=TP THEN GOTO 2200
2230 IF I0<XLIMIT THEN LET I0=I0-1; GOTO 2000
2240 LET I0=0
2290 GOTO 2000
3900 REM " --- Next field"
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3970 IF I0<XLIMIT THEN GOTO 2000
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/<F1>=Change Columns)?"
4015 LET V0$="S",V1$="KSC",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
4020 IF HISTORY$<>"Y" THEN LET V0$="Y",V1$="C"
4025 LET HISTORY=0,I0=0
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4500,4000,4000,4900
4080 IF V$="HISTORY" THEN GOTO 4800
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4110 FOR NC=1 TO MC
4120 DIM X0$(3+P[4]),A[MP],U[MP]
4130 LET X0$(1)=A0$+COLUMNS$(NC,1)
4140 FIND (GLM02_DEV,KEY=X0$,DOM=4200)IOL=GLM02A
4200 REM " --- Move amounts/units"
4210 FOR NP=0 TO P[0]
4220 IF TYPES$(NC,1)="A" THEN LET A[NP]=VALUES[NP,NC]
4230 IF TYPES$(NC,1)="U" THEN LET U[NP]=VALUES[NP,NC]
4250 NEXT NP
4290 WRITE (GLM02_DEV,KEY=X0$)IOL=GLM02A
4300 REM 
4380 NEXT NC
4390 GOTO 4900
4500 REM " --- Change display columns"
4510 LET V2=8
4550 CALL "GLC.SA",SYS01_DEV,GLM08_DEV,V2,COLHDR$,VALID$,COLUMNS$,TYPES$
4590 GOTO 1000
4800 REM " --- History"
4810 LET HISTORY=1
4890 GOTO 2000
4900 REM " --- Return to submenu"
4990 RUN MENU$,ERR=9900
5000 REM " --- Display"
5010 DIM TITLE$(16),MASK$(16)
5015 IF NC>MC THEN GOTO 5300
5020 LET V1$=COLUMNS$(NC,1),V2$=TYPES$(NC,1)
5030 CALL "GLC.FA",GLM08_DEV,N0$,V1$,V2$,X$,H$,STATUS
5040 LET BALANCE=0,V1=(NC-1)*WIDTH+OFFSET,TITLE$(16-LEN(H$))=H$
5050 IF V2$="A" THEN LET MASK$=M1$,MASK=LEN(MASK$)
5060 IF V2$="U" THEN LET MASK$=M2$,MASK=LEN(MASK$)
5070 IF STATUS>0 THEN LET MASK$=J1$(1,MASK)
5080 PRINT @(V1,L0),TITLE$,
5100 REM 
5110 FOR NP=0 TO P[0]
5120 IF V2$="A" THEN LET AMOUNT=A[NP]
5125 IF V2$="U" THEN LET AMOUNT=U[NP]
5130 IF NP=0 THEN LET BALANCE=AMOUNT
5135 IF NP>0 THEN LET BALANCE=BALANCE+AMOUNT
5140 LET X$=MASK$
5145 IF STATUS=0 THEN LET X$=STR(AMOUNT:MASK$)
5150 PRINT @(V1+FIELD-MASK,L1+NP),X$
5155 LET VALUES[NP,NC]=AMOUNT
5160 NEXT NP
5170 LET VALUES[TP,NC]=BALANCE,X$=MASK$
5175 IF STATUS=0 THEN LET X$=STR(BALANCE:MASK$)
5180 PRINT @(V1+FIELD-MASK,L1+TP),X$
5190 RETURN 
5200 REM " --- Redisplay totals if changed"
5210 LET BALANCE=0
5220 FOR NP=0 TO P[0]
5230 LET BALANCE=BALANCE+VALUES[NP,NC]
5240 NEXT NP
5250 IF BALANCE=VALUES[TP,NC] THEN GOTO 5290
5260 PRINT @(V1,L1+TP),BALANCE:MASK$,
5270 LET VALUES[TP,NC]=BALANCE
5290 RETURN 
5300 REM " --- Blank Column"
5310 LET V1=(NC-1)*WIDTH+OFFSET
5320 PRINT @(V1,L0),MASK$,
5330 FOR NP=0 TO P[0]
5340 PRINT @(V1,L1+NP),MASK$,
5350 NEXT NP
5360 PRINT @(V1,L1+TP),MASK$
5390 RETURN 
6000 REM " --- Duplicate budget amount"
6010 LET AMOUNT=VALUES[1,NC],V2$=TYPES$(NC,1),V1=(NC-1)*WIDTH+OFFSET
6015 IF V2$="A" THEN LET A[0]=VALUES[0,NC],A[1]=VALUES[1,NC],MASK$=M1$
6020 IF V2$="U" THEN LET U[0]=VALUES[0,NC],U[1]=VALUES[1,NC],MASK$=M2$
6025 FOR X=2 TO P[0]
6030 LET VALUES[X,NC]=AMOUNT
6035 IF V2$="A" THEN LET A[X]=AMOUNT
6040 IF V2$="U" THEN LET U[X]=AMOUNT
6045 NEXT X
6050 LET NUMBER=NP,STATUS=0,MASK=LEN(MASK$)
6060 GOSUB 5100
6070 LET NP=NUMBER
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

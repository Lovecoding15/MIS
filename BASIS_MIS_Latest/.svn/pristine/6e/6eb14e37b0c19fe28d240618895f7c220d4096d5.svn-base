0010 REM "WOS - Close Recurring Work Orders - Parameters               "
0020 REM "Program:  WOU.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Mar99 KJS: Y2K year processing (470,485)
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open Lock/Files "
0105 LET FILES=8
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-03",FILES$[2]="WOE-01",FILES$[3]="WOT-01"
0120 LET FILES$[4]="WOT-21",FILES$[5]="WOT-31",FILES$[6]="WOE-04"
0125 LET FILES$[7]="SYS-01",FILES$[8]="WOM-10"
0150 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE03_DEV=CHANNELS[1],WOE01_DEV=CHANNELS[2],WOT01_DEV=CHANNELS[3]
0165 LET WOT21_DEV=CHANNELS[4],WOT31_DEV=CHANNELS[5],WOE04_DEV=CHANNELS[6]
0170 LET SYS01_DEV=CHANNELS[7],WOM10_DEV=CHANNELS[8]
0200 REM " --- IOLISTS "
0210 WOM10A: IOLIST X0$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 WOS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters "
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[2],G[4]
0460 FOR X=0 TO 4
0470 LET G[X]=NUM(G2$(X*2+1,2),ERR=0471)
0480 NEXT X
0485 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P4$(1,2)),P[2]=FNYY_YEAR(P4$(3,2))
0500 REM " --- Initialize Data "
0510 LET H0=60,H1=120,H=0,H1$=N1$,H2$=N3$
0520 LET L9=50,L=L9+1
0530 DIM J$(80,"-")
0550 LET M0=LEN(M0$),M1=LEN(M1$),G5=LEN(G5$)
0700 REM " --- Background "
0710 FOR X=4 TO 5 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(16,3),"Period <1 Thru "+STR(P[1]:"##")+"/End>: ",'SF',@(0,22),'CL',
1000 REM " --- Options "
1010 SETERR 9000
1020 LET V0$="S",V1$="E",V2$="",V3$="",V4$="",V0=3,V1=40,V2=3
1030 GOSUB 7000
1040 IF V3=4 THEN GOTO 9900
1060 LET I=NUM(V$,ERR=1000)
1080 IF I<1 OR I>P[1] THEN GOTO 1000
1100 REM " --- Determine Period Limits "
1120 LET V=I
1140 CALL "SYC.PA",SYS01_DEV,I,P[2],V0$,V9$,STATUS
1150 IF STATUS THEN GOTO 9900
1160 LET P8$=V0$,P9$=V9$,P9=I
1170 LET H3$="Period "+STR(I)+"  From "+FNB$(V0$)+" To "+FNB$(V9$)
1180 PRINT @(40,3),H3$(8),
1200 REM " --- Correct? "
1220 LET V4$="Close All Recurring Work Orders For This Period: "
1230 CALL "SYC.YN",1,V4$,0,V$,V3
1232 PRINT @(0,5),'CL',
1240 IF V3=4 THEN GOTO 9900
1250 IF V$="NO" THEN GOTO 1000
1260 IF V$<>"YES" THEN GOTO 1200
4000 REM " --- Overlay "
4020 LET T0$=""
4100 READ (WOM10_DEV,KEY=N0$+"A",DOM=4120)
4120 READ (WOM10_DEV,END=4200)IOL=WOM10A
4130 IF POS(N0$=X0$)<>1 THEN GOTO 4200
4140 IF X0$(26,1)="R" THEN LET T0$=T0$+X0$(4,2)
4160 GOTO 4120
4200 RUN "WOU.DB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

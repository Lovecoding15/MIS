0010 REM "ARR - NAME LISTING"
0020 REM "Program ARR.RA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ARM-04",FILES$[3]="ARM-10"
0135 LET FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET ARM01_DEV=CHANNELS[1],ARM04_DEV=CHANNELS[2],ARM10_DEV=CHANNELS[3]
0190 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLists
0230 ARM10A: IOLIST X0$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$,WHEN$=F0$(7,3)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0450 DIM P[2]
0480 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0500 REM " --- Init Data
0510 LET H=0,H1$=N1$,H2$=N3$,L9=59,L=L9+1,WIDTH=132
0520 DIM J$(80,"-"),J0$(20,"0"),J1$(20," ")
0530 LET M9$="(###)-000-0000"
0700 REM " --- Background
0710 FOR X=4 TO 10 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(23,3),"Report Sequence:",@(25,5),"Starting Code:",@(27,7),"Ending Code:",'SF',
1000 REM " --- Type
1010 PRINT 'CF',
1020 LET V0$="S",V1$="KE",V2$="M",V3$="MST",V4$=" M=Terms Code,  S=Salesperson,  T=Territory",V0=1,V1=40,V2=3
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1060 LET P7$=V$
1090 PRINT @(0,22),'CL',
1200 REM " --- Starting Number
1210 LET I9$=""
1220 LET V0$="S",V1$="C",V2$=I1$,V3$="",V4$="<F1>=First",V0=3,V1=40,V2=5
1230 PRINT @(V1,V2),'CL'
1240 GOSUB 7000
1250 IF V3=2 THEN GOTO 1000
1255 IF V3=1 THEN LET V$=""; GOTO 1280
1260 IF V$<>"" THEN IF P7$="M" THEN LET V$=V$+"  "; LET V$=V$(1,2)
1265 IF V$<>"" THEN IF P7$<>"M" THEN LET V$=V$+"   "; LET V$=V$(1,3)
1270 GOSUB 1500
1274 IF VALID$="N" THEN GOTO 1200
1276 PRINT @(V1,V2),V$," ",DISPLAY$,'CL',
1280 LET I1$=V$
1290 IF V$="" THEN PRINT @(40,5),"First",'CL',; LET I9$="First"
1300 REM " --- Ending Number
1320 LET V0$="S",V1$="C",V2$=I2$,V3$="",V4$="<F1>=Last",V0=3,V1=40,V2=7
1330 PRINT @(V1,V2),'CL'
1340 GOSUB 7000
1350 IF V3=2 THEN GOTO 1200
1355 IF V3=1 THEN LET V$=""; GOTO 1380
1357 IF V$<>"" THEN IF P7$="M" THEN LET V$=V$+"  "; LET V$=V$(1,2)
1358 IF V$<>"" THEN IF P7$<>"M" THEN LET V$=V$+"   "; LET V$=V$(1,3)
1360 IF V$<>"" THEN IF I1$>V$ THEN GOTO 1300
1370 GOSUB 1500
1374 IF VALID$="N" THEN GOTO 1300
1376 PRINT @(V1,V2),V$," ",DISPLAY$,'CL',
1380 LET I2$=V$
1390 IF V$="" THEN PRINT @(40,7),"Last",'CL',
1395 GOTO 4000
1500 REM " --- Verify Terms Code/Salesperson/Territory"
1510 LET VALID$="N"
1520 IF P7$="M" THEN LET V5$="A" ELSE IF P7$="S" THEN LET V5$="F" ELSE LET V5$="H"
1530 FIND (ARM10_DEV,KEY=N0$+V5$+V$,DOM=1590)IOL=ARM10A
1540 LET VALID$="Y"
1550 IF P7$="M" THEN LET DISPLAY$=X0$(6,20) ELSE LET DISPLAY$=X0$(7,20)
1590 RETURN 
4000 REM " --- Correct?
4010 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V3=4 THEN GOTO 9900
4040 IF V$="N" THEN GOTO 1000
4060 IF V$<>"Y" THEN GOTO 4000
4100 CALL "SYC.GA",7,1,"","",STATUS
4110 IF STATUS THEN GOTO 9900
4200 RUN "ARR.RB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "SYC - SpeedSearch Manager"
0020 REM "Program SYC.QA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "12Aug96 Caj: Close the files opened here (9950)
0032 REM "02Jan97 JWK: Error 47 if security used (3450)
0033 REM "05Feb97 JAL: Ignore <F5> from within SpeedSearch (420)"
0034 REM "28Jul98 JWK: Clear Options box when there are no options (1445,1455)
0040 REM 
0045 REM "STATUS: 0=No error (returned)"
0050 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER STATUS
0100 REM " --- Open files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SYM-01",FILES$[3]="SYM-06"
0140 LET FILES$[4]="SYM-11",FILES$[5]="SYM-32"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],SYM01_DEV=CHANNELS[2],SYM06_DEV=CHANNELS[3]
0180 LET SYM11_DEV=CHANNELS[4],SYM32_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 SYM06A: IOLIST A0$(1),A1$(1)
0220 SYM32A: IOLIST USER$(1),PATH$[ALL]
0270 SYS01T: IOLIST TERMINAL$,F0$
0400 REM " --- SpeedSearch already invoked?"
0410 DIM SPEED$(128)
0420 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=0450)
0430 GOTO 9950
0450 REM " --- Parameters"
0460 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0470 LET N0$=F0$(16,2),SECURITY$=F0$(15,1),PRINTER$=F0$(13,2)+F0$(25,1)
0480 LET MAXOPT=15,MAXPATH=20,MAXARG=5,MAXPAGE=21
0500 REM " --- Initializations"
0510 DIM USER$(3),PATH$[MAXPATH,5],A0$(2),A1$(40),MESSAGE$[2],MENUPGM$(6)
0550 LET WIDTH=26,SWIDTH=22,HEIGHT=17,WIN_X=80-WIDTH-SWIDTH,WIN_Y=3
0560 LET L=1,X1=1,SHEIGHT=17,SWIN_X=80-SWIDTH,SWIN_Y=WIN_Y,SNAME$=""
0570 LET TITLE$="SpeedSearch",NAME$="",STITLE$="Options",USER$(1)=F0$(4,3)
0580 LET STATUS=0,PAGES=2,PAGE=1,MAXLINES=SHEIGHT-2,PGM$=PGM(-2)
0600 REM " --- Initialize string table variables"
0610 LET LEVEL=1,PATH$[LEVEL,1]=PGM$,PATH$[LEVEL,2]="SSEARCH MANAGER"
0620 LET SPEED$(1,2)=N0$,SPEED$(57,2)=STR(MAXOPT),SPEED$(65,1)=SECURITY$
0630 LET SPEED$(63,2)=STR(MAXOPT),SPEED$(47,3)=USER$,SPEED$(51,6)=""
0640 LET X$=STBL("!SPEEDSEARCH",SPEED$),X$=STBL("!SYS01",STR(SYS01_DEV))
0650 LET X$=STBL("!SYM01",STR(SYM01_DEV)),X$=STBL("!SYM11",STR(SYM11_DEV))
0660 LET X$=STBL("!SYM32",STR(SYM32_DEV))
0700 REM " --- Background"
0710 IF TNAME$<>"" THEN CALL "SYC.WD",TNAME$
0720 IF MNAME$<>"" THEN CALL "SYC.WD",MNAME$
0730 IF BNAME$<>"" THEN CALL "SYC.WD",BNAME$
0740 IF NAME$<>"" THEN GOTO 0780
0750 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0755 LET HEADING$(3)="Search Category",FOOTING$(3)="F4=End  F5=Restart"
0760 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0765 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0770 CALL "SYC.WA",0,SWIDTH,SHEIGHT,SWIN_X,SWIN_Y,STITLE$,SNAME$
0780 LET ONAME$=SNAME$
0790 CALL "SYC.WB",NAME$,0
1000 REM " --- Retrieve SpeedSearch Main Menu"
1010 DIM LLIST$[PAGES,MAXOPT,6],CATLIST$[PAGES,MAXOPT,6],G$(WIDTH-2)
1020 LET OPTIONS=0,PAGE=1
1030 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTIONS,CATLIST$[ALL],W$,MAXROW,MENU$,""
1040 IF OPTIONS=0 THEN GOTO 9900
1200 REM " --- Store options"
1210 FOR X=1 TO OPTIONS
1220 LET G$(2)=FNP$(CATLIST$[1,X,1]),LLIST$[PAGE,X,1]=G$
1230 LET LLIST$[PAGE,X,2]=STR(X),LLIST$[PAGE,X,3]=CATLIST$[1,X,4]
1240 LET LLIST$[PAGE,X,5]=SNAME$,CATLIST$[1,X,2]=FNP$(CATLIST$[1,X,2])
1290 NEXT X
1400 REM " --- Retrieve options for each search catagory"
1410 DIM MORE$[PAGES,MAXOPT,MAXLINES],OPTLIST$[PAGES,MAXOPT,6],G$(SWIDTH-2)
1420 FOR X=1 TO OPTIONS
1430 LET MENU$=CATLIST$[1,X,2],SUBOPTIONS=0
1440 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,MENU$,SECURITY$,SUBOPTIONS,OPTLIST$[ALL],W$,W,X$,""
1445 LET MORE$[PAGE,X,0]=STR(MAXLINES)
1450 IF SUBOPTIONS=0 THEN GOTO 1490
1460 FOR W=1 TO SUBOPTIONS
1470 LET G$(1)=OPTLIST$[1,W,1],MORE$[PAGE,X,W]=G$
1480 NEXT W
1490 NEXT X
1600 REM " --- Last catagory is always Last Search"
1610 DIM G$(WIDTH-2)
1620 LET X=OPTIONS+1,G$(3)="Last Search",LLIST$[PAGE,X,1]=G$
1630 LET LLIST$[PAGE,X,2]="LAST",LLIST$[PAGE,X,5]=SNAME$
1640 LET MORE$[PAGE,X,0]=STR(MAXLINES),MORE$[PAGE,X,1]=" This option will"
1650 LET MORE$[PAGE,X,2]=" display your",MORE$[PAGE,X,3]=" last SpeedSearch"
1660 LET MORE$[PAGE,X,4]=" session.",OPTIONS=X,MAXROW=OPTIONS+1
2000 REM " --- Perform lightbar selection"
2010 GOSUB 6200
2020 IF SELECTION$="END" THEN GOTO 4000
2030 IF SELECTION$="" THEN GOTO 4000
2040 IF FKEY=4 THEN GOTO 4000
2100 REM " --- Repeat last search?"
2110 IF SELECTION$<>"LAST" THEN GOTO 2500
2200 REM " --- Retrieve last search and go to last step"
2210 DIM OPTION$[MAXARG]
2220 FIND (SYM32_DEV,KEY=USER$,DOM=2400)IOL=SYM32A
2230 LET LEVEL=MAXPATH
2240 FOR X=1 TO MAXPATH
2250 IF FNP$(PATH$[X,1])="" THEN LET LEVEL=X,X=MAXPATH+1
2290 NEXT X
2300 REM " --- Reset current search level and params for execution"
2310 IF LEVEL<2 THEN GOTO 2400
2320 LET LEVEL=LEVEL-1,OPTION$[1]=PATH$[LEVEL,1],OPTION$[2]=PATH$[LEVEL,2]
2330 LET OPTION$[3]=PATH$[LEVEL,3],MENU$=PATH$[LEVEL,4]
2340 IF LEVEL>2 THEN GOSUB 5000
2400 REM " --- Make sure option isn't this program"
2410 IF FNP$(OPTION$[1])=PGM$ THEN GOTO 2000
2420 LET SELECTION$=OPTION$[1]
2490 GOTO 3300
2500 REM " --- Establish and initialize OPTION$ array"
2510 DIM OPTION$[MAXARG]
2520 LET OPTION$[1]=SELECTION$
3000 REM " --- Incrememt current search level"
3010 LET LEVEL=LEVEL+1
3020 IF LEVEL<=MAXPATH THEN GOTO 3200
3100 REM " --- Exceeded maximum search level - roll back PATH$"
3110 FOR X=1 TO MAXPATH
3120 FOR Y=0 TO 5
3130 LET PATH$[X-1,Y]=PATH$[X,Y]
3140 NEXT Y
3150 NEXT X
3160 FOR X=0 TO 5
3170 LET PATH$[MAXPATH,X]=""
3180 NEXT X
3190 LET LEVEL=MAXPATH
3200 REM " --- Record current search and create or clear windows as needed"
3210 LET PATH$[LEVEL,1]=OPTION$[1],PATH$[LEVEL,2]=OPTION$[2]
3220 LET PATH$[LEVEL,3]=OPTION$[3],PATH$[LEVEL,4]=MENU$
3230 IF LEVEL=3 THEN IF TNAME$="" THEN GOSUB 5000
3240 IF LEVEL>1 THEN GOTO 3300
3250 IF TNAME$<>"" THEN CALL "SYC.WD",TNAME$
3260 IF MNAME$<>"" THEN CALL "SYC.WD",MNAME$
3270 IF BNAME$<>"" THEN CALL "SYC.WD",BNAME$
3300 REM " --- Initialize SPEEDSEARCH$ global variable"
3310 LET SPEED$(1)=STBL("!SPEEDSEARCH")
3320 LET SPEED$(3,8)=TNAME$,SPEED$(35,2)=STR(TWIDTH),SPEED$(37,2)=STR(THEIGHT)
3330 LET SPEED$(11,8)=MNAME$,SPEED$(39,2)=STR(MWIDTH),SPEED$(41,2)=STR(MHEIGHT)
3340 LET SPEED$(19,8)=BNAME$,SPEED$(43,2)=STR(BWIDTH),SPEED$(45,2)=STR(BHEIGHT)
3350 LET SPEED$(27,8)=ONAME$,SPEED$(59,2)=STR(MAXARG),SPEED$(61,2)=STR(MAXPATH)
3360 LET SPEED$(83,2)=STR(SWIDTH),SPEED$(85,2)=STR(SHEIGHT)
3370 LET SPEED$(51,6)=MENU$,SPEED$(63,2)=STR(MAXLINES)
3380 LET SPEED$(66,2)=STR(MAXPAGE),SPEED$(1)=STBL("!SPEEDSEARCH",SPEED$)
3400 REM " --- Store top window display data and check security"
3410 LET KEY$=OPTION$[3],TYPE$=OPTION$[2],DISPLAY$=""
3420 CALL "SYC.QB",DISPLAY$,KEY$,TYPE$
3430 IF (FNP$(SEARCHSEC$)="" OR SEARCHSEC$="0") AND FNP$(PASSWORD$)="" THEN GOTO 3500
3450 LET X$=LLIST$[1,PICK,5],LLIST$[1,PICK,5]=SEARCHSEC$
3460 LET LLIST$[1,PICK,6]=PASSWORD$
3470 CALL "SYC.QX","M",SECURITY$,PICK,LLIST$[ALL],X
3480 LET LLIST$[1,PICK,5]=X$
3490 IF X>0 THEN GOTO 3600
3500 REM " --- Call selected option"
3510 LET D_STATUS=1,SEARCHSEC$="",PASSWORD$=""
3520 CALL SELECTION$,ERR=3600,D_STATUS,OPTION$[ALL]
3530 LET SPEED$(1)=STBL("!SPEEDSEARCH"),N0$=SPEED$(1,2)
3600 REM " --- Execution error?"
3610 IF D_STATUS<>1 THEN GOTO 3700
3620 LET TITLE=1,OPTION$[1]="PRIOR"
3630 LET MESSAGE$[0]="  This SpeedSearch Selection Is Not Available"
3640 LET MESSAGE$[1]="System Not Installed Or Requested Task Not Found"
3650 IF SELECTION$="PRIOR" THEN LET MESSAGE$[1]="No Previous SpeedSearches Were Found To Repeat",TITLE=2
3660 LET MESSAGE$[2]="        Press <Enter> To Continue"
3670 CALL "SYC.XA",TITLE,MESSAGE$[ALL],2,-1,-1,V$,V3
3680 PRINT @(0,0),'SB','CI',
3700 REM " --- Determine next action"
3710 IF OPTION$[1]=SELECTION$ THEN LET OPTION$[1]="PRIOR"
3720 IF OPTION$[1]="EXIT" OR OPTION$[1]="" THEN GOTO 4000
3730 IF OPTION$[1]="STARTOVER" THEN GOTO 0600
3740 IF OPTION$[1]<>"PRIOR" THEN GOTO 3800
3750 LET LEVEL=LEVEL-1
3760 IF LEVEL<3 THEN GOTO 0600
3770 LET OPTION$[1]=PATH$[LEVEL,1],OPTION$[2]=PATH$[LEVEL,2]
3780 LET OPTION$[3]=PATH$[LEVEL,3],MENU$=PATH$[LEVEL,4],SELECTION$=OPTION$[1]
3790 GOTO 3200
3800 REM " --- Next selection a menu?"
3810 IF LEN(FNP$(OPTION$[1]))>3 THEN GOTO 3900
3815 LET X$=FNP$(OPTION$[1]),PICK=0
3820 FOR X=1 TO MAXROW
3830 IF X$<>CATLIST$[1,X,2] THEN GOTO 3850
3835 IF CATLIST$[1,X,4]<>"M" THEN GOTO 3850
3840 LET PICK=X
3845 EXITTO 3880
3850 NEXT X
3860 LET OPTION$[1]="PRIOR"
3870 GOTO 3900
3880 GOSUB 6300
3890 LET OPTION$[1]=SELECTION$
3900 REM " --- Set selection for next call"
3910 LET SELECTION$=OPTION$[1]
3990 GOTO 3000
4000 REM " --- All done. Save current search path and update printer"
4010 WRITE (SYM32_DEV,KEY=USER$)IOL=SYM32A
4100 REM " --- Delete windows"
4110 IF TNAME$<>"" THEN CALL "SYC.WD",TNAME$
4120 IF MNAME$<>"" THEN CALL "SYC.WD",MNAME$
4130 IF BNAME$<>"" THEN CALL "SYC.WD",BNAME$
4140 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
4150 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
4200 REM " --- Update printer?"
4210 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=4900)IOL=SYS01T
4220 IF F0$(13,2)+F0$(25,1)=PRINTER$ THEN GOTO 4900
4300 REM " --- In base window?"
4310 LET WINDOWS=1
4320 PRINT (0,ERR=4360)'EE'+'CI'+'WINDOW'("LIST"),
4330 READ RECORD(0,SIZ=1,ERR=4360)COUNT$
4340 LET WINDOWS=ASC(COUNT$)
4350 READ RECORD(0,SIZ=WINDOWS,ERR=4360)LIST$
4360 PRINT (0,ERR=4400)'BE',
4400 REM " --- Display new printer"
4410 IF WINDOWS>1 THEN GOTO 4900
4420 PRINT 'SB',@(74,1),FILL(4),@(74,1),F0$(13,2),'SF',
4430 IF F0$(25,1)<>" " THEN PRINT 'SB',"-",F0$(25,1),'SF',
4900 REM " --- All done"
4990 GOTO 9900
5000 REM " --- Establish window parameters"
5010 IF POS(SELECTION$="SYC.QKSYC.QZ",6) THEN GOTO 5190
5020 LET SPEED$(1)=STBL("!SPEEDSEARCH"),A0$(1)=SPEED$(1,2),A1$(1)="Firm "+A0$
5030 FIND (SYM06_DEV,KEY=A0$,DOM=5040)IOL=SYM06A
5040 LET TWIDTH=58,THEIGHT=5,TWIN_X=0,TWIN_Y=0,TNAME$=""
5050 LET MWIDTH=58,MHEIGHT=13,MWIN_X=0,MWIN_Y=TWIN_Y+THEIGHT,MNAME$=""
5060 LET BWIDTH=58,BHEIGHT=5,BWIN_X=0,BWIN_Y=MWIN_Y+MHEIGHT,BNAME$=""
5070 LET TTITLE$=FNP$(A1$),MTITLE$="Detail Display",BTITLE$="More"
5100 REM " --- Open windows"
5110 CALL "SYC.WA",0,TWIDTH,THEIGHT,TWIN_X,TWIN_Y,TTITLE$,TNAME$
5120 CALL "SYC.WA",0,MWIDTH,MHEIGHT,MWIN_X,MWIN_Y,MTITLE$,MNAME$
5130 CALL "SYC.WA",0,BWIDTH,BHEIGHT,BWIN_X,BWIN_Y,BTITLE$,BNAME$
5140 CALL "SYC.WB",MNAME$,0
5190 RETURN 
6200 REM " --- Main Menu Processing"
6210 LET MENU$="",SEARCHSEC$="",PASSWORD$=""
6220 CALL "SYC.SA",1,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAXROW,NAME$,HEIGHT,WIDTH,FKEY
6230 IF FKEY=-2 THEN GOTO 6200
6240 IF FKEY=5 THEN GOTO 6200
6250 IF SELECTION$="LAST" THEN GOTO 6400
6290 LET PICK=NUM(SELECTION$,ERR=6300)
6300 REM " --- Set variables based on selection"
6310 LET SELECTION$=FNP$(CATLIST$[1,PICK,2]),PARAMETER$=CATLIST$[1,PICK,3]
6320 LET SEARCHSEC$=FNP$(CATLIST$[1,PICK,5]),PASSWORD$=FNP$(CATLIST$[1,PICK,6])
6350 IF CATLIST$[1,PICK,4]<>"M" THEN GOTO 6400
6370 LET MENUPGM$(1)=PARAMETER$,MENU$=SELECTION$,SELECTION$=MENUPGM$
6400 REM " --- Selection completed"
6410 LET L=1,X1=1
6490 RETURN 
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- SpeedSearch already invoked"
9810 LET STATUS=1
9890 GOTO 9970
9900 REM " --- Return to caller"
9910 IF TNAME$<>"" THEN CALL "SYC.WD",TNAME$
9915 IF MNAME$<>"" THEN CALL "SYC.WD",MNAME$
9920 IF BNAME$<>"" THEN CALL "SYC.WD",BNAME$
9925 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
9930 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9933 LET X$=STBL("!CLEAR","!SPEEDSEARCH",ERR=9935)
9935 LET X$=STBL("!CLEAR","!SYS01",ERR=9937)
9937 LET X$=STBL("!CLEAR","!SYM01",ERR=9940)
9940 LET X$=STBL("!CLEAR","!SYM11",ERR=9950)
9950 FOR X=1 TO FILES
9960 CLOSE (CHANNELS[X],ERR=9970)
9970 NEXT X
9990 EXIT 
9999 END

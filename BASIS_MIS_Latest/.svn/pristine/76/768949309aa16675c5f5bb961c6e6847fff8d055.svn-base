0010 REM "APE - Invoice Entry"
0020 REM "Program APE.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "31Mar97 WGH: Initialize !GLCONTROL based on current program (620)"
0032 REM "04Dec97 KJS: Can't do Retentions on a Cash Basis (1070)"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=13
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APE-01",FILES$[2]="APM-01",FILES$[3]="APM-02"
0140 LET FILES$[4]="APM-03",FILES$[5]="APT-01",FILES$[6]="SYS-01"
0143 LET FILES$[8]="APM-09",FILES$[9]="APT-11",FILES$[10]="APM-10"
0145 LET FILES$[11]="APE-11"
0150 LET FILES$[12]="GLM-01"
0160 CALL "SYC.DA",1,1,11,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET APE01_DEV=CHANNELS[1],APM01_DEV=CHANNELS[2],APM02_DEV=CHANNELS[3]
0185 LET APM03_DEV=CHANNELS[4],APT01_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0190 LET APM09_DEV=CHANNELS[8],APT11_DEV=CHANNELS[9],APM10_DEV=CHANNELS[10]
0195 LET APE11_DEV=CHANNELS[11]
0200 REM " --- IOLists
0260 APS01A: IOLIST X$,P1$,P2$,P3$(1),P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters
0405 DIM P3$(35),P[2],G[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 FOR X=0 TO 4
0455 LET G[X]=NUM(G2$(X*2+1,2),ERR=0460)
0460 NEXT X
0465 LET G[2]=FNYY_YEAR(G2$(5,2))
0475 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(9,1))
0500 REM " --- Initializations"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 DIM A0$(78),A[4],D0$(14),D1$(105),W0$(22),W[1],W1$(G[4]+1),X[2],W2$(10)
0550 LET K0=1,K9=8,L0=15,L9=20,A0$(1,2)=N0$,D9$=F0$(7,3)
0570 LET M8$="(###)-###-####",M9$="###-##-####",G5=LEN(G5$),G6=LEN(G6$)
0580 CALL "SYC.RM",M1$,M1,7,14
0590 CALL "SYC.RM",G7$,G7,5,8
0600 REM 
0610 LET GL$="N",STATUS=0
0620 CALL "GLC.BA",ERR=0630,PGM(-2),N2$,GLW11$,GL$,STATUS
0630 IF STATUS<>0 THEN GOTO 9900
0640 IF GL$<>"Y" THEN GOTO 0680
0650 LET FILES$[13]=GLW11$,OPTIONS$[13]="C"
0660 CALL "SYC.DA",1,12,13,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0670 IF STATUS>0 THEN GOTO 9900
0675 LET GLM01_DEV=CHANNELS[12],GLW11_DEV=CHANNELS[13]
0800 REM " --- Options"
0810 LET V0$="D",V1$="CE",V2$=D9$,V3$="",V0=8,V1=45,V2=10,STATUS=0
0815 LET V4$="<F1>=Current Date   <F3>=Invoice Date"
0820 PRINT @(10,10),"Enter The Default Accounting Date:"
0830 GOSUB 7000
0840 ON V3 GOTO 0860,0845,0800,0855,9900
0845 LET V$=F0$(7,3)
0850 GOTO 0860
0855 LET V$=""
0860 IF V$="" THEN GOTO 0880
0870 IF GL$="Y" THEN CALL "GLC.EA",V$,"Y",PERIOD$,YEAR$,STATUS
0875 IF STATUS>99 THEN GOTO 0800
0880 LET D9$=V$
1000 REM " --- Display screen"
1010 PRINT 'CF',
1020 CALL "SYC.WC",1,0,80,0,0,5,0
1030 CALL "SYC.WC",1,0,80,0,0,13,0
1040 LET X1$="",X2$="",X3$=""
1050 IF P3$(1,1)="Y" THEN LET X1$="AP Type:"
1060 IF P3$(2,1)="Y" THEN LET X2$="Dist Code:"
1070 IF P3$(3,1)="Y" THEN LET X3$="Retention Amt:"
1090 PRINT 'SB',@(0,3),"Vendor Number:",@(9,4),"Name:",@(6,6),X1$,@(3,7),"Invoice No:",@(4,8),X2$,@(4,9),"Pmt Group:",@(3,10),"Terms Code:",@(9,11),"Hold:",@(23,8),"Invoice Date:",@(25,9),"Acctg Date:",@(27,10),"Due Date:",@(22,11),"Discount Date:",@(44,6),"Total Invoice Amt:",@(46,7),"Net Invoice Amt:",@(47,8),"Adjustment Amt:",@(49,9),"Discount Amt:",@(48,10),X3$,@(52,11),"Reference:",@(57,12),"Memo:",'SF',
1100 REM 
1110 LET X1$="",X2$="",X3$=""
1120 IF GL$="Y" THEN LET X1$="Account"
1130 IF GL$="Y" THEN IF G4$(4,1)="Y" THEN LET X2$="Units"
1140 IF P3$(16,1)="Y" THEN LET X3$="Miscellaneous"
1150 PRINT 'SB',@(0,14),"Seq",@(5),X1$,@(20),X3$,@(47+M1),"Amount",@(64+G7),X2$,'SF',
4000 REM " --- Overlay
4010 LET V=G[1]
4020 CALL "SYC.PA",SYS01_DEV,V,G[2],V0$,V9$,STATUS
4030 LET R9$=V0$,V=NUM(P4$(1,2))
4040 CALL "SYC.PA",SYS01_DEV,V,G[2],V0$,V9$,STATUS
4050 LET R8$=FNC$(V0$)
4060 LET R8$(5,2)=FNYEAR_YY21$(FNYY_YEAR(R8$(5,2))+FNYY_YEAR(P4$(3,2))-G[2])
4070 LET R8$=FND$(R8$)
4090 RUN "APE.AB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

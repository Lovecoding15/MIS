0010 REM "ARR - Conversion Invoice/Finance Charge Register"
0020 REM "Program ARR.JA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "19Nov98 WGH: Clarify prompt for posting to GL (860)
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARE-02",FILES$[2]="ARM-01",FILES$[3]="ARM-10"
0130 LET FILES$[4]="ART-01",FILES$[5]="ART-43",FILES$[6]="ART-53"
0140 LET FILES$[7]="SYS-01",OPTIONS$[1]="L"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET ARE02_DEV=CHANNELS[1],ARM01_DEV=CHANNELS[2],ARM10_DEV=CHANNELS[3]
0185 LET ART01_DEV=CHANNELS[4],ART43_DEV=CHANNELS[5],ART53_DEV=CHANNELS[6]
0190 LET SYS01_DEV=CHANNELS[7]
0200 REM " --- IOLists
0210 ARE02A: IOLIST A0$,A[ALL]
0220 ARM01A: IOLIST *,B1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0330 ARM10D: IOLIST *,Z0$
0400 REM " --- Parameters
0405 DIM P[6],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$,SOURCE$=PGM(-2)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[6]=NUM(P3$(4,1))
0455 FOR X=0 TO 4
0460 LET G[X]=NUM(G2$(X*2+1,2),ERR=0465)
0465 NEXT X
0470 LET G[2]=FNYY_YEAR(G2$(5,2))
0500 REM " --- Initializations"
0510 DIM HEADINGS$[2],A[1]
0520 LET H0=132,H=0,L9=59,L=L9+1,ERRORS=0,WHEN$=F0$(7,3),CLOCK$=""
0540 LET HEADINGS=2,HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0570 LET M0=LEN(M0$),M1=LEN(M1$)
0600 REM " --- Additional File Opens
0650 LET GL$="N",STATUS=0
0655 CALL "GLC.BA",ERR=0660,SOURCE$,N2$,GLW11$,GL$,STATUS
0660 IF STATUS THEN GOTO 9900
0665 IF GL$<>"Y" THEN GOTO 0700
0670 LET FILES$[8]="GLM-01",FILES$[9]=GLW11$,OPTIONS$[9]="CL"
0675 CALL "SYC.DA",1,8,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS THEN GOTO 9900
0690 LET GLM01_DEV=CHANNELS[8],GLW11_DEV=CHANNELS[9]
0800 REM " --- Run Report?
0810 LET V4$="Do You Want To Print The "+N3$
0820 CALL "SYC.YN",1,V4$,2,V$,V3
0830 IF V3=4 THEN GOTO 9900
0840 IF V$<>"Y" THEN GOTO 9900
0850 IF GL$<>"Y" THEN GOTO 0900
0860 LET V4$="Do You Want To Post To The General Ledger When This Register Is Updated"
0870 CALL "SYC.YN",0,V4$,0,V$,V3
0880 IF V$<>"YES" THEN LET GL$="N"
0900 REM " --- File initializations"
0910 CALL "SYC.NB","Printing",M0,COLUMN
0950 CALL "SYC.GA",7,1,"","",STATUS
0960 IF STATUS THEN GOTO 9900
1000 REM " --- Initial Read
1020 READ (ARE02_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Customer Read
1120 READ (ARE02_DEV,END=4000)IOL=ARE02A
1200 REM " --- Breaks
1220 IF A0$(1,2)<>N0$ THEN GOTO 4000
1240 PRINT @(COLUMN,11),FNF$(A0$(5,P[0]),M0$),
1500 REM " --- Print
1520 IF L+1>L9 THEN GOSUB 5000
1540 GOSUB 6000
1560 PRINT (7)FNF$(A0$(5,P[0]),M0$)," ",B1$(1,30),@(38),A0$(11,7),@(48),A0$(18,2),@(53),A0$(20,2),@(58),FNB$(A0$(22,3)),@(67),FNB$(A0$(25,3)),@(76),FNB$(A0$(28,3)),@(85),A0$(31,1),@(H0-M1*2),A[0]:M1$,A[1]:M1$
1580 LET L=L+1,T0=T0+A[0],T1=T1+A[1]
2000 REM " --- Update G/L"
2010 IF GL$<>"Y" THEN GOTO 3900
2020 GOSUB 6100
2030 LET ACCOUNT$=Z0$(1,G[4]),GLDATE$=A0$(22,3),AMOUNT=A[0]
2035 LET REF1$=FNF$(A0$(5,P[0]),M0$),REF2$=A0$(11,7),MEMO$=B1$(1,30)
2050 GOSUB GLRECAP
2060 LET ACCOUNT$=Z0$(2*G[4]+1,G[4]),AMOUNT=-A[0]
2070 GOSUB GLRECAP
2300 REM " --- Check G/L"
2310 LET GLSTATUS=0
2320 IF GL$<>"Y" THEN GOTO 3900
2330 CALL "GLC.EA",GLDATE$,"N",PERIOD$,YEAR$,GLSTATUS
2340 IF GLSTATUS>0 THEN CALL "GLC.GA",7,A0$(22,3),GLSTATUS,L,ERRORS,STATUS
3900 REM 
3990 GOTO 1100
4000 REM " --- Totals
4010 IF L+2>L9 THEN GOSUB 5000
4020 PRINT (7)""
4030 PRINT (7)@(H0-M1*2-18),"Total For Register",@(H0-M1*2),T0:M1$,T1:M1$
4070 LET PAGE=H,WIDTH=H0,UPDATE$="ARU.CA"
4080 IF GL$="Y" THEN RUN "GLR.XA"
4090 RUN UPDATE$,ERR=9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)@(0),"Customer",@(38),"Invoice",@(47),"Dist",@(52),"Terms",@(58),"Invoice",@(69),"Due",@(76),"Discount"
5050 PRINT (7)@(0),"No.",@(M0+1),"Name",@(38),"Number",@(47),"Code",@(52),"Code",@(60),"Date",@(69),"Date",@(78),"Date",@(85),"Type",@(H0-M1-7),"Amount",@(H0-9),"Discount"
5060 PRINT (7)""
5080 LET L=L+3
5090 RETURN 
6000 REM " --- Find Customer
6020 DIM B1$(30)
6040 LET B1$(1)="Not On File"
6060 FIND (ARM01_DEV,KEY=N0$+A0$(5,6),DOM=6080)IOL=ARM01A
6090 RETURN 
6100 REM " --- Dist Code
6110 DIM Z0$(10*G[4])
6120 FIND (ARM10_DEV,KEY=N0$+"D"+A0$(18,2),DOM=6150)IOL=ARM10D
6190 RETURN 
6900 REM " --- Standard G/L Recap Routine"
6910 GLRECAP: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLW11_DEV,GLT05_DEV,ACCOUNT$,GLDATE$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "BMM - Bill Maintenance II
0020 REM "Program BMM.MB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0047 SETESC 9000
0050 SETERR 9000
0055 PRECISION NUM(P6$(5,1))
0060 ON O9 GOTO 2000,4000
0200 REM " --- IOLists
0210 BMM01A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
1000 REM " --- Key
1010 EXTRACT (BMM01_DEV,KEY=A0$(1,22),DOM=1011)IOL=BMM01A
1900 LET I0=0
2000 REM " --- Drawing Number
2030 LET V0$="S",V1$="",V2$=A0$(23,25),V4$="Enter a Drawing Number",V0=25,V1=18,V2=7
2040 GOSUB 7000
2050 LET A0$(23,25)=V$
2090 GOTO 3900
2100 REM " --- Drawing Revision
2120 LET V0$="S",V1$="",V2$=A0$(48,5),V0=5,V1=18,V2=8
2130 GOSUB 7000
2140 LET A0$(48,5)=V$
2190 GOTO 3900
2200 REM " --- Unit of Measure
2210 IF A2$(48,2)="  " THEN LET A2$(48,2)=SALEUNIT$
2220 LET V0$="S",V1$="C",V2$=A2$(48,2),V3$="",V4$="<F1> To Blank Out Unit Of Measure",V0=2,V1=69,V2=5
2230 GOSUB 7000
2240 IF V3=1 THEN LET V$="  "
2250 LET A2$(48,2)=V$
2260 PRINT @(V1,V2),A2$(48,2),
2290 GOTO 3900
2300 REM " --- Standard Lot Size
2310 IF A[5]=0 THEN LET A[5]=1
2320 LET V0$="N",V1$="",V2$=STR(A[5]),V3$="######0",V0=7,V1=69,V2=7
2330 IF A2$(40,1)="Y" THEN LET A[5]=1; PRINT @(69,7),A[5]:V3$,; GOTO 2390
2340 GOSUB 7000
2350 LET V=INT(ABS(V))
2360 IF V=0 THEN LET V=1
2370 LET A[5]=V
2380 PRINT @(V1,V2),A[5]:"######0",
2390 GOTO 3900
2400 REM " --- Estimated Yield
2410 IF A[4]<=0 THEN LET A[4]=100
2420 LET V0$="N",V1$="C",V2$=STR(A[4]:"##0.0"),V3$="##0.0",V0=5,V1=69,V2=6
2430 GOSUB 7000
2440 LET A[4]=V
2450 IF A[4]<=0 THEN GOTO 2400
2460 IF A[4]>100 THEN LET A[4]=100; GOTO 2400
2490 GOTO 3900
2500 REM " --- Revision Code
2520 LET V0$="S",V1$="",V2$=A2$(38,2),V0=2,V1=39,V2=8
2540 GOSUB 7000
2560 LET A2$(38,2)=V$
2590 GOTO 3900
2600 REM " --- Phantom Item?
2620 LET V0$="Y",V1$="C",V2$=A2$(40,1),V3$="",V4$="Y=Phantom (Not Inventoried)/N=Not Phantom (Inventoried)",V0=1,V1=69,V2=4
2630 IF V2$=" " THEN LET V2$="N"
2640 GOSUB 7000
2660 LET A2$(40,1)=V$
2690 GOTO 3900
2700 REM " --- Revision Date
2720 LET V0$="D",V1$="",V2$=A2$(35,3),V0=8,V1=69,V2=8
2740 GOSUB 7000
2760 LET A2$(35,3)=V$
2790 GOTO 3900
3900 REM " --- Next Field
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2500,2600,2200,2400,2300,2700,3990
4000 REM " --- Options
4010 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4050 ON POS(V$="YN") GOTO 4000,4100,1900
4100 REM " --- Write
4120 WRITE (BMM01_DEV,KEY=A0$(1,22))IOL=BMM01A
4190 IF O0=0 THEN RUN "BMM.MD"
4300 RUN "BMM.MA"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 4300
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "OPR - Order/Picking List (Form #203)"
0020 REM "Program BSG.01 - based on OPR.PA"
0040 REM 
0050 REM "O9$=Return Program
0051 REM "O9 =1 - Batch Print
0052 REM "O9 =2 - On Demand Print
0053 REM "PRTR_DEV=Open Printer Channel
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/lock files"
0110 LET FILES1=3
0120 DIM FILES1$[FILES1],OPTIONS1$[FILES1],CHANNELS1[FILES1]
0130 LET FILES1$[1]="BMM-01",FILES1$[2]="BMM-02"
0140 LET FILES1$[3]="CSM-12"
0160 CALL "SYC.DA",1,1,FILES1,FILES1$[ALL],OPTIONS1$[ALL],CHANNELS1[ALL],BATCH,STATUS
0170 IF STATUS<>0 THEN GOTO 4100
0180 LET BMM01_DEV=CHANNELS1[1],BMM02_DEV=CHANNELS1[2]
0190 LET CSM12_DEV=CHANNELS1[3]
0200 REM " --- IOLists"
0210 ARE03A: IOLIST A0$(1),A[ALL]
0220 ARE13A: IOLIST W0$,W1$(1),W[ALL]
0230 ARE23A: IOLIST T0$,T1$,T2$,T[0],T[1],T[2]
0240 ARE33A: IOLIST D0$,D1$
0250 ARM01A: IOLIST B0$,B1$
0260 ARM03A: IOLIST C0$,C1$
0270 ARM10A: IOLIST X3$
0280 ARM10E: IOLIST Y0$
0290 ARM10F: IOLIST X1$
0300 ARM10G: IOLIST *,X4$
0310 IVM01A: IOLIST *,X2$(1),X7$(1)
0320 IVM02A: IOLIST *,X5$(1)
0350 ARE73A: IOLIST WX0$,WX1$,WX[ALL]; REM WA# 207
0360 ARE83A: IOLIST WY0$,WY1$,WY2$,WY[ALL]; REM WA# 207
0370 CSM03A: IOLIST CS0$,CS1$,CS2$
0500 REM " --- Initialize Data"
0505 DIM WX1$(6),WX[4],WY[2]; REM WA# 207
0510 PRECISION I[2]
0520 LET H0=0,L9=30,HEADINGS=0,WHEN$=F0$(7,3); REM "WA:200
0530 DIM H9$(40,$0A$),HEADINGS$[HEADINGS]
0550 LET MULT_WH=0
0560 READ (IVM10_DEV,KEY=N0$+"C",DOM=0570)
0570 READ (IVM10_DEV)
0580 LET K10$=KEY(IVM10_DEV,END=0600)
0590 IF POS(N0$+"C"=K10$)=1 THEN LET MULT_WH=1
0800 REM " --- Print Prices?"
0810 IF O9=1 THEN GOTO 0900
0820 READ (ARE03_DEV,KEY=A0$(1,20),DOM=0830)
0830 LET P8$="N"
0840 IF A0$(21,1)<>"S" THEN GOTO 0850
0842 LET V4$="Do You Wish To Print Prices On The Picking List"
0844 REM CALL "SYC.YN",0,V4$,0,V$,V3
0846 REM LET P8$=V$
0850 IF V3=4 THEN GOTO 9900
0855 IF P8$="NO" THEN LET P8$="N"
0857 IF P8$="YES" THEN LET P8$="Y"
0858 IF P8$="Y" THEN LET P6$="S"; REM "WA:200
0859 IF P8$="N" THEN LET P6$="P"; REM "WA:200
0860 ON POS(P8$="YN") GOTO 0840,0880,0870
0870 IF A0$(21,1)="S" THEN LET P8$="N"
0900 REM " --- Background"
0910 PRINT @(0,21),'CL','SB',@(30,21),"Printing:",'SF',
0950 REM " --- Position Header File"
0960 IF O9<>2 THEN GOTO 1000
0970 READ (ARE03_DEV,KEY=A0$(1,20),DOM=9900)IOL=ARE03A
0975 LET TOTAL=0
0980 GOTO 1145
1000 REM " --- Initial Read"
1020 READ (ARE04_DEV,KEY=N0$+"O"+"  "+P5$,DOM=1030)
1030 READ (ARE43_DEV,KEY=N0$,DOM=1031); REM "WA:200
1100 REM " --- Main Read"
1105 IF O9=2 THEN GOTO 4040
1110 LET TOTAL=0
1115 LET REPRINT$=""
1120 REM LET K1$=KEY(ARE04_DEV,END=4000)
1125 REM READ (ARE04_DEV)
1130 LET ARE43_K$=KEY(ARE43_DEV,END=4000); READ (ARE43_DEV); REM "WA:200
1131 LET K1$=ARE43_K$(1,2)+"O"+ARE43_K$(3,2)+ARE43_K$(12,6)+ARE43_K$(5,7); REM "WA:200
1132 READ (ARE04_DEV,KEY=K1$,DOM=1130); REM "WA:200
1135 IF K1$(1,3)<>N0$+"O" THEN GOTO 4000
1140 FIND (ARE03_DEV,KEY=K1$(1,2)+K1$(4)+"000",DOM=1200)IOL=ARE03A
1145 IF A0$(68,1)="Y" THEN GOSUB 6800
1150 IF A0$(100,1)="Y" THEN LET REPRINT$=A0$(68,1),A0$(68,1)="N"
1155 IF POS(A0$(68,1)="Y")<>0 THEN GOTO 1100
1160 IF O9<>2 AND POS(A0$(69,1)="YS12")<>0 THEN GOTO 1100
1170 IF A0$(21,1)="V" THEN GOTO 1100
1175 IF C9$(7,1)="Y" THEN IF A0$(101,1)="C" AND C9$(18,1)="N" THEN GOTO 1100
1177 IF A0$(108,1)<>"Y" THEN GOTO 1100; REM WA# 205
1180 LET AFTER$="NO"; GOSUB 6000
1182 IF PRINT$="NO" THEN GOTO 1100
1185 PRINT @(40,21),FNF$(A0$(5,P[0]),M0$),"  ",A0$(11,7),
1190 GOTO 1400
1200 REMOVE (ARE04_DEV,KEY=K1$)
1210 GOTO 1100
1400 REM " --- Heading"
1410 LET B$="",OTHWHSE$=""
1420 DIM B1$(325)
1440 LET B1$(1)="Not On File"
1460 READ (ARM01_DEV,KEY=N0$+A0$(5,6),DOM=1490)IOL=ARM01A
1470 LET B$=B1$(31,72)+B1$(179,48)+B1$(103,9)
1480 CALL "SYC.AA",B$,24,5,P[2],30
1490 LET B$=B1$(1,30)+B$
1495 GOSUB 10000; REM WA# 206
1496 IF FNP$(A0$(102,6))="" THEN LET C$=B$; GOTO 1600; REM "WA:200
1500 REM " --- Ship-To"
1520 DIM C1$(190),C$(190)
1530 REM IF FNP$(A0$(102,6))="" THEN LET C1$(1,4)="SAME",C$(1,81)=""; GOTO 1590
1540 IF A0$(102,6)<>"000099" THEN GOTO 1570
1550 FIND (ARE33_DEV,KEY=N0$+A0$(5,13),DOM=1600)IOL=ARE33A
1555 LET C1$=D1$(1,90)+FILL(30)+D1$(91)
1560 GOTO 1580
1570 FIND (ARM03_DEV,KEY=N0$+A0$(5,6)+A0$(102,6),DOM=1600)IOL=ARM03A
1580 LET C$=C1$(31,90)+FILL(30)+C1$(121,25)+" "+C1$(146,2)+"  "+C1$(157,15)+FILL(15," ")+C1$(148,9)
1585 CALL "SYC.AX",C$,30,6,P[2],30
1590 LET C$=C1$(1,30)+C$
1600 REM " --- Print Heading"
1605 DIM J1$(100)
1610 IF LEN(C$)<LEN(B$) THEN LET C$=C$+J1$(1,LEN(B$)-LEN(C$))
1700 REM " --- Salesperson"
1710 LET X1$="HOUSE"
1720 FIND (ARM10_DEV,KEY=N0$+"F"+A0$(59,3),DOM=1740)IOL=ARM10F
1730 LET X1$=X1$(7,20)
1750 REM " --- Terms"
1760 LET X3$=""
1770 FIND (ARM10_DEV,KEY=N0$+"A"+A0$(62,2),DOM=1790)IOL=ARM10A
1780 LET X3$=X3$(6,15)
1800 REM " --- Print Heading"
1810 GOSUB 12100; REM "WA:200
1815 LET T0=0,P=0
1820 GOSUB 5000
1900 REM " --- Position Detail File"
1910 READ (ARE13_DEV,KEY=A0$(1,17),DOM=2000)
2000 REM " --- Detail"
2020 LET K$=KEY(ARE13_DEV,END=3900)
2040 IF K$(1,17)<>A0$(1,17) THEN GOTO 3900
2060 READ (ARE13_DEV,KEY=K$)IOL=ARE13A
2100 REM " --- Type"
2110 DIM Y0$(30)
2120 FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=2160)IOL=ARM10E
2130 IF POS(A0$(21,1)="P")=0 AND W1$(44,1)="N" THEN GOTO 2150; REM "Not committed don't print
2140 IF POS(W0$(31,2)=OTHWHSE$)=0 THEN LET OTHWHSE$=OTHWHSE$+W0$(31,2)
2150 IF WHSE$<>"" AND W0$(31,2)<>WHSE$ THEN GOTO 2000
2155 IF POS(Y0$(25,1)="MNO")>0 THEN GOTO 2250
2160 DIM X2$(60),X7$(62); LET X2$(1)="**Not On File**"
2170 IF POS(Y0$(25,1)=" SRDP")=0 THEN GOTO 2250
2180 FIND (IVM01_DEV,KEY=N0$+W0$(33,20),DOM=2200)IOL=IVM01A
2190 GOSUB 5900
2230 DIM X5$(10)
2240 FIND (IVM02_DEV,KEY=N0$+W0$(31,2)+W0$(33,20),DOM=2250)IOL=IVM02A
2250 IF Y0$(25,1)="M" AND POS(Y0$(28,1)=" BO")=0 THEN GOTO 2300
2255 GOSUB 12000; REM "WA:200
2260 GOSUB 5200
2270 REM " --- print configurator/serial number info"
2275 GOSUB 15400
2280 REM " --- See if this is a kit --- WA# 207
2285 LET ITEM$=W0$(33,20)
2290 GOSUB 13000
2300 REM " --- Total"
2320 LET T0=T0+W[6]
2400 REM " --- Any Serial/Lot #'s?"
2410 IF POS(I3$(17,1)="LS")=0 THEN GOTO 2900
2420 IF POS(Y0$(25,1)=" SRDP")=0 THEN GOTO 2900
2430 IF X7$(19,1)<>"Y" THEN GOTO 2900
2500 REM " --- OK There Are Lots"
2505 DIM T[2]; LET T$="",T9=0
2510 READ (ARE23_DEV,KEY=W0$(1,20),DOM=2520)
2520 LET K9$=KEY(ARE23_DEV,END=2600)
2540 IF POS(W0$(1,20)=K9$)<>1 THEN GOTO 2600
2560 READ (ARE23_DEV)IOL=ARE23A
2570 IF T[0]=0 THEN GOTO 2520
2580 GOSUB 5400
2585 LET T9=T9+T[1]
2590 GOTO 2520
2610 IF T9>=W[2] THEN GOTO 2660
2620 DIM T1$(I[3],"_"),T[2]
2625 LET Y9=W[2]-T9; IF I3$(17,1)="L" THEN LET Y9=3
2630 FOR Y=1 TO Y9
2640 GOSUB 5400
2650 NEXT Y
2660 IF L+1>L9 THEN GOTO 2700
2670 LET PRINT_ITEM$=ITEM$
2680 LET L=L+1
2900 GOTO 2000
3900 REM " --- Total"
3920 GOSUB 5600
3930 IF O9=2 THEN GOTO 4000
3940 EXTRACT (ARE03_DEV,KEY=A0$(1,20),ERR=3990)IOL=ARE03A
3950 IF A0$(100,1)="Y" THEN LET A0$(68,1)=REPRINT$ ELSE LET A0$(68,1)="B"
3960 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
3990 GOTO 1100
4000 REM " --- Totals"
4005 IF O9=2 THEN IF P6$="S" THEN GOTO 9900
4010 IF O9<>2 THEN GOTO 4100
4020 LET AFTER$="YES"; GOSUB 6000
4040 IF A0$(100,1)="Y" THEN LET A0$(68,1)=REPRINT$,A0$(100,1)=""; GOTO 4060
4050 IF PRINT$="YES" AND ALLPRINT$="YES" THEN LET A0$(68,1)="Y"
4060 IF A0$(68,1)<>"Y" THEN LET A0$(68,1)="N"
4065 LET A0$(69,1)="N"
4070 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
4080 IF A0$(68,1)="Y" THEN REMOVE (ARE04_DEV,KEY=N0$+"O"+A0$(3,15),DOM=4090)
4100 IF PRTR_DEV>0 THEN CLOSE (PRTR_DEV,ERR=4105)
4102 IF P6$="S" THEN GOTO 9900; REM "WA:200
4105 IF O9=2 THEN GOTO 9900
4110 PRINT @(0,21),'CL',
4120 LET V4$="Are You Ready To Update The Order/Picking List"
4140 CALL "SYC.YN",0,V4$,0,V$,V3
4150 IF V3=4 OR V$="NO" THEN GOTO 9900
4160 IF V$<>"YES" THEN GOTO 4100
4190 PRINT @(20,16),"Now Clearing Print File........",
4220 READ (ARE04_DEV,KEY=N0$+"O",DOM=4230)
4300 REM " --- Clear Print Flags"
4320 LET K1$=KEY(ARE04_DEV,END=4900)
4340 READ (ARE04_DEV,KEY=K1$)
4400 IF K1$(1,3)<>N0$+"O" THEN GOTO 4900
4420 EXTRACT (ARE03_DEV,KEY=K1$(1,2)+K1$(4)+"000",DOM=4500)IOL=ARE03A
4425 IF A0$(100,1)="Y" THEN LET A0$(100,1)="",A0$(69,1)="N"; GOTO 4475
4430 IF POS(A0$(68,1)="B")=0 THEN GOTO 4300
4440 IF POS(A0$(69,1)="YS12")<>0 THEN GOTO 4300
4445 LET AFTER$="YES"; GOSUB 6000
4460 LET A0$(69,1)="N"
4470 IF PRINT$="YES" AND ALLPRINT$="YES" THEN LET A0$(68,1)="Y"
4475 IF A0$(68,1)<>"Y" THEN LET A0$(68,1)="N"
4480 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
4500 IF A0$(68,1)="Y" THEN REMOVE (ARE04_DEV,KEY=K1$)
4520 GOTO 4300
4900 PRINT @(0,16),'CL',
4990 GOTO 9900
5000 REM " --- Heading"
5001 IF P6$="S" THEN LET ORDERHEADER$="S A L E S   A C K N O W L E D G E M E N T"; REM "WA:200
5002 IF P6$="P" THEN LET ORDERHEADER$="L I E F E R S C H E I N"
5005 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5006 LET P=P+1; PRINT (PRTR_DEV)'LF','LF'
5010 FOR I=1 TO 7
5020 PRINT (PRTR_DEV)@(H0+2),B$(I*30-29,30); REM ,@(H0+45),C$(I*30-29,30),'LF',
5030 NEXT I
5040 PRINT (PRTR_DEV)'LF',@(H0+59),"LS-NR.: ",A0$(11,7)," ",A0$(23,1),'LF',@(H0+60),"DATUM: ",FNB4$(A0$(77,3)),'LF',@(H0+60),"SEITE: ",P,'LF'
5048 PRINT (PRTR_DEV)@(H0+1),ORDERHEADER$
5049 PRINT (PRTR_DEV)@(H0+45),"Bestell Nr.: ",A0$(49,10)
5050 PRINT (PRTR_DEV)@(H0+0),FILL(80,"-")
5060 PRINT (PRTR_DEV)@(H0+5),"Terms: ",X3$,@(H0+45),"Versand Per: ",A0$(32,10),'LF',@(H0+0),FILL(80,"-")
5075 IF A0$(21,1)="P" OR P8$="Y" THEN PRINT (PRTR_DEV)@(H0+65-M4+4),"E. Preis",
5080 PRINT (PRTR_DEV)@(H0+1),"Menge           Produkt                  ",
5090 IF MULT_WH>0 THEN PRINT (PRTR_DEV)@(H0+47),"Whse",
5100 PRINT (PRTR_DEV)@(H0+52),"Port Id",
5110 IF A0$(21,1)="P" OR P8$="Y" THEN PRINT (PRTR_DEV)@(H0+74),"Total",
5120 PRINT (PRTR_DEV)'LF',@(H0+0),FILL(80,"-")
5180 LET L=0
5190 RETURN 
5200 REM " --- Item Detail"
5210 IF L+1>L9 THEN GOSUB 5800
5215 REM IF Y0$(25,1)<>"M" THEN PRINT (7)""; LET L=L+1
5216 PRINT (7)""; LET L=L+1
5220 REM PRINT (PRTR_DEV)@(H0),W0$(18,3),
5250 IF POS(Y0$(25,1)="MO")<>0 THEN GOTO 5280
5255 REM IF W1$(44,1)="N" THEN PRINT (PRTR_DEV)@(H0+13),"Ship:",FNB4$(W1$(41,3)), ELSE PRINT (PRTR_DEV)@(H0+13),"______ ______",
5256 REM IF W1$(44,1)="N" THEN PRINT (PRTR_DEV)@(H0+8),"Ship:",FNB4$(W1$(41,3)), ELSE PRINT (PRTR_DEV)@(H0+8),"______",
5260 PRINT (PRTR_DEV)@(H0+1),W[2],
5265 REM PRINT (PRTR_DEV)@(H0+74),"______",
5280 IF POS(Y0$(25,1)="MO")<>0 THEN PRINT (PRTR_DEV)@(H0+17),W1$(1,40),
5285 IF Y0$(25,1)="N" THEN PRINT (PRTR_DEV)@(H0+17),"Non-Stock",
5300 IF POS(Y0$(25,1)=" SRDP")<>0 THEN PRINT (PRTR_DEV)@(H0+17),W0$(33,20),
5330 IF A0$(21,1)="P" THEN GOTO 5340
5331 IF POS(Y0$(25,1)=" SRDPN")<>0 AND MULT_WH>0 THEN PRINT (PRTR_DEV)@(H0+48),W0$(31,2),
5332 PRINT (PRTR_DEV)@(H0+52),PORT$,; REM "WA:200
5333 IF Y0$(27,1)="Y" THEN PRINT (PRTR_DEV)@(H0+61),"*Drop Ship",; GOTO 5336
5334 IF POS(Y0$(25,1)=" SRDP")<>0 THEN PRINT (PRTR_DEV)@(H0+61),X5$,
5336 IF P8$<>"Y" THEN GOTO 5380
5340 IF POS(Y0$(25,1)=" SRDPN")<>0 THEN PRINT (PRTR_DEV)@(H0+68-M4),FNDM$(STR(W[1]:M4$)),@(H0+79-M1),FNDM$(STR(W[6]:M1$)); LET TOTAL=TOTAL+W[6]
5350 IF POS(Y0$(25,1)="O")<>0 THEN PRINT (PRTR_DEV)@(H0+79-M1),W[6]:M1$,; LET TOTAL=TOTAL+W[6]
5380 PRINT (PRTR_DEV)'LF',
5385 LET L=L+1
5387 IF POS(Y0$(25,1)=" SRDPN")<>0 THEN IF L+1>L9 THEN GOSUB 5800
5390 IF POS(Y0$(25,1)=" SRDP")<>0 THEN PRINT (PRTR_DEV)@(H0+17),X2$; LET L=L+1
5395 IF Y0$(25,1)="N" THEN PRINT (PRTR_DEV)@(H0+17),W1$(1,40); LET L=L+1
5399 RETURN 
5400 REM " --- Serial #'s Here"
5410 IF L<=L9-1 THEN GOTO 5440
5415 GOSUB 5800
5420 IF POS(Y0$(25,1)=" SRDP")<>0 THEN PRINT (PRTR_DEV)@(H0+28),W0$(33,20),"(Continued)"; LET L=L+1
5440 IF I3$(17,1)="L" THEN GOTO 5470
5450 PRINT (PRTR_DEV)@(H0+24),"S/N: ",T1$
5460 GOTO 5480
5470 PRINT (PRTR_DEV)@(H0+24),"Lot: ",T1$,"   Shipped: ",
5475 IF T[1]<>0 THEN PRINT (PRTR_DEV)T[1]:M2$ ELSE PRINT (PRTR_DEV)T1$
5480 LET L=L+1
5490 RETURN 
5600 REM " --- Total"
5630 GOSUB 6600
5650 GOSUB 6500
5670 PRINT (PRTR_DEV)'FF',
5690 RETURN 
5800 REM " --- Continued"
5820 IF L9-L-1>0 THEN PRINT (PRTR_DEV)H9$(1,L9-L-1)
5840 PRINT (PRTR_DEV)@(H0+64),"Continued"
5850 PRINT (PRTR_DEV)'FF',
5870 GOSUB 5000
5890 RETURN 
5900 REM " --- Compress Description"
5910 LET X2$=FNZ$(X2$,I[4],I[5],I[6])
5920 IF LEN(X2$)>79-H0-25 THEN LET X2$=X2$(1,79-H0-25)
5990 RETURN 
6000 REM " --- Look for Valid Lines Here"
6005 LET PRINT$="NO",ALLPRINT$="YES"; DIM W1$(64)
6010 READ (ARE13_DEV,KEY=A0$(1,17),DOM=6020)
6020 LET K3$=KEY(ARE13_DEV,END=6180)
6040 IF POS(A0$(1,17)=K3$)<>1 THEN GOTO 6180
6050 READ (ARE13_DEV)IOL=ARE13A
6060 FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=6120)IOL=ARM10E
6067 IF A0$(21,1)="P" THEN GOTO 6100
6070 IF W1$(44,1)="N" THEN GOTO 6120
6090 IF WHSE$<>"" AND W0$(31,2)<>WHSE$ THEN IF W1$(45,1)<>"Y" THEN LET ALLPRINT$="NO"; GOTO 6120 ELSE GOTO 6120
6092 IF A0$(100,1)<>"Y" AND W1$(45,1)="Y" THEN GOTO 6120; REM "Not a reprint and already printed for selected warehouse(s)
6094 IF A0$(100,1)="Y" AND W1$(45,1)<>"Y" THEN GOTO 6120; REM "A reprint and not printed yet for selected warehouse(s)
6100 IF AFTER$="YES" THEN LET W1$(45,1)="Y"; WRITE (ARE13_DEV,KEY=W0$(1,20))IOL=ARE13A
6110 LET PRINT$="YES"
6120 GOTO 6020
6180 READ (ARE03_DEV,KEY=A0$(1,20))
6190 RETURN 
6500 REM " --- Certification"
6505 GOTO 6590; REM "Print comment in header
6510 FOR X=1 TO 2
6520 FIND (ARM10_DEV,KEY=N0$+"G"+A0$(70,2)+STR(X),DOM=6580)IOL=ARM10G
6530 FOR Y=1 TO 5
6535 IF X4$(Y*40-39,40)=J1$(1,40) THEN GOTO 6570
6540 IF X=1 AND Y=1 THEN IF L+2>L9 THEN GOSUB 5800 ELSE PRINT (PRTR_DEV)'LF'; LET L=L+2
6545 IF L+1>L9 THEN GOSUB 5800
6550 PRINT (PRTR_DEV)@(H0+27),X4$(Y*40-39,40)
6560 LET L=L+1
6570 NEXT Y
6580 NEXT X
6590 RETURN 
6600 REM " --- Print Totals"
6605 IF P8$<>"Y" THEN GOTO 6690
6610 IF L+6>L9 THEN GOSUB 5800
6620 PRINT (PRTR_DEV)'LF',@(H0+79-M1),FILL(M1-1,"-"); REM "WA:200
6640 PRINT (PRTR_DEV)@(H0+63-M1),"Gesamt Netto:",@(H0+79-M1),FNDM$(STR(TOTAL:M1$))
6650 REM IF A[2]<>0 THEN PRINT (PRTR_DEV)@(H0+67-M1),"Discount:",@(H0+79-M1),-A[2]:M1$; LET L=L+1
6660 IF A[1]<>0 THEN PRINT (PRTR_DEV)@(H0+69-M1),"Fracht:",@(H0+79-M1),FNDM$(STR(A[1]:M1$)); LET L=L+1
6670 IF A[0]<>0 THEN PRINT (PRTR_DEV)@(H0+72-M1),"UST:",@(H0+79-M1),FNDM$(STR(A[0]:M1$)); LET L=L+1
6675 PRINT (PRTR_DEV)@(H0+79-M1),FILL(M1-1,"-")
6680 PRINT (PRTR_DEV)@(H0+63-M1),"Gesamtbetrag:",@(H9+79-M1),FNDM$(STR(TOTAL-A[2]+A[1]+A[0]:M1$))
6685 LET L=L+4
6690 RETURN 
6800 REM " --- Verify If Pick Ticket Is Still Printed"
6810 READ (ARE13_DEV,KEY=A0$(1,17),DOM=6820)
6820 LET K$=KEY(ARE13_DEV,END=6870)
6830 IF POS(A0$(1,17)=K$)<>1 THEN GOTO 6870
6840 READ (ARE13_DEV,KEY=K$,DOM=6860)IOL=ARE13A
6850 IF W1$(45,1)="Y" THEN GOTO 6890
6860 GOTO 6820
6870 LET A0$(68,1)="N",A0$(100,1)=""
6890 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNZ$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
8300 DEF FNDM$(Q1$)
8310 LET Q2$=".,."; REM period,comma,period
8320 FOR Q=1 TO LEN(Q1$); LET Q0=POS(Q1$(Q,1)=Q2$); IF Q0 THEN LET Q1$(Q,1)=Q2$(Q0+1,1) FI; NEXT Q
8330 RETURN Q1$
8340 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- End"
9910 READ (ARE03_DEV,KEY=A0$(1,20),DOM=9940)IOL=ARE03A
9920 LET A0$(69,1)="N"
9930 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
9940 IF PRTR_DEV>0 THEN CLOSE (PRTR_DEV,ERR=9950)
9950 FOR I=1 TO FILES1; REM WA# 207
9955 CLOSE (CHANNELS1[I],ERR=9956)
9960 NEXT I
9990 IF P6$="S" THEN IF O9=2 THEN RUN O8$
9995 RUN O9$
9999 END
10000 REM " --- Get 30 character address from Telemaster"
10010 READ (TMM01_DEV,KNUM=0,KEY=A0$(1,2)+A0$(5,6),DOM=10011)
10020 LET TMM01_K$=KEY(TMM01_DEV,END=10090)
10030 IF POS(A0$(1,2)+A0$(5,6)=TMM01_K$)<>1 THEN GOTO 10090
10040 READ (TMM01_DEV,KEY=TMM01_K$)TMM0$
10070 REM LET B$=TMM0$(142,85)+" "+TMM0$(227,2)+FILL(62," ")+TMM0$(229,9)
10071 LET B$=TMM0$(142,60)+TMM0$(389,60)+TMM0$(202,25)+" "+TMM0$(227,2)+"  "+TMM0$(275,2)+FILL(28," ")+TMM0$(229,9)
10080 CALL "SYC.AX",B$,30,6,P[2],30
10085 LET B$=B1$(1,30)+B$
10090 RETURN 
12000 REM " --- Find Port Id
12010 LET PORT$=""
12030 IF Y0$(25,1)<>"S" THEN GOTO 12090
12040 FIND (CSM03_DEV,KEY=N0$+W0$(39,3),DOM=12090)IOL=CSM03A; REM "WA:200
12050 LET PORT$=CS2$(1,5)
12090 RETURN 
12100 REM " --- Get Comment For Header
12110 DIM X4$(40)
12120 FIND (ARM10_DEV,KEY=N0$+"G"+A0$(70,2)+"1",DOM=12121)IOL=ARM10G
12190 RETURN 
13000 REM " --- Explode BOMs"
13005 LET OFFSET=5
13010 READ (BMM01_DEV,KEY=N0$+ITEM$,DOM=13900)
13020 READ (BMM02_DEV,KEY=N0$+ITEM$,DOM=13021)
13100 REM " --- Next BOM Detail"
13120 LET BMM02_K$=KEY(BMM02_DEV,END=13800)
13130 IF POS(N0$+ITEM$=BMM02_K$)<>1 THEN GOTO 13800
13150 READ (BMM02_DEV,KEY=BMM02_K$)BM0$,BM1$,BM2$,BOM_QTY
13200 REM " --- See if there is a sub-assembly
13220 READ (BMM01_DEV,KEY=N0$+BM1$(1,20),DOM=13400)
13300 REM " --- Imbedded BOMS, save key and process them
13310 LET LEFT_OFF$=BMM02_K$+LEFT_OFF$
13320 LET ITEM$=BM1$(1,20)
13325 GOSUB 15200
13330 LET OFFSET=OFFSET+5
13340 GOTO 13020
13400 REM " --- Not multi-level, print it
13420 LET PRINT_ITEM$=BM1$(1,20)
13430 GOSUB 15200
13490 GOTO 13100
13800 REM " --- See if there's any clean-up to do
13810 IF LEN(LEFT_OFF$)=0 THEN GOTO 13900
13820 READ (BMM02_DEV,KEY=LEFT_OFF$(1,25))BM0$,BM1$,BM2$,BOM_QTY
13830 LET ITEM$=LEFT_OFF$(3,20)
13840 LET LEFT_OFF$=LEFT_OFF$(26)
13845 LET OFFSET=OFFSET-5
13850 GOTO 13100
13900 REM " --- Done w/ std bom - add media & type of sale lines "
13910 GOSUB 14000
13990 RETURN 
14000 REM "Media type BOM lines"
14010 LET B_KEY$=N0$+"F"+WX1$(1,3)
14020 GOSUB 14100
14030 LET B_KEY$=N0$+"G"+WX1$(4,3)
14040 GOSUB 14100
14090 RETURN 
14100 REM " --- read & print it
14110 READ (CSM12_DEV,KEY=B_KEY$,DOM=14111)
14120 LET CSM12_K$=KEY(CSM12_DEV,END=14190)
14130 IF POS(B_KEY$=CSM12_K$)<>1 THEN GOTO 14190
14140 READ (CSM12_DEV,KEY=CSM12_K$)BM0$,BM1$,BM2$,BOM_QTY
14150 GOSUB 15200
14180 GOTO 14120
14190 RETURN 
14500 REM Date with 4 digit year - German version dd.mm.yyyy
14505 DEF FNA4$(Q$,Q2$)=STR(((ASC(Q$)-32)+1900)*POS(" "<>Q2$(2,1)):"0000")
14510 DEF FNB4$(Q1$)=FNA$(Q1$(3),Q1$)+"."+FNA$(Q1$(2),Q1$)+"."+FNA4$(Q1$(1),Q1$)
15200 REM " --- bom Detail"
15210 IF L+1>L9 THEN GOSUB 5800
15250 IF BM2$(4,1)="M" THEN GOTO 15280
15255 PRINT (PRTR_DEV)@(H0+8),"______",
15260 PRINT (PRTR_DEV)@(H0+1),W[2]*BOM_QTY,
15280 IF BM2$(4,1)="M" THEN PRINT (PRTR_DEV)@(H0+17),BM2$(35,40),
15290 IF BM2$(4,1)="M" AND CVS(BM2$(65,20),2)<>"" THEN PRINT (PRTR_DEV)'LF',@(H0+17),BM2$(75,20),; LET L=L+1
15300 IF BM2$(4,1)<>"S" THEN GOTO 15360
15310 PRINT (PRTR_DEV)@(H0+17),BM1$,
15320 DIM X2$(60),X7$(62); LET X2$(1)="**Not On File**"
15330 FIND (IVM01_DEV,KEY=N0$+BM1$,DOM=15350)IOL=IVM01A
15340 GOSUB 5900
15350 PRINT (PRTR_DEV)'LF',@(H0+19),X2$,; LET L=L+1
15380 PRINT (PRTR_DEV)'LF',
15385 LET L=L+1
15399 RETURN 
15400 REM " --- configuration detail
15405 FIND (ARE73_DEV,KEY=W0$(1,20),DOM=15900)IOL=ARE73A
15410 IF L+1>L9 THEN GOSUB 5800
15415 PRINT (PRTR_DEV)@(H0+20),"Type: ",WX1$(4,3),
15420 IF WX[1]<>0 THEN PRINT (PRTR_DEV)@(H0+30),"Media: ",WX1$(1,3),
15430 IF WX[1]<>0 THEN PRINT (PRTR_DEV)"  Sets: ",WX[1]:"#0",
15435 PRINT (PRTR_DEV)'LF',
15440 LET L=L+1
15445 IF L+1>L9 THEN GOSUB 5800
15455 PRINT (PRTR_DEV)@(H0+24),"Anzahl User: ",WX[2],
15460 PRINT (PRTR_DEV)'LF',
15465 LET L=L+1
15500 REM " --- serial number detail"
15510 LET ACT$="",DEACT$="",MOD$=""
15520 READ (ARE83_DEV,KEY=W0$(1,20),DOM=15521)
15530 LET ARE83_K$=KEY(ARE83_DEV,END=15600)
15540 IF POS(W0$(1,20)=ARE83_K$)<>1 THEN GOTO 15600
15550 READ (ARE83_DEV,KEY=ARE83_K$)IOL=ARE83A
15560 IF WY1$(1,1)="M" THEN LET MOD$=MOD$+WY2$
15570 IF WY1$(1,1)="A" THEN LET ACT$=ACT$+WY2$
15580 IF WY1$(1,1)="D" THEN LET DEACT$=DEACT$+WY2$
15590 GOTO 15530
15600 REM " --- print them
15610 LET STR$=MOD$,TITLE$="Seriennummer: "
15620 GOSUB 15700
15630 LET STR$=ACT$,TITLE$="Neue Seriennummer: "
15640 GOSUB 15700
15650 LET STR$=DEACT$,TITLE$="Deaktivierte Seriennummer: "
15660 GOSUB 15700
15670 GOTO 15900
15700 REM " --- print them
15705 IF STR$="" THEN GOTO 15790
15710 LET SLINES=INT(LEN(STR$)/40)+1
15715 LET SNUMBS=INT(LEN(STR$)/20)
15720 IF L+SLINES+1>L9 THEN GOSUB 5800
15730 PRINT (PRTR_DEV)@(H0+17),TITLE$; LET L=L+1
15740 PRINT (PRTR_DEV)@(H0+20),
15750 FOR SL=1 TO SNUMBS
15760 PRINT (PRTR_DEV)STR$((SL-1)*20+1,20),"  ",
15770 IF MOD(SL,2)=0 THEN PRINT (PRTR_DEV)'LF',@(H0+20),; LET L=L+1
15780 NEXT SL
15785 IF MOD(SNUMBS,2)<>0 THEN PRINT (PRTR_DEV)'LF',; LET L=L+1
15790 RETURN 
15900 REM " --- Done"
15990 RETURN 

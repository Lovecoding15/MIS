0010 REM "POR - Quality Assurance Work In Process Report (Input Overlay)"
0020 REM "Program POR.RA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=9
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="SYS-01",FILES$[2]="POE-03",FILES$[3]="POE-13"
0120 LET FILES$[4]="POE-63",FILES$[5]="POM-02",FILES$[6]="POM-03"
0125 LET FILES$[7]="APM-01",FILES$[8]="IVM-01"
0150 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],POE03_DEV=CHANNELS[2],POE13_DEV=CHANNELS[3]
0165 LET POE63_DEV=CHANNELS[4],POM02_DEV=CHANNELS[5],POM03_DEV=CHANNELS[6]
0170 LET APM01_DEV=CHANNELS[7],IVM01_DEV=CHANNELS[8]
0200 REM " --- IOLIST's"
0220 APM01A: IOLIST B0$(1),B1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,I5$,I6$,I7$,I8$,I9$,I10$
0280 POS01A: IOLIST P0$,P1$,P2$,P3$,P4$
0290 APS01A: IOLIST X$,U1$,U2$,U3$,X$,M0$
0400 REM " --- Parameters"
0405 DIM I[7]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO",AP$="N",AR$="N",SF$="N",LS$="N",LF$="N"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=POS01A
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET VENDLEN=NUM(U2$(1,2)),I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1))
0445 LET I[2]=NUM(I2$(7,2)),I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2))
0450 LET I[5]=NUM(I2$(13,2)),I[6]=NUM(I2$(3,2)),I[7]=FNYY_YEAR(I4$(3,2))
0460 IF POS(I3$(17,1)="LS")>0 THEN LET LS$="Y"
0465 IF POS(I3$(3,1)="LF")>0 THEN LET LF$="Y"
0500 REM " --- Initializations"
0510 DIM B0$(8),B1$(30)
0550 LET M0=LEN(M0$),BEGVEND$="",ENDVEND$="",BEGDATE$="",ENDDATE$=""
0560 LET BEGREC$="",ENDREC$="",PAGEBREAK$="N",FVENDOR$="",TVENDOR$=""
0600 REM " --- Additional File Opens"
0610 IF LS$="N" THEN GOTO 0700
0620 LET FILES$[9]="POE-23"
0630 CALL "SYC.DA",1,9,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0640 IF STATUS THEN GOTO 9900
0650 LET POE23_DEV=CHANNELS[9]
0700 REM " --- Background"
0710 FOR X=4 TO 16 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(22,3),"Beginning Vendor:",@(25,5),"Ending Vendor:",@(16,7),"Beginning Receipt Date:",@(19,9),"Ending Receipt Date:",@(20,11),"Beginning Receiver:",@(23,13),"Ending Receiver:",@(28,15),"Page Break:",'SF',
0790 GOSUB 5000
1000 REM " --- Beginning vendor"
1010 LET V4$="Enter A Valid Vendor Number (<F1>=First/<F3>=Lookup)"
1020 LET V0$="M",V1$="KCE",V2$=BEGVEND$,V3$=M0$,V0=6,V1=40,V2=3,I0=0
1030 GOSUB 7000
1040 ON V3 GOTO 1100,1150,3900,1050,3900
1050 CALL "SYC.LV",V$
1100 IF V$="" THEN GOTO 1150
1110 LET BEGVEND$=V$
1120 GOTO 1160
1150 LET BEGVEND$="",V$="",V4$="First"
1160 GOSUB 5100
1170 IF STATUS THEN GOTO 1000
1190 GOTO 3900
1200 REM " --- Ending vendor"
1210 LET V4$="Enter A Valid Vendor Number (<F1>=Last/<F3>=Lookup)"
1220 LET V0$="M",V1$="KCE",V2$=ENDVEND$,V3$=M0$,V0=6,V1=40,V2=5
1230 GOSUB 7000
1240 ON V3 GOTO 1300,1350,3900,1250,3900
1250 CALL "SYC.LV",V$
1300 IF V$="" THEN GOTO 1350
1310 IF BEGVEND$<>"" THEN IF V$<BEGVEND$ THEN GOTO 1200
1320 LET ENDVEND$=V$
1340 GOTO 1360
1350 LET ENDVEND$="",V$="",V4$="Last"
1360 GOSUB 5100
1370 IF STATUS THEN GOTO 1200
1390 GOTO 3900
1400 REM " --- Beginning Date
1410 LET V4$="Enter The Beginning Receipt Date To Include On The Register (<F1>=First)"
1420 LET V0$="D",V1$="C6N",V2$=BEGDATE$,V3$="",V0=6,V1=40,V2=7
1430 GOSUB 7000
1440 ON V3 GOTO 1460,1450,3900,1400,3900
1450 LET V$=""
1460 LET BEGDATE$=V$,V4$="First"
1470 GOSUB 5200
1490 GOTO 3900
1600 REM " --- Ending Date
1610 LET V4$="Enter The Ending Receipt Date To Include On The Register (<F1>=Last)"
1620 LET V0$="D",V1$="C6N",V2$=ENDDATE$,V3$="",V0=6,V1=40,V2=9
1630 GOSUB 7000
1640 ON V3 GOTO 1660,1650,3900,1600,3900
1650 LET V$=""
1660 IF V$<>"" THEN IF V$<BEGDATE$ THEN GOTO 1600
1670 LET ENDDATE$=V$,V4$="Last"
1680 GOSUB 5200
1690 GOTO 3900
1800 REM " --- Beginning Receiver"
1810 LET V4$="Enter A Valid Receiver Number (<F1>=First/<F3>=Lookup)"
1820 LET V0$="Z",V1$="KCE",V2$=BEGREC$,V3$="",V0=7,V1=40,V2=11
1830 GOSUB 7000
1840 ON V3 GOTO 1900,1950,3900,1850,3900
1850 CALL "POC.LB","Q",V$
1860 IF LEN(V$)<15 THEN GOTO 1800
1870 LET V$=V$(9,7)
1900 IF V$="" THEN GOTO 1950
1910 LET BEGREC$=V$
1940 GOTO 1980
1950 LET BEGREC$="",V$="First"
1980 GOSUB 5300
1990 GOTO 3900
2000 REM " --- Ending Receiver"
2010 LET V4$="Enter A Valid Receiver Number (<F1>=Last/<F3>=Lookup)"
2020 LET V0$="Z",V1$="KCE",V2$=ENDREC$,V3$="",V0=7,V1=40,V2=13
2030 GOSUB 7000
2040 ON V3 GOTO 2100,2150,3900,2050,3900
2050 CALL "POC.LB","Q",V$
2060 IF LEN(V$)<15 THEN GOTO 2000
2070 LET V$=V$(9,7)
2100 IF V$="" THEN GOTO 2150
2110 IF BEGREC$<>"" THEN IF V$<BEGREC$ THEN GOTO 2000
2120 LET ENDREC$=V$
2140 GOTO 2180
2150 LET ENDREC$="",V$="Last"
2180 GOSUB 5300
2190 GOTO 3900
2200 REM " --- Page break?"
2210 LET V4$="Page Break Between Vendors (Y/N)?"
2220 LET V0$="Y",V1$="C",V2$=PAGEBREAK$,V3$="",V0=1,V1=40,V2=15
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 LET PAGEBREAK$=V$
2290 GOTO 3900
3900 REM " --- Next field"
3910 IF V3=4 THEN GOTO 3990
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,2200,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 IF V3=4 THEN GOTO 9900
4030 ON POS(V$="YN") GOTO 4000,4040,1000
4090 RUN "POR.RB"
5000 REM " --- Display"
5005 LET V1=40,V2=3,V$=BEGVEND$,V4$="First",X=80-(V1+M0+1)
5010 GOSUB 5100
5015 LET V2=5,V$=ENDVEND$,V4$="Last",FVENDOR$=X$
5020 GOSUB 5100
5025 LET V2=7,V$=BEGDATE$,V4$="First",TVENDOR$=X$
5030 GOSUB 5200
5035 LET V2=9,V$=ENDDATE$,V4$="Last"
5040 GOSUB 5200
5050 LET V2=11,V$=BEGREC$,V4$="First"
5055 GOSUB 5300
5060 LET V2=13,V$=ENDREC$,V4$="Last"
5065 GOSUB 5300
5070 PRINT @(V1,15),PAGEBREAK$,
5090 RETURN 
5100 REM " --- Display vendor"
5105 LET X$=""
5110 IF V$="" THEN GOTO 5160
5115 LET B0$(1)=N0$+V$,B1$(1)="",STATUS=11
5120 FIND (APM01_DEV,KEY=B0$,DOM=5190)IOL=APM01A
5130 LET X$=FNF$(B0$(3,VENDLEN),M0$)+" "+B1$(1,30),STATUS=0
5140 PRINT @(V1,V2),X$(1,MIN(LEN(X$),40)),
5150 GOTO 5380
5160 PRINT @(V1,V2),V4$,'CL',
5170 LET X$=V4$
5180 LET STATUS=0
5190 RETURN 
5200 REM " --- Display dates"
5210 IF V$="" THEN LET X$=V4$
5220 IF V$<>"" THEN LET X$=FNB6$(V$)
5230 PRINT @(V1,V2),X$,'CL',
5290 RETURN 
5300 REM " --- Display receiver"
5310 IF V$="" THEN LET V$=V4$
5320 PRINT @(V1,V2),V$,'CL',
5390 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

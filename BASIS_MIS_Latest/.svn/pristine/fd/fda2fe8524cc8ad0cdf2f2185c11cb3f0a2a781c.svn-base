0010 REM "IVR - Lot/Serial Activity Report (Input Overlay)"
0020 REM "Program IVR.6A"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=10
0110 DIM FILES$[FILES],FILEOPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-03"
0130 LET FILES$[4]="IVM-10",FILES$[5]="IVM-14",FILES$[6]="IVM-15"
0140 LET FILES$[7]="IVM-07",FILES$[8]="IVM-08",FILES$[9]="IVM-17"
0150 CALL "SYC.DA",1,1,6,FILES$[ALL],FILEOPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM03_DEV=CHANNELS[3]
0180 LET IVM10_DEV=CHANNELS[4],IVM14_DEV=CHANNELS[5],IVM15_DEV=CHANNELS[6]
0200 REM " --- IOLIST's"
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0280 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$
0285 GLS01A: IOLIST G0$,G1$,G2$,G3$(1),G4$
0400 REM " --- Parameters"
0405 DIM P[5],G[1],G3$(325),I[1],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="IV",VH$=""
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0445 IF POS(I3$(17,1)="LS")=0 THEN GOTO 4900
0450 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1)),P[2]=NUM(I2$(7,2))
0455 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0460 LET G[0]=NUM(G2$(1,2))
0465 CALL "SYC.VA","AP",INFO$[ALL]
0470 LET AP$=INFO$[20]
0475 IF AP$="N" THEN GOTO 0500
0480 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0490 LET I[0]=NUM(P2$(1,2)),FILES$[10]="APM-01",VH$="Vendor"
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM FILES[15],PARAMS$[3],OPTIONS$[7]
0550 LET FILES[1]=IVM01_DEV,FILES[10]=IVM10_DEV,FILES[14]=IVM14_DEV
0555 LET PARAMS$[0]=N0$,PARAMS$[1]=I1$,PARAMS$[2]=I2$,PARAMS$[3]=I3$,M9$="00"
0570 LET BEGPER$=I4$(1,2),BEGYEAR$=I4$(3,2),ENDPER$=BEGPER$,ENDYEAR$=BEGYEAR$
0575 LET FILES[15]=IVM15_DEV,SEQUENCE$="I",STATUS$="B",BEGLOT$="",ENDLOT$=""
0580 LET M0=LEN(M0$)
0600 REM " --- Additional file opens"
0610 CALL "SYC.DA",1,7,FILES,FILES$[ALL],FILEOPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0615 IF STATUS>0 THEN GOTO 9900
0620 LET IVM07_DEV=CHANNELS[7],IVM08_DEV=CHANNELS[8]
0640 LET IVM17_DEV=CHANNELS[9],APM01_DEV=CHANNELS[10]
0700 REM " --- Background"
0710 PRINT 'SB',@(30,3),"Sequence:",@(18,4),"Open, Closed Or Both:",@(22,5),"Beginning Period:",@(24,6),"Beginning Year:",@(25,7),"Ending Period:",@(27,8),"Ending Year:",'SF',
0720 CALL "IVC.IB","B","D",9,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0730 PRINT 'SB',@(11,16),"Beginning Lot/Serial Number:",@(14,17),"Ending Lot/Serial Number:",'SF',
0900 REM 
0990 GOSUB 5000
1000 REM " --- Sequence"
1010 LET V4$="List Items By Item Number Or Lot/Serial # (I=Item Number/L=Lot/Serial Number)"
1020 LET V0$="S",V1$="CE",V2$=SEQUENCE$,V3$="IL",V0=1,V1=40,V2=3,I0=0
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1000,3900
1050 LET SEQUENCE$=V$
1090 GOTO 3900
1200 REM " --- Open/Closed/Both"
1210 LET V4$="List Open Lot/Serial Numbers, Closed Or Both (O=Open/C=Closed/B=Both)?"
1220 LET V0$="S",V1$="C",V2$=STATUS$,V3$="",V0=1,V1=40,V2=4
1230 GOSUB 7000
1240 ON V3 GOTO 1250,1200,3900,1200,3900
1250 LET STATUS$=V$
1290 GOTO 3900
1400 REM " --- Beginning Period"
1410 LET V0$="N",V1$="C",V2$=BEGPER$,V3$=M9$,V0=2,V1=40,V2=5
1420 LET V4$="Enter The Beginning Period For This Report (1 Through "+STR(G[0])+")"
1430 GOSUB 7000
1440 ON V3 GOTO 1450,1400,3900,1400,3900
1450 IF V<1 OR V>G[0] THEN GOTO 1400
1460 LET BEGPER$=STR(V:M9$),PERIOD=V,DESCRIPTION$=G3$((V-1)*16+27,16)
1470 PRINT @(V1,V2),BEGPER$," ",DESCRIPTION$,
1490 GOTO 3900
1600 REM " --- Beginning Year"
1610 LET V0$="D",V1$="CY",V2$=BEGYEAR$,V3$="",V0=2,V1=40,V2=6
1620 LET V4$="Enter The Beginning Year For This Report"
1630 GOSUB 7000
1640 ON V3 GOTO 1650,1600,3900,1600,3900
1650 IF V$="" THEN GOTO 1600
1660 LET BEGYEAR$=V$
1670 PRINT @(V1,V2),FNYY21_YY$(BEGYEAR$),
1690 GOTO 3900
1800 REM " --- Ending Period"
1810 LET V0$="N",V1$="C",V2$=ENDPER$,V3$=M9$,V0=2,V1=40,V2=7
1820 LET V4$="Enter The Beginning Period For This Report (1 Through "+STR(G[0])+")"
1830 GOSUB 7000
1840 ON V3 GOTO 1850,1800,3900,1800,3900
1850 IF V<1 OR V>G[0] THEN GOTO 1800
1860 LET ENDPER$=STR(V:M9$),DESCRIPTION$=G3$((V-1)*16+27,16)
1870 PRINT @(V1,V2),ENDPER$," ",DESCRIPTION$,
1890 GOTO 3900
2000 REM " --- Ending Year"
2010 LET V0$="D",V1$="CY",V2$=ENDYEAR$,V3$="",V0=2,V1=40,V2=8
2020 LET V4$="Enter The Ending Year For This Report"
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 IF V$="" THEN GOTO 2000
2060 LET ENDYEAR$=V$
2070 PRINT @(V1,V2),FNYY21_YY$(ENDYEAR$),
2090 GOTO 3900
2200 REM " --- Process input"
2210 CALL "IVC.IB","I","D",9,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
2220 ON V3 GOTO 2230,2200,3900,2200,3900
2290 GOTO 3900
2400 REM " --- Beginning Lot/Serial Number"
2410 LET V4$="Enter The Beginning Lot/Serial Number To Include On This Report (<F1>=First)"
2420 LET V0$="S",V1$="KCE",V2$=BEGLOT$,V3$="",V0=P[2],V1=40,V2=16
2430 GOSUB 7000
2440 ON V3 GOTO 2460,2450,3900,2400,3900
2450 LET V$=""
2460 LET BEGLOT$=V$,X$="First"
2475 IF BEGLOT$<>"" THEN LET X$=BEGLOT$
2480 PRINT @(V1,V2),X$,'CL',
2490 GOTO 3900
2600 REM " --- Ending Lot/Serial Number"
2610 LET V4$="Enter The Ending Lot/Serial Number To Include On This Report (<F1>=Last)"
2620 LET V0$="S",V1$="KCE",V2$=ENDLOT$,V3$="",V0=P[2],V1=40,V2=17
2630 GOSUB 7000
2640 ON V3 GOTO 2660,2650,3900,2600,3900
2650 LET V$=""
2660 LET ENDLOT$=V$,X$="Last"
2670 IF BEGLOT$<>"" THEN IF ENDLOT$<BEGLOT$ THEN GOTO 2600
2675 IF ENDLOT$<>"" THEN LET X$=ENDLOT$
2680 PRINT @(V1,V2),X$,'CL',
2690 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,2200,2400,2600,3980
3980 GOSUB 6900
3990 IF STATUS>0 THEN GOTO 1000
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4030 ON POS(V$="YN") GOTO 4000,4050,1000
4090 RUN "IVR.6B"
4900 REM " --- Lotted/Serialized Inventory not enabled"
4910 CALL "IVC.SB","L",STATUS
4990 GOTO 9900
5000 REM " --- Display"
5010 LET V=NUM(BEGPER$),V1$=G3$((V-1)*16+27,16),V3$="First"
5020 LET V=NUM(ENDPER$),V2$=G3$((V-1)*16+27,16),V4$="Last"
5030 IF BEGLOT$<>"" THEN LET V3$=BEGLOT$
5040 IF ENDLOT$<>"" THEN LET V3$=ENDLOT$
5050 PRINT @(40,3),SEQUENCE$,@(40,4),STATUS$,@(40,5),BEGPER$," ",V1$,@(40,6),FNYY21_YY$(BEGYEAR$),@(40,7),ENDPER$," ",V2$,@(40,8),FNYY21_YY$(ENDYEAR$),
5060 CALL "IVC.IB","D","D",9,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
5070 PRINT @(40,16),V3$,'CL',@(40,17),V4$,'CL',
5090 RETURN 
6900 REM " --- Verify that beginning period/year > ending period/year"
6910 DIM MESSAGE$[1]
6920 LET STATUS=0
6950 IF ENDYEAR$+ENDPER$>=BEGYEAR$+BEGPER$ THEN GOTO 6990
6960 LET MESSAGE$[0]="Beginning Period/Year Cannot Be Greater Than Ending Period/Year"
6970 LET MESSAGE$[1]="            Press <Enter> To Continue",STATUS=1
6980 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

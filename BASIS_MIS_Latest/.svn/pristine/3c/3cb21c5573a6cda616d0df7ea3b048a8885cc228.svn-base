0010 REM "POC - Line Item Additional Options Entry"
0020 REM "Program POC.AO"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0043 REM "TYPE$      : Record Type R=Requisition/P=Purchase Order (input)"
0045 REM "CHANNELS[n]: Channel number (input)"
0047 REM "             [1]=POM-04   [3]=WOE-22
0050 REM "             [2]=WOE-01   [4]=WOE-32"
0053 REM "PARAMS$[n] : Parameters (input/returned)"
0055 REM "             [0]=Line Item (1st string)    [3]=Line Code"
0057 REM "             [1]=Line Item (2nd string)    [4]=Shop Floor (Y/N)"
0060 REM "             [2]=Line Item (3rd string)    [5]=Special Order (Y/N)"
0063 REM "LINE       : Line for window (input)"
0065 REM "STATUS     : 0=No error (returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER TYPE$,CHANNELS[ALL],PARAMS$[ALL],LINE,STATUS
0100 REM " --- Open/Lock Files"
0110 LET POM04_DEV=CHANNELS[1],WOE01_DEV=CHANNELS[2]
0120 LET WOE22_DEV=CHANNELS[3],WOE32_DEV=CHANNELS[4]
0200 REM " --- IOLIST's"
0210 WOE01A: IOLIST A0$(1)
0220 WOE22A: IOLIST B0$(1),B1$(1),B[ALL]
0230 WOE32A: IOLIST B0$(1),B1$(1),B[ALL]
0240 POM04A: IOLIST D0$(1),D1$(1)
0400 REM " --- Parameters"
0410 LET W0$=PARAMS$[0],W1$=PARAMS$[1],W2$=PARAMS$[2]
0420 LET Y1$=PARAMS$[3],SF$=PARAMS$[4],SO$=PARAMS$[5]
0500 REM " --- Initializations"
0510 DIM A0$(75),B0$(15),B0$(15),B1$(151),B[13],D0$(5),D1$(32),MESSAGE$[1]
0550 LET WIDTH=52,HEIGHT=7,WIN_X=28,WIN_Y=LINE-HEIGHT
0560 LET N0$=W0$(1,2),TITLE$="Additional Options",WO$="N"
0570 IF SF$="N" THEN GOTO 0700
0575 IF POS(Y1$(21,1)="SN")=0 THEN GOTO 0700
0580 IF Y1$(21,1)="S" THEN IF SO$<>"Y" THEN GOTO 0700
0590 LET WO$="Y"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT 'SB',@(3,0),"Date Promised:",@(1,1),"Not Before Date:",@(9,2),"Message:",'SF',
0730 IF WO$="Y" THEN PRINT 'SB',@(6,3),"Work Order:",'SF',
0790 GOSUB 5000
1000 REM " --- Date Promised"
1010 LET V4$="Delivery Was Promised By This Date"
1020 LET V0$="D",V1$="C6N",V2$=W1$(9,6),V3$="",V0=6,V1=18,V2=0,I0=0
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1000,3900
1050 LET W1$(9,6)=V$
1090 GOTO 3900
1200 REM " --- Not Before Date"
1210 LET V4$="Delivery Can Not Be Made Before This Date"
1220 LET V0$="D",V1$="C6N",V2$=W1$(15,6),V3$="",V0=6,V1=18,V2=1
1230 GOSUB 7000
1240 ON V3 GOTO 1250,1200,3900,1200,3900
1250 LET W1$(15,6)=V$
1290 GOTO 3900
1400 REM " --- Message"
1410 LET V4$="Message To Print With This Line (<F3>=Lookup)"
1420 LET V0$="S",V1$="C",V2$=W1$(38,3),V3$="",V0=3,V1=18,V2=2
1425 GOSUB 7000
1430 ON V3 GOTO 1450,1400,3900,1440,3900
1440 CALL "POC.LK",2,V1,V2,V$
1450 LET D0$(1)=N0$+V$,D1$(1)=""
1460 IF FNP$(V$)<>"" THEN FIND (POM04_DEV,KEY=D0$,DOM=1400)IOL=POM04A
1470 PRINT @(V1,V2),D0$(3)," ",D1$(1,20),
1480 LET W1$(38,3)=V$
1490 GOTO 3900
1600 REM " --- Work Order Number"
1610 IF WO$="N" THEN GOTO 3900
1620 LET V4$="Work Order For This Item (<F1>=None/<F3>=Lookup)"
1625 LET V0$="Z",V1$="C",V2$=W2$(1,7),V3$="",V0=7,V1=18,V2=3
1630 GOSUB 7000
1635 ON V3 GOTO 1650,1800,3900,1640,3900
1640 CALL "SYC.LW",V$,""
1645 IF V$="" THEN GOTO 1600
1650 IF V$="" THEN GOTO 1800
1660 LET A0$(1)=N0$+"  "+V$
1670 FIND (WOE01_DEV,KEY=A0$(1,11),DOM=1600)IOL=WOE01A
1680 IF POS(A0$(15,1)="QC")=0 THEN GOTO 1750
1690 LET MESSAGE$[0]="This Work Order Is A Quote Or Closed (<Enter>=Continue)"
1700 CALL "SYC.XA",2,MESSAGE$[ALL],0,WIN_Y+HEIGHT,WIN_X,V$,V3
1710 IF V$<>"SPECIAL" THEN GOTO 1600
1750 LET W2$(1,7)=A0$(5,7)
1760 PRINT @(V1,V2),W2$(1,7),
1790 GOTO 3900
1800 REM " --- No work order"
1810 LET W2$(1,10)=""
1820 PRINT @(V1,V2),'CL',
1890 GOTO 3900
2000 REM " --- Work Order Materials/Subcontract Line"
2010 IF WO$="N" THEN GOTO 3900
2030 IF FNP$(W2$(1,7))="" THEN GOTO 3900
2040 LET V4$="Materials",X$="B",CHANNEL=WOE22_DEV
2050 IF Y1$(21,1)="N" THEN LET V4$="Subcontracts",X$="C",CHANNEL=WOE32_DEV
2060 LET V4$="Work Order "+V4$+" Line For This Item"
2070 LET V0$="Z",V1$="CR",V2$=W2$(8,3),V3$="",V0=3,V1=26,V2=3
2080 GOSUB 7000
2090 ON V3 GOTO 2100,2000,3900,2000,3900
2100 LET B0$=N0$+"  "+W2$(1,7)+X$+V$
2110 FIND (CHANNEL,KEY=B0$,DOM=2000)
2120 LET W2$(8,3)=V$
2190 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,2000,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="Y",V1$="CER",V2$="Y",V3$="",V0=1,V2=HEIGHT-3,V1=FNV1(V4$,WIDTH)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4500,4000,4000,9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Make sure that work orders have valid line"
4110 IF WO$="N" THEN GOTO 4200
4120 IF FNP$(W2$(1,7))="" THEN GOTO 4200
4130 LET B0$(1)=N0$+"  "+W2$(1,7)+"B"+W2$(8,3),CHANNEL=WOE22_DEV
4140 IF Y1$(21,1)="N" THEN LET B0$(12,1)="C",CHANNEL=WOE32_DEV
4150 FIND (CHANNEL,KEY=B0$,DOM=4170)
4160 GOTO 4200
4170 LET MESSAGE$[0]="Detail Line Number Is Required When A Work Order"
4175 LET MESSAGE$[1]="Is Specified (<Enter>=Continue)",V3=0,I0=3
4180 CALL "SYC.XA",2,MESSAGE$[ALL],1,WIN_Y+HEIGHT,WIN_X,V$,V3
4190 IF V$<>"SPECIAL" THEN GOTO 3900
4200 REM " --- Return updated record"
4210 LET PARAMS$[1]=W1$,PARAMS$[2]=W2$
4290 GOTO 9900
5000 REM " --- Display ribbon information"
5010 LET PROMISED$="",BEFORE$="",WORKORDER$=""
5020 IF FNP$(W1$(9,6))<>"" THEN LET PROMISED$=FNB6$(W1$(9,6))
5030 IF FNP$(W1$(15,6))<>"" THEN LET BEFORE$=FNB6$(W1$(15,6))
5040 IF WO$="N" THEN GOTO 5060
5045 IF FNP$(W2$(1,7))="" THEN GOTO 5060
5050 LET WORKORDER$=W2$(1,7)
5055 IF FNP$(W2$(8,3))<>"" THEN LET WORKORDER$=WORKORDER$+" "+W2$(8,3)
5060 LET D0$(1)=N0$+W1$(38,3),D1$(1)=""
5070 FIND (POM04_DEV,KEY=D0$,DOM=5080)IOL=POM04A
5080 PRINT @(18,0),PROMISED$,'CL',@(18,1),BEFORE$,'CL',@(18,2),D0$(3)," ",D1$(1,20),@(18,3),WORKORDER$,'CL',
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV1(Q$,Q)=FNC(Q$,Q)+LEN(Q$)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9950 EXIT 
9999 END

0010 REM "POE - Purchase Order Invoice Entry (Header/Ribbon)"
0020 REM "Program POE.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "04Dec97 KJS: Use correct array element for adjustments (3620,3650,5100)"
0032 REM "17Jun98 WGH: Don't allow AP type unless using multiple AP types (1205)"
0033 REM "12Aug98 WGH: Allow invoicing more than 99 PO detail lines on one invoice (550)
0034 REM "18May99 KJS: Avoid endless loop if there's an error in calling GLC.BA (480)"
0040 REM 
0050 SETERR 9000
0060 SETESC 9000
0070 ON O0 GOTO 0080,4800,4900,4200
0080 BEGIN 
0085 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=18
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="SYS-01",FILES$[2]="POE-05",FILES$[3]="POE-15"
0120 LET FILES$[4]="POE-25",FILES$[5]="POE-35",FILES$[6]="POE-02"
0125 LET FILES$[7]="POE-12",FILES$[8]="POM-02",FILES$[9]="POT-04"
0130 LET FILES$[10]="POT-14",FILES$[11]="APM-01",FILES$[12]="APM-02"
0135 LET FILES$[13]="APM-09",FILES$[14]="APM-10",FILES$[15]="APT-01"
0140 LET FILES$[16]="IVM-01"
0155 CALL "SYC.DA",1,1,16,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0165 LET SYS01_DEV=CHANNELS[1],POE05_DEV=CHANNELS[2],POE15_DEV=CHANNELS[3]
0170 LET POE25_DEV=CHANNELS[4],POE35_DEV=CHANNELS[5],POE02_DEV=CHANNELS[6]
0175 LET POE12_DEV=CHANNELS[7],POM02_DEV=CHANNELS[8],POT04_DEV=CHANNELS[9]
0180 LET POT14_DEV=CHANNELS[10],APM01_DEV=CHANNELS[11],APM02_DEV=CHANNELS[12]
0185 LET APM09_DEV=CHANNELS[13],APM10_DEV=CHANNELS[14],APT01_DEV=CHANNELS[15]
0190 LET IVM01_DEV=CHANNELS[16]
0200 REM " --- IOLIST's"
0210 POE05A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0220 APM01A: IOLIST B0$(1),B1$(1)
0230 APM10A: IOLIST C0$(1)
0240 APM10B: IOLIST D0$(1)
0250 APM02A: IOLIST U0$(1),U1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,I5$,I6$,M2$,M3$
0280 POS01A: IOLIST R0$,R1$,R2$,R3$,R4$
0285 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0290 APS01A: IOLIST X$,P1$,P2$,P3$,X$,M0$,M1$
0300 APM10C: IOLIST S0$(1),S[ALL]
0310 APM10D: IOLIST T0$(1)
0400 REM " --- Parameters"
0405 DIM I[6],P[3],G[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO",GL$="N",STATUS=0,SOURCE$=PGM(-2)
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=POS01A
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0435 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0440 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1)),I[2]=NUM(I2$(7,2))
0445 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0450 LET I[6]=NUM(I2$(3,2)),P[0]=NUM(P2$(1,2))
0455 FOR X=0 TO 4
0460 LET G[X]=NUM(G2$(X*2+1,2),ERR=0465)
0465 NEXT X
0470 LET G[2]=FNYY_YEAR(G2$(5,2))
0480 CALL "GLC.BA",ERR=0481,SOURCE$,N2$,GLW11$,GL$,STATUS
0490 IF STATUS THEN GOTO 9900
0500 REM " --- Initializations"
0510 DIM A0$(20),A1$(64),A[8],B0$(8),B1$(195),MESSAGE$[2],VENDOR$(8)
0520 DIM DESCRIPTION$[1],FIELD[1],POSITION[1],LENGTH[1]
0550 LET A0$(1)=N0$,L0=11,INCREMENT=10,INCDETAIL=5,HL=3,APTYPE$="",RETENTION$=""
0560 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),RP=79-M1
0565 LET PM$="(###) ###-####",NF$="(Not On File)",FT$=" For This Invoice"
0570 LET DEFAULT$=F0$(18,6),NAME$="",L9=21
0580 IF P3$(1,1)="Y" THEN LET HL=4,APTYPE$="AP Type:"
0590 IF P3$(3,1)="Y" THEN LET RETENTION$="Retention Amount:"
0600 REM " --- Additional file opens"
0610 IF GL$<>"Y" THEN GOTO 0700
0620 LET FILES$[17]="GLM-01",FILES$[18]=GLW11$,OPTIONS$[18]="C"
0630 CALL "SYC.DA",1,17,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0640 IF STATUS THEN GOTO 9900
0650 LET GLM01_DEV=CHANNELS[17],GLW11_DEV=CHANNELS[18]
0700 REM " --- Background"
0710 PRINT 'SB',@(1,3),"Vendor:",@(3,4),"Name:",@(0,5),"Address:",@(47,3),APTYPE$,@(47,HL),"Invoice:",@(47,HL+1),"Contact:",@(49,HL+2),"Phone:",@(51,HL+3),"Fax:",'SF',
0720 GOSUB 5200
0800 REM " --- Get default accounting date"
0810 CALL "SYC.WA",0,46,3,17,L0-3,"",NAME$
0820 PRINT 'SB',@(1,0),"Enter The Default Accounting Date:",'SF',
0830 LET V0$="D",V1$="R6N",V2$=DEFAULT$,V3$="",V4$="",V0=8,V1=37,V2=0
0840 GOSUB 7000
0850 ON V3 GOTO 0860,0830,0830,0830,9900
0860 LET DEFAULT$=V$
0890 CALL "SYC.WD",NAME$
0900 REM " --- Position files"
0990 READ (POE05_DEV,KEY=A0$,DOM=1000)
1000 REM " --- Vendor number"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(POE05_DEV,END=1050)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(5,6)
1050 LET V0$="M",V1$="KCE",V3$=P1$,V0=6,V1=9,V2=3
1055 LET V4$="Enter A Valid Vendor (<F3>=Lookup)"
1060 GOSUB 7000
1070 ON V3 GOTO 1100,1100,1000,1080,9900
1080 CALL "SYC.LV",V$
1090 IF V$="" THEN GOTO 0900
1100 REM " --- Retrieve and display vendor info"
1110 DIM B0$(8),B1$(195),U0$(10),U1$(25)
1115 LET B0$(1)=N0$+V$,U0$(1)=B0$,K$=U0$
1120 CALL "POC.VA","VCN",B0$,P[0],M0$,APM01_DEV,APM09_DEV,R3$,L0,STATUS
1125 IF STATUS THEN GOTO 1000
1130 LET A0$(1)=N0$,A0$(5)=B0$(3),PHONE$="",FAX$=""
1135 FIND (APM01_DEV,KEY=B0$,DOM=1000)IOL=APM01A
1140 IF FNP$(B1$(112,10))<>"" THEN LET PHONE$=FNF$(B1$(112,10),PM$)
1145 IF FNP$(B1$(186,10))<>"" THEN LET FAX$=FNF$(B1$(186,10),PM$)
1150 PRINT @(56,HL+1),B1$(126,20),@(56,HL+2),PHONE$,'CL',@(56,HL+3),FAX$,'CL',
1170 READ (APM02_DEV,KEY=K$,DOM=1180)IOL=APM02A
1175 GOTO 1190
1180 LET K$=KEY(APM02_DEV,END=1190)
1190 IF POS(B0$=K$)=1 THEN LET A0$(3,2)=K$(9)
1200 REM " --- AP Type"
1205 IF P3$(1,1)<>"Y" THEN LET A0$(3,2)=""; GOTO 1290
1210 LET V4$="Enter A Valid AP Type (<F3>=Lookup)"
1215 LET V0$="S",V1$="C",V2$=A0$(3,2),V3$="",V0=2,V1=56,V2=3
1220 GOSUB 7000
1225 ON V3 GOTO 1240,1200,1000,1230,1000
1230 GOSUB 6000
1235 IF V$="" THEN GOTO 1200
1240 GOSUB 5500
1250 IF STATUS THEN GOTO 1200
1260 LET A0$(3,2)=V$
1290 READ (POE05_DEV,KEY=A0$,DOM=1300)
1300 REM " --- Invoice Number"
1310 LET V2$="",V2$=KEY(POE05_DEV,END=1340)
1320 IF POS(A0$(1,10)=V2$)<>1 THEN LET V2$=""
1330 IF V2$<>"" THEN LET V2$=V2$(11)
1340 LET V4$="Enter A Valid Invoice Number"
1350 LET V0$="S",V1$="CR",V3$="",V0=10,V1=56,V2=HL
1355 GOSUB 7000
1360 ON V3 GOTO 1390,1300,1370,1300,1000
1370 IF P3$(1,1)<>"Y" THEN GOTO 1000
1380 GOTO 1200
1390 LET A0$(11)=V$,ADJUST=0,K$=A0$(1,20)+"00"
1400 REM " --- Read invoice header"
1410 FIND (APT01_DEV,KEY=K$,DOM=1450)
1420 LET ADJUST=1
1450 FIND (POE05_DEV,KEY=A0$,DOM=1500)IOL=POE05A
1480 GOSUB 5000
1490 GOTO 4000
1500 REM " --- New record?"
1510 CALL "SYC.YN",1,"Is This A New Record",2,V$,V3
1520 ON V3 GOTO 1530,1500,1500,1500,1300
1590 ON POS(V$="YN") GOTO 1500,1600,1300
1600 REM " --- New record initializations"
1610 DIM A1$(64),A[8],U0$(10),U1$(24)
1620 LET U0$(1)=B0$+A0$(3,2)
1630 FIND (APM02_DEV,KEY=U0$,DOM=1000)IOL=APM02A
1640 LET A1$(1,6)=F0$(18,6),A1$(7,6)=DEFAULT$,DISPLAY=0,A1$(25,1)="N"
1650 LET A1$(28,2)=U1$(1,2),A1$(30,2)=U1$(3,2),A1$(26,2)=U1$(5,2)
1660 GOSUB 5800
1670 GOSUB 6500
1690 GOSUB 5000
2000 REM " --- Distribution Code"
2010 LET V4$="Enter A Valid Distribution Code (<F3>=Lookup)",I0=0,DISPLAY=1
2020 LET V0$="S",V1$="KC",V2$=A1$(28,2),V3$="",V0=2,V1=19,V2=L0-1
2030 GOSUB 7000
2040 ON V3 GOTO 2100,2000,1400,2050,3900
2050 GOSUB 6100
2060 IF V$="" THEN GOTO 2000
2100 REM " --- Valid distribution code?"
2110 GOSUB 5600
2120 IF STATUS THEN GOTO 2000
2130 LET A1$(28,2)=V$
2190 GOTO 3900
2200 REM " --- Payment Group"
2210 LET V4$="Enter A Valid Payment Group (<F3>=Lookup)",DISPLAY=1
2220 LET V0$="S",V1$="KC",V2$=A1$(30,2),V3$="",V0=2,V1=19,V2=L0
2230 GOSUB 7000
2240 ON V3 GOTO 2300,2200,3900,2250,3900
2250 GOSUB 6200
2260 IF V$="" THEN GOTO 2200
2300 REM " --- Valid payment group?"
2310 GOSUB 5700
2320 IF STATUS THEN GOTO 2200
2330 LET A1$(30,2)=V$
2390 GOTO 3900
2400 REM " --- Terms Code
2410 LET V4$="Enter A Valid Terms Code (<F3>=Lookup)",DISPLAY=1
2420 LET V0$="S",V1$="KC",V2$=A1$(26,2),V3$="",V0=2,V1=19,V2=L0+1
2430 GOSUB 7000
2440 ON V3 GOTO 2500,2400,3900,2450,3900
2450 GOSUB 6300
2460 IF V$="" THEN GOTO 2400
2500 REM " --- Valid terms?
2510 GOSUB 5800
2520 IF STATUS THEN GOTO 2400
2530 LET A1$(26,2)=V$
2590 GOTO 3900
2600 REM " --- Hold Invoice?"
2610 LET V4$="Hold This Invoice (Y/N)?"
2620 LET V0$="Y",V1$="CR",V2$=A1$(25,1),V3$="",V0=1,V1=19,V2=L0+2
2630 GOSUB 7000
2640 ON V3 GOTO 2650,2600,3900,2600,3900
2650 LET A1$(25,1)=V$
2690 GOTO 3900
2700 REM " --- Invoice Date"
2710 LET V4$="Enter The Date Of This Invoice"
2720 LET V0$="D",V1$="CR6N",V2$=A1$(1,6),V3$="",V0=8,V1=19,V2=L0+3
2730 GOSUB 7000
2740 ON V3 GOTO 2750,2700,3900,2700,3900
2750 LET A1$(1,6)=V$
2760 IF FNP$(A1$(7,6))="" THEN LET A1$(7,6)=V$
2800 REM " --- Calculate Discount and Due Dates"
2810 GOSUB 6500
2820 PRINT @(19,L0+4),FNB6$(A1$(7,6)),@(19,L0+5),FNB6$(A1$(13,6)),@(19,L0+6),FNB6$(A1$(19,6)),
2890 GOTO 3900
2900 REM " --- Accounting Date"
2910 LET V4$="Enter The Accounting Date"+FT$,STATUS=0
2920 LET V0$="D",V1$="CR6N",V2$=A1$(7,6),V3$="",V0=8,V1=19,V2=L0+4
2930 GOSUB 7000
2940 ON V3 GOTO 2950,2900,3900,2900,3900
2950 IF GL$="Y" THEN CALL "GLC.EA",V$,"Y",PERIOD$,YEAR$,STATUS
2960 IF STATUS>99 THEN GOTO 2900
2970 LET A1$(7,6)=V$
2990 GOTO 3900
3000 REM " --- Due Date"
3010 LET V4$="Enter The Due Date"+FT$
3020 LET V0$="D",V1$="CR6N",V2$=A1$(13,6),V3$="",V0=8,V1=19,V2=L0+5
3030 GOSUB 7000
3040 ON V3 GOTO 3050,3000,3900,3000,3900
3050 LET A1$(13,6)=V$
3090 GOTO 3900
3100 REM " --- Discount Date"
3110 LET V4$="Enter The Discount Date"+FT$
3120 LET V0$="D",V1$="C6N",V2$=A1$(19,6),V3$="",V0=8,V1=19,V2=L0+6
3130 GOSUB 7000
3140 ON V3 GOTO 3150,3100,3900,3100,3900
3150 LET A1$(19,6)=V$,DISCOUNT$=FILL(8)
3160 IF FNP$(A1$(19,6))<>"" THEN LET DISCOUNT$=FNB6$(A1$(19,6))
3170 PRINT @(V1,V2),DISCOUNT$,
3190 GOTO 3900
3200 REM " --- Reference"
3210 LET V4$="Enter Any Optional Reference Information"+FT$
3220 LET V0$="S",V1$="C",V2$=A1$(32,10),V3$="",V0=10,V1=19,V2=L0+7
3230 GOSUB 7000
3240 ON V3 GOTO 3250,3200,3900,3200,3900
3250 LET A1$(32,10)=V$
3290 GOTO 3900
3300 REM " --- Memo"
3310 LET V4$="Enter Any Optional Memo Information"+FT$
3320 LET V0$="S",V1$="C",V2$=A1$(42,16),V3$="",V0=16,V1=19,V2=L0+8
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3300,3900,3300,3900
3350 LET A1$(42,16)=V$
3390 GOTO 3900
3400 REM " --- Invoice Amount"
3405 IF ADJUST THEN GOTO 3900
3410 LET V4$="Enter The Total Amount Of This Invoice"
3420 LET V0$="N",V1$="C",V2$=STR(A[0]),V3$=M1$,V0=M1,V1=RP,V2=L0-1
3430 GOSUB 7000
3440 ON V3 GOTO 3450,3400,3900,3400,3900
3450 LET A[0]=V,A[3]=V
3460 PRINT @(RP,L0),A[3]:M1$,
3490 GOTO 3900
3500 REM " --- Net Invoice Amount"
3505 IF ADJUST THEN GOTO 3900
3510 LET V4$="Enter The Net Discountable Invoice Amount"
3520 LET V0$="N",V1$="C",V2$=STR(A[3]),V3$=M1$,V0=M1,V1=RP,V2=L0
3530 GOSUB 7000
3540 ON V3 GOTO 3550,3500,3900,3500,3900
3550 LET A[3]=V
3560 LET A[1]=A[3]*S[0]/100
3570 PRINT @(RP,L0+2),A[1]:M1$,
3590 GOTO 3900
3600 REM " --- Adjustment amount"
3605 IF ADJUST=0 THEN GOTO 3900
3610 LET V4$="Enter The Adjustment Amount"+FT$
3620 LET V0$="N",V1$="C",V2$=STR(A[0]),V3$=M1$,V0=M1,V1=RP,V2=L0+1
3630 GOSUB 7000
3640 ON V3 GOTO 3650,3600,3900,3600,3900
3650 LET A[0]=V
3690 GOTO 3900
3700 REM " --- Discount amount"
3710 LET V4$="Enter Any Discount Amount"+FT$
3720 LET V0$="N",V1$="C",V2$=STR(A[1]),V3$=M1$,V0=M1,V1=RP,V2=L0+2
3730 GOSUB 7000
3740 ON V3 GOTO 3750,3700,3900,3700,3900
3750 LET A[1]=V
3790 GOTO 3900
3800 REM " --- Retention amount"
3810 IF P3$(3,1)<>"Y" THEN GOTO 3900
3820 IF P3$(4,1)="Y" THEN GOTO 3900
3830 LET V4$="Enter Any Retention Amount"+FT$
3840 LET V0$="N",V1$="C",V2$=STR(A[2]),V3$=M1$,V0=M1,V1=RP,V2=L0+3
3850 GOSUB 7000
3860 ON V3 GOTO 3870,3800,3900,3800,3900
3870 LET A[2]=V
3890 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,2400,2600,2700,2900,3000,3100,3200,3300,3400,3500,3600,3700,3800,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,1000
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Update header"
4110 LET O0=0
4120 WRITE (POE05_DEV,KEY=A0$)IOL=POE05A
4190 RUN "POE.EB"
4200 REM " --- Delete invoice"
4210 LET CHANNEL=POE35_DEV
4220 IF GL$="Y" THEN GOSUB 6600
4230 LET CHANNEL=POE25_DEV
4240 GOSUB 6600
4250 LET CHANNEL=POE15_DEV
4260 GOSUB 6600
4270 REMOVE (POE05_DEV,KEY=A0$,DOM=4280)
4280 GOSUB 5200
4290 GOTO 1000
4800 REM " --- Returning from completed invoice entry"
4810 GOSUB 5200
4890 GOTO 1000
4900 REM " --- Returning from PO Selection overlay"
4910 GOSUB 5200
4920 GOSUB 5000
4990 GOTO 4000
5000 REM " --- Display record"
5010 LET INVOICE$="",NET$="",ADJUSTMENT$="",RETAIN$="",DISCOUNT$="",DISPLAY=1
5020 LET V1=19,V2=L0-1,V$=A1$(28,2)
5030 GOSUB 5600
5040 LET V1=19,V2=L0,V$=A1$(30,2)
5050 GOSUB 5700
5060 LET V1=19,V2=L0+1,V$=A1$(26,2)
5070 GOSUB 5800
5080 IF ADJUST THEN GOTO 5100
5085 LET INVOICE$=STR(A[0]:M1$),NET$=STR(A[3]:M1$),X$="(Invoice)"
5090 GOTO 5150
5100 LET ADJUSTMENT$=STR(A[0]:M1$),X$="(Adjustment)"
5150 IF P3$(3,1)="Y" THEN LET RETAIN$=STR(A[2]:M1$)
5160 IF FNP$(A1$(19,6))<>"" THEN LET DISCOUNT$=FNB6$(A1$(19,6))
5170 PRINT @(67,HL),X$,@(19,L0+2),A1$(25,1),@(19,L0+3),FNB6$(A1$(1,6)),@(19,L0+4),FNB6$(A1$(7,6)),@(19,L0+5),FNB6$(A1$(13,6)),@(19,L0+6),DISCOUNT$,@(19,L0+7),A1$(32,10),@(19,L0+8),A1$(42,16),@(RP,L0-1),INVOICE$,'CL',@(RP,L0),NET$,'CL',@(RP,L0+1),ADJUSTMENT$,@(RP,L0+2),A[1]:M1$,@(RP,L0+3),RETAIN$,'CL',
5190 RETURN 
5200 REM " --- Display ribbon literals"
5210 CALL "SYC.WC",1,0,80,1,0,L0-2,0
5220 PRINT @(0,L0-1),'CE','SB',"Distribution Code:",@(RP-22),"Total Invoice Amount:",@(4,L0),"Payment Group:",@(RP-20),"Net Invoice Amount:",@(7,L0+1),"Terms Code:",@(RP-19),"Adjustment Amount:",@(13,L0+2),"Hold:",@(RP-17),"Discount Amount:",@(5,L0+3),"Invoice Date:",@(RP-18),RETENTION$,@(2,L0+4),"Accounting Date:",@(9,L0+5),"Due Date:",@(4,L0+6),"Discount Date:",@(8,L0+7),"Reference:",@(13,L0+8),"Memo:",'SF',
5290 RETURN 
5500 REM " --- Read AP Type"
5510 DIM C0$(32),U0$(10),U1$(24)
5520 LET C0$(1)=N0$+"A"+V$,C0$(6)=NF$,U0$(1)=N0$+A0$(5,6)+V$,STATUS=11
5530 FIND (APM10_DEV,KEY=C0$(1,5),DOM=5590)IOL=APM10A
5540 FIND (APM02_DEV,KEY=U0$,DOM=5590)IOL=APM02A
5550 PRINT @(V1,V2),C0$(4,2)," ",C0$(6,20),
5560 LET STATUS=0
5590 RETURN 
5600 REM " --- Read Distribution Code"
5610 DIM D0$(32)
5620 LET D0$(1)=N0$+"B"+V$,D0$(6)=NF$,STATUS=11
5630 FIND (APM10_DEV,KEY=D0$(1,5),DOM=5690)IOL=APM10B
5640 IF DISPLAY THEN PRINT @(V1,V2),D0$(4,2)," ",D0$(6,19),
5650 LET STATUS=0
5690 RETURN 
5700 REM " --- Read Payment Group"
5710 DIM T0$(32)
5720 LET T0$(1)=N0$+"D"+V$,T0$(6)=NF$,STATUS=11
5730 FIND (APM10_DEV,KEY=T0$(1,5),DOM=5790)IOL=APM10D
5740 IF DISPLAY THEN PRINT @(V1,V2),T0$(4,2)," ",T0$(6,20),
5750 LET STATUS=0
5790 RETURN 
5800 REM " --- Read Terms Code"
5810 DIM S0$(32),S[2]
5820 LET S0$(1)=N0$+"C"+V$,S0$(6)=NF$,STATUS=11
5830 FIND (APM10_DEV,KEY=S0$(1,5),DOM=5690)IOL=APM10C
5840 IF DISPLAY THEN PRINT @(V1,V2),S0$(4,2)," ",S0$(6,20),
5850 LET STATUS=0
5890 RETURN 
6000 REM " --- AP Type lookup"
6010 LET RECORD$="A",TITLE$="AP Types"
6090 GOTO 6400
6100 REM " --- Distribution Code lookup"
6110 LET RECORD$="B",TITLE$="AP Distribution Codes"
6190 GOTO 6400
6200 REM " --- Payment Group lookup"
6210 LET RECORD$="D",TITLE$="Payment Groups"
6290 GOTO 6400
6300 REM " --- Terms lookup"
6310 LET RECORD$="C",TITLE$="AP Terms Codes"
6400 REM " --- Call Windowed Lookup"
6410 LET DESCRIPTION$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6420 LET DESCRIPTION$[1]="Description",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6470 LET NUMBER=1,COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,ROW=L0,V$="SKIP"
6480 CALL "SYC.LK",APM10_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6490 RETURN 
6500 REM " --- Calculate Discount and Due Dates"
6510 LET DAYS=S[1],INVOICE$=FND$(FNN$(A1$(1,6)))
6520 CALL "SYC.CD",S0$(26,1),INVOICE$,DAYS,DUE$,STATUS
6530 LET A1$(13,6)=FNYY_YY21$(FNM$(FNC$(DUE$))),DAYS=S[2]
6540 CALL "SYC.CD",S0$(26,1),INVOICE$,DAYS,DISCOUNT$,STATUS
6550 LET A1$(19,6)=FNYY_YY21$(FNM$(FNC$(DISCOUNT$)))
6590 RETURN 
6600 REM " --- Remove detail records"
6610 READ (CHANNEL,KEY=A0$,DOM=6620)
6620 LET K$=KEY(CHANNEL,END=6690)
6630 IF POS(A0$=K$)<>1 THEN GOTO 6690
6640 REMOVE (CHANNEL,KEY=K$)
6650 GOTO 6620
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8070 DEF FNN$(Q$)=Q$(3,4)+Q$(1,2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9910 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9950 RUN "SYS.AA"
9999 END

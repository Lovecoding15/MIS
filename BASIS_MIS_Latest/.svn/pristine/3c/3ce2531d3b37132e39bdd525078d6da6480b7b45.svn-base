0010 REM "IVC - Lot/Serial Number Input"
0020 REM "Program IVC.SA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "19Aug97 WGH: Use 10-char instead of 7-char for lot/serial location (2310,2360,5010)"
0040 REM 
0050 REM "ITEM$    : Firm ID/Warehouse/Item Number (input)"
0052 REM "A0$      : IVM-07 0$ string or null (output)"
0054 REM "A1$      : IVM-07 1$ string/null (output)"
0056 REM "A[ALL]   : IVM-07 numerics (output)"
0058 REM "L0       : Beginning line"
0060 REM "L9       : Ending line"
0062 REM "TYPE$    : Type of Transaction (R/A/C/I)"
0063 REM "IVE11_DEV: IVE-11 Channel (input)"
0064 REM "CURR$    : Current Transaction Info (input)"
0070 REM 
0075 SETERR 9000
0080 SETESC 9000
0090 ENTER ITEM$,A0$,A1$,A[ALL],L0,L9,TYPE$,IVE11_DEV,CURR$
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="IVM-07",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET IVM07_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 IVM07A: IOLIST A0$,A1$,A[ALL]
0220 IVE11A: IOLIST S0$(1),S1$(1),S2$(1)
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM P[5]
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0425 LET N0$=F0$(16,2)
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0445 IF POS(P3$(17,1)="SL")=0 THEN GOTO 9900
0450 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[3]=NUM(P2$(7,2))
0455 LET P[2]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2)),P[5]=NUM(P2$(13,2))
0460 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),P9$="Lot/Serial"
0490 IF L9-L0-2<0 THEN GOTO 9900
0500 REM " --- Initializations"
0510 DIM O[3],LOTSER$(20),S0$(53),S1$(22),S2$(62)
0520 LET L0=L0+1,L=L0,O[3]=79-M3
0550 FOR X=2 TO 0 STEP -1
0560 LET O[X]=O[X+1]-M2
0570 NEXT X
0590 IF LEN(A0$)<44 THEN LET A0$=A0$+FILL(44-LEN(A0$))
0595 IF LEN(A1$)<95 THEN LET A1$=A1$+FILL(95-LEN(A1$))
0700 REM " --- Background"
0740 PRINT 'SB',@(1,L0-1),P9$+" #/Comment",@(O[0]+M2-8),"On Hand",@(O[1]+M2-6),"Avail",@(O[2]+M2-4),"Qty",@(O[3]+M3-5),"Cost",'SF'
0800 GOSUB 5000
1000 REM " --- Lot/Serial Number"
1010 LET V0$="S",V1$="CE",V3$="",V0=P[3],V1=1,V2=L0,I0=0
1020 LET V2$=A0$(25,20),V4$="Enter A New "+P9$+" Number/End"
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1000,4000
1050 IF FNP$(V$)="" THEN GOTO 1000
1060 LET LOTSER$(1)=V$
1100 REM " --- Check Current Session Entries for Duplicate Lot/Serial #s "
1110 IF TYPE$<>"R" THEN GOTO 1200
1120 READ (IVE11_DEV,KEY=CURR$(1,9),DOM=1130)
1130 LET K11$=KEY(IVE11_DEV,END=1200)
1140 IF K11$(1,9)<>CURR$(1,9) THEN GOTO 1200
1150 READ (IVE11_DEV,KEY=K11$)IOL=IVE11A
1160 IF ITEM$(3,22)+V$=S1$+CVS(S2$(1,20),2) THEN GOTO 1220
1170 GOTO 1130
1200 REM " --- Retrieve record"
1210 FIND (IVM07_DEV,KEY=ITEM$+LOTSER$,DOM=1500)IOL=IVM07A
1215 IF A[0]=0 THEN GOTO 1500
1220 IF P3$(17,1)<>"S" THEN GOTO 1300
1225 DIM MESS$[1]
1230 LET MESS$[0]="This Serial Number Already Exists (<Enter>=Continue)"
1240 CALL "SYC.XA",2,MESS$[ALL],0,-1,-1,V$,V3
1290 GOTO 1000
1300 REM " --- Lot already exists"
1310 LET V4$="WARNING: This Lot # Already Exists. Add To Existing Lot #"
1320 CALL "SYC.YN",1,V4$,1,V$,V3
1390 ON POS(V$="YN") GOTO 1300,1400,1000
1400 GOSUB 5000
1500 REM " --- Initialize for new record"
1530 LET A0$(1)=ITEM$+LOTSER$
2000 REM " --- Quantity"
2010 IF P3$(17,1)="S" AND A[0]=0 THEN LET A[0]=1
2020 LET V0$="N",V1$="C",V2$=STR(A[0]),V3$=M2$,V0=7,V1=O[2],V2=L0
2030 LET V4$="Enter The Quantity For This Transaction"
2035 IF POS(TYPE$="RI")>0 THEN LET V4$="Enter A Positive Quantity For This Transaction"
2040 GOSUB 7000
2050 ON V3 GOTO 2060,2000,3900,2000,3900
2060 IF P3$(17,1)="S" AND ABS(V)>1 THEN GOTO 2000
2065 IF POS(TYPE$="RI")>0 AND V<0 THEN GOTO 2000
2070 LET A[0]=V
2080 PRINT @(V1,V2),V:M2$,
2090 GOTO 3900
2100 REM " --- Cost"
2110 LET V0$="N",V1$="C",V2$=STR(A[3]),V3$=M3$,V0=7,V1=O[3],V2=L0
2120 LET V4$="Enter The Unit Cost"
2140 GOSUB 7000
2150 ON V3 GOTO 2160,2100,3900,2100,3900
2160 LET A[3]=V
2170 PRINT @(V1,V2),V:M3$
2190 GOTO 3900
2200 REM " --- Comments"
2210 LET V0$="S",V1$="C",V2$=A1$(14,40),V3$="",V0=40,V1=6,V2=L0+1
2220 LET V4$="Enter Any Optional Comments"
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 LET A1$(14,40)=V$
2290 GOTO 3900
2300 REM " --- Location"
2310 LET V0$="S",V1$="C",V2$=A1$(1,10),V3$="",V0=10,V1=66,V2=L0+1
2320 LET V4$="Enter The Current Location"
2330 PRINT 'SB',@(48,L+1),"Current Location: ",'SF',
2340 GOSUB 7000
2350 ON V3 GOTO 2360,2300,3900,2300,3900
2360 LET A1$(1,10)=V$
2390 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,2300,3990
3990 LET I0=0
4000 REM " --- All done"
4090 GOTO 9900
5000 REM " --- Display existing record"
5010 PRINT @(0,L0),A0$(25,P[3]),@(O[0]),A[0]:M3$,@(O[1]),A[0]-A[1]:M3$,@(O[2]),A[0]:M2$,@(O[3]),A[3]:M3$,@(6,L0+1),A1$(14,40),@(48),'SB',"Current Location: ",'SF',@(66),A1$(1,10)
5020 IF A1$(54,1)="C" THEN PRINT @(0,L0+1),"*C*"
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to calling program"
9910 PRINT @(0,L0-1),'CL',@(0,L0),'CL',@(0,L0+1),'CL',
9930 CLOSE (IVM07_DEV,ERR=9940)
9940 CLOSE (SYS01_DEV,ERR=9950)
9950 EXIT 
9999 END

0010 REM "ADC - Called Routine To Load ADD+ON 6.0 Parameters"
0020 REM "Program: ADC.01"
0030 REM "+----------------------------------------------------------+"
0031 REM "|            ADminstrator System Version 1.0.2a            |
0032 REM "|     Copyright (C) 1994 MicroAccounting Systems, Inc.     |
0033 REM "|                 All Rights Reserved"                     |
0034 REM "+----------------------------------------------------------+
0050 ENTER PRM$[ALL],STATUS
0060 REM "+-----------------------------------------+ Documentation Block"
0061 REM "| PRM$[X,Y] X=Module, Y=Parameter         |
0062 REM "|           X=AP=1,X=AR=2,X=BM=3,X=DC=4   |
0063 REM "|           X=FA=5,X=GL=6,X=IV=7,X=MP=8   |
0065 REM "|           X=OP=9,X=PO=10,X=PR=11,X=SA=12|
0066 REM "|           X=SH=13,X=SY=14,X=WO=15       |
0067 REM "|           X=TS=21,X=TB=22,X=DC=23       |
0068 REM "|           X=VS=24,X=EF=25               |
0075 REM "|        Y  0=Installed Y/N               |
0076 REM "|        Y  1=Key Size Parameter          |
0077 REM "|        Y  2=Key Size Maximum            |
0078 REM "|        Y  3=Key (Output) Mask           |
0079 REM "|        Y  4=Current Period              |
0080 REM "|        Y  5=Current Year"               |
0081 REM "|        Y  6=Dollar Mask                 |
0082 REM "|        Y  7=Unit Mask                   |
0083 REM "|        Y  8=Rate Mask                   |
0084 REM "|        Y  9=ADD+ON 2$ Param String      |
0085 REM "|        Y 10=ADD+ON 3$ Param String      |
0086 REM "|        Y 11=ADD+ON 4$ Param String      |
0087 REM "|        Y 12=Input Mask, if any          |
0094 REM "+-----------------------------------------+
0095 REM "| PRM$[0,0] = Terminal Record             |
0098 REM "+-----------------------------------------+
0099 SETERR 9000; SETESC 9900
0100 REM " --- Open files"
0110 LET SYS01_DEV=UNT; OPEN (SYS01_DEV,ERR=9800)"SYS-01"
0120 LET ADS01_DEV=UNT; OPEN (ADS01_DEV,ERR=9800)"ADS-01"
0130 LET SYM22_DEV=UNT; OPEN (SYM22_DEV,ERR=9800)"SYM-22"
0140 LET SYM04_DEV=UNT; OPEN (SYM04_DEV,ERR=9800)"SYM-04"
0200 REM " --- IOLIST's"
0260 IOLIST P0$,P1$,P2$,P3$,P4$,P5$,P6$,P7$,P8$
0270 IOLIST *,F0$,F1$,F2$,F3$,F4$,F5$
0280 IOLIST X0$,X1$
0500 REM " --- Init data"
0510 LET AP=1,AR=2,BM=3,DC=4,FA=5,GL=6,IV=7,MP=8,OP=9,PO=10,PR=11,SA=12,SH=13,SY=14,WO=15,TS=21,TB=22,DC=23,VS=24,EF=25
0520 LET INSTALL=0,KEYLEN=1,KEYMAX=2,KEYMASK=3,CPERIOD=4,CYEAR=5,DMASK=6,UMASK=7,RMASK=8,TWO=9,THREE=10,FOUR=11,EMASK=12
0540 DIM PRM$[30,12]
0550 LET FID=0,TOPCODE=1,TDATE=2,TMENU=3,TPRINTER=4,TSECURE=5,TFIRM=6,TPMODE=7,TPROG=8,TPOPTS=9,TFNAME=10,TPNAME=11,TLDATE=12
1000 REM " --- Terminal Record"
1010 READ (SYS01_DEV,KEY="T"+FID(0),DOM=6000)IOL=0270
1020 LET PRM$[FID,TOPCODE]=F0$(4,3),PRM$[FID,TDATE]=F0$(7,3),PRM$[FID,TMENU]=F2$,PRM$[FID,TPRINTER]=F0$(13,2),PRM$[FID,TSECURE]=F0$(15,1),PRM$[FID,TFIRM]=F0$(16,2),PRM$[FID,TLDATE]=F0$(18,6),PRM$[FID,TPMODE]=F0$(25,1),PRM$[FID,TPOPTS]="",PRM$[FID,TFNAME]=F4$,PRM$[FID,TPNAME]=F5$
1030 LET N0$=PRM$[FID,TFIRM]
1050 REM " --- Printers by operator"
1060 READ (SYM22_DEV,KEY=PRM$[FID,TOPCODE],DOM=1061)
1065 LET K$=KEY(SYM22_DEV,END=1100); READ (SYM22_DEV)
1070 IF K$(1,3)<>PRM$[FID,TOPCODE] THEN GOTO 1100
1075 LET PRM$[FID,TPOPTS]=PRM$[FID,TPOPTS]+K$(4,2)
1080 GOTO 1065
1100 REM " --- AP Accounts Payable System Parameters"
1110 LET PRM$[AP,INSTALL]="N"
1120 LET SYM$="AP"; GOSUB 6300; IF NOT_INSTALLED THEN GOTO 1200
1130 LET PRM$[AP,INSTALL]="Y"
1140 LET PRM$[AP,KEYLEN]=P2$(1,2),PRM$[AP,KEYMAX]=P2$(3,2),PRM$[AP,KEYMASK]=P5$,PRM$[AP,CPERIOD]=P4$(1,2),PRM$[AP,CYEAR]=P4$(3,2),PRM$[AP,DMASK]=P6$,PRM$[AP,UMASK]=P7$,PRM$[AP,RMASK]=P8$
1150 LET PRM$[AP,TWO]=P2$,PRM$[AP,THREE]=P3$,PRM$[AP,FOUR]=P4$
1200 REM " --- AR Accounts Receivables System Parameters"
1210 LET PRM$[AR,INSTALL]="N"
1220 LET SYM$="AR"; GOSUB 6300; IF NOT_INSTALLED THEN GOTO 1300
1230 LET PRM$[AR,INSTALL]="Y"
1240 LET PRM$[AR,KEYLEN]=P2$(1,2),PRM$[AR,KEYMAX]=P2$(3,2),PRM$[AR,KEYMASK]=P5$,PRM$[AR,CPERIOD]=P4$(1,2),PRM$[AR,CYEAR]=P4$(3,2),PRM$[AR,DMASK]=P6$,PRM$[AR,UMASK]=P7$,PRM$[AR,RMASK]=P8$,PRM$[AR,EMASK]=P1$
1250 LET PRM$[AR,TWO]=P2$,PRM$[AR,THREE]=P3$,PRM$[AR,FOUR]=P4$
1300 REM " --- BM Bill Of Materials System Parameters"
1305 LET PRM$[BM,INSTALL]="N"
1310 LET SYM$="IV"; GOSUB 6300; IF NOT_INSTALLED THEN GOTO 1400
1320 READ (SYS01_DEV,KEY=N0$+"BM"+"00",DOM=1400)X0$,X1$,X2$,X3$,X0,X1,X2
1330 LET PRM$[BM,INSTALL]="Y"
1340 LET PRM$[BM,UMASK]=X1$,PRM$[BM,DMASK]=P6$,PRM$[BM,RMASK]=P8$
1350 LET PRM$[BM,KEYLEN]=P2$(1,2),PRM$[BM,KEYMAX]="20",PRM$[BM,CPERIOD]=P4$(1,2),PRM$[BM,CYEAR]=P4$(3,2)
1360 LET PRM$[BM,TWO]=STR(X0),PRM$[BM,THREE]=STR(X1),PRM$[BM,FOUR]=STR(X2)
1400 REM " --- GL General Ledger System Parameters"
1410 LET PRM$[GL,INSTALL]="N"
1420 LET SYM$="GL"; GOSUB 6300; IF NOT_INSTALLED THEN GOTO 1500
1440 LET PRM$[GL,INSTALL]="Y"
1445 LET PRM$[GL,KEYLEN]=P2$(7,2),PRM$[GL,KEYMAX]=P2$(9,2),PRM$[GL,KEYMASK]=P5$,PRM$[GL,CPERIOD]=P2$(3,2),PRM$[GL,CYEAR]=P2$(5,2)
1450 LET PRM$[GL,TWO]=P2$,PRM$[GL,THREE]=P3$,PRM$[GL,FOUR]=P4$
1460 LET PRM$[GL,EMASK]=P1$,PRM$[GL,DMASK]=P6$,PRM$[GL,UMASK]=P7$
1500 REM " --- IV Inventory System Parameters"
1510 LET PRM$[IV,INSTALL]="N"
1520 LET SYM$="IV"; GOSUB 6300; IF NOT_INSTALLED THEN GOTO 1600
1530 LET PRM$[IV,INSTALL]="Y"
1540 LET PRM$[IV,KEYLEN]=P2$(1,2),PRM$[IV,KEYMAX]="20",PRM$[IV,CPERIOD]=P4$(1,2),PRM$[IV,CYEAR]=P4$(3,2),PRM$[IV,DMASK]=P6$,PRM$[IV,UMASK]=P7$,PRM$[IV,RMASK]=P8$
1550 LET PRM$[IV,TWO]=P2$,PRM$[IV,THREE]=P3$,PRM$[IV,FOUR]=P4$
1600 REM " --- FA Fixed Asset System Parameters"
1610 LET PRM$[FA,INSTALL]="N"
1700 REM " --- MP Materials Planning System Parameters"
1710 LET PRM$[MP,INSTALL]="N"
1800 REM " --- OP Order Processing System Parameters"
1810 LET PRM$[OP,INSTALL]="N"
1820 LET SYM$="AR"; GOSUB 6300; IF NOT_INSTALLED THEN GOTO 1900
1830 LET PRM$[OP,INSTALL]="Y"
1860 FOR J=1 TO 12; LET PRM$[OP,J]=PRM$[AR,J]; NEXT J
2000 REM " --- PO Purchase Order System Parameters"
2010 LET PRM$[PO,INSTALL]="N"
2020 LET SYM$="PO"; GOSUB 6300; IF NOT_INSTALLED THEN GOTO 2200
2030 LET PRM$[PO,INSTALL]="Y"
2070 IF PRM$[IV,INSTALL]="Y" THEN GOTO 2170
2080 LET PRM$[PO,KEYLEN]=P2$(1,2),PRM$[PO,KEYMAX]="20",PRM$[PO,CPERIOD]=P4$(1,2),PRM$[PO,CYEAR]=P4$(3,2),PRM$[PO,DMASK]=P6$,PRM$[PO,UMASK]=P7$,PRM$[PO,RMASK]=P8$
2120 LET PRM$[PO,TWO]=P2$,PRM$[PO,THREE]=P3$,PRM$[PO,FOUR]=P4$
2160 GOTO 2200
2170 FOR J=1 TO 12; LET PRM$[PO,J]=PRM$[IV,J]; NEXT J
2200 REM " --- PR Payroll System Parameters"
2210 LET PRM$[PR,INSTALL]="N"
2220 LET SYM$="PR"; GOSUB 6300; IF NOT_INSTALLED THEN GOTO 2300
2230 LET PRM$[PR,INSTALL]="Y"
2245 LET PRM$[PR,KEYLEN]=P2$(1,2),PRM$[PR,KEYMAX]=P2$(3,2),PRM$[PR,KEYMASK]=P5$,PRM$[PR,CPERIOD]=P4$(1,2),PRM$[PR,CYEAR]=P4$(3,2),PRM$[PR,DMASK]=P6$,PRM$[PR,UMASK]=P7$,PRM$[PR,RMASK]=P8$,PRM$[PR,EMASK]=P1$
2250 LET PRM$[PR,TWO]=P2$,PRM$[PR,THREE]=P3$,PRM$[PR,FOUR]=P4$
2300 REM " --- SA Sales Analysis System Parameters"
2310 LET PRM$[SA,INSTALL]="N"
2320 LET SYM$="SA"; GOSUB 6300; IF NOT_INSTALLED THEN GOTO 2400
2330 LET PRM$[SA,INSTALL]="Y"
2350 FOR J=1 TO 12; LET PRM$[SA,J]=PRM$[AR,J]; NEXT J
2360 LET PRM$[SA,TWO]=P1$,PRM$[SA,THREE]=P2$
2400 REM " --- SY System Parameters"
2700 REM " --- WO System Parameters"
2710 LET PRM$[WO,INSTALL]="N"
2720 LET SYM$="WO"; GOSUB 6300; IF NOT_INSTALLED THEN GOTO 2800
2730 LET PRM$[WO,INSTALL]="Y"
2740 IF P3$(4,1)="Y" THEN FOR J=1 TO 12; LET PRM$[WO,J]=PRM$[PR,J]; NEXT J; GOTO 2780
2750 LET PRM$[WO,KEYLEN]=P2$(1,2),PRM$[WO,KEYMAX]=P2$(3,2),PRM$[WO,KEYMASK]=P5$,PRM$[WO,CPERIOD]=P4$(1,2),PRM$[WO,CYEAR]=P4$(3,2),PRM$[WO,DMASK]=P6$,PRM$[WO,UMASK]=P7$,PRM$[WO,RMASK]=P8$,PRM$[WO,EMASK]=P1$
2780 LET PRM$[WO,TWO]=P2$,PRM$[WO,THREE]=P3$,PRM$[WO,FOUR]=P4$; REM "These are always W/O parameters, even if P/R is installed
3000 REM " --- TS System Parameters"
3010 LET PRM$[TS,INSTALL]="N"
3020 DIM Z1$(50),Z[5]; READ (ADS01_DEV,KEY=N0$+"TS"+"00",DOM=3100)*,Z1$,Z[0],Z[1],Z[2],Z[3],Z[4],Z[5]
3025 DIM X0$(40); FIND (SYM04_DEV,KEY="TS",DOM=3100)*,X0$(1)
3030 IF X0$(39,1)<>"Y" THEN GOTO 3100
3035 LET PRM$[TS,INSTALL]="Y"
3040 FOR J=1 TO 12; LET PRM$[TS,J]=PRM$[AR,J]; NEXT J
3050 LET PRM$[TS,TWO]=STR(Z[1]),PRM$[TS,THREE]=STR(Z[2])
3060 READ RECORD(ADS01_DEV,KEY=N0$+"TS"+"00")X0$
3070 LET PRM$[TS,FOUR]=X0$
3100 REM " --- Shop Floor Data Collection Parameters"
3115 LET PRM$[DC,INSTALL]="N"
3120 DIM X0$(40); FIND (SYM04_DEV,KEY="DC",DOM=3200)*,X0$(1),X1$,X2$
3125 IF X0$(39,1)<>"Y" THEN GOTO 3200
3130 LET PRM$[DC,INSTALL]="Y",PRM$[DC,TWO]=X1$,PRM$[DC,FOUR]=X2$
3160 DIM X0$(40); READ (ADS01_DEV,KEY=N0$+"DC"+"00",DOM=3200)IOL=0260
3165 LET PRM$[DC,THREE]=P3$
4000 REM " --- Program Termination"
4990 GOTO 9900
6000 REM " --- Terminal Record Missing"
6040 DIM A1$(25),A2$(30),A3$(8)
6060 LET A0$="T"+FID(0)
6080 LET X$=DAY,X$=X$(7,2)+X$(1,2)+X$(4,2),Y$=CHR(NUM(X$(1,2))+1)+CHR(NUM(X$(3,2))+1)+CHR(NUM(X$(5,2))+1)
6100 LET A1$(7,3)=Y$,A1$(1,6)="SYS",A1$(13,2)="PF",A1$(15,1)="0",A1$(16,2)="01",A1$(18,6)=X$
6120 WRITE (SYS01_DEV,KEY=A0$)A0$,A1$,A2$,A3$,A4$,A5$,A6$
6290 GOTO 1000
6300 REM " --- Is this system installed?
6310 LET NOT_INSTALLED=1
6320 REM LET P0$="",P1$="",P3$="",P4$="",P5$="",P6$="",P7$="",P8$=""
6330 REM LET X0$="",X1$="",X2$="",X3$="",X0=0,X1=0,X2=0,X3=0,X4=0
6340 FIND (SYM04_DEV,KEY=SYM$,DOM=6390)IOL=0280
6350 IF X1$(39,1)<>"Y" THEN GOTO 6390
6360 FIND (SYS01_DEV,KEY=N0$+SYM$+"00",DOM=6390)IOL=0260
6370 LET NOT_INSTALLED=0
6390 RETURN 
9000 ESCAPE
9010 LET ER0$=LST(PGM(TCB(5))),ER1$=STR(TCB(5)),ER2$=PGM(-1),ER0=ERR,ER1=0
9020 IF ER0=0 AND POS(" (PTR"=ER0$)>0 THEN GOTO 9600
9030 CALL "ADC.EA",ERR=9080,ER0$,ER1$,ER2$,ER0,ER1
9040 IF ER1<>1 THEN GOTO 9800
9100 REM " --- Retry"
9120 RETRY
9800 REM "---- Error Exit"
9820 IF ER0$="" THEN LET STATUS=ERR ELSE LET STATUS=ER0
9900 REM "---- Exit"
9920 CLOSE (SYS01_DEV,ERR=9921)
9930 CLOSE (ADS01_DEV,ERR=9931)
9940 CLOSE (SYM22_DEV,ERR=9941)
9950 CLOSE (SYM04_DEV,ERR=9951)
9999 END

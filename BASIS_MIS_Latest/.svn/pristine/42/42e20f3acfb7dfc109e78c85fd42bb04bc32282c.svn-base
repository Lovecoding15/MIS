0010 REM "APE - MANUAL CHECK ENTRY 5"
0020 REM "Program APE.CE"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "12Mar98 WGH: <F2>=Change may process wrong detail line (1410,1620,2010,3020)"
0032 REM "23Sep99 WGH: Display GL Account only when GL is installed (5045)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLISTS"
0210 APE12A: IOLIST W0$,W1$(1),W2$(1),W[0],W[1]
0230 GLM01A: IOLIST X0$,X1$
0500 REM " --- Init Data"
0515 DIM W1$(G[4]+1)
0520 LET W0$(1)=Y0$(1,27),W[0]=Y[0]+Y[2],W1$(1,G[4])=C2$
0530 GOSUB 6000
0540 GOSUB 5100
0550 GOSUB 5600
0800 REM " --- Options"
0810 IF O9=1 THEN LET W[0]=0
1000 REM " --- Check Line"
1020 IF L>L9 THEN PRINT @(0,L0),'LD',
1040 IF L>L9 THEN LET L=L9
1050 IF W1=0 THEN LET W1=1
1100 REM " --- Get Next Key"
1120 LET K$=KEY(APE12_DEV,END=1400)
1130 IF LEN(K$)<27 THEN GOTO 1400
1140 IF K$(1,27)<>Y$(1,27) THEN GOTO 1400
1150 READ (APE12_DEV)IOL=APE12A
1160 LET W$=W0$(1,29)
1170 IF P3$(1,1)<>"Y" THEN LET W$(3,2)=""
1190 GOTO 1490
1400 REM " --- SEQ"
1410 IF V3<>2 THEN LET W0=NUM(W0$(28,2))+1 ELSE LET W0=NUM(W0$(28,2))
1420 LET V0$="Z",V1$="KER",V2$=STR(W0),V3$="",V0=2,V1=0,V2=L,I0=0
1430 GOSUB 7000
1440 IF V3=4 THEN PRINT @(0,L),"  "; GOTO 4000
1450 IF V$="00" THEN GOTO 1400
1460 LET W0$(28,2)=V$,W$=W0$(1,29)
1470 IF P3$(1,1)<>"Y" THEN LET W$(3,2)=""
1480 FIND (APE12_DEV,KEY=W$,DOM=1600)IOL=APE12A
1490 IF W$<=K9$ THEN GOSUB 5400
1500 GOSUB 5000
1510 LET W9=-W[0]
1530 GOTO 3000
1600 REM " --- Setup"
1610 LET W9=0
1620 PRINT @(5,L),'CL',
1690 GOTO 3900
1700 REM " --- Account"
1710 IF GL$<>"Y" THEN GOTO 3900
1720 LET V0$="M",V1$="RC",V2$=W1$(1,G[3]),V3$=G1$,V0=G[4],V1=5,V2=L
1730 LET V4$="Enter A Valid G/L Account, <F3>=Lookup"
1740 GOSUB 7000
1750 IF V3=2 THEN GOTO 1850
1760 IF V3<>3 THEN GOTO 1790
1770 CALL "SYC.LG",V$
1780 IF V$="" THEN GOTO 1700
1790 LET W1$(1,G[4])=V$
1800 FIND (GLM01_DEV,KEY=N0$+W1$(1,G[4]),DOM=1700)IOL=GLM01A
1810 PRINT @(5,L),FNF$(W1$(1,G[3]),G5$),
1820 IF P3$(16,1)<>"Y" THEN PRINT @(6+LEN(G5$),L),X1$(1,28),
1830 IF P3$(16,1)="Y" THEN PRINT @(V1,V2+1),X1$(1,28),
1850 GOTO 3900
2000 REM " --- Miscellaneous
2010 IF P3$(16,1)<>"Y" THEN GOTO 2090
2020 LET V0$="S",V1$="C",V2$=W2$(1),V0=10,V1=20,V2=L
2040 GOSUB 7000
2045 IF V3=2 THEN GOTO 2090
2050 LET W2$(1)=V$
2090 GOTO 3900
2100 REM  " --- Amount"
2110 IF W[0]=0 THEN LET W[0]=Y[0]-Y[2]-T0
2120 LET V0$="N",V2$=STR(W[0]),V3$=M1$,V0=0,V1=55,V2=L
2130 GOSUB 7000
2140 LET W[0]=V
2150 IF V3=3 THEN LET W[0]=-W[0]
2220 PRINT @(V1,V2),W[0]:M1$,
2290 GOTO 3900
2300 REM " --- Units"
2305 IF GL$<>"Y" THEN GOTO 3900
2310 IF G4$(3,1)<>"Y" THEN GOTO 3900
2340 LET V0$="N",V2$=STR(W[1]),V3$=G7$,V0=0,V1=70,V2=L
2350 GOSUB 7000
2370 IF V3=2 THEN GOTO 2390
2380 LET W[1]=V
2390 GOTO 3900
3000 REM " --- CORRECT"
3020 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=W$,I0=5
3025 LET V4$="<Enter>=OK, <F2>=Change, <D>=Delete"
3030 GOSUB 7000
3040 IF V3=2 THEN GOTO 3900
3060 IF V$="" THEN GOTO 3120
3080 IF V$="D" THEN GOTO 3200
3090 GOTO 3000
3120 WRITE (APE12_DEV,KEY=W$)IOL=APE12A
3130 LET T0=T0+W9+W[0]
3135 GOSUB 5600
3140 LET L=L+1,W9$=""
3145 PRINT @(0,L),'CL'
3155 LET W[0]=0,W[1]=0
3160 IF V3=4 THEN GOTO 4000
3190 GOTO 1000
3200 REMOVE (APE12_DEV,KEY=W$,DOM=3210)
3210 LET T0=T0+W9
3220 PRINT @(0,L),'CL',
3240 GOSUB 5600
3290 GOTO 1000
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1400,1700,2000,2100,2300,3000
3990 LET I0=K0
4000 REM " --- Options"
4010 LET V0$="S",V1$="KC^",V2$="Y",V3$="",V4$="Is The Detail Information Correct (Y/N/Review/List/Delete)?",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4035 IF V$="R" THEN LET V$="REVIEW"
4037 IF V$="L" THEN LET V$="LIST"
4040 IF V$="REVIEW" THEN GOTO 4320
4050 IF V$="LIST" THEN GOTO 4400
4060 ON POS(V$="YN") GOTO 4000,4100,1000
4100 IF T0=Y[0]-Y[2] THEN GOTO 4500
4120 IF T0=Y[0]+Y[2] THEN GOTO 4500
4130 PRINT @(8,22),"*** This Transaction Is Out Of Balance By ",T0-Y[0]-Y[2]:M1$," <Enter>: ",'RB',
4140 LET V0$="S",V1$="C",V2$="",V3$="",V4$="",V0=1,V1=57+LEN(M1$),V2=22
4150 GOSUB 7000
4160 GOTO 1000
4200 REM " --- Delete"
4205 REM " -- Remove Invoice Records
4210 REMOVE (APE22_DEV,KEY=Y$,DOM=4215)
4215 LET K$=KEY(APE22_DEV,END=4240)
4220 IF LEN(K$)<27 OR LEN(Y$)<27 THEN GOTO 4240
4225 IF K$(1,27)<>Y$(1,27) THEN GOTO 4240
4230 REMOVE (APE22_DEV,KEY=K$)
4235 GOTO 4215
4240 REM " -- Remove Distribution Records
4243 READ (APE12_DEV,KEY=Y$(1,27),DOM=4245)
4245 LET K$=KEY(APE12_DEV,END=4270)
4250 IF LEN(K$)<27 OR LEN(Y$)<27 THEN GOTO 4270
4255 IF K$(1,27)<>Y$(1,27) THEN GOTO 4270
4260 REMOVE (APE12_DEV,KEY=K$)
4265 GOTO 4245
4270 LET T[0]=T[0]-Y[0],T[1]=T[1]-Y[1],T[2]=T[2]-Y[2]
4275 GOSUB 5400
4280 GOSUB 5500
4290 RUN "APE.CD"
4320 READ (APE12_DEV,KEY=Y$,DOM=4340)
4340 GOSUB 5400
4390 GOTO 1000
4400 REM " --- List"
4420 GOSUB 5100
4490 GOTO 4000
4500 REM " --- Return"
4560 GOSUB 5400
4580 RUN "APE.CD"
5000 REM " --- Display"
5010 LET X1$="Not On File"
5015 IF GL$<>"Y" THEN LET X1$="GL Not Installed"+FILL(12); GOTO 5040
5020 FIND (GLM01_DEV,KEY=N0$+W1$(1,G[4]),DOM=5040)IOL=GLM01A
5030 LET X1$=X1$(1,28),W1=1
5040 IF G4$(3,1)="Y" THEN PRINT @(70,L),W[1]:G7$,
5045 PRINT @(0,L),W0$(28,2),
5050 IF GL$="Y" THEN PRINT @(5),FNF$(W1$(1,G[3]),G5$),
5055 PRINT @(55),W[0]:M1$,
5060 IF P3$(16,1)="Y" THEN PRINT @(20,L),W2$
5070 IF P3$(16,1)<>"Y" THEN PRINT @(6+LEN(G5$),L),X1$
5090 RETURN
5100 REM " --- List"
5110 GOSUB 5400
5120 LET L=L0
5130 READ (APE12_DEV,KEY=Y$,DOM=5140)
5140 LET K$=KEY(APE12_DEV,END=5300)
5150 IF LEN(K$)<27 THEN GOTO 5300
5160 IF K$(1,27)<>Y$(1,27) THEN GOTO 5300
5170 READ (APE12_DEV)IOL=APE12A
5200 IF L<=L9 THEN GOTO 5250
5205 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Press <Enter> To Continue",V0=1,V1=FNV(V4$),V2=22
5210 GOSUB 7000
5220 IF V3=4 THEN GOTO 5300
5230 PRINT @(0,L0),'CE',
5240 LET L=L0
5250 GOSUB 5000
5260 LET L=L+1,K9$=K$
5290 GOTO 5140
5300 RETURN
5400 REM " --- Clear Screen"
5420 PRINT @(0,L0),'CE',
5440 LET L=L0
5490 RETURN
5500 REM " --- Display Total"
5520 PRINT @(23,8),T[0]:M1$,T[1]:M1$,T[0]-T[2]-T[1]:M1$,
5540 IF P3$(3,1)="Y" THEN PRINT @(23+3*M1,8),T[2]:M1$,
5590 RETURN
5600 REM " --- Display Distribution Goal"
5620 PRINT @(63,13),Y[0]-Y[2]-T0:M1$,
5690 RETURN
6000 REM " --- Accumulate Distribution Record "
6010 READ (APE12_DEV,KEY=Y$,DOM=6020)
6020 LET T0=0
6030 LET K$=KEY(APE12_DEV,END=6095)
6040 IF LEN(K$)<27 THEN GOTO 6095
6050 IF K$(1,27)<>Y0$(1,27) THEN GOTO 6095
6060 READ (APE12_DEV)IOL=APE12A
6070 LET T0=T0+W[0]
6080 GOTO 6030
6095 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- FUNCTIONS"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
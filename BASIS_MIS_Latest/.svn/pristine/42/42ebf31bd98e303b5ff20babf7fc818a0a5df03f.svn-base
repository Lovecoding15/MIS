0010 REM "ARR - COMMENT LISTING"
0020 REM "Program ARR.RC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ARM-04",FILES$[3]="ARM-05"
0130 LET FILES$[4]="ARM-10",FILES$[5]="SYS-01"
0160 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET ARM01_DEV=CHANNELS[1],ARM04_DEV=CHANNELS[2],ARM05_DEV=CHANNELS[3]
0185 LET ARM10_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5]
0200 REM " --- IOLists
0230 ARM10F: IOLIST X0$
0240 ARM10H: IOLIST X0$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$,WHEN$=F0$(7,3)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0450 DIM P[2]
0480 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0500 REM " --- Init Data
0510 LET H0=40,H1=70,H=0,H1$=N1$,H2$=N3$,L9=59,L=L9+1,WIDTH=80,M1=LEN(M0$)
0700 REM " --- Background
0710 FOR X=4 TO 10 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(23,3),"Report Sequence:",@(25,5),"Starting Code:",@(27,7),"Ending Code:",'SF',
1000 REM " --- Type
1010 PRINT 'CF',
1020 LET V0$="S",V1$="E",V2$=P7$,V3$="ST",V4$="S=Salesman, T=Territory",V0=1,V1=40,V2=3
1030 IF POS(V2$="ST")=0 THEN LET V2$="S"
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1055 IF P7$<>V$ THEN LET I1$="",I2$=""
1060 LET P7$=V$
1090 PRINT @(0,22),'CL',
1200 REM " --- Starting Number
1220 LET V0$="S",V1$="C",V2$=I1$,V3$="",V0=3,V1=40,V2=5,V4$="<F1>=First, <F3>=Code Lookup"
1240 GOSUB 7000
1250 IF V3=2 THEN GOTO 1000
1260 IF V3=3 THEN GOSUB 1400; IF V$="" THEN GOTO 1200
1270 IF V3=1 THEN LET V$=""
1280 IF V$="" THEN PRINT @(40,5),"First",'CL',; GOTO 1290
1282 GOSUB 1500
1284 IF VALID$="N" THEN GOTO 1200
1286 PRINT @(V1,V2),V$," ",X0$(7,20),'CL',
1290 LET I1$=V$
1300 REM " --- Ending Number
1320 LET V0$="S",V1$="C",V2$=I2$,V3$="",V0=3,V1=40,V2=7,V4$="<F1>=Last, <F3>=Code Lookup"
1340 GOSUB 7000
1350 IF V3=2 THEN GOTO 1200
1355 IF V3=3 THEN GOSUB 1400; IF V$="" THEN GOTO 1300
1370 IF V3=1 THEN LET V$=""
1375 IF V$<>"" THEN IF I1$>V$ THEN GOTO 1300
1380 IF V$="" THEN PRINT @(40,7),"Last",'CL',; GOTO 1390
1382 GOSUB 1500
1384 IF VALID$="N" THEN GOTO 1300
1386 PRINT @(V1,V2),V$," ",X0$(7,20),'CL',
1390 LET I2$=V$
1395 IF I2$="" THEN LET I2$="ZZZ"
1398 GOTO 4000
1400 REM " --- Salesrep/Territory Lookup
1410 IF P7$="S" THEN LET V5$="F" ELSE LET V5$="H"
1420 LET V4=1,V5=ARM10_DEV,V6=4,V7=3,V8=7,V9=20
1430 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
1440 RETURN 
1500 REM " --- Verify Salesperson/Territory"
1510 IF P7$="S" THEN LET V5$="F" ELSE LET V5$="H"
1520 LET VALID$="N"
1530 IF V5$="F" THEN FIND (ARM10_DEV,KEY=N0$+V5$+V$,DOM=1590)IOL=ARM10F
1540 IF V5$="H" THEN FIND (ARM10_DEV,KEY=N0$+V5$+V$,DOM=1590)IOL=ARM10H
1550 LET VALID$="Y"
1590 RETURN 
4000 REM " --- Correct?
4010 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V3=4 THEN GOTO 9900
4040 IF V$="N" THEN GOTO 1000
4060 IF V$<>"Y" THEN GOTO 4000
4100 RUN "ARR.RD"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

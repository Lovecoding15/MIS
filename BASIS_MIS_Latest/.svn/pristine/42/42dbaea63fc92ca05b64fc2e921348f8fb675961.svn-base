0010 REM "IVU - EOQ, Order Point & Safety Stock Report (Update Overlay)"
0020 REM "Program IVU.OA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "04Oct96 JAL: Don't update EOQ when carrying/unit cost is zero (570,721,725,810,817,1225,2225)"
0040 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 PRECISION P[1]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$,A2$(1),A3$,A4$,A5$,A6$,A[ALL]
0220 IVM02A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0230 IVW11A: IOLIST D0$(1),D1$,D2$(1),D[ALL]
0240 IVW21A: IOLIST D0$(1),D1$,D2$(1),D[ALL]
0500 REM " --- Initializations"
0510 DIM UPDATE$[2],PROMPTS$[2],O[2]
0550 LET UPDATE$[0]="Y",O[0]=3,PROMPTS$[0]="Update Economic Order Quantities"
0555 LET UPDATE$[1]="Y",O[1]=5,PROMPTS$[1]="Update Order Point Quantities"
0560 LET UPDATE$[2]="Y",O[2]=7,PROMPTS$[2]="Update Safety Stock Quantities"
0570 IF CARRY=0 THEN LET UPDATE$[0]="N"
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0720 FOR X=0 TO 2
0725 PRINT 'SB',@(38-LEN(PROMPTS$[X]),O[X]),PROMPTS$[X],": ",'SF',UPDATE$[X],
0730 CALL "SYC.WC",1,0,80,0,0,O[X]+1,0
0740 NEXT X
0800 REM " --- Options"
0805 FOR X=0 TO 2
0810 LET V2$=UPDATE$[X],V4$=PROMPTS$[X]+" (Y/N)?",V$=V2$
0815 LET V0$="Y",V1$="C",V3$="",V0=1,V1=40,V2=O[X]
0817 IF X=0 AND CARRY=0 THEN GOTO 0840
0820 GOSUB 7000
0825 ON V3 GOTO 0840,0810,0830,0810,9900
0830 LET X=MAX(0,X-1)
0835 GOTO 0810
0840 LET UPDATE$[X]=V$
0845 NEXT X
0850 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
0855 ON V3 GOTO 0860,0850,0850,0850,9900
0860 ON POS(V$="YN") GOTO 0850,0870,0800
0870 IF UPDATE$[0]="N" AND UPDATE$[1]="N" AND UPDATE$[2]="N" THEN GOTO 9900
0875 LET V4$="OK To Update The "+F5$
0880 CALL "SYC.YN",0,V4$,0,V$,V3
0890 IF V$<>"YES" THEN GOTO 9900
0910 CALL "SYC.NB","Updating",7+P[0],COLUMN
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
0970 REM " --- Position file"
0980 IF LEVEL$="W" THEN GOTO 1900
0990 READ (IVW11_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next IVW-11 record"
1010 LET K$=KEY(IVW11_DEV,END=1900)
1020 IF POS(N0$=K$)<>1 THEN GOTO 1900
1030 PRINT @(COLUMN,11),K$(5,3)," ",K$(8,P[0]),
1040 READ (IVW11_DEV)IOL=IVW11A
1200 REM " --- Update item"
1210 LET A0$(1)=N0$+D0$(8)
1220 FIND (IVM01_DEV,KEY=A0$,DOM=1800)IOL=IVM01A
1225 IF D[9]=0 THEN GOTO 1240
1230 IF UPDATE$[0]="Y" THEN IF A2$(26,1)<>"N" THEN LET A2$(26,1)="D",A[6]=D[8]
1240 IF UPDATE$[1]="Y" THEN IF A2$(27,1)<>"N" THEN LET A2$(27,1)="D",A[4]=D[6]
1250 IF UPDATE$[2]="Y" THEN IF A2$(28,1)<>"N" THEN LET A2$(28,1)="D",A[5]=D[4]
1290 WRITE (IVM01_DEV,KEY=A0$)IOL=IVM01A
1800 REM " --- Loop back for next record"
1890 GOTO 1000
1900 REM " --- Position file"
1910 IF LEVEL$="I" THEN GOTO 4000
1920 PRINT @(COLUMN,11),FILL(4+P[0]),
1990 READ (IVW21_DEV,KEY=N0$,DOM=2000)
2000 REM " --- Read next IVW-21 record"
2010 LET K$=KEY(IVW21_DEV,END=4000)
2020 IF POS(N0$=K$)<>1 THEN GOTO 4000
2040 PRINT @(COLUMN,11),K$(3,2)," ",K$(5,3)," ",K$(8,P[0]),
2050 READ (IVW21_DEV)IOL=IVW21A
2200 REM " --- Update warehouse"
2210 LET B0$(1)=D0$(1,4)+D0$(8)
2220 FIND (IVM02_DEV,KEY=B0$,DOM=2900)IOL=IVM02A
2225 IF D[9]=0 THEN GOTO 2240
2230 IF UPDATE$[0]="Y" THEN IF B1$(57,1)<>"N" THEN LET B1$(57,1)="D",B[6]=D[8]
2240 IF UPDATE$[1]="Y" THEN IF B1$(58,1)<>"N" THEN LET B1$(58,1)="D",B[4]=D[6]
2250 IF UPDATE$[2]="Y" THEN IF B1$(59,1)<>"N" THEN LET B1$(59,1)="D",B[5]=D[4]
2290 WRITE (IVM02_DEV,KEY=B0$)IOL=IVM02A
2900 REM " --- Loop back for next record"
2990 GOTO 2000
4000 REM " --- All done"
4090 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

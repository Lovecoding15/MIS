0010 REM "OPU - Sales Register Update (Inventory And General Ledger)"
0020 REM "Program OPU.CB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0205 APM12A: IOLIST X0$,X1$(1),X0
0210 ARE03A: IOLIST A0$,A[ALL]
0215 ARE04A: IOLIST ARE04_0$
0220 ARE13A: IOLIST W0$,W1$(1),W[ALL]
0230 ARE20A: IOLIST H0$,H1$,H[ALL]
0240 ARE23A: IOLIST C0$,C1$,C2$,C[ALL]
0250 ARM01A: IOLIST B0$(1),B1$(1)
0260 ARM02A: IOLIST X0$,X1$,X[ALL]
0270 ARM06A: IOLIST D0$,D1$(1),D[ALL]
0280 ARM10A: IOLIST U0$(1),U[ALL]
0285 ARM10B: IOLIST X0$(1),X0,X1
0290 ARM10C: IOLIST *,X$(1)
0295 ARM10D: IOLIST *,X$(1)
0300 ARM10E: IOLIST Y0$(1),Y1$(1)
0310 ARS10C: IOLIST S0$,S[ALL]
0315 ARS10S: IOLIST X0$,X[ALL]
0320 ART01A: IOLIST X0$(1),X[ALL]
0330 ART02A: IOLIST XK$,X2,X3,X4,X5
0340 ART03A: IOLIST X0$,A[ALL]
0350 ART11A: IOLIST X0$,X1$(1),X[ALL]
0360 ART23A: IOLIST C0$,C1$,C2$,C[ALL]
0370 IVM01A: IOLIST X0$,X1$,X2$,X3$,X4$,X5$,X6$(1)
0380 IVM02A: IOLIST X0$,X1$
0500 REM " --- Initializations"
0510 DIM C[2],T[15],X1$(20)
0700 REM " --- Background"
0710 PRINT @(40,5),"2 - Inventory And General Ledger",'CL',
1000 REM " --- Position file"
1090 READ (ARE04_DEV,KEY=N0$+"I",DOM=1100)
1100 REM " --- Read next invoice header"
1110 LET K2$="",K3$="",K4$=""
1120 LET K0$=KEY(ARE04_DEV,END=4000)
1130 READ (ARE04_DEV)
1135 IF K0$(1,3)<>N0$+"I" THEN GOTO 4000
1140 EXTRACT (ARE03_DEV,KEY=K0$(1,2)+K0$(4)+"000",DOM=1100)IOL=ARE03A
1145 IF A0$(68,2)<>"Y1" AND A0$(68,2)<>"Y2" THEN GOTO 1100
1150 IF A0$(99,1)<>"Y" THEN GOTO 1170
1155 DIM H0$(56),H1$(30),H[1]
1160 FIND (ARE20_DEV,KEY=A0$(1,17),DOM=1170)IOL=ARE20A
1170 IF FNP$(A0$(24,3))="" THEN LET A0$(24,3)=F0$(7,3)
1175 LET A0$(69,1)="2"
1180 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
1185 LET T9B=0,T9F=0,T0=0,T1=0,T8=0,T8C=0,NEWSHPDT$=""
1190 IF A0$(21,1)="V" THEN GOTO 3900
1195 IF A0$(22,1)<>"I" THEN GOTO 3900
1200 REM " --- Display record"
1210 PRINT @(COLUMN,11),FNF$(A0$(5,P[0]),M0$)," ",A0$(11,7),
1300 REM " --- Retrieve Distribution Code"
1310 DIM Z0$(10*G[4])
1320 LET D$=A0$(66,2)
1330 GOSUB 6100
1340 LET Z0$(1)=X$,Z9$=Z0$
1400 REM " --- Retrieve Cash Receipts Code"
1410 IF A0$(99,1)<>"Y" THEN GOTO 1500
1420 DIM X$(10),Z6$(10*G[4],"0")
1430 FIND (ARM10_DEV,KEY=N0$+"C"+H0$(21,3),DOM=1500)IOL=ARM10C
1440 LET Z0$(G[4]+1,G[4])=X$(1,10),Z6$(1)=Z0$,X$=Z9$
1500 REM " --- Retrieve Customer"
1510 DIM B0$(8),B1$(30)
1520 LET B0$(1)=N0$+A0$(5,6),B1$(1)="Unknown Customer"
1530 FIND (ARM01_DEV,KEY=B0$,DOM=1540)IOL=ARM01A
1540 IF A0$(99,1)="Y" THEN IF FNP$(H1$)<>"" THEN LET B1$(1)=H1$
1600 REM " --- Initialize G/L Posting Fields"
1610 IF GL$<>"Y" THEN GOTO 2000
1620 LET REF1$=FNF$(A0$(5,P[0]),M0$),REF2$=A0$(42,7),MEMO$=B1$,WHEN$=A0$(24,3)
1900 REM " --- Position Invoice Detail File"
1920 READ (ARE13_DEV,KEY=K0$(1,2)+K0$(4,15),DOM=2000)
2000 REM " --- Read next line item"
2020 LET K1$=KEY(ARE13_DEV,END=3000)
2040 IF K1$(1,17)<>K0$(1,2)+K0$(4,15) THEN GOTO 3000
2060 READ (ARE13_DEV)IOL=ARE13A
2070 LET Z0$(1)=Z9$,REF3$=W0$(18,3),ACCT$=""
2080 IF A0$(99,1)="Y" THEN LET Z0$(1)=Z6$
2100 REM " --- Retrieve Order Line Code"
2110 IF W0$(21,1)=K2$ THEN GOTO 2190
2115 LET K2$=W0$(21,1)
2120 DIM Y0$(32),Y1$(G[4]+2)
2130 LET Y1$(1,G[4])=FILL(G[4],"0")
2140 FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=2150)IOL=ARM10E
2190 IF W1$(44,1)="N" THEN GOTO 2900
2200 REM " --- Retrieve Inventory Item"
2210 PRECISION I[2]
2220 IF POS(Y0$(25,1)="SPN")=0 THEN GOTO 2700
2230 DIM X6$(5*G[4])
2240 FIND (IVM01_DEV,KEY=N0$+W0$(33,20),DOM=2250)IOL=IVM01A
2250 IF Y0$(25,1)="N" THEN GOTO 2300
2260 LET X0$=N0$+W0$(31,2)+W0$(33,20)
2270 FIND (IVM02_DEV,KEY=X0$,DOM=2300)IOL=IVM02A
2280 LET D$=X1$(13,2)
2300 REM " --- Sales Account"
2310 IF R9$="N" AND Y0$(25,1)<>"P" AND Y0$(27,1)<>"Y" THEN LET D$=A0$(66,2); GOTO 2320; REM "Invoice Ribbon
2315 IF POS(Y0$(25,1)="PN")>0 OR Y0$(27,1)="Y" THEN LET D$=Y1$(11,2); REM "Order Line Item
2320 GOSUB 6100
2325 IF STATUS<>0 THEN GOTO 2340
2330 LET Z0$(2*G[4]+1,G[4])=X$(2*G[4]+1,G[4])
2335 GOTO 2350
2340 GOSUB 6150
2350 REM " --- Inventory & COGS Accounts"
2360 IF R9$="N" AND Y0$(25,1)="N" AND Y0$(27,1)<>"Y" THEN GOSUB 6100; IF STATUS<>0 THEN GOSUB 6150; REM "Order Line Item
2370 LET Z0$(6*G[4]+1,2*G[4])=X$(6*G[4]+1,2*G[4]); REM "Invoice Ribbon, or Inventory Warehouse Record, or Order Line Item
2380 IF R9$="N" AND Y0$(25,1)="S" AND Y0$(27,1)<>"Y" THEN LET Z0$(6*G[4]+1,2*G[4])=X6$(1,2*G[4]); REM "Inventory Item Master File
2390 LET ACCOUNT$=Z0$(6*G[4]+1,G[4])
2400 REM " --- Serialized/Lotted Items"
2410 IF Y0$(25,1)="N" OR Y0$(27,1)="Y" THEN GOTO 2600; REM "Non-Stock or Drop-Ship?
2420 LET LS_NBR$="",COST=W[0],QNTY=W[4]
2430 IF LS$<>"Y" OR X2$(19,1)<>"Y" THEN GOTO 2500
2440 READ (ARE23_DEV,KEY=W0$(1,20),DOM=2450)
2450 LET ARE23_K$=KEY(ARE23_DEV,END=2600)
2460 IF POS(W0$(1,20)=ARE23_K$)<>1 THEN GOTO 2600
2470 READ (ARE23_DEV)IOL=ARE23A
2480 LET LS_NBR$=C1$,COST=C[2],QNTY=C[1]
2500 REM " --- Inventory Item Update"
2510 LET IV_INFO$[1]=W0$(31,2),IV_INFO$[2]=W0$(33,20),IV_INFO$[3]=LS_NBR$
2520 LET IV_REFS$[0]=A0$(24,3),IV_REFS$[1]=A0$(5,6),IV_REFS$[2]=A0$(11,7),IV_REFS$[3]=A0$(42,7),IV_REFS$[5]=A0$(18,3),IV_REFS$[6]=W0$(21,1),IV_REFS$[7]=ACCOUNT$
2530 LET IV_REFS[0]=-QNTY,IV_REFS[1]=COST,IV_REFS[2]=W[1]
2540 CALL "IVC.UA","OP",IV_FILES[ALL],IV_INFO[ALL],IV_PARAMS$[ALL],IV_INFO$[ALL],IV_REFS$[ALL],IV_REFS[ALL],IV_STATUS
2550 REM " --- Lot/Serial Invoice History"
2555 IF LS$<>"Y" OR X2$(19,1)<>"Y" THEN GOTO 2600
2560 LET C0$=ARE23_K$(1,10)+A0$(42,7)+ARE23_K$(18,6)
2570 IF IH$="Y" THEN WRITE (ART23_DEV,KEY=C0$)IOL=ART23A
2580 REMOVE (ARE23_DEV,KEY=ARE23_K$)
2590 GOTO 2450
2600 REM " --- Post G/L Cost"
2615 PRECISION 2
2620 LET AMOUNT=(-W[0])*W[4],UNITS=-W[4]
2630 IF A0$(99,1)="Y" THEN LET REF3$=W0$(18,3)+" "+"(Cash)"
2640 GOSUB GLPOST
2650 LET ACCOUNT$=Z0$(7*G[4]+1,G[4]),AMOUNT=-AMOUNT,UNITS=-UNITS
2660 GOSUB GLPOST
2700 REM " --- G/L Sales Distribution"
2710 IF POS(Y0$(25,1)="M")<>0 THEN GOTO 2900
2720 IF Y1$(1,G[4])<>FILL(G[4],"0") AND Y0$(25,1)="O" THEN LET Z0$(2*G[4]+1,G[4])=Y1$(1,G[4])
2750 LET ACCOUNT$=Z0$(1,G[4]),AMOUNT=W[6],UNITS=0
2760 IF A0$(99,1)="Y" THEN LET ACCOUNT$=Z0$(G[4]+1,G[4])
2770 GOSUB GLPOST
2780 LET ACCOUNT$=Z0$(2*G[4]+1,G[4]),AMOUNT=-AMOUNT,UNITS=-W[4]
2790 GOSUB GLPOST
2800 LET T0=T0+W[6],T1=T1+W[0]*W[4]
2830 IF A0$(24,3)>G9$ THEN GOTO 2870
2840 IF W[6]>0 THEN LET T[0]=T[0]+W[6]
2850 IF W[6]<0 THEN LET T[1]=T[1]+W[6]
2860 LET T[4]=T[4]+W[0]*W[4]
2865 GOTO 2900
2870 IF W[6]>0 THEN LET T[8]=T[8]+W[6]
2880 IF W[6]<0 THEN LET T[9]=T[9]+W[6]
2890 LET T[12]=T[12]+W[0]*W[4]
2900 REM " --- Remove/Update line item"
2910 IF W[3]<>0 THEN LET T9B=T9B+1
2912 IF W1$(44,1)="N" THEN LET T9F=T9F+1; GOTO 2930
2914 IF P3$(15,1)="Y" AND A0$(5,6)=P3$(16,6) THEN GOTO 2960
2915 IF W[3]=0 THEN GOTO 2960; REM "No B/O
2918 LET W[2]=W[3],W[3]=0
2920 IF Y0$(25,1)="O" AND W[6]=0 AND W[1]<>0 THEN LET W[6]=W[1],W[1]=0; GOTO 2930
2925 LET W[4]=W[2],W[6]=W[2]*W[1]
2930 LET W1$(45,1)="N"
2932 LET T8=T8+W[6],T8C=T8C+W[2]*W[0]
2934 IF W1$(41,3)<NEWSHPDT$ OR NEWSHPDT$="" THEN LET NEWSHPDT$=W1$(41,3)
2936 PRECISION I[2]
2940 WRITE (ARE13_DEV,KEY=K1$)IOL=ARE13A
2945 PRECISION 2
2950 GOTO 2000
2960 REMOVE (ARE13_DEV,KEY=K1$,DOM=2965)
2965 REMOVE (ARE07_DEV,KEY=N0$+W0$(31,2)+W0$(33,20)+W0$(3,2)+W0$(11,10)+W0$(5,6),DOM=2970)
2970 READ (ARE13_DEV,KEY=K1$,DOM=2980)
2990 GOTO 2000
3000 REM " --- Generate AR Open Invoice Records"
3010 IF T0<>A[6] THEN LET A[6]=T0
3020 IF A0$(99,1)="Y" THEN GOTO 3400
3030 DIM X0$(50),X[1]
3040 LET X0$(1)=A0$(1,10)+A0$(42,7)+"00"
3050 FIND (ART01_DEV,KEY=X0$(1,19),DOM=3100)IOL=ART01A
3060 GOTO 3200
3100 REM " --- AR Open Invoice Header"
3110 LET X0$(20)=A0$(66,2)+A0$(62,2)+A0$(24,3),X0$(33,1)=A0$(21,1),X0$(41,10)=A0$(86,10)
3115 DIM U0$(30),U[2]
3120 FIND (ARM10_DEV,KEY=N0$+"A"+A0$(62,2),DOM=3125)IOL=ARM10A
3125 LET V=U[1]
3130 CALL "SYC.CD",U0$(26,1),X0$(24,3),V,V$,STATUS
3140 LET X0$(27,3)=V$,V=U[2]
3150 CALL "SYC.CD",U0$(26,1),X0$(24,3),V,V$,STATUS
3160 LET X0$(30,3)=V$
3170 LET X[0]=T0+A[0]+A[1]-A[2],X[1]=(T0-A[2])*U[0]/100
3180 WRITE (ART01_DEV,KEY=X0$(1,19))IOL=ART01A
3190 GOTO 3500
3200 REM " --- AR Open Invoice Detail Summary Record"
3210 DIM X1$(11),X[1]
3220 LET X0$=X0$(1,17)+"99"
3230 READ (ART11_DEV,KEY=X0$,DOM=3300)
3240 EXTRACT (ART11_DEV,KEY=X0$)IOL=ART11A
3250 IF X1$(1,1)="C" THEN LET X1$(1,1)="A"
3260 LET X1$(2)=A0$(24,3)+A0$(42,7)
3270 LET X[0]=X[0]+T0+A[0]+A[1]-A[2]
3280 WRITE (ART11_DEV,KEY=X0$)IOL=ART11A
3290 GOTO 3500
3300 REM " --- AR Open Invoice Detail"
3310 LET X0$=X0$(1,17)+"01"
3320 LET K$=KEYP(ART11_DEV,END=3360)
3330 IF K$(1,17)<>X0$(1,17) THEN GOTO 3360
3340 LET X0$(18,2)=STR(NUM(K$(18,2))+1:"00")
3360 LET X1$(1)="I"+A0$(24,3)+A0$(42,7)
3370 LET X[0]=T0+A[0]+A[1]-A[2]
3380 WRITE (ART11_DEV,KEY=X0$)IOL=ART11A
3390 GOTO 3500
3400 REM " --- Bank Reconciliation"
3410 IF BR$<>"Y" THEN GOTO 3500
3440 DIM X1$(40)
3450 LET X0$=N0$+Z6$(G[4]+1,G[4])+FNYY_YY21$(FNM$(FNC$(A0$(24,3))))+"S"
3455 LET X1$(1)="DO"+FNYY_YY21$(FNM$(FNC$(A0$(24,3))))+"Sales Register Update",X0=0
3460 FIND (APM12_DEV,KEY=X0$,DOM=3470)IOL=APM12A
3470 LET X0=X0+T0+A[0]+A[1]-A[2]
3475 WRITE (APM12_DEV,KEY=X0$)IOL=APM12A
3500 REM " --- Update ARM-02 Customer Summary"
3510 DIM X[10]
3520 EXTRACT (ARM02_DEV,KEY=N0$+A0$(5,6)+A0$(3,2),DOM=3600)IOL=ARM02A
3530 LET X1$(14,3)=A0$(24,3)
3540 IF A0$(99,1)<>"Y" THEN LET X[1]=X[1]+T0+A[0]+A[1]-A[2]
3542 LET Q9=X[0]+X[1]+X[2]+X[3]+X[4]+X[5]; REM "Current Credit
3545 WRITE (ARM02_DEV,KEY=X0$)IOL=ARM02A
3549 GOTO 3600
3550 REM " --- Update MTD Cash Totals"
3555 IF A0$(99,1)<>"Y" THEN GOTO 3600
3560 DIM S[6]
3565 LET S0$=N0$+"C"
3570 EXTRACT (ARS10_DEV,KEY=S0$,DOM=3580)IOL=ARS10C
3580 IF A0$(24,3)<=G9$ THEN LET S[0]=S[0]+H[0]
3585 IF A0$(24,3)>G9$ THEN LET S[3]=S[3]+H[0]
3590 WRITE (ARS10_DEV,KEY=S0$)IOL=ARS10C
3600 REM " --- Accoumulate MTD Sales"
3610 IF A0$(24,3)<=G9$ THEN LET T[2]=T[2]+A[0],T[3]=T[3]+A[1],T[5]=T[5]+A[2]
3620 IF A0$(24,3)>G9$ THEN LET T[10]=T[10]+A[0],T[11]=T[11]+A[1],T[13]=T[13]+A[2]
3630 LET X1$=""
3640 DIM X0$(46+G[4]),X[10]
3700 REM " --- Post Tax, Freight And Discount to G/L"
3703 IF GL$<>"Y" THEN GOTO 3770
3705 LET WHEN$=A0$(24,3),REF3$="",UNITS=0
3710 IF A0$(99,1)="Y" THEN GOTO 3730
3720 LET ACCOUNT$=Z0$(1,G[4]),AMOUNT=A[0]+A[1]-A[2]
3725 GOSUB GLPOST
3727 GOTO 3740
3730 LET ACCOUNT$=Z0$(G[4]+1,G[4]),AMOUNT=A[0]+A[1]-A[2],REF3$="Cash Sale"
3735 GOSUB GLPOST
3740 LET ACCOUNT$=Z0$(3*G[4]+1,G[4]),AMOUNT=A[2]
3750 GOSUB GLPOST
3760 LET ACCOUNT$=Z0$(4*G[4]+1,G[4]),AMOUNT=-A[1]
3765 GOSUB GLPOST
3770 REM " --- Write Taxes, Sales Tax GL Account Update"
3775 GOSUB 6700
3800 REM " --- Update ARM-06 Payment Summary"
3810 DIM D0$(8),D1$(16),D[21]
3820 LET D0$=N0$+A0$(5,6)
3830 FIND (ARM06_DEV,KEY=D0$,DOM=3835)IOL=ARM06A
3835 LET D0$=N0$+A0$(5,6),D1$(1,3)=A0$(24,3)
3840 IF A0$(24,3)>G9$ THEN GOTO 3870
3845 LET D[0]=D[0]+T0,D[3]=D[3]+T1,D[1]=D[1]+T0,D[4]=D[4]+T1
3850 IF D[18]<Q9 THEN LET D[18]=Q9; REM "MTD High Credit
3855 IF D[19]<Q9 THEN LET D[19]=Q9; REM "YTD High Credit
3860 GOTO 3890
3870 LET D[8]=D[8]+T0,D[9]=D[9]+T1
3880 IF D[21]<Q9 THEN LET D[21]=Q9
3890 WRITE (ARM06_DEV,KEY=D0$)IOL=ARM06A
3900 REM " --- Remove/Rewrite Header"
3905 REMOVE (ARE04_DEV,KEY=K0$,DOM=3910)
3910 LET X0$=A0$,X0$(11,7)=X0$(42,7),X0$(42,7)=A0$(11,7),A[6]=T0,A[7]=T1
3915 WRITE (ART03_DEV,KEY=X0$(1,20))IOL=ART03A
3920 IF A0$(21,1)="V" THEN LET T9B=0,T9F=0
3925 IF (T9B=0 AND T9F=0) OR A0$(5,6)=P3$(16,6) THEN GOTO 3970
3930 LET A0$(22,1)="O",A0$(24,3)="",A0$(42,7)="",A[1]=0,A0$(68,2)="NN",A[0]=0,A[1]=0,A[2]=0,A[3]=0,A[4]=0,A[5]=0,A[6]=T8,A[7]=T8C
3935 IF T9B>0 THEN LET A0$(23,1)="B" ELSE LET A0$(23,1)=""; REM "Are there B/O items?
3940 IF NEWSHPDT$<>"" THEN LET A0$(27,3)=NEWSHPDT$
3945 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
3950 LET ARE04_0$=A0$(1,2)+"O"+A0$(3,15)
3955 WRITE (ARE04_DEV,KEY=ARE04_0$)IOL=ARE04A
3960 READ (ARE04_DEV,KEY=K0$,DOM=3965)
3965 GOTO 3985
3970 REMOVE (ARE03_DEV,KEY=K0$(1,2)+K0$(4,15)+"000",DOM=3975)
3975 REMOVE (ARE33_DEV,KEY=K0$(1,2)+K0$(6,13),DOM=3980)
3980 REMOVE (ARE43_DEV,KEY=K0$(1,2)+K0$(4,2)+K0$(12,7)+K0$(6,6),DOM=3985)
3985 IF A0$(99,1)="Y" THEN REMOVE (ARE20_DEV,KEY=A0$(1,17),DOM=3990)
3990 GOTO 1100
4000 REM " --- End"
4010 DIM X[15]
4015 LET X0$=N0$+"S"
4020 EXTRACT (ARS10_DEV,KEY=X0$,DOM=4030)IOL=ARS10S
4030 FOR X=0 TO 15
4040 LET X[X]=X[X]+T[X]
4050 NEXT X
4060 WRITE (ARS10_DEV,KEY=X0$)IOL=ARS10S
4080 IF GL$="Y" THEN CALL "GLC.CA",STATUS
4090 GOTO 9900
6100 REM " --- Retrieve Distribution Code"
6110 IF D$=K3$ THEN GOTO 6135
6115 LET K3$=D$,STATUS=1
6120 DIM X$(10*G[4])
6130 FIND (ARM10_DEV,KEY=N0$+"D"+K3$,DOM=6140)IOL=ARM10D
6135 LET STATUS=0
6140 RETURN 
6150 REM " --- Missing Distribution Code"
6155 DIM MESSAGE$[1]
6160 LET MESSAGE$[0]="Missing Distribution Code "+D$+" For Customer "+FNF$(A0$(5,P[0]),M0$)
6170 LET MESSAGE$[1]="Order "+A0$(11,7)+" - Line "+W0$(18,3)+" (<Enter>=Continue)"
6180 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
6190 RETURN 
6600 REM " --- Update Sales Tax History"
6605 LET X2=0,X3=0,X4=0,X5=0
6610 FIND (ART02_DEV,KEY=X$,DOM=6620)IOL=ART02A
6620 LET XK$=X$,X2=X2+A[4],X3=X3+X,X4=X4+T0+A[1]-A[2]
6630 IF A[4]<0 THEN LET X5=X5+A[4]
6640 WRITE (ART02_DEV,KEY=X$)IOL=ART02A
6690 RETURN 
6700 REM " --- Calculate and store taxes"
6705 PRECISION 4
6710 DIM X0$(60),TAX$(60)
6720 LET X$=N0$+A0$(80,2)+FNYY_YY21$(FNA$(A0$(24,1),A0$(24,3)))+FNA$(A0$(25,1),A0$(24,3))
6725 LET X0=0,X1=0,X=A[0],Y=0
6730 FIND (ARM10_DEV,KEY=N0$+"B"+A0$(80,2),DOM=6855)IOL=ARM10B
6735 LET TAX$(1)=X0$,X=X0*A[4]/100
6740 PRECISION 2
6745 LET X=X*1
6750 IF X1<>0 AND ABS(X)>X1 THEN LET X=X1*SGN(X)
6752 LET Y=X
6754 GOSUB 6600
6760 LET ACCOUNT$=TAX$(46,G[4]),AMOUNT=-X,UNITS=0
6765 GOSUB GLPOST
6770 FOR X9=1 TO 10
6775 PRECISION 4
6780 LET X1$=TAX$(26+2*(X9-1),2)
6785 IF FNP$(X1$)="" THEN GOTO 6850
6790 DIM X0$(60)
6795 LET X$(3,2)=X1$,X0=0,X1=0,X=0
6800 FIND (ARM10_DEV,KEY=N0$+"B"+X1$,DOM=6850)IOL=ARM10B
6805 LET X=X0*A[4]/100
6810 PRECISION 2
6820 LET X=X*1
6822 IF X1<>0 AND ABS(X)>X1 THEN LET X=X1*SGN(X)
6824 LET Y=Y+X
6830 GOSUB 6600
6840 LET ACCOUNT$=X0$(46,G[4]),AMOUNT=-X,UNITS=0
6845 GOSUB GLPOST
6850 NEXT X9
6855 PRECISION 2
6860 IF A[0]=Y THEN GOTO 6890
6865 LET X=A[0]-Y,Y0=T0,Y1=A[1],Y2=A[2],Y4=A[4],T0=0,A[1]=0,A[2]=0,A[4]=0
6870 GOSUB 6600
6875 LET T0=Y0,A[1]=Y1,A[2]=Y2,A[4]=Y4,Y0=0,Y1=0,Y2=0,Y4=0
6880 LET ACCOUNT$=TAX$(46,G[4]),AMOUNT=-X,UNITS=0
6885 GOSUB GLPOST
6890 PRECISION I[2]
6895 RETURN 
6900 REM " --- Standard G/L Posting Routine"
6910 GLPOST: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,WHEN$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8085 DEF FNP$(Q$)=CVS(Q$,2)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "APM - BALANCE CHECK BOOK"
0020 REM "Program APM.EE"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM 10Feb00 WGH: Erase Please Wait message after removing paid transactions (3990)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 ON O0 GOTO 110,110,2500
0105 REM " --- O0=2 On Return From APM.EF When In Balance"
0200 REM " --- IOLists"
0230 APM13A: IOLIST C0$,C1$,C[0],C[1],C[2],C[3]
0310 APM11A: IOLIST W0$(1),W1$(1),W[0],W[1]
0320 APM12A: IOLIST W0$(1),W1$(1),W[0],W[1]
0500 REM " --- Init"
0520 LET W0$(1)=A0$(1,2+P[4]),L0=6,L=L0+2,L9=L+13
0540 LET T0=0,T1=0,T2=0,S0=0,S1=0,S2=0
0700 REM " --- Background"
0730 PRINT 'SB',@(10,12),"Starting Balance:",@(10,14),"Checks Outstanding:",@(10,16),"Transactions Outstanding:",@(42,17),FILL(M1,"-"),@(10,18),"Ending Balance:",'SF',
0900 PRINT @(42,12),C[1]:M1$,
0940 LET T0=0,T1=0,T2=0
1000 REM " --- Find Outstanding Checks"
1050 READ (APM11_DEV,KEY=A0$(1,2+P[4]),DOM=1100)
1100 REM " --- Get Next Key"
1120 LET K$=KEY(APM11_DEV,END=1500)
1140 IF K$(1,2+P[4])<>A0$(1,2+P[4]) THEN GOTO 1500
1150 READ (APM11_DEV)IOL=APM11A
1200 IF W1$(2,1)<>"O" THEN GOTO 1100
1220 IF W1$(3,6)>C1$(7,6) THEN GOTO 1100
1240 LET T0=T0+W[0],S0=S0+1
1290 PRINT @(42,14),T0:M1$,S0:M2$,
1390 GOTO 1100
1500 REM " --- Find Outstanding Transactions"
1550 READ (APM12_DEV,KEY=A0$(1,2+P[4]),DOM=1600)
1600 REM " --- Get Next Key"
1620 LET K$=KEY(APM12_DEV,END=2000)
1640 IF K$(1,2+P[4])<>A0$(1,2+P[4]) THEN GOTO 2000
1650 READ (APM12_DEV)IOL=APM12A
1700 IF W1$(2,1)<>"O" THEN GOTO 1600
1720 IF W1$(3,6)>C1$(7,6) THEN GOTO 1600
1740 LET T1=T1+W[0],S1=S1+1
1790 PRINT @(42,16),T1:M1$,S1:M2$,
1890 GOTO 1600
2000 REM " --- Ending Balance"
2020 LET T2=C[1]-T0+T1
2040 PRINT @(42,18),T2:M1$,
2050 IF C[2]=T2 THEN PRINT @(10,20),"This Account Is In Balance .... "; GOTO 2200
2090 LET T9$=" OVER"
2100 IF C[2]>T2 THEN LET T9$="SHORT"
2140 DIM MESSAGE$[0]
2150 LET MESSAGE$[0]="This Account Is *** "+T9$+" *** By "+STR(ABS(C[2]-T2):M1$)
2160 CALL "SYC.XA",0,MESSAGE$[ALL],0,22,-1,V$,V3
2200 CALL "SYC.YN",1,"Do You Wish To Print A Listing Of All Transactions",1,V$,V3
2210 IF V$="N" THEN IF C[2]=T2 THEN GOTO 2500 ELSE GOTO 4000
2220 IF V$="Y" THEN GOTO 2300
2240 GOTO 2200
2350 GOSUB 5400
2380 IF C[2]=T2 THEN LET O0=2
2390 RUN "APM.EF"
2500 REM " --- Update?"
2510 CALL "SYC.YN",1,"Do You Wish To Remove All Paid Transactions",1,V$,V3
2520 IF V$="N" THEN GOTO 4000
2540 IF V$<>"Y" THEN GOTO 2500
2560 GOSUB 5100
2570 PRINT @(20,18),"Removing Paid Transactions. Please Wait...",
3000 REM " --- Remove Outstanding Checks"
3050 READ (APM11_DEV,KEY=A0$(1,2+P[4]),DOM=3100)
3100 REM " --- Get Next Key"
3120 LET K$=KEY(APM11_DEV,END=3500)
3140 IF K$(1,2+P[4])<>A0$(1,2+P[4]) THEN GOTO 3500
3150 READ (APM11_DEV)IOL=APM11A
3160 PRINT @(35,20),W0$(3+P[4]),
3200 IF W1$(2,1)="O" THEN GOTO 3100
3220 IF W1$(3,6)>C1$(7,6) THEN GOTO 3100
3300 REMOVE (APM11_DEV,KEY=K$,DOM=3310)
3390 GOTO 3100
3500 REM " --- Find Outstanding Transactions"
3550 READ (APM12_DEV,KEY=A0$(1,2+P[4]),DOM=3600)
3600 REM " --- Get Next Key"
3620 LET K$=KEY(APM12_DEV,END=3900)
3640 IF K$(1,2+P[4])<>A0$(1,2+P[4]) THEN GOTO 3900
3650 READ (APM12_DEV)IOL=APM12A
3660 PRINT @(35,20),W0$(3+P[4]),
3700 IF W1$(2,1)<>"P" THEN GOTO 3600
3720 IF W1$(3,6)>C1$(7,6) THEN GOTO 3600
3800 REMOVE (APM12_DEV,KEY=K$,DOM=3810)
3890 GOTO 3600
3900 REM " --- Re-Write"
3920 LET C1$(1,6)=C1$(7,6),C1$(7,6)=""
3940 LET C[0]=C[1],C[1]=0,C[2]=0
3950 WRITE (APM13_DEV,KEY=C0$)IOL=APM13A
3990 PRINT @(0,18),'CL',
4000 REM " --- Continue"
4020 LET O0=1
4040 RUN "APM.EA"
5400 REM " --- Clear Screen"
5420 PRINT @(0,L0),'CE',
5440 LET L=L0+2
5490 RETURN
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
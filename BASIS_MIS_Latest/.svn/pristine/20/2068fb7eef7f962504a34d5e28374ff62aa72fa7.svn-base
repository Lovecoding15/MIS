0010 REM "APM - Payment Group Maintenance"
0020 REM "Program APM.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 BEGIN 
0085 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APM-10",FILES$[2]="SYS-01"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET APM10_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLISTS"
0210 APM10D: IOLIST A0$(1)
0260 APS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0500 REM " --- Initializations"
0510 DIM A0$(32)
0590 LET K0=1,K9=5,A0$(1,3)=N0$+"D"
0700 REM " --- Background"
0710 PRINT 'SB',@(0,3),"Payment Group:",@(2,4),"Description:",'SF',
0900 REM " --- Position File"
0990 READ (APM10_DEV,KEY=A0$(1,3),DOM=1000)
1000 REM " --- Payment Group"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(APM10_DEV,END=1050)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4)
1050 LET V0$="S",V1$="KE",V3$="",V4$="Enter A Valid Payment Group (<F3>=Lookup)",V0=2,V1=15,V2=3
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN GOSUB 6000
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(4)=V$
1200 REM " --- Retrieve record"
1220 READ (APM10_DEV,KEY=A0$(1,K9),DOM=1400)IOL=APM10D
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Initialize new record"
1510 LET A0$(6)=""
2000 REM " --- Description"
2010 LET V0$="S",V1$="",V2$=A0$(6,20),V3$="",V0=20,V1=15,V2=4,I0=0
2020 LET V4$="Enter The Description For This Payment Group"
2030 GOSUB 7000
2040 LET A0$(6,20)=V$
2090 GOTO 3900
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="KC",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4110 WRITE (APM10_DEV,KEY=A0$(1,K9))IOL=APM10D
4190 GOTO 1000
4200 REM " --- Delete"
4210 REMOVE (APM10_DEV,KEY=A0$(1,K9),DOM=4230)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4590 ON POS(V$="YN") GOTO 4500,4900,9900
4900 REM " --- Report Initializations"
4905 LET PROGRAM$="",TOTAL$="AP Payment Groups",HEADINGS=3,WIDTH=80
4910 LET CHANNEL=APM10_DEV,NUMBER=1,RECORD$="D"
4915 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4920 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4925 DIM HEADINGS$[HEADINGS]
4930 LET FIELD$[0]="Code",FIELD[0]=1,POSITION[0]=4
4940 LET LENGTH[0]=2,TYPE$[0]="S",MASK$[0]=""
4945 LET FIELD$[1]="Description",FIELD[1]=1,POSITION[1]=6
4950 LET LENGTH[1]=20,TYPE$[1]="S",MASK$[1]=""
4980 LET HEADINGS$[0]=N1$,HEADINGS$[1]=TOTAL$+" Listing"
4990 RUN "SYR.AA"
5000 REM " --- Display"
5010 PRINT @(15,3),A0$(4,2),@(15,4),A0$(6,20),
5090 RETURN 
6000 REM " --- Code Lookup"
6010 LET RECORD$="D",NUMBER=1,TITLE$="AP Payment Groups"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6050 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6060 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6070 LET V$="SKIP",ROW=V2+2
6080 CALL "SYC.LK",APM10_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

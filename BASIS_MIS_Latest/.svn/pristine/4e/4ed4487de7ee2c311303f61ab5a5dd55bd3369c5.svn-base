0010 REM "PRU - Check Update
0020 REM "Program PRU.CB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 15Dec97 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "23Oct97 WGH: Maintain full PRECISION of Calculation Rate (6619-6626,6659,6666)"
0032 REM "31Dec98 WGH: Check reversals aren't being recorded correctly in Bank Rec (5415,5470-5475)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 APM11A: IOLIST X0$,X1$,X[ALL]
0220 PRE01A: IOLIST B0$,B1$
0230 PRE11A: IOLIST W0$,W[ALL],W1$
0240 PRE21A: IOLIST W0$,W[ALL]
0250 PRE31A: IOLIST W0$,W[ALL]
0260 PRE41A: IOLIST W0$,W[ALL]
0270 PRE51A: IOLIST W0$,W[ALL]
0272 PRM01A: IOLIST R0$,R1$(1)
0280 PRM10A: IOLIST X0$
0290 PRM10B: IOLIST *,Z5$
0300 PRM10E: IOLIST S0$,S1$,S2$,S3$,S4$,S5$,S6$,S7$,S8$
0310 PRM10J: IOLIST O0$,O1$,O2$,O3$,O4$,O5$,O6$
0320 PRT01A: IOLIST Y0$(1),Y[ALL],Z[ALL]
0330 PRT02A: IOLIST D0$,D[ALL]
0340 PRT03A: IOLIST E0$,E[ALL]
0350 PRT04A: IOLIST H0$(1),H1$,H[ALL]
0360 PRT11A: IOLIST Y0$(1),Y[ALL],Z[ALL]
0370 PRT21A: IOLIST Y0$,Y[ALL],Z[ALL]
0380 PRT31A: IOLIST Y0$,Y[ALL],Z[ALL]
0390 PRW01A: IOLIST A0$
0500 REM " --- Initialization
0510 LET T1$="365052026024012004001",T2$="DWBSMQA"
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
1000 REM " --- Init Read
1020 READ (PRW01_DEV,KEY=N0$,DOM=1100)
1100 REM " --- Main Read
1105 NEXT_PRW01: 
1110 LET T0=0,T1=0,T2=0
1120 LET K0$=KEY(PRW01_DEV,END=4000)
1140 READ (PRW01_DEV)IOL=PRW01A
1160 IF A0$(1,2)<>N0$ THEN GOTO 4000
1170 PRINT @(COLUMN,11),A0$(3,7),
1180 IF A0$(10,1)="V" THEN GOTO 3800
1200 REM " --- Check Header
1210 DIM U[7],I[50],R1$(240),Q[20]
1220 FIND (PRM01_DEV,KEY=N0$+A0$(14,9),DOM=1230)IOL=PRM01A
1230 LET B0$=A0$(1,2)+A0$(14,9)+A0$(10,1)+A0$(23,2)+A0$(3,7)
1240 IF A0$(10,1)="C" THEN LET B0$(15,7)=""
1250 READ (PRE01_DEV,KEY=B0$,DOM=3900)IOL=PRE01A
1260 LET T0=0,T1=0,T2=0,T3=0,T9$="",T9$="E",P9$=A0$(11,3),P9=POS(FNYY_YY21$(FNC2$(P9$))<=P7$,4),P9=INT((P9+3)/4),Q9$=""; REM "P9=Calendar qtr in which check date was written"    
1270 LET PER=NUM(T1$(POS(B0$(25,1)=T2$)*3-2,3))
1280 GOSUB PROCESS_ANNIV
1290 LET V0$=R1$(221,2),MEMO$=FNP$(R1$(17,14))+" "+FNP$(R1$(1,16))
1300 GOSUB DEPT
1310 LET T9$="",Z3$=S1$(1,G[4]),Z4$=S1$(G[4]+1,G[4]),J7$=""
1600 REM " --- Check Detail
1700 REM " --- Earnings
1710 READ_PRE11: 
1720 READ (PRE11_DEV,KEY=B0$(1,21),DOM=NEXT_PRE11)
1730 NEXT_PRE11: 
1740 LET K1$=KEY(PRE11_DEV,END=READ_PRE21)
1750 IF K1$(1,21)<>B0$(1,21) THEN GOTO READ_PRE21
1760 READ (PRE11_DEV)IOL=PRE11A
1770 IF B0$(12,1)="V" THEN GOTO DELETE11
1790 LET REF1$=FNF$(R0$(3,P[0]),M0$),REF2$=A0$(3,7),UNITS=0; REM "GL Inits"
1800 GOSUB CHECK_HIST
1900 PRECISION P[3]
1910 DIM Y[11],Z[4],Y0$(14)
1920 LET Y0$(1,14)=B0$(1,11)+"A"+W0$(25,2)
1930 EXTRACT (PRT01_DEV,KEY=Y0$(1,14),DOM=1940)IOL=PRT01A
1940 IF P9$<=G9$ THEN LET Y[0]=Y[0]+W[2],Y[6]=Y[6]+W[1]
1950 IF P9$>G9$ THEN LET Y[5]=Y[5]+W[2],Y[11]=Y[11]+W[1]
1960 IF P9>0 THEN LET Y[P9]=Y[P9]+W[2],Y[6+P9]=Y[6+P9]+W[1]
1970 FIND (PRM10_DEV,KEY=N0$+"A"+W0$(25,2),DOM=2010)IOL=PRM10A
1980 IF X0$(26,1)="N" THEN GOTO 2010
1990 IF POS(X0$(26,1)="FU")<>0 THEN LET Z[4]=Z[4]+W[1]
2000 IF POS(X0$(26,1)="D")<>0 THEN LET Z[4]=Z[4]+W[2]
2010 WRITE (PRT01_DEV,KEY=Y0$(1,14))IOL=PRT01A
2020 PRECISION 2
2030 LET V$=W0$(22,1)
2040 GOSUB UNION_HIST
2050 LET V0$=W0$(29,2),T0=T0+W[2]*1
2060 GOSUB 5600
2070 GOSUB DEPT
2075 DELETE11: 
2080 REMOVE (PRE11_DEV,KEY=K1$)
2090 GOTO NEXT_PRE11
2100 REM " --- Deductions
2110 READ_PRE21: 
2120 READ (PRE21_DEV,KEY=B0$(1,21),DOM=NEXT_PRE21)
2130 NEXT_PRE21: 
2140 LET K1$=KEY(PRE21_DEV,END=READ_PRE31)
2150 IF K1$(1,21)<>B0$(1,21) THEN GOTO READ_PRE31
2160 READ (PRE21_DEV)IOL=PRE21A
2170 IF B0$(12,1)="V" THEN GOTO DELETE21
2190 LET REF1$=FNF$(R0$(3,P[0]),M0$),REF2$=A0$(3,7),UNITS=0; REM "GL Inits"
2200 GOSUB CHECK_HIST
2210 DIM Y[5],Z[1],Y0$(21)
2220 LET Y0$(1,14)=B0$(1,11)+"B"+W0$(25,2)
2230 EXTRACT (PRT11_DEV,KEY=Y0$(1,14),DOM=2240)IOL=PRT11A
2240 IF P9$<=G9$ THEN LET Y[0]=Y[0]+W[2]
2250 IF P9$>G9$ THEN LET Y[5]=Y[5]+W[2]
2260 IF P9>0 THEN LET Y[P9]=Y[P9]+W[2]
2270 REM "Reduces the balance on Ded w/balance"
2280 IF POS("B"=Y0$(21,1))<>0 AND Z[1]<>0 THEN LET Z[1]=Z[1]-W[2]; IF Z[1]<0 THEN LET Z[1]=0
2290 WRITE (PRT11_DEV,KEY=Y0$(1,14))IOL=PRT11A
2300 LET V$=W0$(22,1)
2310 GOSUB UNION_HIST
2320 LET T1=T1+W[2],V0$=R1$(221,2)
2330 GOSUB DEPT
2340 DELETE21: 
2350 REMOVE (PRE21_DEV,KEY=K1$)
2360 GOTO NEXT_PRE21
2400 REM " --- Tax
2410 READ_PRE31: 
2420 READ (PRE31_DEV,KEY=B0$(1,21),DOM=NEXT_PRE31)
2430 NEXT_PRE31: 
2440 LET K1$=KEY(PRE31_DEV,END=READ_PRE41)
2450 IF K1$(1,21)<>B0$(1,21) THEN GOTO READ_PRE41
2460 READ (PRE31_DEV)IOL=PRE31A
2470 IF B0$(12,1)="V" THEN GOTO DELETE31
2490 LET REF1$=FNF$(R0$(3,P[0]),M0$),REF2$=A0$(3,7),UNITS=0; REM "GL Inits"
2500 GOSUB CHECK_HIST
2510 GOSUB PROCESS_TAXNDED
2520 DELETE31: 
2530 REMOVE (PRE31_DEV,KEY=K1$)
2540 GOTO NEXT_PRE31
2600 REM " --- Contributions
2610 READ_PRE41: 
2620 READ (PRE41_DEV,KEY=B0$(1,21),DOM=NEXT_PRE41)
2630 NEXT_PRE41: 
2640 LET K1$=KEY(PRE41_DEV,END=ACCRUALS)
2650 IF K1$(1,21)<>B0$(1,21) THEN GOTO ACCRUALS
2660 READ (PRE41_DEV)IOL=PRE41A
2670 IF B0$(12,1)="V" THEN GOTO DELETE41
2690 LET REF1$=FNF$(R0$(3,P[0]),M0$),REF2$=A0$(3,7),UNITS=0; REM "GL Inits"
2700 GOSUB PROCESS_TAXNDED
2710 DELETE41: 
2720 REMOVE (PRE41_DEV,KEY=K1$)
2730 GOTO NEXT_PRE31
2800 REM " --- Accruals
2805 ACCRUALS: 
2810 READ_PRE51: 
2820 READ (PRE51_DEV,KEY=B0$(1,21),DOM=NEXT_PRE51)
2830 NEXT_PRE51: 
2840 LET K1$=KEY(PRE51_DEV,END=WRITE_CHKHIST)
2850 IF K1$(1,21)<>B0$(1,21) THEN GOTO WRITE_CHKHIST
2860 READ (PRE51_DEV)IOL=PRE51A
2870 IF B0$(12,1)="V" THEN GOTO DELETE51
2890 LET REF1$=FNF$(R0$(3,P[0]),M0$),REF2$=A0$(3,7),UNITS=0; REM "GL Inits"
2910 PRECISION P[3]
2920 DIM Y[11],Z[4],Y0$(14)
2930 LET Y0$(1,14)=B0$(1,11)+"A"+W0$(25,2)
2940 EXTRACT (PRT01_DEV,KEY=Y0$(1,14),DOM=2950)IOL=PRT01A
2950 FIND (PRM10_DEV,KEY=N0$+"A"+W0$(25,2),DOM=3000)IOL=PRM10A
2960 IF X0$(27,1)<>"A" THEN GOTO 2990
2970 LET Z[1]=Z[1]+W[2]*1
2980 GOTO 3010
2990 IF P9>0 THEN LET Z[1]=Z[1]+W[2]
3000 IF P9<=0 THEN LET Z[2]=Z[2]+W[2]
3010 WRITE (PRT01_DEV,KEY=Y0$(1,14))IOL=PRT01A
3030 DELETE51: 
3040 REMOVE (PRE51_DEV,KEY=K1$)
3050 GOTO NEXT_PRE51
3500 REM " --- Write Check History
3505 WRITE_CHKHIST: 
3510 PRECISION 2
3515 IF A0$(10,1)="V" THEN GOTO 3800
3520 LET D0$=B0$(1,11)+A0$(3,7)+A0$(11,3)+A0$(10,1)
3530 DIM D[7]
3540 EXTRACT (PRT02_DEV,KEY=D0$(1,18),DOM=3550)IOL=PRT02A
3550 LET D[0]=D[0]+T0-T1-T2
3552 FOR X=1 TO 7
3554 LET D[X]=D[X]+U[X]
3556 NEXT X
3580 WRITE (PRT02_DEV,KEY=D0$(1,18))IOL=PRT02A
3700 REM " --- GL Cash
3710 LET ACCOUNT$=Z3$,WHEN$=P9$,REF3$="Payroll",AMOUNT=(-T0)+T1+T2,UNITS=0
3720 GOSUB GLPOST
3730 LET ACCOUNT$=Z4$,AMOUNT=-AMOUNT
3740 GOSUB GLPOST
3800 REM " --- Write Open Check
3810 IF A0$(10,1)="V" THEN LET P9$=A0$(11,3)
3820 LET E0$=A0$(1,22)+P9$
3840 DIM E[0]
3860 EXTRACT (PRT03_DEV,KEY=E0$(1,9),DOM=3870)IOL=PRT03A
3870 LET E[0]=E[0]+T0-T1-T2
3880 LET E0$=A0$(1,22)+P9$
3890 WRITE (PRT03_DEV,KEY=E0$(1,9))IOL=PRT03A
3895 IF P3$(16,1)="Y" AND GL$="Y" THEN GOSUB 5200
3900 REM " --- Remove Header
3910 IF A0$(10,1)="V" THEN GOTO 3940
3920 REMOVE (PRE01_DEV,KEY=B0$(1,21),DOM=3940)
3930 READ (PRE01_DEV,KEY=B0$(1,21),DOM=3940)
3940 REMOVE (PRW01_DEV,KEY=K0$)
3950 READ (PRW01_DEV,KEY=K0$,DOM=3990)
3990 GOTO NEXT_PRW01
4000 REM " --- Clear Remainder
4010 CLR_HEADER: 
4020 READ (PRE01_DEV,KEY=N0$,DOM=4030)
4030 LET K$=KEY(PRE01_DEV,END=CLR_EARNINGS)
4040 IF K$(1,2)<>N0$ THEN GOTO CLR_EARNINGS
4050 REMOVE (PRE01_DEV,KEY=K$)
4060 GOTO 4030
4200 CLR_EARNINGS: 
4210 READ (PRE11_DEV,KEY=N0$,DOM=4220)
4220 LET K$=KEY(PRE11_DEV,END=CLR_DEDS)
4230 IF K$(1,2)<>N0$ THEN GOTO CLR_DEDS
4240 REMOVE (PRE11_DEV,KEY=K$)
4250 GOTO 4220
4300 CLR_DEDS: 
4310 READ (PRE21_DEV,KEY=N0$,DOM=4320)
4320 LET K$=KEY(PRE21_DEV,END=CLR_TAXES)
4330 IF K$(1,2)<>N0$ THEN GOTO CLR_TAXES
4340 REMOVE (PRE21_DEV,KEY=K$)
4350 GOTO 4320
4400 CLR_TAXES: 
4410 READ (PRE31_DEV,KEY=N0$,DOM=4420)
4420 LET K$=KEY(PRE31_DEV,END=CLR_CONTRIBS)
4430 IF K$(1,2)<>N0$ THEN GOTO CLR_CONTRIBS
4440 REMOVE (PRE31_DEV,KEY=K$)
4450 GOTO 4420
4500 CLR_CONTRIBS: 
4510 READ (PRE41_DEV,KEY=N0$,DOM=4520)
4520 LET K$=KEY(PRE41_DEV,END=CLR_ACCRUALS)
4530 IF K$(1,2)<>N0$ THEN GOTO CLR_ACCRUALS
4540 REMOVE (PRE41_DEV,KEY=K$)
4550 GOTO 4520
4600 CLR_ACCRUALS: 
4610 READ (PRE51_DEV,KEY=N0$,DOM=4620)
4620 LET K$=KEY(PRE51_DEV,END=NEXT_OVERLAY)
4630 IF K$(1,2)<>N0$ THEN GOTO NEXT_OVERLAY
4640 REMOVE (PRE51_DEV,KEY=K$)
4650 GOTO 4620
4900 REM " --- Next Overlay
4905 NEXT_OVERLAY: 
4910 RUN "PRU.CC"
5200 REM " --- Bank Rec
5210 DIM X1$(50),X[1]
5220 IF A0$(10,1)<>"V" THEN GOTO 5300
5240 LET X9$=X9$+E0$(3,7)
5290 RETURN 
5300 REM " --- Write Voids
5320 IF X9$="" THEN GOTO 5400
5340 LET X0$=E0$(1,2)+Z3$+X9$(1,7),X1$(1,2)="PV"
5350 LET X1$(3,6)=FNYY_YY21$(FNM$(FNC$(E0$(11,3)))),X1$(9,30)="Void"
5360 WRITE (APM11_DEV,KEY=X0$)IOL=APM11A
5370 IF LEN(X9$)>8 THEN LET X9$=X9$(8) ELSE LET X9$=""
5390 GOTO 5300
5400 REM " --- Detail
5410 LET X0$=E0$(1,2)+Z3$+E0$(3,7),X1$(1,2)="PO"
5430 LET X1$(3,6)=FNYY_YY21$(FNM$(FNC$(E0$(11,3)))),X1$(9,30)=MEMO$
5460 FIND (APM11_DEV,KEY=X0$,DOM=5470)IOL=APM11A
5470 LET X[0]=X[0]+T0-T1-T2
5475 IF E0$(10,1)="R" THEN LET X1$(2,1)="R"
5480 WRITE (APM11_DEV,KEY=X0$)IOL=APM11A
5485 DIM X[3]
5590 RETURN 
5600 REM " --- Accumulate Depts
5620 LET X=POS(V0$=Q9$)
5630 IF X=0 THEN LET Q9$=Q9$+V0$; GOTO 5620
5640 LET Q[INT(X/2)]=Q[INT(X/2)]+W[2]
5680 RETURN 
5700 REM " --- Update Union History File (PRT-04)
5705 UNION_HIST: 
5710 IF P3$(9,1)<>"Y" THEN GOTO 5810
5720 DIM O0$(36),O1$(111),O2$(20),O3$(40),O4$(20),O5$(20)
5730 FIND (PRM10_DEV,KEY=N0$+"J"+R1$(238,3),DOM=5810)IOL=PRM10J
5740 DIM H0$(47),H[4]
5750 LET H0$(1)=N0$+R1$(238,3)+R1$(1,30)+B0$(3,9)+P8$,H1$=R1$(136,9)
5760 EXTRACT (PRT04_DEV,KEY=H0$(1,47),DOM=5770)IOL=PRT04A
5770 IF V$="A" THEN IF POS(W0$(25,2)=O4$,2)<>0 THEN LET H[0]=H[0]+W[1],H[1]=H[1]+W[2]
5780 IF V$="B" THEN IF POS(W0$(25,2)=O5$,2)<>0 THEN LET H[2]=H[2]+W[2]
5790 IF V$="D" THEN IF POS(W0$(25,2)=O3$,2)<>0 THEN LET H[4]=H[4]+W[2]
5800 WRITE (PRT04_DEV,KEY=H0$(1,47))IOL=PRT04A
5810 RETURN 
5900 REM " --- Accrued Payroll"
5910 LET ACCOUNT$=Z1$,WHEN$=Z9$
5915 PRECISION P[3]
5920 LET ANNDOL=W[2]*PER,ANNUNT=W[1]*PER
5930 LET DAYDOL=ANNDOL/365,DAYUNT=ANNUNT/365
5935 PRECISION 2
5950 LET AMOUNT=P8*DAYDOL,UNITS=P8*DAYUNT
5955 GOSUB GLPOST
5960 LET WHEN$=Z7$,AMOUNT=-AMOUNT,UNITS=-UNITS
5965 GOSUB GLPOST
5970 LET ACCOUNT$=Z2$,AMOUNT=-AMOUNT,UNITS=0
5975 GOSUB GLPOST
5980 LET WHEN$=Z9$,AMOUNT=-AMOUNT,UNITS=0
5985 GOSUB GLPOST
5990 RETURN 
6000 REM " --- Department
6005 DEPT: 
6020 IF V0$=K2$ THEN GOTO 6060
6030 LET K2$=V0$
6040 DIM Z0$(G[4],"0"),S1$(G[4]*2,"0")
6050 FIND (PRM10_DEV,KEY=N0$+"E"+K2$,DOM=6060)IOL=PRM10E
6060 IF T9$="E" THEN RETURN 
6070 DIM Z1$(G[4],"0"),Z2$(G[4],"0")
6080 LET V1=0,Z9$=P9$
6090 IF P3$(17,1)="Y" THEN LET Z9$=P8$
6100 ON POS(W0$(22,1)="ABCD") GOTO 6300,6110,6180,6230,6270
6110 LET Z2$=Z4$,Z9$=P8$,REF3$=W0$(25,2)+" "+W0$(27,2)+" "+W0$(29,2)
6120 LET X=POS(W0$(25,2)=S2$,2)
6130 IF X=0 THEN GOTO 6300
6140 LET Z1$=S3$(INT(X/2)*G[4]+1,G[4])
6150 IF P8<>0 THEN GOSUB 5900
6160 LET V1=W[1]
6170 GOTO 6300
6180 IF W0$(25,2)=K5$ THEN GOTO 6210
6190 LET K5$=W0$(25,2),Z5$=Z1$
6200 FIND (PRM10_DEV,KEY=N0$+"B"+K5$,DOM=6210)IOL=PRM10B
6210 LET Z2$=Z4$,Z1$=Z5$,REF3$="Deduct "+W0$(25,2)
6220 GOTO 6300
6230 LET X=POS(W0$(25,2)=S4$,2),Z2$=Z4$,REF3$="Tax "+W0$(25,2)
6240 IF X=0 THEN GOTO 6300
6250 LET Z1$=S5$(INT(X/2)*G[4]+1,G[4])
6260 GOTO 6300
6270 LET X=POS(W0$(25,2)=S6$,2),REF3$="Contrib "+W0$(25,2)
6280 IF X=0 THEN GOTO 6300
6290 LET Z2$=S8$(INT(X/2)*G[4]+1,G[4])
6295 LET Z1$=S7$(INT(X/2)*G[4]+1,G[4])
6300 LET ACCOUNT$=Z1$,WHEN$=Z9$,AMOUNT=W[2]
6305 IF POS(W0$(22,1)="BC")<>0 THEN LET AMOUNT=-AMOUNT
6310 GOSUB GLPOST
6315 LET ACCOUNT$=Z2$,AMOUNT=-AMOUNT,UNITS=0
6320 GOSUB GLPOST
6325 RETURN 
6330 REM " --- Check History
6333 CHECK_HIST: 
6335 LET X=POS(W0$(22,1)+W0$(25,2)=U0$,3)
6340 IF X=0 THEN GOTO 6375
6345 LET X0=POS(U1$(X,3)=U2$,3)
6350 IF X0=0 THEN GOTO 6375
6355 LET X0=INT((X0+2)/3),X=POS(U3$(X0*2-1,2)=U4$)
6360 IF X=0 THEN GOTO 6375
6365 LET X=INT((X+1)/2)
6370 IF X>0 AND X<7 THEN GOTO 6380
6375 LET X=7
6380 LET U[X]=U[X]+W[2]
6385 RETURN 
6400 REM " --- Anniversary Processing"
6405 PROCESS_ANNIV: 
6410 IF P8$<=R1$(164,3) THEN GOTO 6595
6440 LET R1$(164,1)=FNI$(FNYY_YEAR(FNA$(R1$(164,1),R1$(164,3)))+1); REM "Increment anniversary year
6450 WRITE (PRM01_DEV,KEY=R0$)IOL=PRM01A
6460 REM " --- Roll Accruals"
6470 PRECISION P[3]
6480 DIM Y[11],Z[4],Y0$(14)
6490 READ (PRT01_DEV,KEY=B0$(1,11)+"A",DOM=6500)
6500 LET K$=KEY(PRT01_DEV,END=6590)
6510 IF K$(1,12)<>B0$(1,11)+"A" THEN GOTO 6590
6520 READ (PRT01_DEV,END=6590)IOL=PRT01A
6530 FIND (PRM10_DEV,KEY=N0$+"A"+Y0$(13,2),DOM=6500)IOL=PRM10A
6540 IF X0$(26,1)="N" THEN GOTO 6500
6550 IF X0$(27,1)="C" THEN GOTO 6500
6560 LET Z[0]=Z[0]+Z[1]-Z[4],Z[1]=Z[2],Z[2]=0,Z[4]=0
6570 WRITE (PRT01_DEV,KEY=K$)IOL=PRT01A
6580 GOTO 6500
6590 PRECISION 2
6595 RETURN 
6600 REM " --- Process Taxes & Contributions
6605 PROCESS_TAXNDED: 
6610 IF W0$(22,1)="C" THEN DIM Y0$(48),Y[17],Z[0] ELSE DIM Y0$(19),Y[17],Z[1]
6615 LET Y0$(1,14)=B0$(1,11)+W0$(22,1)+W0$(25,2),Q0=W[0],Q1=W[1],Q2=W[2],Q3=T0,Q7=0
6619 PRECISION P[3]
6620 IF W0$(22,1)="C" THEN EXTRACT (PRT21_DEV,KEY=Y0$(1,14),DOM=6626)IOL=PRT21A
6625 IF W0$(22,1)="D" THEN EXTRACT (PRT31_DEV,KEY=Y0$(1,14),DOM=6626)IOL=PRT31A
6626 PRECISION 2
6630 IF P9$<=G9$ THEN LET Y[0]=Y[0]+T0,Y[6]=Y[6]+W[0],Y[12]=Y[12]+W[2]
6640 IF P9$>G9$ THEN LET Y[5]=Y[5]+T0,Y[11]=Y[11]+W[0],Y[17]=Y[17]+W[2]
6650 IF P9>0 THEN LET Y[P9]=Y[P9]+T0,Y[6+P9]=Y[6+P9]+W[0],Y[12+P9]=Y[12+P9]+W[2]
6659 PRECISION P[3]
6660 IF W0$(22,1)="C" THEN WRITE (PRT21_DEV,KEY=Y0$(1,14))IOL=PRT21A
6665 IF W0$(22,1)="D" THEN WRITE (PRT31_DEV,KEY=Y0$(1,14))IOL=PRT31A
6666 PRECISION 2
6670 LET V$=W0$(22,1)
6680 GOSUB UNION_HIST
6690 IF W0$(22,1)="C" THEN LET T2=T2+W[2]
6700 IF Q3=0 THEN LET Q9$=""
6710 IF Q9$="" THEN LET Q9$=R1$(221,2),Q[0]=W[2],Q3=W[2]
6720 FOR Q9=1 TO LEN(Q9$) STEP 2
6730 LET V0$=Q9$(Q9,2),Q4=0
6740 IF Q3<>0 THEN LET Q4=Q[INT(Q9/2)]*100/Q3
6750 IF Q0*Q4<>0 THEN LET W[0]=Q0*Q4/100
6760 IF Q1*Q4<>0 THEN LET W[1]=Q1*Q4/100
6770 IF Q2*Q4<>0 THEN LET W[2]=Q2*Q4/100
6780 IF Q9+2>LEN(Q9$) THEN IF Q7+W[2]<>Q2 THEN LET W[2]=Q2-Q7
6790 LET Q7=Q7+W[2]
6800 GOSUB DEPT
6810 NEXT Q9
6820 RETURN 
6900 REM " --- Standard G/L Posting Routine"
6910 GLPOST: 
6920 IF GL$<>"Y" THEN GOTO 6940
6930 CALL "GLC.AA",GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,WHEN$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6940 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8035 DEF FNC2$(Q1$)=FNA$(Q1$(1),Q1$)+FNA$(Q1$(2),Q1$)
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8065 DEF FNI$(Q)=CHR(Q+32)
8085 DEF FNP$(Q$)=CVS(Q$,2)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

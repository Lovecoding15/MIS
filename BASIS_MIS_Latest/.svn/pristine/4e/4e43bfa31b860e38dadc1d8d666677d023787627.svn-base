0010 REM "SYM - Public Program Listing"
0020 REM "Program SYM.PB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 SYM05A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0500 REM " --- Initializations"
0510 DIM HEADINGS$[2]
0520 LET L9=50,L=L9+1,WIDTH=80,PAGE=0,HEADINGS=2,WHEN$=F0$(7,3),CLOCK$=""
0540 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$+" Listing",NUMBER=0
0550 LET DESC$="Program",NAME$="",START$="",FINISH$=""
0700 REM " --- Background"
0720 LET WIN_WIDTH=MAX(32,16+LEN(DESC$)+8),WIN_HEIGHT=5
0725 LET WIN_Y=10,WIN_X=INT((80-WIN_WIDTH)/2),TITLE$="Report Selections"
0750 CALL "SYC.WA",0,WIN_WIDTH,WIN_HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0760 PRINT 'SB',@(1,0),"Beginning ",DESC$,@(4,1),"Ending ",DESC$,'SF',
0770 LET COLUMN=12+LEN(DESC$)
0800 REM " --- Get starting/ending programs"
0810 GOSUB 6000
0820 CALL "SYC.WD",NAME$
0900 REM " --- Position File"
0910 CALL "SYC.GA",7,1,"","",STATUS
0920 IF STATUS THEN GOTO 9900
0980 CALL "SYC.NB","Printing",8,COLUMN
0990 READ (SYM05_DEV,KEY=START$,DOM=1000)
1000 REM " --- Main Read"
1010 LET K$=KEY(SYM05_DEV,END=4000)
1020 IF FINISH$<>"" THEN IF K$(1,8)>FINISH$ THEN GOTO 4000
1030 READ (SYM05_DEV,END=4000)IOL=SYM05A
1200 REM " --- Level breaks?"
1210 IF A0$(1,8)=LAST$ THEN GOTO 1400
1220 LET LAST$=A0$(1,8),PGM$=FNP$(LAST$)
1230 PRINT @(COLUMN,11),LAST$,
1240 GOSUB 6500
1250 IF L+4>L9 THEN GOSUB 5000
1260 PRINT (7)""
1270 PRINT (7)@(5),A0$(1,8)," ",DESCRIPTION$,@(58),SIZE:M2$
1280 PRINT (7)""
1290 LET L=L+3,NUMBER=NUMBER+1
1400 REM " --- Print Record"
1410 IF L+1>L9 THEN GOSUB 5000
1420 PRINT (7)@(1),A0$(9),@(5),A1$," ",A2$(1,50),@(58),A[0]:M2$,@(66),A2$(51,1),@(70),A2$(52,1)
1430 LET L=L+1
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF L+2>L9 THEN GOSUB 5000
4020 PRINT (7)""
4030 PRINT (7)NUMBER," Program(s) Listed"
4090 GOTO 9900
5000 REM " --- Report heading"
5010 LET L=HEADINGS+2
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)@(1),"Seq",@(5),"Program",@(14),"Description",@(60),"Size",@(65),"Add?",@(70),"Priority"
5090 RETURN 
6000 REM " --- Beginning value"
6010 LET V0$="S",V1$="",V2$=START$,V3$=""
6020 LET V4$="<F1>=First",V0=8,V1=COLUMN,V2=0
6030 GOSUB 7000
6040 ON V3 GOTO 6100,6050,6000,6000,6400
6050 LET START$=""
6060 PRINT @(V1,V2),"First",'CL',
6090 GOTO 6200
6100 REM 
6110 IF V$="" THEN GOTO 6050
6120 LET START$=V$
6130 PRINT @(V1,V2),START$,'CL',
6200 REM " --- Ending value"
6210 LET V0$="S",V1$="",V2$=FINISH$,V3$=""
6220 LET V4$="<F1>=Last",V0=8,V1=COLUMN,V2=1
6230 GOSUB 7000
6240 ON V3 GOTO 6300,6250,6000,6300,6400
6250 LET FINISH$=""
6260 PRINT @(V1,V2),"Last",'CL',
6290 GOTO 6400
6300 REM 
6310 IF V$="" THEN GOTO 6250
6320 LET FINISH$=V$
6330 IF LEN(FINISH$)<8 THEN LET FINISH$=FINISH$+FILL(8-LEN(FINISH$))
6340 PRINT @(V1,V2),FINISH$,'CL',
6400 REM " --- Correct?"
6410 LET V0$="Y",V1$="",V2$="Y",V3$="",V4$="Is The Above Correct (Y/N)?"
6420 LET V0=1,V1=MAX(INT((WIN_WIDTH-LEN(V4$))/2)-1,0),V2=WIN_HEIGHT-3
6430 PRINT @(V1,V2),V4$,
6440 LET V1=V1+LEN(V4$),V4$=""
6450 GOSUB 7000
6460 PRINT @(0,V2),'CL',
6470 ON V3 GOTO 6480,6000,6000,6000,9900
6480 ON POS(V$="YN") GOTO 6400,6490,6000
6490 RETURN 
6500 REM " --- Verify program existance and return size/description"
6510 LET CHANNEL=UNT,STATUS=11,DESCRIPTION$(1)="",SIZE=0
6520 OPEN (CHANNEL,ERR=6680)PGM$
6530 LET X$=FID(CHANNEL,ERR=6680)
6540 IF X$(1,1)<>$04$ THEN GOTO 6680
6550 READ RECORD(CHANNEL,SIZ=10)X$
6560 LET SIZE=DEC($00$+X$(1,2))
6570 CALL "SYC.UB",CHANNEL,"",X$,"10",STATUS
6580 LET STATUS=0,P=POS("REM"=X$)
6590 IF P=0 THEN GOTO 6680
6600 LET X$=X$(P+3),P=POS("-"=X$)
6610 IF P=0 THEN GOTO 6680
6620 LET X$=FNP$(FNS$(X$(P+1)))
6630 IF X$="" THEN GOTO 6680
6640 IF X$(LEN(X$))=$22$ THEN LET X$=X$(1,LEN(X$)-1)
6650 LET DESCRIPTION$(1)=X$
6680 CLOSE (CHANNEL,ERR=6690)
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNS$(Q$)=CVS(Q$,1)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9910 CALL "SYC.WD",NAME$
9950 RUN "SYS.AA"
9999 END

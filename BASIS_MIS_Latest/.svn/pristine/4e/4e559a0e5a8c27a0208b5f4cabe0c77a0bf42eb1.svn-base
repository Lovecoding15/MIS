0010 REM "ARM - Customer Maintenance (Lead Overlay)"
0020 REM "Program ARM.MA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETESC 9000
0055 SETERR 9000
0060 ON O0 GOTO 0070,4000,0700
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=24
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ARM-02",FILES$[3]="ARM-03"
0130 LET FILES$[4]="ARM-04",FILES$[5]="ARM-05",FILES$[6]="ARM-06"
0135 LET FILES$[7]="ARM-09",FILES$[8]="ARM-10",FILES$[9]="ARS-10"
0140 LET FILES$[10]="ARM-10",FILES$[11]="ARS-10",FILES$[12]="ART-01"
0145 LET FILES$[13]="ART-11",FILES$[14]="SYS-01"
0150 LET FILES$[15]="ARE-03",FILES$[16]="ARE-13",FILES$[17]="ARE-20"
0155 LET FILES$[18]="ARE-23",FILES$[19]="ART-33",FILES$[20]="ART-03"
0160 LET FILES$[21]="ART-13",FILES$[22]="ART-23",FILES$[23]="ART-33"
0165 LET FILES$[24]="IVM-01"
0170 CALL "SYC.DA",1,1,14,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0175 IF STATUS>0 THEN GOTO 9900
0180 LET ARM01_DEV=CHANNELS[1],ARM02_DEV=CHANNELS[2],ARM03_DEV=CHANNELS[3]
0183 LET ARM04_DEV=CHANNELS[4],ARM05_DEV=CHANNELS[5],ARM06_DEV=CHANNELS[6]
0185 LET ARM09_DEV=CHANNELS[7],ARM10_DEV=CHANNELS[8],ARS10_DEV=CHANNELS[9]
0187 LET ARM10_DEV=CHANNELS[10],ARS10_DEV=CHANNELS[11],ART01_DEV=CHANNELS[12]
0190 LET ART11_DEV=CHANNELS[13],SYS01_DEV=CHANNELS[14]
0200 REM " --- IOLists
0210 ARM01A: IOLIST A0$(1),A1$(1)
0220 ARM02A: IOLIST B0$,B1$
0230 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0240 ARS01B: IOLIST C9$(1)
0250 ARS10N: IOLIST Y0$,Y[ALL]
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0320 IVS01A: IOLIST X$,X$,I2$,I3$,X$,X$,I6$,I7$,I8$
0330 SHS01A: IOLIST X$,SHIP$
0400 REM " --- Parameters
0405 DIM P[2],I[3],G[4],C9$(36),I3$(20)
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$,SHIP$="N"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0430 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0440 FIND (SYS01_DEV,KEY=N0$+"SH00",DOM=0450)IOL=SHS01A
0445 LET SHIP$=SHIP$(1,1)
0450 FOR X=0 TO 4
0455 LET G[X]=NUM(G2$(X*2+1,2),ERR=0460)
0460 NEXT X
0465 LET G[2]=FNYY_YEAR(G2$(5,2))
0470 LET C9$(7,1)="N"
0475 FIND (SYS01_DEV,KEY=N0$+"AR01",DOM=0480)IOL=ARS01B
0480 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0485 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=0500)IOL=IVS01A
0490 LET I[0]=NUM(I2$(1,2)),I[2]=NUM(I2$(5,1)),I[3]=NUM(I2$(7,2)),S0$=I3$(17,1)
0500 REM " --- Initializations"
0510 DIM J0$(20,"0"),J$(80,"-")
0520 DIM A0$(8),A1$(305),B0$(10),B1$(64),B[10],E[5],Y[4]
0530 LET K0=1,K9=8,L0=6,L1=7,L9=20,O8=0,A0$(1,2)=N0$,NEW_CUST$="NO"
0550 LET M8$="(###)-###-####",M9$="###-##-####",G5=LEN(G5$),M7$="00000"
0560 CALL "SYC.RM",M1$,M1,6,14
0600 REM " --- Other modules installed?
0605 DIM INFO$[20]
0610 CALL "SYC.VA","OP",INFO$[ALL]
0615 LET OP$=INFO$[20]
0620 CALL "SYC.VA","SA",INFO$[ALL]
0625 LET SA$=INFO$[20]
0630 REM " --- Other file opens
0645 IF OP$<>"Y" THEN GOTO 0680
0660 CALL "SYC.DA",1,15,24,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0670 IF STATUS>0 THEN GOTO 9900
0680 LET ARE03_DEV=CHANNELS[15],ARE13_DEV=CHANNELS[16],ARE20_DEV=CHANNELS[17]
0685 LET ARE23_DEV=CHANNELS[18],ARE33_DEV=CHANNELS[19],ART03_DEV=CHANNELS[20]
0690 LET ART13_DEV=CHANNELS[21],ART23_DEV=CHANNELS[22],ART33_DEV=CHANNELS[23]
0695 LET IVM01_DEV=CHANNELS[24]
0700 REM " --- Background
0710 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0720 PRINT 'SB',@(2,3),"Customer No:",@(9,4),"Name:",@(0,6),'CE','SF',
0790 IF O0<>2 THEN GOTO 0900
0800 REM " --- Clear After Delete"
0820 PRINT @(0,L0),'CE',
0890 GOTO 1000
0900 REM " --- Init File"
0920 READ (ARM01_DEV,KEY=A0$(1,2),DOM=0930)
1000 REM " --- Key"
1010 PRINT 'CF',
1015 PRINT @(15,4),'CL',@(15,3),'CL',
1020 LET V2$="",V2$=KEY(ARM01_DEV,END=1050)
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1050 LET V0$="M",V1$="KCE",V3$=P1$,V0=6,V1=15,V2=3,V4$="<F3>=Customer Lookup"
1055 IF P3$(13,1)="Y" THEN LET V4$="<F2>=Auto Number   "+V4$
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 9900
1080 IF V3=3 THEN CALL "SYC.LC",V$
1085 IF P3$(13,1)="Y" AND V3=2 THEN PRINT @(V1,V2),FILL(6); GOSUB 6000
1090 IF V$="" THEN GOTO 0900
1100 LET A0$(3)=V$
1110 PRINT @(15,3),FNF$(A0$(3,P[0]),M0$)
1200 REM " --- Find Key"
1220 FIND (ARM01_DEV,KEY=A0$(1,K9),DOM=1400)IOL=ARM01A
1230 FIND (ARM02_DEV,KEY=A0$(1,K9)+"  ",DOM=1240)IOL=ARM02A
1300 REM " --- Display"
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record"
1405 IF C9=1 THEN LET C9=0; GOTO 1500
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1430 IF V3<>0 THEN GOTO 1000
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-Up"
1510 LET A1$(1)="",O0=0,O8=1,NEW_CUST$="YES"
1530 LET A1$(227,1)="Y"
1590 GOTO 2100
2000 REM " --- Overlay"
2020 LET O0=1
2100 RUN "ARM.MB"
4000 REM " --- Options"
4010 DIM MENUS$[4]
4020 LET MENUS=1,MENUS$[MENUS]="ARM.MA",WIN_X=40,WIN_Y=L0+1,NEW_CUST$="NO"
4030 LET HISTORY$="",O0=1,O9=0
4040 IF OP$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="ARM.ML"
4050 IF OP$="Y" THEN IF P3$(77,1)="Y" THEN IF B1$(38,1)="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="ARM.MI"
4060 IF P3$(12,1)="Y" AND OP$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="ARM.MJ"
4065 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4070 CALL "SYC.WC",1,0,80,0,0,L0-1,0
4075 PRINT @(0,L0),'CE',
4080 IF SELECTION$="" THEN GOTO 1000
4085 IF SELECTION$="SYC.KA" THEN GOTO 4100
4090 RUN SELECTION$,ERR=4000
4100 REM " --- Customer comments"
4110 DIM INFO$[5],INFO[5]
4120 LET X$="Customer "+FNF$(A0$(3,P[0]),M0$)+" "+FNP$(A1$(1,30))
4130 LET INFO$[0]="ARM-05",INFO$[1]=A0$,INFO$[2]=X$,ACTION=0,INFO[1]=L0
4150 CALL "SYC.KA",ACTION,INFO$[ALL],INFO[ALL]
4190 GOTO 4000
5000 REM " --- Display"
5020 PRINT 'SB',@(15,3),FNF$(A0$(3,P[0]),M0$),@(15,4),A1$(1,30),'SF',
5090 RETURN 
6000 REM " --- Auto Number"
6001 CALL "SYC.YN",1,"Are You Certain That You Wish To Create A New Number",1,V$,V3
6004 IF V$="N" THEN EXITTO 1000
6006 IF V$<>"Y" THEN GOTO 6000
6010 LET C9=1,Y0$=N0$+"N"
6020 EXTRACT (ARS10_DEV,KEY=Y0$,DOM=6030)IOL=ARS10N
6030 LET I=Y[0]
6040 DIM MAXNUM$(P[0],"9")
6050 LET V$=STR(I:J0$(1,P[0]))
6052 LET V$=V$+J0$,V$=V$(1,6)
6055 LET I=I+1
6060 IF I<=NUM(MAXNUM$) THEN GOTO 6070
6061 DIM MESSAGE$[1]
6062 LET MESSAGE$[0]="There Are No More Numbers Available (<Enter>=Continue)"
6066 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
6067 READ (ARS10_DEV); REM "Unextract the Record"
6068 EXITTO 1000
6070 LET Y[0]=I
6075 FIND (ARM01_DEV,KEY=N0$+V$,DOM=6080); GOTO 6030
6080 WRITE (ARS10_DEV,KEY=N0$+"N")IOL=ARS10N
6090 PRINT @(15,3),FNF$(V$(1,P[0]),M0$),
6099 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

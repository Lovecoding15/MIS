0010 REM "POE - Purchase Order Invoice Entry (GL Distribution)"
0020 REM "Program POE.ED"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST B0$(1),B1$(1)
0310 POE35A: IOLIST W0$(1),W1$(1),W[ALL]
0500 REM " --- Initializations"
0510 DIM W0$(23),W1$(32),W[4],B0$(12),B1$(35),O[4]
0550 LET W0$(1)=A0$,L=L0,S1=77-M1,UH=78,UNITS$="",O[4]=77
0560 LET G5=LEN(G5$),G6=LEN(G6$),G7=LEN(G7$),O[1]=4,O[2]=O[1]+MAX(9,G5+1)
0570 IF G4$(3,1)="Y" THEN LET O[4]=76-G7,UH=76-6,UNITS$="Units"
0580 LET O[3]=O[4]-G6-1,DW=MIN(35,O[3]-O[2]-1),INVOICE=TOTAL
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,1,0,L0-2,0
0720 PRINT @(8,L0-2),'SB',"GL Distribution",@(S1-8)," Total: ",'SF',TOTAL:M1$,@(0,L0-1),'CE','SB',"Seq Account",@(O[2]),"Description",@(O[3]+G6-7),"Amount",@(UH),UNITS$,'SF',
0900 REM " --- Display any existing lines"
0910 GOSUB 6000
0920 GOSUB 5600
0930 IF NUMBER=0 THEN GOTO 1200
0990 GOTO 4000
1000 REM " --- Overflow"
1010 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L0),'LD',
1020 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1110 LET K$=KEY(POE35_DEV,END=1200)
1120 IF POS(A0$=K$)<>1 THEN GOTO 1200
1130 READ (POE35_DEV)IOL=POE35A
1190 GOTO 1330
1200 REM " --- Sequence Number"
1220 LET NUMBER=NUM(W0$(21))+INCREMENT
1230 LET V4$="Enter A 3 Digit Sequence Number"
1240 LET V0$="Z",V1$="KERC",V2$=STR(NUMBER),V3$="",V0=3,V1=0,V2=L,I0=0
1250 GOSUB 7000
1260 IF V3=4 THEN PRINT @(V1,L),FILL(3),; GOTO 4000
1270 IF V$="000" OR V$="" THEN GOTO 1200
1300 REM " --- Display record"
1310 LET W0$(21)=V$
1320 FIND (POE35_DEV,KEY=W0$,DOM=1400)IOL=POE35A
1330 IF W0$<=K9$ THEN GOSUB 5900
1340 GOSUB 5000
1390 GOTO 3300
1400 REM " --- New Record"
1410 DIM W1$(32),W[4]
1450 PRINT @(0,L),W0$(21,3),@(4),'CL',
1490 GOTO 2000
2000 REM " --- GL Account Number"
2010 LET V4$="Enter A Valid GL Account Number (<F3>=Lookup)"
2020 LET V0$="M",V1$="C",V2$=W1$(1,G[3]),V3$=G5$,V0=G[4],V1=4,V2=L
2030 GOSUB 7000
2040 ON V3 GOTO 2100,2000,1200,2050,2000
2050 CALL "SYC.LG",V$
2100 REM " --- Retrieve GL Account"
2110 LET B0$(1)=N0$+V$
2120 FIND (GLM01_DEV,KEY=B0$,DOM=2000)IOL=GLM01A
2130 PRINT @(V1,V2),FNF$(B0$(3,G[3]),G5$),@(O[2]),B1$(1,DW),
2150 LET W1$(1,10)=B0$(3)
2190 GOTO 3200
2200 REM " --- Amount"
2210 LET V4$="Enter The Amount For This Distribution Line"
2220 LET V0$="N",V1$="C",V2$=STR(W[0]),V3$=G6$,V0=G6,V1=O[3],V2=L
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3200,2200,3200
2250 LET W[0]=V
2260 PRINT @(V1,V2),W[0]:G6$,
2290 GOTO 3200
2400 REM " --- Units"
2410 IF G4$(3,1)<>"Y" THEN GOTO 3200
2415 LET V4$="Enter The Units For This Distribution Line"
2420 LET V0$="N",V1$="C",V2$=STR(W[1]),V3$=G7$,V0=G7,V1=O[4],V2=L
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3200,2400,3200
2450 LET W[1]=V
2460 PRINT @(V1,V2),W[1]:G7$,
2490 GOTO 3200
3200 REM " --- Sequence Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 2000,2200,2400,3300
3300 REM " --- Line Correct?"
3310 LET V4$="<Enter>=Correct     <F2>=Change     <D>=Delete"
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=W0$,I0=0
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3300,3200,3300,3350
3350 PRINT @(0,21),'CL',
3360 IF V$="" THEN GOTO 3400
3370 IF V$="D" THEN GOTO 3600
3390 GOTO 3300
3400 REM " --- Write header"
3410 WRITE (POE35_DEV,KEY=W0$)IOL=POE35A
3420 GOSUB 6000
3430 LET L=L+1
3450 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3600 REM " --- Remove"
3610 REMOVE (POE35_DEV,KEY=W0$,DOM=3630)
3620 GOSUB 6000
3630 PRINT @(0,L),'CL',
3640 IF V3=4 THEN GOTO 4000
3690 GOTO 1000
4000 REM " --- Correct?"
4010 PRINT @(3,22),"Is The Above Correct (Y/N/List/Review/Delete/Line #/<F2>=Detail)?",
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=69
4030 GOSUB 7000
4035 ON V3 GOTO 4040,4000,4300,4000,4400
4050 IF V$="L" OR V$="LIST" THEN GOTO 4800
4055 IF V$="R" OR V$="REVIEW" THEN GOTO 4900
4060 IF V$="DELETE" THEN GOTO 4200
4065 LET V=NUM(V$,ERR=4090)
4070 IF V<1 OR V>999 THEN GOTO 4090
4075 LET V$=STR(INT(V):"000")
4080 GOTO 1300
4090 ON POS(V$="YN") GOTO 4000,4400,1000
4200 REM " --- Delete"
4210 PRINT @(0,L0-1),'CE',
4220 LET O0=3
4290 RUN "POE.EA"
4300 REM " --- <F2> Return to detail"
4310 GOSUB 6500
4320 IF STATUS THEN GOTO 4000
4330 IF V3=4 THEN GOTO 4450
4350 PRINT @(0,L0-1),'CE',
4390 RUN "POE.EC"
4400 REM " --- <F4> Exit"
4410 GOSUB 6500
4420 IF STATUS THEN GOTO 4000
4430 IF V3=2 THEN GOTO 4350
4450 PRINT @(0,L0-1),'CE',
4460 LET O0=1
4490 RUN "POE.EA"
4800 REM " --- List"
4810 GOSUB 5600
4890 GOTO 4000
4900 REM " --- Review"
4910 READ (POE35_DEV,KEY=A0$,DOM=4920)
4920 GOSUB 5900
4990 GOTO 1000
5000 REM " --- Display"
5010 LET NUMBER=NUMBER+1,B0$(1)=N0$+W1$(1,10),B1$(1)=NF$
5020 FIND (GLM01_DEV,KEY=B0$,DOM=5080)IOL=GLM01A
5030 IF G4$(3,1)="Y" THEN LET UNITS$=STR(W[1]:G7$)
5080 PRINT @(0,L),W0$(21,3),@(4),FNF$(B0$(3,G[3]),G5$),@(O[2]),B1$(1,DW),@(O[3]),W[0]:G6$,@(O[4]),UNITS$,
5090 RETURN 
5600 REM " --- List"
5610 LET NUMBER=0
5620 GOSUB 5900
5640 READ (POE35_DEV,KEY=A0$,DOM=5650)
5650 LET K$=KEY(POE35_DEV,END=5790)
5660 IF POS(A0$=K$)<>1 THEN GOTO 5790
5670 READ (POE35_DEV)IOL=POE35A
5690 IF L<=L9 THEN GOTO 5750
5700 REM " --- Full screen"
5710 LET V4$="Press <Enter> To Continue:"
5720 LET V0$="S",V1$="CE",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5730 GOSUB 7000
5740 IF V3=4 THEN GOTO 5790
5745 GOSUB 5900
5750 GOSUB 5000
5760 LET L=L+1,K9$=W0$
5770 GOTO 5650
5790 RETURN 
5900 REM " --- Clear Screen"
5910 PRINT @(0,L0),'CE',
5920 LET L=L0
5990 RETURN 
6000 REM " --- Accumulate total distribution"
6010 LET TOTAL=0,X$=W0$
6090 READ (POE35_DEV,KEY=A0$,DOM=6100)
6100 REM " --- Read next line item"
6110 LET K$=KEY(POE35_DEV,END=6200)
6120 IF POS(A0$=K$)<>1 THEN GOTO 6200
6130 READ (POE35_DEV)IOL=POE35A
6140 LET TOTAL=TOTAL+W[0]
6190 GOTO 6100
6200 REM " --- Done with total"
6210 LET BALANCE=TOTAL-(A[0]-INVOICE)
6220 PRINT @(S1,L0-2),BALANCE:M1$,
6280 READ (POE35_DEV,KEY=X$,DOM=6290)IOL=POE35A
6290 RETURN 
6500 REM " --- Check that everything is in balance before exiting"
6510 GOSUB 6000
6520 LET STATUS=0
6530 IF BALANCE=0 THEN GOTO 6590
6535 LET X$="This Invoice Is Out Of Balance By "+FNS$(FNP$(STR(BALANCE:G6$)))
6540 LET X=LEN(X$)
6545 IF X<48 THEN LET X$=FILL(INT((48-X)/2))+X$
6550 LET MESSAGE$[0]=X$,X=LEN(X$),STATUS=1,V=0
6555 IF X>48 THEN LET V=INT((X-48)/2)
6560 LET MESSAGE$[1]=FILL(V)+"<Enter>=Continue/<F2>=Detail/<F4>=Exit"
6565 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
6570 IF V$="SPECIAL" THEN LET STATUS=0
6580 IF V3=2 OR V3=4 THEN LET STATUS=0
6590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8075 DEF FNS$(Q$)=CVS(Q$,1)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

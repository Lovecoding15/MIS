0010 REM "POX - Rebuild Purchase Order Cross-References"
0020 REM "Program POX.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files
0105 LET FILES=21
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="SYS-01",FILES$[2]="POE-01",FILES$[3]="POE-02"
0120 LET FILES$[4]="POE-03",FILES$[5]="POE-04",FILES$[6]="POE-11"
0125 LET FILES$[7]="POE-12",FILES$[8]="POE-13",FILES$[9]="POE-14"
0130 LET FILES$[10]="POM-02",FILES$[11]="POE-31",FILES$[12]="POE-32"
0135 LET FILES$[13]="POE-33",FILES$[14]="POE-34",FILES$[15]="POE-41"
0140 LET FILES$[16]="POE-42",FILES$[17]="POE-43",FILES$[18]="POE-44"
0145 LET FILES$[19]="POE-52",FILES$[20]="POE-53",FILES$[21]="POE-54"
0150 CALL "SYC.DA",1,1,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],POE01_DEV=CHANNELS[2],POE02_DEV=CHANNELS[3]
0165 LET POE03_DEV=CHANNELS[4],POE04_DEV=CHANNELS[5],POE11_DEV=CHANNELS[6]
0170 LET POE12_DEV=CHANNELS[7],POE13_DEV=CHANNELS[8],POE14_DEV=CHANNELS[9]
0175 LET POM02_DEV=CHANNELS[10]
0200 REM " --- IOLists
0210 POE02A: IOLIST A0$(1),A1$(1)
0220 POE12A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1)
0310 POM02A: IOLIST Y0$(1),Y1$(1)
0330 POE32A: IOLIST X3$(1)
0340 POE42A: IOLIST X4$(1)
0350 POE52A: IOLIST X5$(1)
0500 REM " --- Initializations"
0510 DIM A0$(22),A1$(160),W0$(25),W1$(48),W2$(32),W3$(22),Y0$(4),Y1$(32)
0550 FOR X=11 TO 21
0560 LET OPTIONS$[X]="CL"
0570 NEXT X
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,1,0,4,0
0720 PRINT 'SB',@(28,3),"Processing:",'SF',
0800 REM " --- Options
0810 LET V4$="Do You Wish To Rebuild Purchase Order Cross-References"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM " --- Clear Files
0910 CALL "SYC.DA",1,11,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0920 IF STATUS THEN GOTO 9900
0930 LET POE31_DEV=CHANNELS[11],POE32_DEV=CHANNELS[12],POE33_DEV=CHANNELS[13]
0940 LET POE34_DEV=CHANNELS[14],POE41_DEV=CHANNELS[15],POE42_DEV=CHANNELS[16]
0950 LET POE43_DEV=CHANNELS[17],POE44_DEV=CHANNELS[18],POE52_DEV=CHANNELS[19]
0960 LET POE53_DEV=CHANNELS[20],POE54_DEV=CHANNELS[21]
0990 CALL "SYC.NB","Updating",25,COLUMN
1000 REM " --- Requisitions"
1010 DIM X3$(15),X4$(38),X5$(22)
1020 LET K=15,N=9,REQUISITION=1,RECEIPT=0,X$="Requisitions"
1030 LET HEADER_DEV=POE01_DEV,DETAIL_DEV=POE11_DEV
1040 LET XREF3_DEV=POE31_DEV,XREF4_DEV=POE41_DEV,XREF5_DEV=0
1050 GOSUB 6000
1500 REM " --- Purchase Orders"
1510 DIM X3$(15),X4$(38),X5$(22)
1520 LET K=15,N=9,REQUISITION=0,RECEIPT=0,X$="Purchase Orders"
1530 LET HEADER_DEV=POE02_DEV,DETAIL_DEV=POE12_DEV
1540 LET XREF3_DEV=POE32_DEV,XREF4_DEV=POE42_DEV,XREF5_DEV=POE52_DEV
1550 GOSUB 6000
2000 REM " --- QA Receipts"
2010 DIM X3$(22),X4$(45),X5$(22)
2020 LET K=22,N=16,REQUISITION=0,RECEIPT=1,X$="Quality Assurance Receipts"
2030 LET HEADER_DEV=POE03_DEV,DETAIL_DEV=POE13_DEV
2040 LET XREF3_DEV=POE33_DEV,XREF4_DEV=POE43_DEV,XREF5_DEV=POE53_DEV
2050 GOSUB 6000
2500 REM " --- PO Receipts"
2510 DIM X3$(22),X4$(45),X5$(22)
2520 LET K=22,N=16,REQUISITION=0,RECEIPT=1,X$="Purchase Order Receipts"
2530 LET HEADER_DEV=POE04_DEV,DETAIL_DEV=POE14_DEV
2540 LET XREF3_DEV=POE34_DEV,XREF4_DEV=POE44_DEV,XREF5_DEV=POE54_DEV
2550 GOSUB 6000
4000 REM " --- All done"
4090 GOTO 9900
6000 REM " --- Rebuild xrefs"
6010 PRINT @(40,3),X$,'CL',
6090 READ (HEADER_DEV,KEY="",DOM=6100)
6100 REM " --- Read next header"
6110 READ (HEADER_DEV,END=6900)IOL=POE02A
6120 IF RECEIPT THEN GOTO 6200
6130 PRINT @(COLUMN,11),A0$(1,2)," ",A0$(3,6)," ",A0$(9,7),
6140 LET X3$(1)=A0$(1,2)+A0$(9,7)+A0$(3)
6150 WRITE (XREF3_DEV,KEY=X3$)IOL=POE32A
6160 IF FNP$(A1$(112,7))="" THEN GOTO 6400
6170 LET X5$(1)=A0$(1,2)+A1$(112,7)+A0$(3)
6180 IF XREF5_DEV THEN WRITE (XREF5_DEV,KEY=X5$)IOL=POE52A
6190 GOTO 6400
6200 REM " --- Receipt xrefs"
6210 PRINT @(COLUMN,11),A0$(1,2)," ",A0$(3,6)," ",A0$(9,7)," ",A0$(16,7),
6220 LET X3$(1)=A0$(1,2)+A0$(16,7)+A0$(3)
6230 LET X5$(1)=A0$(1,2)+A0$(9,7)+A0$(3,6)+A0$(16)
6240 WRITE (XREF3_DEV,KEY=X3$)IOL=POE32A
6250 WRITE (XREF5_DEV,KEY=X5$)IOL=POE52A
6400 REM " --- Position detail file"
6490 READ (DETAIL_DEV,KEY=A0$(1,K),DOM=6500)
6500 REM " --- Read next detail"
6510 LET K$=KEY(DETAIL_DEV,END=6700)
6520 IF POS(A0$(1,K)=K$)<>1 THEN GOTO 6700
6530 READ (DETAIL_DEV)IOL=POE12A
6540 IF W0$(1,2)+W1$(1,2)=Y0$(1,4) THEN GOTO 6600
6550 LET Y0$(1,4)=W0$(1,2)+W1$(1,2),Y1$(1)=""
6560 FIND (POM02_DEV,KEY=Y0$,DOM=6600)IOL=POM02A
6600 REM " --- Standard type line?"
6610 IF Y1$(21,1)<>"S" THEN GOTO 6500
6620 LET X4$(1)=W0$(1,2)+W3$(3)+W0$(N,10)+W0$(3)
6630 WRITE (XREF4_DEV,KEY=X4$)IOL=POE42A
6690 GOTO 6500
6700 REM " --- Done with line items"
6790 GOTO 6100
6900 REM " --- Done with current file set"
6990 RETURN 
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

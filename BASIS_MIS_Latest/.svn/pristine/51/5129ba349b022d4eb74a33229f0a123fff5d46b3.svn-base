0010 REM "GLC - Audit Control (Create !GLCONTROL)"
0020 REM "Program GLC.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0042 REM "PROGRAM$ : Calling register program (Input)"
0044 REM "SYSTEM$  : Calling application module (Input)"
0046 REM "GLW11$   : GLW-11 file name (returned)"
0048 REM "GL$      : Post to G/L (Y/N)? (Returned)"
0050 REM "STATUS   : 0=No Error (Returned)
0075 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER PROGRAM$,SYSTEM$,GLW11$,GL$,STATUS
0100 REM " --- Open/Lock Files"
0105 LET FILES=5
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="SYM-04",FILES$[3]="SYM-29"
0150 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9200
0170 LET SYS01_DEV=CHANNELS[1],SYM04_DEV=CHANNELS[2],SYM29_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0220 GLM06A: IOLIST B0$(1),B1$(1),B2$(1),B3$,B[ALL]
0230 GLM03A: IOLIST C0$(1),C1$,C[ALL]
0250 SYM04A: IOLIST D0$(1),D1$(1)
0260 GLS01A: IOLIST G0$,G1$,G2$,G3$(1),G4$(1),M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$
0280 PARAMS: IOLIST X$,P1$,P2$,P3$,P4$
0400 REM " --- Parameters"
0410 DIM D0$(2),D1$(64),G3$(325),G4$(32),INFO$[20]
0415 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),D0$(1)=SYSTEM$,MP=13,MODULE$=SYSTEM$
0425 IF SYSTEM$="BM" THEN LET MODULE$="IV"
0435 IF SYSTEM$="PO" THEN LET MODULE$="IV"
0440 IF SYSTEM$="OP" THEN LET MODULE$="AR"
0445 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 FIND (SYS01_DEV,KEY=N0$+MODULE$+"00",DOM=9200)IOL=PARAMS
0455 FIND (SYM04_DEV,KEY=D0$,DOM=9200)IOL=SYM04A
0460 CALL "SYC.VA",SYSTEM$,INFO$[ALL]
0465 LET GL$=INFO$[9]
0490 IF GL$<>"Y" THEN GOTO 4000
0500 REM " --- Initializations"
0510 DIM B0$(20),B1$(64),B2$(30),B[6],C0$(32),C[2]
0520 DIM GLCONTROL$(640),MESSAGE$[5],M0$(7,"0"),PRCS$(10),PRGM$(8)
0530 LET PRGM$(1)=PROGRAM$
0550 LET FOUND=0,LINES=0,ACTION=0,TOTPER=NUM(G2$(1,2))
0560 LET CR$="Press <Enter> To Continue",GLW11_DEV=0
0600 REM " --- Additional File Opens"
0610 LET FILES$[4]="GLM-03",FILES$[5]="GLM-06"
0630 CALL "SYC.DA",1,4,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0640 IF STATUS>0 THEN GOTO 9200
0650 LET GLM03_DEV=CHANNELS[4],GLM06_DEV=CHANNELS[5]
0900 REM " --- Extract current period/year for system"
0910 IF SYSTEM$="GL" THEN GOTO 0950
0920 LET SYSPERIOD$=P4$(1,2),SYSYEAR$=P4$(3,2)
0930 GOTO 1000
0950 LET SYSPERIOD$=P2$(3,2),SYSYEAR$=P2$(5,2)
1000 REM " --- Clear !GLCONTROL (if it exists)"
1010 LET X$=STBL("!CLEAR","!GLCONTROL",ERR=1020)
1100 REM " --- Retrieve SYM-29 Process"
1110 READ (SYM29_DEV,KEY=PRGM$,DOM=1120)
1120 LET SYM29_K$=KEY(SYM29_DEV,END=1500)
1140 IF POS(PRGM$=SYM29_K$)<>1 THEN GOTO 1500
1150 LET PRCS$(1)=SYM29_K$(9,10)
1200 REM " --- Retrieve GLM-06 record"
1210 LET B0$(1)=N0$+PRCS$+PRGM$
1220 FIND (GLM06_DEV,KEY=B0$,DOM=3500)IOL=GLM06A
1240 LET FOUND=1
1290 GOTO 1500
1500 REM " --- Initialize !GLCONTROL"
1510 LET GLCONTROL$(1,2)=N0$,GLCONTROL$(3,8)=PRGM$,GLCONTROL$(11,2)=SYSTEM$
1520 LET GLCONTROL$(13,1)=D1$(46,1),GLCONTROL$(14,2)=SYSPERIOD$
1530 LET GLCONTROL$(16,2)=FNYY_YY21$(SYSYEAR$),GLCONTROL$(18,2)=B1$(1,2)
1540 LET GLCONTROL$(20,7)="",GLCONTROL$(27,1)="N"
1550 LET GLCONTROL$(596,10)=PRCS$
1600 REM " --- Include G/L Parameters"
1610 LET GLCONTROL$(28,10)=G2$,GLCONTROL$(38,7)=G4$(1,7)
1620 LET GLCONTROL$(559,MP)=G3$(274,MP)
2000 REM " --- Set fields based on GLM-06 record"
2010 IF FOUND=0 THEN GOTO 2300
2020 LET GLCONTROL$(48,1)=B1$(37,1),GLCONTROL$(49,1)=B1$(38,1)
2030 LET GLCONTROL$(50,1)=B1$(40,1),GLCONTROL$(51,30)=B2$
2080 IF GLCONTROL$(38,1)="Y" THEN LET GLCONTROL$(38,1)=B1$(39,1)
2100 REM " --- Update Audit Control"
2130 LET B[0]=B[0]+1
2140 IF B[0]>9999999 THEN LET B[0]=1
2150 IF B1$(37,1)="Y" THEN LET GLCONTROL$(20,7)=STR(B[0]:M0$)
2200 REM " --- Update GLM-06 Record"
2210 LET B1$(11,6)=F0$(18,6),X$=FNE$(Q$),B1$(17,4)=X$(1,2)+X$(4,2)
2280 WRITE (GLM06_DEV,KEY=B0$)IOL=GLM06A
2290 GOTO 2500
2300 REM " --- Missing GLM-06 record"
2310 LET GLCONTROL$(48,3)="NYN",GLCONTROL$(51,30)="Summarized G/L Posting"
2500 REM " --- Set fields based on Journal ID Code"
2510 LET C0$(1)=N0$+B1$(1,2),GLCONTROL$(45,3)="NNN"
2520 FIND (GLM03_DEV,KEY=C0$(1,4),DOM=2600)IOL=GLM03A
2530 LET GLCONTROL$(46,2)=C0$(26,2)
2600 REM " --- Determine fiscal year limits"
2610 LET NP=NUM(GLCONTROL$(28,2)),Y2=FNYY_YEAR(GLCONTROL$(32,2)),Y1=Y2-2
2620 IF GLCONTROL$(43,1)="Y" THEN LET Y1=Y2-1,Y2=Y2+1
2700 REM " --- Calculate period beginning/ending dates"
2710 FOR Y=Y1 TO Y2
2720 LET X=169
2730 IF Y=Y1 THEN LET X=91
2735 IF Y=Y2 THEN LET X=247
2740 FOR P=1 TO NP
2750 LET XP=P,XY=Y
2760 CALL "SYC.PA",SYS01_DEV,XP,XY,BEGDATE$,ENDDATE$,STATUS
2770 IF STATUS=0 THEN LET GLCONTROL$(X,6)=FNYY_YY21$(FNM$(FNC$(BEGDATE$))),GLCONTROL$(X+234,6)=FNYY_YY21$(FNM$(FNC$(ENDDATE$)))
2775 LET X=X+6
2780 NEXT P
2790 NEXT Y
2800 REM " --- Store Application & G/L Period Beginning/Ending Dates"
2810 LET XP=NUM(GLCONTROL$(14,2)),XY=FNYY_YEAR(GLCONTROL$(16,2))
2820 CALL "SYC.PA",SYS01_DEV,XP,XY,BEGDATE$,ENDDATE$,STATUS
2830 IF STATUS=0 THEN LET GLCONTROL$(572,6)=FNYY_YY21$(FNM$(FNC$(BEGDATE$))),GLCONTROL$(578,6)=FNYY_YY21$(FNM$(FNC$(ENDDATE$)))
2840 LET XP=NUM(GLCONTROL$(30,2)),XY=FNYY_YEAR(GLCONTROL$(32,2))
2850 CALL "SYC.PA",SYS01_DEV,XP,XY,BEGDATE$,ENDDATE$,STATUS
2860 IF STATUS=0 THEN LET GLCONTROL$(584,6)=FNYY_YY21$(FNM$(FNC$(BEGDATE$))),GLCONTROL$(590,6)=FNYY_YY21$(FNM$(FNC$(ENDDATE$)))
2900 REM " --- Create !GLCONTROL"
2910 LET GLCONTROL$(1)=STBL("!GLCONTROL",GLCONTROL$,ERR=4020)
3000 REM " --- Open summary work file"
3005 IF FOUND=0 THEN GOTO 4000
3010 IF B1$(38,1)<>"Y" THEN GOTO 4000
3020 LET GLW11$=FNP$(B1$(31,6))
3030 IF GLW11$="" THEN LET GLW11$="GLW-11"
3040 LET FILES$[4]=GLW11$,CHANNELS[4]=0,OPTIONS$[4]="F"
3050 CALL "SYC.DA",1,4,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
3060 IF STATUS THEN GOTO 3100
3070 LET GLW11_DEV=CHANNELS[4]
3080 IF GLW11_DEV=0 THEN GOTO 3100
3090 GOTO 4000
3100 REM " --- Create work file"
3110 LET BATCH$=""
3120 LET PROCESS$=STBL("!PROCESS",ERR=3150),BATCH$="."+PROCESS$(13,3)
3150 CALL "SYC.ML",GLW11$+BATCH$,"GLW-11",STATUS
3160 IF STATUS<>0 THEN GOTO 3700
3190 GOTO 3000
3500 REM " --- Missing GLM-06 Control Record"
3510 LET MESSAGE$[0]="The G/L Posting Control Record For This Task Is Missing. Use The"
3520 LET MESSAGE$[1]="G/L Posting Control Maintenance Task To Define The Parameters"
3530 LET MESSAGE$[2]="For This Task. "+CR$
3560 LET ACTION=2,LINES=3,STATUS=11
3590 GOTO 9400
3700 REM " --- Unable to define summary work file"
3710 LET MESSAGE$[0]="Unable To Define G/L Summary Work File. "+CR$
3720 LET ACTION=1,LINES=2
3790 GOTO 9400
3900 REM " --- Register in use"
3910 LET MESSAGE$[0]="This Report Is Being Run By Another User. "+CR$
3920 LET ACTION=1,LINES=1,STATUS=256
3990 GOTO 9400
4000 REM " --- All Done"
4010 LET STATUS=0
4090 GOTO 9900
8000 REM " --- Function Definitions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8050 DEF FNE$(Q$)=DATE(0:"%Hz:%Mz")
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9220 LET LINES=0
9290 GOTO 9400
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9400 REM " --- Disable GL if error and return requested"
9410 LET GL$="N"
9420 IF STATUS=0 THEN LET STATUS=E0
9430 IF STATUS=0 THEN LET STATUS=ERR
9440 IF STATUS=0 THEN LET STATUS=256
9450 IF LINES>0 THEN CALL "SYC.XA",ERR=9900,ACTION,MESSAGE$[ALL],LINES-1,22,-1,V$,V3
9460 GOTO 9900
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9830 LET STATUS=ERR
9900 REM " --- Return to calling program"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9921)
9930 NEXT X
9950 EXIT 
9999 END

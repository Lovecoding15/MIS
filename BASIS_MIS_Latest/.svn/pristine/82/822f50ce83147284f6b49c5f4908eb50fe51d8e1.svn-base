0010 REM "MPU - MRP Generation - Input Overlay                             "
0020 REM "Program MPU.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "09Oct96 JJD: Open the proper calendar files for processing.(105,140,145,625,660,665,670)"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files "
0105 LET FILES=34
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0112 LET FILES$[1]="MPE-01",FILES$[2]="BMM-01",FILES$[3]="BMM-02"
0115 LET FILES$[4]="BMM-03",FILES$[5]="POE-01",FILES$[6]="POE-02"
0117 LET FILES$[7]="POE-12",FILES$[8]="POE-42",FILES$[9]="POE-01"
0120 LET FILES$[10]="POE-11",FILES$[11]="SYS-01",FILES$[12]="IVM-01"
0122 LET FILES$[13]="IVM-02",FILES$[14]="MPE-01",FILES$[15]="MPM-01"
0125 LET FILES$[16]="POM-02",FILES$[17]="ARM-10",FILES$[18]="ARE-03"
0127 LET FILES$[19]="ARE-07",FILES$[20]="ARE-13",FILES$[21]="WOE-01"
0130 LET FILES$[22]="WOE-03",FILES$[23]="WOE-23",FILES$[24]="WOE-02"
0132 LET FILES$[25]="WOE-22",FILES$[26]="WOE-32",FILES$[27]="WOE-07"
0135 LET FILES$[28]="WOM-05",FILES$[29]="WOM-06",FILES$[30]="WOE-04"
0140 LET FILES$[31]="MPW-06",FILES$[32]="MPW-02",FILES$[33]="MPM-02"
0145 LET FILES$[34]="POM-01"
0150 CALL "SYC.DA",1,1,16,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0157 LET MPE01_DEV=CHANNELS[1],BMM01_DEV=CHANNELS[2],BMM02_DEV=CHANNELS[3]
0160 LET BMM03_DEV=CHANNELS[4],POE01_DEV=CHANNELS[5],POE02_DEV=CHANNELS[6]
0162 LET POE12_DEV=CHANNELS[7],POE42_DEV=CHANNELS[8],POE01_DEV=CHANNELS[9]
0165 LET POE11_DEV=CHANNELS[10],SYS01_DEV=CHANNELS[11],IVM01_DEV=CHANNELS[12]
0167 LET IVM02_DEV=CHANNELS[13],MPE01X_DEV=CHANNELS[14],MPM01_DEV=CHANNELS[15]
0170 LET POM02_DEV=CHANNELS[16]
0200 REM " --- IOLISTS"
0210 BMS01A: IOLIST X$,P1$,P2$,P3$,P4,P5
0220 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0230 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$
0240 MPM01A: IOLIST *,DESC$(1)
0400 REM " --- Parameters "
0405 DIM P[2],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="BM",N4$=F5$
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=BMS01A
0425 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0430 CALL "SYC.VA","OP",INFO$[ALL]
0435 LET AR$=INFO$[20]
0440 CALL "SYC.VA","WO",INFO$[ALL]
0445 LET WO$=INFO$[20]
0500 REM " --- Initialize Data "
0505 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0510 LET SAFETY_STOCK$="N",PURCH_REQ$="Y",PURGE_REQ$="Y"
0515 LET INCLUDE_QUOTE$="N",INCLUDE_PLANNED$="N",PURGE_PLANNED$="N"
0520 IF WO$<>"Y" THEN GOTO 0530
0525 LET INCLUDE_QUOTE$="Y",INCLUDE_PLANNED$="Y",PURGE_PLANNED$="Y"
0530 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$)
0535 IF M2<10 THEN CALL "SYC.RM",M2$,M2,10,15
0540 LET IVS01_PRECISION=NUM(I2$(5,1))
0545 PRECISION IVS01_PRECISION
0600 REM " --- Additional File Opens "
0605 IF AR$<>"Y" THEN GOTO 0625
0610 CALL "SYC.DA",1,17,20,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0615 IF STATUS THEN GOTO 9900
0620 LET ARM10_DEV=CHANNELS[17],ARE03_DEV=CHANNELS[18],ARE07_DEV=CHANNELS[19]
0622 LET ARE13_DEV=CHANNELS[20]
0625 IF WO$<>"Y" THEN GOTO 0660
0630 CALL "SYC.DA",1,21,30,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0635 IF STATUS THEN GOTO 9900
0640 LET WOE01_DEV=CHANNELS[21],WOE03_DEV=CHANNELS[22],WOE23_DEV=CHANNELS[23]
0645 LET WOE02_DEV=CHANNELS[24],WOE22_DEV=CHANNELS[25],WOE32_DEV=CHANNELS[26]
0650 LET WOE07_DEV=CHANNELS[27],WOM05_DEV=CHANNELS[28],WOM06_DEV=CHANNELS[29]
0655 LET WOE04_DEV=CHANNELS[30]
0660 CALL "SYC.DA",1,33,34,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0665 IF STATUS THEN GOTO 9900
0670 LET MPM02_DEV=CHANNELS[33],POM01_DEV=CHANNELS[34]
0700 REM " --- Background "
0705 FOR X=4 TO 10 STEP 2
0710 CALL "SYC.WC",1,0,80,0,0,X,0
0715 NEXT X
0720 IF WO$<>"Y" THEN GOTO 0740
0725 FOR X=12 TO 16 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0735 NEXT X
0740 PRINT 'SB',@(25,3),"Forecast Type:",
0745 PRINT @(3,5),"Include Allowance for Safety Stock?:",
0750 PRINT @(2,7),"Include Purchases from Requisitions?:",
0755 PRINT @(5,9),"Purge Prior Planned Requisitions?:",
0760 IF WO$<>"Y" THEN GOTO 0780
0765 PRINT @(11,11),"Include Quoted Work Orders?:",
0770 PRINT @(10,13),"Include Planned Work Orders?:",
0775 PRINT @(6,15),"Purge Prior Planned Work Orders?:",
0780 PRINT 'SF',
1000 REM " --- Option "
1010 PRINT 'CF'
1020 LET FORECAST_TYPE$=""
1030 PRINT @(40,5),SAFETY_STOCK$,@(40,7),PURCH_REQ$,@(40,9),PURGE_REQ$
1040 IF WO$="Y" THEN PRINT @(40,11),INCLUDE_QUOTE$,@(40,13),INCLUDE_PLANNED$,@(40,15),PURGE_PLANNED$
1050 LET V0$="S",V1$="RE",V2$="",V0=3,V1=40,V2=3,Q8$="Y",Q9$="N",PURCH_REQ$="Y",I0=1,X=1
1060 LET V4$="Enter Selections, <F1>=All, <F3>=Lookup, <F4>=End Of Selections"
1070 GOSUB 7000
1080 IF V3=4 AND FORECAST_TYPE$="" THEN GOTO 9900
1090 IF V3=4 THEN GOTO 3900
1100 IF V3=1 THEN GOTO 1500
1110 IF V3<>3 THEN GOTO 1140
1120 PRINT @(0,22),'CL',
1130 GOSUB 4500
1140 LET V$=V$+"   ",V$=V$(1,3)
1150 DIM DESC$(30)
1160 FIND (MPM01_DEV,KEY=N0$+V$,DOM=1050)IOL=MPM01A
1170 LET X=POS(V$=FORECAST_TYPE$,3)
1180 IF X>0 THEN GOTO 1210
1190 LET FORECAST_TYPE$=FORECAST_TYPE$+V$
1200 GOTO 1170
1210 IF WO$="Y" THEN PRINT @(40,11),INCLUDE_QUOTE$,@(40,13),INCLUDE_PLANNED$,@(40,15),PURGE_PLANNED$
1220 LET X=INT(X/3)+1
1230 REM " --- Display Forecast Type Entered "
1240 IF X<=4 THEN PRINT @(5,16+X),V$,@(10,16+X),DESC$
1250 IF X>4 AND X<=8 THEN PRINT @(45,12+X),V$,@(50,12+X),DESC$
1260 GOTO 1050
1500 REM " --- Get all Forecast Types"
1510 LET X=1,FORECAST_TYPE$=""
1520 READ (MPM01_DEV,KEY=N0$,DOM=1530)
1530 LET K$=KEY(MPM01_DEV,END=1630)
1540 IF POS(N0$=K$)<>1 THEN GOTO 1630
1550 DIM DESC$(30)
1560 READ (MPM01_DEV)IOL=MPM01A
1570 IF POS(K$(3,3)=FORECAST_TYPE$,3)>0 THEN GOTO 1530
1580 LET FORECAST_TYPE$=FORECAST_TYPE$+K$(3,3)
1590 IF X<=4 THEN PRINT @(5,16+X),K$(3,3),@(10,16+X),DESC$
1600 IF X>4 AND X<=8 THEN PRINT @(45,12+X),K$(3,3),@(50,12+X),DESC$
1610 LET X=X+1
1620 GOTO 1530
1630 PRINT @(45,3),"***All***",
1640 GOTO 3900
1800 REM " --- Safety Stock "
1810 LET V0$="Y",V1$="C",V2$=SAFETY_STOCK$,V3$="",V4$="Y=Include Safety Stock In MRP Calculations, N=Do Not Include",V0=1,V1=40,V2=5
1820 GOSUB 7000
1830 IF POS(V$="YN")=0 THEN GOTO 1800
1840 LET SAFETY_STOCK$=V$
1850 GOTO 3900
2000 REM " --- Purchase Requisitions?
2010 LET V0$="Y",V1$="C",V2$=PURCH_REQ$,V3$="",V4$="Y=Items on Purchase Requisitions are On Order, N=Ignore Requisitions",V0=1,V1=40,V2=7
2020 GOSUB 7000
2040 IF POS(V$="YN")=0 THEN GOTO 2000
2050 LET PURCH_REQ$=V$
2060 GOTO 3900
2400 REM " --- Purge requisitions?
2410 LET V0$="Y",V1$="C",V2$=PURGE_REQ$,V3$="",V4$="Y=Delete Requisitions Created from Prior MRP Generations, N=Leave Reqs",V0=1,V1=40,V2=9
2420 GOSUB 7000
2440 IF POS(V$="YN")=0 THEN GOTO 2400
2450 LET PURGE_REQ$=V$
2460 GOTO 3900
2600 REM " --- Include Quoted Work Orders
2610 IF WO$="N" THEN GOTO 2670
2620 LET V0$="Y",V1$="C",V2$=INCLUDE_QUOTE$,V3$="",V4$="Y=Include Demand from Quoted Work Orders, N=Ignore Quotes",V0=1,V1=40,V2=11
2630 GOSUB 7000
2650 IF POS(V$="YN")=0 THEN GOTO 2600
2660 LET INCLUDE_QUOTE$=V$
2670 GOTO 3900
2800 REM " --- Include Planned work Orders?
2810 IF WO$="N" THEN GOTO 2870
2820 LET V0$="Y",V1$="C",V2$=INCLUDE_PLANNED$,V3$="",V4$="Y=Include Demand from Planned Work Orders, N=Ignore Planned WO's",V0=1,V1=40,V2=13
2830 GOSUB 7000
2850 IF POS(V$="YN")=0 THEN GOTO 2800
2860 LET INCLUDE_PLANNED$=V$
2870 GOTO 3900
3000 REM " --- Purge Planned work Orders?
3010 IF WO$="N" THEN GOTO 3070
3020 LET V0$="Y",V1$="C",V2$=PURGE_PLANNED$,V3$="",V4$="Y=Delete Planned Work Orders from Prior MRP Generations, N=Leave Them",V0=1,V1=40,V2=15
3030 GOSUB 7000
3050 IF POS(V$="YN")=0 THEN GOTO 3000
3060 LET PURGE_PLANNED$=V$
3070 GOTO 3900
3900 REM " --- Next Field "
3910 IF V3=4 AND I0>1 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1000,1800,2000,2400,2600,2800,3000,4000
4000 REM " --- Overlay "    
4010 LET MESS$="Is The Above Information Correct (Yes/No)?"
4020 LET V0$="S",V1$="C",V2$="No",V3$="",V4$=MESS$,V0=3,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 IF V$="NO" THEN LET V$="",FORECAST_TYPE$="",R$="",R1$=""; GOTO 1000
4060 IF V$<>"YES" THEN GOTO 4000
4070 PRINT @(0,17),'CE'
4080 PRINT @(0,17),'CE'
4090 RUN "MPU.AB"
4500 REM " --- MRP Type Lookup "
4510 LET NUMBER=1,TITLE$="MRP Forecast Types"
4520 DIM DESC$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
4530 LET DESC$[0]="Type",DESC$[1]="Description",ROW=V2+1
4540 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=3,RECORD$=""
4550 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
4560 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,TITLE$="MRP Type Lookup"
4570 CALL "SYC.LK",MPM01_DEV,RECORD$,NUMBER,TITLE$,DESC$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
4580 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

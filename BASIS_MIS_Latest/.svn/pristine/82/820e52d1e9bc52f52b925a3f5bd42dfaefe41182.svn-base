0001 REM Pipe to key generator to get drop dead date <SYKEYDRD>
0010 ENTER SERIAL_NO$,PORT_ID$,REVISION$,KEY$,DROPDEAD$
0011 SETERR 0800
0012 SETESC 0800
0020 LET SERIAL_NO$=CVS(SERIAL_NO$,3),REVISION$=CVS(REVISION$,3)
0030 LET TEMP_FILE_NAME$=STBL("AON")+"tmp/SYKEYDRD"+FID(0)
0040 LET KEY_PROGRAM$=STBL("AON")+"bas/key"
0150 ERASE TEMP_FILE_NAME$,ERR=160
0160 STRING TEMP_FILE_NAME$
0170 LET TEMP=UNT
0180 OPEN (TEMP)TEMP_FILE_NAME$
0210 PRINT (TEMP)"serial "+SERIAL_NO$
0220 PRINT (TEMP)"portid "+PORT_ID$
0230 PRINT (TEMP)"rev "+REVISION$
0240 PRINT (TEMP)"key ",KEY$
0250 PRINT (TEMP)"?"
0270 LET PIPE=UNT
0280 OPEN (PIPE)"<"+KEY_PROGRAM$+"<"+TEMP_FILE_NAME$
0290 READ (PIPE,ERR=400)A$
0300 IF POS("serial"=cvs(A$,3))=1 THEN LET SERIAL_IN$=A$
0305 IF POS("portid"=A$)<>0 THEN LET PORTID_IN$=A$
0310 IF POS("rev"=A$)<>0 THEN LET REV_IN$=A$
0315 IF POS("users"=A$)<>0 THEN LET USERS_IN$=A$
0320 IF POS("dropdead"=A$)<>0 THEN LET DROPDEAD_IN$=A$
0350 GOTO 0290
0400 CLOSE (PIPE); CLOSE (TEMP)
0410 ERASE TEMP_FILE_NAME$,ERR=420
0420 IF POS(SERIAL_NO$=SERIAL_IN$)=0 THEN GOTO NO_GOOD
0430 IF POS(PORT_ID$=PORTID_IN$)=0 THEN GOTO NO_GOOD
0440 LET DROPDEAD$="NONE"
0450 IF LEN(DROPDEAD_IN$)>8 THEN LET DROPDEAD$=DROPDEAD_IN$(9)
0460 REM LET P=POS("("=DROPDEAD$); IF P>1 THEN LET DROPDEAD$=DROPDEAD$(1,P-1)
0470 LET DROPDEAD$=CVS(DROPDEAD$,3)
0590 GOTO EXIT_900
0800 NO_GOOD: 
0810 LET DROPDEAD$="FAILED"
0900 EXIT_900: 
0930 EXIT

0010 REM "GLR - Balance Sheet/Profit & Loss Statement"
0020 REM "Program GLR.PB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "11Jun98 WGH: May not WRITE last Chart Of Accounts break to GLW-02 (930,1150)"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$(1),A1$(1)
0220 GLM02A: IOLIST X0$(1),A[ALL]
0400 REM " --- Parameters"
0410 LET BREAK$="",DESC$=""
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"01",DOM=0490)
0430 CALL "GLC.RG",GLM10_DEV,N0$,BREAK$,DESC$
0490 CALL "SYC.RM",M1$,M1,15,20
0500 REM " --- Initializations"
0510 DIM A0$(2+P[4]),A1$(40),X0$(3+P[4]),J1$(M1,"-"),J2$(M1,"-")
0520 DIM A[MP],T[3],S[10],O[1]
0550 LET H0=80,O[1]=H0-M1,O[0]=O[1]-M1-1,MTDHDR$="",YTDHDR$=""
0560 LET PERIOD=NUM(BEGPER$),WIDTH=O[0]-1
0570 IF BS$="N" THEN LET MTDHDR$="Period To Date",YTDHDR$="Year To Date"
0580 IF BS$="Y" THEN LET J1$="",WIDTH=O[1]-1
0590 LET MTDHDR=O[0]+M1-LEN(MTDHDR$),YTDHDR=O[1]+M1-LEN(YTDHDR$)
0700 REM " --- Background"
0750 CALL "SYC.NB"," Sorting",M0,COLUMN
0800 REM " --- Which GLM-02 record?"
0805 LET YEAR=0,YEAR=FNYY_YEAR(BEGYEAR$,ERR=0810)
0810 IF P4$(6,1)="Y" THEN GOTO 0850
0820 IF YEAR=P[2] THEN LET RECORD$="4"
0830 IF YEAR=P[2]-1 THEN LET RECORD$="0"
0840 IF YEAR=P[2]-2 THEN LET RECORD$="2"
0845 GOTO 0900
0850 IF YEAR=P[2] THEN LET RECORD$="0"
0860 IF YEAR=P[2]-1 THEN LET RECORD$="2"
0875 IF YEAR=P[2]+1 THEN LET RECORD$="4"
0910 DIM Y1$(P[4],"0")
0920 LET Z1$=BREAK$
0930 IF LEN(Z1$)>=P[4] THEN LET Y1$=Z1$(1,P[4]),Z1$=Z1$(P[4]+1) ELSE LET Z1$=""
0990 READ (GLM01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next GLM-01 record"
1010 READ (GLM01_DEV,END=1900)IOL=GLM01A
1020 IF POS(N0$=A0$)<>1 THEN GOTO 1900
1100 REM 
1110 IF Z1$="" THEN GOTO 1300
1120 IF FNP$(Z1$(1,P[4]))="" THEN GOTO 1300
1130 IF Z1$(1,P[4])>A0$(3,P[4]) THEN GOTO 1300
1140 LET Y1$=Z1$(1,P[4])
1150 IF LEN(Z1$)>=P[4] THEN LET Z1$=Z1$(P[4]+1) ELSE LET Z1$=""
1190 GOTO 1100
1300 REM " --- Write sort record"
1310 LET X=POS(A1$(36,1)="IEALC")
1320 IF X=0 THEN LET X=6
1330 LET X$=STR(X)
1340 WRITE (GLW02_DEV,KEY=N0$+X$+Y1$+A0$(3))
1350 PRINT @(COLUMN,11),FNF$(A0$(3,P[3]),M0$)
1390 GOTO 1000
1900 REM " --- Position sort file"
1910 PRINT @(0,10),'CL',@(0,11),'CL',@(0,12),'CL',
1920 CALL "SYC.NB","Printing",20,COLUMN
1990 READ (GLW02_DEV,KEY=N0$,DOM=2000)
2000 REM " --- Read next sort record"
2010 LET K$=KEY(GLW02_DEV,END=4000)
2020 READ (GLW02_DEV)
2100 REM " --- Additional reads"
2110 DIM A[MP]
2120 LET A0$=N0$+K$(14),X0$(1)=A0$+RECORD$
2130 FIND (GLM01_DEV,KEY=A0$,DOM=2000)IOL=GLM01A
2140 FIND (GLM02_DEV,KEY=X0$,DOM=2200)IOL=GLM02A
2200 REM " --- Account Break?"
2210 IF K$(3,P[4]+1)=T0$ THEN GOTO 3100
2220 GOSUB 6200
2300 REM " --- Chart of Accounts Break?"
2310 IF K$(3,1)=TYPE$ THEN GOTO 3000
2400 REM 
2410 IF TYPE$<>"1" THEN GOTO 2500
2420 LET TOTAL$="Total Income",MTD=S[0],YTD=S[1]
2430 IF BS$="N" THEN GOSUB 6000
2500 REM 
2510 IF TYPE$<>"2" THEN GOTO 2600
2520 LET TOTAL$="Total Expense",MTD=S[2],YTD=S[3]
2530 IF BS$="N" THEN GOSUB 6000
2540 LET TOTAL$="Profit/Loss",MTD=S[0]-S[2],YTD=S[1]-S[3]
2550 GOSUB 6000
2590 IF BS$="N" THEN GOTO 4000
2600 REM 
2610 IF TYPE$<>"3" THEN GOTO 2700
2620 LET TOTAL$="Total Assets",MTD=S[4],YTD=S[5]
2630 GOSUB 6000
2700 REM 
2710 IF TYPE$<>"5" THEN GOTO 2900
2720 LET TOTAL$="Total Capital",MTD=S[6],YTD=S[7]
2730 GOSUB 6000
2900 REM 
2910 LET TYPE$=K$(3,1)
3000 REM " --- Print subheading"
3010 IF BS$="Y" THEN IF POS(TYPE$="12")>0 THEN GOTO 3100
3020 GOSUB 5200
3100 REM " --- Calculate current balance"
3110 LET BALANCE=0
3120 FOR X=0 TO PERIOD
3130 IF POS(A1$(36,1)="LCI")>0 THEN LET A[X]=-A[X]
3140 LET BALANCE=BALANCE+A[X]
3150 NEXT X
3160 LET AMOUNT=A[PERIOD]
3300 REM " --- Print line"
3310 IF BS$="Y" THEN IF POS(TYPE$="12")>0 THEN GOTO 3500
3320 DIM X$(WIDTH)
3330 LET MTD$="",YTD$=STR(BALANCE:M1$),X$(3)=A1$(1,35)
3340 IF BS$="N" THEN LET MTD$=STR(AMOUNT:M1$)
3350 IF INCLUDE$="Y" THEN LET X$(3)=FNF$(A0$(3,P[3]),M0$)+" "+A1$(1,35)
3360 IF L+4>L9 THEN GOSUB 5200
3370 PRINT (7)X$,@(O[0]),MTD$,@(O[1]),YTD$
3380 LET L=L+1
3500 REM " --- Accumulate totals"
3510 LET T[0]=T[0]+AMOUNT,T[1]=T[1]+BALANCE
3520 IF K$(3,1)="1" THEN LET S[0]=S[0]+AMOUNT,S[1]=S[1]+BALANCE
3530 IF K$(3,1)="2" THEN LET S[2]=S[2]+AMOUNT,S[3]=S[3]+BALANCE
3540 IF K$(3,1)="3" THEN LET S[4]=S[4]+AMOUNT,S[5]=S[5]+BALANCE
3550 IF K$(3,1)="4" THEN LET S[6]=S[6]+AMOUNT,S[7]=S[7]+BALANCE
3560 IF K$(3,1)="5" THEN LET S[8]=S[8]+AMOUNT,S[9]=S[9]+BALANCE
3900 REM " --- Loop back for next sort record"
3990 GOTO 2000
4000 REM " --- Totals"
4005 IF BS$="N" THEN GOTO 9900
4010 IF L>L9 THEN GOSUB 5000
4015 LET T9$="END"
4020 GOSUB 6200
4030 LET TOTAL$="Current Period Retained Earnings"
4040 LET MTD=S[0]-S[2],YTD=S[1]-S[3]
4050 GOSUB 6000
4060 LET S[8]=S[8]+S[0]-S[2],S[9]=S[9]+S[1]-S[3]
4070 LET TOTAL$="Total Liabilities & Equity"
4075 LET MTD=S[6]+S[8],YTD=S[7]+S[9]
4080 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 IF BS$="Y" THEN GOTO 5090
5050 PRINT (7)""
5060 PRINT (7)@(MTDHDR),MTDHDR$,@(YTDHDR),YTDHDR$
5070 LET L=L+2
5090 RETURN 
5200 REM " --- Subheading"
5210 IF BS$="Y" THEN IF POS(TYPE$="12")>0 THEN GOTO 5290
5220 IF L+4>L9 THEN GOSUB 5000
5230 PRINT @(COLUMN,11),TITLE$,
5240 PRINT (7)""
5250 PRINT (7)TITLE$
5260 LET L=L+2
5290 RETURN 
6000 REM " --- Chart of Accounts Break"
6010 IF BS$="Y" THEN IF POS(TYPE$="12")>0 THEN GOTO 6090
6020 LET MTD$="",YTD$=STR(YTD:M1$),L=L+2
6030 IF BS$="N" THEN LET MTD$=STR(MTD:M1$)
6040 PRINT (7)@(O[0]),J1$,@(O[1]),J2$
6050 PRINT (7)TOTAL$,@(O[0]),MTD$,@(O[1]),YTD$
6090 RETURN 
6200 REM " --- Subtotal"
6210 IF T0$="" THEN GOTO 6300
6220 IF FNP$(K$(4,P[4]))="" THEN GOTO 6300
6230 LET MTD=T[0],YTD=T[1],TOTAL$=""
6240 GOSUB 6000
6300 IF T9$="END" THEN RETURN 
6310 DIM T[3]
6320 LET T0$=K$(3,P[4]+1)
6330 LET W=POS(T0$(2,P[4])=BREAK$,P[4])
6340 IF W<>0 THEN LET TITLE$=DESC$(20*INT(W/P[4])+1,20)
6390 RETURN 
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

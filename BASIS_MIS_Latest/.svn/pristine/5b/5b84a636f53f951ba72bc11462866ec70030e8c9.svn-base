0010 REM "IVX - Rebuild Inventory On-Order Quantities"
0020 REM "Program IVX.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "14Nov96 Caj: Was increasing Max Qty instead of Qty On Hand (3230)
0032 REM "12Nov99 JAL: Position WOE-01 for current firm (2290)"
0040 REM 
0070 BEGIN
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=5
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-02"
0150 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1]
0200 REM " --- IOLIST's"
0210 IVM02A: IOLIST A0$(1),A1$(1),A2$,A[ALL]
0220 POE12A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1),B4$(1),B5$,B6$,B[ALL]
0230 WOE01A: IOLIST C0$,C1$,C[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0400 REM " --- Parameters"
0405 DIM P[1],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="IV"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1))
0450 CALL "SYC.VA","PO",INFO$[ALL]
0460 LET PO$=INFO$[20]
0470 CALL "SYC.VA","WO",INFO$[ALL]
0475 LET SF$=INFO$[20]
0480 IF PO$="Y" THEN GOTO 0500
0485 IF SF$="Y" THEN GOTO 0500
0490 GOTO 4900
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM A0$(24),A1$(64),A[20],B0$(18),B1$(48),B2$(32),B3$(22),B4$(40),B[12]
0520 DIM C0$(75),C1$(141),C[11]
0600 REM " --- Additional file opens"
0610 IF SF$="Y" THEN LET FILES$[3]="WOE-01"
0620 IF PO$="Y" THEN LET FILES$[4]="POE-12",FILES$[5]="POE-42"
0630 CALL "SYC.DA",1,2,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0640 IF STATUS THEN GOTO 9900
0650 LET IVM02_DEV=CHANNELS[2],WOE01_DEV=CHANNELS[3]
0660 LET POE12_DEV=CHANNELS[4],POE42_DEV=CHANNELS[5]
0700 REM " --- Background"
0710 PRINT 'SB',@(33,3),"Phase:",'SF',
0720 CALL "SYC.WC",1,0,80,0,0,4,0
0800 REM " --- Options
0810 LET V4$="Do You Wish To Rebuild Inventory On-Order Quantities"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM " --- Position warehouse file"
0910 LET X$="Clearing Current On-Order Quantities",X=3+P[0]
0920 GOSUB 6000
0990 READ (IVM02_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Clear IVM-02 On Hand"
1010 LET K$=KEY(IVM02_DEV,END=1900)
1015 IF POS(N0$=K$)<>1 THEN GOTO 1900
1020 PRINT @(COLUMN,11),K$(3,2)," ",K$(5,P[0]),
1030 EXTRACT (IVM02_DEV,KEY=K$)IOL=IVM02A
1040 LET A[1]=0
1050 WRITE (IVM02_DEV,KEY=K$)IOL=IVM02A
1090 GOTO 1000
1900 REM " --- Purchase Orders"
1910 IF PO$="N" THEN GOTO 2900
1920 LET X$="Purchase Order On-Order Quantities",X=3+P[0]
1930 GOSUB 6000
1990 READ (POE42_DEV,KEY=N0$,DOM=2000)
2000 REM " --- Read next PO sort record"
2010 LET K$=KEY(POE42_DEV,END=2900)
2015 IF POS(N0$=K$)<>1 THEN GOTO 2900
2020 READ (POE42_DEV)
2030 PRINT @(COLUMN,11),K$(3,P[0]),
2040 LET B0$(1)=K$(1,2)+K$(33,6)+K$(23,10)
2050 FIND (POE12_DEV,KEY=B0$,DOM=2000)IOL=POE12A
2055 LET A0$(1)=N0$+B3$
2060 EXTRACT (IVM02_DEV,KEY=A0$,DOM=2000)IOL=IVM02A
2070 LET A[1]=A[1]+((B[3]-B[7])*B[0])
2080 WRITE (IVM02_DEV,KEY=A0$)IOL=IVM02A
2090 GOTO 2000
2900 REM " --- Work Orders"
2910 IF SF$="N" THEN GOTO 4000
2920 LET X$="Work Order On-Order Quantities",X=7
2930 GOSUB 6000
2990 READ (WOE01_DEV,KEY=N0$,DOM=3000)
3000 REM " --- Read next work order"
3010 LET K$=KEY(WOE01_DEV,END=4000)
3020 IF POS(N0$=K$)<>1 THEN GOTO 4000
3030 PRINT @(COLUMN,11),K$(5,7),
3040 READ (WOE01_DEV)IOL=WOE01A
3100 REM " --- Is it open?"
3110 IF C0$(14,1)<>"I" THEN GOTO 3000
3120 IF POS(C0$(15,1)="CPQ")>0 THEN GOTO 3000
3130 IF C[0]<=C[2] THEN GOTO 3000
3200 REM " --- Update warehouse"
3210 LET A0$(1)=C0$(1,2)+C0$(54)
3220 EXTRACT (IVM02_DEV,KEY=A0$,DOM=3000)IOL=IVM02A
3230 LET A[1]=A[1]+(C[0]-C[2])
3240 WRITE (IVM02_DEV,KEY=A0$)IOL=IVM02A
3290 GOTO 3000
4000 REM " --- All done"
4090 GOTO 9900
4900 REM " --- PO or WO must be installed"
4910 DIM MESSAGE$[1]
4920 LET MESSAGE$[0]="Inventory On-Order Quantities Can Only Exist If The Purchase Order And/Or"
4930 LET MESSAGE$[1]="Shop Floor Control System(s) Are Installed (<Enter>=Continue"
4940 CALL "SYC.XA",2,MESSAGE$[ALL],1,-1,-1,V$,V3
4990 GOTO 9900
6000 REM " --- Update screen"
6010 PRINT @(40,3),X$,'CL',@(0,5),'CE',
6020 CALL "SYC.NB","Updating",X,COLUMN
6090 RETURN
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
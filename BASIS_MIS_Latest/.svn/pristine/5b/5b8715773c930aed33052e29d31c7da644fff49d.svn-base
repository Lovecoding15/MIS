0010 REM "DDR - Data Dictionary Listing (Report)"
0020 REM "Program DDR.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLists
0210 DDM01A: IOLIST A0$(1),A1$(1),A[ALL]
0500 REM " --- Initializations
0510 DIM A0$(12),A1$(105),A[2],MILESTONE$(39)
0540 LET HEADINGS=5,PAGE=0,WIDTH=132,ELEMENTS=0,L9=59,L=L9+1,WHEN$=F0$(7,3)
0550 DIM HEADINGS$[HEADINGS],TYPES$[9],DASHES$(WIDTH,"-")
0560 LET TYPES$[1]="Str",TYPES$[2]="Zero",TYPES$[3]="Y/N"
0570 LET TYPES$[4]="Hyph",TYPES$[5]="Mask",TYPES$[6]="Time"
0580 LET TYPES$[7]="Date",TYPES$[8]="Num",TYPES$[9]="Asc"
0590 LET VERSION$="6.0.0",M$="##0"
0595 LET VERSION$=STBL("!VERSION",ERR=0600)
0600 LET H1$="ADD+ON Software - Version "+VERSION$,H2$=N4$
0610 LET H4$="All Entries From ",X$="All Revisions"
0615 IF REPORT$="N" THEN LET H4$="Not-Used Entries From "
0620 IF REVISION$<>"" THEN LET X$="Revised On or After "+FNB6$(REVISION$)
0630 IF BEGNAME$<>"" THEN LET H4$=H4$+BEGNAME$ ELSE LET H4$=H4$+"First Data Name"
0640 LET H4$=H4$+" Through "
0650 IF ENDNAME$<>"" THEN LET H4$=H4$+ENDNAME$ ELSE LET H4$=H4$+"Last Data Name"
0660 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$,HEADINGS$[3]=H4$
0670 LET HEADINGS$[4]="Includes Data Types "+DATATYPE$,HEADINGS$[5]=X$
0700 REM " --- Background
0710 CALL "SYC.WC",1,0,80,0,0,14,0
0720 PRINT 'SB',@(31,13),"Printing",'SF',
0900 REM " --- Position file
0910 LET ENDNAME$=ENDNAME$+FILL(12-LEN(ENDNAME$)),K$=BEGNAME$
0980 READ (DDM01_DEV,KEY=K$,DOM=1000)
0990 GOTO 1020
1000 REM " --- Read next data element"
1010 LET K$=KEY(DDM01_DEV,END=4000)
1020 IF FNP$(ENDNAME$)<>"" THEN IF K$(1,12)>ENDNAME$ THEN GOTO 4000
1030 READ (DDM01_DEV,KEY=K$)IOL=DDM01A
1050 REM " --- Not-Used?
1060 IF REPORT$<>"N" THEN GOTO 1100
1070 READ (DDM06_DEV,KEY=A0$,DOM=1080)
1080 LET K$=KEY(DDM06_DEV,END=3900)
1090 IF POS(A0$=K$)=1 THEN GOTO 3900
1095 IF DELETE THEN REMOVE (DDM01_DEV,KEY=A0$)
1100 REM " --- Range/Edit Tests
1110 IF DATATYPE$<>"" THEN IF POS(A1$(37,1)=DATATYPE$)=0 THEN GOTO 3900
1120 IF REVISION$<>"" THEN IF A1$(31,6)<REVISION$ THEN GOTO 3900
1150 LET MILESTONE$(1)=A0$+" "+A1$(1,30)
1160 PRINT @(40,13),MILESTONE$
1500 REM " --- Print detail line
1510 IF L+1>L9 THEN GOSUB 5000
1520 LET X$=A1$(37,1),X=POS(X$="SZYHMTDNA")
1530 IF X>0 THEN LET X$=TYPES$[X]
1540 LET MASK$=A1$(41,15)
1550 CALL "DDC.MA",N0$,A1$(38,2),A1$(40,1),MASK$,SYS01_DEV
1560 PRINT (7)A0$,@(13),A1$(1,30),@(44),X$,@(49),A1$(38,2)," ",A1$(40,1)," ",MASK$,@(70),A[0]:M$,@(75),A[1]:M$,@(79),A1$(68,12),@(92),A1$(56,12),@(105),A1$(80,1)," ",A1$(81,6),@(114),A1$(87,1)," ",A1$(88,6),@(123),FNB6$(A1$(31,6))
1570 LET L=L+1,ELEMENTS=ELEMENTS+1
3900 REM " --- Loop back for next record
3990 GOTO 1000
4000 REM " --- All done
4010 IF L+2>L9 THEN GOSUB 5000
4020 LET X$=STR(ELEMENTS)
4030 IF ELEMENTS=0 THEN LET X$="No"
4040 PRINT (7)""
4050 PRINT (7)X$," Data Dictionary Entries Listed"
4090 GOTO 9900
5000 REM " --- Print report heading
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS THEN GOTO 9900
5040 PRINT (7)@(105),"  READ",@(114),"  READ"
5050 PRINT (7)@(44),"Data",@(49),"--------Mask--------",@(70),"Phy",@(75),"Dis",@(79),"Compression",@(92),"Expansion",@(105),"  WRITE",@(114),"  ONLY"
5060 PRINT (7)"Data Name",@(13),"Description",@(44),"Type",@(49),"Sy T Appearance",@(70),"Len",@(75),"Len",@(79),"Code",@(92),"Code",@(105),"SL Pswrd",@(114),"SL Pswrd",@(123),"Revised"
5070 PRINT (7)DASHES$
5080 LET L=L+4
5090 RETURN 
8000 REM " --- Functions
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM 
9950 RUN "SYS.AA"
9999 END

0010 REM "IVR - Inventory Valuation Report (Input Overlay)"
0020 REM "Program IVR.NA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=15
0110 DIM FILES$[FILES],FILEOPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0130 LET FILES$[4]="IVM-03",FILES$[5]="IVM-10",FILES$[6]="IVM-12"
0140 LET FILES$[7]="IVM-14",FILES$[8]="IVM-15",FILES$[9]="IVM-03"
0150 CALL "SYC.DA",1,1,8,FILES$[ALL],FILEOPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0180 LET IVM03_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],IVM12_DEV=CHANNELS[6]
0190 LET IVM14_DEV=CHANNELS[7],IVM15_DEV=CHANNELS[8],IVM03_DEV2=CHANNELS[9]
0200 REM " --- IOLIST's"
0230 IVM10C: IOLIST X0$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0280 GLS01A: IOLIST G0$,G1$,G2$,G3$,G4$,M0$
0290 ARS01A: IOLIST P0$,P1$,P2$,P3$
0400 REM " --- Parameters"
0405 DIM P[5],G[5],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="IV",LS$="N",LF$="N"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0425 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1)),P[2]=NUM(I2$(7,2))
0430 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0440 IF I3$(3,1)<>"N" THEN LET LF$="Y"
0445 IF I3$(17,1)<>"N" THEN LET LS$="Y"
0450 CALL "SYC.VA",N2$,INFO$[ALL]
0455 LET GL$=INFO$[9],AR$="N",DI$="N",COST$=I3$(8,1)
0460 IF GL$="N" THEN GOTO 0500
0465 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0470 LET G[4]=NUM(G2$(9,2))
0475 CALL "SYC.VA","AR",INFO$[ALL]
0480 LET AR$=INFO$[20]
0485 IF AR$="N" THEN GOTO 0500
0490 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0495 LET DI$=P3$(11,1)
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM FILES[15],PARAMS$[3],OPTIONS$[7],B[12],HEADINGS$[7],X0$(32)
0540 LET FILES[1]=IVM01_DEV,FILES[10]=IVM10_DEV,FILES[14]=IVM14_DEV
0550 LET FILES[15]=IVM15_DEV
0560 LET PARAMS$[0]=N0$,PARAMS$[1]=I1$,PARAMS$[2]=I2$,PARAMS$[3]=I3$
0570 LET LEVEL$="D",INCLUDE$="N",PAGEBREAK$="N",WHICHQTY$="B"
0580 IF LF$="Y" THEN LET WHICHQTY$="O"
0600 REM " --- Additional File Opens"
0610 IF LS$="Y" THEN LET FILES$[10]="IVM-07",FILES$[11]="IVM-17"
0620 IF LF$="Y" THEN LET FILES$[12]="IVM-04"
0630 IF GL$="Y" THEN LET FILES$[13]="GLM-01",FILES$[14]="GLW-04",FILEOPTIONS$[14]="C"
0640 IF DI$="Y" THEN LET FILES$[15]="ARM-10"
0650 CALL "SYC.DA",1,10,FILES,FILES$[ALL],FILEOPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0660 IF STATUS>0 THEN GOTO 9900
0670 LET IVM07_DEV=CHANNELS[10],IVM17_DEV=CHANNELS[11],IVM04_DEV=CHANNELS[12]
0680 LET GLM01_DEV=CHANNELS[13],GLW04_DEV=CHANNELS[14],ARM10_DEV=CHANNELS[15]
0700 REM " --- Background"
0710 PRINT 'SB',@(24,3),"Costing Method:",@(26,4),"Detail Level:",'SF',
0730 CALL "IVC.IB","B","D",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0740 PRINT 'SB',@(12,12),"Include Zero Balance Items:",@(12,13),"Page Break By Product Type:",@(5,14),"Use Ending Balance Or Qty On Hand:",'SF',
0900 REM " --- Display default values"
0950 GOSUB 5000
1000 REM " --- Costing method"
1010 LET V4$="Enter The Costing Method To Use (A=Average/S=Standard/R=Replacement/L=Landed)"
1020 LET V0$="S",V1$="CE",V2$=COST$,V3$="ASRL",V0=1,V1=40,V2=3,I0=0
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,3900,1000,3900
1050 LET COST$=V$
1090 GOTO 3900
1200 REM " --- Detail Level"
1210 LET V4$="P=Totals By Product Type     S=Summary By Item     D=Detail By Item"
1220 LET V0$="S",V1$="CE",V2$=LEVEL$,V3$="PSD",V0=1,V1=40,V2=4
1230 GOSUB 7000
1240 ON V3 GOTO 1250,1200,3900,1200,3900
1250 LET LEVEL$=V$
1260 IF LEVEL$="P" THEN LET INCLUDE$="N",PAGEBREAK$="N",WHICHQTY$="B"
1290 GOTO 3900
1400 REM " --- Process standard input"
1410 CALL "IVC.IB","I","D",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
1420 ON V3 GOTO 1430,1400,3900,1400,3900
1490 GOTO 3900
1600 REM " --- Include zero balance items?"
1610 LET V0$="Y",V1$="C",V2$=INCLUDE$,V3$="",V0=1,V1=40,V2=12,V$="N"
1615 IF LEVEL$="P" THEN GOTO 1650
1620 LET V4$="Include Items With Zero Balances On This Report (Y/N)?"
1630 GOSUB 7000
1640 ON V3 GOTO 1650,1600,3900,1600,3900
1650 LET INCLUDE$=V$
1660 PRINT @(V1,V2),INCLUDE$,
1690 GOTO 3900
1800 REM " --- Page break by product type?"
1810 LET V0$="Y",V1$="C",V2$=PAGEBREAK$,V3$="",V0=1,V1=40,V2=13,V$="N"
1815 IF LEVEL$="P" THEN GOTO 1850
1820 LET V4$="Page Break When Product Type Changes (Y/N)?"
1830 GOSUB 7000
1840 ON V3 GOTO 1850,1800,3900,1800,3900
1850 LET PAGEBREAK$=V$
1860 PRINT @(V1,V2),PAGEBREAK$,'CL',
1890 GOTO 3900
2000 REM " --- Ending Balance or Qty On Hand?
2005 IF LEVEL$="P" OR LF$="Y" THEN GOTO 3900
2010 LET V0$="S",V1$="C",V2$=WHICHQTY$,V3$="BO",V0=1,V1=40,V2=14
2020 LET V4$="Calcuate Total Value Using Ending Balance Or Quantity On Hand (B/O)?"
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 LET WHICHQTY$=V$
2060 PRINT @(V1,V2),WHICHQTY$,'CL',
2090 GOTO 3900
3900 REM " --- Next Field"
3902 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4030 ON POS(V$="YN") GOTO 4000,4050,1000
4100 REM " --- Set option variables"
4110 LET BEGWHSE$=OPTIONS$[1],ENDWHSE$=OPTIONS$[2],BEGPROD$=OPTIONS$[3]
4120 LET ENDPROD$=OPTIONS$[4],BEGITEM$=OPTIONS$[5],ENDITEM$=OPTIONS$[6]
4130 LET TYPE$=OPTIONS$[7],HEADINGS$[0]=F4$,H3$="All Item Types",H1=4
4140 LET HEADINGS$[1]=F5$+" For Period "+I4$(1,2)+" "+FNYY21_YY$(I4$(3,2)),H2=5
4150 IF TYPE$<>"" THEN LET H3$="Item Type "+FNP$(TYPE$)
4160 IF INCLUDE$="Y" THEN LET H4$="Including Zero Balances" ELSE LET H4$="No Zero Balances"
4170 IF WHICHQTY$="B" THEN LET H5$="Using Ending Balances" ELSE LET H5$="Using Quantity On Hand"
4180 LET HEADINGS$[2]=H4$+" "+H5$
4190 ON POS(LEVEL$="PSD") GOTO 9900,4200,4300,4400
4200 REM " --- Product totals"
4210 LET FORMAT=1,HEADINGS=7,H1=5,H2=6
4220 LET HEADINGS$[3]="Product Type Totals For "+H3$
4230 LET WHSE1$="First",WHSE2$="Last",X0$(6,24)="(Not On File)"
4235 IF BEGWHSE$="" THEN GOTO 4250
4240 FIND (IVM10_DEV,KEY=N0$+"C"+BEGWHSE$,DOM=4245)IOL=IVM10C
4245 LET WHSE1$=X0$(4,2)+" "+FNP$(X0$(6,24)),X0$(6,24)="(Not On File)"
4250 IF ENDWHSE$="" THEN GOTO 4270
4255 FIND (IVM10_DEV,KEY=N0$+"C"+ENDWHSE$,DOM=4260)IOL=IVM10C
4260 LET WHSE2$=X0$(4,2)+" "+FNP$(X0$(6,24))
4270 LET WHSE1$="From Warehouse: "+WHSE1$,WHSE2$="  To Warehouse: "+WHSE2$
4280 LET WHSE=MAX(LEN(WHSE1$),LEN(WHSE2$)),H1=5,H2=6
4290 GOTO 4900
4300 REM " --- Summary by item"
4310 LET FORMAT=2,HEADINGS=6,HEADINGS$[1]="Summary "+HEADINGS$[1]
4390 GOTO 4900
4400 REM " --- Detail report"
4410 LET FORMAT=3,HEADINGS=6,HEADINGS$[1]="Detail "+HEADINGS$[1]
4420 IF LS$="Y" THEN LET FORMAT=6
4430 IF LF$="Y" THEN LET FORMAT=5
4440 IF LS$="Y" AND LF$="Y" THEN LET FORMAT=4
4900 REM " --- Run report overlay"
4990 RUN "IVR.NB"
5000 REM " --- Display"
5010 PRINT @(40,3),COST$,@(40,4),LEVEL$,
5020 CALL "IVC.IB","D","D",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
5030 PRINT @(40,12),INCLUDE$,@(40,13),PAGEBREAK$,@(40,14),WHICHQTY$
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "POR - Purchase Order Cash Requirements Report
0020 REM "Program POR.MA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Sep96 JAL: Period/Days description should be consistent (1180)"
0032 REM "19Sep94 Caj: When run by 'P'eriod, date headers were wrong (485)
0033 REM "20Nov96 Caj: Allow for entry of single character whses (ie '4 ')(1475,1480,1490,1500,5070)
0034 REM "03Jan97 Caj: Allow <F1> for All Warehouses (1410,1430,1435,1440,1445,1465,5060,5070)
0040 REM 
0070 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="APM-01",FILES$[2]="APM-10",FILES$[3]="IVM-10"
0140 LET FILES$[4]="POE-02",FILES$[5]="POE-12",FILES$[6]="POM-02"
0145 LET FILES$[7]="SYS-01"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET APM01_DEV=CHANNELS[1],APM10_DEV=CHANNELS[2],IVM10_DEV=CHANNELS[3]
0190 LET POE02_DEV=CHANNELS[4],POE12_DEV=CHANNELS[5],POM02_DEV=CHANNELS[6]
0195 LET SYS01_DEV=CHANNELS[7]
0200 REM " --- IOLists
0280 IVM10C: IOLIST X1$(1)
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 APS01A: IOLIST U0$,VENDIMASK$,U2$,U3$,U4$,VENDOMASK$
0330 GLS01A: IOLIST *,G1$,G2$,G3$
0340 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0350 POS01A: IOLIST P0$,P1$,P2$,P3$,P4$
0400 REM " --- Parameters
0405 DIM INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO",WHEN$=F0$(7,3)
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=POS01A
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 LET VENDLEN=NUM(U2$(1,2))
0450 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0460 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0465 LET IVPRECISION=NUM(I2$(5,1)),ITEMLEN=NUM(I2$(1,2))
0470 LET TOTALPERIODS=NUM(G2$(1,2))
0480 FOR X=1 TO 13
0485 LET PERENDDATE$=PERENDDATE$+FNA$(G3$(2*X-1,1),G3$)+FNA$(G3$(2*X,1),G3$)
0490 NEXT X
0500 REM " --- Init Data
0510 PRECISION IVPRECISION
0520 DIM WHSENAME$(24)
0530 LET WHSE$=I3$(6,2),PERORDAYS$="P",PORD_DESC$="GL Period"
0540 LET DATEFROM$=F0$(18,6)
0550 LET SUMMORDET$="D",SORD_DESC$="Detail",NUM_DAYS=30
0560 CALL "SYC.RM",M1$,M1,9,12
0700 REM " --- Background
0710 FOR X=4 TO 12 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(24,3),"Beginning Date:",@(18,5),"Period or Days (P/D):",@(24,7),"Number of Days:",@(29,9),"Warehouse:",@(15,11),"Summary or Detail (S/D):"
0760 GOSUB REFRESH
1000 REM " --- Beginning Date
1010 LET V0$="D",V1$="6N",V2$=DATEFROM$,V3$="",V4$="Starting Date For First Time Period",V0=6,V1=40,V2=3
1020 GOSUB 7000
1030 ON V3 GOTO 1050,1040,1070,1000,4000
1040 LET V$=""
1050 IF V$="" THEN GOTO 1000
1060 LET DATEFROM$=V$
1070 GOTO 3900
1100 REM " --- Use GL period or user-defined # of days to calc?
1110 LET V0$="S",V1$="C",V2$=PERORDAYS$,V3$="PD",V4$="P=G/L Period  D=Number Of Days",V0=1,V1=40,V2=5
1120 GOSUB 7000
1130 ON V3 GOTO 1140,1140,1280,1140,4000
1140 IF V$="" THEN GOTO 1100
1150 LET PERORDAYS$=V$
1160 ON POS(PERORDAYS$="PD") GOTO 1100,1180,1200
1180 LET PORD_DESC$="GL Period"
1190 GOTO 1280
1200 LET PORD_DESC$="Custom Number of Days"
1280 PRINT @(V1+2,V2),'CL',PORD_DESC$
1290 GOTO 3900
1300 REM " --- Number of days in user-defined period
1310 IF PERORDAYS$="P" THEN GOTO 3900
1320 LET V0$="N",V1$="R",V2$=STR(NUM_DAYS),V3$="###",V4$="Number Of Days In Each Time Period",V0=3,V1=40,V2=7
1330 GOSUB 7000
1340 IF V<1 THEN GOTO 1300
1350 LET NUM_DAYS=V
1390 GOTO 3900
1400 REM " --- Warehouse
1410 LET V0$="S",V1$="",V2$=WHSE$,V3$="",V4$="Enter A Valid Warehouse ID  (<F1>=All/<F3>=Lookup)",V0=2,V1=40,V2=9
1420 GOSUB 7000
1430 ON V3 GOTO 1465,1435,1590,1450,4000
1435 LET WHSE$=""
1440 PRINT @(V1,V2),"All Warehouses",'CL',
1445 GOTO 1590
1450 CALL "IVC.LK",3,V1,V2,V$
1460 IF V$="" THEN GOTO 1400
1465 IF V$="" THEN GOTO 1435
1470 DIM X1$(160)
1475 LET X1$(1)=N0$+"C"+V$
1480 FIND (IVM10_DEV,KEY=X1$(1,5),DOM=1400)IOL=IVM10C
1490 LET WHSE$=X1$(4,2),WHSENAME$(1)=X1$(6,24)
1500 PRINT @(V1,V2),WHSE$," ",WHSENAME$(1,24),'CL',
1590 GOTO 3900
1600 REM " --- Summary or Detail
1610 LET V0$="S",V1$="C",V2$=SUMMORDET$,V3$="SD",V4$="S=Summary  D=Detail",V0=1,V1=40,V2=11
1620 GOSUB 7000
1630 ON V3 GOTO 1640,1640,1780,1640,4000
1640 IF V$="" THEN GOTO 1600
1650 LET SUMMORDET$=V$
1660 ON POS(SUMMORDET$="SD") GOTO 1600,1680,1700
1680 LET SORD_DESC$="Summary"
1690 GOTO 1780
1700 LET SORD_DESC$="Detail"
1780 PRINT @(V1+2,V2),'CL',SORD_DESC$
1790 GOTO 3900
3900 REM " --- Next Field
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,1300,1400,1600,4000
4000 REM " --- Correct
4010 GOSUB REFRESH
4020 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run print overlay
4110 IF DATEFROM$="" THEN LET DATEFROM$=FILL(6)
4190 RUN "POR.MB"
5000 REM " --- Refresh screen data
5005 REFRESH: 
5010 PRINT 'CF',
5020 PRINT @(40,3),FNB6$(DATEFROM$),'CL',
5030 PRINT @(40,5),PERORDAYS$," ",PORD_DESC$,'CL',
5040 IF PERORDAYS$="D" THEN PRINT @(40,7),NUM_DAYS,'CL',
5060 IF WHSE$="" THEN PRINT @(40,9),"All Warehouses",'CL',
5070 IF WHSE$<>"" THEN PRINT @(40,9),WHSE$," ",WHSENAME$(1,24),'CL',
5100 PRINT @(40,11),SUMMORDET$," ",SORD_DESC$,'CL',
5190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

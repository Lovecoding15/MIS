0010 REM "IVE - Transaction Entry (Header Overlay)"
0020 REM "Program IVE.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "31Mar97 WGH: Initialize !GLCONTROL based on current program (655)"
0032 REM "02Jun97 JWK: Allow only one user at a time per reference no (60,950,990,1210,4130)
0033 REM "18Feb98 WGH: Don't allow changing existing Issue and Commit type Transaction Codes (1205,1220,5230,5250,5295)"
0034 REM "11Nov99 WGH: Verify transaction date (2060-2070)
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0060 ON O0 GOTO 80,990,4200
0080 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=10
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="IVE-01",FILES$[2]="IVE-11",FILES$[3]="IVM-01"
0130 LET FILES$[4]="IVM-02",FILES$[5]="IVM-10",FILES$[6]="IVM-14"
0135 LET FILES$[7]="SYS-01",FILES$[8]="IVM-07",FILES$[9]="IVM-08"
0140 LET FILES$[10]="GLM-01"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET IVE01_DEV=CHANNELS[1],IVE11_DEV=CHANNELS[2],IVM01_DEV=CHANNELS[3]
0185 LET IVM02_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],IVM14_DEV=CHANNELS[6]
0190 LET SYS01_DEV=CHANNELS[7]
0200 REM " --- IOLists
0210 IVE01A: IOLIST A0$(1)
0215 IVE11A: IOLIST W0$(1),W1$(1),W2$(1),W[ALL]
0240 IVM10B: IOLIST X0$(1),X1$(1)
0250 IVM02A: IOLIST L0$(1),L1$,L2$,L[ALL]
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0290 IVM07A: IOLIST R0$,R1$,R[ALL]
0400 REM " --- Parameters"
0405 DIM I[5],G[1]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N4$=F5$,N9$=N0$
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0425 FIND (SYS01_DEV,KEY=N9$+"GL00",DOM=435)IOL=GLS01A
0430 LET G[0]=NUM(G2$(7,2)),G[1]=NUM(G2$(9,2)),LF$="N",LS$="N"
0435 LET I[0]=NUM(P2$(1,2)),I[1]=NUM(P2$(7,2)),I[2]=NUM(P2$(5,1))
0440 LET I[3]=NUM(P2$(9,2)),I[4]=NUM(P2$(11,2)),I[5]=NUM(P2$(13,2))
0445 LET P9$=P3$(17,1),P8$=""
0450 IF POS(P9$="LS")>0 THEN LET LS$="Y",P8$="Lot/Serial #"
0460 IF POS(P3$(3,1)="LF")>0 THEN LET LF$="Y"
0500 REM " --- Init Data"
0505 PRECISION I[2]
0510 DIM IV_FILES[44],IV_INFO$[3],IV_INFO[0],IV_PARAMS$[4]
0515 DIM IV_PARAMS[3],IV_REFS$[11],IV_REFS[5]
0520 LET IV_FILES[0]=SYS01_DEV,IV_FILES[1]=IVM01_DEV,IV_FILES[2]=IVM02_DEV
0525 LET IV_FILES[10]=IVM10_DEV,IV_FILES[14]=IVM14_DEV
0530 LET IV_PARAMS$[0]=F0$(7,3),IV_PARAMS$[1]=F0$(4,3),IV_PARAMS$[2]=P2$
0535 LET IV_PARAMS$[3]=P3$,IV_PARAMS$[4]=P4$,IV_INFO$[0]=N0$
0540 LET IV_PARAMS[0]=I[0],IV_PARAMS[1]=I[3],IV_PARAMS[2]=I[4],IV_PARAMS[3]=I[5]
0545 DIM J0$(20,"0"),J$(80,"-"),J1$(50," "),C[7],R[12],D0$(24),D1$(128),Z[15]
0550 DIM A0$(50),A[3],W0$(53),W1$(22),W2$(67),W[2],D[10],L[20],B[10],O[1]
0555 DIM L0$(24),X0$(32),X1$(10)
0560 LET K0=1,L0=9,L1=9,L9=19,A0$(1,2)=N0$,G5=0,DP=10+MAX(I[0],6),DW=79-DP
0565 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),M9$=M2$,GLH$="",LSH$=""
0570 IF GL$="Y" THEN LET GLH$="GL Acct"
0575 IF LS$="Y" THEN LET LSH$=P8$
0580 CALL "SYC.RM",M9$,M9,8,10
0600 REM " --- Additional file opens"
0610 IF LS$<>"Y" THEN GOTO 0650
0620 CALL "SYC.DA",1,8,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS>0 THEN GOTO 9900
0640 LET IVM07_DEV=CHANNELS[8],IVM08_DEV=CHANNELS[9]
0645 LET IV_FILES[7]=IVM07_DEV
0650 LET GL$="N",STATUS=0
0655 CALL "GLC.BA",ERR=660,PGM(-2),N2$,GLW11$,GL$,STATUS
0660 IF STATUS<>0 THEN GOTO 9900
0670 IF GL$<>"Y" THEN GOTO 0700
0680 CALL "SYC.DA",1,10,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0685 IF STATUS>0 THEN GOTO 9900
0690 LET GLM01_DEV=CHANNELS[10],G5=LEN(G5$)
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,5,0
0720 CALL "SYC.WC",1,0,80,0,0,8,0
0730 PRINT 'SB',@(0,3),"Reference Number:",@(53),"Date:",@(0,4),"Transaction Code:",@(50),"Comment:",@(4,L0-3),"Whse  Item",@(DP),"Description",@(0,L0-2),"Seq",@(5),GLH$,@(7+G5),"Who?",@(12+G5),"Prod",@(77-M1-M2-5),"Cost",@(77-M1-4),"Qty",@(77-10),"Extension",'SF',
0900 REM " --- Init File"
0950 READ (IVE01_DEV,KEY=A0$(1,2),DOM=1000)
0990 READ (IVE01_DEV,KEY=A0$(1,12),DOM=991)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(IVE01_DEV,END=1050)
1030 IF V2$<>"" THEN IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1032 IF LEN(V2$)<12 THEN LET V2$=""
1035 IF V2$<>"" THEN IF V2$(10,3)<>"000" THEN READ (IVE01_DEV,KEY=V2$(1,9)+"Z",DOM=1000)
1040 IF V2$<>"" THEN LET A0$(3,7)=V2$(3,7),A0$(10,3)="000",V2$=V2$(3,7)
1050 LET V0$="Z",V1$="KCE",V3$="",V0=7,V1=18,V2=3
1055 LET V4$="Enter A Transaction Reference Number"
1060 GOSUB 7000
1070 ON V3 GOTO 1090,1000,1000,1000,9900
1090 IF V$="" THEN GOTO 0900
1100 LET A0$(3,7)=V$,A0$(10,3)="000"
1200 REM " --- Find Key"
1205 LET NEWENTRY=1
1210 EXTRACT (IVE01_DEV,KEY=A0$(1,12),DOM=1400)IOL=IVE01A
1220 LET NEWENTRY=0
1300 REM " --- Display"
1310 GOSUB 5000
1320 IF STATUS>0 THEN GOTO 2200
1390 GOTO 4000
1400 REM " --- New Entry Here"
1410 CALL "SYC.YN",1,"Is This A New Entry",2,V$,V3
1440 ON V3 GOTO 1490,1400,1400,1400,1000
1490 ON POS(V$="YN") GOTO 1400,2000,1000
2000 REM " --- Date
2010 LET V0$="D",V1$="CE",V2$=A0$(15,3),V3$="",V0=8,V1=59,V2=3,I0=0
2020 LET V4$="Enter The Date Of This Transaction"
2030 IF FNP$(A0$(15,3))="" THEN LET V2$=F0$(7,3)
2040 GOSUB 7000
2050 ON V3 GOTO 2060,2000,1000,2000,1000
2060 IF GL$="Y" THEN CALL "GLC.EA",V$,"Y",PERIOD$,YEAR$,STATUS; IF STATUS>99 THEN GOTO 2000
2070 LET A0$(15,3)=V$
2090 GOTO 3900
2200 REM " --- Transaction Code"
2210 LET V4$="Enter A Valid Transaction Code (<F3>=Lookup)",I0=1
2220 LET V0$="S",V1$="RC",V2$=A0$(13,2),V3$="",V0=2,V1=18,V2=4
2230 GOSUB 7000
2235 ON V3 GOTO 2250,2200,3900,2240,3900
2240 CALL "IVC.LK",2,V1,V2,V$
2250 GOSUB 5200
2260 IF STATUS>0 THEN GOTO 2200
2280 LET A0$(13,2)=V$
2300 PRINT @(V1,V2),A0$(13,2)," ",TRANS$
2390 GOTO 3900
2400 REM " --- Comment"
2410 LET V0$="S",V1$="",V2$=A0$(18,20),V3$="",V0=20,V1=59,V2=4
2420 LET V4$="Enter Up To "+STR(V0)+" Characters"
2440 GOSUB 7000
2450 ON V3 GOTO 2460,2400,3900,2400,3900
2460 LET A0$(18,20)=V$
2500 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,2400,4000
4000 REM " --- Correct"
4010 LET V4$="Is The Heading Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4050 IF V$="DELETE" THEN GOTO 4200
4060 ON POS(V$="YN") GOTO 4000,4070,2000
4070 LET V$=A0$(13,2)
4080 GOSUB 5200
4090 IF STATUS>0 THEN GOTO 2200
4100 REM " --- Overlay"
4110 LET O0=1,O9=0
4120 WRITE (IVE01_DEV,KEY=A0$(1,12))IOL=IVE01A
4130 EXTRACT (IVE01_DEV,KEY=A0$(1,12))
4190 RUN "IVE.AB"
4200 REM " --- Delete"
4210 READ (IVE11_DEV,KEY=A0$(1,9),DOM=4220)
4220 LET K$=KEY(IVE11_DEV,END=4330)
4230 IF K$(1,2)<>N0$ THEN GOTO 4330
4240 IF LEN(K$)<9 THEN GOTO 4330
4250 IF K$(1,9)<>A0$(1,9) THEN GOTO 4330
4260 READ (IVE11_DEV)IOL=IVE11A
4270 IF POS(TYPE$="IC")=0 THEN GOTO 4310
4280 LET IV_INFO$[1]=W1$(1,2),IV_INFO$[2]=W1$(3,20),IV_INFO$[3]=W2$(1,20)
4290 LET IV_REFS[0]=W[0],ACTION$="UC"
4300 CALL "IVC.UA",ACTION$,IV_FILES[ALL],IV_INFO[ALL],IV_PARAMS$[ALL],IV_INFO$[ALL],IV_REFS$[ALL],IV_REFS[ALL],IV_STATUS
4310 REMOVE (IVE11_DEV,KEY=K$)
4320 GOTO 4220
4330 REMOVE (IVE01_DEV,KEY=A0$(1,12),DOM=4340)
4340 GOTO 1000
5000 REM " --- Display"
5010 PRINT @(59,3),FNB$(A0$(15,3)),
5020 LET V$=A0$(13,2)
5030 GOSUB 5200
5040 PRINT @(59),A0$(18,20)
5090 RETURN
5200 REM " --- Display transaction code and description"
5210 DIM TRANS$(20),X0$(32),X1$(10)
5220 LET STATUS=1,V$=V$+"  ",V$=V$(1,2)
5230 FIND (IVM10_DEV,KEY=N0$+"B"+V$,DOM=5290)IOL=IVM10B
5240 LET TRANS$=X0$(6,20),TYPE$=X0$(26,1),POSTGL$=X0$(27,1),STATUS=0
5245 IF POSTGL$="N" THEN LET X1$=""
5250 REM " --- Verify Not Changing An Existing Issue Or Commit Transaction Code"
5255 IF NEWENTRY THEN GOTO 5290
5260 DIM X0$(32)
5265 FIND (IVM10_DEV,KEY=N0$+"B"+A0$(13,2),DOM=5275)IOL=IVM10B
5270 IF (POS(X0$(26,1)="IC") AND POS(TYPE$="IC")) OR (POS(X0$(26,1)="IC")=0 AND POS(TYPE$="IC")=0) THEN GOTO 5290
5275 LET NUMBER=1; DIM MESSAGE$[NUMBER]
5280 LET STATUS=1,TRANS$=X0$(26,1),MESSAGE$[0]="Can't change the Transaction Code to Type "+TYPE$+".",MESSAGE$[1]="          <Enter> to Continue"
5285 CALL "SYC.XA",2,MESSAGE$[ALL],NUMBER,-1,-1,X$,X
5290 PRINT @(18,4),A0$(13,2)," ",TRANS$,
5295 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
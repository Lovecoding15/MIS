0010 REM "SYC - Lot/Serial Number Lookup"
0020 REM "Program SYC.LL"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "19Aug97 WGH: Use 10-char instead of 7-char for lot/serial location (2560)"
0040 REM 
0045 REM "ACTION$: Records to display - Open/Closed/>Zero/All (input)"
0050 REM "A0$    : Firm ID/Warehouse/Item Number (input)"
0053 REM "       : IVM-07 0$ string/null/NONE (output)"
0060 REM "A1$    : IVM-07 1$ string/null (output)"
0065 REM "A[ALL] : IVM-07 numerics (output)"
0070 REM 
0075 SETERR 9000
0080 SETESC 9000
0090 ENTER ACTION$,A0$,A1$,A[ALL]
0100 REM " --- Open Files"
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-07"
0135 LET FILES$[4]="IVM-10",FILES$[5]="SYM-06",FILES$[6]="APM-01"
0160 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM07_DEV=CHANNELS[3]
0190 LET IVM10_DEV=CHANNELS[4],SYM06_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 IVM07A: IOLIST A0$(1),A1$(1),A[ALL]
0220 IVM01A: IOLIST B0$(1),B1$(1)
0230 APM01A: IOLIST X$,REF$(1)
0240 IVM10C: IOLIST X0$(1)
0250 SYM06A: IOLIST X$,COMPANY$
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 APS01A: IOLIST X$,I1$,I2$,I3$,I4$,M0$,X$,X$,X$
0400 REM " --- Parameters"
0405 DIM P[5],I[1],INFO$[20],SPEED$(128)
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0423 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0425 LET N0$=F0$(16,2),SPEEDSEARCH$="N"
0450 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=0460)
0455 LET N0$=SPEED$(1,2),SPEEDSEARCH$="Y"
0460 FIND (SYM06_DEV,KEY=N0$,DOM=9800)IOL=SYM06A
0465 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0470 IF POS(P3$(17,1)="SL")=0 THEN GOTO 9900
0475 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[3]=NUM(P2$(7,2))
0480 LET P[2]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2)),P[5]=NUM(P2$(13,2))
0485 CALL "SYC.VA","AP",INFO$[ALL]
0490 LET AP$=INFO$[20],M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$)
0500 REM " --- Initializations"
0505 PRECISION NUM(P2$(5,1))
0510 DIM B0$(22),B1$(60),X0$(32)
0520 LET X0$(1)=A0$(1,2)+"C"+A0$(3,2)+"(Unknown)"
0530 LET IWIDTH=79,IHEIGHT=3,IWIN_X=1,IWIN_Y=3,ITITLE$=""
0540 LET WIDTH=P[3]+12,HEIGHT=12,WIN_X=1,WIN_Y=IWIN_Y+IHEIGHT
0545 IF WIDTH<36 THEN LET WIDTH=36
0550 LET MAX_ROW=HEIGHT-5,SWIDTH=79-WIDTH,SHEIGHT=HEIGHT
0555 LET SWIN_X=WIN_X+WIDTH,SWIN_Y=WIN_Y,STITLE$="Lot/Serial Number Detail"
0560 LET W=30,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3,OTITLE$="Lot/Serial Number"
0565 LET OWIDTH=LEN(OTITLE$)+6,OHEIGHT=3,START$=""
0570 IF OWIDTH<P[3]+4 THEN LET OWIDTH=P[3]+4
0585 IF ACTION$="" THEN LET ACTION$="B"
0590 LET PREFIX$=A0$
0600 REM " --- Window arrays"
0610 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0620 LET HEADING$(2)="Lot/Serial #",HEADING$(WIDTH-9)="Status"
0630 LET FOOTING$(2)="F1=Restart F4=End PgUp PgDn"
0640 LET TITLE$="Lot/Serial Number Lookup"
0650 REM " --- Additional file opens"
0655 IF AP$<>"Y" THEN GOTO 0700
0660 CALL "SYC.DA",1,6,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0665 IF STATUS>0 THEN GOTO 9900
0667 LET APM01_DEV=CHANNELS[6]
0670 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0675 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(3,2))
0700 REM " --- Read IVM-01 record"
0710 DIM G2$(IWIDTH-2)
0715 LET B0$(1)=PREFIX$(1,2)+PREFIX$(5)
0720 FIND (IVM01_DEV,KEY=B0$,DOM=9900)IOL=IVM01A
0725 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=0730)IOL=IVM10C
0730 CALL "SYC.WA",0,IWIDTH,IHEIGHT,IWIN_X,IWIN_Y,ITITLE$,INAME$
0735 LET G2$(2)="Item "+FNP$(B0$(3))+" "+FNITEM$(B1$,P[2],P[4],P[5])
0740 LET X$="Warehouse "+X0$(4,2)+" "+FNP$(X0$(6,20))
0743 LET G2$(IWIDTH-LEN(X$)-2)=X$
0745 PRINT @(0,0),'SB','BR',G2$,'ER','SF',
0750 REM " --- Background"
0760 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0765 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0790 CALL "SYC.WA",0,SWIDTH,SHEIGHT,SWIN_X,SWIN_Y,STITLE$,SNAME$
0800 REM " --- Options"
0805 CALL "SYC.WB",SNAME$,0
0810 PRINT 'CS',
0815 CALL "SYC.WB",NAME$,0
0820 PRINT 'CF',
0825 CALL "SYC.WA",0,OWIDTH,OHEIGHT,OWIN_X,OWIN_Y,OTITLE$,ONAME$
0830 LET V0$="S",V1$="KCE",V2$="",V4$="",V0=P[3],V1=1,V2=0
0840 GOSUB 7000
0850 IF V3=6 THEN GOTO 9900
0860 IF V3=5 THEN GOTO 9900
0870 IF V3=4 THEN GOTO 9900
0880 LET START$=V$
0890 CALL "SYC.WD",ONAME$
0900 REM " --- Detail Window"
0910 IF SPEEDSEARCH$<>"Y" THEN GOTO 1000
0950 REM " --- Display SpeedSearch Company"
0955 DIM X$(WIDTH+SWIDTH)
0960 LET X$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
0965 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
0970 CALL "SYC.WA",0,SWIDTH,3,SWIN_X,SWIN_Y+SHEIGHT,"",CNAME$
0975 PRINT @(0,0),'BR',X$,'ER',
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,SHEIGHT-2]
1020 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$=""
1090 CALL "SYC.WB",NAME$,0
1300 REM " --- Position IVM-07 file"
1310 DIM A0$(44),A1$(96),A[12]
1370 LET A0$(1)=PREFIX$,A0$(25)=START$
1380 READ (IVM07_DEV,KEY=A0$,DOM=1400)
1385 LET K$=A0$
1390 GOTO 1430
1400 REM " --- Read next record"
1410 DIM A1$(96),A[12]
1420 LET K$=KEY(IVM07_DEV,END=3000)
1430 IF POS(A0$(1,24)=K$)<>1 THEN GOTO 3000
1440 READ (IVM07_DEV,KEY=K$,ERR=3000)IOL=IVM07A
2000 REM " --- Display page?"
2010 IF L<MAX_ROW THEN GOTO 2100
2020 LET XMODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 0800
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2100 REM " --- Check against display selection"
2120 IF ACTION$="O" THEN IF A1$(54,1)<>" " THEN GOTO 2900
2130 IF ACTION$="C" THEN IF A1$(54,1)<>"C" THEN GOTO 2900
2140 IF ACTION$="Z" THEN IF A[0]-A[1]<=0 OR A1$(54,1)="C" THEN GOTO 2900
2200 REM " --- Lookup window record"
2210 DIM G1$(WIDTH-2)
2220 LET G1$(2)=A0$(25)
2240 IF A1$(54,1)=" " THEN LET G1$(WIDTH-9)="Open"
2245 IF A1$(54,1)="C" THEN LET G1$(WIDTH-9)="Closed"
2250 IF A1$(54,1)="L" THEN LET G1$(WIDTH,9)="Locked"
2260 LET RECEIPT$=FNLATEST$(A1$(64,3),A1$(67,3))
2265 LET ISSUE$=FNLATEST$(A1$(70,3),A1$(73,3))
2290 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$
2300 REM " --- Retrieve vendor name"
2310 LET VENDOR$=""
2320 IF AP$="N" THEN GOTO 2500
2325 LET VENDOR$=A1$(55,6)
2330 IF FNP$(VENDOR$)="" THEN GOTO 2500
2340 DIM REF$(30)
2350 LET REF$(1)="(Unknown Vendor)"
2360 FIND (APM01_DEV,KEY=N0$+VENDOR$,DOM=2370)IOL=APM01A
2370 LET VENDOR$=FNF$(VENDOR$(1,I[0]),M0$)+" "+FNP$(REF$)
2500 REM " --- Detail window record"
2510 DIM G2$(SWIDTH-2)
2515 IF AP$="Y" THEN LET G2$(1)="   {Vendor}"+VENDOR$,MORE$[PAGE,X1,1]=G2$
2520 LET G2$(1)="  {Comment}"+A1$(14,40),MORE$[PAGE,X1,2]=G2$,G2$(1)=""
2525 IF FNP$(RECEIPT$)<>"" THEN LET G2$(1)=" {Received}"+FNB$(RECEIPT$)
2530 IF FNP$(ISSUE$)<>"" THEN LET G2$(SWIDTH-20)="{Issued}"+FNB$(ISSUE$)
2540 LET MORE$[PAGE,X1,3]=G2$,G2$(1)="     {Cost}"+STR(A[3]:M3$)
2560 LET G2$(SWIDTH-20)="{Location}"+A1$(1,10),MORE$[PAGE,X1,5]=G2$
2570 LET G2$(1)="  {On Hand}"+STR(A[0]:M2$),MORE$[PAGE,X1,7]=G2$
2580 LET G2$(1)="{Committed}"+STR(A[1]:M2$),MORE$[PAGE,X1,8]=G2$
2590 LET G2$(1)="{Available}"+STR(A[0]-A[1]:M2$),MORE$[PAGE,X1,9]=G2$
2680 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),LLIST$[PAGE,X1,5]=SNAME$
2690 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next IVM-07 record"
2990 GOTO 1400
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3035 LET MESSAGE$[0]="No Lot/Serial Numbers Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page"
3510 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8100 REM " --- Multi-Line Functions"
8110 DEF FNLATEST$(Q1$,Q2$)
8120 LET Q3$=""
8130 IF FNP$(Q1$)<>"" THEN LET Q3$=Q1$
8140 IF FNP$(Q2$)<>"" THEN IF Q2$>Q3$ THEN LET Q3$=Q2$
8150 RETURN Q3$
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to calling program"
9903 DIM A0$(44),A1$(96),A[12]
9905 FIND (IVM07_DEV,KEY=SELECTION$,ERR=9910)IOL=IVM07A
9910 FOR X=1 TO FILES
9915 CLOSE (CHANNELS[X],ERR=9916)
9920 NEXT X
9925 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9930 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
9935 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
9940 IF INAME$<>"" THEN CALL "SYC.WD",INAME$
9945 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9950 EXIT 
9999 END

0010 REM "APR - Computer Check Register"
0020 REM "Program APR.FA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- OPEN/LOCK FILES"
0110 LET FILES=11
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APW-01",FILES$[2]="APM-01",FILES$[3]="APM-03"
0140 LET FILES$[4]="APE-04",FILES$[5]="APM-10",FILES$[6]="SYS-01"
0150 LET FILES$[7]="APT-02",FILES$[8]="APT-01",FILES$[9]="APT-11"
0160 CALL "SYC.DA",1,1,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET APW01_DEV=CHANNELS[1],APM01_DEV=CHANNELS[2],APM03_DEV=CHANNELS[3]
0185 LET APE04_DEV=CHANNELS[4],APM10_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0190 LET APT02_DEV=CHANNELS[7],APT01_DEV=CHANNELS[8],APT11_DEV=CHANNELS[9]
0200 REM " --- IOLISTS"
0210 APW01A: IOLIST A0$
0220 APM01A: IOLIST B0$,B1$
0230 APM03A: IOLIST B0$,B1$
0240 APE04A: IOLIST D0$,D[0],D[1],D[2]
0250 APM10A: IOLIST H3$(1)
0255 APM10B: IOLIST Z$,Z0$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0300 APT02A: IOLIST S0$,S[0],S[1]
0310 APT01A: IOLIST R0$(1),R[ALL]
0320 APT11A: IOLIST W0$,W1$(1),W[ALL]
0400 REM " --- PARAMETERS"
0405 DIM P[2],G[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N3$=F5$,SOURCE$=PGM(-2)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 FOR X=0 TO 4
0455 LET G[X]=NUM(G2$(X*2+1,2),ERR=0460)
0460 NEXT X
0465 LET G[2]=FNYY_YEAR(G2$(5,2))
0470 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(9,1))
0500 REM " --- Initializations"
0510 DIM J$(80,"-"),D[2],U[2],T[2],V[2],S[1],HEADINGS$[1],O[6],H[6]
0540 LET L9=50,L=L9+1,WIDTH=132,PAGE=0,HEADINGS=1,WHEN$=F0$(7,3)
0550 LET M0=LEN(M0$),HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,CLOCK$=""
0600 REM " --- Additional File Opens"
0650 LET GL$="N",STATUS=0
0655 CALL "GLC.BA",ERR=0660,SOURCE$,N2$,GLW11$,GL$,STATUS
0660 IF STATUS<>0 THEN GOTO 9900
0665 IF GL$<>"Y" THEN GOTO 0700
0670 LET FILES$[10]="GLM-01",FILES$[11]=GLW11$,OPTIONS$[11]="C"
0675 CALL "SYC.DA",1,10,11,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS>0 THEN GOTO 9900
0690 LET GLM01_DEV=CHANNELS[10],GLW11_DEV=CHANNELS[11]
0700 REM " --- Set print positions"
0710 CALL "SYC.RM",M1$,M1,10,15
0715 LET R1$="",R2$="",U1$=J$(1,M1),U2$="",ERRORS=0
0720 LET O[6]=WIDTH-1,H[6]=O[6],O[1]=9,H[1]=9,O[2]=19,H[2]=19
0730 IF P3$(3,1)<>"Y" THEN GOTO 0750
0740 LET O[6]=WIDTH-M1,H[6]=O[6]+M1-10
0745 LET R1$="Retention",R2$="   Amount",U2$=U1$
0750 FOR X=5 TO 3 STEP -1
0760 LET O[X]=O[X+1]-M1,H[X]=O[X]+M1-9
0770 NEXT X
0800 REM " --- Standard stock loaded?"
0810 LET V4$="Has Standard Stock Been Loaded In The Printer"
0820 CALL "SYC.YN",1,V4$,2,V$,V3
0830 IF V3=4 THEN GOTO 9900
0840 IF V$<>"Y" THEN GOTO 9900
0850 REM " --- Run Report?
0860 LET V4$="Do You Want To Print The "+N3$
0870 CALL "SYC.YN",1,V4$,2,V$,V3
0880 IF V3=4 THEN GOTO 9900
0890 IF V$<>"Y" THEN GOTO 9900
0895 rem GOSUB 10000; REM "WA:200
0900 REM " --- File Initializations"
0920 CALL "BPS.01",3,PRTR$,STATUS; IF STATUS THEN GOTO 9900
0940 CALL "SYC.GA",7,1,"",PRTR$,STATUS
0980 CALL "SYC.NB","Printing",10,COLUMN
0990 IF STATUS THEN GOTO 9900
1000 REM " --- INIT READ"
1020 READ (APW01_DEV,KEY=N0$,DOM=1030)
1100 REM " --- MAIN READ"
1120 LET K$=KEY(APW01_DEV,END=4000)
1130 IF K$(1,2)<>N0$ THEN GOTO 4000
1140 READ (APW01_DEV)IOL=APW01A
1150 PRINT @(COLUMN,11),A0$(3,2)," ",A0$(5,7),
1200 REM " --- BREAKS"
1210 IF K$(3,2)<>T0$ THEN GOSUB 6000
1220 LET FIRST=0,GLDATE$=A0$(13,3),GLSTATUS=0
1230 IF GL$="Y" THEN CALL "GLC.EA",GLDATE$,"N",PERIOD$,YEAR$,GLSTATUS
1300 REM " --- VOID"
1310 IF A0$(12,1)<>"V" THEN GOTO 1400
1320 IF L>L9 THEN GOSUB 5000
1330 PRINT (7)A0$(5,7),@(O[1]),FNB$(A0$(13,3)),@(O[2]),"(Void)"
1340 LET L=L+1
1390 GOTO 3900
1500 REM " --- DETAIL"
1505 ON FIRST GOTO 1510,1600
1510 DIM V[2]
1520 READ (APE04_DEV,KEY=A0$(1,4)+A0$(16,16),DOM=3900)IOL=APE04A
1530 IF LEN(D0$)<62 THEN GOTO 1320
1540 IF D0$(56,7)<>A0$(5,7) THEN GOTO 1320
1550 LET D$=D0$(1,10),FIRST=1
1560 GOSUB 6400
1570 LET MEMO$=X0$,REF1$=X1$,REF2$=A0$(22,10),REF3$=A0$(5,7)
1580 IF P3$(1,1)="Y" THEN LET REF3$=REF3$+" "+A0$(3,2)
1590 GOTO 1700
1600 REM 
1610 LET K$=KEY(APE04_DEV,END=3000)
1620 IF K$(1,10)<>D$ THEN GOTO 3000
1630 IF K$(5,4)="TEMP" THEN GOTO 3000
1640 READ (APE04_DEV)IOL=APE04A
1650 LET REF2$=D0$(11,10)
1700 REM " --- ACCUMULATE"
1710 FOR X=0 TO 2
1720 LET V[X]=V[X]+D[X],T[X]=T[X]+D[X],U[X]=U[X]+D[X]
1730 NEXT X
1790 IF GL$<>"Y" THEN GOTO 2900
1800 REM " --- Additional reads"
1810 DIM R0$(71),R[2],W1$(11),W[2]
1820 FIND (APT01_DEV,KEY=A0$(1,4)+A0$(16,6)+D0$(11,10)+"00")IOL=APT01A
1830 LET D9$=R0$(23,2),T0=R[0]
1840 READ (APT11_DEV,KEY=R0$(1,20),DOM=1850)
1850 LET K$=KEY(APT11_DEV,END=1900)
1860 IF POS(R0$(1,20)=K$)<>1 THEN GOTO 1900
1870 READ (APT11_DEV)IOL=APT11A
1880 IF POS(W1$(1,1)="IA") THEN LET T0=T0+W[0]
1890 GOTO 1850
1900 REM " --- DIST CODE"
1920 IF D9$=K2$ THEN GOTO 2000
1930 LET K2$=D9$
1940 DIM Z0$(4*G[4],"0")
1950 FIND (APM10_DEV,KEY=N0$+"B"+K2$,DOM=1960)IOL=APM10B
2000 REM " --- CASH BASIS"
2010 IF P3$(4,1)<>"Y" THEN GOTO 2200
2035 LET ACCOUNT$="",T9=0,T0=0
2040 READ (APT02_DEV,KEY=D0$(1,10)+"I"+D0$(11,10),DOM=2050)
2050 LET K$=KEY(APT02_DEV,END=2120)
2055 IF LEN(K$)<21 THEN GOTO 2120
2060 IF K$(1,21)<>D0$(1,10)+"I"+D0$(11,10) THEN GOTO 2120
2070 READ (APT02_DEV)IOL=APT02A
2075 IF T0=0 THEN LET T0=D[0]-D[2]
2077 IF T0=0 THEN GOTO 2050
2080 LET ACCOUNT$=S0$(22,G[4])
2085 LET AMOUNT=S[0]*((D[0]-D[2])/T0),UNITS=S[1]*((D[0]-D[2])/T0)
2090 GOSUB GLRECAP
2095 LET T9=T9+AMOUNT
2100 GOTO 2050
2120 IF ACCOUNT$="" THEN GOTO 2150
2130 IF T9=D[0]-D[2] THEN GOTO 2150
2140 LET AMOUNT=D[0]-D[2]-T9
2145 GOSUB GLRECAP
2200 REM " --- DIST CASH"
2220 LET ACCOUNT$=Z0$(G[4]+1,G[4]),AMOUNT=(-D[0])+D[2]+D[1],UNITS=0
2240 GOSUB GLRECAP
2300 REM " --- PAYABLE"
2310 IF P3$(4,1)="Y" THEN GOTO 2400
2320 LET ACCOUNT$=Z0$(1,G[4]),AMOUNT=D[0]-D[2],UNITS=0
2340 GOSUB GLRECAP
2400 REM " --- DISCOUNT"
2410 IF D[1]=0 THEN GOTO 2500
2420 LET ACCOUNT$=Z0$(2*G[4]+1,G[4]),AMOUNT=-D[1],UNITS=0
2440 GOSUB GLRECAP
2900 REM 
2990 GOTO 1500
3500 REM " --- PRINT"
3510 IF L+1>L9 THEN GOSUB 5000
3520 LET X$=""
3550 IF P3$(3,1)="Y" THEN LET X$=STR(V[2]:M1$)
3560 PRINT (7)A0$(5,7),@(O[1]),FNB$(A0$(13,3)),@(O[2]),X1$," ",X0$,@(O[3]),V[0]:M1$,@(O[4]),V[1]:M1$,@(O[5]),V[0]-V[2]-V[1]:M1$,@(O[6]),X$
3565 LET L=L+1
3570 IF GLSTATUS>0 THEN CALL "GLC.GA",7,GLDATE$,GLSTATUS,L,ERRORS,STATUS
3900 REM 
3990 GOTO 1100
4000 REM " --- TOTALS"
4010 IF L>L9 THEN GOSUB 5000
4020 LET T9$="E"
4030 GOSUB 6000
4035 LET X$=""
4040 IF P3$(3,1)="Y" THEN LET X$=STR(U[2]:M1$)
4045 PRINT (7)""
4050 PRINT (7)@(O[3]),U1$,@(O[4]),U1$,@(O[5]),U1$,@(O[6]),U2$
4055 PRINT (7)@(O[2]),"Total For Report",@(O[3]),U[0]:M1$,@(O[4]),U[1]:M1$,@(O[5]),U[0]-U[2]-U[1]:M1$,@(O[6]),X$
4060 IF ERRORS=0 THEN GOTO 4100
4070 PRINT (7)""
4080 PRINT (7)@(O[2]),"WARNING: This Register Is Missing Information"
4100 REM " --- Run next overlay"
4110 LET UPDATE$="APU.BA"
4180 IF GL$="Y" THEN RUN "GLR.XA"
4190 RUN UPDATE$
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 IF H3$<>"" THEN PRINT (7)@(FNC(H3$,WIDTH)),H3$; LET L=L+1
5050 PRINT (7)""
5060 PRINT (7)"Check",@(H[1]),"Check",@(H[3])," Invoice",@(H[4]),"Discount",@(H[5])," Payment",@(H[6]),R1$
5070 PRINT (7)"Number",@(H[1]),"Date",@(H[2]),"Vendor",@(H[3]),"  Amount",@(H[4]),"  Amount",@(H[5]),"  Amount",@(H[6]),R2$
5080 PRINT (7)""
5085 LET L=L+4
5090 RETURN 
6000 REM " --- AP TYPE BREAK"
6010 IF T0$="" THEN GOTO 6100
6020 IF P3$(1,1)<>"Y" THEN GOTO 6100
6030 LET X$=""
6040 IF P3$(3,1)="Y" THEN LET X$=STR(T[2]:M1$)
6050 PRINT (7)""
6060 PRINT (7)@(O[3]),U1$,@(O[4]),U1$,@(O[5]),U1$,@(O[6]),U2$
6070 PRINT (7)@(O[2]),"AP Type Totals",@(O[3]),T[0]:M1$,@(O[4]),T[1]:M1$,@(O[5]),T[0]-T[2]-T[1]:M1$,@(O[6]),X$
6090 LET T1$="",L=L9+3
6100 IF T9$="E" THEN GOTO 6290
6120 DIM T[2],H3$(32)
6140 LET T0$=A0$(3,2),T9$=""
6150 IF P3$(1,1)<>"Y" THEN GOTO 6290
6160 LET X$="AP Type "+T0$+" ",H3$(6,20)="Unknown AP Type",E0=1
6170 FIND (APM10_DEV,KEY=N0$+"A"+T0$,DOM=6200)IOL=APM10A
6180 LET E0=0
6200 LET H3$=X$+FNP$(H3$(6,20)),ERRORS=ERRORS+E0
6290 RETURN 
6400 REM  " --- VENDOR NAME"
6500 IF T9$<>"" THEN GOTO 6790
6510 DIM X1$(M0)
6520 LET X0$="Unknown Vendor",X2$="",E0=1
6530 IF D0$(5,4)="TEMP" THEN GOTO 6600
6550 FIND (APM01_DEV,KEY=N0$+D0$(5,6),DOM=6570)IOL=APM01A
6560 LET X0$=B1$(1,30),E0=0
6570 LET X1$(1)=FNF$(D0$(5,P[0]),M0$)
6590 GOTO 6700
6600 REM " --- TEMP VENDOR"
6610 LET X1$(1)=D0$(5,4)
6620 FIND (APM03_DEV,KEY=D0$(1,4)+D0$(11,10),DOM=6640)IOL=APM03A
6630 LET X0$=B1$(1,30),E0=0
6720 LET ERRORS=ERRORS+E0
6790 RETURN 
6900 REM " --- Standard G/L Recap Routine"
6910 GLRECAP: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLW11_DEV,GLT05_DEV,ACCOUNT$,GLDATE$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
10000 REM " --- Printer select
10010 DIM OPTION$[2]
10030 CALL "SYC.QP",STAT,OPTION$[ALL]
10050 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
10060 PRINT 'SB',@(74,1),FILL(4),@(74,1),F0$(13,2)
10070 IF F0$(13,2)<>"PF" THEN PRINT @(76,1),"-",F0$(25,1),
10080 PRINT 'SF',
10090 RETURN 
46500 END

0010 REM "BMM - Bill Maintenance
0020 REM "Program BMM.MA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETESC 9000
0055 SETERR 9000
0060 ON O0 GOTO 0070,4000,1000,6400
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=15
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="BMM-01",FILES$[2]="BMM-01",FILES$[3]="BMM-02"
0130 LET FILES$[4]="BMM-03",FILES$[5]="BMM-04",FILES$[6]="BMM-05"
0135 LET FILES$[7]="BMM-06",FILES$[8]="BMM-08",FILES$[9]="BMM-09"
0140 LET FILES$[10]="IVM-01",FILES$[11]="IVM-02",FILES$[12]="IVM-10"
0145 LET FILES$[13]="IVM-14",FILES$[14]="SYS-01"
0150 LET FILES$[15]="APM-01"
0160 CALL "SYC.DA",1,1,14,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS<>0 THEN GOTO 9900
0175 LET BMM01_DEV=CHANNELS[1],BMM01_DEV2=CHANNELS[2],BMM02_DEV=CHANNELS[3]
0180 LET BMM03_DEV=CHANNELS[4],BMM04_DEV=CHANNELS[5],BMM05_DEV=CHANNELS[6]
0185 LET BMM06_DEV=CHANNELS[7],BMM08_DEV=CHANNELS[8],BMM09_DEV=CHANNELS[9]
0190 LET IVM01_DEV=CHANNELS[10],IVM02_DEV=CHANNELS[11],IVM10_DEV=CHANNELS[12]
0195 LET IVM14_DEV=CHANNELS[13],SYS01_DEV=CHANNELS[14]
0200 REM " --- IOLists
0210 BMM01A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0230 IVM01A: IOLIST *,DESC$(1),PROD$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 BMS01A: IOLIST X$,P1$,P2$,P3$,P4,X5,BOMP6
0320 IVS01A: IOLIST X$,P5$,P6$,WH$,X$,X$,P7$,P8$,P9$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="BM",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=BMS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV"+"00",DOM=9800)IOL=IVS01A
0445 LET WH$=WH$(6,2)
0450 DIM P[2],DESC[3],PARAMS[3]
0455 LET P[0]=NUM(P6$(1,2))
0460 FOR X=1 TO 3
0470 LET DESC[X]=NUM(P6$((X-1)*2+9,2))
0475 LET PARAMS[X]=DESC[X]
0480 NEXT X
0485 LET PARAMS[0]=P[0]
0492 IF NUM(P6$(5,1))>BOMP6 THEN LET P6=NUM(P6$(5,1)) ELSE LET P6=BOMP6
0495 PRECISION P6
0496 DIM INFO$[20]; CALL "SYC.VA","AP",INFO$[ALL]
0497 LET AP$=INFO$[20]
0498 CALL "SYC.VA","WO",INFO$[ALL]
0499 LET WO$=INFO$[20]
0500 REM " --- Init Data
0520 DIM A0$(52),A1$(30),A2$(57),A[11]
0525 DIM B1$(20),B[10],R1$(110),R[11],Z1$(69),Z[9]
0530 LET K0=1,L0=10,L9=20,L2=L0+2
0540 LET A0$(1,2)=N0$
0550 LET M1=LEN(M1$),P99$=P9$
0555 CALL "SYC.RM",P1$,P1,9,9
0560 CALL "SYC.RM",P2$,P2,7,7
0565 CALL "SYC.RM",P8$,P8,5,8
0570 CALL "SYC.RM",P9$,P9,6,8
0575 LET M2$=P8$,M9$="#####0-",M5$="####.00-"
0580 LET M7$=P9$,M9$="#####0-",M0=P[0],M7=LEN(M7$),P7=LEN(P7$),M2=LEN(M2$),M9=LEN(M9$)
0585 LET M6$="###.00",P4$="###0"
0590 DIM IV_CHANS[14]
0595 LET IV_CHANS[1]=IVM01_DEV,IV_CHANS[14]=IVM14_DEV
0600 REM " --- Additional File Opens
0630 IF AP$="N" THEN GOTO 0700
0660 CALL "SYC.DA",1,15,15,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0670 IF STATUS<>0 THEN GOTO 9900
0680 LET APM01_DEV=CHANNELS[15]
0700 REM " --- Background
0710 CALL "SYC.WC",1,0,80,0,0,9,0
0740 PRINT 'SB',@(5,3),"Bill Number:",@(39),"Last Used:",@(5,4),"Description:",@(54),"Phantom Bill?:",@(52,5),"Unit Of Measure:",@(50,6),"Estimated Yield %:",@(2,7),"Drawing Number:",@(50),"Standard Lot Size:",@(0,8),"Drawing Revision:",@(24),"Revision Code:",@(42),"Created:",@(60),"Revised:",
0790 PRINT 'SF',
0900 REM " --- Init File
0920 READ (BMM01_DEV,KEY=A0$(1,2),DOM=1000)
1000 REM " --- Key
1010 DIM ITEM$[6],ITEM[12],DESC$(60)
1020 PRINT 'CF',
1030 LET DEFAULT$="",DEFAULT$=KEY(BMM01_DEV,END=1070)
1040 IF DEFAULT$(1,2)<>A0$(1,2) THEN LET DEFAULT$=""
1050 IF LEN(DEFAULT$)<22 AND DEFAULT$<>"" THEN LET DEFAULT$=DEFAULT$+FILL(22-LEN(DEFAULT$))
1060 IF DEFAULT$<>"" THEN LET DEFAULT$=DEFAULT$(3,20)
1070 LET ACTION$="N",V1=18,V2=3
1080 LET V4$="Enter A Bill Number (<F1>=Item Lookup/<F3>=Bill Number Lookup)"
1090 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1100 IF V3=4 THEN GOTO 9900
1110 IF V3<>1 THEN GOTO 1140
1120 CALL "SYC.LI",V$
1130 GOTO 1160
1140 IF V3<>3 THEN GOTO 1210
1150 CALL "SYC.LB",V$
1160 IF V$="" THEN GOTO 0900
1170 LET A0$(3,20)=V$+FILL(20-LEN(V$))
1180 FIND (IVM01_DEV,KEY=A0$(1,22),DOM=1000)IOL=IVM01A
1190 LET SALEUNIT$=PROD$(4,2)
1200 GOTO 1240
1210 IF ITEM$[0]="" THEN GOTO 0900
1220 LET A0$(3,20)=ITEM$[0]
1230 LET DESC$(1)=ITEM$[1],SALEUNIT$=ITEM$[2](4,2)
1240 PRINT @(V1,V2),A0$(3,P[0]),
1250 REM " --- Find Key
1260 FIND (BMM01_DEV,KEY=A0$(1,22),DOM=1400)IOL=BMM01A
1270 LET O0=0,O2=0
1300 REM " --- Display
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record
1410 GOSUB 5500
1420 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-up
1510 LET A2$(1)="",A2$(40,1)="N",A2$(45,3)=F0$(7,3),A2$(48,2)=SALEUNIT$
1520 LET O0=0,O9=0,O2=1,A0$(23)=""
1530 DIM A[11]
1540 LET A[4]=100,A[5]=1
2000 REM " --- Overlay
2040 RUN "BMM.MB"
4000 REM " --- Options
4010 DIM MENUS$[2]
4015 LET MENUS=1,MENUS$[MENUS]="BMM.MA",WIN_X=60,WIN_Y=15,O0=1,O9=0
4025 IF AP$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="BMM.MJ"
4030 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4040 CALL "SYC.WC",1,0,80,0,0,9,0
4050 PRINT @(0,10),'CE',
4060 IF SELECTION$="" THEN GOTO 1000
4070 IF SELECTION$="HARDCOPY" THEN GOTO 4200
4075 IF SELECTION$="PICKLIST" THEN GOTO 4300
4080 IF SELECTION$="TOTALS" THEN GOTO 6400
4085 IF SELECTION$="SYC.KA" THEN GOTO 4100
4090 IF SELECTION$="BMM.MB" THEN LET O9=1
4095 RUN SELECTION$,ERR=4000
4100 REM " --- Comments
4110 DIM INFO$[5],INFO[5]
4120 LET X$="Bill "+A0$(3,20)+" "+DESC$,INFO[1]=L0
4130 LET INFO$[0]="BMM-09",INFO$[1]=A0$(1,22),INFO$[2]=X$,ACTION=0
4150 CALL "SYC.KA",ACTION,INFO$[ALL],INFO[ALL]
4190 GOTO 4000
4200 REM " --- Print Hard Copy
4210 GOSUB 6700
4220 IF V3=4 THEN PRINT @(0,12),'CE',; GOTO 4000
4230 LET PRTR_DEV=UNT
4240 CALL "SYC.GA",PRTR_DEV,1,"","",V1
4250 IF STATUS THEN GOTO 4000
4260 CALL "BMC.AA",2,PRTR_DEV,A0$(3,20),0,COMMENT$,"",SELECT$,PDATE$,STATUS
4270 PRINT @(0,10),'CE',
4280 CLOSE (PRTR_DEV,ERR=4290)
4290 GOTO 4000
4300 REM " --- Print Packing List
4310 GOSUB PROD_DATE
4315 IF V3=4 THEN GOTO 4000
4330 LET PRTR_DEV=UNT
4335 CALL "SYC.GA",PRTR_DEV,1,"","",V1
4340 IF STATUS THEN GOTO 4000
4350 PRINT @(30,12),"Printing...",
4360 CALL "BMC.CA",PRTR_DEV,A0$(3,20),0,PDATE$,STATUS
4370 PRINT @(0,12),'CE',
4380 CLOSE (PRTR_DEV,ERR=4390)
4390 GOTO 4000
5000 REM " --- Display
5020 PRINT @(18,3),A0$(3,P[0]),@(50),FNB$(A2$(41,3)),@(18,4),DESC$(1,MIN(DESC[1],36)),@(69,4),A2$(40,1),@(18,5),DESC$(DESC[1]+1,MIN(DESC[2],34)),@(69,5),A2$(48,2),@(18,6),DESC$(DESC[1]+DESC[2]+1,MIN(DESC[3],32)),@(69,6),STR(A[4]:"##0.0"),
5025 IF A2$(44,1)="B" THEN PRINT @(59,3),"In Production Entry" ELSE IF A2$(44,1)="W" THEN PRINT @(59,3),"In Work Orders",
5030 PRINT @(18,7),A0$(23,25),@(69,7),A[5]:"######0",@(18,8),A0$(48,5),@(39,8),A2$(38,2),@(51,8),FNB$(A2$(45,3)),@(69,8),FNB$(A2$(35,3)),
5090 RETURN 
5500 REM " --- Display New Record Description
5550 PRINT @(18,4),DESC$(1,MIN(DESC[1],36)),@(18,5),DESC$(DESC[1]+1,MIN(DESC[2],34)),@(18,6),DESC$(DESC[1]+DESC[2]+1,MIN(DESC[3],32)),
5560 PRINT @(69,5),A2$(48,2),@(51,8),FNB$(F0$(7,3)),
5590 RETURN 
6000 REM " --- Get Effective Date
6005 PROD_DATE: 
6010 IF PDATE$="" THEN LET PDATE$=F0$(7,3)
6020 PRINT 'SB',@(22,13),"Production Date:",'SF',
6030 LET V0$="D",V1$="R",V2$=PDATE$,V3$="",V4$="Enter Production Date",V0=8,V1=40,V2=13
6040 GOSUB 7000
6080 LET PDATE$=V$
6085 RETURN 
6100 REM " --- Get Warehouse
6110 WAREHOUSE: 
6120 LET GO_UP$="N"
6130 IF WHSE$="" THEN LET WHSE$=WH$
6140 PRINT 'SB',@(28,15),"Warehouse:",'SF',
6150 LET V0$="S",V1$="R",V2$=WHSE$,V3$="",V4$="Enter Warehouse ID (<F3>=Lookup)",V0=2,V1=40,V2=15
6160 GOSUB 7000
6180 ON V3 GOTO 6210,6210,6260,6190,6210
6190 CALL "IVC.LK",3,V1,V2,V$
6210 IF V$="" THEN GOTO WAREHOUSE
6220 LET WHSE$=V$
6230 FIND (IVM10_DEV,KEY=N0$+"C"+WHSE$,DOM=6250)
6235 PRINT @(V1,V2),WHSE$
6240 GOTO 6270
6250 GOTO WAREHOUSE
6260 LET GO_UP$="Y"
6270 RETURN 
6400 REM " --- Calculate Totals
6410 PRECISION P6
6415 LET WHSE$="",EA_STATUS=0
6420 IF O0=3 THEN LET PDATE$=F0$(7,3)
6430 CALL "SYC.WC",1,0,80,0,0,9,0
6440 PRINT @(10,9),'SB',"Total Information",'SF',
6450 GOSUB PROD_DATE
6460 GOSUB WAREHOUSE
6470 IF GO_UP$="Y" THEN GOTO 6450
6480 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)? ",V0=6,V1=FNV(V4$),V2=22
6485 GOSUB 7000
6490 ON POS(V$="YN") GOTO 6480,6500,6450
6500 REM " --- Print Total Information
6510 DIM CHANN[9]
6520 LET CHANN[0]=BMM01_DEV,CHANN[1]=BMM02_DEV,CHANN[2]=BMM03_DEV
6530 LET CHANN[3]=BMM05_DEV,CHANN[4]=BMM08_DEV,CHANN[5]=IVM02_DEV
6535 LET CHANN[9]=SYS01_DEV
6560 CALL "BMC.EA",A0$(3,20),CHANN[ALL],A[5],PDATE$,N0$,AP$,"N",1,MAT_COST,LAB_COST,OH_COST,SUB_COST,A[5],A[5],"N",WHSE$,EA_STATUS
6580 PRINT @(0,10),'CE','SB',@(18,13),"Material :",@(14,14),"Direct Labor : ",@(18,15),"Overhead :",@(15,16),"Subcontract :",@(21,18),"Total : ",'SF',
6590 CALL "SYC.WC",1,0,LEN(P99$),0,31,17,0
6600 PRINT @(31,13),MAT_COST*P4:P99$,@(31,14),LAB_COST:P99$,@(31,15),OH_COST:P99$,@(31,16),SUB_COST:P99$,@(31,18),MAT_COST*P4+LAB_COST+OH_COST+SUB_COST:P99$," As Of ",FNB$(PDATE$),
6605 DIM MESSAGE$[0]
6610 LET MESSAGE$[0]="<Enter> To Continue: ",TITLE=0
6615 IF EA_STATUS=11 THEN LET MESSAGE$[0]="Cost may be inaccurate since there's no warehouse record for item.",TITLE=3
6620 CALL "SYC.XA",TITLE,MESSAGE$[ALL],0,22,-1,V$,V3
6630 LET O2=0
6640 GOTO 4000
6700 REM " --- Get Effective Date Or All And Comment for Hard Copy
6710 IF PDATE$="" THEN LET PDATE$=F0$(7,3)
6720 PRINT 'SB',@(22,13),"Production Date:",@(29,14),"Comments:",'SF',
6730 LET V0$="D",V1$="R",V2$=PDATE$,V3$="",V4$="Enter Production Date <F1> For All",V0=8,V1=40,V2=13
6740 GOSUB 7000
6750 LET PDATE$=V$
6760 IF V3=1 THEN PRINT @(V1,V2),"All",'CL',; LET SELECT$="A" ELSE LET SELECT$="C"
6770 IF V3=4 THEN GOTO 6890
6800 REM " --- Comments
6810 LET V0$="Y",V1$="",V2$="Y",V3$="",V4$="Include Comments (Y/N)?",V0=1,V1=40,V2=14
6820 GOSUB 7000
6830 IF V3=2 THEN GOTO 6730
6840 LET COMMENT$=V$
6890 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

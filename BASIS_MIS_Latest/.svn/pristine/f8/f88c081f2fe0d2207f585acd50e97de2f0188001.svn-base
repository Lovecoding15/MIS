0010 REM "SYC - SpeedSearch Window Processing"
0020 REM "Program SYC.QW"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "07Jun96 JAL: Added missing internal documentation (40-78)"
0032 REM "21Aug96 WGH: Initial key value is not passed to the next search option after a DO NOW (3010)"
0033 REM "11Nov98 WGH: More window may be wrong when page back after more than 20 pages (460,580)
0040 REM 
0042 REM "ACTION$        : Action code (input)"
0044 REM "               : O=Process option window"
0046 REM "OPTION$[n]     : Std SpeedSearch options array (input/returned)"
0048 REM "OPTLIST$[a,b,c]: Option list (input/returned)"
0049 REM "               : a = Number of pages, must be greater than one"
0050 REM "               : b = Option number
0051 REM "               : c = Option element"
0052 REM "               :     1 = Description"
0053 REM "               :     2 = Program/Menu Name"
0054 REM "               :     3 = Parameter String"
0055 REM "               :     4 = Option Type"
0056 REM "               :     5 = Security Level"
0057 REM "               :     6 = Password"
0061 REM "LLIST$[a,b,c]  : Standard display list (input)"
0062 REM "               : a=Page   b=Line         c=1 through 5"
0063 REM "               : [page,line,1]=Text to display in window"
0064 REM "               : [page,line,2]=Value to return when selected"
0065 REM "               : [page,line,5]=Detail window name"
0066 REM "MORE$[a,b,c]   : Additional information (input)"
0067 REM "               : a=Page   b=LLIST$ Line  c=Line"
0068 REM "               : [page,line,0]=Number of lines to display"
0069 REM "               : [page,line,n]=Text to display in detail window"
0070 REM "WININFO$[n]    : Window string parameters (input)"
0071 REM "               : [1]=Current key value   [2]=Secondary key value"
0073 REM "WININFO[n]     : Window numeric parameters (input/returned)"
0074 REM "               : [0]=Restart flag        [2]=Maximum # of pages"
0075 REM "               : [1]=Current page        [3]=Maximum option line"
0076 REM "               : [4]=Max 'more' lines
0077 REM "FKEY           : Function key value (returned)"
0078 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
0400 REM " --- Parameters"
0410 LET X$=STBL("!SPEEDSEARCH"),ONAME$=FNP$(X$(27,8)),OWIDTH=NUM(X$(83,2))
0420 LET OHEIGHT=NUM(X$(85,2)),MAXOPT=NUM(X$(57,2))
0430 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0440 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0450 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0460 LET SHEIGHT=NUM(X$(45,2))
0500 REM " --- Initializations"
0550 LET PAGE=WININFO[1],PAGES=WININFO[2],OMAX_ROW=WININFO[3]
0570 LET CONTROL=0,MAXROW=HEIGHT-5,OPTION=OMAX_ROW-1,FKEY=0,MAXMORE=WININFO[4]
0580 IF MAXMORE=0 THEN LET MAXMORE=SHEIGHT-2
0900 REM " --- Branch based on ACTION$"
0990 IF POS("O"=ACTION$)>0 THEN GOTO 2000
1000 REM " --- Middle window processing"
1010 CALL "SYC.WB",NAME$,0
1020 CALL "SYC.SA",5,LLIST$[ALL],MORE$[ALL],ANSWER$,PAGE,MAXROW,NAME$,HEIGHT,WIDTH,FKEY
1030 IF FKEY=-1 THEN GOTO 3600
1040 IF FKEY=-2 THEN GOTO 3600
1050 IF FKEY=4 THEN GOTO 3200
1060 IF FKEY=5 THEN GOTO 3400
1070 IF ANSWER$="END" THEN GOTO 3200
1080 IF FKEY=-16 THEN LET PAGE=PAGE+1
1085 IF PAGE>PAGES-1 THEN GOTO 1200
1090 GOTO 4000
1200 REM " --- Roll back pages"
1210 CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAXROW,5,MAXMORE
1290 GOTO 4000
2000 REM " --- Option window processing"
2010 CALL "SYC.WB",ONAME$,0
2020 PRINT 'CS',
2025 LET PAGE=1
2030 CALL "SYC.SA",0,OPTLIST$[ALL],MORE$[ALL],ANSWER$,PAGE,OMAX_ROW,ONAME$,OHEIGHT,OWIDTH,FKEY
2040 IF FKEY=-16 THEN GOTO 2000
2050 IF FKEY=-1 THEN GOTO 3600
2060 IF FKEY=-2 THEN GOTO 3600
2070 IF FKEY=4 THEN GOTO 3200
2080 IF FKEY=5 THEN GOTO 3400
2090 IF ANSWER$="END" THEN GOTO 3200
2200 REM " --- Which option was selected?"
2210 FOR X=1 TO OPTION
2220 IF OPTLIST$[1,X,2]<>ANSWER$ THEN GOTO 2290
2230 LET OPTION$[2]=OPTLIST$[1,X,3],SELECTION=X
2240 LET SECURITY$=OPTLIST$[1,X,5],PASSWORD$=OPTLIST$[1,X,6]
2290 NEXT X
2400 REM " --- Security check"
2410 IF SECURITY$="" AND PASSWORD$="" THEN GOTO 2600
2420 CALL "SYC.QX","M",SECURITY$,SELECTION,OPTLIST$[ALL],STATUS
2430 IF STATUS>0 THEN GOTO 2000
2600 REM " --- Return selection"
2610 LET OPTION$[1]=FNP$(ANSWER$)
2620 IF LEN(OPTION$[2])>=LEN(PARAM$) THEN IF OPTION$[2](1,LEN(PARAM$))=PARAM$ THEN LET OPTION$[3]=WININFO$[1]
2800 REM " --- Call now?"
2810 IF OPTION$[2]<>"CALL NOW" THEN GOTO 3000
2820 CALL FNP$(OPTION$[1]),ERR=1000,NAME$
2890 GOTO 2000
3000 REM " --- Do now?"
3010 IF OPTION$[2]="DO NOW" THEN LET CONTROL=1,OPTION$[2]=PARAM$
3020 GOTO 4000
3200 REM " --- <F4> Exit"
3210 LET OPTION$[1]="EXIT"
3290 GOTO 4000
3400 REM " --- <F5> SpeedSearch"
3410 LET OPTION$[1]="STARTOVER"
3490 GOTO 4000
3600 REM " --- Arrows"
3610 LET OPTION$[1]="PRIOR"
3690 GOTO 4000
4000 REM " --- Processing completed"
4010 LET WININFO$[0]=ANSWER$,WININFO[0]=CONTROL,WININFO[1]=PAGE
4090 GOTO 9900
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9950 EXIT 
9999 END

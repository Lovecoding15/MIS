0010 REM "IVR - Lot/Serial Activity Report (Report Overlay)"
0020 REM "Program IVR.6B"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 CALL "SYC.GA",7,1,"","",STATUS
0120 IF STATUS THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$,A5$,A6$,A[ALL]
0220 IVM07A: IOLIST B0$(1),B1$(1),B[ALL]
0230 IVM10C: IOLIST X0$(1)
0240 APM01A: IOLIST C0$(1),C1$(1)
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A[12],C0$(8),C1$(30)
0520 DIM PERIOD$[1],HEADINGS$[6],O[13],B0$(44),B1$(96),B[12]
0540 LET FILES[2]=IVM17_DEV,FILES[3]=IVM03_DEV,T0$="",T1$="",X$="Item",W=7+P[0]
0545 IF SEQUENCE$="L" THEN LET X$="Lot/Serial Number",W=P[2]
0555 LET HEADINGS$[0]=F4$,WHEN$=F0$(7,3),CLOCK$="",H9$="",NUMBER=0
0560 LET WIDTH=132,PAGE=0,L9=59,L=L9+1,L1=0,SELECTION$="END",HEADINGS=6
0570 LET BEGWHSE$=OPTIONS$[1],ENDWHSE$=OPTIONS$[2],BEGPROD$=OPTIONS$[3]
0575 LET ENDPROD$=OPTIONS$[4],BEGITEM$=OPTIONS$[5],ENDITEM$=OPTIONS$[6]
0580 LET TYPE$=OPTIONS$[7],X$="Item"
0585 LET PERIOD$[0]=BEGYEAR$+BEGPER$,PERIOD$[1]=ENDYEAR$+ENDPER$
0590 LET HEADINGS$[1]=F5$+" By "+X$+" From Period "+BEGPER$+" "+FNYY21_YY$(BEGYEAR$)+" Through Period "+ENDPER$+" "+FNYY21_YY$(ENDYEAR$)
0600 REM " --- Print positions"
0610 CALL "SYC.RM",M2$,M2,11,15
0615 LET O[13]=WIDTH-M2,O[2]=P[0]+1
0620 FOR X=12 TO 8 STEP -1
0625 LET O[X]=O[X+1]-M2
0630 NEXT X
0635 LET O[1]=6,O[3]=O[1]+MAX(P[2],15)+1,O[4]=O[3]+9,O[5]=O[4]+9,O[7]=O[8]-5
0650 LET O[6]=O[5]+M0+1,DW=O[7]-O[2],VW=O[8]-O[5]
0655 IF SEQUENCE$<>"L" THEN GOTO 0700
0660 LET O[1]=3,O[2]=O[1]+3,O[3]=MAX(P[2],15)+1,O[4]=O[2]+P[0]+1,O[5]=O[3]+9
0665 LET O[6]=O[5]+9,DW=O[7]-O[4],VW=O[8]-O[6]
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",W,COLUMN
0800 REM " --- Report headings"
0805 DIM H1$(WIDTH),H2$(WIDTH)
0810 LET X1$="First",X2$=X1$,X2=5,X3$=X1$,X3=29,H8$="All Item Types"
0815 IF TYPE$<>"" THEN LET H8$="Item Type "+FNP$(TYPE$)
0820 IF BEGITEM$<>"" THEN LET X1$=BEGITEM$
0825 IF BEGPROD$<>"" THEN LET X2$=BEGPROD$,X2=3
0830 IF ENDPROD$="" THEN IF X2=3 THEN LET X2=4
0833 IF BEGLOT$<>"" THEN LET X3$=BEGLOT$,X3=24+P[2]
0835 IF ENDLOT$<>"" THEN IF X3=29 THEN LET X3=24+P[2]
0837 LET H1$(1)="From Item: "+X1$,X3$="From Lot/Serial Number: "+X3$
0840 IF SEQUENCE$<>"L" THEN GOTO 0850
0843 LET X3$=X3$+"     From Warehouse: ",X1$="First"
0845 IF BEGWHSE$<>"" THEN LET X1$=BEGWHSE$
0847 LET X3$=X3$+X1$,X3=LEN(X3$)
0850 LET X3=INT((WIDTH-X3)/2),H1$(X3)=X3$,H1$(WIDTH-X2-13)="From Product: "+X2$
0853 LET X1$="Last",X2$=X1$,X3$=X1$
0855 IF ENDITEM$<>"" THEN LET X1$=ENDITEM$
0857 IF ENDPROD$<>"" THEN LET X2$=ENDPROD$
0860 IF ENDLOT$<>"" THEN LET X3$=ENDLOT$
0863 LET H2$(1)="  To Item: "+X1$,X3$="  To Lot/Serial Number: "+X3$
0865 IF SEQUENCE$<>"L" THEN GOTO 0885
0867 LET X3$=X3$+"        To Warehouse: ",X1$="Last"
0870 IF ENDWHSE$<>"" THEN LET X1$=ENDWHSE$
0875 LET X3$=X3$+X1$
0885 LET H2$(X3)=X3$,H2$(WIDTH-X2-13)="  To Product: "+X2$
0890 LET HEADINGS$[2]=H8$,HEADINGS$[4]=H1$,HEADINGS$[5]=H2$
0900 REM " --- Position lot/serial master"
0910 IF SEQUENCE$="L" THEN GOTO 0950
0915 LET K$=N0$+BEGWHSE$
0920 IF BEGWHSE$<>"" THEN LET K$=K$+BEGITEM$
0930 IF BEGWHSE$<>"" AND BEGITEM$<>"" THEN LET K$=K$+BEGLOT$
0940 READ (IVM07_DEV,KEY=K$,DOM=1000)
0945 GOTO 1020
0950 REM " --- Position lot/serial xref"
0960 LET K$=N0$+BEGLOT$
0970 IF BEGLOT$<>"" THEN LET K$=K$+BEGWHSE$
0975 IF BEGLOT$<>"" AND BEGWHSE$<>"" THEN LET K$=K$+BEGITEM$
0980 READ (IVM08_DEV,KEY=K$,DOM=1000)
1000 REM " --- Get next lot/serial master"
1010 IF SEQUENCE$="L" THEN GOTO 1600
1020 LET K$=KEY(IVM07_DEV,END=4000)
1030 IF POS(N0$=K$)<>1 THEN GOTO 4000
1200 REM " --- By Warehouse: Valid Warehouse/Product/Item?"
1210 IF ENDWHSE$<>"" THEN IF K$(3,2)>ENDWHSE$ THEN GOTO 4000
1220 IF BEGITEM$<>"" THEN IF K$(5,P[0])<BEGITEM$(1,P[0]) THEN GOTO 1300
1230 IF ENDITEM$<>"" THEN IF K$(5,P[0])>ENDITEM$(1,P[0]) THEN GOTO 1350
1240 IF BEGLOT$<>"" THEN IF K$(25,P[2])<BEGLOT$(1,P[2]) THEN GOTO 1400
1250 IF ENDLOT$<>"" THEN IF K$(25,P[2])>ENDLOT$(1,P[2]) THEN GOTO 1450
1290 GOTO 1500
1300 REM " --- Skip to beginning item for this warehouse'
1310 READ (IVM07_DEV,KEY=K$(1,4)+BEGPROD$,DOM=3900)
1340 GOTO 3900
1350 REM " --- Skip to next warehouse"
1360 READ (IVM07_DEV,KEY=K$(1,4)+$FF$,DOM=3900)
1390 GOTO 3900
1400 REM " --- Skip to beginning lot/serial number"
1410 READ (IVM07_DEV,KEY=K$(1,24)+$FF$,DOM=3900)
1440 GOTO 3900
1450 REM " --- Skip to next item"
1460 READ (IVM07_DEV,KEY=K$(1,24)+$FF$,DOM=3900)
1490 GOTO 3900
1500 REM " --- Level breaks?"
1510 READ (IVM07_DEV)IOL=IVM07A
1520 IF STATUS$="O" THEN IF B[0]=0 THEN GOTO 3900
1530 IF STATUS$="C" THEN IF B[0]<>0 THEN GOTO 3900
1540 IF K$(3,2)<>T0$ THEN GOSUB 6000
1550 IF K$(5,P[0])<>T1$ THEN GOSUB 6200
1560 IF TYPE$<>"" THEN IF A2$(53,3)<>TYPE$ THEN GOTO 3900
1590 GOTO 2000
1600 REM " --- Read next lot/serial xref"
1610 LET K$=KEY(IVM08_DEV,END=4000)
1620 IF POS(N0$=K$)<>1 THEN GOTO 4000
1630 IF ENDLOT$<>"" THEN IF K$(3,P[2])>ENDLOT$(1,P[2]) THEN GOTO 4000
1640 IF BEGWHSE$<>"" THEN IF K$(23,2)<BEGWHSE$ THEN GOTO 1700
1650 IF ENDWHSE$<>"" THEN IF K$(23,2)>ENDWHSE$ THEN GOTO 1750
1660 IF BEGITEM$<>"" THEN IF K$(25,P[0])<BEGITEM$(1,P[0]) THEN GOTO 1800
1670 IF ENDITEM$<>"" THEN IF K$(25,P[0])>ENDITEM$(1,P[0]) THEN GOTO 1850
1690 GOTO 1900
1700 REM " --- Skip to beginning warehouse for this lot/serial number"
1710 READ (IVM08_DEV,KEY=K$(1,22)+BEGWHSE$,DOM=3900)
1740 GOTO 3900
1750 REM " --- Skip to next lot/serial number"
1760 READ (IVM08_DEV,KEY=K$(1,22)+$FF$,DOM=3900)
1790 GOTO 3900
1800 REM " --- Skip to beginning item"
1810 READ (IVM08_DEV,KEY=K$(1,24)+$FF$,DOM=3900)
1840 GOTO 3900
1850 REM " --- Skip to next warehouse"
1860 READ (IVM08_DEV,KEY=K$(1,24)+$FF$,DOM=3900)
1890 GOTO 3900
1900 REM " --- Retrieve lot/serial master"
1910 READ (IVM08_DEV)
1920 PRINT @(COLUMN,11),K$(3,P[2]),
1930 LET K$=K$(1,2)+K$(23)+K$(3,20)
1940 LET A0$(1)=N0$+K$(5),A1$(1)="(Not On File)",A2$(1)=""
1950 FIND (IVM07_DEV,KEY=K$,DOM=3900)IOL=IVM07A
1960 IF STATUS$="O" THEN IF B[0]=0 THEN GOTO 3900
1970 IF STATUS$="C" THEN IF B[0]<>0 THEN GOTO 3900
1980 FIND (IVM01_DEV,KEY=A0$,DOM=2000)IOL=IVM01A
2000 REM " --- Retrieve item and activity"
2010 DIM ITEM$[3],ACTIVITY[12]
2020 LET ACTION$="L",ITEM$[0]=N0$,ITEM$[1]=K$(3,2)
2030 LET ITEM$[2]=K$(5,20),ITEM$[3]=K$(25)
2080 CALL "IVC.WB",ACTION$,FILES[ALL],ITEM$[ALL],PERIOD$[ALL],ACTIVITY[ALL],STATUS
2090 CALL "IVC.PA",SELECTION$,ACTIVITY[ALL],BALANCE
2500 REM " --- Format detail line fields"
2510 DIM VENDOR$(VW)
2520 LET RECEIPT$=FNL$(B1$(64,3),B1$(67,3))
2530 LET ISSUED$=FNL$(B1$(70,3),B1$(73,3))
2540 IF FNP$(RECEIPT$)<>"" THEN LET RECEIPT$=FNB$(RECEIPT$)
2550 IF FNP$(ISSUED$)<>"" THEN LET ISSUED$=FNB$(ISSUED$)
2560 IF AP$="N" THEN GOTO 3000
2570 LET C0$(1)=N0$+B1$(55,6)
2580 FIND (APM01_DEV,KEY=C0$,DOM=3000)IOL=APM01A
2590 LET VENDOR$(1)=FNF$(C0$(3,I[0]),M0$)+" "+C1$
3000 REM " --- Print detail line by item"
3010 IF SEQUENCE$="L" THEN GOTO 3500
3020 LET NEEDED=2
3030 IF H9$<>"" THEN LET NEEDED=NEEDED+2
3040 IF L+NEEDED>L9 THEN GOSUB 5000
3050 IF H9$<>"" THEN GOSUB 5400
3060 PRINT (7)@(O[1]),B0$(25,P[2]),@(O[3]),RECEIPT$,@(O[4]),ISSUED$,@(O[5]),VENDOR$,@(O[8]),ACTIVITY[0]:M2$,@(O[9]),ACTIVITY[1]:M2$,@(O[10]),ACTIVITY[3]:M2$,@(O[11]),ACTIVITY[5]:M2$,@(O[12]),ACTIVITY[7]:M2$,@(O[13]),BALANCE:M2$
3070 PRINT (7)@(O[9]),ACTIVITY[2]:M2$,@(O[10]),ACTIVITY[4]:M2$,@(O[11]),ACTIVITY[6]:M2$,@(O[12]),ACTIVITY[8]:M2$
3090 LET L=L+2,L1=L1+1,NUMBER=NUMBER+1
3200 REM " --- Accumulate item totals"
3210 FOR X=0 TO 11
3220 LET ITEM[X]=ITEM[X]+ACTIVITY[X]
3230 NEXT X
3240 LET ITEM[12]=ITEM[12]+BALANCE
3290 GOTO 3900
3500 REM " --- Print detail line by lot/serial number"
3510 DIM DESCRIPTION$(DW)
3520 IF L+2>L9 THEN GOSUB 5000
3530 LET L=L+2,DESCRIPTION$(1)=FNITEM$(A1$,P[3],P[4],P[5])
3540 PRINT (7)@(O[0]),B0$(25,P[2]),@(O[3]),RECEIPT$,@(O[5]),ISSUED$,@(O[6]),VENDOR$,@(O[8]),ACTIVITY[0]:M2$,@(O[9]),ACTIVITY[1]:M2$,@(O[10]),ACTIVITY[3]:M2$,@(O[11]),ACTIVITY[5]:M2$,@(O[12]),ACTIVITY[7]:M2$,@(O[13]),BALANCE:M2$
3570 PRINT (7)@(O[1]),B0$(3,2),@(O[2]),A0$(3,P[0]),@(O[4]),DESCRIPTION$,@(O[7]),A2$(1,3),@(O[9]),ACTIVITY[2]:M2$,@(O[10]),ACTIVITY[4]:M2$,@(O[11]),ACTIVITY[6]:M2$,@(O[12]),ACTIVITY[8]:M2$
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF NUMBER>0 THEN GOTO 9900
4020 GOSUB 5000
4030 PRINT (7)"No Lot/Serial Number Activity Found"
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+4
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 REM " --- Subheading by item"
5110 IF SEQUENCE$="L" THEN GOTO 5200
5120 PRINT (7)@(O[3]),"Received",@(O[4]),"Sold Or",@(O[8]+M2-10),"Beginning",@(O[9]+M2-9),"Received",@(O[10]+M2-7),"Sold &",@(O[11]+M2-11),"Adjustments",@(O[12]+M2-9),"Transfers",@(O[13]+M2-8),"Ending"
5130 PRINT (7)@(O[0]),"Item",@(O[1]),"Lot/Serial #",@(O[3]),"Or Built",@(O[4]),"Issued",@(O[5]),VH$,@(O[7]),"Prod",@(O[8]+M2-10),"Balance",@(O[9]+M2-9),"& Built",@(O[10]+M2-7),"Issued",@(O[11]+M2-11),"& Returns",@(O[12]+M2-9),"In & Out",@(O[13]+M2-8),"Balance"
5140 PRINT (7)""
5190 GOTO 5290
5200 REM " --- Subheading by lot/serial number"
5220 PRINT (7)@(O[0]),"Lot/Serial #",@(O[3]),"Received",@(O[5]),"Sold Or",@(O[8]+M2-10),"Beginning",@(O[9]+M2-9),"Received",@(O[10]+M2-7),"Sold &",@(O[11]+M2-11),"Adjustments",@(O[12]+M2-9),"Transfers",@(O[13]+M2-8),"Ending"
5230 PRINT (7)@(O[1]),"Wh",@(O[2]),"Item",@(O[3]),"Or Built",@(O[5]),"Issued",@(O[6]),VH$,@(O[7]),"Prod",@(O[8]+M2-10),"Balance",@(O[9]+M2-9),"& Built",@(O[10]+M2-7),"Issued",@(O[11]+M2-11),"& Returns",@(O[12]+M2-9),"In & Out",@(O[13]+M2-8),"Balance"
5240 PRINT (7)""
5290 RETURN 
5400 REM " --- Item subheading"
5410 IF L+2>L9 THEN GOSUB 5000
5420 PRINT (7)H9$
5430 PRINT (7)""
5440 LET L=L+2,H9$=""
5490 RETURN 
6000 REM " --- Warehouse Break"
6010 IF T0$="" THEN GOTO 6100
6020 GOSUB 6200
6100 DIM X0$(32)
6120 LET T0$=K$(3,2),X0$(6,24)="(Not on File)",L=L9+1
6130 FIND (IVM10_DEV,KEY=N0$+"C"+T0$,DOM=6140)IOL=IVM10C
6140 LET HEADINGS$[2]="Warehouse "+T0$+" "+FNP$(X0$(6,24))+" For "+H8$
6190 RETURN 
6200 REM " --- Item Break"
6210 IF T1$="" THEN GOTO 6300
6220 IF L1<1 THEN GOTO 6300
6230 IF L+2>L9 THEN GOSUB 5000
6240 LET X$="Total For Item "+T1$,L=L+2
6260 PRINT (7)@(O[8]-LEN(X$)),X$,@(O[8]),ITEM[0]:M2$,@(O[9]),ITEM[1]:M2$,@(O[10]),ITEM[3]:M2$,@(O[11]),ITEM[5]:M2$,@(O[12]),ITEM[7]:M2$,@(O[13]),ITEM[12]:M2$
6270 PRINT (7)@(O[9]),ITEM[2]:M2$,@(O[10]),ITEM[4]:M2$,@(O[11]),ITEM[6]:M2$,@(O[12]),ITEM[8]:M2$
6300 DIM H9$(O[7]+3),DESCRIPTION$(DW),ITEM[12]
6310 LET T1$=K$(5,P[0]),A0$(1)=N0$+T1$,A1$(1)="(Not On File)",A2$(1)="",L1=0
6320 FIND (IVM01_DEV,KEY=A0$,DOM=6330)IOL=IVM01A
6330 LET DESCRIPTION$(1)=FNITEM$(A1$,P[3],P[4],P[5])
6340 LET H9$(1)=A0$(3,P[0]),H9$(O[2]+1)=DESCRIPTION$,H9$(O[7]+1)=A2$(1,3)
6350 PRINT @(COLUMN,11),K$(3,2)," ",K$(5,P[0]),
6390 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
8300 REM " --- Multi-Line Functions"
8310 DEF FNL$(Q1$,Q2$)
8320 IF CVS(Q1$,2)="" THEN LET Q1$="ZZZ"
8330 IF CVS(Q2$,2)="" THEN LET Q2$="ZZZ"
8340 LET Q$=Q1$
8350 IF Q2$>Q1$ THEN LET Q$=Q2$
8360 IF Q$="ZZZ" THEN LET Q$="   "
8370 RETURN Q$
8380 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

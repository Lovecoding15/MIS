0010 REM "BM Bill of Materials Indented Bills
0020 REM "Program:  BMR.ZA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=11
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="BMM-01",FILES$[2]="BMM-01",FILES$[3]="BMM-02"
0130 LET FILES$[4]="BMM-03",FILES$[5]="BMM-05",FILES$[6]="BMM-08"
0135 LET FILES$[7]="IVM-01",FILES$[8]="IVM-02",FILES$[9]="IVM-10"
0140 LET FILES$[10]="IVM-14",FILES$[11]="SYS-01"
0160 CALL "SYC.DA",1,1,11,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS<>0 THEN GOTO 9900
0175 LET BMM01_DEV=CHANNELS[1],BMM01_DEV2=CHANNELS[2],BMM02_DEV=CHANNELS[3]
0180 LET BMM03_DEV=CHANNELS[4],BMM05_DEV=CHANNELS[5],BMM08_DEV=CHANNELS[6]
0185 LET IVM01_DEV=CHANNELS[7],IVM02_DEV=CHANNELS[8],IVM10_DEV=CHANNELS[9]
0190 LET IVM14_DEV=CHANNELS[10],SYS01_DEV=CHANNELS[11]
0200 REM " --- IOLists
0230 IVM01A: IOLIST D0$,D1$(1)
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 BMS01A: IOLIST *,*,*,*,R0,P9
0320 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0450 FIND (SYS01_DEV,KEY=N0$+"BM00",DOM=9800)IOL=BMS01A
0460 DIM P[2],DESC[3],PARAMS[3]
0470 FOR X=1 TO 3
0480 LET DESC[X]=NUM(P2$((X-1)*2+9,2))
0485 LET PARAMS[X]=DESC[X]
0490 NEXT X
0495 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P4$(1,2)),P[2]=FNYY_YEAR(P4$(3,2))
0497 LET PARAMS[0]=P[0]
0500 REM " --- Init Data
0510 DIM IV_CHANS[14]
0515 LET IV_CHANS[1]=IVM01_DEV,IV_CHANS[14]=IVM14_DEV
0520 LET L9=59,L=L9+1
0525 DIM DASHES$(80,"-")
0530 DIM A[11],B[10],C[11],S[9],D1$(60)
0535 LET M0$=M2$,M1$=M3$
0540 LET M0=LEN(M0$),M1=LEN(M1$)
0545 IF M0*3+M1*4<=64 THEN GOTO 0565
0550 IF M0>8 THEN CALL "SYC.RM",M0$,M0,8,M0-1
0555 IF M1>10 THEN CALL "SYC.RM",M1$,M1,10,M1-1
0560 GOTO 0545
0565 IF M0<6 THEN CALL "SYC.RM",M0$,M0,6,6
0570 IF M1<7 THEN CALL "SYC.RM",M1$,M1,7,7
0575 DIM DESCRIPTION$[1],FIELD[1],POSITION[1],LENGTH[1]
0580 LET NUMBER=1,TITLE$="Warehouse Lookup",RECORD$="C"
0585 LET DESCRIPTION$[0]="Whse",POSITION[0]=4,LENGTH[0]=2
0590 LET DESCRIPTION$[1]="Description",POSITION[1]=6,LENGTH[1]=24
0595 LET FIELD[0]=1,FIELD[1]=1,COLUMN=40,ROW=4
0600 REM " --- Build headings
0610 LET MHEAD$="<"+DASHES$(1,M0+M1-2)+">",LHEAD$=MHEAD$,SHEAD$=MHEAD$,THEAD$="<"+DASHES$(1,M1-1)+">"
0615 LET MHEAD$(INT(LEN(MHEAD$)/2)-3,8)="Material"
0620 LET LHEAD$(INT(LEN(LHEAD$)/2)-1,5)="Labor"
0625 LET SHEAD$(INT(LEN(SHEAD$)/2)-4,11)="Subcontract"
0630 LET THEAD$(INT(LEN(THEAD$)/2)-1,5)="Total"
0640 DIM UCHEAD$(LEN(MHEAD$))
0645 LET UCHEAD$(M0-5,5)="Units",UCHEAD$(LEN(UCHEAD$)-4,4)="Cost"
0700 REM " --- Background
0710 FOR X=4 TO 14 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(23,3),"Level Selection:",@(14,5),"Include Cost Information:",@(17,7),"Beginning Bill Number:",@(20,9),"Ending Bill Number:",@(24,11),"Effective Date:",@(29,13),"Warehouse:",'SF'
1000 REM " --- Options
1005 LET I0=0
1200 REM " --- Level Selection
1210 PRINT 'CF',
1220 LET V0$="Z",V1$="KCE",V2$=P8$,V3$="",V4$="Select Level 1 Through 9 Or <F1> For All Levels",V0=1,V1=40,V2=3
1240 GOSUB 7000
1260 IF V3=4 THEN GOTO 9900
1270 IF V3=1 THEN LET V$=""
1280 LET P8$=V$,LEVEL=NUM(V$,ERR=1200)
1300 IF V$="" THEN LET M8$="All Levels Selected",LEVEL=9 ELSE LET M8$="All Levels Up To "+STR(LEVEL)
1310 IF LEVEL=0 THEN GOTO 1200
1320 PRINT @(V1,V2),'CL',M8$
1390 GOTO 3900
1400 REM " --- Include Cost?
1420 LET V0$="Y",V1$="C",V2$=SHOWCOST$,V3$="YN",V4$="Include Cost Amounts?",V0=1,V1=40,V2=5
1430 IF V2$="" THEN LET V2$="Y"
1440 GOSUB 7000
1445 IF V3=2 THEN GOTO 3900
1450 IF POS(V$="YN")=0 THEN GOTO 1400
1460 LET SHOWCOST$=V$
1490 GOTO 3900
1600 REM " --- Starting Bill
1605 BILLFROM: 
1620 DIM ITEM$[6],ITEM[12]
1630 LET ACTION$="N",DEFAULT$=BILLFROM$,V1=40,V2=7
1640 LET V4$="Enter A Valid Bill Number, <F1>=First, <F3>=Lookup"
1650 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1660 IF V3=2 THEN GOTO NEXT_BILLFROM
1670 IF V3=4 THEN GOTO 9900
1675 F3_BILLFROM: 
1680 IF V3<>3 THEN GOTO FIRST_BILLFROM
1690 CALL "SYC.LB",V$
1700 GOSUB GET_DESC
1710 IF V$="" THEN GOTO BILLFROM
1720 LET BILLFROM$=V$
1730 GOTO NEXT_BILLFROM
1740 FIRST_BILLFROM: 
1745 IF V3<>1 AND ITEM$[0]<>"" THEN GOTO IVCIA_BILLFROM
1750 PRINT @(V1,V2),'CL',"First"
1760 LET BILLFROM$=""
1765 GOTO NEXT_BILLFROM
1770 IVCIA_BILLFROM: 
1775 FIND (BMM01_DEV,KEY=N0$+ITEM$[0],DOM=BILLFROM)
1780 LET BILLFROM$=ITEM$[0],ITEM$[1]=ITEM$[1]+FILL(28)
1785 PRINT @(V1,V2),ITEM$[0](1,P[0])," ",ITEM$[1](1,28),
1790 NEXT_BILLFROM: 
1795 GOTO 3900
1800 REM " --- Ending Bill
1805 BILLTHRU: 
1810 DIM ITEM$[6],ITEM[12]
1820 LET ACTION$="N",DEFAULT$=BILLTHRU$,V1=40,V2=9,V$=""
1830 LET V4$="Enter A Valid Bill Number, <F1>=Last, <F3>=Lookup"
1840 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1850 IF V3=2 THEN GOTO NEXT_BILLTHRU
1860 IF V3=4 THEN GOTO 9900
1865 F3_BILLTHRU: 
1870 IF V3<>3 THEN GOTO LAST_BILLTHRU
1880 CALL "SYC.LB",V$
1885 IF V$="" OR V$<BILLFROM$ THEN GOTO BILLTHRU
1890 GOSUB GET_DESC
1900 GOTO ASSIGN_V
1905 LAST_BILLTHRU: 
1910 IF V3<>1 AND ITEM$[0]<>"" THEN GOTO IVCIA_BILLTHRU
1920 PRINT @(V1,V2),'CL',"Last"
1930 LET V$=""
1940 ASSIGN_V: 
1950 LET BILLTHRU$=V$
1955 GOTO NEXT_BILLTHRU
1960 IVCIA_BILLTHRU: 
1965 FIND (BMM01_DEV,KEY=N0$+ITEM$[0],DOM=BILLTHRU)
1970 IF ITEM$[0]<BILLFROM$ THEN GOTO BILLTHRU
1975 LET BILLTHRU$=ITEM$[0],ITEM$[1]=ITEM$[1]+FILL(28)
1980 PRINT @(V1,V2),'CL',ITEM$[0](1,P[0])," ",ITEM$[1](1,28),
1985 NEXT_BILLTHRU: 
1990 GOTO 3900
2200 REM " --- Effective Date
2210 IF PDATE$="" THEN LET PDATE$=F0$(7,3)
2220 LET V0$="D",V1$="CER",V2$=PDATE$,V3$="",V4$="Enter Effective Date",V0=8,V1=40,V2=11
2230 GOSUB 7000
2240 IF V3=2 THEN GOTO 2290
2280 LET PDATE$=V$
2290 GOTO 3900
2300 REM " --- Warehouse
2320 LET V0$="S",V1$="KC",V2$=WHS$,V3$="",V4$="<F3> To Lookup",V0=2,V1=40,V2=13
2330 GOSUB 7000
2340 IF V3=2 THEN GOTO 2390
2350 IF V3=3 THEN CALL "SYC.LK",IVM10_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
2360 IF V$="" THEN GOTO 2300
2370 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=2300)
2375 PRINT @(V1,V2),V$,
2380 LET WHS$=V$
2390 GOTO 3900
3900 REM " --- Next Field
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1400,1600,1800,2200,2300,4000
4000 REM " --- Overlay
4010 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4060 IF V$="N" THEN GOTO 1000
4080 IF V$<>"Y" THEN GOTO 4000
4100 RUN "BMR.ZB"
6100 REM " --- Find Bill Description
6105 GET_DESC: 
6110 DIM X0$(60)
6120 LET V$=V$+FILL(20-LEN(V$))
6130 FIND (BMM01_DEV,KEY=N0$+V$,DOM=6180)
6140 FIND (IVM01_DEV,KEY=N0$+V$,DOM=6180)IOL=IVM01A
6150 LET X0$(1)=FNITEM$(D1$,DESC[1],DESC[2],DESC[3])
6160 PRINT @(V1,V2),'CL',V$(1,P[0])," ",X0$(1,38-P[0]),
6170 GOTO 6190
6180 LET V$=""
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

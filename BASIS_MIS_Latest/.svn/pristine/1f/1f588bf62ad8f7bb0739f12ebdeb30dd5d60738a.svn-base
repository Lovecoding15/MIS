0010 REM "SYX - SpeedInstall Utility (Install/Remove)"
0020 REM "Program SYX.JC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 CLOSE (SOURCE_DEV,ERR=0120)
0120 CLOSE (TARGET_DEV,ERR=0130)
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 SYM99A: IOLIST A0$,A1$(1),A2$,A[0],A[1],A[2],A[3],A[4]
0220 SYM03A: IOLIST B0$,B1$(1),B2$,B[0],B[1],B[2]
0400 REM " --- Parameters"
0500 REM " --- Initializations"
0510 DIM A0$(7),A1$(64),A[4],B1$(77),B[2]
0520 LET RECS=0,NUM_F=0,NUM_I=0,NUM_R=0,T0$="",T1$=""
0530 LET L9=59,L=L9+1,HEADINGS=3,WHEN$=F0$(7,3),CLOCK$="",WIDTH=80,PAGE=0,DONE=0
0540 LET HEADINGS=3,MAXKEY=40
0550 DIM HEADINGS$[HEADINGS],J1$(WIDTH)
0560 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0570 IF I_R$="I" THEN LET HEADINGS$[2]="Installing In Firm "+TARGET$,H0$="Installing",H1$="Installed" ELSE LET HEADINGS$[2]="Removing From Firm "+TARGET$,H0$="Removing",H1$="Removed"
0600 REM " --- Print Positions"
0610 DIM O[3]
0620 LET O[0]=0,O[1]=6,O[2]=10,O[3]=WIDTH-1-MAXKEY
0700 REM " --- Background"
0710 LET BOXSIZE=MAXKEY+11,X_BOX=INT((80-BOXSIZE)/2),Y_BOX=L0+3,BOX_COL0=X_BOX+9,BOX_COL1=X_BOX+16
0720 CALL "SYC.WC",0,0,BOXSIZE,5,X_BOX,Y_BOX,0
0730 PRINT 'SB',@(INT((80-LEN(H0$)-2)/2),Y_BOX)," ",H0$," ",
0740 PRINT @(X_BOX+1,Y_BOX+1),J1$(1,BOXSIZE-2),@(X_BOX+4),"File",
0750 PRINT @(X_BOX+1,Y_BOX+2),J1$(1,BOXSIZE-2),@(X_BOX+2),"Record",
0760 PRINT @(X_BOX+1,Y_BOX+3),J1$(1,BOXSIZE-2),@(X_BOX+5),"Key",'SF',
0900 REM " --- Position SYM-99"
0990 READ (SYM99_DEV,KEY="",DOM=1000)
1000 REM " --- Get Next SYM-99 Record"
1010 LET K$=KEY(SYM99_DEV,END=4000)
1020 READ (SYM99_DEV,KEY=K$)IOL=SYM99A
1030 IF A1$(32,1)<>"Y" THEN GOTO 1000
1040 DIM INF0$[20]
1050 CALL "SYC.VA",A1$(33,2),INFO$[ALL]
1060 IF INFO$[3]<>"Y" THEN GOTO 1000
1200 REM " --- Level breaks"
1210 IF K$(1,6)<>T0$ THEN GOSUB 6000
1220 IF K$(1,7)<>T1$ THEN GOSUB 6200
1230 IF SFILE$<>"Y" THEN GOTO 1000
2000 REM " --- Position Source File"
2020 LET SK0$=SOURCE$
2040 IF A1$(31,1)="Y" THEN LET SK0$=SK0$+A0$(7,1)
2060 READ (SOURCE_DEV,KEY=SK0$,DOM=2100)
2080 LET SK$=SK0$
2090 GOTO 2120
2100 REM " --- Install/Remove Target Record"
2110 LET SK$=KEY(SOURCE_DEV,END=3000)
2120 IF POS(SK0$=SK$)<>1 THEN GOTO 3000
2130 READ RECORD(SOURCE_DEV,KEY=SK$,DOM=2900)S$
2140 LET TK$=TARGET$+SK$(3),T$=TARGET$+S$(3)
2150 IF I_R$="I" THEN WRITE RECORD(TARGET_DEV,KEY=TK$)T$ ELSE REMOVE (TARGET_DEV,KEY=TK$,DOM=2160)
2160 PRINT @(BOX_COL0,Y_BOX+3),SK$(LEN(SK0$)+1),
2170 IF L+1>L9 THEN GOSUB 5000; GOSUB 5200; GOSUB 5400
2180 PRINT (7)@(O[3]),SK$(LEN(SK0$)+1)
2190 LET L=L+1,NUM_I=NUM_I+1,RECS=RECS+1
2900 REM " --- Reset Source File Pointer In Case Of File Expansion"
2910 READ (SOURCE_DEV,KEY=SK$,DOM=2920)
2950 REM " --- Next Source Record"
2990 GOTO 2100
3000 REM " --- Next SYM-99 Record"
3010 IF RECS=0 THEN GOTO 3090
3020 LET RECS=0
3030 IF L+1>L9 THEN GOTO 3090
3040 PRINT (7)'LF',
3050 LET L=L+1
3090 GOTO 1000
4000 REM " --- All Done"
4010 LET DONE=1
4020 GOSUB 6200; GOSUB 6000
4030 IF L+3>L9 THEN GOSUB 5000
4050 PRINT (7)@(O[0]),NUM_F:"0000"," Files Processed"
4060 PRINT (7)@(O[0]),NUM_R:"0000"," Record ID's Processed"
4070 PRINT (7)@(O[0]),NUM_I:"0000"," Individual Records ",H1$
4100 REM " --- Return To Header Overlay"
4110 PRINT @(0,L0),'CE',
4120 RUN "SYX.JA"
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5050 PRINT (7)@(O[0]),"File",@(O[1]),"Rec",@(O[2]),"Description",@(O[3]),"Source Key Value",'LF'
5060 LET L=L+2
5090 RETURN 
5200 REM " --- File Heading"
5210 IF L+5>L9 THEN GOSUB 5000
5220 PRINT (7)@(O[0]),A0$(1,6),@(O[2]),FDESC$,'LF'
5230 LET L=L+2
5290 RETURN 
5400 REM " --- Record Heading"
5410 IF L+3>L9 THEN GOSUB 5000; GOSUB 5200
5420 PRINT (7)@(O[1]),A0$(7,1),@(O[2]),RDESC$,'LF'
5430 LET L=L+2
5490 RETURN 
6000 REM " --- File Break"
6020 IF T0$="" THEN GOTO 6100
6040 PRINT @(BOX_COL0,Y_BOX+2),J1$(1,MAXKEY),
6050 PRINT @(BOX_COL0,Y_BOX+3),J1$(1,MAXKEY),
6060 CLOSE (SOURCE_DEV,ERR=6070)
6070 CLOSE (TARGET_DEV,ERR=6080)
6100 REM 
6110 IF DONE>0 THEN GOTO 6190
6120 LET T0$=A0$(1,6),SFILE$="N",B1$(1)="Not On File"
6130 READ (SYM03_DEV,KEY=A0$(1,6),DOM=6140)IOL=SYM03A
6140 LET FILES$[5]=T0$,FILES$[6]=T0$
6145 CALL "SYC.DA",1,5,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
6150 LET SOURCE_DEV=CHANNELS[5],TARGET_DEV=CHANNELS[6]
6155 IF STATUS=0 THEN LET SFILE$="Y"
6160 IF SFILE$="Y" THEN LET FDESC$=B1$(1,30) ELSE LET FDESC$=WARN$
6170 PRINT @(BOX_COL0,Y_BOX+1),A0$(1,6),@(BOX_COL1),FDESC$,
6180 GOSUB 5200
6185 LET NUM_F=NUM_F+1
6190 RETURN 
6200 REM " --- Record Break"
6220 IF T1$="" THEN GOTO 6300
6240 PRINT @(BOX_COL0,Y_BOX+3),J1$(1,MAXKEY),
6300 REM 
6310 IF DONE>0 THEN GOTO 6390
6320 LET T1$=A0$(1,7)
6340 IF SFILE$="Y" THEN LET RDESC$=A1$(1,30) ELSE LET RDESC$=WARN$
6350 PRINT @(BOX_COL0,Y_BOX+2),A0$(7,1),@(BOX_COL1),RDESC$,
6360 GOSUB 5400
6380 LET NUM_R=NUM_R+1
6390 RETURN 
8000 REM " --- Functions"
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "APM - BANK NAME AND ADDRESS"
0020 REM "Program APM.EG"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0230 APM13A: IOLIST C0$,C1$(1),C[ALL]
0500 REM " --- Init Data"
0510 DIM C1$(180)
0700 REM " --- Background"
0730 PRINT 'SB',@(6,7),"Account Name:",@(9,9),"Bank Name:",@(6,10),"Bank Address:",@(18,11),":",@(18,12),":",@(4,14),"Account Number:",'SF',
0900 REM " --- Init File"
0920 LET C0$=A0$
0930 DIM C1$(190),C[3]
0950 READ (APM13_DEV,KEY=C0$,DOM=0990)IOL=APM13A
0960 GOSUB 5000
0970 GOTO 4000
2000 REM " --- Account Name"
2010 LET V4$="Enter Up To Thirty Characters"
2020 LET V0$="S",V1$="",V2$=C1$(13,30),V0=30,V1=20,V2=7,I0=0
2040 GOSUB 7000
2060 LET C1$(13,30)=V$
2090 GOTO 3900
2100 REM " --- Bank Name
2110 LET V4$="Enter Up To Thirty Characters"
2120 LET V0$="S",V1$="",V2$=C1$(58,30),V0=30,V1=20,V2=9
2140 GOSUB 7000
2160 LET C1$(58,30)=V$
2190 GOTO 3900
2200 REM " --- Bank Address 1"
2210 LET V4$="Enter Up To Thirty Characters"
2220 LET V0$="S",V1$="",V2$=C1$(88,30),V0=30,V1=20,V2=10
2240 GOSUB 7000
2260 LET C1$(88,30)=V$
2290 GOTO 3900
2300 REM " --- Bank Address 2"
2310 LET V4$="Enter Up To Thirty Characters"
2320 LET V0$="S",V1$="",V2$=C1$(118,30),V0=30,V1=20,V2=11
2340 GOSUB 7000
2360 LET C1$(118,30)=V$
2390 GOTO 3900
2400 REM " --- Bank Address 3"
2410 LET V4$="Enter Up To Thirty Characters"
2420 LET V0$="S",V1$="",V2$=C1$(148,30),V0=30,V1=20,V2=12
2440 GOSUB 7000
2460 LET C1$(148,30)=V$
2490 GOTO 3900
2500 REM " --- Account Number"
2510 LET V4$="Enter the Bank-Assigned Account Number"
2520 LET V0$="S",V1$="",V2$=C1$(43,15),V0=15,V1=20,V2=14
2540 GOSUB 7000
2560 LET C1$(43,15)=V$
2590 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,2300,2400,2500,3990
4000 REM " --- Options"
4005 LET I0=0
4010 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 WRITE (APM13_DEV,KEY=C0$)IOL=APM13A
4180 LET O0=1
4190 RUN "APM.EA"
4200 REM " --- Delete"
4210 CALL "SYC.YN",1,"Do You Wish To Delete All Information For This Account",1,V$,V3
4230 ON POS(V$="YN") GOTO 4200,4300,4180
4300 PRINT @(10,18),"Deleting All Information For This Account. Please Wait...",
4390 READ (APM11_DEV,KEY=A0$(1,2+P[4]),DOM=4400)
4400 REM " --- Get Next APM-11"
4420 LET K$=KEY(APM11_DEV,END=4490)
4440 IF K$(1,2+P[4])<>A0$(1,2+P[4]) THEN GOTO 4490
4460 READ (APM11_DEV)
4470 REMOVE (APM11_DEV,KEY=K$,DOM=4480)
4480 GOTO 4400
4490 READ (APM12_DEV,KEY=A0$(1,2+P[4]),DOM=4500)
4500 REM " --- Get Next APM-12"
4520 LET K$=KEY(APM12_DEV,END=4590)
4540 IF K$(1,2+P[4])<>A0$(1,2+P[4]) THEN GOTO 4590
4560 READ (APM12_DEV)
4570 REMOVE (APM12_DEV,KEY=K$,DOM=4580)
4580 GOTO 4500
4600 REM " --- Remove APM-13"
4620 REMOVE (APM13_DEV,KEY=A0$,DOM=4700)
4970 GOSUB 6100
4980 LET O0=0
4990 RUN "APM.EA"
5000 REM " --- Print"
5020 PRINT @(20,7),C1$(13,30),@(20,9),C1$(58,30),@(20,10),C1$(88,30),@(20,11),C1$(118,30),@(20,12),C1$(148,30),@(20,14),C1$(43,15),
5090 RETURN 
6100 REM " --- Clear Screen"
6120 PRINT @(0,L0),'CE'
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

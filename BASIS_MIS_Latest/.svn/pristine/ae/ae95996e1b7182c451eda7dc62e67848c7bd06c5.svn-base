0010 REM "MPR - Planned Purchase Report (Input Overlay)"
0020 REM "Program MPR.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "19Sep96 JAL: Specific buyer code not stored properly (515)"
0032 REM "30Dec98 WGH: Use Inventory ABC Analysis Factors, and current Period and Year (310,405,422,560-565)
0040 REM 
0080 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=10
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="MPM-01",FILES$[2]="MPW-01",FILES$[3]="APM-01"
0120 LET FILES$[4]="IVM-01",FILES$[5]="SYS-01",FILES$[6]="IVM-02"
0125 LET FILES$[7]="IVM-05",FILES$[8]="IVM-10",FILES$[9]="IVM-03"
0130 LET FILES$[10]="IVM-12"
0145 LET OPTIONS$[2]="C"
0150 CALL "SYC.DA",1,1,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET MPM01_DEV=CHANNELS[1],MPW01_DEV=CHANNELS[2],APM01_DEV=CHANNELS[3]
0165 LET IVM01_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5],IVM02_DEV=CHANNELS[6]
0170 LET IVM05_DEV=CHANNELS[7],IVM10_DEV=CHANNELS[8],IVM03_DEV=CHANNELS[9]
0175 LET IVM12_DEV=CHANNELS[10]
0200 REM " --- IOLIST's"
0210 GLS01A: IOLIST G0$,G1$,G2$
0220 MPM01A: IOLIST S0$(1),S1$(1),S2$,S[ALL]
0240 APM01A: IOLIST C0$(1),C1$(1)
0250 IVM01A: IOLIST D0$(1),D1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,X$,M1$,M2$,M3$
0280 APS01A: IOLIST R0$,R1$,R2$,R3$,R4$,M0$
0300 IVM10C: IOLIST X0$(1)
0305 IVM10F: IOLIST X0$(1)
0310 IVS01C: IOLIST *,ABCP[ALL],FACTORS[ALL]
0400 REM " --- Parameters"
0405 DIM P[6],R[6],ABCP[25],FACTORS[25]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N3$=F5$
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0422 FIND (SYS01_DEV,KEY=N0$+N2$+"02",DOM=9800)IOL=IVS01C
0425 LET P[0]=NUM(P2$(1,2)),IVS01_PRECISION=NUM(P2$(5,1)),NUMBER=1
0430 LET P[2]=NUM(P2$(9,2)),P[3]=NUM(P2$(11,2)),P[4]=NUM(P2$(13,2))
0435 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0440 LET R[0]=NUM(R2$(1,2))
0445 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9900)IOL=GLS01A
0500 REM " --- Initializations"
0505 PRECISION IVS01_PRECISION
0510 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
0515 DIM FORECAST$(3),WHSE$(2),BEGVEND$(6),ENDVEND$(6),BUYER$(3)
0520 DIM BEGITEM$(P[0]),ENDITEM$(P[0]),J0$(20,"0")
0525 LET WHSE$(1)=P3$(6,2),BEGPER=0,ENDPER=0,M4$=M2$,MP=26
0530 LET INCLUDE$="N",ALLBUYERS=1
0535 LET HEADINGS=4
0540 DIM HEADINGS$[HEADINGS]
0545 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,L9=59,L=L9+1
0550 LET CLOCK$="",WHEN$=F0$(7,3),PAGE=0,WIDTH=132
0555 LET MAX_PERIOD$=G2$(1,2)
0560 LET CURR_PERIOD$=P4$(1,2)
0565 LET CURR_YEAR$=P4$(3,2)
0600 REM 
0610 LET M4$=M2$,M5$="###0-",M0=LEN(M0$),M5=LEN(M5$)
0620 CALL "SYC.RM",M1$,M1,8,15
0625 LET X=M1
0630 CALL "SYC.RM",M2$,M2,8,10
0640 CALL "SYC.RM",M3$,M3,8,10
0650 CALL "SYC.RM",M4$,M4,X,M1
0700 REM " --- Background"
0710 FOR X=4 TO 20 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(25,3),"Forecast Type:",@(24,5),"Warehouse Code:",@(22,7),"Beginning Vendor:",@(25,9),"Ending Vendor:",@(13,11),"Include Alternate Vendors:",@(28,13),"Buyer Code:",@(24,15),"Beginning Item:",@(27,17),"Ending Item:",@(27,19),"From Period:",@(24,21),"Through Period:",'SF',
0900 REM 
0910 READ (MPM01_DEV,KEY=N0$,DOM=0920)
0920 LET K$=KEY(MPM01_DEV,END=0990)
0930 IF POS(N0$=K$)<>1 THEN GOTO 0990
0940 LET FORECAST$(1)=K$(3,3)
0990 GOSUB 5000
1000 REM " --- Forecast Type"
1010 LET V0$="S",V1$="CE",V2$=FORECAST$,V0=3,V1=40,V2=3,I0=0
1020 LET V4$="Enter A Valid Forecast Type (<F3>=Lookup)"
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1045,3900
1045 GOSUB 6000
1050 GOSUB 5200
1060 IF STATUS<>0 THEN GOTO 1000
1070 LET FORECAST$(1)=V$
1090 GOTO 3900
1200 REM " --- Warehouse"
1210 LET V0$="S",V1$="C",V2$=WHSE$,V3$="",V0=2,V1=40,V2=5
1220 LET V4$="Enter A Valid Warehouse Code (<F3>=Lookup)"
1230 GOSUB 7000
1235 ON V3 GOTO 1250,1200,3900,1240,3900
1240 GOSUB 6100
1250 GOSUB 5300
1260 IF STATUS<>0 THEN GOTO 1200
1280 LET WHSE$(1)=V$
1290 GOTO 3900
1400 REM " --- Beginning Vendor"
1410 LET V0$="M",V1$="C",V2$=BEGVEND$(1,R[0]),V3$=M0$,V0=6,V1=40,V2=7
1420 LET V4$="Enter A Valid Vendor Number (<F1>=First/<F3>=Lookup)"
1430 GOSUB 7000
1435 IF V$<>"" THEN IF V$(1,LEN(V$))=J0$(1,LEN(V$)) THEN LET V$=""
1440 ON V3 GOTO 1470,1445,3900,1455,3900
1445 LET V$=""
1450 GOTO 1470
1455 CALL "SYC.LV",V$
1460 IF V$="" THEN GOTO 1400
1470 GOSUB 5400
1475 IF STATUS<>0 THEN GOTO 1400
1480 LET BEGVEND$(1)=V$
1490 GOTO 3900
1600 REM " --- Ending Vendor"
1610 LET V0$="M",V1$="C",V2$=ENDVEND$(1,R[0]),V3$=M0$,V0=6,V1=40,V2=9
1620 LET V4$="Enter A Valid Vendor Number (<F1>=Last/<F3>=Lookup)"
1630 GOSUB 7000
1635 IF V$<>"" THEN IF V$(1,LEN(V$))=J0$(1,LEN(V$)) THEN LET V$=""
1640 ON V3 GOTO 1670,1645,3900,1655,3900
1645 LET V$=""
1650 GOTO 1670
1655 CALL "SYC.LV",V$
1660 IF V$="" THEN GOTO 1600
1670 GOSUB 5400
1675 IF STATUS<>0 THEN GOTO 1600
1680 LET ENDVEND$(1)=V$
1690 GOTO 3900
1800 REM " --- Include Alternate Vendors?"
1810 LET V0$="Y",V1$="C",V2$=INCLUDE$,V3$="",V0=1,V1=40,V2=11
1820 LET V4$="Include Alternate Vendors On The Report (Y/N)?"
1830 GOSUB 7000
1840 ON V3 GOTO 1850,1800,3900,1800,3900
1850 LET INCLUDE$=V$
1860 PRINT @(V1,V2),INCLUDE$,'CL',
1890 GOTO 3900
2000 REM " --- Buyer Code"
2010 LET V0$="S",V1$="C",V2$=BUYER$,V3$="",V0=3,V1=40,V2=13
2020 LET V4$="Enter A Valid Buyer Code (<F1>=All/<F3>=Lookup)"
2030 GOSUB 7000
2040 ON V3 GOTO 2060,2045,3900,2055,3900
2045 LET V$="",ALLBUYERS=1
2047 GOSUB 5500
2050 GOTO 3900
2055 GOSUB 6200
2060 LET ALLBUYERS=0
2070 GOSUB 5500
2075 IF STATUS<>0 THEN GOTO 2000
2080 LET BUYER$(1)=V$
2090 GOTO 3900
2200 REM " --- Beginning Item"
2210 LET V0$="S",V1$="C",V2$=BEGITEM$,V3$="",V0=P[0],V1=40,V2=15
2220 LET V4$="Enter An Item Number (<F1>=First/<F3>=Lookup)"
2230 GOSUB 7000
2240 ON V3 GOTO 2270,2245,3900,2255,3900
2245 LET V$=""
2250 GOTO 2270
2255 CALL "SYC.LI",V$
2260 IF V$="" THEN GOTO 2200
2270 GOSUB 5600
2280 LET BEGITEM$(1)=V$
2290 GOTO 3900
2400 REM " --- Ending Item"
2410 LET V0$="S",V1$="C",V2$=ENDITEM$,V3$="",V0=P[0],V1=40,V2=17
2420 LET V4$="Enter An Item Number (<F1>=Last/<F3>=Lookup)"
2430 GOSUB 7000
2440 ON V3 GOTO 2470,2445,3900,2455,3900
2445 LET V$=""
2450 GOTO 1470
2455 CALL "SYC.LI",V$
2460 IF V$="" THEN GOTO 2400
2470 GOSUB 5600
2480 LET ENDITEM$(1)=V$
2490 GOTO 3900
2600 REM " --- Beginning Period"
2610 LET V0$="N",V1$="C",V2$=STR(BEGPER),V3$="##",V0=2,V1=40,V2=19
2620 LET V4$="Enter The Beginning Period To Print (1 To "+STR(S[0])+")"
2630 GOSUB 7000
2640 ON V3 GOTO 2650,2600,3900,2600,3900
2650 IF V<1 OR V>S[0] THEN GOTO 2600
2660 LET BEGPER=V
2670 PRINT @(V1,V2),BEGPER:"#0",
2690 GOTO 3900
2800 REM " --- Ending Period"
2810 LET V0$="N",V1$="C",V2$=STR(ENDPER),V3$="##",V0=2,V1=40,V2=21
2820 LET V4$="Enter The Ending Period To Print ("+STR(BEGPER)+" To "+STR(S[0])+")"
2830 GOSUB 7000
2840 ON V3 GOTO 2850,2800,3900,2800,3900
2850 IF V<BEGPER OR V>S[0] THEN GOTO 2800
2860 LET ENDPER=V
2870 PRINT @(V1,V2),ENDPER:"#0",
2890 GOTO 3900
3900 REM " --- Next Field "
3905 IF V3=4 THEN GOSUB 5000; GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,2200,2400,2600,2800,4000
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4060,4000,4000,4000,9900
4060 ON POS(V$="YN") GOTO 4000,4070,1000
4070 GOSUB 5000
4090 RUN "MPR.AB"
5000 REM " --- Display"
5010 LET V1=40,V2=3,V$=FORECAST$
5015 GOSUB 5200
5020 LET H5$="Forecast: "+X$,V2=5,V$=WHSE$
5030 GOSUB 5300
5040 LET H6$="Warehouse: "+X$,V2=7,V$=FNP$(BEGVEND$)
5050 GOSUB 5400
5060 LET H3$="Beginning Vendor: "+X$,V2=9,V$=FNP$(ENDVEND$)
5070 GOSUB 5400
5080 LET H4$="   Ending Vendor: "+X$
5090 PRINT @(40,11),INCLUDE$,'CL',
5100 LET V2=13,V$=FNP$(BUYER$)
5110 GOSUB 5500
5120 LET H9$="Buyer Code: "+X$,V2=15,V$=FNP$(BEGITEM$)
5130 GOSUB 5600
5140 LET H7$="Beginning Item: "+FNP$(X$),V2=17,V$=FNP$(ENDITEM$)
5150 GOSUB 5600
5160 LET H8$="   Ending Item: "+FNP$(X$),V2=19
5170 GOSUB 5700
5180 LET P9$=X$
5190 RETURN 
5200 REM " --- Forecast"
5210 DIM S0$(5),S1$(30),S[1]
5220 LET STATUS=1,S0$(1)=N0$+V$
5230 FIND (MPM01_DEV,KEY=S0$,DOM=5250)IOL=MPM01A
5240 LET STATUS=0
5250 LET X$=S0$(3,3)+" "+FNP$(S1$)
5255 PRINT @(V1,V2),X$,'CL',
5260 IF S0$(3,3)=FORECAST$ AND BEGPER>0 THEN GOTO 5290
5270 LET BEGPER=1,ENDPER=S[0],V2=19
5280 GOSUB 5700
5290 RETURN 
5300 REM " --- Warehouse"
5310 DIM X0$(32)
5320 LET STATUS=1,X0$(1)=N0$+"C"+V$
5330 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=5350)IOL=IVM10C
5340 LET STATUS=0
5350 LET X$=X0$(4,2)+" "+FNP$(X0$(6,24))
5360 PRINT @(V1,V2),X$,'CL',
5390 RETURN 
5400 REM " --- Vendor"
5410 DIM C0$(8),C1$(30)
5415 IF FNP$(V$)="" THEN GOTO 5470
5420 LET STATUS=1,C0$(1)=N0$+V$
5430 FIND (APM01_DEV,KEY=C0$,DOM=5450)IOL=APM01A
5440 LET STATUS=0
5450 LET X$=FNF$(C0$(3,R[0]),M0$)+" "+C1$(1,30)
5455 PRINT @(V1,V2),X$,'CL',
5460 GOTO 5490
5470 LET STATUS=0,X$="First"
5475 IF V2=9 THEN LET X$="Last"
5480 PRINT @(V1,V2),X$,'CL',
5490 RETURN 
5500 REM " --- Buyer Code"
5510 DIM X0$(32)
5515 IF ALLBUYERS>0 THEN GOTO 5570
5520 LET STATUS=1,X0$(1)=N0$+"F"+V$
5530 FIND (IVM10_DEV,KEY=X0$(1,6),DOM=5550)IOL=IVM10F
5540 LET STATUS=0
5550 LET X$=X0$(4,3)+" "+FNP$(X0$(7,20))
5560 PRINT @(V1,V2),X$,'CL',
5565 GOTO 5590
5570 LET STATUS=0,X$="All"
5580 PRINT @(V1,V2),"All",'CL',
5590 RETURN 
5600 REM " --- Item"
5610 DIM D0$(22),D1$(P[2]+P[3]+P[4]),X$(79-(41+P[0]))
5615 IF FNP$(V$)="" THEN GOTO 5670
5620 LET D0$(1,22)=N0$+V$
5630 FIND (IVM01_DEV,KEY=D0$,DOM=5650)IOL=IVM01A
5635 LET X$(1)=FNZ$(D1$,P[2],P[3],P[4])
5650 PRINT @(V1,V2),D0$(3,P[0])," ",X$,'CL',
5655 LET X$=D0$(3,P[0])
5660 GOTO 5690
5670 LET X$="First"
5675 IF V2=17 THEN LET X$="Last"
5680 PRINT @(V1,V2),X$,'CL',
5690 RETURN 
5700 REM " --- Beginning/Ending Period"
5710 PRINT @(V1,V2),BEGPER:"#0",'CL',@(V1,V2+2),ENDPER:"#0",'CL',
5720 IF BEGPER<>1 THEN GOTO 5780
5730 IF ENDPER<>S[0] THEN GOTO 5780
5740 LET X$="All Periods"
5750 GOTO 5790
5780 LET X$="Periods "+STR(BEGPER)+" Through "+STR(ENDPER)
5790 RETURN 
6000 REM " --- Forecast Lookup"
6010 LET RECORD$="",TITLE$="MRP Forecast Types"
6030 LET DESCRIPTION$[0]="Type",CHANNEL=MPM01_DEV,FIELD[1]=2
6040 LET POSITION[0]=3,LENGTH[0]=3,POSITION[1]=1,LENGTH[1]=30
6090 GOTO 6400
6100 REM " --- Warehouse Lookup"
6110 LET RECORD$="C",TITLE$="Warehouse Lookup"
6130 LET DESCRIPTION$[0]="ID",CHANNEL=IVM10_DEV,FIELD[1]=1
6140 LET POSITION[0]=4,LENGTH[0]=2,POSITION[1]=6,LENGTH[1]=24
6190 GOTO 6400
6200 REM " --- Buyer Code Lookup"
6210 LET RECORD$="F",TITLE$="Buyer Code Lookup"
6230 LET DESCRIPTION$[0]="Code",CHANNEL=IVM10_DEV,FIELD[1]=1
6240 LET POSITION[0]=4,LENGTH[0]=3,POSITION[1]=7,LENGTH[1]=20
6290 GOTO 6400
6400 REM " --- Call Windowed Lookup"
6410 LET FIELD[0]=1,DESCRIPTION$[1]="Description"
6440 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,ROW=V2+2,V$="SKIP"
6450 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8090 DEF FNZ$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

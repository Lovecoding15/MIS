0010 REM "ADM - MAS Printer Maintenance"
0020 REM "Program: ADM.BA"
0030 REM "+----------------------------------------------------------+"
0031 REM "|            ADminstrator System Version 1.0.2a            |
0032 REM "|     Copyright (C) 1994 MicroAccounting Systems, Inc.     |
0033 REM "|                 All Rights Reserved"                     |
0034 REM "+----------------------------------------------------------+
0040 REM 
0041 REM "KJS|10/16/96|Version 6.0
0048 REM 
0049 SETERR 9000
0050 ON O0 GOTO 0051,0051,0051,0051,0051,0051,0051,0051,0051,0500
0060 BEGIN 
0100 REM "----  Open/Lock Files"
0110 OPEN (1)"ADS-01"; REM "--- ADministator Parameter File"
0120 OPEN (2)"SYM-07"; REM "--- ADD+ON Printer Header File
0130 OPEN (3)"SYM-17"; REM "--- ADD+ON Printer Detail File
0140 OPEN (4)"ADS-01"; REM "---ADministrator Param file, 2nd channel
0200 REM "----  IOlists"
0210 ADS01P: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$(1),A6$(1),A[ALL]
0240 SYM07: IOLIST D0$,D1$
0250 SYM17: IOLIST E0$,E1$,E2$,E3$
0400 REM "----  Parameters"
0420 LET PGM$=PGM(-2); RUN "ADS.01"
0500 REM "----  System Parameter Check"
0510 IF STATUS<>0 THEN GOTO 9900
0600 REM "---- Local Variables"
0620 DIM A0$(8),A1$(40),A2$(30),A3$(30),A[4],A4$(50*3),A5$(50*3),A6$(30)
0630 DIM BAR$(80)
0700 REM "---- Background"
0720 PRINT 'SB',@(8,3),"Printer ID:",@(6,4),"Printer Mode:",@(7,5),"Description:",@(0,6),'BR','SB',BAR$,@(40-INT(LEN("FORM FEED CONTROL")/2),6),"Form Feed Control",'ER','SB',@(8,7),"Before Job:",@(30,7),"Between Pages:",@(57,7),"After Job:",@(7,8),"Print Width:",@(29,8),"Lines Per Page:",@(3,9),"Hex Control Seq:",@(4,15),"Post Job Reset:",
0740 CALL "ADC.WC",1,1,0,11,18,10,0
0790 PRINT 'SF',
0900 REM "---- Positioning Read"
0920 READ (1,KEY="P",DOM=0921)
1020 PRINT 'CF',@(20,10),'CE',; CALL "ADC.WC",1,1,0,11,18,10,0; PRINT 'SB',@(4,15),"Post Job Reset",
1100 REM "---- Key"
1130 DIM A0$(8); LET A0$(1,1)="P"
1140 LET V2$=KEY(1,END=0900)
1150 IF V2$(1,1)<>"P" THEN LET V2$=""; GOTO 1180
1160 LET A0$(2)=V2$(2),V2$=V2$(2,2)
1170 GOTO 1200
1180 LET A0$(2)="",V2$=""
1200 REM "---- Printer Name"
1220 LET V0$="S",V1$="C",V3$="",V4$="2 Character Printer ID, <F3>=Lookup, <F4>=End",V0=2,V2=3,V1=20; GOSUB 7000; IF LEN(V$)<2 AND V3<>4 THEN GOTO 1200
1225 IF V3=3 THEN CALL "ADC.05","10",V$,X$; IF V$="" OR V$="END" THEN GOTO 1200 ELSE PRINT @(20,3),V$,
1230 IF V3=4 THEN GOTO 9900
1240 LET A0$(2,2)=V$
1260 GOSUB 6000
1300 REM "---- Printer Mode"
1320 LET V0$="S",V1$="C",V2$=A0$(7,1),V3$="",V4$="Enter A Printer Mode Or Blank For Description Record, <F3>=Lookup",V0=1,V1=20,V2=4; GOSUB 7000
1340 IF V3=2 OR V3=4 THEN GOTO 1200
1350 IF V3=3 THEN LET X$=A0$(2,2),V$=""; CALL "ADC.05","21",X$,V$; IF V$="" OR V$="END" THEN GOTO 1300 ELSE PRINT @(20,4),V$,
1360 LET A0$(7,1)=V$
1400 REM "---- Main Read"
1440 READ (1,KEY=A0$,DOM=1500)IOL=ADS01P
1460 GOSUB 5000
1480 GOTO 4000
1500 REM "---- New Record?"
1520 LET V0$="S",V1$="C",V2$="Y",V3$="YNyn",V4$="Is This A New Record? <Y/N>: ",V0=1,V1=40+INT(LEN(V4$)/2)+2,V2=22; GOSUB 7000; LET V$=FNU$(V$)
1540 IF V$<>"Y" THEN GOTO 1000
1600 REM "---- New Record Setup"
1620 DIM A1$(30),A2$(30),A[4],A3$(30),A4$(50*3),A5$(50*3),A6$(30)
2000 REM "---- Description"
2020 LET V0$="S",V1$="C",V2$=A1$(1,30),V3$="",V4$="Enter The Printer Description Or Print Mode Description",V0=30,V1=20,V2=5,I0=0; GOSUB 7000
2060 LET A1$(1,30)=V$
2090 IF A0$(7,1)=" " THEN GOTO 4000 ELSE GOTO 3900
2100 REM "---- Form Feed Between Pages?"
2120 LET V0$="S",V1$="C",V2$=A2$(2,1),V3$="",V4$="Form Feed Between Pages? <Y/N>",V0=1,V1=45,V2=7; GOSUB 7000; LET V$=FNU$(V$)
2150 LET A2$(2,1)=V$
2190 GOTO 3900
2200 REM "---- Form Feed Flag"
2220 LET V0$="S",V1$="C",V2$=A2$(1,1),V3$="YNyn",V4$="Form Feed At The Beginning Of Each Page? <Y/N>",V0=1,V1=20,V2=7
2230 GOSUB 7000
2240 LET A2$(1,1)=V$
2290 GOTO 3900
2300 REM "---- Print Width"
2320 LET V0$="N",V1$="C",V2$=STR(A[0]),V3$="000",V4$="Print Width For This Mode In Characters",V0=3,V1=20,V2=8
2340 GOSUB 7000
2360 LET A[0]=V
2390 GOTO 3900
2400 REM "---- Hexidecimal Initialization Codes"
2420 LET X0$=A4$,X0=9
2440 GOSUB 6200
2450 LET A4$=X0$
2490 GOTO 3900
2500 REM "---- Hexidecimal End-Of-Job Codes"
2520 LET X0$=A5$,X0=15
2540 GOSUB 6200
2550 LET A5$=X0$
2590 GOTO 3900
2800 REM "---- Form Feed After Job"
2820 LET V0$="S",V1$="C",V2$=A2$(3,1),V3$="YNyn",V4$="Form Feed At The End Of Print Jobs? <Y/N>",V0=1,V1=69,V2=7; GOSUB 7000; LET V$=FNU$(V$)
2850 LET A2$(3,1)=V$
2890 GOTO 3900
2900 REM "---- Lines Per Page"
2920 LET V0$="N",V1$="C",V2$=STR(A[1]),V3$="000",V4$="Lines On Page In This Print Mode",V0=3,V1=45,V2=8; GOSUB 7000
2940 IF V<20 THEN GOTO 2900
2960 LET A[1]=V
2990 GOTO 3900
3900 REM "---- Sequence Control"
3910 IF V3=2 THEN GOTO 3940
3920 LET I0=I0+1
3930 GOTO 3960
3940 IF I0>0 THEN LET I0=I0-1
3960 ON I0 GOTO 2000,2200,2100,2800,2300,2900,2400,2500,3000,3200,4000
4000 REM " --- Correct?"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct? <Y/N/COPY/DELETE>: ",V0=6,V1=40+INT(LEN(V4$)/2)+2,V2=22; GOSUB 7000; LET V$=FNU$(V$)
4060 IF V$="N" THEN GOTO 2000
4070 IF V$="DELETE" THEN GOTO 4200
4080 IF V$="COPY" THEN GOSUB 10000; GOSUB 5000; GOTO 4000
4100 REM "---- Write"
4120 WRITE (1,KEY=A0$)IOL=ADS01P
4125 IF A0$(7,1)<>" " THEN GOTO 4150
4130 GOSUB 6800; REM "Create SYM07/17 record
4190 GOTO 1000
4200 REM "---- Delete"
4210 REMOVE (1,KEY=A0$,DOM=4211)
4220 IF A0$(7,1)<>" " THEN GOTO 4250
4240 REMOVE (2,KEY=A0$(2,2),DOM=4241)
4245 GOTO 4290
4250 REM "Detail
4270 REMOVE (4,KEY=A0$(2,2)+A0$(7,1),DOM=4271)
4290 GOTO 1000
5000 REM "---- Display Record"
5020 PRINT 'SF',@(20,5),A1$,
5040 IF A0$(7,1)=" " THEN GOTO 5190
5050 PRINT 'SF',@(20,7),A2$(1,1),@(45,7),A2$(2,1),@(69,7),A2$(3,1),
5080 PRINT 'SF',@(20,8),A[0]:"000",@(45,8),A[1]:"000",
5120 LET X0$=A4$,X0=9; GOSUB 5900
5140 LET X0$=A5$,X0=15; GOSUB 5900
5190 RETURN 
5900 REM "---- Display Hex Control Strings"
5920 FOR J=0 TO 2
5940 PRINT @(20,J*2+X0),X0$(J*50+1,50),
5945 PRINT @(20,J*2+X0+1),'CL','SF','BR',
5950 FOR K=0 TO 48 STEP 2
5955 IF X0$(J*50+1+K,2)=$2020$ THEN PRINT "  ",; GOTO 5975
5960 IF ATH(X0$(J*50+1+K,2))=$1B$ THEN PRINT "Ec",; GOTO 5975
5965 IF DEC(ATH(X0$(J*50+1+K,2)))<32 OR DEC(ATH(X0$(J*50+1+K,2)))>126 THEN PRINT DEC(ATH(X0$(J*50+1+K,2))):"00",; GOTO 5975
5970 PRINT ATH(X0$(J*50+1+K,2))," ",
5975 NEXT K
5980 PRINT 'ER',
5985 NEXT J
5990 RETURN 
6000 REM "---- Check Printer Existence For Task"
6010 LET J=0
6040 LET X$=TSK(J,ERR=6100)
6050 IF X$(1,1)<>$01$ OR A0$(2,2)<>X$(3,2) THEN LET J=J+1; GOTO 6040
6090 GOTO 6190
6100 REM "No printer found
6120 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Printer Not Configured! Check Basic Configuration Info. <CR>=Continue: ",V0=1,V1=40+INT(LEN(V4$)/2)+2,V2=22; GOSUB 7000
6190 RETURN 
6200 REM "---- HexiDecimal Control Sequences"
6220 FOR J=0 TO 2
6240 LET V0$="S",V1$="C",V2$=X0$(J*50+1,50),V3$="",V4$="Enter Hexidecimal Codes, <F1>=Character Conversion Help",V0=50,V1=20,V2=J*2+X0; GOSUB 7000
6245 LET X$=ATH(FNP$(V$),ERR=6240)
6250 IF V3=4 THEN GOSUB 5900; EXITTO 6990
6255 IF V3=2 AND J>0 THEN LET J=J-1; GOTO 6221 ELSE IF V3=2 AND J=0 THEN GOSUB 5900; EXITTO 6590 FI FI
6260 LET X0$(J*50+1,50)=V$
6280 PRINT @(20,J*2+X0+1),'CL','SF','BR',
6320 FOR K=0 TO 48 STEP 2
6340 IF X0$(J*50+1+K,2)=$2020$ THEN PRINT "  ",; GOTO 6390
6350 IF ATH(X0$(J*50+1+K,2))=$1B$ THEN PRINT "Ec",; GOTO 6390
6360 IF DEC(ATH(X0$(J*50+1+K,2)))<32 OR DEC(ATH(X0$(J*50+1+K,2)))>126 THEN PRINT DEC(ATH(X0$(J*50+1+K,2))):"00",; GOTO 6390
6370 PRINT ATH(X0$(J*50+1+K,2))," ",
6390 NEXT K
6420 PRINT 'SF','ER',
6490 NEXT J
6590 RETURN 
6600 REM "---- Create SYM-07 Record from ADS"
6610 DIM D0$(2),D1$(48)
6620 LET D0$(1,2)=A0$(2,2); REM "---- Printer ID
6630 LET D1$(1,30)=A1$; REM "---- Printer Description
6640 LET D1$(31,1)=A2$(1,1); REM "---- Initial FF
6690 RETURN 
6700 REM " --- Create SYM-17 record from ADS"
6710 DIM E0$(3),E1$(30),E2$(60),E3$(16)
6720 LET E0$(1,2)=A0$(2,2),E0$(3,1)=A0$(7,1); REM "Printer ID and mode
6730 LET E1$(1,30)=A1$(1,30); REM "Printer description
6740 LET E2$(1,60)=A4$; REM "Init printer hex codes
6790 RETURN 
6800 REM " --- Write SYM-07/17 records"
6801 REM "Header
6810 IF A0$(7,1)<>" " THEN GOTO 6850
6820 GOSUB 6600
6830 WRITE (2,KEY=D0$)IOL=SYM07
6840 GOTO 6890
6850 REM "Detail
6860 GOSUB 6700
6870 WRITE (3,KEY=E0$)IOL=SYM17
6890 RETURN 
7000 REM " --- User Input Routine"
7020 CALL "ADC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-1),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7040 IF V3=5 THEN GOTO 7100
7050 IF V3=6 THEN GOTO 7200
7085 LET I$=V$
7090 RETURN 
7100 REM " --- Error During Input"
7200 REM " --- Escape Requested"
7210 PRINT @(0,22),'CL','CURSOR'("ON"),
7220 ESCAPE
7290 GOTO 7000
7300 REM "---- Special User Input"
7320 CALL "ADC.IB",V0$,V1$,V2$,V3$,V4$,PGM(-1),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7330 IF V3<5 OR V3>6 THEN GOTO 7350
7340 ESCAPE
7390 RETURN 
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR((ASC(Q$)-32)*POS(" "<>Q2$(2,1)):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(NUM(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8090 DEF FNP$(Q$)=CVS(Q$,2)
8170 DEF FNS$(Q$)=CVS(Q$,2)
8180 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM "---- Errors"
9010 LET ER0$=LST(PGM(TCB(5))),ER1$=STR(TCB(5)),ER2$=PGM(-1),ER0=ERR,ER1=0
9020 IF ER0=0 AND POS(" (PTR"=ER0$)>0 THEN GOTO 9600
9030 IF ER0=14 AND O0<>0 THEN LET O0=1; GOTO 0001
9040 CALL "ADC.EA",ERR=9080,ER0$,ER1$,ER2$,ER0,ER1
9050 IF ER1=1 THEN GOTO 9100
9060 IF ER1=3 THEN GOTO 9800
9080 PRINT @(0,22),'CL',ER0$,'LF','LF',; ESCAPE
9100 REM "---- Retry"
9120 RETRY
9200 REM "---- Printer Offline"
9220 CALL "ADS.EB",V3
9230 IF V3=4 THEN GOTO 9900
9240 IF V3=6 THEN ESCAPE
9290 RETRY
9800 REM "---- Error Exit"
9820 IF ER0$="" THEN LET STATUS=ERR ELSE LET STATUS=ER0
9900 REM "---- Exit"
9990 RUN "ADS.AA"
10000 REM "---- Printer Copy"
10560 LET ORIG_A0$=A0$
10570 LET P_ID$="",P_MODE$=""
10720 PRINT @(0,18),'CE',
10730 CALL "ADC.WC",1,0,80,0,0,18,0; CALL "ADC.WC",1,0,80,0,0,21,0
10740 PRINT 'SB',@(6,19),"From Printer:",@(48,19),"To Printer:",@(9,20),"From Mode:",@(51,20),"To Mode:",
11100 REM "---- From Printer"
11120 LET V0$="S",V1$="C",V2$=A0$(2,2),V3$="",V4$="Printer ID To Copy From, <F3>=Lookup",V0=2,V1=20,V2=19; GOSUB 7000
11140 IF V3=4 THEN GOTO 11900
11150 IF V3=3 THEN CALL "ADC.05","10",V$,X$; IF V$="" OR V$="END" THEN GOTO 11100 ELSE PRINT @(20,19),V$,
11155 LET V$=V$+"  ",V$=V$(1,2)
11160 FIND (1,KEY="P"+V$+"     ",DOM=11100)IOL=ADS01P
11170 PRINT @(23,19),A1$(1,20),
11200 REM "---- From Font"
11220 LET V0$="S",V1$="C",V2$=A0$(7,1),V3$="",V4$="Enter The Font ID Or <F1>=All, <F3>=Lookup",V0=1,V1=20,V2=20; GOSUB 7000
11230 IF V3=2 OR V3=4 THEN GOTO 11100
11240 IF V3=1 THEN LET A0$(7,1)=""; GOTO 11300
11250 IF V3=3 THEN LET X$=A0$(2,2); CALL "ADC.05","21",X$,V$; IF V$="" OR V$="END" THEN GOTO 11200 ELSE PRINT @(20,20),V$,
11280 FIND (1,KEY=A0$(1,6)+V$+" ",DOM=11200)IOL=ADS01P
11285 PRINT @(23,20),A1$(1,20),
11300 REM "---- To Printer"
11320 LET V0$="S",V1$="C",V2$=P_ID$,V3$="",V4$="Enter The Printer ID To Copy To",V0=2,V1=60,V2=19; GOSUB 7000
11330 IF V3=2 OR V3=4 THEN GOTO 11200
11340 IF LEN(FNP$(V$))<2 THEN GOTO 11300
11370 LET P_ID$=V$
11400 REM "---- To Font ID"
11410 IF A0$(7,1)=" " THEN GOTO 11500
11420 LET V0$="S",V1$="C",V2$=P_MODE$,V3$="",V4$="Enter The Font ID To Copy Data To",V0=1,V1=60,V2=20; GOSUB 7000
11430 IF V3=2 OR V3=4 THEN GOTO 11300
11440 IF LEN(FNP$(V$))<1 THEN GOTO 11400
11470 LET P_MODE$=V$
11500 REM "---- Copy All Records"
11520 IF A0$(7,1)<>" " THEN GOTO 11700
11530 LET K$=A0$(1,6)
11540 READ (1,KEY=K$,DOM=11541)
11550 LET K1$=KEY(1,END=11900)
11560 IF K1$(1,6)<>K$ THEN GOTO 11900
11570 READ (1,KEY=K1$)IOL=ADS01P
11580 LET A0$(2,2)=P_ID$
11590 WRITE (4,KEY=A0$)IOL=ADS01P
11600 GOSUB 6800; REM "Update SYM-07/17
11690 GOTO 11550
11700 REM "---- Copy Specific Record"
11710 LET A0$(2,2)=P_ID$,A0$(7,1)=P_MODE$
11720 WRITE (1,KEY=A0$)IOL=ADS01P
11730 GOSUB 6800; REM "Update SYM-07/17
11900 REM "---- Exit Copy Process"
11910 PRINT @(0,18),'CE',
11920 READ (1,KEY=ORIG_A0$)IOL=ADS01P
11990 RETURN 
65000 END

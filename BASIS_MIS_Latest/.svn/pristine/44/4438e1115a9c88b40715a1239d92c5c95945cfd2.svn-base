0010 REM "IVM - Item Maintenance (Work Order Where Used Inquiry)"
0020 REM "Program IVM.MQ
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 PRECISION P[1]
0200 REM " --- IOLists
0210 APM01A: IOLIST *,VENDNAME$(1)
0220 IVM02A: IOLIST S0$(1),S1$(1)
0230 IVM10C: IOLIST X0$(1)
0240 WOE01A: IOLIST C0$(1),C1$(1),C[ALL]
0250 WOE22A: IOLIST X0$(1),X1$(1),X[ALL]
0500 REM " --- Initializations
0505 LET ITEM$=A0$(3),HEIGHT=18,MAXROW=12
0510 DIM X0$(32),O[3],C0$(75),C1$(141),C[11],HEADING1$(WIDTH),HEADING2$(WIDTH)
0520 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,1]
0540 LET L=1,PAGE=1,X0=0,X1=1,SELECTION$="",NUMBER=1
0550 CALL "SYC.RM",WOM2$,WOM2,10,11
0555 CALL "SYC.RM",WOM3$,WOM3,6,11
0560 REM " --- Headings
0565 LET HEADING1$(1)="  Work"+FILL(3)+"Type/"+FILL(3)+"Start"+FILL(5)+"Last"+FILL(5)+"Date"
0570 LET HEADING1$(H0-10)="Quantity"
0580 LET HEADING2$(1)=" Order # Status  Date"+FILL(4)+"Activity Required Finished Item"
0590 LET HEADING2$(H0-10)="Required"
0615 REM " --- Footers & column variables
0620 LET FOOTING$(1)=" <F1>=Next Warehouse"
0625 LET FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0630 LET O[0]=H0-WOM2
0700 REM " --- Background
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT 'CS','SB',@(1,0),"Warehouse ID:",@(57),"Last Built:",'SF',
0730 CALL "SYC.WC",1,0,WIDTH-2,1,0,1,0
0740 PRINT @(0,2),'SB','BR',HEADING1$,@(0,3),HEADING2$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Position file
0910 IF PAGES<3 THEN GOTO 4000
0990 READ (IVM03_DEV,KEY=A0$(1,2)+"G"+A0$(3),DOM=1000)
1000 REM " --- Warehouse ID
1005 PRINT 'CF',
1010 LET V2$="",V2$=KEY(IVM03_DEV,END=1040)
1020 IF POS(A0$(1,2)+"G"+A0$(3)=V2$)<>1 THEN LET V2$=""
1030 IF V2$<>"" THEN LET V2$=V2$(24)
1040 LET V0$="S",V1$="KE",V3$="",V4$="",V0=2,V1=15,V2=0
1050 GOSUB 7000
1055 ON V3 GOTO 1080,1000,1000,1060,4000
1060 CALL "IVC.LK",3,V1,V2,V$
1070 IF V$="" THEN GOTO 0900
1080 LET WHSE$=V$+"  ",WHSE$=WHSE$(1,2)
1100 REM " --- Retrieve record
1105 DIM X0$(32)
1110 FIND (IVM10_DEV,KEY=N0$+"C"+WHSE$,DOM=1000)IOL=IVM10C
1120 LET K$=A0$(1,2)+"G"+A0$(3)+WHSE$
1130 READ (IVM03_DEV,KEY=K$,DOM=1140)
1140 PRINT @(15,0),WHSE$," ",X0$(6,17),
1200 REM " --- Get Last Built Date
1210 DIM S0$(24),S1$(64)
1220 LET LASTBUILT$=FILL(8)
1230 FIND (IVM02_DEV,KEY=A0$(1,2)+WHSE$+ITEM$,DOM=1500)IOL=IVM02A
1240 LET LASTBUILT$=FNB$(S1$(31,3))
1250 IF S1$(31,3)=FILL(3) THEN LET LASTBUILT$=FILL(8)
1260 PRINT @(69,0),LASTBUILT$,
1500 REM " --- Get Data
1505 LET X0=0,LINE=1,PAGE=1,X1=1,L=1,TOTAL=0
1510 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,1]
1520 READ (WOE03_DEV,KEY=N0$+"E  "+WHSE$+ITEM$,DOM=READ_WOE03)
1525 READ_WOE03: 
1530 LET K03$=KEY(WOE03_DEV,END=NOMORE)
1535 IF LEN(K03$)<25 THEN GOTO NOMORE
1540 IF K03$(1,27)<>K$(1,2)+"E  "+K$(24,2)+K$(4,20) THEN GOTO NOMORE
1550 READ (WOE03_DEV)
1560 LET C0$(1)=K03$(1,2)+K03$(26,9)
1600 FIND (WOE01_DEV,KEY=C0$(1,11),DOM=NEXT_WOE03)IOL=WOE01A
1610 IF C0$(54,2)<>WHSE$ THEN GOTO NEXT_WOE03
1620 IF POS(C0$(15,1)="OPQ")=0 THEN GOTO NEXT_WOE03; REM "No Closed WOs
1645 REM " --- Assign Data
1650 LET WONUM$=C0$(5,7),TYPE$=C0$(12,2),SPLAT_SPACE$="  "
1660 LET STARTDATE$=FNB$(C0$(25,3)),LASTACTIV$=FNB$(C0$(28,3))
1670 IF C0$(25,3)=FILL(3) THEN LET STARTDATE$=FNB$(C0$(19,3)),SPLAT_SPACE$="* "
1680 IF C0$(25,3)=FILL(3) AND C0$(19,3)=FILL(3) THEN LET STARTDATE$=FILL(8)
1690 IF C0$(28,3)=FILL(3) THEN LET LASTACTIV$=FILL(8)
1710 LET STATUS$=C0$(15,1)
1720 REM " --- Get Date Required, Finish Item and Qty Required
1730 DIM X0$(15),X1$(151),X[13]
1740 FIND (WOE22_DEV,KEY=C0$(1,11)+"B"+K03$(35,3),DOM=1800)IOL=WOE22A
1750 LET DATEREQD$=FNB$(X1$(4,3))
1760 IF X1$(4,3)=FILL(3) THEN LET DATEREQD$=FILL(8)
1770 LET QTYREQD=X[2],QTYREQD$=STR(QTYREQD:WOM2$),FINISHITEM$=C1$(1,20)
1780 IF C0$(14,1)="I" THEN LET FINISHITEM$=C0$(56,P[0])
1790 LET TOTAL=TOTAL+QTYREQD
1800 REM " --- Display page?
1810 IF L+1<=MAXROW THEN GOTO 1900
1820 GOSUB 5400
1830 IF FKEY=1 THEN GOTO 4000
1840 IF FKEY=4 THEN GOTO 4000
1850 IF SELECTION$<>"" THEN GOTO 4000
1860 LET L=1,X1=1
1900 REM " --- Store detail line
1910 DIM G1$(WIDTH-2)
1915 LET G1$(2)=WONUM$+" "+TYPE$+"  "+STATUS$+" "+STARTDATE$+SPLAT_SPACE$+LASTACTIV$+" "+DATEREQD$+" "+FINISHITEM$
1930 LET G1$(O[0])=QTYREQD$
1935 LET LLIST$[PAGE,X1,1]=G1$,X1=X1+1,X0=X0+1,L=L+1
1980 REM " --- Loop back for more
1990 NEXT_WOE03: 
1995 GOTO READ_WOE03
2000 REM " --- No more transactions
2005 NOMORE: 
2010 IF X0>0 THEN GOTO 3000
2020 DIM MESSAGE$[1]
2030 LET MESSAGE$[0]="No transactions found (<Enter>=Continue)"
2040 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
2090 GOTO 1000
3000 REM " --- Display page?
3010 IF L+1<=MAXROW THEN GOTO 3200
3020 GOSUB 5400
3030 IF FKEY=1 THEN GOTO 4000
3040 IF FKEY=4 THEN GOTO 4000
3050 IF SELECTION$<>"" THEN GOTO 4000
3060 LET L=1,X1=1
3200 REM " --- Store totals line
3210 DIM G1$(WIDTH-2)
3220 LET G1$(O[0]-6)="Total "+STR(TOTAL:WOM2$)
3230 LET LLIST$[PAGE,X1,1]=G1$,X1=X1+1,X0=X0+1,L=L+1
3900 REM " --- Display last page
3910 LET SELECTION$="|EOF"
3920 GOSUB 5400
3990 GOTO 1000
4000 REM " --- Run menu overlay
4010 IF SELECTION$="END" THEN LET FKEY=4,SELECTION$=""
4050 CALL "SYC.WD",NAME$
4090 RUN "IVM.MA"
5400 REM " --- Window processing"
5410 LET MODE=32
5420 CALL "SYC.SA",MODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAXROW,NAME$,HEIGHT,WIDTH,FKEY
5460 IF FKEY=-16 OR SELECTION$="" THEN LET PAGE=PAGE+1
5470 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAXROW,5,1
5485 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAXROW,5,1
5490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9910 CALL "SYC.WD",NAME$
9950 RUN "SYS.AA"
9999 END

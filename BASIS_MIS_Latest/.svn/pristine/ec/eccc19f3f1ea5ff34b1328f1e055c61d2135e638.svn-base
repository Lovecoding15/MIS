0010 REM "IVR - Cost Change Valuation Report"
0020 REM "Program IVR.WA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=11
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0125 LET FILES$[4]="IVM-03",FILES$[5]="IVM-10",FILES$[6]="IVM-14"
0130 LET FILES$[7]="IVE-06",FILES$[8]="IVW-06",FILES$[9]="GLM-01"
0135 LET FILES$[10]="GLW-04",FILES$[11]="ARM-10"
0145 LET OPTIONS$[7]="L",OPTIONS$[8]="C",OPTIONS$[10]="C"
0150 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0180 LET IVM03_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],IVM14_DEV=CHANNELS[6]
0190 LET IVE06_DEV=CHANNELS[7],IVW06_DEV=CHANNELS[8]
0200 REM "--- IOLIST's"
0210 IVM02A: IOLIST A0$(1),A1$(1),A2$,A[ALL]
0220 IVM01A: IOLIST B0$(1),B1$(1),B2$,B3$,B4$,B5$,B6$(1),B[ALL]
0250 IVE06A: IOLIST C0$(1),C1$,C2$,C[ALL]
0255 IVW06A: IOLIST Y0$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0275 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0290 GLW04A: IOLIST X0$,X0
0300 ARM10D: IOLIST D0$(1),D1$(1)
0350 IVM10A: IOLIST U0$(1)
0360 IVM10C: IOLIST W0$(1)
0400 REM " --- Parameters"
0405 DIM P[7],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N3$=F5$
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0425 IF I3$(8,1)<>"S" THEN GOTO 4900
0430 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0435 LET G[4]=NUM(G2$(7,2)),P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1))
0440 LET P[5]=NUM(I2$(9,2)),P[6]=NUM(I2$(11,2)),P[7]=NUM(I2$(13,2))
0450 CALL "SYC.VA",N2$,INFO$[ALL]
0455 LET GL$=INFO$[9],AR$="N"
0460 IF GL$<>"Y" THEN GOTO 0500
0470 CALL "SYC.VA","AR",INFO$[ALL]
0475 LET AR$=INFO$[20]
0480 IF AR$<>"Y" THEN GOTO 0500
0485 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=0500)IOL=ARS01A
0490 LET AR$=P3$(11,1)
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM A0$(24),A1$(64),A[20],B0$(22),B1$(60),B6$(50),B[12],O[8]
0515 DIM C0$(27),U0$(32),W0$(32),C[4],D[1],S[1],T[1],R[1],HEADINGS$[3]
0520 LET HEADINGS$[0]=F4$,HEADINGS$[2]=F5$,HEADINGS=2,PAGE=0,WIDTH=132
0525 LET WHEN$=F0$(7,3),EFFECTIVE$=WHEN$,CLOCK$="",M1=LEN(M1$)
0530 CALL "SYC.RM",M2$,M2,7,12
0535 CALL "SYC.RM",M3$,M3,8,12
0550 LET O[1]=P[0],O[8]=WIDTH-M1,O[7]=O[8]-M1,O[6]=O[7]-M3
0555 LET O[5]=O[6]-M1,O[4]=O[5]-M3,O[3]=O[4]-M2,O[2]=O[3]-3
0560 LET DW=MIN(O[2]-O[1]+1,P[5]+P[6]+P[7]),L9=55,L=L9+1,DONE=0,SPLIT=0
0565 LET H3$="",H4$="",H5$=""
0600 REM " --- Additional File Opens"
0605 IF GL$<>"Y" THEN GOTO 0650
0610 CALL "SYC.DA",1,9,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0615 IF STATUS>0 THEN GOTO 9900
0620 LET GLM01_DEV=CHANNELS[9],GLW04_DEV=CHANNELS[10]
0625 IF BYITEM$="Y" THEN GOTO 0650
0630 CALL "SYC.DA",1,11,11,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0635 IF STATUS>0 THEN GOTO 9900
0640 LET ARM10_DEV=CHANNELS[11]
0650 REM " --- Set default effective date"
0660 LET K$=KEY(IVE06_DEV,END=0700)
0670 IF POS(N0$=K$)=1 THEN LET EFFECTIVE$=K$(3,3)
0700 REM " --- Background"
0710 PRINT 'SB',@(24,3),"Effective Date:",'SF',
0720 CALL "SYC.WC",1,0,80,0,0,4,0
0800 REM " --- Effective Date"
0805 PRINT 'CF',
0810 LET V4$="Enter The Effective Date For Changes To Be Listed On This Report (<F1>=All)"
0815 LET V0$="D",V1$="CE",V2$=EFFECTIVE$,V3$="",V0=3,V1=40,V2=3
0820 GOSUB 7000
0825 ON V3 GOTO 0850,0830,0800,0800,9900
0830 LET EFFECTIVE$=""
0835 PRINT @(V1,V2),"All",'CL',
0840 GOTO 0900
0850 LET EFFECTIVE$=V$
0900 REM " --- Report Initializations"
0910 CALL "SYC.GA",7,1,"","",STATUS
0920 IF STATUS THEN GOTO 9900
0930 CALL "SYC.NB"," Sorting",16+P[0],COLUMN
0940 GOSUB 5500
0950 PRINT @(COLUMN-9),FILL(12+P[0]),@(COLUMN-9),'SB',"Printing",'SF',
0990 READ (IVW06_DEV,KEY="",DOM=1000)
1000 REM " --- Next IVW-06 record"
1010 LET K$=KEY(IVW06_DEV,END=4000)
1020 IF K$(3,3)>F0$(7,3) THEN GOTO 4000
1030 READ (IVW06_DEV)
1040 PRINT @(COLUMN,11),FNB$(K$(3,3))," ",K$(6,2)," ",K$(8,3)," ",K$(11,P[0]),
1050 LET C0$(1)=K$(1,7)+K$(11)
1060 READ (IVE06_DEV,KEY=C0$,DOM=3900)IOL=IVE06A
1200 REM " --- Level breaks?"
1210 IF K$(3,3)<>T0$ THEN GOSUB 6000
1220 IF K$(6,2)<>T1$ THEN GOSUB 6200
1230 IF K$(8,3)<>T2$ THEN GOSUB 6400
1400 REM " --- Additional reads"
1410 DIM A0$(24),A1$(64),A[20],B0$(22),B1$(60),B6$(50),B[12]
1420 LET B0$(1)=N0$+C0$(8),A0$(1)=N0$+C0$(6)
1430 FIND (IVM01_DEV,KEY=B0$,DOM=3900)IOL=IVM01A
1440 FIND (IVM02_DEV,KEY=A0$,DOM=3900)IOL=IVM02A
2000 REM " --- Format detail line fields"
2010 DIM DESCRIPTION$(DW)
2020 LET DESCRIPTION$(1)=FNITEM$(B1$,P[5],P[6],P[7])
2030 LET ONHAND=A[0],OLDCOST=A[11],NEWCOST=C[0]
2200 REM " --- Calculate extensions at precision 2"
2210 PRECISION 2
2220 LET OLDVALUE=ONHAND*OLDCOST
2230 LET NEWVALUE=ONHAND*NEWCOST
2240 LET CHANGE=NEWVALUE-OLDVALUE
2250 PRECISION P[1]
2300 REM " --- New product?"
2310 IF H5$="" THEN GOTO 2500
2320 IF L+3+NL>L9 THEN GOSUB 5000
2330 IF NL>0 THEN PRINT (7)""
2340 PRINT (7)H5$
2350 PRINT (7)""
2360 LET L=L+2+NL,NL=1,H5$=""
2500 REM " --- Print detail line"
2510 IF L>L9 THEN GOSUB 5000
2520 PRINT (7)@(O[0]),B0$(3),@(O[1]),DESCRIPTION$,@(O[2]),B2$(4,2),@(O[3]),ONHAND:M2$,@(O[4]),OLDCOST:M3$,@(O[5]),OLDVALUE:M1$,@(O[6]),NEWCOST:M3$,@(O[7]),NEWVALUE:M1$,@(O[8]),CHANGE:M1$
2530 LET L=L+1,NL=1
2900 REM " --- Accumulate totals"
2910 LET D[0]=D[0]+OLDVALUE,D[1]=D[1]+NEWVALUE
2920 LET S[0]=S[0]+OLDVALUE,S[1]=S[1]+NEWVALUE
2930 LET T[0]=T[0]+OLDVALUE,T[1]=T[1]+NEWVALUE
2940 LET R[0]=R[0]+OLDVALUE,R[1]=R[1]+NEWVALUE
3000 REM " --- G/L distribution"
3010 IF GL$<>"Y" THEN GOTO 3900
3020 IF AR$<>"Y" THEN GOTO 3200
3100 REM " --- By Distribution Code"
3110 DIM D0$(32),D1$(8*G[4],"0")
3120 LET D0$(1)=N0$+"D"+A1$(13,2)
3130 FIND (ARM10_DEV,KEY=D0$(1,5),DOM=3140)IOL=ARM10D
3140 LET ACCOUNT$=D1$(6*G[4]+1,G[4])
3190 GOTO 3500
3200 REM " --- By Item"
3210 DIM ACCOUNT$(G[4],"0")
3220 IF FNP$(B6$(1,G[4]))<>"" THEN LET ACCOUNT$=B6$(1,G[4])
3500 REM " --- Write summary record"
3510 LET AMOUNT=CHANGE
3520 GOSUB 6900
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF L>L9 THEN GOSUB 5000
4020 LET DONE=1
4030 GOSUB 6000
4040 LET TOTAL$="Report",T0=R[0],T1=R[1]
4050 GOSUB 6800
4060 IF GL$<>"Y" THEN GOTO 9900
4070 PRINT @(COLUMN,11),FILL(16+P[0])
4090 RUN "GLR.XB"
4900 REM " --- Not Standard Costing "
4910 CALL "IVC.SB","S",STATUS
4920 LET STATUS=999
4930 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+3,NL=0
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5060 PRINT (7)@(O[4]+M3-8),"Current",@(O[5]+M1-8),"Current",@(O[6]+M3-5),"New",@(O[7]+M1-6),"New",@(O[8]+M1-9),"Change"
5070 PRINT (7)@(O[0]),"Item",@(O[1]),"Description",@(O[2]),"UM",@(O[3]+M2-8),"On Hand",@(O[4]+M3-8),"Cost",@(O[5]+M1-8),"Value",@(O[6]+M3-5),"Cost",@(O[7]+M1-6),"Value",@(O[8]+M1-9),"In Value"
5080 PRINT (7)""
5090 RETURN 
5500 REM " --- Sort IVE-06 by product type"
5510 DIM Y0$(30)
5580 LET FIRST$=N0$+EFFECTIVE$
5590 READ (IVE06_DEV,KEY=FIRST$,DOM=5600)
5600 REM " --- Read next entry record"
5610 LET K$=KEY(IVE06_DEV,END=5900)
5620 IF POS(FIRST$=K$)<>1 THEN GOTO 5900
5630 READ (IVE06_DEV)
5640 PRINT @(COLUMN,11),FNB$(K$(3,3))," ",K$(6,2)," ",K$(8,P[0]),
5650 LET B0$(1)=K$(1,2)+K$(8)
5660 FIND (IVM01_DEV,KEY=B0$,DOM=5600)IOL=IVM01A
5700 REM " --- Write sort record"
5710 LET Y0$(1)=K$(1,7)+B2$(1,3)+K$(8)
5720 WRITE (IVW06_DEV,KEY=Y0$)IOL=IVW06A
5790 GOTO 5600
5900 REM " --- Sort completed"
5990 RETURN 
6000 REM " --- Date break"
6010 IF T0$="" THEN GOTO 6100
6020 IF L+3>L9 THEN GOSUB 5000
6030 GOSUB 6200
6040 LET TOTAL$=H2$,T0=D[0],T1=D[1]
6050 GOSUB 6800
6100 IF DONE>0 THEN GOTO 6190
6110 DIM D[1]
6120 LET T0$=K$(3,3),H2$=FNB$(K$(3,3))
6130 LET HEADINGS$[1]=F5$+" For Changes Effective "+H2$
6190 RETURN 
6200 REM " --- Warehouse break"
6210 IF T1$="" THEN GOTO 6300
6220 IF L+4>L9 THEN GOSUB 5000
6230 GOSUB 6400
6240 LET TOTAL$=H3$,T0=T[0],T1=T[1]
6250 GOSUB 6800
6300 IF DONE>0 THEN GOTO 6390
6310 DIM T[1],W0$(32)
6320 LET T1$=K$(6,2),W0$(6)="Not On File"
6330 FIND (IVM10_DEV,KEY=N0$+"C"+T1$,DOM=6340)IOL=IVM10C
6340 LET H3$="Warehouse "+T1$+" "+FNP$(W0$(6,24)),L=L9+1
6350 LET HEADINGS$[2]=H3$,HEADINGS=3
6390 RETURN 
6400 REM " --- Product break"
6410 IF T2$="" THEN GOTO 6500
6420 LET TOTAL$=T5$,T0=S[0],T1=S[1]
6430 GOSUB 6800
6500 IF DONE>0 THEN GOTO 6590
6510 DIM S[1],U0$(32)
6520 LET T2$=K$(8,3),U0$(7)="(Not On File)"
6530 FIND (IVM10_DEV,KEY=N0$+"A"+T2$,DOM=6540)IOL=IVM10A
6540 LET H5$="Product "+T2$+" "+FNP$(U0$(7,20)),T5$=H5$
6590 RETURN 
6800 REM " --- Print totals"
6810 LET T2=T1-T0,L=L+1,TOTAL$="Totals For "+TOTAL$
6830 PRINT (7)@(O[1]),TOTAL$,@(O[5]),T0:M1$,@(O[7]),T1:M1$,@(O[8]),T2:M1$
6890 RETURN 
6900 REM " --- Update G/L Summary"
6910 IF GL$<>"Y" THEN GOTO 6990
6930 IF AMOUNT=0 THEN GOTO 6990
6940 LET X0$=N0$+ACCOUNT$,X0=0
6950 FIND (GLW04_DEV,KEY=X0$,DOM=6960)IOL=GLW04A
6960 LET X0=X0+AMOUNT
6970 WRITE (GLW04_DEV,KEY=X0$)IOL=GLW04A
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

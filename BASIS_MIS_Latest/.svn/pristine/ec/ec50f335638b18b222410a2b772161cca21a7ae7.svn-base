0010 REM "WOS - Work Order Processing - Operation Requirements Processing"
0020 REM "Program:  WOE.AD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "26Feb97 JJD: Reference correct position in W1$ for line type (3410)"
0032 REM "24Jun97 WGH: Add input prompts for setup time, hours/piece and move time (2220,6020,6120)"
0040 REM 
0055 REM "L9=MAX LINES, L1=FIRST LINES, L0=HEADER LINE, L=CURRENT LINE
0085 SETESC 9000
0090 SETERR 9000
0095 PRECISION IVS01_PRECISION
0100 REM " --- Open/Lock Files
0200 REM " --- IOLISTs"
0210 WOE02A: IOLIST W0$(1),W1$(1),W[ALL]
0215 WOE03D: IOLIST OPNSSORT$
0220 WOM05A: IOLIST X0$,X1$,X2$,X[ALL]
0225 WOM06A: IOLIST SCHEDSORT$
0230 OPCODEA: IOLIST Y0$,Y[ALL]
0500 REM " --- Init Data
0505 DIM SCHEDSORT$(18),OPNSSORT$(21)
0510 LET L0=11,L1=L0+2,L9=19,K9$="",MESS$=""
0520 DIM W0$(15),W1$(151),W[13],V[3],Y[13],O[8]
0530 LET W0$(1)=A0$(1,11)+"A",L=L1
0550 LET M2=LEN(M2$),M3=LEN(M3$),M9=M2+M3
0560 CALL "SYC.RM",M2$,M2,10,10
0570 CALL "SYC.RM",M3$,M3,11,11
0580 LET O[0]=74-M3-M2*3,O[1]=75-M3-M2*2,O[2]=76-M3-M2,O[3]=77-M3
0590 LET O[4]=73-M3-M2*4,O[5]=74-M3-M2*3,O[6]=75-M3-M2*2,O[7]=76-M3-M2,O[8]=77-M3
0700 REM " --- Display
0710 PRINT @(0,L0),'CE'
0720 PRINT 'SB',@(2,L0-1),"Operation Requirements",
0730 PRINT @(0,L0),"Seq  Op Code",@(O[1]-7),"Setup",@(O[2]-7),"Pc/Hr",@(62),"Each/Total",
0740 PRINT @(8,L0+1),"Date Req",@(O[5]-7),"Queue",@(O[6]-8),"Hrs/Pc",@(O[7]-6),"Move",@(O[8]-7),"Hours",@(72),"Cost",
0750 PRINT 'SF'
0760 GOSUB 5100
0770 IF K9$<>"" THEN GOTO 3700
0780 IF A0$(15,1)="C" THEN GOTO 3700
1000 IF L>L9 THEN PRINT @(0,21),'CL',@(0,22),'CL',@(0,L1),'LD','LD'; LET L=L-2
1100 REM " --- Get Next Key"
1105 LET K$=KEY(WOE02_DEV,END=1200)
1110 IF K$="" THEN GOTO 1200
1115 IF K$(1,2)<>N0$ THEN GOTO 1200
1120 IF K$(1,12)<>A0$(1,11)+"A" THEN GOTO 1200
1125 FIND (WOE02_DEV,KEY=K$)IOL=WOE02A
1130 LET OW1$=W1$,EXIST$="Y"
1135 DIM Y0$(30),Y[13]
1140 LET Y0$(1)=N0$+W1$(1,3)+"** Not On File **"
1145 FIND (OPCODE_DEV,KEY=N0$+W1$(1,3),DOM=1150)IOL=OPCODEA
1150 GOTO 1340
1200 REM " --- Sequence "
1210 PRINT @(0,21),'CL',@(0,22),'CL',
1220 IF L>L9 THEN PRINT @(0,L1),'LD','LD'; LET L=L-2; GOTO 1220
1230 IF V3<>2 THEN LET W0=NUM(W0$(13,3))+5
1240 LET V0$="Z",V1$="ER",V2$=STR(W0),V3$="",V0=3,V1=0,V2=L,I0=0
1250 GOSUB 7000
1260 IF V3=4 THEN GOTO 3700
1270 IF V$="000" THEN GOTO 1200
1280 LET W0$(13,3)=V$
1290 FIND (WOE02_DEV,KEY=W0$(1,15),DOM=1400)IOL=WOE02A
1300 LET OW1$=W1$,EXIST$="Y"
1310 DIM Y0$(30),Y[13]
1320 LET Y0$(1)=N0$+W1$(1,3)+"** Not On File **"
1330 FIND (OPCODE_DEV,KEY=N0$+W1$(1,3),DOM=1340)IOL=OPCODEA
1340 IF W0$(1,15)<=K9$ THEN GOSUB 5400
1350 GOSUB 5000
1360 GOTO 3270
1400 REM " --- Setup"
1410 DIM W[13]
1420 REM " --- Set Up Defaults
1430 LET W1$(1,90)="",W1$(92)="",W1$(4,3)=F0$(7,3),EXIST$="N"
1440 PRINT @(4,L),'CL',@(0,L+1),'CL',
1450 GOTO 3200
1500 REM " --- Operation Code "
1510 IF W1$(91,1)="M" THEN LET MESS$="Y"; GOTO 2400
1520 IF W1$(91,1)=" " THEN LET W1$(91,1)="S"
1530 IF V3=2 AND EXIST$="Y" THEN GOTO 3200
1540 LET V0$="S",V1$="CR",V2$=W1$(1,3),V3$="",V4$="Enter A Valid Operation Code",V0=3,V1=4,V2=L,I0=1
1550 FIND (WOE02_DEV,KEY=W0$(1,15),DOM=1580)IOL=WOE02A
1560 LET OW1$=W1$,EXIST$="Y",V4$=V4$+", <F3>=Lookup"
1570 GOTO 1590
1580 LET V4$=V4$+", <F1>=Message Line, <F3>=Lookup",EXIST$="N"
1590 GOSUB 7000
1600 IF EXIST$="Y" THEN GOTO 1650
1610 IF V3<>1 THEN GOTO 1650
1620 LET W1$(91,1)="M",MESS$="Y"
1630 PRINT @(4,L),'CL',@(4,L+1),'CL',
1640 GOTO 2400
1650 IF V3=2 OR V3=4 THEN GOTO 3200
1660 IF V3<>3 THEN GOTO 1740
1670 LET CHANNEL=OPCODE_DEV,NUMBER=1,RECORD$="",TITLE$="Operation Codes"
1680 DIM DESC$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
1690 LET DESC$[0]="Code",DESC$[1]="Description"
1700 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=3
1710 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
1720 LET ROW=5,COLUMN=5
1730 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESC$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
1740 LET W1$(1,3)=V$
1750 DIM Y[13]
1760 FIND (OPCODE_DEV,KEY=N0$+W1$(1,3),DOM=1500)IOL=OPCODEA
1770 LET W1$(7,20)=Y0$(6,20)
1780 PRINT @(V1,V2),V$,@(8,L),W1$(7,20)
1790 IF Y[1]=0 THEN LET Y[1]=1
1800 IF W[4]=0 THEN LET W[4]=1
1810 IF W[5]=0 THEN LET W[5]=Y[1]
1820 IF W[6]=0 THEN LET W[6]=Y[0]
1830 IF W[7]=0 THEN LET W[7]=W[6]*Y[2]
1840 IF EXIST$="N" THEN LET W[8]=Y[4],W[9]=Y[5]
1850 GOSUB 2600
1860 GOSUB 5000
1870 GOTO 3200
1900 REM " --- Date Required
1910 IF MESS$="Y" THEN GOTO 1980
1920 IF POS(" "<>W1$(4,3))=0 THEN LET W1$(4,3)=F0$(7,3)
1930 LET V0$="D",V1$="C",V2$=W1$(4,3),V3$="",V4$="Enter The Date This Operation Should Be Completed",V0=8,V1=8,V2=L+1
1940 GOSUB 7000
1950 IF W1$(4,3)<>V$ THEN GOSUB 6200; LET CHANGE$="Y" ELSE LET CHANGE$="N"
1960 LET W1$(4,3)=V$
1970 IF CHANGE$="Y" THEN GOSUB 6300
1980 GOTO 3200
2200 REM " --- Number Of Hours
2210 IF MESS$="Y" THEN GOTO 2260
2220 LET V0$="N",V1$="",V2$=STR(W[4]),V3$=M2$,V4$="Enter Number Of Hours Needed To Create One Piece",V0=7,V1=O[5],V2=L+1
2230 GOSUB 7000
2240 LET W[4]=V
2250 GOSUB 2600
2260 GOTO 3200
2300 REM " --- Pieces Per Hour
2310 IF MESS$="Y" THEN GOTO 2370
2320 LET V0$="N",V1$="",V2$=STR(W[5]),V3$=M2$,V4$="Enter Pieces Per Hour - Must Be Greater Than 0",V0=7,V1=O[1],V2=L
2330 GOSUB 7000
2340 IF V<=0 THEN GOTO 2300
2350 LET W[5]=V
2360 GOSUB 2600
2370 GOTO 3200
2400 REM " --- Message
2410 LET V0$="S",V1$="C",V2$=W1$(92,60),V3$="",V4$="Enter Message",V0=60,V1=4,V2=L+1
2420 FIND (WOE02_DEV,KEY=W0$(1,15),DOM=2450)IOL=WOE02A
2430 LET OW1$=W1$,EXIST$="Y"
2440 GOTO 2460
2450 LET V4$=V4$+", <F1>=Standard Line",EXIST$="N"
2460 GOSUB 7000
2470 IF EXIST$="N" THEN IF V3=1 THEN LET MESS$="N",W1$(91,1)="S"; PRINT @(4,L+1),'CL',; GOTO 1500
2480 LET W1$(92,60)=V$
2490 GOTO 3200
2600 REM " --- Calc Totals Here
2610 IF W[5]=0 THEN LET W[5]=1
2620 LET W[0]=FNOPNOSET(X),W[1]=FNOPNOSETDOL(X)
2630 LET W2=W[2]
2640 LET W[2]=FNOP(X),W[3]=FNOPDOL(X)
2650 IF W2<>W[2] THEN GOSUB 6200; GOSUB 6300
2660 GOSUB 5000
2670 RETURN 
3200 REM " --- Seq Control"
3210 IF V3=4 THEN IF I0=1 THEN GOTO 3360 ELSE GOTO 3270
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1200,1500,1900,6000,2300,2200,6100,3270
3270 REM " --- Correct"
3280 PRINT @(78,L),'CL',@(0,22),'CL'
3290 LET V0$="S",V1$="C",V2$="",V0=1,V1=78,V2=L,K9$=W0$(1,15),V4$="<Enter>=OK, <F2>=Change, <D>=Delete",I0=7
3300 GOSUB 7000
3310 LET W0=NUM(W0$(13,3))
3320 IF V3=2 THEN GOTO 3200
3330 IF V$="" THEN GOTO 3360
3340 IF V$="D" THEN GOTO 3500
3350 GOTO 3270
3360 PRINT @(0,21),'LD','LD',
3370 REM " --- Write"
3380 IF W1$(91,1)="M" AND V3=4 AND I0=1 THEN GOTO 3500
3390 IF W1$(91,1)="S" THEN FIND (OPCODE_DEV,KEY=N0$+W1$(1,3),DOM=3500)
3400 WRITE (WOE02_DEV,KEY=W0$(1,15))IOL=WOE02A
3410 IF W1$(91,1)="S" AND LEN(OW1$)>5 THEN IF OW1$(1,6)<>W1$(1,6) THEN REMOVE (WOE03_DEV,KEY=N0$+"D  "+OW1$(1,6)+W0$(5,7)+W0$(13,3),DOM=3420)
3420 LET OPNSSORT$=N0$+"D  "+W1$(1,6)+W0$(5,7)+W0$(13,3)
3430 IF W1$(91,1)="S" THEN WRITE (WOE03_DEV,KEY=OPNSSORT$)IOL=WOE03D
3440 LET L=L+2
3450 IF V3=4 THEN GOTO 3700
3460 GOTO 1000
3500 REM " --- Delete"
3510 REMOVE (WOE02_DEV,KEY=W0$(1,15),DOM=3520)
3520 LET OPNSSORT$=N0$+"D  "+W1$(1,6)+W0$(5,7)+W0$(13,3)
3530 REMOVE (WOE03_DEV,KEY=OPNSSORT$,DOM=3540)
3540 GOSUB 6200
3550 PRINT @(0,L),'LD','LD'
3560 IF V3=4 THEN GOTO 3700
3570 GOTO 1100
3700 REM " --- Options"
3710 LET V4$="Is The Detail Information Correct?"
3720 IF A0$(15,1)="C" THEN LET V4$=V4$+" (Y/List): "; GOTO 3740
3730 LET V4$=V4$+" (Y/N/Review/List): "
3740 LET V0$="S",V1$="C",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
3750 GOSUB 7000
3760 IF A0$(15,1)="C" THEN IF V$="N" THEN LET V$=""; GOTO 3790 ELSE GOTO 3780
3770 IF V$="R" THEN GOTO 4030
3780 IF V$="L" THEN GOTO 4000
3790 ON POS(V$="YN") GOTO 3700,3900,1200
3900 REM " --- Close & Get Ready
3910 IF O0=0 THEN IF I9$="N" THEN RUN "WOE.AE" ELSE IF I9$<>"I" THEN RUN "WOE.AF"
3920 LET O9=2
3930 RUN "WOE.AA"
4000 REM " --- List"
4010 GOSUB 5100
4020 GOTO 3700
4030 REM " --- Review"
4040 READ (WOE02_DEV,KEY=A0$(1,11)+"A",DOM=4050)
4050 GOSUB 5400
4060 LET V3=0
4070 GOTO 1100
5000 REM " --- Display"
5010 IF W1$(91,1)="M" THEN PRINT @(0,L),W0$(13,3),'CL',@(0,L+1),'CL',@(4),W1$(92,60),; LET MESS$="Y"; GOTO 5060
5020 LET MESS$="N"
5030 DIM Y[13]; FIND (OPCODE_DEV,KEY=N0$+W1$(1,3),DOM=5050)IOL=OPCODEA
5040 PRINT @(0,L),'CL',W0$(13,3),@(4),W1$(1,3),@(8),W1$(7,20),@(O[0]),W[8]:M2$,@(O[1]),W[5]:M2$,@(O[2]),W[0]:M2$,@(O[3]),W[1]:M3$,
5050 PRINT @(0,L+1),'CL',@(8),FNB$(W1$(4,3)),@(O[4]),Y[3]:M2$,@(O[5]),W[4]:M2$,@(O[6]),W[9]:M2$,@(O[7]),W[2]:M2$,@(O[8]),W[3]:M3$,
5060 RETURN 
5100 REM " --- List"
5110 GOSUB 5400
5120 READ (WOE02_DEV,KEY=A0$(1,11)+"A",DOM=5130)
5130 LET K$=KEY(WOE02_DEV,END=5300)
5135 IF K$(1,2)<>N0$ THEN GOTO 5300
5140 IF K$(1,12)<>W0$(1,11)+"A" THEN GOTO 5300
5150 FIND (WOE02_DEV,KEY=K$)IOL=WOE02A
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="KCE",V2$="",V3$="",V4$="Continue? (<Enter>/End): ",V0=1,V1=50,V2=22
5215 GOSUB 7000
5220 IF V3=4 THEN GOTO 5300
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+2,K9$=W0$(1,15)
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5420 PRINT @(0,L1),'CE',
5440 LET L=L1
5490 RETURN 
5700 REM " --- Compress Desc
5720 LET D9$=D1$
5730 LET Z=POS(" "<>D9$); IF Z>1 THEN LET D9$=D9$(Z)
5740 LET X8=POS("  "=D9$); IF X8<>0 THEN LET Y8=POS(" "<>D9$(X8+2)); LET D9$=D9$(1,X8)+D9$(X8+Y8+1); IF Y8<>0 THEN GOTO 5740
5750 LET D1$=D9$
5790 RETURN 
6000 REM " --- Setup Time
6010 IF MESS$="Y" THEN GOTO 6080
6020 LET V0$="N",V1$="",V2$=STR(W[8]),V3$=M2$,V4$="Enter Amount Of Time Needed To Set Up Assembly Of Entire Lot",V0=M2,V1=O[0],V2=L
6030 GOSUB 7000
6040 IF V<>W[8] THEN GOSUB 6200; LET CHANGE$="Y" ELSE LET CHANGE$="N"
6050 LET W[8]=V
6060 IF CHANGE$="Y" THEN GOSUB 6300
6070 GOSUB 2600
6080 GOTO 3200
6100 REM " --- Move Time
6110 IF MESS$="Y" THEN GOTO 6180
6120 LET V0$="N",V1$="",V2$=STR(W[9]),V3$=M2$,V4$="Enter Amount Of Time Needed To Move To Next Process",V0=M2,V1=O[6],V2=L+1
6130 GOSUB 7000
6140 IF V<>W[9] THEN GOSUB 6200; LET CHANGE$="Y" ELSE LET CHANGE$="N"
6150 LET W[9]=V
6160 IF CHANGE$="Y" THEN GOSUB 6300
6170 GOSUB 2600
6180 GOTO 3200
6200 REM " --- Remove Schedule Detail Records"
6210 READ (WOM06_DEV,KEY=W0$(1,2)+W0$(5,7)+W0$(13,3),DOM=6220)
6220 LET K34$=KEY(WOM06_DEV,END=6280)
6230 IF POS(W0$(1,2)+W0$(5,7)+W0$(13,3)=K34$)<>1 THEN GOTO 6280
6240 REMOVE (WOM06_DEV,KEY=K34$)
6250 LET SCHEDSORT$=K34$(1,2)+K34$(13,6)+K34$(3,10)
6260 REMOVE (WOM05_DEV,KEY=SCHEDSORT$,DOM=6270)
6270 GOTO 6220
6280 RETURN 
6300 REM " --- Write Schedule Detail Records"
6310 DIM X[9],X1$(20),X2$(20)
6320 LET X[0]=Y[3]
6330 LET X0$=A0$(1,2)+W1$(1,3)+W1$(4,3)+W0$(5,7)+W0$(13,3)
6340 LET X[1]=W[8],X[2]=W[2]-W[8],X[3]=W[9]
6350 WRITE (WOM05_DEV,KEY=X0$)IOL=WOM05A
6355 LET SCHEDSORT$=X0$(1,2)+X0$(9,10)+X0$(3,6)
6360 WRITE (WOM06_DEV,KEY=SCHEDSORT$)IOL=WOM06A
6390 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8070 DEF FNOPNOSET(Q)=100*(W[4]/W[5])/A[6]
8075 DEF FNOPNOSETDOL(Q)=100*(W[4]*(W[6]+W[7])/W[5])/A[6]
8080 DEF FNOP(Q)=100*(A[0]*W[4]/W[5])/A[6]+W[8]
8085 DEF FNOPDOL(Q)=100*(A[0]*W[4]*(W[6]+W[7])/W[5])/A[6]+W[8]*(W[6]+W[7])
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9220 GOTO 3900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

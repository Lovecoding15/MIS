0010 REM "OPX - Rebuild Order/Invoice Print File"
0020 REM "Program OPX.FA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ARE-03",FILES$[2]="ARE-13",FILES$[3]="SYS-01"
0140 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET ARE03_DEV=CHANNELS[1],ARE13_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLISTS"
0210 ARE03A: IOLIST A0$
0220 ARE04A: IOLIST B0$
0230 ARE13A: IOLIST W0$,W1$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters "
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)
0800 REM " --- Options
0810 LET V4$="Do You Wish To Rebuild The Order/Invoice Print File"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0850 REM " --- Clear And Lock ARE-04"
0860 LET FILES$[4]="ARE-04",OPTIONS$[4]="CL"
0870 CALL "SYC.DA",1,4,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0880 IF STATUS>0 THEN GOTO 9900
0890 LET ARE04_DEV=CHANNELS[4]
0900 REM " --- Initialize File "
0915 CALL "SYC.NB","Updating",21,COLUMN
0920 READ (ARE03_DEV,KEY="",DOM=0930)
1000 REM " --- Read Open Order/Invoice Records "
1020 LET ARE03_K$=KEY(ARE03_DEV,END=4000)
1040 READ (ARE03_DEV)IOL=ARE03A
1060 PRINT @(COLUMN,11),A0$(1,2)," ",A0$(5,6)," ",A0$(11,7)," ",A0$(18,3),
1100 REM " --- Skip Voids"
1120 IF A0$(21,1)="V" THEN GOTO 1900
1200 REM " --- All Invoices Need ARE-04 Records"
1220 IF A0$(22,1)="I" THEN GOTO 1800; REM "Got one
1300 REM " --- Skip Already Printed Orders, Except Reprints"
1320 IF A0$(22,1)<>"O" THEN GOTO 1900
1340 IF A0$(68,1)<>"Y" THEN GOTO 1400
1360 IF A0$(100,1)<>"Y" THEN GOTO 1900
1380 GOTO 1800; REM "Got one
1400 REM " --- Orders With Non-Printed Committed Detail Lines Need ARE-04 Records"
1410 READ (ARE13_DEV,KEY=A0$(1,17),DOM=1420)
1420 LET ARE13_K$=KEY(ARE13_DEV,END=1900)
1430 IF POS(A0$(1,17)=ARE13_K$)<>1 THEN GOTO 1900
1440 READ (ARE13_DEV)IOL=ARE13A
1450 PRINT @(COLUMN,11),W0$(1,2)," ",W0$(5,6)," ",W0$(11,7)," ",W0$(18,3),
1460 IF W1$(44,2)="YN" THEN GOTO 1800; REM "Got one
1470 GOTO 1420
1800 REM " --- Write ARE-04 Record"
1820 LET B0$=A0$(1,2)+A0$(22,1)+"  "+A0$(5,13)
1840 WRITE (ARE04_DEV,KEY=B0$,DOM=1850)IOL=ARE04A
1900 REM " --- Next ARE-03 Record"
1990 GOTO 1000
4000 REM " --- Done"
4100 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

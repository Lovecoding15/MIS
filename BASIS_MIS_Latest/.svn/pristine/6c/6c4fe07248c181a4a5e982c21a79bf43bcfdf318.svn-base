0010 REM "GLM - Revision Code Maintenance (Lead Overlay)"
0020 REM "Program GLM.MA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-01",FILES$[2]="GLM-02",FILES$[3]="GLM-08"
0140 LET FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM01_DEV=CHANNELS[1],GLM02_DEV=CHANNELS[2],GLM08_DEV=CHANNELS[3]
0185 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$(1),A1$(1)
0220 GLM02A: IOLIST B0$(1),A[ALL],U[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 GLS01A: IOLIST P0$,P1$,P2$(1),P3$(1),P4$(1),M0$,M1$,M2$,M3$
0280 GLM08A: IOLIST C0$(1),C1$(1),C2$(1),C3$(1),C4$,C5$,C[ALL]
0400 REM " --- Parameters"
0410 DIM P2$(10),P3$(325),P4$(32),P[4]
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N3$=F5$
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0450 IF P4$(2,1)<>"Y" THEN GOTO 4900
0460 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),MP=13
0480 FOR X=0 TO 4
0485 LET P[X]=NUM(P2$(X*2+1,2),ERR=0490)
0490 NEXT X
0495 LET P[2]=FNYY_YEAR(P2$(5,2))
0500 REM " --- Initializations"
0510 DIM A0$(2+P[4]),A1$(40),C0$(4),C1$(64),C2$(P[4]),C3$(P[4]),C[8]
0520 DIM B0$(3+P[4]),A[MP],U[MP]
0590 LET M4$="-##0.00%",ACTION$="R"
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,7,0
0750 PRINT 'SB',@(25,3),"Revision Code:",@(22,4),"Amounts or Units:",@(27,5),"Description:",@(26,6),"Column Title:",@(23,8),"Revision Source:",@(14,9),"Beginning Account Number:",@(17,10),"Ending Account Number:",@(25,11),"Wildcard Mask:",@(21,12),"Amount/Percentage:",@(33,13),"Value:",@(29,14),"Overwrite:",@(23,15),"Rounding Factor:",@(26,16),"Date Created:",@(25,17),"Last Revision:",'SF',
0900 REM " --- Position file"
0990 READ (GLM08_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Revision Code"
1005 PRINT 'CF',
1010 LET V2$="",V2$=KEY(GLM08_DEV,END=1040)
1020 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1030 IF V2$<>"" THEN LET C0$(3,1)=V2$(3,1),C0$(4,1)=V2$(4,1)
1040 LET V0$="S",V1$="C^",V2$=C0$(3,1),V3$="",V0=1,V1=40,V2=3,I0=0
1045 LET V4$="Enter The Revision Code (A through Z/<F3>=Lookup)"
1050 GOSUB 7000
1055 ON V3 GOTO 1080,1000,1000,1060,4500
1060 CALL "GLC.LA",ACTION$,SELECTION$,FLAGS$,DESCRIPTION$,TITLE$
1065 IF SELECTION$="" THEN GOTO 1000
1070 LET C0$(1,2)=N0$,C0$(3,1)=SELECTION$,C0$(4,1)=FLAGS$
1075 GOTO 1200
1080 IF POS(V$="ABCDEFGHIJKLMNOPQRSTUVWXYZ")=0 THEN GOTO 1000
1090 LET C0$(1,2)=N0$,C0$(3,1)=V$
1100 REM " --- Amounts or Units?"
1105 IF P4$(3,1)="Y" THEN GOTO 1120
1110 LET C0$(4,1)="A"
1115 GOTO 1200
1120 LET V4$="Amounts or Units (A/U)?"
1125 LET V0$="S",V1$="C^",V2$=C0$(4,1),V3$="",V0=1,V1=40,V2=4
1130 GOSUB 7000
1135 ON V3 GOTO 1140,1100,1000,1100,4500
1140 IF POS(V$="AU")=0 THEN GOTO 1100
1190 LET C0$(4,1)=V$
1200 REM " --- Retrieve record"
1210 READ (GLM08_DEV,KEY=C0$,DOM=1400)IOL=GLM08A
1220 READ RECORD(GLM08_DEV,KEY=C0$,DOM=1400)R9$
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1490,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record"
1510 DIM C1$(64),C2$(P[4]),C3$(P[4]),C[8]
1520 LET C4$="",C1$(46,6)=F0$(18,6),C1$(52,6)=F0$(18,6)
1530 LET C1$(58,1)="0",C1$(59,1)="A",C1$(60,1)="N",C1$(61,1)="C"
1590 GOSUB 5000
1600 REM " --- Description"
1610 LET V0=30,V0$="S",V1$="C",V2$=C1$(1,V0),V3$="",V1=40,V2=5,I0=2
1620 LET V4$="Enter The Description Of This Budget Revision"
1630 GOSUB 7000
1640 ON V3 GOTO 1650,1600,3900,1600,3900
1650 LET C1$(1,V0)=V$
1690 GOTO 3900
1700 REM " --- Column Description"
1710 LET V0=15,V0$="S",V1$="C",V2$=C1$(31,V0),V3$="",V1=40,V2=6
1720 LET V4$="Enter The Abbreviated Column Description Of This Budget Revision"
1730 GOSUB 7000
1740 ON V3 GOTO 1750,1700,3900,1700,3900
1750 LET C1$(31,V0)=V$
1790 GOTO 3900
1800 REM " --- Revision Source"
1810 LET V0=1,V0$="S",V1$="CR",V2$=C1$(58,V0),V3$="",V1=40,V2=8
1820 LET V4$="Enter The G/L Source ID For This Budget Revision (<F3>=Lookup)"
1830 GOSUB 7000
1840 ON V3 GOTO 1870,1800,3900,1850,3900
1850 LET ACTION$=""
1853 CALL "GLC.LA",ACTION$,V$,FLAG$,DESCRIPTION$,TITLE$
1855 IF V$<>"" THEN IF FLAG$<>C0$(4,1) THEN GOSUB 5400; GOTO 1800
1860 IF V$="" THEN GOTO 1800
1870 CALL "GLC.FA",GLM08_DEV,N0$,V$,C0$(4,1),DESCRIPTION$,X$,STATUS
1875 IF STATUS<>0 THEN GOTO 1800
1880 LET C1$(58,V0)=V$
1885 PRINT @(V1,V2),C1$(58,V0)," ",DESCRIPTION$,'CL',
1890 GOTO 3900
2000 REM " --- Beginning Account Number"
2010 LET V4$="Enter A Beginning Account Number (<F1>=First/<F3>=Lookup)"
2015 LET V0$="M",V1$="C",V2$=FNP$(C2$(1,P[3])),V3$=M0$,V0=P[4],V1=40,V2=9
2020 GOSUB 7000
2025 ON V3 GOTO 2060,2030,3900,2050,3900
2030 LET C2$(1)=""
2035 PRINT @(V1,V2),"First",'CL',
2040 GOTO 2090
2050 CALL "SYC.LG",V$
2055 IF V$="" THEN GOTO 2000
2060 IF V$="" THEN GOTO 2030
2065 GOSUB 5200
2070 IF STATUS>0 THEN GOTO 2000
2080 LET C2$(1)=A0$(3)
2085 PRINT @(V1,V2),DESCRIPTION$,'CL',
2090 GOTO 3900
2100 REM " --- Ending Account Number"
2110 LET V4$="Enter An Ending Account Number (<F1>=Last/<F3>=Lookup)"
2115 LET V0$="M",V1$="C",V2$=FNP$(C3$(1,P[3])),V3$=M0$,V0=P[4],V1=40,V2=10
2120 GOSUB 7000
2125 ON V3 GOTO 2160,2130,3900,2150,3900
2130 LET C3$(1)=""
2135 PRINT @(V1,V2),"Last",'CL',
2140 GOTO 2190
2150 CALL "SYC.LG",V$
2155 IF V$="" THEN GOTO 2100
2160 IF V$="" THEN GOTO 2130
2165 GOSUB 5200
2170 IF STATUS>0 THEN GOTO 2100
2175 IF V$<C2$ THEN GOTO 2100
2180 LET C3$(1)=A0$(3)
2185 PRINT @(V1,V2),DESCRIPTION$,'CL',
2190 GOTO 3900
2200 REM " --- Wildcard Mask"
2210 LET V4$="Enter The Wildcard Mask To Be Used In Selecting Accounts For This Revision"
2215 LET V0$="W",V1$="C",V2$=C4$,V3$=M0$,V0=P[4],V1=40,V2=11
2220 GOSUB 7000
2225 ON V3 GOTO 2230,2200,3900,2200,3900
2230 IF V$<>"" THEN GOTO 2250
2235 LET C4$=""
2245 GOTO 2280
2250 LET C4$=V$
2280 PRINT @(V1,V2),C4$,'CL',
2290 GOTO 3900
2300 REM " --- Amount/Percentage"
2310 LET V4$="Revise Source Information By Flat Amount Or Percentage (A=Amount/P=Percentage)"
2320 LET V0$="S",V1$="C",V2$=C1$(59,1),V3$="AP",V0=1,V1=40,V2=12
2330 GOSUB 7000
2340 ON V3 GOTO 2350,2300,3900,2300,3900
2350 IF POS(V$="AP")=0 THEN GOTO 2300
2360 LET C1$(59,1)=V$
2390 GOTO 3900
2400 REM " --- Value"
2410 LET V4$="Flat Amount",MASK$=M1$
2415 IF C1$(59,1)="P" THEN LET V4$="Percentage",MASK$=M4$
2420 LET V4$="Enter The "+V4$+" To Increase/Decrease Source Amount By"
2425 LET V0$="N",V1$="C",V2$=STR(C[0]),V3$=MASK$,V0=LEN(MASK$),V1=40,V2=13
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 LET C[0]=V
2480 PRINT @(V1,V2),C[0]:MASK$,'CL',
2490 GOTO 3900
2500 REM " --- Overwrite?"
2510 LET V4$="Overwrite Any Existing Records (Y/N)?"
2520 LET V0$="Y",V1$="C",V2$=C1$(60,1),V3$="",V0=1,V1=40,V2=14
2530 GOSUB 7000
2540 ON V3 GOTO 2550,2500,3900,2500,3900
2550 LET C1$(60,1)=V$
2590 GOTO 3900
2600 REM " --- Rounding Factor"
2610 LET V4$="Select The Rounding Factor To Use When Generating New Budget Amount"
2620 PRINT @(FNC(V4$,80),21),V4$,
2630 LET V4$="C=Cents   D=Dollars   H=Hundreds   T=Thousands"
2640 IF C0$(4,1)="U" THEN LET V4$="C=2 Decimal Places   D=Units   H=Hundreds   T=Thousands"
2650 LET V0$="S",V1$="C",V2$=C1$(61,1),V3$="CDHT",V0=1,V1=40,V2=15
2660 GOSUB 7000
2665 PRINT @(0,21),'CL',
2670 ON V3 GOTO 2680,2600,3900,2600,3900
2680 IF POS(V$="CDHT")=0 THEN GOTO 2600
2685 LET C1$(61,1)=V$
2690 GOTO 3900
3900 REM " --- Next field"
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,1600,1700,1800,2000,2100,2200,2300,2400,2500,2600,3990
3990 LET I0=0
4000 REM " --- Correct?"
4005 GOSUB 5000
4010 LET V0$="S",V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V1$="KC",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write"
4110 WRITE (GLM08_DEV,KEY=C0$)IOL=GLM08A
4120 READ RECORD(GLM08_DEV,KEY=C0$)N9$
4130 IF N9$=R9$ THEN GOTO 4190
4140 LET C1$(52,6)=F0$(18,6)
4150 WRITE (GLM08_DEV,KEY=C0$)IOL=GLM08A
4190 GOTO 1000
4200 REM " --- Remove"
4210 LET V4$="OK to Purge All Records For The Budget Revision"
4220 CALL "SYC.YN",0,V4$,0,V$,V3
4230 IF V$<>"YES" THEN GOTO 4000
4240 LET ZAP=0,K$=C0$(1,3)+"U"
4250 IF C0$(4,1)="U" THEN LET K$(4,1)="A"
4260 FIND (GLM08_DEV,KEY=K$,DOM=4280)
4270 LET ZAP=1
4280 PRINT @(28,18),'SB',"Processing:",'SF',
4290 READ (GLM01_DEV,KEY=N0$,DOM=4300)
4300 REM " --- Remove GLM-02 Budget Records"
4310 LET K$=KEY(GLM01_DEV,END=4400)
4320 IF POS(N0$=K$)<>1 THEN GOTO 4400
4330 READ (GLM01_DEV)
4335 PRINT @(40,18),FNF$(K$(3,P[3]),M0$),
4340 LET B0$(1)=K$,B0$(LEN(B0$),1)=C0$(3,1)
4350 FIND (GLM02_DEV,KEY=B0$,DOM=4390)IOL=GLM02A
4355 ON ZAP GOTO 4380,4360
4360 IF C0$(4,1)="A" THEN DIM A[MP]
4365 IF C0$(4,1)="U" THEN DIM U[MP]
4370 WRITE (GLM02_DEV,KEY=B0$)IOL=GLM02A
4375 GOTO 4390
4380 REMOVE (GLM02_DEV,KEY=B0$)
4390 GOTO 4300
4400 REM " --- Remove GLM-08 Budget Revision Record"
4410 REMOVE (GLM08_DEV,KEY=C0$,DOM=4420)
4420 PRINT @(0,18),'CE',
4490 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4600,9900
4600 REM " --- Run report overlay"
4690 RUN "GLM.MB"
4900 REM " --- Budgets not specified on parameter record"
4910 CALL "GLC.HA",STATUS
4990 GOTO 9900
5000 REM " --- Display"
5010 LET MASK$=M1$,BEGACCT$="First",ENDACCT$="Last"
5015 IF C1$(59,1)="P" THEN LET MASK$=M4$
5020 IF FNP$(C2$)="" THEN GOTO 5040
5025 LET V$=C2$
5030 GOSUB 5200
5035 LET BEGACCT$=DESCRIPTION$
5040 IF FNP$(C3$)="" THEN GOTO 5060
5045 LET V$=C3$
5050 GOSUB 5200
5055 LET ENDACCT$=DESCRIPTION$
5060 CALL "GLC.FA",GLM08_DEV,N0$,C1$(58,1),C0$(4,1),DESCRIPTION$,X$,STATUS
5070 PRINT @(40,3),C0$(3,1),@(40,4),C0$(4,1),@(40,5),C1$(1,30),@(40,6),C1$(31,15),@(40,8),C1$(58,1)," ",DESCRIPTION$,'CL',@(40,9),BEGACCT$,'CL',@(40,10),ENDACCT$,'CL',@(40,11),C4$,@(40,12),C1$(59,1),@(40,13),C[0]:MASK$,@(40,14),C1$(60,1),@(40,15),C1$(61,1),@(40,16),FNB6$(C1$(46,6)),@(40,17),FNB6$(C1$(52,6))
5090 RETURN 
5200 REM " --- Retrieve and return G/L Account"
5210 DIM A1$(40),DESCRIPTION$(38)
5220 LET A0$(1)=N0$+V$,A1$(1,35)="(Not On File)",STATUS=1
5230 FIND (GLM01_DEV,KEY=A0$,DOM=5250)IOL=GLM01A
5240 LET STATUS=0
5250 LET DESCRIPTION$(1)=FNF$(A0$(3,P[3]),M0$)+" "+A1$(1,35)
5290 RETURN 
5400 REM " --- Invalid source"
5410 DIM MESSAGE$[1]
5420 LET V$="Amounts",X$="Units"
5430 IF FLAG$="A" THEN LET V$="Units",X$="Amounts"
5440 LET MESSAGE$[0]=X$+" May Not Be Selected As The Source For A Budget Revision"
5450 LET MESSAGE$[1]="When The Output Will Be "+V$+" (<Enter>=Continue)"
5480 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
5490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

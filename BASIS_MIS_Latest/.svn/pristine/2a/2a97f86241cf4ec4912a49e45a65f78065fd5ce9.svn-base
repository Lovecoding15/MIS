0010 REM "ARM - Distribution Code Maintenance"
0020 REM "Program ARM.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0140 LET FILES$[1]="ARM-10",FILES$[2]="SYS-01",FILES$[3]="GLM-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0185 LET ARM10_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists" 
0210 ARM10D: IOLIST A0$(1),A1$(1)
0220 GLM01A: IOLIST B0$(1),B1$(1)
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0405 DIM G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 LET G[3]=NUM(G2$(7,2)),G[4]=NUM(G2$(9,2))
0460 CALL "SYC.VA","PO",INFO$[ALL]
0470 LET PO$=INFO$[20]
0500 REM " --- Init Data"
0510 DIM A0$(30),A1$(11*G[4]),B0$(12),B1$(35)
0550 LET CHANNEL=ARM10_DEV,RECORD$="D",TITLE$="AR Distribution Codes",GL$="N"
0560 LET P$="Enter A Valid GL Account (<F3>=Lookup)",G5=LEN(G5$)
0590 LET K0=1,K9=5,A0$(1,3)=N0$+RECORD$,MODULES$="AROPIVWOBMPO"
0600 REM " --- Additional File Opens"
0610 FOR X=1 TO LEN(MODULES$) STEP 2
0620 IF GL$="Y" THEN GOTO 0650
0630 CALL "SYC.VA",MODULES$(X,2),INFO$[ALL]
0640 LET GL$=INFO$[9]
0650 NEXT X
0660 IF GL$<>"Y" THEN GOTO 0700
0670 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS THEN GOTO 9900
0690 LET GLM01_DEV=CHANNELS[3]
0700 REM " --- Background"
0710 PRINT 'SB',@(14,3),"Distribution Code:",@(20,4),"Description:",'SF',
0720 IF GL$<>"Y" THEN GOTO 0800
0730 PRINT 'SB',@(4,5),"Accounts Receivable Account:",@(19,6),"Cash Account:",@(18,7),"Sales Account:",@(15,8),"Discount Account:",@(16,9),"Freight Account:",@(14,10),"Inventory Account:",@(5,11),"Cost Of Goods Sold Account:",'SF',
0740 IF PO$<>"Y" THEN GOTO 0900
0750 PRINT 'SB',@(3,12),"Inventory Adjustment Account:",@(8,13),"COGS Adjustment Account:",@(14,14),"Purchases Account:",@(0,15),"Purchase Price Variance Account:",'SF',
0900 REM " --- Init File"
0990 READ (ARM10_DEV,KEY=A0$(1,3),DOM=1000)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(ARM10_DEV,END=1050)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4)
1050 LET V0$="S",V1$="KEC",V3$="",V0=2,V1=33,V2=3,V4$="Enter A Valid Distribution Code (<F3>=Lookup)"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN GOSUB 6000
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(4)=V$
1200 REM " --- Find Key"
1210 FIND (ARM10_DEV,KEY=A0$(1,K9),DOM=1400)IOL=ARM10D
1300 REM " --- Display"
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-up"
1510 LET A0$(6)="",A1$(1)=""
2000 REM " --- Description"
2010 LET V0$="S",V1$="",V2$=A0$(6,20),V3$="",V0=20,V1=33,V2=4,I0=0
2020 LET V4$="Enter The Description For This Distribution Code"
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,1000,2000,3900
2050 LET A0$(6,20)=V$
2090 GOTO 3900
2200 REM " --- AR Account"
2210 LET X=1,V2=5
2220 GOSUB 6200
2290 GOTO 3900
2400 REM " --- Cash Account"
2410 LET X=11,V2=6
2420 GOSUB 6200
2490 GOTO 3900
2600 REM " --- Sales Account"
2610 LET X=21,V2=7
2620 GOSUB 6200
2690 GOTO 3900
2800 REM " --- Discount Account"
2810 LET X=31,V2=8
2820 GOSUB 6200
2890 GOTO 3900
3000 REM " --- Freight Account"
3010 LET X=41,V2=9
3020 GOSUB 6200
3090 GOTO 3900
3200 REM " --- Inventory Account"
3210 LET X=61,V2=10
3220 GOSUB 6200
3290 GOTO 3900
3400 REM " --- COGS Account"
3410 LET X=71,V2=11
3420 GOSUB 6200
3490 GOTO 3900
3500 REM " --- Inventory Adjustment Account"
3505 IF PO$<>"Y" THEN GOTO 4000
3510 LET X=51,V2=12
3520 GOSUB 6200
3590 GOTO 3900
3600 REM " --- COGS Adjustment Account"
3610 LET X=81,V2=13
3620 GOSUB 6200
3690 GOTO 3900
3700 REM " --- Purchases Account"
3710 LET X=101,V2=14
3720 GOSUB 6200
3790 GOTO 3900
3800 REM " --- Purchases Price Variance Account"
3810 LET X=91,V2=15
3820 GOSUB 6200
3890 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,2400,2600,2800,3000,3200,3400,3500,3600,3700,3800,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4110 WRITE (ARM10_DEV,KEY=A0$(1,K9))IOL=ARM10D
4190 GOTO 1000
4200 REM " --- Delete"
4210 REMOVE (ARM10_DEV,KEY=A0$(1,K9),DOM=4220)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4600,9900
4600 REM " --- Report Initializations"
4610 LET PROGRAM$="",TOTAL$=TITLE$,NUMBER=8,HEADINGS=3,WIDTH=132
4615 IF GL$<>"Y" THEN LET NUMBER=1,WIDTH=80
4620 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4630 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4640 DIM HEADINGS$[HEADINGS]
4650 LET FIELD$[0]="Code",FIELD[0]=1,POSITION[0]=4
4660 LET LENGTH[0]=2,TYPE$[0]="S",MASK$[0]=""
4670 LET FIELD$[1]="Description",FIELD[1]=1,POSITION[1]=6
4680 LET LENGTH[1]=20,TYPE$[1]="S",MASK$[1]=""
4685 IF GL$<>"Y" THEN GOTO 4900
4690 LET FIELD$[2]="A/R Account  ",FIELD[2]=2,POSITION[2]=1
4700 LET LENGTH[2]=G[3],TYPE$[2]="M",MASK$[2]=G5$
4710 LET FIELD$[3]="Cash Account ",FIELD[3]=2,POSITION[3]=11
4720 LET LENGTH[3]=G[3],TYPE$[3]="M",MASK$[3]=G5$
4730 LET FIELD$[4]="Sales Account",FIELD[4]=2,POSITION[4]=21
4740 LET LENGTH[4]=G[3],TYPE$[4]="M",MASK$[4]=G5$
4750 LET FIELD$[5]="Disc Account ",FIELD[5]=2,POSITION[5]=31
4760 LET LENGTH[5]=G[3],TYPE$[5]="M",MASK$[5]=G5$
4770 LET FIELD$[6]="Frgt Account ",FIELD[6]=2,POSITION[6]=41
4780 LET LENGTH[6]=G[3],TYPE$[6]="M",MASK$[6]=G5$
4790 LET FIELD$[7]="Inv Account  ",FIELD[7]=2,POSITION[7]=61
4800 LET LENGTH[7]=G[3],TYPE$[7]="M",MASK$[7]=G5$
4810 LET FIELD$[8]="COGS Account ",FIELD[8]=2,POSITION[8]=71
4820 LET LENGTH[8]=G[3],TYPE$[8]="M",MASK$[8]=G5$
4900 REM 
4970 LET HEADINGS$[0]=N1$,HEADINGS$[1]=TOTAL$+" Listing"
4980 IF GL$="Y" THEN IF PO$="Y" THEN RUN "ARM.DB"
4990 RUN "SYR.AA"
5000 REM " --- Display"
5010 PRINT @(33,3),A0$(4,2),@(33,4),A0$(6,20),
5020 IF GL$<>"Y" THEN GOTO 5190
5030 LET V$=A1$(1,10),V1=33,V2=5
5035 GOSUB 5200
5040 LET V$=A1$(11,10),V2=6
5045 GOSUB 5200
5050 LET V$=A1$(21,10),V2=7
5055 GOSUB 5200
5060 LET V$=A1$(31,10),V2=8
5070 GOSUB 5200
5075 LET V$=A1$(41,10),V2=9
5080 GOSUB 5200
5085 LET V$=A1$(61,10),V2=10
5090 GOSUB 5200
5095 LET V$=A1$(71,10),V2=11
5100 GOSUB 5200
5105 IF PO$="N" THEN GOTO 5190
5110 LET V$=A1$(51,10),V2=12
5115 GOSUB 5200
5120 LET V$=A1$(81,10),V2=13
5125 GOSUB 5200
5130 LET V$=A1$(101,10),V2=14
5135 GOSUB 5200
5140 LET V$=A1$(91,10),V2=15
5145 GOSUB 5200
5190 RETURN 
5200 REM " --- Read/display GL account"
5210 LET B0$(1)=N0$+V$,B1$(1)="",STATUS=11,DW=MIN(35,45-G5)
5220 FIND (GLM01_DEV,KEY=B0$,DOM=5290)IOL=GLM01A
5230 PRINT @(V1,V2),FNF$(B0$(3,G[3]),G5$)," ",B1$(1,DW),
5240 LET STATUS=0
5290 RETURN 
6000 REM " --- Code Lookup"
6010 LET NUMBER=1
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6050 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6060 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,V$="SKIP",ROW=V2+2
6080 CALL "SYC.LK",ARM10_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
6200 REM " --- GL Account Number Input"
6210 IF GL$<>"Y" THEN GOTO 6290
6220 LET V0$="M",V1$="RC",V2$=A1$(X,G[3]),V3$=G1$,V4$=P$,V0=G[4],V1=33
6230 GOSUB 7000
6235 ON V3 GOTO 6250,6200,6290,6240,6290
6240 CALL "SYC.LG",V$
6250 IF V$="" THEN GOTO 6200
6260 GOSUB 5200
6270 IF STATUS THEN GOTO 6200
6280 LET A1$(X,10)=V$
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

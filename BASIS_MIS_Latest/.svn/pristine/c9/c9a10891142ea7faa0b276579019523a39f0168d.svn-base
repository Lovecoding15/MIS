0010 REM "IVP - Parameter Maintenance (Display Columns)"
0020 REM "Program IVP.AD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 IVS01D: IOLIST D0$,D1$(1)
0500 REM " --- Initializations"
0510 DIM D1$(36)
0590 LET D0$=N0$+N2$+"03"
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0720 CALL "SYC.WC",1,0,80,0,0,12,0
0730 PRINT 'SB',@(27,3),"Inventory Activity Inquiry",@(9,5),"Column",@(18),"Type",@(25),"Description",@(58),"Column Title",@(26,13),"Lot/Serial Activity Inquiry",@(9,15),"Column",@(18),"Type",@(25),"Description",@(58),"Column Title",'SF',
0740 FOR X=1 TO 6
0750 PRINT 'SB',@(9,5+X),X,@(9,15+X),X,'SF',
0760 NEXT X
1200 REM " --- Retrieve record"
1210 FIND (SYS01_DEV,KEY=D0$,DOM=1400)IOL=IVS01D
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1500 REM " --- Initialize new record"
1510 DIM D1$(36)
1550 LET D1$(1)="IN OUTADJXFRRETEND",D1$(19)=D1$,I0=0
1590 GOSUB 5000
1600 REM " --- Display column"
1610 IF LS$<>"Y" THEN IF I0>5 THEN GOTO 3900
1620 LET X=I0+1,V2=6+I0
1630 IF I0>5 THEN LET X=I0-5,V2=15+X
1640 LET V4$="Select The Information To Be Displayed In Column "+STR(X)+" (<F3>=Lookup)"
1650 LET V0$="S",V1$="C",V2$=D1$(I0*3+1,3),V3$="",V0=3,V1=18
1660 GOSUB 7000
1670 ON V3 GOTO 1700,1600,3900,1680,3900
1680 CALL "IVC.LC",V$
1700 REM " --- Verify"
1710 LET V$=V$+"   ",V$=V$(1,3)
1720 CALL "IVC.FA",V$,V3$,V4$,V
1730 IF V>0 THEN GOTO 1600
1740 LET D1$(I0*3+1,3)=V$
1750 PRINT @(V1,V2),'CL',V$,@(25),V3$,@(58),V4$
1790 GOTO 3900
3900 REM " --- Next field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1600,1600,1600,1600,1600,1600,1600,1600,1600,1600,1600,1600,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?",I0=0
4020 LET V0$="S",V1$="KC",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write"
4110 WRITE (SYS01_DEV,KEY=D0$)IOL=IVS01D
4190 GOTO 4900
4200 REM " ---- Remove"
4210 REMOVE (SYS01_DEV,KEY=D0$,DOM=4220)
4290 GOTO 4900
4900 REM " --- Run menu overlay"
4990 RUN "IVP.AA"
5000 REM " --- Display"
5010 FOR X=1 TO 6
5020 LET V1$=D1$((X-1)*3+1,3),V2$=D1$((X-1)*3+19,3)
5030 CALL "IVC.FA",V1$,V3$,V4$,V
5040 PRINT @(18,5+X),'CL',V1$,@(25),V3$,@(58),V4$
5050 CALL "IVC.FA",V2$,V3$,V4$,V
5060 PRINT @(18,15+X),'CL',V2$,@(25),V3$,@(58),V4$
5080 NEXT X
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9220 GOTO 4900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

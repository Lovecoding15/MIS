0010 REM "SAC - Sales Analysis Field Entry"
0020 REM "Program SAC.IA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "25Jun96 WGH: <F3> lookups repeat a second time (6390)"
0032 REM "03Sep96 WGH: Distribution Code lookup display is incorrect (6330-6340)"
0033 REM "01Sep98 WGH: Ship-To description not displayed correctly (6120-6140,6470)
0040 REM 
0050 REM "F1KEY$         : How the <F1> key is handled (Input)
0051 REM "                 I = Inquiry, catch <F1> for Sales Analysis Lookup
0052 REM "                 R = Report, pass <F1> back to caller in V3
0053 REM "TYPE$          : V0$ field type for SYC.IA (Input)
0054 REM "DEFAULT$       : V2$ field default for SYC.IA (Input)
0055 REM "IMASK$         : V3$ field input mask for SYC.IA (Input)
0056 REM "OMASK$         : Output display mask (Input)
0057 REM "PROMPT$        : V4$ field prompt to display for SYC.IA (Input)
0058 REM "FLDLEN         : Length of key segment (Input)
0059 REM "COLUMN         : V1 field column position for SYC.IA (Input)
0060 REM "ROW            : V2 field row position for SYC.IA (Input)
0061 REM "INLEN          : Number of characters in input field (Input)
0062 REM "N0$            : Firm ID (Input)
0063 REM "DESCCHAN       : Channel of file to obtain description (Input)
0064 REM "DESCREC$       : ID of record to obtain description (Input)
0065 REM "DESCSTR        : IOLIST string containing description (Input)
0066 REM "DESCPOS        : Start position of description in DESCSTR (Input)
0067 REM "DESCLEN        : Length of description in DESCSTR (Input)
0068 REM "IV_DESC[n]     : Inventory item description parameters (Input)
0069 REM "                  [0]        = Item number length
0070 REM "                  [1] to [3] = Description segment lengths
0071 REM "IVM14_DEV      : IVM-14 channel (Input)
0072 REM "FY$            : Current Sales Analysis fiscal year (Input)
0073 REM "FIELD          : Current segment field number (Input)
0074 REM "SAM_DEV        : Channel of sales analysis drive file (Input)
0075 REM "LITERALS$[x]   : Segment field literals (Input)
0076 REM "FIELDS$[x]     : Segment fields (Input/Returned)
0077 REM "V3             : V3 function key value from SYC.IA (Returned)
0079 REM 
0080 SETESC 9000
0085 SETERR 9000
0090 ENTER F1KEY$,TYPE$,DEFAULT$,IMASK$,OMASK$,PROMPT$,FLDLEN,COLUMN,ROW,INLEN,N0$,DESCCHAN,DESCREC$,DESCSTR,DESCPOS,DESCLEN,IV_DESC[ALL],IVM14_DEV,FY$,FIELD,SAM_DEV,LITERALS$[ALL],FIELDS$[ALL],V3
0200 REM " --- IOLists
0220 DESCCHANA: IOLIST X$[ALL]
0400 REM " --- Parameters"
0410 IF F1KEY$<>"I" THEN LET F1KEY$="R"
0500 REM " --- Initialize Data"
0510 LET BAD_SELECTION=0
0520 LET V0$=TYPE$,V1$="C",V2$=DEFAULT$,V3$=IMASK$,V4$=PROMPT$,V0=FLDLEN,V1=COLUMN,V2=ROW
0900 REM " --- Field Type"
0910 IF LITERALS$[FIELD]="Item" THEN GOTO 1200
1000 REM " --- Get Segment Field"
1030 GOSUB 7000
1040 ON V3 GOTO 1070,1050,1090,1060,1090
1050 IF F1KEY$="I" THEN GOSUB F1_LOOKUP; IF V$="" THEN GOTO 0500
1055 GOTO 1070
1060 GOSUB F3_LOOKUP; IF V$="" THEN GOTO 0500
1070 GOSUB FILE_VERIFY
1080 IF BAD_SELECTION THEN GOTO 0500
1090 GOTO 4000
1200 REM " --- Get Item Field"
1205 DIM IV_FILES[14],IV_ITEM$[6],IV_ITEM[12]
1210 LET IV_FILES[1]=DESCCHAN,IV_FILES[14]=IVM14_DEV
1220 LET V0=INLEN
1230 CALL "IVC.IA","N",V1,V2,IV_FILES[ALL],N0$,V2$,V4$,IV_DESC[ALL],IV_ITEM$[ALL],IV_ITEM[ALL],V3
1235 LET V$=IV_ITEM$[0]
1240 ON V3 GOTO 1270,1250,1290,1260,1290
1250 IF F1KEY$="I" THEN GOSUB F1_LOOKUP; IF V$="" THEN GOTO 0500
1255 GOTO 1270
1260 GOSUB F3_LOOKUP; IF V$="" THEN GOTO 0500
1270 GOSUB FILE_VERIFY
1280 IF BAD_SELECTION THEN GOTO 0500
1290 GOTO 4000
4000 REM " --- All Done"
4020 IF F1KEY$="R" AND V$="" THEN GOTO 4080
4040 IF V$="" THEN LET V$=DEFAULT$
4060 LET V$=V$+FILL(FLDLEN),V$=V$(1,FLDLEN)
4080 LET FIELDS$[FIELD]=V$,DEFAULT$=V$;rem FIELDS$[FIELD] and DEFAULT$ are the same variable, passed by the calling program as FIELDS$[ALL] and FIELDS$[FIELD]
4090 GOTO 9900
6000 REM " --- File Verify Selection"
6001 FILE_VERIFY: 
6005 IF F1KEY$="R" AND V$="" THEN GOTO 6090
6010 LET BAD_SELECTION=1
6015 DIM X$[DESCSTR],X$[DESCSTR](DESCPOS+DESCLEN),DESC$(80-V1),FLD$(INLEN)
6020 LET V$=V$+FILL(FLDLEN),V$=V$(1,FLDLEN)
6025 IF DESCCHAN=0 THEN GOTO 6050
6030 IF LITERALS$[FIELD]="Customer Ship-To" THEN LET DESCREC$=FIELDS$[FIELD-1]
6035 LET X$[DESCSTR](DESCPOS)="Unknown "+LITERALS$[FIELD]
6040 READ (DESCCHAN,KEY=N0$+DESCREC$+V$,DOM=6100)IOL=DESCCHANA
6050 LET DESC$(1)=X$[DESCSTR](DESCPOS,DESCLEN)
6060 LET FLD$=V$
6070 IF V0$="M" THEN LET FLD$=FNF$(FLD$(1,INLEN),OMASK$)
6075 IF LITERALS$[FIELD]="Item" THEN LET DESC$(1)=FNITEM$(X$[DESCSTR](DESCPOS,DESCLEN),IV_DESC[1],IV_DESC[2],IV_DESC[3]),FLD$=FLD$(1,INLEN)
6080 LET DESC$(1)=FLD$+"  "+DESC$
6085 PRINT @(V1,V2),DESC$,
6090 LET BAD_SELECTION=0
6095 RETURN 
6100 REM " --- Accept Unknown Record?"
6120 IF FNP$(V$)="" AND LITERALS$[FIELD]="Customer Ship-To" THEN LET DESC$(1)="Bill-To Address"; GOTO 6060
6140 IF V3=1 THEN GOTO 6050
6190 RETURN 
6200 REM " --- <F3> Lookup"
6205 F3_LOOKUP: 
6210 IF LITERALS$[FIELD]="Customer" THEN CALL "SYC.LC",V$; RETURN 
6220 IF LITERALS$[FIELD]="Item" THEN CALL "SYC.LI",V$; RETURN 
6230 IF LITERALS$[FIELD]="Non-Stock Item" THEN RETURN 
6240 IF LITERALS$[FIELD]="Product Type" THEN CALL "IVC.LK",1,V1,V2,V$; RETURN 
6250 IF LITERALS$[FIELD]="Customer Ship-To" THEN CALL "SYC.LT","S",FIELDS$[FIELD-1],V$; RETURN 
6260 IF LITERALS$[FIELD]="SIC" THEN RETURN 
6270 IF LITERALS$[FIELD]="Vendor" THEN CALL "SYC.LV",V$; RETURN 
6280 IF LITERALS$[FIELD]="Warehouse" THEN CALL "IVC.LK",3,V1,V2,V$; RETURN 
6300 REM " --- Standard Code Lookup"
6310 LET NMBR=1
6320 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6330 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=FLDLEN
6340 LET DESCRIPT$[1]="Description",FIELD[1]=1,POSITION[1]=POSITION[0]+LENGTH[0],LENGTH[1]=20
6350 CALL "SYC.LK",DESCCHAN,DESCREC$,NMBR,LITERALS$[FIELD]+" Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],1,3,V$
6390 RETURN 
6400 REM " --- <F1> Lookup"
6405 F1_LOOKUP: 
6420 LET PREFIX$=N0$+FY$
6430 IF FIELD=1 THEN GOTO 6480
6440 FOR X=1 TO FIELD-1
6450 LET PREFIX$=PREFIX$+FIELDS$[X]
6460 NEXT X
6470 IF LITERALS$[FIELD]="Customer Ship-To" THEN LET DESCREC$=FIELDS$[FIELD-1]
6480 CALL "SAC.LS",N0$,SAM_DEV,LITERALS$[FIELD],V0$,IMASK$,OMASK$,FLDLEN,INLEN,DESCCHAN,DESCREC$,DESCSTR,DESCPOS,DESCLEN,IV_DESC[ALL],PREFIX$,V$
6490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9800
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Return Error Encountered"
9810 LET V3=4
9900 REM " --- Return to Calling Program"
9950 EXIT 
9999 END

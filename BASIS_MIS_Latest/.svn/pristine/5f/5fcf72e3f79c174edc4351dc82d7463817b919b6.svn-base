0010 REM "APM - DEPOSITS/SERVICE CHARGES"
0020 REM "Program APM.EC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0310 APM12A: IOLIST W0$(1),W1$(1),W[ALL]
0500 REM " --- Init"
0515 LET L0=6
0520 LET W0$(1)=A0$(1,2+P[4]),T0=0,L=L0+2,L9=21
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,7,0
0740 PRINT 'SB',@(0,6)," Number  Type  Trans Date  Description                        Amount   Posted",'SF',
0920 GOSUB 5100
0940 GOTO 4000
1000 REM " --- Overflow"
1005 GOTO 1020
1010 IF Q9$="R" THEN GOTO 1400 ELSE READ (APM11_DEV,KEY=A0$(1,2+P[4]),DOM=1015)
1020 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L0+2),'LD',
1040 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1120 LET K$=KEY(APM12_DEV,END=1400)
1125 LET W0$(3+P[4])=""
1130 IF K$(1,2)<>A0$(1,2) THEN GOTO 1400
1140 IF K$(3,P[4])<>A0$(3,P[4]) THEN GOTO 1400
1150 READ (APM12_DEV)IOL=APM12A
1160 IF Q9$="R" THEN GOTO 1490
1400 REM " --- Reference Number"
1410 LET V4$="Enter a Reference Code of Up to Seven Characters"
1420 LET V0$="S",V1$="KER",V2$=W0$(3+P[4],7),V3$="",V0=7,V1=0,V2=L,I0=0
1430 GOSUB 7000
1440 IF V3=4 THEN GOTO 4000
1450 IF V$="" THEN GOTO 1400
1460 LET W0$(3+P[4],7)=V$
1480 FIND (APM12_DEV,KEY=W0$,DOM=1600)IOL=APM12A
1485 IF POS(Q9$="PN")<>0 THEN GOTO 1500
1490 IF W0$(1,9+P[4])<=K9$ THEN GOSUB 5400
1500 GOSUB 5000
1520 LET I0=2
1525 IF Q9$="P" THEN LET I0=4; GOTO 2400
1530 GOTO 3300
1600 REM " --- Setup"
1620 LET W1$(1)=""
1640 DIM W[2]
2000 REM " --- TYPE"
2020 LET V0$="S",V1$="RC",V2$=W1$(1,1),V0=1,V1=11,I0=0,V3$="DSA",V4$="D=Deposit  S=Service Charge  A=Adjustment"
2030 GOSUB 7000
2040 IF POS(V$="DSA")=0 THEN GOTO 2000
2060 LET W1$(1,1)=V$
2090 GOTO 3200
2100 REM " --- Trans Date"
2110 LET V4$="Enter the Date of This Transaction"
2120 LET V0$="D",V1$="R6N",V2$=W1$(3,6),V0=8,V1=16
2130 GOSUB 7000
2140 IF V3=2 THEN GOTO 2190
2160 LET W1$(3,6)=V$
2190 GOTO 3200
2200 REM " --- Description"
2210 LET V4$="Enter a Description of Up to Thirty Characters"
2220 LET V0$="S",V1$="",V2$=W1$(9,30),V0=30,V1=27
2230 GOSUB 7000
2260 LET W1$(9,30)=V$
2290 GOTO 3200
2300 REM " --- Amount"
2310 LET V4$="Reductions to an Account Must Be Entered as a Negative"
2320 LET V0$="N",V1$="",V2$=STR(W[0]),V3$=M1$,V1=69-M1,V0=M1
2330 GOSUB 7000
2360 LET W[0]=V
2390 GOTO 3200
2400 REM " --- Paid"
2420 LET V0$="S",V1$="RC",V2$=W1$(2,1),V3$="OP",V4$="O=Outstanding  P=Posted",V0=1,V1=72,V2=L
2425 IF V2$=" " THEN LET V2$="O"
2430 GOSUB 7000
2460 LET W1$(2,1)=V$
2490 GOTO 3200
3200 REM " --- Seq Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 2000,2100,2200,2300,2400,3300
3300 REM " --- Correct"
3320 LET V0$="S",V1$="C",V2$="",V0=1,V1=78,V2=L,K9$=W0$(1,9+P[4]),I0=5
3325 LET V4$="<Enter>=OK     <F2>=Change     <D>=Delete"
3330 GOSUB 7000
3340 IF V3=2 THEN GOTO 3200
3350 IF V$="" THEN GOTO 3400
3360 IF V$="D" THEN GOTO 3700
3370 IF V$="M" THEN GOTO 3800
3380 GOTO 3300
3400 REM " --- Write"
3420 WRITE (APM12_DEV,KEY=W0$)IOL=APM12A
3450 LET L=L+1
3460 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3700 REM " --- Delete"
3720 REMOVE (APM12_DEV,KEY=W0$,DOM=3730)
3750 PRINT @(0,L),'CL',
3760 IF V3=4 THEN GOTO 4000
3790 GOTO 1000
4000 REM " --- Options"
4010 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/Review/List/Post)?",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4025 LET Q9$=V$
4030 IF V$="P" OR V$="POST" THEN LET Q9$="P"; GOTO 4700
4040 IF V$="R" OR V$="REVIEW" THEN LET Q9$="R"; GOTO 4600
4050 IF V$="L" OR V$="LIST" THEN LET Q9$="L"; GOTO 4400
4070 ON POS(V$="YN") GOTO 4000,4100,1000
4180 LET O0=1
4190 RUN "APM.EA"
4400 REM " --- List"
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4620 READ (APM12_DEV,KEY=A0$(1,2+P[4]),DOM=4630)
4640 GOSUB 5400
4690 GOTO 1000
4700 REM " --- Pay, Correct"
4710 IF L>20 THEN LET L=L9; PRINT @(0,L0+2),'LD',
4715 IF L=L9 THEN GOTO 1400
4720 LET K$=KEY(APM12_DEV,END=1400)
4730 IF K$(1,2+P[4])<>A0$(1,2+P[4]) THEN GOTO 4770
4740 READ (APM12_DEV)IOL=APM12A
4750 GOTO 1400
4770 READ (APM12_DEV,KEY=A0$(1,2+P[4]),DOM=4780)
4790 GOTO 4700
5000 REM " --- Display"
5020 PRINT @(0,L),'CL',W0$(3+P[4],7),@(11,L),W1$(1,1),@(16,L),FNB6$(W1$(3,6)),@(27,L),W1$(9,30),
5030 PRINT @(69-M1,L),W[0]:M1$,
5040 PRINT @(72,L),W1$(2,1),
5090 RETURN 
5100 REM " --- List"
5110 GOSUB 5400
5120 READ (APM12_DEV,KEY=A0$(1,2+P[4]),DOM=5130)
5130 LET K$=KEY(APM12_DEV,END=5300)
5150 IF K$(1,2+P[4])<>A0$(1,2+P[4]) THEN GOTO 5300
5160 READ (APM12_DEV)IOL=APM12A
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Press <Enter> To Continue:",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5225 IF V3=4 THEN GOTO 5300
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+1,K9$=W0$(1,9+P[4])
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5420 PRINT @(0,L0+2),'CE',
5440 LET L=L0+2
5490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

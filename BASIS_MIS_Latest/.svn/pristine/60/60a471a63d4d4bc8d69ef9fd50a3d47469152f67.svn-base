0010 REM "POE - Quality Assurance Receipt Entry (Rejection Codes)"
0020 REM "Program POE.CD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLists"
0210 POE63A: IOLIST T0$(1),T1$(1),T2$,T[ALL]
0240 POM03A: IOLIST H0$,H1$(1),H2$
0500 REM " --- Initialize Data"
0510 PRECISION I[1]
0520 DIM H1$(32),T0$(28),T1$(32),T[6]
0530 LET L8=20,Q_COL=MAX(43,Q_COL)
0540 LET T0$(1)=W0$(1,25),TOT_QNTY=0,DFLT_INTLS$=""
0700 REM " --- Background"
0710 PRINT @(0,L8+1),'CE',
0720 WHILE L>L2
0730 PRINT @(0,L1),'LD',
0740 LET L=L-1
0750 WEND
0910 GOSUB 5100
0920 LET TOT_QNTY=0
0930 READ (POE63_DEV,KEY=W0$(1,25),DOM=0940)
0940 LET K$=KEY(POE63_DEV,END=0990)
0950 IF POS(W0$(1,25)=K$)<>1 THEN GOTO 0990
0960 READ (POE63_DEV)IOL=POE63A
0970 LET TOT_QNTY=TOT_QNTY+T[0]
0980 GOTO 0940
0990 IF TOT_QNTY=QNTY THEN GOTO 4000
1000 REM " --- Overflow"
1020 IF L>L8 THEN PRINT @(0,L8+1),'CE',@(0,L2),'LD',; LET L=L-1
1040 IF V3=4 THEN GOTO 4000
1100 REM " --- Next Key
1120 LET K$=KEY(POE63_DEV,END=1400)
1140 IF POS(W0$(1,20)=K$)<>1 THEN GOTO 1400
1160 READ (POE63_DEV)IOL=POE63A
1180 GOTO 1490
1400 REM " --- Seq"
1410 IF TOT_QNTY<QNTY THEN LET T0=NUM(T0$(26,3))+1 ELSE LET T0=0
1420 LET V0$="Z",V1$="KCER",V2$=STR(T0:"000"),V3$="",V0=3,V1=4,V2=L,I0=0
1425 LET V4$="Enter A Sequence Number (<Enter>=Next)"
1430 GOSUB 7000
1435 IF V3=2 THEN GOTO 1420
1440 IF V3=4 THEN GOTO 4000
1450 IF NUM(V$)=0 THEN GOTO 1400
1460 LET T0$(26,3)=V$
1480 FIND (POE63_DEV,KEY=T0$,DOM=1600)IOL=POE63A
1490 LET SEQ_QNTY=T[0]
1500 GOSUB 5000
1530 GOTO 3300
1600 REM " --- Setup"
1610 IF TOT_QNTY=QNTY THEN GOTO 4000
1620 DIM T1$(32),T[6]
1630 LET T[0]=QNTY-TOT_QNTY
1640 LET SEQ_QNTY=0
2000 REM " --- Enter Rejection Code"
2020 LET V0$="S",V1$="CE",V2$=T1$(1,3),V3$="",V4$="Enter A Valid Rejection Code (<F3>=Lookup)",V0=3,V1=8,V2=L,I0=1
2030 GOSUB 7000
2040 ON V3 GOTO 2070,2000,3200,2050,3200
2050 GOSUB 6100; IF V$="" THEN GOTO 2000
2060 LET V$=V$+FILL(3),V$=V$(1,3)
2070 FIND (POM03_DEV,KEY=N0$+V$,DOM=2000)IOL=POM03A
2075 LET T1$(1,3)=V$
2080 GOSUB 5000
2090 GOTO 3200
2200 REM " --- Rejected Quantity"
2220 LET V0$="N",V1$="C",V2$=STR(T[0]),V3$=M2$,V4$="Enter The Quantity Rejected",V0=M2,V1=Q_COL,V2=L
2230 GOSUB 7000
2240 ON V3 GOTO 2260,2200,3200,2200,3200
2260 IF V<0 OR V>QNTY THEN GOTO 2200
2270 LET T[0]=V
2280 PRINT @(V1,V2),T[0]:M2$,
2290 GOTO 3200
2400 REM " --- Return Authorization Number"
2420 LET V0$="S",V1$="C",V2$=T1$(7,15),V3$="",V4$="Enter An Optional Return Authorization Number",V0=15,V1=Q_COL+1+M2,V2=L
2430 GOSUB 7000
2440 ON V3 GOTO 2460,2400,3200,2400,3200
2460 LET T1$(7,15)=V$
2490 GOTO 3200
2600 REM " --- Inspector's Initials"
2610 IF FNP$(T1$(4,3))="" THEN LET T1$(4,3)=DFLT_INTLS$
2620 LET V0$="S",V1$="C",V2$=T1$(4,3),V3$="",V4$="Enter The Inspector's Initials ",V0=3,V1=Q_COL+1+M2+16,V2=L
2630 GOSUB 7000
2640 ON V3 GOTO 2660,2600,3200,2600,3200
2660 LET T1$(4,3)=V$
2690 GOTO 3200
3200 REM " --- Seq Control"
3210 IF V3=4 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,2000,2200,2400,2600,3300
3300 REM " --- Correct"
3320 LET V0$="",V2$="",V3$="",V0=1,V1=78,V2=L,V4$="<Enter>=OK     <D>=Delete",I0=5,K9$=T0$
3330 GOSUB 7000
3340 ON V3 GOTO 3360,3300,3200,3300,3350
3350 LET V$=""
3360 IF V$="" THEN GOTO 3400
3370 IF V$="D" THEN GOTO 3500
3390 GOTO 3300
3400 REM " --- Write"
3420 LET TOT_QNTY=TOT_QNTY-SEQ_QNTY+T[0]
3440 WRITE (POE63_DEV,KEY=T0$)IOL=POE63A
3460 LET L=L+1
3480 PRINT @(0,21),'CE',
3485 IF TOT_QNTY=QNTY THEN GOTO 4000
3490 GOTO 1000
3500 REM " --- Delete"
3520 LET TOT_QNTY=TOT_QNTY-SEQ_QNTY
3540 REMOVE (POE63_DEV,KEY=T0$,DOM=3550)
3560 PRINT @(0,L),'CL',
3580 PRINT @(0,21),'CE',
3590 GOTO 1000
4000 REM " --- Options"
4005 PRINT @(0,L),'CE',
4010 IF TOT_QNTY<>QNTY THEN GOSUB 6000
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="Is The Rejection Information Correct (Y/N/Review/List/Delete)?",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4000
4050 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4060 IF V$="L" OR V$="LIST" THEN GOTO 4400
4070 IF V$="DELETE" THEN GOTO 4200
4080 IF V$="" THEN LET V$="Y"
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run Overlay"
4110 PRINT @(72,L),'CL',@(0,L2),'CE',
4120 LET L=L2,K9$=W0$,W0=NUM(W0$(18,3)),V$=""
4190 RUN "POE.CC"
4200 REM " --- Delete All Assignments Here"
4220 READ (POE63_DEV,KEY=W0$(1,25),DOM=4230)
4230 LET K$=KEY(POE63_DEV,END=4280)
4240 IF POS(W0$(1,25)=K$)<>1 THEN GOTO 4280
4250 REMOVE (POE63_DEV,KEY=K$)
4270 GOTO 4230
4290 GOTO 4100
4400 REM " --- List"
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4620 READ (POE63_DEV,KEY=W0$(1,25),DOM=4630)
4640 GOSUB 5400
4690 GOTO 1000
5000 REM " --- Display"
5010 IF T0$<=K9$ THEN GOSUB 5400
5020 DIM H1$(32)
5030 LET H1$(1)="Not On File"
5040 FIND (POM03_DEV,KEY=N0$+T1$(1,3),DOM=5070)IOL=POM03A
5060 PRINT @(4,L),T0$(26,3),@(8),T1$(1,3),@(12),H1$(1,30),@(Q_COL),T[0]:M2$,@(Q_COL+1+M2),T1$(7,15),@(Q_COL+1+M2+16),T1$(4,3),
5080 LET K9$=T0$,DFLT_INTLS$=T1$(4,3)
5090 RETURN 
5100 REM " --- List"
5110 GOSUB 5400
5120 READ (POE63_DEV,KEY=W0$(1,25),DOM=5130)
5130 LET K$=KEY(POE63_DEV,END=5290)
5140 IF POS(W0$(1,25)=K$)<>1 THEN GOTO 5290
5150 READ (POE63_DEV)IOL=POE63A
5200 IF L<=L8 THEN GOTO 5250
5210 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Press <Enter> To Continue",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5225 IF V3=4 THEN GOTO 5290
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+1
5280 GOTO 5130
5290 RETURN 
5400 REM " --- Clear Screen
5410 FOR X=L2 TO L8
5420 PRINT @(0,X),'CL',
5430 NEXT X
5440 LET L=L2
5490 RETURN 
6000 REM " --- Assigned Number Warning"
6010 LET V0$="Only "+STR(TOT_QNTY)+" Items Were Assigned Rejection Codes."
6020 LET V1$="The Total Quantity Rejected Is "+STR(QNTY)+"."
6030 LET V2$="<Enter>=Continue",NUMBER=2,X=MAX(LEN(V0$),LEN(V1$))+2
6040 DIM MESSAGE$[NUMBER](X)
6050 LET V1=FNC(V0$,X),MESSAGE$[0](V1)=V0$
6060 LET V1=FNC(V1$,X),MESSAGE$[1](V1)=V1$
6070 LET V1=FNC(V2$,X-6),MESSAGE$[2](V1)=V2$
6080 CALL "SYC.XA",2,MESSAGE$[ALL],NUMBER,22,-1,V$,V3
6090 RETURN 
6100 REM " --- Code Lookup"
6105 LET NUMBER=1
6110 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6120 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6130 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=3,TITLE$="Rejection Code Lookup"
6140 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6150 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,V$="SKIP",ROW=V2+2
6160 CALL "SYC.LK",POM03_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)+1
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "POR - Purchase Order Receipt Register (Receipts By Order Overlay)"
0020 REM "Program POR.DC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "15Jul96 WGH: Prevent Error 40 in line 3620 when Conversion Factor is zero (3610-3680)"
0032 REM "29Oct96 WGH: Items are reported as Received Complete when they aren't (6610)"
0033 REM "06Jan97 JJD: Handle Special Order item for work orders properly (350,510,1530,1540,1550,2140)"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 POE04A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0220 ARM01A: IOLIST B0$(1),B1$(1)
0230 IVM01A: IOLIST C0$(1),C1$(1)
0240 IVM02A: IOLIST D0$(1),D1$(1),D2$,D[ALL]
0310 POE14A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0320 IVM10C: IOLIST X0$(1)
0330 POM02A: IOLIST Y0$(1),Y1$(1)
0340 ARE13A: IOLIST L0$(1),L1$(1),L[ALL]
0350 WOE01A: IOLIST Z0$(1)
0500 REM " --- Initializations"
0505 PRECISION I[1]
0510 DIM DESCRIPTION$(40),B0$(8),B1$(30),L0$(52),L1$(64),L[14],Z0$(75)
0550 LET HEADINGS$[2]="Receipts By Work Order",NUMBER=0,T=5,SALES=0
0560 LET M1$=I6$,M2$=I7$,M3$=I8$,M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),DONE=0
0570 LET MAXM1=15,MAXM2=12,MAXM3=12,L=L9+1,SUBHEAD$="",T0$="",T1$=""
0580 LET ISSUES=0,SUBCONTRACTS=0,NOTOPEN=0,WO=0
0600 REM " --- Report initializations and print positions"
0605 IF M+M1+M2+M3<94 THEN GOTO 0650
0610 LET MAXM1=MAXM1-1,MAXM2=MAXM2-1,MAXM3=MAXM3-1
0615 CALL "SYC.RM",M1$,M1,11,MAXM1
0620 CALL "SYC.RM",M2$,M2,9,MAXM2
0625 CALL "SYC.RM",M3$,M3,6,MAXM3
0630 GOTO 0600
0650 LET O[11]=WIDTH-8,O[10]=O[11]-6,O[9]=O[10]-12,O[8]=O[9]-8,O[7]=O[8]-M-1
0660 LET O[6]=O[7]-11,O[5]=O[6]-M1,O[4]=O[5]-M3,O[3]=O[4]-M2,O[2]=O[3]-3
0670 LET O[0]=0,O[1]=O[0]+12
0800 REM " --- Shop Floor in use?"
0810 IF SF$<>"Y" THEN GOTO 2500
0900 REM " --- Position files"
0910 PRINT @(40,5),HEADINGS$[2],'CL',@(COLUMN,11),FILL(BW),
0990 READ (POW08_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Get next sort record"
1010 LET K$=KEY(POW08_DEV,END=2000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 2000
1030 READ (POW08_DEV)
1040 PRINT @(COLUMN,11),K$(3,2)," ",K$(5,7),
1100 REM " --- Level breaks?"
1110 IF T0$<>K$(3,2) THEN GOSUB 6000
1130 IF SUBHEAD$<>"" THEN GOSUB 5400
1200 REM " --- Read receipt header and detail"
1210 LET A0$(1)=K$(1,2)+K$(15),W0$(1)=K$(1,2)+K$(15)
1220 GOSUB 6400
1230 IF STATUS THEN GOTO 1900
1400 REM " --- Calculate extension and set detail line fields"
1410 PRECISION 2
1420 LET PO_EXTENSION=W[1]*W[7]
1430 PRECISION I[1]
1440 LET WO$=W2$(1,7)
1450 IF FNP$(W2$(8,3))<>"" THEN LET WO$=WO$+"-"+W2$(8,3)
1500 REM " --- Work order released?"
1510 LET MARK$=""
1515 IF Y1$(21,1)<>"S" THEN GOTO 1600
1520 IF D1$(60,1)<>"Y" THEN GOTO 1600
1530 LET K$=N0$+"  "+W2$(1,7)
1540 READ (WOE01_DEV,KEY=K$,DOM=1560)IOL=WOE01A
1550 IF Z0$(15,1)="O" THEN GOTO 1600
1560 LET MARK$="*",NOTOPEN=NOTOPEN+1
1600 REM " --- Print detail line"
1610 IF L+2>L9 THEN GOSUB 5000
1620 PRINT (7)@(O[0]),WO$,MARK$,@(O[1]),W3$(3,I[0]),@(O[2]),W1$(22,2),@(O[3]),W[7]:M2$,@(O[4]),W[1]:M3$,@(O[5]),PO_EXTENSION:M1$,@(O[6]),W1$(24,10),@(O[7]),FNF$(A0$(3,VENDLEN),M0$),@(O[8]),A0$(9,7),@(O[9]),W0$(16,7),"-",W0$(23,3),@(O[10]),COMPLETE$,@(O[11]),FNB6$(A1$(29,6))
1640 PRINT (7)@(O[1]),DESCRIPTION$
1800 REM " --- Accumulate totals"
1810 LET WHSE=WHSE+PO_EXTENSION,REPORT=REPORT+PO_EXTENSION
1820 LET L=L+2,NUMBER=NUMBER+1
1900 REM " --- Loop back for next sort record"
1990 GOTO 1000
2000 REM " --- Done with work orders"
2010 LET DONE=1
2020 IF NUMBER=0 THEN GOTO 2500
2030 GOSUB 6000
2040 LET TOTALS$="Total For Register",TOTAL=REPORT
2050 GOSUB 6900
2100 REM " --- Any unreleased work orders?
2110 IF NOTOPEN=0 THEN GOTO 2500
2120 IF L+3>L9 THEN GOSUB 5000
2130 PRINT (7)""
2140 PRINT (7)"WARNING: ",$22$,"*",$22$," Indicates That This Is A ",$22$,"Special Order",$22$," Inventory Item For A Work Order Which is Not Open."
2150 IF P3$(69,1)="Y" THEN GOTO 2500
2160 PRINT (7)"          The Receipt Register Update May Not Be Run Until These Work Orders Are Released Or This Line Is Deleted."
2170 LET WO=NOTOPEN
2500 REM " --- Initializations for Sales Order Report"
2510 IF OP$<>"Y" THEN GOTO 4900
2520 LET HEADINGS$[2]="Receipts By Customer Sales Order",NUMBER=0,T=6,SALES=1
2530 LET M1$=I6$,M2$=I7$,M3$=I8$,M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),DONE=0
2540 LET NUMBER=0,MAXM1=15,MAXM2=12,MAXM3=12,L=L9+1,SUBHEAD$="",T0$="",T1$=""
2600 REM " --- Print positions"
2605 IF M+M1+(M2*2)+M3<94 THEN GOTO 2650
2610 LET MAXM1=MAXM1-1,MAXM2=MAXM2-1,MAXM3=MAXM3-1
2615 CALL "SYC.RM",M1$,M1,11,MAXM1
2620 CALL "SYC.RM",M2$,M2,9,MAXM2
2625 CALL "SYC.RM",M3$,M3,6,MAXM3
2630 GOTO 2600
2650 LET O[12]=WIDTH-8,O[11]=O[12]-6,O[10]=O[11]-5,O[9]=O[10]-12,O[8]=O[9]-8
2660 LET O[7]=O[8]-M-1,O[6]=O[7]-M1,O[5]=O[6]-M3,O[4]=O[5]-M2,O[3]=O[4]-M2
2670 LET O[0]=0,O[1]=O[0]+12,O[2]=O[3]-3
2900 REM " --- Position files"
2910 PRINT @(40,5),HEADINGS$[2],'CL',@(COLUMN,11),FILL(BW),
2990 READ (POW09_DEV,KEY=N0$,DOM=3000)
3000 REM " --- Get next sort record"
3010 LET K$=KEY(POW09_DEV,END=4000)
3020 IF POS(N0$=K$)<>1 THEN GOTO 4000
3030 READ (POW09_DEV)
3040 PRINT @(COLUMN,11),K$(3,2)," ",FNF$(K$(5,CUSTLEN),P8$)," ",K$(11,7),
3100 REM " --- Level breaks?"
3110 IF T0$<>K$(3,2) THEN GOSUB 6000
3120 IF T1$<>K$(5,6) THEN GOSUB 6200
3130 IF SUBHEAD$<>"" THEN GOSUB 5400
3150 IF CUSTOMER$="" THEN GOTO 3200
3160 IF L+4>L9 THEN GOSUB 5000
3170 PRINT (7)""
3180 PRINT (7)CUSTOMER$
3190 LET L=L+2,CUSTOMER$=""
3200 REM " --- Read receipt header and detail"
3210 LET A0$(1)=K$(1,2)+K$(21),W0$(1)=K$(1,2)+K$(21)
3220 GOSUB 6400
3230 IF STATUS THEN GOTO 3900
3400 REM " --- Retrieve order line item"
3410 LET L0$(1)=N0$,L0$(5)=K$(5,16)
3420 FIND (ARE13_DEV,KEY=L0$(1,20),DOM=3900)IOL=ARE13A
3600 REM " --- Calculate extensions and branch based on line type"
3610 IF W[0]=0 THEN LET W[0]=1
3620 LET COST=W[1]/W[0],QTY=W[7]*W[0]
3670 PRECISION 2
3680 LET PO_EXTENSION=QTY*COST
3690 PRECISION I[1]
3700 REM " --- Print detail line"
3710 IF L+2>L9 THEN GOSUB 5000
3720 PRINT (7)@(O[0]),W2$(17,7),"-",W2$(24,3),@(O[1]),W3$(3,I[0]),@(O[2]),UM$,@(O[3]),L[2]:M2$,@(O[4]),QTY:M2$,@(O[5]),COST:M3$,@(O[6]),PO_EXTENSION:M1$,@(O[7]),FNF$(A0$(3,VENDLEN),M0$),@(O[8]),A0$(9,7),@(O[9]),W0$(16,7),"-",W0$(23,3),@(O[10]),DROP$,@(O[11]),COMPLETE$,@(O[12]),FNB6$(A1$(29,6))
3730 PRINT (7)@(O[1]),DESCRIPTION$
3740 LET L=L+2
3800 REM " --- Accumulate totals"
3810 LET CUSTOMER=CUSTOMER+PO_EXTENSION,WHSE=WHSE+PO_EXTENSION
3820 LET REPORT=REPORT+PO_EXTENSION,L=L+2,NUMBER=NUMBER+1
3900 REM " --- Loop back for next sort record"
3990 GOTO 3000
4000 REM " --- All done"
4010 LET DONE=1
4020 IF NUMBER=0 THEN GOTO 4900
4030 GOSUB 6000
4040 LET TOTALS$="Total For Register",TOTALS[ALL]=REPORT[ALL]
4050 GOSUB 6900
4900 REM " --- Run next overlay"
4990 RUN "POR.DD"
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+3
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS THEN EXITTO 9900
5040 IF SALES THEN GOTO 5200
5100 REM " --- Heading for work order receipts"
5110 PRINT (7)@(O[3]+M2-9),"Quantity",@(O[4]+M3-5),"PO",@(O[5]+M1-10),"PO",@(O[6]),"Receiving",@(O[10]),"Rec'd",@(O[11]+2),"Date"
5120 PRINT (7)@(O[0]),"Work Order",@(O[1]),"Item",@(O[2]),"UM",@(O[3]+M2-9),"Received",@(O[4]+M3-5),"Cost",@(O[5]+M1-10),"Extension",@(O[6]),"Location",@(O[7]),"Vendor",@(O[8]),"Rec #",@(O[9]),"PO Number",@(O[10]),"Comp?",@(O[11]),"Received"
5190 RETURN 
5200 REM " --- Heading for sales order receipts"
5210 PRINT (7)@(O[3]+M2-9),"Order",@(O[4]+M2-9),"Quantity",@(O[5]+M3-5),"Unit",@(O[10]),"Drop",@(O[11]),"Rec'd",@(O[12]+2),"Date"
5220 PRINT (7)@(O[0]),"Sales Order",@(O[1]),"Item",@(O[2]),"UM",@(O[3]+M2-9),"Quantity",@(O[4]+M2-9),"Received",@(O[5]+M3-5),"Cost",@(O[6]+M1-10),"Extension",@(O[7]),"Vendor",@(O[8]),"Rec #",@(O[9]),"PO Number",@(O[10]),"Ship",@(O[11]),"Comp?",@(O[12]),"Received"
5290 RETURN 
5400 REM " --- Subheading"
5410 IF L+4>L9 THEN GOSUB 5000
5420 PRINT (7)""
5430 PRINT (7)SUBHEAD$
5440 PRINT (7)""
5450 LET L=L+3,SUBHEAD$=""
5490 RETURN 
6000 REM " --- Warehouse break"
6010 IF T0$="" THEN GOTO 6100
6020 IF SALES THEN GOSUB 6200
6030 LET TOTALS$="Total For Warehouse "+FNP$(WHSE$),TOTAL=WHSE
6040 GOSUB 6900
6100 IF DONE THEN GOTO 6190
6110 DIM WHSE[1]
6120 LET T0$=K$(3,2),X0$(1)=N0$+"C"+T0$+NF$
6130 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=6140)IOL=IVM10C
6140 LET WHSE$=X0$(4,2)+" "+X0$(6,24),SUBHEAD$="Warehouse "+FNP$(WHSE$)
6190 RETURN 
6200 REM " --- Customer break"
6210 IF T1$="" THEN GOTO 6300
6220 LET TOTALS$="Total For "+FNF$(T1$(1,CUSTLEN),P8$)+" "+FNP$(B1$(1,30))
6230 LET TOTAL=CUSTOMER
6240 GOSUB 6900
6300 IF DONE THEN GOTO 6390
6310 LET T1$=K$(5,6),CUSTOMER=0,B0$(1)=N0$+T1$,B1$(1)=NF$
6320 FIND (ARM01_DEV,KEY=B0$,DOM=6330)IOL=ARM01A
6350 LET CUSTOMER$="Customer "+FNF$(B0$(3,CUSTLEN),P8$)+" "+B1$(1,30)
6390 RETURN 
6400 REM " --- Read receipt header and detail"
6410 LET STATUS=11
6420 FIND (POE04_DEV,KEY=A0$,DOM=6690)IOL=POE04A
6430 FIND (POE14_DEV,KEY=W0$,DOM=6690)IOL=POE14A
6440 IF Y0$(1,4)=N0$+W1$(1,2) THEN GOTO 6500
6450 LET Y0$(1)=N0$+W1$(1,2)
6460 FIND (POM02_DEV,KEY=Y0$,DOM=6690)IOL=POM02A
6500 REM " --- Retrieve inventory item and warehouse"
6510 LET C0$(1)=N0$+W3$(3),C1$(1)="",DESCRIPTION$(1)=W4$
6520 LET D0$(1)=N0$+W3$,D1$(1)="",D[11]=0
6530 IF Y1$(21,1)<>"S" THEN GOTO 6600
6540 FIND (IVM01_DEV,KEY=C0$,DOM=6690)IOL=IVM01A
6550 FIND (IVM02_DEV,KEY=D0$,DOM=6690)IOL=IVM02A
6560 LET DESCRIPTION$(1)=FNITEM$(C1$,I[3],I[4],I[5])
6600 REM " --- Format detail line fields"
6610 LET X=W[3]-W[4]-W[7],COMPLETE$="No",DROPSHIP$="No",STATUS=0
6620 IF A1$(134,1)="Y" THEN LET COMPLETE$="Yes"
6630 IF X<=0 THEN LET COMPLETE$="Yes"
6640 IF Y1$(21,1)="S" THEN LET ISSUES=ISSUES+1
6650 IF Y1$(21,1)="N" THEN LET SUBCONTRACTS=SUBCONTRACTS+1
6660 IF Y1$(22,1)="Y" THEN LET DROPSHIP$="Yes"
6690 RETURN 
6900 REM " --- Print totals"
6910 IF L+1>L9 THEN GOSUB 5000
6920 PRINT (7)@(O[T]-LEN(TOTALS$)),TOTALS$,@(O[T]),TOTAL:M1$
6930 LET L=L+1
6990 RETURN 
8000 REM " --- Functions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

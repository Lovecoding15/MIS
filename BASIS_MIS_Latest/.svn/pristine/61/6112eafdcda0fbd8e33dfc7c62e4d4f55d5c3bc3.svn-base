0010 REM "ARE - SIMPLE INVOICE ENTRY/HEADING"
0020 REM "Program ARE.FB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0060 ON O0 GOTO 0070,1000
0100 REM " --- Open Files
0200 REM " --- IOLists
0210 ARE05A: IOLIST B0$,B1$
0220 ARM01A: IOLIST CUS0$,CUS1$
0230 ARM02A: IOLIST X$,AR1$
0240 ARM10A: IOLIST TERM$
0250 ARM10D: IOLIST DISTCD$,ACCTS$
0270 ARS10N: IOLIST NEXTINV$,NEXT[ALL],NEXTINV
0500 REM " --- Init Data
0510 LET O0=1
0520 DIM B0$(12),B1$(60),B[2],W1$(80)
0530 DIM NEXT[3]
0540 LET B0$(1,2)=A0$(1,2)
0900 REM " --- Init File
0920 READ (ARE05_DEV,KEY=N0$,DOM=0930)
1000 REM " --- Invoice Number
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(ARE05_DEV,END=1050)
1030 IF V2$(1,2)<>B0$(1,2) THEN LET V2$=""
1032 IF V2$="" THEN GOTO 1050
1035 IF V2$(10,3)<>"000" THEN READ (ARE05_DEV,KEY=V2$(1,9)+"Z",DOM=1000)
1040 IF V2$<>"" THEN LET V2$=V2$(3,7)
1050 LET V0$="Z",V1$="KEC",V4$="Enter Invoice Number (<F1>=Next Invoice Number)",V0=7,V1=15,V2=3,O8=0,O9=0
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 9900
1080 IF V3=1 THEN GOTO 1150
1090 IF V$="" THEN GOTO 0900
1100 LET B0$(3)=V$
1110 GOTO 1200
1150 LET B0=0,O8=1
1160 EXTRACT (ARS10_DEV,KEY=N0$+"N",ERR=1000,DOM=1170)IOL=ARS10N
1165 LET B0$(3)=STR(NEXTINV:J0$(1,7))
1170 LET NEXTINV=NEXTINV+1,X$=N0$+"N"
1172 IF NEXTINV>9999999 THEN LET NEXTINV=1
1175 WRITE (ARS10_DEV,KEY=X$)IOL=ARS10N
1195 PRINT @(15,3),B0$(3,7),
1200 REM " --- Find Record
1210 LET B0$(10,3)="000"
1220 EXTRACT (ARE05_DEV,KEY=B0$,DOM=1400)IOL=ARE05A
1240 IF B1$(17,1)<>"V" THEN GOTO 1300
1245 PRINT 'RB',
1260 CALL "SYC.YN",1,"This Invoice Has Been Voided - Reuse It",1,V$,V3
1265 IF V$="N" THEN GOTO 4800
1270 IF V$<>"Y" THEN GOTO 1260
1275 GOSUB 6100
1280 IF OUT$="N" THEN GOTO 1540
1285 IF V$="N" THEN GOTO 4800
1290 IF V$<>"Y" THEN GOTO 1275
1295 LET B1$(17,1)="A"
1297 GOTO 1540
1300 REM " --- Display
1310 GOSUB 6000
1320 GOSUB 5000
1340 GOSUB 5200
1390 GOTO 4000
1400 REM " --- New Record
1410 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="Is This A New Record (Y/N)?",V0=1,V1=FNV(V4$),V2=22
1420 GOSUB 7000
1440 ON POS(V$="YN") GOTO 1400,1500,1450
1450 IF O8=1 THEN GOTO 4200
1490 GOTO 1000
1500 REM " --- Set-up
1510 LET B1$(1)=F0$(18,6),B1$(17,1)="I"
1520 LET O9=1
1540 GOSUB 5000
1600 REM " --- Date
1610 IF B1$(1,6)="      " THEN LET B1$(1,6)=F0$(18,6)
1620 LET V0$="D",V1$="R6N",V2$=B1$(1,6),V0=8,V1=15,V2=4,I0=0,STATUS=0
1640 GOSUB 7000
1650 LET INVDATE$=V$
1660 IF GL$="Y" THEN CALL "GLC.EA",INVDATE$,"Y",PER$,YR$,STATUS
1670 IF STATUS>99 THEN GOTO 1600
1680 LET B1$(1,6)=INVDATE$
1690 GOTO 3900
1700 REM " --- Customer
1720 LET V0$="M",V1$="KC",V2$=B1$(7,P[0]),V3$=P1$,V4$="Enter A Valid Customer Number (<F3>=Lookup)",V0=6,V1=15,V2=5
1730 GOSUB 7000
1735 IF V3=2 THEN GOTO 1890
1737 IF V3=3 THEN CALL "SYC.LC",V$
1740 DIM CUS1$(282)
1750 FIND (ARM01_DEV,KEY=N0$+V$,DOM=1700)IOL=ARM01A
1755 FIND (ARM02_DEV,KEY=N0$+V$+"  ",DOM=1700)IOL=ARM02A
1760 LET B1$(7,6)=V$
1770 IF B1$(13,2)="  " THEN LET B1$(13,2)=AR1$(8,2)
1780 IF B1$(15,2)="  " THEN LET B1$(15,2)=AR1$(4,2)
1790 GOSUB 5200
1800 REM " --- Check Outstanding Inv
1810 GOSUB 6100
1820 IF OUT$="N" THEN LET B1$(17,1)="I"; GOTO 1890
1860 IF V$="Y" THEN LET B1$(17,1)="A"; GOTO 1890
1870 IF V$<>"N" THEN GOTO 1800
1880 GOTO 1000
1890 GOTO 3900
1900 REM " --- Dist Code
1920 LET V0$="S",V2$=B1$(13,2),V0=2,V1=55,V2=3,V1$="KC",V4$="Enter A Valid Distribution Code (<F3>=Lookup)"
1940 GOSUB 7000
1942 IF V3=2 THEN GOTO 1990 ELSE IF V3<>3 THEN GOTO 1950
1944 LET V4=1,V5=ARM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="D"
1946 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
1950 IF V$="" THEN GOTO 1900
1955 DIM DISTCD$(30),ACCTS$(80)
1957 LET V$=V$+"  ",V$=V$(1,2)
1960 FIND (ARM10_DEV,KEY=N0$+"D"+V$,DOM=1900)IOL=ARM10D
1965 LET B1$(13,2)=V$
1970 PRINT @(58,3),DISTCD$(6,20)
1980 LET DEFACT$=ACCTS$(G[4]*2+1,G[4])
1990 GOTO 3900
3000 REM " --- Terms Code 
3020 LET V0$="S",V2$=B1$(15,2),V0=2,V1=55,V2=4,V1$="KC",V4$="Enter A Valid Terms Code (<F3>=Lookup)"
3030 GOSUB 7000
3032 IF V3=2 THEN GOTO 3090 ELSE IF V3<>3 THEN GOTO 3040
3034 LET V4=1,V5=ARM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="A"
3036 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
3038 IF V$="" THEN GOTO 3000
3040 LET V$=V$+"  ",V$=V$(1,2)
3050 FIND (ARM10_DEV,KEY=N0$+"A"+V$,DOM=3000)IOL=ARM10A
3060 PRINT @(58,4),TERM$(6,20),
3070 LET B1$(15,2)=V$
3090 GOTO 3900
3900 REM " --- Next Field
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 IF ABS(I0)>1000 THEN GOTO 3990
3960 ON I0 GOTO 1600,1700,1900,3000,3990
4000 REM " --- Correct
4015 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="KC",V2$="",V3$="",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF O9=0 AND V3=4 THEN GOTO 4800
4040 IF V$="N" THEN GOTO 1600
4050 IF O8=0 AND O9=0 AND V$="" AND V3=0 THEN LET V$="Y"
4060 IF V$="Y" THEN GOTO 4100
4070 IF V$="DELETE" THEN GOTO 4200
4090 GOTO 4000
4100 WRITE (ARE05_DEV,KEY=B0$)IOL=ARE05A
4190 RUN "ARE.FC"
4200 REM " --- Delete
4220 READ (ARE15_DEV,KEY=B0$(1,9),DOM=4230)
4240 LET K$=KEY(ARE15_DEV,END=4300)
4250 IF K$(1,9)<>B0$(1,9) THEN GOTO 4300
4260 REMOVE (ARE15_DEV,KEY=K$)
4290 GOTO 4240
4300 LET B1$(17,1)="V"
4320 WRITE (ARE05_DEV,KEY=B0$)IOL=ARE05A
4390 GOTO 1000
4800 REM " --- Next Invoice
4820 READ (ARE05_DEV,KEY=B0$(1,9)+$FF$,DOM=4830)
4890 GOTO 1000
5000 REM " --- Display
5040 PRINT @(15,4),FNB6$(B1$(1,6))
5080 GOSUB 5400
5090 RETURN 
5200 REM " --- Cust Disp
5210 PRINT @(15,5),FNF$(B1$(7,P[0]),M0$),
5220 PRINT @(15,6),CUS1$(1,30),@(15,7),CUS1$(31,24),@(15,8),CUS1$(55,24),@(15,9),CUS1$(79,24),@(15,10),FNZ$(CUS1$(103)),
5240 PRINT @(55,3),B1$(13,2),@(55,4),B1$(15,2),
5290 RETURN 
6000 REM " --- Get Customer Info
6020 DIM CUS1$(282),ACCTS$(40)
6030 FIND (ARM01_DEV,KEY=N0$+B1$(7,6),DOM=6050)IOL=ARM01A
6040 FIND (ARM02_DEV,KEY=N0$+B1$(7,6)+"  ",DOM=6050)IOL=ARM02A
6050 FIND (ARM10_DEV,KEY=N0$+"D"+B1$(13,2),DOM=6060)IOL=ARM10D
6060 LET DEFACT$=ACCTS$(G[4]*2+1,G[4])
6080 FIND (ARM10_DEV,KEY=N0$+"D"+B1$(15,2),DOM=6090)IOL=ARM10A
6090 RETURN 
6100 REM " --- Check Outstanding Inv
6110 LET OUT$="N"
6120 FIND (ART01_DEV,KEY=N0$+"  "+B1$(7,6)+B0$(3,7)+"00",DOM=6190)
6130 LET OUT$="Y"
6140 PRINT 'RB',
6150 LET V4$="** This Invoice Is Outstanding. Is This An Adjustment"
6160 CALL "SYC.YN",1,V4$,1,V$,V3
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "GLM - G/L Account Maintenance (Financial Statements)"
0020 REM "Program GLM.AG"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "22Apr99 WGH: Don't change Assignment Numbers to fill gaps in the sequence (6270,6370)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$(1),A1$(1)
0220 GLM11A: IOLIST R0$(1),R1$(1)
0230 GLM21A: IOLIST S0$(1),S1$(1)
0240 GLM12A: IOLIST X0$(1)
0250 GLM22A: IOLIST W0$(1),W1$(1)
0500 REM " --- Initializations"
0510 DIM X0$(32),W0$(8),W1$(46),R$(60),R0$(14),R1$(6),S0$(14),S1$(6)
0520 GOSUB INIT_R
0550 LET N=10,L1=L0+2,FIRM$=N0$
0560 IF CONSOLIDATED$="Y" THEN LET FIRM$="99"
0700 REM " --- Background"
0750 PRINT 'SB',@(0,L1-1),"Assignment",@(2,L1),"Number",@(11),"Report",@(18),"Description",@(40),"Line",@(45),"Description",'SF',
1300 REM " --- Display"
1310 GOSUB 5000
1390 GOTO 4000
2000 REM " --- Report"
2010 LET X=I0/2+1,P=(X-1)*6+1,V0$="Z",V1$="CK",V2$=R$(P,2),V3$=""
2020 LET V0=2,V1=13,V2=L1+X
2025 LET V4$="Enter A Valid Financial Report Number (<F3>=Lookup)"
2030 GOSUB 7000
2040 ON V3 GOTO 2060,2000,3900,2045,3900
2045 CALL "GLC.LF",FIRM$,V$
2050 IF V$="" THEN GOTO 2000
2060 LET X0$(1,4)=FIRM$+V$,X0$(5)="0000"
2065 GOSUB 6000
2070 IF V$="" THEN LET R$(P,2)=V$; GOTO 3900
2080 IF V>0 THEN GOTO 2000
2085 LET R$(P,2)=V$
2090 GOTO 3900
2200 REM " --- Line"
2205 LET X=(I0-1)/2+1,P=(X-1)*6+3,V$=""
2210 LET V0=4,V1=40,V2=L1+X,V0$="Z",V1$="CK",V2$=R$(P,4),V3$=""
2215 IF FNP$(R$(P-2,2))="" THEN GOTO 2260
2220 LET V4$="Enter A Valid Financial Report Line Number (<F3>=Lookup)"
2230 GOSUB 7000
2240 ON V3 GOTO 2260,2200,3900,2245,3900
2245 LET REPORT$=R$(P-2,2)
2250 CALL "GLC.LL",REPORT$,FIRM$,V$
2255 IF V$="" THEN GOTO 2200
2260 LET R$(P,4)=V$,W0$(1)=FIRM$+R$(P-2,2)+R$(P,4)
2265 GOSUB 6100
2270 IF V$="" THEN GOTO 3900
2280 IF V>0 THEN GOTO 2200
2290 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V0$="Y",V4$="Is The Above Information Correct (Y/N)?",I0=0
4020 LET V1$="C",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4110 GOSUB ASSIGN_R
4900 REM " --- Return to menu"
4990 RUN MENU$,ERR=9900
5000 REM " --- Display"
5010 FOR X=1 TO N
5020 LET P=(X-1)*6+1,V2=L1+X
5030 PRINT @(4,V2),X:"##",
5040 LET V1=13,X0$(1)=FIRM$+R$(P,2)+"0000"
5050 GOSUB 6000
5060 LET V1=40,W0$(1)=FIRM$+R$(P,6)
5070 GOSUB 6100
5080 NEXT X
5090 RETURN 
6000 REM " --- Retrieve and display report description"
6010 LET X0$(9)="",V=1
6020 FIND (GLM12_DEV,KEY=X0$(1,8),DOM=6040)IOL=GLM12A
6030 LET V=0
6040 PRINT @(V1,V2),X0$(3,2),@(V1+5,V2),X0$(9,20),
6090 RETURN 
6100 REM " --- Retrieve and display line description"
6110 DIM W1$(46)
6115 LET V=1
6120 FIND (GLM22_DEV,KEY=W0$,DOM=6140)IOL=GLM22A
6130 LET V=0
6140 PRINT @(V1,V2),W0$(5,4),@(V1+5,V2),W1$(2,35),
6190 RETURN 
6200 REM " --- Initialize temp string, R$, w/GLM-11 or GLM-21 data
6210 INIT_R: 
6220 IF CONSOLIDATED$="Y" THEN GOTO USE_GLM21
6225 READ (GLM11_DEV,KEY=A0$,DOM=READ_GLM11)
6230 READ_GLM11: 
6235 FOR CNT=1 TO 55 STEP 6
6240 LET K$=KEY(GLM11_DEV,END=6280)
6250 IF K$(1,12)<>A0$(1,12) THEN EXITTO DONE_GLM11
6260 READ (GLM11_DEV,KEY=K$)IOL=GLM11A
6270 LET R$(NUM(R0$(13,2))*6-5,6)=R1$(1,6)
6280 NEXT CNT
6290 DONE_GLM11: 
6295 GOTO DONE_INIT
6300 USE_GLM21: 
6325 READ (GLM21_DEV,KEY=A0$,DOM=READ_GLM21)
6330 READ_GLM21: 
6335 FOR CNT=1 TO 55 STEP 6
6340 LET K$=KEY(GLM21_DEV,END=6380)
6350 IF K$(1,12)<>A0$(1,12) THEN EXITTO DONE_GLM21
6360 READ (GLM21_DEV,KEY=K$)IOL=GLM21A
6370 LET R$(NUM(S0$(13,2))*6-5,6)=S1$(1,6)
6380 NEXT CNT
6390 DONE_GLM21: 
6395 GOTO DONE_INIT
6400 DONE_INIT: 
6410 RETURN 
6500 REM " --- Assign R$ to GLM-11 or GLM-21 vars & WRITE
6510 ASSIGN_R: 
6520 IF CONSOLIDATED$="Y" THEN GOTO WRITE_GLM21
6530 WRITE_GLM11: 
6540 LET ASSIGNNUM=1
6545 FOR CNT=1 TO 55 STEP 6
6550 LET R0$(1)=A0$(1,12)+STR(ASSIGNNUM:"00")
6560 LET R1$=R$(CNT,6)
6570 IF FNP$(R1$)<>"" THEN WRITE (GLM11_DEV,KEY=R0$)IOL=GLM11A ELSE REMOVE (GLM11_DEV,KEY=R0$,DOM=6575)
6575 LET ASSIGNNUM=ASSIGNNUM+1
6580 NEXT CNT
6595 GOTO DONE_WRITE
6630 WRITE_GLM21: 
6640 LET ASSIGNNUM=1
6645 FOR CNT=1 TO 55 STEP 6
6650 LET S0$(1)=A0$(1,12)+STR(ASSIGNNUM:"00")
6660 LET S1$=R$(CNT,6)
6670 IF FNP$(S1$)<>"" THEN WRITE (GLM21_DEV,KEY=S0$)IOL=GLM21A ELSE REMOVE (GLM21_DEV,KEY=S0$,DOM=6675)
6675 LET ASSIGNNUM=ASSIGNNUM+1
6680 NEXT CNT
6700 DONE_WRITE: 
6710 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

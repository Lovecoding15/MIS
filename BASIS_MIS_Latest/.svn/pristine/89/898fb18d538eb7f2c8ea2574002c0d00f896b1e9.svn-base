0010 REM "POR - Purchase Orders by Item Report
0020 REM "Program POR.JA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "21Nov96 Caj: Allow for entry of single character whses (ie '4 ')(1105,1110,1120,1130,1300,1315,1320,1330,1340,8070)
0040 REM 
0070 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="APM-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0130 LET FILES$[4]="IVM-10",FILES$[5]="IVM-14",FILES$[6]="POE-12"
0135 LET FILES$[7]="POM-02",FILES$[8]="POW-15",FILES$[9]="SYS-01"
0150 LET OPTIONS$[8]="C"
0160 CALL "SYC.DA",1,1,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET APM01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0180 LET IVM10_DEV=CHANNELS[4],IVM14_DEV=CHANNELS[5],POE12_DEV=CHANNELS[6]
0185 LET POM02_DEV=CHANNELS[7],POW15_DEV=CHANNELS[8],SYS01_DEV=CHANNELS[9]
0200 REM " --- IOLists
0240 APM01A: IOLIST X0$,X1$(1)
0250 IVM10C: IOLIST X1$(1)
0280 POE12A: IOLIST *,*,*,WHSEITEM$(1)
0290 POW15A: IOLIST Z$(1)
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 APS01A: IOLIST U0$,VENDIMASK$,U2$,U3$,U4$,VENDOMASK$
0330 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0340 POS01A: IOLIST P0$,P1$,P2$,P3$,P4$
0400 REM " --- Parameters
0405 DIM INFO$[20],DESCLEN[3],PARAMS[3]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO",WHEN$=F0$(7,3)
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=POS01A
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 LET VENDLEN=NUM(U2$(1,2))
0435 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET IVPRECISION=NUM(I2$(5,1)),ITEMLEN=NUM(I2$(1,2))
0445 FOR X=1 TO 3
0450 LET DESCLEN[X]=NUM(I2$((X-1)*2+9,2))
0460 LET PARAMS[X]=DESCLEN[X]
0465 NEXT X
0475 LET PARAMS[0]=ITEMLEN
0500 REM " --- Init Data
0510 PRECISION IVPRECISION
0520 DIM IV_CHANS[14]
0525 LET IV_CHANS[1]=IVM01_DEV,IV_CHANS[14]=IVM14_DEV
0530 LET WHICHDATE$="O",DATETYPE$="Order"
0540 LET TYPE$="P"; REM "Purchase Order -- POC.LB
0550 CALL "SYC.RM",M1$,M1,10,14
0560 CALL "SYC.RM",M2$,M2,9,11
0570 CALL "SYC.RM",M3$,M3,5,11
0580 CALL "SYC.RM",M5$,M5,7,10
0585 DIM VENDFNAME$(30),VENDTNAME$(30),WHSEFNAME$(24),WHSETNAME$(24)
0590 DIM ITEMFDESC$(30),ITEMTDESC$(30)
0700 REM " --- Background
0750 PRINT 'SB',@(19,3),"Beginning Warehouse:",@(22,4),"Ending Warehouse:",@(24,5),"Beginning Item:",@(27,6),"Ending Item:",@(14,7),"Beginning Purchase Order:",@(17,8),"Ending Purchase Order:",@(22,9),"Beginning Vendor:",@(25,10),"Ending Vendor:",@(28,11),"Which Date:",@(24,12),"Beginning Date:",@(27,13),"Ending Date:"
0760 GOSUB REFRESH
1000 REM " --- Warehouse From
1010 LET V0$="S",V1$="",V2$=WHSEFROM$,V3$="",V4$="Enter A Valid Warehouse ID  (<F1>=First/<F3>=Lookup)",V0=2,V1=40,V2=3,I0=0
1020 GOSUB 7000
1030 ON V3 GOTO 1090,1040,1190,1070,4000
1040 LET WHSEFROM$=""
1050 PRINT @(V1,V2),'CL',@(V1,V2),"First",
1060 GOTO 1190
1070 CALL "IVC.LK",3,V1,V2,V$
1080 IF V$="" THEN GOTO 1000
1090 IF V$="" THEN GOTO 1040
1100 DIM X1$(160)
1105 LET X1$(1)=N0$+"C"+V$
1110 FIND (IVM10_DEV,KEY=X1$(1,5),DOM=1000)IOL=IVM10C
1120 LET WHSEFROM$=X1$(4,2),WHSEFNAME$(1)=X1$(6,24)
1130 PRINT @(V1,V2),WHSEFROM$," ",WHSEFNAME$(1,24),'CL',
1190 GOTO 3900
1200 REM " --- Warehouse Thru
1210 LET V0$="S",V1$="",V2$=WHSETHRU$,V3$="",V4$="Enter A Valid Warehouse ID  (<F1>=Last/<F3>=Lookup)",V0=2,V1=40,V2=4
1220 GOSUB 7000
1230 ON V3 GOTO 1290,1240,1390,1270,4000
1240 LET WHSETHRU$=""
1250 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
1260 GOTO 1390
1270 CALL "IVC.LK",3,V1,V2,V$
1280 IF V$="" THEN GOTO 1200
1290 IF V$="" THEN GOTO 1240
1300 IF WHSEFROM$<>"" AND V$<FNP$(WHSEFROM$) THEN GOTO 1200
1310 DIM X1$(160)
1315 LET X1$(1)=N0$+"C"+V$
1320 FIND (IVM10_DEV,KEY=X1$(1,5),DOM=1200)IOL=IVM10C
1330 LET WHSETHRU$=X1$(4,2),WHSETNAME$(1)=X1$(6,24)
1340 PRINT @(V1,V2),WHSETHRU$," ",WHSETNAME$(1,24),'CL',
1390 GOTO 3900
1400 REM " --- Starting Item
1405 ITEMFROM: 
1420 DIM ITEM$[6],ITEM[12]
1430 LET ACTION$="F",DEFAULT$=ITEMFROM$,V1=40,V2=5
1440 LET V4$="Enter A Valid Item Number"
1450 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1460 IF V3=2 THEN GOTO NEXT_ITEMFROM
1470 IF V3=4 THEN GOTO 4000
1480 IF V3=1 OR ITEM$[0]="" THEN PRINT @(V1,V2),'CL',"First",; LET ITEM$[0]="",ITEMFROM$=""; GOTO NEXT_ITEMFROM
1580 LET ITEMFROM$=ITEM$[0],ITEMFDESC$(1)=ITEM$[1]+FILL(28)
1585 PRINT @(V1,V2),ITEMFROM$(1,ITEMLEN)," ",ITEMFDESC$(1,28),
1590 NEXT_ITEMFROM: 
1595 GOTO 3900
1600 REM " --- Ending Item
1605 ITEMTHRU: 
1610 DIM ITEM$[6],ITEM[12]
1620 LET ACTION$="L",DEFAULT$=ITEMTHRU$,V1=40,V2=6,V$=""
1630 LET V4$="Enter A Valid Item Number"
1640 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1650 IF V3=2 THEN GOTO NEXT_ITEMTHRU
1660 IF V3=4 THEN GOTO 4000
1680 IF V3=1 OR ITEM$[0]="" THEN PRINT @(V1,V2),'CL',"Last",; LET ITEM$[0]="",ITEMTHRU$=""; GOTO NEXT_ITEMTHRU
1770 IF ITEM$[0]<ITEMFROM$ THEN GOTO 1600
1775 LET ITEMTHRU$=ITEM$[0],ITEMTDESC$(1)=ITEM$[1]+FILL(28)
1780 PRINT @(V1,V2),ITEMTHRU$(1,ITEMLEN)," ",ITEMTDESC$(1,28),
1785 NEXT_ITEMTHRU: 
1790 GOTO 3900
2000 REM " --- Beginning Purchase Order
2010 LET V0$="Z",V1$="KCE",V2$=POFROM$,V3$="",V4$="Enter A Valid Purchase Order Number (<F1>=First/<F3>=Lookup)",V0=7,V1=40,V2=7
2020 GOSUB 7000
2030 ON V3 GOTO 2090,2040,2120,2070,4000
2040 LET POFROM$=""
2050 PRINT @(V1,V2),'CL',@(V1,V2),"First",
2060 GOTO 2120
2070 CALL "POC.LB",TYPE$,VENDPO$
2080 IF LEN(VENDPO$)<15 THEN GOTO 2040 ELSE LET V$=VENDPO$(9,7)
2090 IF V$="" THEN GOTO 2040
2100 LET POFROM$=V$
2110 PRINT @(V1,V2),POFROM$,'CL',
2120 GOTO 3900
2200 REM " --- Ending Purchase Order
2210 LET V0$="Z",V1$="KCE",V2$=POTHRU$,V3$="",V4$="Enter A Valid Purchase Order Number (<F1>=Last/<F3>=Lookup)",V0=7,V1=40,V2=8
2220 GOSUB 7000
2230 ON V3 GOTO 2290,2240,2330,2270,4000
2240 LET POTHRU$=""
2250 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
2260 GOTO 2330
2270 CALL "POC.LB",TYPE$,VENDPO$
2280 IF LEN(VENDPO$)<15 THEN GOTO 2240 ELSE LET V$=VENDPO$(9,7)
2290 IF V$="" THEN GOTO 2240
2300 IF POFROM$<>"" THEN IF V$<POFROM$ THEN GOTO 2200
2310 LET POTHRU$=V$
2320 PRINT @(V1,V2),POTHRU$,'CL',
2330 GOTO 3900
2400 REM " --- Beginning Vendor
2410 LET V0$="M",V1$="KCE",V2$=VENDFROM$,V3$=VENDIMASK$,V4$="Enter A Valid Vendor Number (<F1>=First/<F3>=Lookup)",V0=6,V1=40,V2=9
2420 GOSUB 7000
2430 ON V3 GOTO 2490,2440,2540,2470,4000
2440 LET VENDFROM$=""
2450 PRINT @(V1,V2),'CL',@(V1,V2),"First",
2460 GOTO 2540
2470 CALL "SYC.LV",V$
2480 IF V$="" THEN GOTO 2400
2490 IF V$="" THEN GOTO 2440
2500 DIM X1$(195)
2510 FIND (APM01_DEV,KEY=N0$+V$,DOM=2400)IOL=APM01A
2520 LET VENDFROM$=V$,VENDFNAME$(1)=X1$(1,30)
2530 PRINT @(V1,V2),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
2540 GOTO 3900
2600 REM " --- Ending Vendor
2610 LET V0$="M",V1$="KCE",V2$=VENDTHRU$,V3$=VENDIMASK$,V4$="Enter A Valid Vendor Number (<F1>=Last/<F3>=Lookup)",V0=6,V1=40,V2=10
2620 GOSUB 7000
2630 ON V3 GOTO 2690,2640,2750,2670,4000
2640 LET VENDTHRU$=""
2650 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
2660 GOTO 2750
2670 CALL "SYC.LV",V$
2680 IF V$="" THEN GOTO 2600
2690 IF V$="" THEN GOTO 2640
2700 IF VENDFROM$<>"" THEN IF V$<VENDFROM$ THEN GOTO 2600
2710 DIM X1$(195)
2720 FIND (APM01_DEV,KEY=N0$+V$,DOM=2600)IOL=APM01A
2730 LET VENDTHRU$=V$,VENDTNAME$(1)=X1$(1,30)
2740 PRINT @(V1,V2),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
2750 GOTO 3900
2800 REM " --- Which Date?
2810 LET V0$="S",V1$="C",V2$=WHICHDATE$,V3$="OPRD",V4$="O=Date Ordered  P=Promised By Date  R=Date Required  D=Don't Deliver Before",V0=1,V1=40,V2=11
2820 GOSUB 7000
2830 ON V3 GOTO 2840,2840,2950,2840,4000
2840 IF V$="" THEN GOTO 2800
2850 LET WHICHDATE$=V$
2860 ON POS(WHICHDATE$="OPRD") GOTO 2800,2870,2890,2910,2930
2870 LET DATETYPE$="Date Ordered"
2880 GOTO 2950
2890 LET DATETYPE$="Date Promised"
2900 GOTO 2950
2910 LET DATETYPE$="Date Required"
2920 GOTO 2950
2930 LET DATETYPE$="Not Before Date"
2940 GOTO 2950
2950 PRINT @(V1+2,V2),'CL',DATETYPE$
2960 GOTO 3900
3000 REM " --- Beginning Date
3010 LET V0$="D",V1$="6N",V2$=DATEFROM$,V3$="",V4$="",V0=6,V1=40,V2=12
3020 GOSUB 7000
3030 ON V3 GOTO 3050,3040,3070,3000,4000
3040 LET V$=""
3050 IF V$="" THEN PRINT @(V1,V2),"First   ",
3060 LET DATEFROM$=V$
3070 GOTO 3900
3200 REM " --- Ending Date
3210 LET V0$="D",V1$="6N",V2$=DATETHRU$,V3$="",V4$="",V0=6,V1=40,V2=13
3220 GOSUB 7000
3230 ON V3 GOTO 3250,3240,3280,3250,4000
3240 LET V$=""
3250 IF V$="" THEN PRINT @(V1,V2),"Last    ",
3260 IF V$<>"" THEN IF V$<DATEFROM$ THEN GOTO 3200
3270 LET DATETHRU$=V$
3280 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,2000,2200,2400,2600,2800,3000,3200,4000
4000 REM " --- Correct
4010 GOSUB REFRESH
4020 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run print overlay
4110 CALL "SYC.GA",7,1,"","",STATUS
4115 IF STATUS THEN GOTO 9900
4120 GOSUB SORTEM
4130 IF ITEMFROM$="" THEN LET ITEMFROM$=FILL(20),ITEMFDESC$=FILL(30)
4140 IF ITEMTHRU$="" THEN LET ITEMTHRU$=FILL(20),ITEMTDESC$=FILL(30)
4150 IF POFROM$="" THEN LET POFROM$=FILL(7)
4160 IF POTHRU$="" THEN LET POTHRU$=FILL(7)
4170 IF VENDFROM$="" THEN LET VENDFROM$=FILL(6),VENDFNAME$=FILL(30)
4180 IF VENDTHRU$="" THEN LET VENDTHRU$=FILL(6),VENDTNAME$=FILL(30)
4190 RUN "POR.JB"
5000 REM " --- Refresh screen data
5010 REFRESH: 
5020 PRINT 'CF',
5030 IF WHSEFROM$="" THEN PRINT @(40,3),"First",'CL', ELSE PRINT @(40,3),WHSEFROM$," ",WHSEFNAME$,'CL',
5040 IF WHSETHRU$="" THEN PRINT @(40,4),"Last",'CL', ELSE PRINT @(40,4),WHSETHRU$," ",WHSETNAME$,'CL',
5050 IF ITEMFROM$="" THEN PRINT @(40,5),"First" ELSE PRINT @(40,5),ITEMFROM$(1,ITEMLEN)," ",ITEMFDESC$(1,MIN(79-V1-ITEMLEN-1,30)),'CL',
5060 IF ITEMTHRU$="" THEN PRINT @(40,6),"Last" ELSE PRINT @(40,6),ITEMTHRU$(1,ITEMLEN)," ",ITEMTDESC$(1,MIN(79-V1-ITEMLEN-1,30)),'CL',
5070 IF POFROM$="" THEN PRINT @(40,7),"First" ELSE PRINT @(40,7),POFROM$,'CL',
5080 IF POTHRU$="" THEN PRINT @(40,8),"Last" ELSE PRINT @(40,8),POTHRU$,'CL',
5090 IF VENDFROM$="" THEN PRINT @(40,9),"First",'CL', ELSE PRINT @(40,9),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5100 IF VENDTHRU$="" THEN PRINT @(40,10),"Last",'CL', ELSE PRINT @(40,10),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5110 PRINT @(40,11),WHICHDATE$," ",DATETYPE$,'CL',
5120 IF DATEFROM$="" THEN PRINT @(40,12),"First   ", ELSE PRINT @(40,12),FNB6$(DATEFROM$)
5130 IF DATETHRU$="" THEN PRINT @(40,13),"Last    ", ELSE PRINT @(40,13),FNB6$(DATETHRU$)
5140 RETURN 
6800 REM " --- Build POW-15 Requisition Sort by Whse/Item
6805 SORTEM: 
6820 CALL "SYC.NB"," Sorting",5+ITEMLEN,COLUMN
6825 DIM WHSEITEM$(22),Z$(40)
6830 READ (POE12_DEV,KEY=N0$,DOM=6840)
6840 LET K$=KEY(POE12_DEV,END=6910)
6850 IF K$(1,2)<>N0$ THEN GOTO 6910
6860 READ (POE12_DEV,END=6910)IOL=POE12A
6870 LET Z$=K$(1,2)+WHSEITEM$+K$(9,10)+K$(3,6)
6880 PRINT @(COLUMN,11),Z$(1,2)+" "+WHSEITEM$(1,2)+WHSEITEM$(3,ITEMLEN)
6890 WRITE (POW15_DEV,KEY=Z$)IOL=POW15A
6900 GOTO 6840
6910 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8070 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

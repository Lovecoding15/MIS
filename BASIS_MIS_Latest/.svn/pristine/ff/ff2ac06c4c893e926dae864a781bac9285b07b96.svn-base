0010 REM "SHE - Package Processing (Header Entry)"
0020 REM "Program SHE.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "05Feb97 JAL: Remove SHE-04 xref when deleting shipment (4335-4345)"
0032 REM "10Nov97 KJS: Post to 5.2 or 6.0 orders (230,236,465,510,640,690,5545-5550,5560,5570-5571,5580-5585,5710)
0040 REM 
0050 SETERR 9000
0060 SETESC 9000
0065 ON O0 GOTO 0070,1000
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SHE-01",FILES$[3]="SHE-02"
0140 LET FILES$[4]="SHE-04",FILES$[5]="SHM-10"
0160 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1],SHE01_DEV=CHANNELS[2],SHE02_DEV=CHANNELS[3]
0190 LET SHE04_DEV=CHANNELS[4],SHM10_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 SHE01A: IOLIST A0$,A1$(1),A2$(1),A[ALL]
0220 SHE02A: IOLIST W0$,W1$(1),W[ALL]
0225 SHE04A: IOLIST K$
0230 ARE03A: IOLIST ORDER$(1)
0235 ARE33A: IOLIST *,ADDRESS$(1)
0236 ARE03F: IOLIST *,ADDRESS$(1)
0240 ARM01A: IOLIST *,ADDRESS$(1)
0250 ARM03A: IOLIST *,ADDRESS$(1)
0260 ARS01A: IOLIST X$,X$,P2$,X$,X$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0290 SHS01A: IOLIST SHS01A_KEY$,SPEEDSHIP$(1)
0310 SHM10T: IOLIST H0$(1),H[0],H[1]
0400 REM " --- Parameters"
0405 DIM P[3],SPEEDSHIP$(128),INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="SH",SHS01A$=N0$+N2$+"00"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=SHS01A
0440 LET P[0]=6,P[2]=9,P[3]=15,ORDPGM$="",OP$=SPEEDSHIP$(1,1),M0$="000000"
0450 IF OP$<>"Y" THEN GOTO 0500
0460 CALL "SYC.VA","OP",INFO$[ALL]
0465 LET OP$=INFO$[20],OPVER$=INFO$[4]
0470 IF OP$<>"Y" THEN GOTO 0500
0480 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0485 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=9,P[3]=7
0490 LET ORDPGM$=FNN$(SPEEDSHIP$(17,6))
0500 REM " --- Initializations"
0510 DIM J0$(20,"0"),J1$(40," "),I8$(80),ADDRESS$(256),ORDER$(107)
0520 DIM M4$(10,"#"),M5$(10,"0"),U[4],H0$(6),H[1],MESSAGE$[1]
0550 LET M1$="#,##0.00-",M2$="###0.00",M3$="##0.00",M0=LEN(M0$)
0570 LET U[0]=31,U[1]=55,U[2]=79,U[3]=103,U[4]=144
0600 REM " --- Other File Opens"
0610 LET FILES$[6]="SHM-02",FILES$[7]="SHM-05"
0620 IF OP$<>"Y" THEN GOTO 0650
0630 LET FILES$[6]="ARM-01",FILES$[7]="ARM-03"
0640 LET FILES$[8]="ARE-03",FILES$[9]="ARE-33"; IF OPVER$(1,1)<"6" THEN LET OPTIONS$[9]="F"
0650 CALL "SYC.DA",1,6,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0660 IF STATUS THEN GOTO 9900
0670 LET ARM01_DEV=CHANNELS[6],ARM03_DEV=CHANNELS[7]
0680 LET ARE03_DEV=CHANNELS[8],ARE33_DEV=CHANNELS[9]
0690 IF ARE33_DEV=0 THEN LET AON52$="Y" ELSE LET AON52$="N"
0700 REM " --- Background"
0710 CALL "SYC.WC",1,1,1,8,53,3,0
0720 CALL "SYC.WC",1,0,80,1,0,11,0
0730 CALL "SYC.WC",1,0,80,1,0,16,0
0740 CALL "SYC.WC",1,0,80,1,0,18,0
0750 PRINT 'SB',@(1,3),"Order No:",@(30),"Pickup #",@(57),"Shipment Summary",@(2,4),"Shipped:",@(28,4),"Total Pkgs",@(1,5),"Customer:",@(2,6),"Ship To:",@(38,7),"State:",@(38,9),"Zip Code",@(57,5),"Freight:",@(54,6),"Other Chgs:",@(59,7),"Total:",@(56,9),"Packages:",@(59,10),"Voids:",@(2,12),"Package:",@(2,13),"Service:",@(2,14)," Weight:",@(1,17),"Pkg",@(6),"Service",@(15),"Wt",@(21),"Freight",@(64),"Addt'l",@(73),"Total",'SF',
1000 REM " --- Header Initializations"
1010 REM " --- K9=0 for entry / K9=1 for display / A9=1 if record exists"
1020 DIM A0$(20),A1$(168),A2$(64),A[8],W0$(20),W1$(128),W[20],T0$(6),T1$(48)
1030 LET A0$(1,2)=N0$,A0$(18,3)="000",K9=0,C8$="",A9=0,I0=0
1100 REM " --- Clear screen"
1110 PRINT 'CF',
1120 PRINT @(11,3),FILL(19),@(11,4),FILL(10),@(11,5),FILL(39),
1130 FOR X=6 TO 9
1140 PRINT @(11,X),FILL(25),
1150 NEXT X
1160 PRINT @(1,10),FILL(46),@(11,12),FILL(5)
1170 FOR X=4 TO 10
1180 PRINT @(66,X),FILL(12),
1190 NEXT X
1200 REM " --- Order Number"
1210 LET V0$="S",V1$="^CKR",V2$=A0$(3,7),V3$="",V0=7,V1=11,V2=3
1220 LET V4$="<F2>=Manual Shipment   <F3>=Shipment Lookup"
1230 IF OP$="Y" THEN LET V4$="<F1>=Order Lookup   "+V4$
1240 GOSUB 7000
1250 ON V3 GOTO 1260,1300,1400,1500,9900
1260 IF FNP$(V$)="" THEN GOTO 1200
1270 IF V$=FILL(LEN(V$),"0") THEN GOTO 1200
1290 GOTO 1515
1300 REM " --- <F1> Order Lookup"
1310 IF OP$<>"Y" THEN GOTO 1200
1320 DIM X$(10)
1330 CALL "SYC.LO",V$,X$
1340 IF V$="" THEN GOTO 1200
1350 LET A0$(3,7)=V$(7,7),A1$(1,6)=V$(1,6),V$=V$(7,7)
1390 GOTO 1515
1400 REM " --- <F2> Manual Shipment"
1410 FIND (SYS01_DEV,KEY=SHS01A$,DOM=9800)IOL=SHS01A
1420 LET V$=SPEEDSHIP$(62,7),X=NUM(SPEEDSHIP$(63,6)),X=X+1,V3=0
1430 IF X>999999 THEN LET X=1000
1440 LET SPEEDSHIP$(63,6)=STR(X:"000000")
1450 WRITE (SYS01_DEV,KEY=SHS01A$)IOL=SHS01A
1480 PRINT @(V1,V2),V$,
1490 GOTO 1560
1500 REM " --- <F3> Shipment Lookup"
1510 GOSUB 6200
1515 IF V$="" THEN GOTO 1200
1520 IF V$(1,1)="M" THEN GOTO 1550
1530 LET X=NUM(V$,ERR=1200),V$=STR(X:FILL(V0,"0"),ERR=1200)
1540 GOTO 1560
1550 LET X=NUM(V$(2),ERR=1200),V$=V$(1,1)+STR(X:FILL(V0-1,"0"),ERR=1200)
1560 LET A0$(3,V0)=V$
1570 GOSUB 6000
1580 IF A2$(23,1)="Y" THEN GOTO 1000
1590 GOTO 3900
1600 REM " --- Shipment Date"
1605 IF A9=1 THEN GOTO 1690
1610 IF FNP$(A2$(1,6))="" THEN LET A2$(1,6)=F0$(18,6)
1620 LET V0$="D",V1$="C6N",V2$=A2$(1,6),V3$="",V0=6,V1=11,V2=4
1625 LET V4$="Enter the date shipped (<Enter>="+FNB6$(A2$(1,6))+")"
1630 GOSUB 7000
1640 ON V3 GOTO 1650,1600,3900,1600,3900
1650 IF FNP$(V$)="" THEN LET V$=F0$(18,6)
1660 LET A2$(1,6)=V$
1670 PRINT @(V1,V2),FNB6$(A2$(1,6)),
1690 GOTO 3900
1700 REM " --- Customer Number"
1710 IF A9=1 THEN GOTO 2050
1720 LET V4$="Enter customer number (<F1>=New Customer/<F3>=Lookup)"
1730 LET V0$="S",V1$="CKR",V2$=A1$(1,P[0]),V3$="",V0=P[0],V1=11,V2=5
1740 IF OP$<>"Y" THEN GOTO 1780
1750 LET V4$="Enter customer number (<F1>=Find Customer/<F3>=Lookup)"
1760 IF A0$(3,1)="M" THEN LET V4$="Enter customer (<F3>=Lookup)"
1770 LET V0$="M",V3$=M0$,V0=6
1780 GOSUB 7000
1790 ON V3 GOTO 2000,1800,3900,1900,3900
1800 REM " --- <F1> New Customer (Standalone)"
1810 IF OP$="Y" THEN GOTO 1850
1820 CALL "SHC.DA",V$
1830 IF FNP$(V$)="" THEN GOTO 1700
1840 GOTO 2000
1850 REM " --- <F1> Find Customer (ADD+ON)"
1860 IF A0$(3,1)="M" THEN GOTO 1700
1870 GOSUB 5800
1880 IF V$="" THEN GOTO 1700
1890 GOTO 2000
1900 REM " --- <F3> Customer Lookup"
1910 IF OP$="Y" THEN GOTO 1950
1920 CALL "SHC.LC",V$
1930 LET X=NUM(V$,ERR=2000),V$=FILL(6-LEN(V$),"0")+V$
1940 GOTO 2050
1950 CALL "SYC.LC",V$
2000 REM " --- Read and display customer"
2010 LET A1$(1,6)=V$
2020 READ (ARM03_DEV,KEY=N0$+A1$(1,6),ERR=2030)
2030 GOSUB 5500
2050 IF A9<>3 THEN GOTO 3900
2060 LET A9=0
2090 GOTO 1000
2100 REM " --- Customer Name"
2105 IF A9=1 THEN GOTO 2190
2110 LET V0$="S",V1$="C",V2$=A1$(7,24)+A1$(136,6),V3$="",V0=30,V1=18,V2=5
2115 IF OP$="Y" THEN LET V1=12+M0
2120 LET V4$="Enter the customer's name (<F1>=Display Next Ship-To Address)"
2130 GOSUB 7000
2140 ON V3 GOTO 2170,2150,3900,2100,3900
2150 GOSUB 5000
2160 GOTO 2100
2170 IF LEN(V$)<V0 THEN LET V$=V$+FILL(V0-LEN(V$))
2180 LET A1$(7,24)=V$(1,24),A1$(136,6)=V$(25)
2190 GOTO 3900
2200 REM " --- Address Lines"
2210 IF A9=1 THEN GOTO 2290
2215 IF A9=0 THEN GOTO 2225
2220 IF OP$="Y" THEN GOTO 2290
2225 LET X=U[I0-4],V0$="S",V1$="C",V2$=A1$(X,24),V3$=""
2230 LET V4$="Enter an address line",V0=24,V1=11,V2=I0+2
2235 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 LET A1$(X,24)=V$
2260 PRINT @(V1,V2),A1$(X,24),
2290 GOTO 3900
2300 REM " --- Zip Code"
2310 IF A9=1 THEN GOTO 2390
2320 IF A9=0 THEN GOTO 2340
2330 IF OP$="Y" THEN GOTO 2390
2340 LET V0$="S",V1$="CR",V2$=A1$(127,P[2]),V3$="",V0=P[2],V1=38,V2=10
2345 LET V4$="Enter a zip or postal code"
2350 GOSUB 7000
2360 ON V3 GOTO 2370,2300,3900,2300,3900
2370 LET V$=FNP$(V$),A1$(127,P[2])=V$
2380 PRINT @(V1,V2),FNZ$(A1$(127)),
2390 GOTO 3900
2400 REM " --- Extract destination state code from address"
2410 LET X$=FNP$(A1$(144,24)),V3=0
2420 IF LEN(X$)>1 THEN GOTO 2480
2430 FOR X=103 TO 31 STEP -24
2440 LET X$=FNP$(A1$(X,24))
2450 IF LEN(X$)>1 THEN EXITTO 2480
2460 NEXT X
2470 LET X$="  "
2480 LET A1$(142,2)=X$(LEN(X$)-1,2)
2485 PRINT @(45,7),A1$(142,2),
2490 GOTO 3900
3900 REM " --- Next field"
3910 IF V3=4 THEN IF I0<10 THEN LET I0=9
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1600,1700,2100,2200,2200,2200,2200,2200,2300,2400,3990
3990 LET I0=1,A9=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?",V0=6,V2=22
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V1=FNV(V4$)
4030 GOSUB 7000
4035 ON V3 GOTO 4040,4000,4000,4000,9900
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,3900
4100 REM " --- Update SHE-01 header"
4110 WRITE (SHE01_DEV,KEY=A0$)IOL=SHE01A
4120 CALL ORDPGM$,ERR=4130,SHE01_DEV,SHE02_DEV,ARE03_DEV,A0$
4130 IF A2$(1,6)<>O2$ THEN GOSUB 6900
4150 REM " --- Run line item overlay"
4190 RUN "SHE.AB"
4200 REM " --- Delete entire shipment"
4290 READ (SHE02_DEV,KEY=A0$(1,18),DOM=4300)
4300 REM " --- Delete packages"
4310 LET K$=KEY(SHE02_DEV,END=4400)
4320 IF POS(A0$(1,17)=K$)<>1 THEN GOTO 4400
4330 READ (SHE02_DEV,KEY=K$)IOL=SHE02A
4335 REMOVE (SHE02_DEV,KEY=K$)
4340 LET K$=N0$+"A"+W1$(11,1)+W1$(59,2)+A2$(1,6)+W1$(12,4)+W0$(3,15)+W0$(18,3)
4345 REMOVE (SHE04_DEV,KEY=K$,DOM=4350)
4350 LET H0$(1)=N0$+"T"+W1$(11,1)+W1$(59,2),H[0]=0
4360 FIND (SHM10_DEV,KEY=H0$,DOM=4370)IOL=SHM10T
4370 IF H[0]>0 THEN LET H[0]=H[0]-1
4380 WRITE (SHM10_DEV,KEY=H0$)IOL=SHM10T
4390 GOTO 4300
4400 REM " --- Delete header"
4410 DIM A[8]
4420 REMOVE (SHE01_DEV,KEY=A0$,DOM=4430)
4430 CALL ORDPGM$,ERR=4440,SHE01_DEV,SHE02_DEV,ARE03_DEV,A0$
4450 PRINT @(39,4),FILL(8),@(39,4),H[0],
4490 GOTO 1000
5000 REM " --- Display next ship-to address"
5010 LET K$=KEY(ARM03_DEV,END=5070)
5020 READ (ARM03_DEV)IOL=ARM03A
5030 IF POS(N0$+A1$(1,6)=K$)<>1 THEN GOTO 5070
5040 LET LINE4$="",LINE5$=""
5045 GOSUB 5700
5060 GOTO 5090
5070 READ (ARM03_DEV,KEY=N0$+A1$(1,6),DOM=5080)
5080 GOSUB 5500
5090 RETURN 
5100 REM " --- Display record"
5110 LET V$=A1$(1,P[0])
5120 IF OP$="Y" THEN LET V$=FNF$(A1$(1,P[0]),M0$)
5130 PRINT @(11,3),A0$(3,7),@(11,4),FNB6$(A2$(1,6)),@(11,5),V$,@(18,5),A1$(7,24),A1$(136,6),@(11,6),A1$(31,24),@(11,7),A1$(55,24),@(11,8),A1$(79,24),@(11,9),A1$(103,24),@(11,10),A1$(144,24),@(38,10),FNZ$(A1$(127)),@(66,5),A[0]:M1$,@(66,6),A[1]:M1$,@(66,7),A[0]+A[1]:M1$,@(66,9),A[2],@(66,10),A[3],
5190 RETURN 
5500 REM " --- Retrieve shipping address"
5505 IF A9=1 THEN GOTO 5790
5507 DIM ADDRESS$(256)
5510 LET V4$="This customer does not exist"
5515 FIND (ARM01_DEV,KEY=N0$+A1$(1,6),DOM=5600)IOL=ARM01A
5520 LET LINE4$=ADDRESS$(179,24),LINE5$=ADDRESS$(203,24)
5525 IF OP$<>"Y" THEN GOTO 5700
5530 IF A0$(3,1)="M" THEN GOTO 5700
5535 LET A9=2,V4$="This order doesn't exist for this customer"
5540 FIND (ARE03_DEV,KEY=N0$+"  "+A1$(1,6)+A0$(3,7)+"000",DOM=5600)IOL=ARE03A
5545 LET A2$(7,6)=FNYY_YY21$(FNM$(FNC$(ORDER$(77,3)))),A2$(13,10)=ORDER$(49,10),SHIPTO$=ORDER$(102,6)
5546 IF AON52$="Y" THEN LET SHIPTO$=ORDER$(30,2)
5550 IF FNP$(SHIPTO$)="" THEN GOTO 5700
5555 LET LINE4$="",LINE5$=""
5560 IF (AON52$="N" AND SHIPTO$<>"000099") OR (AON52$="Y" AND SHIPTO$<>"99") THEN GOTO 5580
5565 LET V4$="Missing manual ship-to for this order"
5570 IF AON52$="N" THEN FIND (ARE33_DEV,KEY=ORDER$(1,2)+ORDER$(5,13),DOM=5600)IOL=ARE33A
5571 IF AON52$="Y" THEN FIND (ARE03_DEV,KEY=ORDER$(1,2)+ORDER$(5,13)+"F",DOM=5600)IOL=ARE03F
5575 GOTO 5700
5580 LET V4$="Missing ship-to address "+SHIPTO$+" for this customer"
5585 FIND (ARM03_DEV,KEY=N0$+A1$(1,6)+SHIPTO$,DOM=5600)IOL=ARM03A
5590 GOTO 5700
5600 REM " --- Missing order"
5601 LET MESSAGE$[0]=V4$,A9=0
5610 DIM X$(58)
5620 LET X$(FNC(V4$,58))=V4$,A9=0,MESSAGE$[0]=X$
5650 LET MESSAGE$[1]="Press <Enter> For Manual Shipment Or <F4> To Exit"
5660 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
5670 IF V3<>4 THEN GOTO 5700
5680 LET A9=3
5690 GOTO 5790
5700 REM " --- Transfer address information"
5710 LET A1$(7,24)=ADDRESS$(1,24),A1$(31,72)=ADDRESS$(31,72)
5720 LET A1$(127,9)=ADDRESS$(103,9),A1$(136,6)=ADDRESS$(25,6)
5730 LET A1$(103,24)=LINE4$,A1$(144,24)=LINE5$
5740 GOSUB 5100
5790 RETURN 
5800 REM " --- Find customer when given order number"
5810 LET V$=""
5880 PRINT @(0,22),'CL','SB',@(30),"Scanning:",'SF',
5890 READ (ARE03_DEV,KEY=N0$,DOM=5900)
5900 REM " --- Next ARE-03 record"
5910 LET K$=KEY(ARE03_DEV,END=5980)
5915 IF POS(N0$=K$)<>1 THEN GOTO 5980
5920 PRINT @(40,22),K$(5,6)," ",K$(11,7),
5925 IF K$(11,7)=A0$(3,7) THEN GOTO 5970
5930 READ (ARE03_DEV)
5940 GOTO 5900
5970 LET V$=K$(5,6)
5980 PRINT @(0,22),'CL',
5990 RETURN 
6000 REM " --- Retrieve SHE-01 Shipment Header"
6010 READ (SHE01_DEV,KEY=N0$+A0$(3,15)+"000",DOM=6050)IOL=SHE01A
6020 CALL "SHC.HA",SHE01_DEV,SHE02_DEV,A0$,A[ALL]
6030 LET A9=1,K9=1,O2$=A2$(1,6)
6050 GOSUB 5100
6090 RETURN 
6200 REM " --- Shipment Lookup"
6210 LET NUMBER=3,COLUMN=1,ROW=V2+2,RECORD$=""
6220 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6230 LET DESCRIPTION$[0]="Order",FIELD[0]=1,POSITION[0]=3,LENGTH[0]=7
6240 LET DESCRIPTION$[1]="Customer",FIELD[1]=2,POSITION[1]=1,LENGTH[1]=6
6250 LET DESCRIPTION$[2]="Name",FIELD[2]=2,POSITION[2]=7,LENGTH[2]=24
6260 LET DESCRIPTION$[3]="PO Number",FIELD[3]=3,POSITION[3]=13,LENGTH[3]=10
6270 LET TITLE$="Current Shipments"
6280 CALL "SYC.LK",SHE01_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6290 RETURN 
6900 REM " --- Update SHE-04 Package Cross-Reference if ship date changes"
6910 READ (SHE02_DEV,KEY=A0$,DOM=6920)
6920 LET K$=KEY(SHE02_DEV,END=6990)
6925 IF K$(1,17)<>A0$(1,17) THEN GOTO 6990
6930 READ (SHE02_DEV)IOL=SHE02A
6945 LET K$=N0$+"A"+W1$(11,1)+W1$(59,2)+O2$+W1$(12,4)+W0$(3,15)+W0$(18,3)
6950 REMOVE (SHE04_DEV,KEY=K$,DOM=6960)
6960 LET K$=N0$+"A"+W1$(11,1)+W1$(59,2)+A2$(1,6)+W1$(12,4)+W0$(3,15)+W0$(18,3)
6970 WRITE (SHE04_DEV,KEY=K$)IOL=SHE04A
6980 GOTO 6920
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8065 DEF FNN$(Q$)=FNP$(Q$(POS("NONE"=Q$)*4+1))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9910 RUN "SYS.AA"
9999 END

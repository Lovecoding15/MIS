0010 REM "IVC - Inventory SpeedSearch (Comments)"
0020 REM "Program IVC.QE"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open/Lock Files"
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="IVM-01",FILES$[2]="IVM-09",FILES$[3]="IVM-10"
0135 LET FILES$[4]="IVM-13",FILES$[5]="IVM-14",FILES$[6]="IVM-15"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET IVM01_DEV=CHANNELS[1],IVM09_DEV=CHANNELS[2],IVM10_DEV=CHANNELS[3]
0170 LET IVM13_DEV=CHANNELS[4],IVM14_DEV=CHANNELS[5],IVM15_DEV=CHANNELS[6]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1)
0220 IVM09A: IOLIST B0$(1),B1$(1)
0230 IVM10A: IOLIST X0$(1)
0240 IVM13A: IOLIST Y0$(1),Y1$(1)
0250 IVM15A: IOLIST W0$(1),W1$(1)
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 DIM P[5],X$(128)
0420 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2),MAXPAGE=NUM(X$(66,2))
0425 LET TNAME$=FNP$(X$(3,8)),ONAME$=FNP$(X$(27,8)),SNAME$=FNP$(X$(19,8))
0430 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0445 LET SHEIGHT=NUM(X$(45,2)),MAXOPT=NUM(X$(57,2))
0450 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0455 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0460 LET PAGES=INT((DSZ-5000)/4000),SYS01_DEV=NUM(STBL("!SYS01"))
0465 IF PAGES<3 THEN GOTO 9900
0470 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0480 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9900)IOL=IVS01A
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(5,1))
0495 LET P[3]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2)),P[5]=NUM(P2$(13,2))
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM A0$(22),A1$(60),A2$(64),B0$(24),B1$(48),OPTLIST$[1,MAXOPT,6]
0520 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),X0$(32),Y0$(5),Y1$(32)
0530 DIM FILES[14],ITEM$[6],ITEM[12],PARAMS[3],W0$(5),W1$(32)
0540 LET D_STATUS=0,MAXROW=HEIGHT-5,A0$(1)=N0$+OPTION$[3],H0=WIDTH-2,DONE=0
0550 LET FILES[1]=IVM01_DEV,FILES[14]=IVM14_DEV,NF$="(Not On File)"
0560 LET PARAMS[0]=P[0],PARAMS[1]=P[3],PARAMS[2]=P[4],PARAMS[3]=P[5]
0570 LET PGM$=PGM(-2),OPTION=0
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,""
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0720 LET HEADING$(FNC(TITLE$,WIDTH-2))=TITLE$
0730 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0740 PRINT @(0,0),'CS','SB','BR',HEADING$,'ER','SF',@(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",TNAME$,0
0920 PRINT @(0,1),'CL',@(0,2),'CL',@(0,3),'CL',
0930 CALL "SYC.WB",SNAME$,0
0940 PRINT 'CF',
0980 READ (IVM01_DEV,KEY=A0$,DOM=1000)IOL=IVM01A
0990 GOTO 1100
1000 REM " --- Process key field"
1010 GOSUB 6000
1015 CALL "SYC.WB",TNAME$,0
1020 PRINT @(0,0),'CS',@(0,2),'SB'," Item:",'SF',
1025 LET V2$="",V2$=KEY(IVM01_DEV,END=1040)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1035 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1040 LET ACTION$="V",V1=7,V2=2,V4$=""
1045 CALL "IVC.IA",ACTION$,V1,V2,FILES[ALL],N0$,V2$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1050 ON V3 GOTO 1080,1000,1000,1000,1060,1070,1060
1060 LET OPTION$[1]="EXIT"
1065 GOTO 4000
1070 LET OPTION$[1]="STARTOVER"
1075 GOTO 4000
1080 LET V$=FNP$(ITEM$[0])
1090 IF V$="" THEN GOTO 0900
1095 LET A0$(3)=V$
1200 REM " --- Retrieve records for display"
1210 DIM LLIST$[PAGES,MAXROW,5],X0$(32),DESCRIPTION$(H0-8-P[0])
1220 DIM MORE$[PAGES,MAXROW,SHEIGHT-2]
1230 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,X0$(7)=NF$,Y1$(1)=NF$,W1$(1)=NF$
1240 READ (IVM01_DEV,KEY=A0$,DOM=1000)IOL=IVM01A
1250 FIND (IVM10_DEV,KEY=N0$+"A"+A2$(1,3),DOM=1260)IOL=IVM10A
1260 FIND (IVM13_DEV,KEY=N0$+A2$(21,3),DOM=1270)IOL=IVM13A
1270 FIND (IVM15_DEV,KEY=N0$+A2$(53,3),DOM=1280)IOL=IVM15A
1280 LET DESCRIPTION$(1)=FNITEM$(A1$,P[3],P[4],P[5])
1290 READ (IVM09_DEV,KEY=A0$,DOM=1300)
1300 REM " --- Display header/footer information"
1310 LET X$='SB'+" Item: "+'SF'+A0$(3,P[0])+" "+DESCRIPTION$
1320 LET X1$=A0$(3,P[0]),X2$=PARAM$
1330 CALL "SYC.QB",X$,X1$,X2$
1340 PRINT @(0,2),X$,
1360 CALL "SYC.WB",SNAME$,0
1370 PRINT 'CS',@(4,0),'SB',"Product: ",'SF',A2$(1,3)," ",X0$(7,18),'SB',@(H0-20),"Unit Of Measure: ",'SF',A2$(4,2),@(1,1),'SB',"Item Class: ",'SF',A2$(21,3)," ",Y1$(1,19),@(H0-19),'SB',"Stocking Level: ",'SF',A2$(24,1),@(2,2),'SB',"Item Type: ",'SF',A2$(53,3)," ",W1$(1,16),@(H0-22),'SB',"Lotted/Serialized: ",'SF',A2$(19,1),
1500 REM " --- Read comment record"
1510 LET K$=KEY(IVM09_DEV,END=1900)
1520 IF POS(A0$=K$)<>1 THEN GOTO 1900
1530 READ (IVM09_DEV)IOL=IVM09A
1540 IF L<MAXROW THEN GOTO 1600
1545 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
1550 GOSUB 5000
1555 IF DONE THEN GOTO 4000
1570 IF ANSWER$<>"" THEN GOTO 3000
1580 LET L=1,X1=1
1600 REM " --- Store record"
1610 DIM G1$(WIDTH-2)
1620 LET G1$(2)=B1$
1630 IF FNP$(B1$)="" THEN LET G1$(2)="-"
1640 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$(3),X0=X0+1,X1=X1+1,L=L+1
1690 GOTO 1500
1900 REM " --- EOF"
1910 IF X0>0 THEN GOTO 2000
1920 DIM MESSAGE$[1]
1930 LET MESSAGE$[0]="No comments found (<Enter>=Continue)"
1940 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
1990 GOTO 3000
2000 REM " --- Display last page"
2010 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2020 GOSUB 5000
2030 IF DONE THEN GOTO 4000
2040 IF ANSWER$<>"" THEN GOTO 3000
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$=A0$(3),CHOICE2$=""
3030 GOSUB 5000
3040 IF CONTROL=1 THEN GOTO 1000
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CS',
6090 RETURN 
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9950 EXIT 
9999 END

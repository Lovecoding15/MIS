0010 REM "ARR - Aging Analysis Report (Report Overlay) ON HOLD CUSTOMERS ONLY
0020 REM "Program ARR.ZB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open Files
0160 CALL "BPS.01",3,PRTR$,STATUS; IF STATUS THEN GOTO 9900
0170 CALL "SYC.GA",7,1,"",PRTR$,STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLists
0210 ARM01A: IOLIST A0$,A1$,A2$
0220 ARM01A1: IOLIST K$
0230 ARM02A: IOLIST B0$,B1$,B[ALL]
0250 ARM06A: IOLIST D0$(1),D1$,D[ALL]
0260 ARM10A: IOLIST X0$
0500 REM " --- Init Data
0510 LET HEADINGS=4,WIDTH=132,PAGE=0,CLOCK$="",WHEN$=F0$(7,3)
0520 DIM D0$(40),D[8],B[7],C0$(48),X0$(48),HEADINGS$[HEADINGS],O[6]
0560 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,HEADINGS$[2]=H2$
0570 LET HEADINGS$[3]=H3$,L9=59,L=L9+1
0600 REM " --- Print Positions"
0610 LET O[6]=WIDTH-M1,O[5]=O[6]-M1,O[4]=O[5]-M1,O[3]=O[4]-M1
0620 LET O[2]=O[3]-20,O[1]=O[2]-5,O[0]=O[1]-9
0630 LET P8$="A"
0700 REM " --- Background"
0720 CALL "SYC.NB","Printing",M0,COLUMN
1000 REM " --- Initialize"
1020 IF P8$="C" THEN GOTO 1050
1030 READ (ARM04_DEV,KEY=N0$+"A"+FROM_CUST$,DOM=1035)
1035 GOTO 6100
1040 IF FROM_CUST$="" THEN GOTO 1100
1050 READ (ARM01_DEV,KEY=N0$+FROM_CUST$,DOM=1120)IOL=ARM01A1
1060 GOTO 1140
1100 REM " --- Main Read
1110 IF P8$="A" THEN GOTO 6100
1120 LET K$=KEY(ARM01_DEV,END=4000)
1130 IF K$(1,2)<>N0$ THEN GOTO 4000
1140 READ (ARM01_DEV)
1170 FIND (ARM01_DEV,KEY=K$)IOL=ARM01A
1180 IF A0$(1,2)<>N0$ THEN GOTO 4000
2000 REM " --- Find Detail
2010 DIM D[8],D0$(40),D1$(24)
2030 FIND (ARM02_DEV,KEY=K$+"  ",DOM=1100)IOL=ARM02A
2040 FIND (ARM06_DEV,KEY=K$,DOM=2050)IOL=ARM06A
2050 IF B1$(39,1)<>"Y" THEN GOTO 1100
2100 REM " --- Print
2110 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$),
2120 GOSUB 6600
2130 IF L+3>L9 THEN GOSUB 5000
2140 PRINT (7)FNF$(A0$(3,P[0]),M0$),@(M0+1),A1$(1,30),@(O[0]-1)," ",FNB$(D1$(1,3)),@(O[1]),D[6]:"###0",@(O[2]),B1$(4,2),@(O[3]),B[0]:M1$,@(O[4]),B[2]:M1$,@(O[5]),B[4]:M1$,@(O[6]),D[0]:M1$
2150 PRINT (7)FNF$(A1$(112,10),M8$)," ",FNF$(A1$(122,4),"####"),@(20),A1$(228,20),@(O[0]-1)," ",FNB$(D1$(4,3)),@(O[2]),X0$(6,20),@(O[3]),B[1]:M1$,@(O[4]),B[3]:M1$,@(O[5]),B[5]:M1$,@(O[6]),D[1]:M1$
2160 PRINT (7)""
2170 LET L=L+3
2180 GOTO 1100
4000 REM " --- All done"
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+2
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)"Cust #",@(M0+1),"Customer Name",@(O[0]),"Last Inv",@(O[1]),"Days",@(O[2]),"Terms",@(O[3]+M1-7),"Future",@(O[4]+M1-8),"30 Days",@(O[5]+M1-8),"90 Days",@(O[6]+M1-10),"MTD Sales"
5050 PRINT (7)"Telephone",@(16),"Ext",@(20),"Contact",@(O[0]),"Last Pay",@(O[1]),"Pay",@(O[2]),"Code",@(O[3]+M1-8),"Current",@(O[4]+M1-8),"60 Days",@(O[5]+M1-9),"120 Days",@(O[6]+M1-10),"YTD Sales"
5060 PRINT (7)""
5070 LET L=L+3
5090 RETURN 
6100 REM " --- Alpha Sort
6120 LET K1$=KEY(ARM04_DEV,END=4000)
6130 READ (ARM04_DEV)
6140 IF K1$(1,3)<>N0$+"A" THEN GOTO 4000
6150 IF THRU_CUST$<>"" AND CVS(K1$(4,10),2)>THRU_CUST$ THEN GOTO 4000
6160 LET K$=K1$(1,2)+K1$(14,6)
6170 GOTO 1170
6600 REM " --- Find Terms Code
6605 LET X0$(6,15)="***Not On File***"
6610 FIND (ARM10_DEV,KEY=N0$+"A"+B1$(4,2),DOM=6620)IOL=ARM10A
6620 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

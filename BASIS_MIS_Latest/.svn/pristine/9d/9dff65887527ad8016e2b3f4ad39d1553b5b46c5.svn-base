0010 REM "OPR - Ship Schedule Report II"
0020 REM "Program OPR.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0090 SETERR 9000
0095 SETESC 9000
0200 REM " --- IOLISTS "
0210 ARW03A: IOLIST A0$,A1$,A[ALL]
0220 IVM01A: IOLIST *,X1$,X2$,X3$
0230 IVM02A: IOLIST W0$,W1$,W2$,W[ALL]
0500 REM " --- Initialize Data "
0505 LET HEADINGS=4
0510 DIM A[1],T[6],C[5],W[16],HEADINGS$[HEADINGS]
0540 LET H1$=N4$,H0=132,H=0,L9=59,L=L9+1,T0=0
0550 IF POS("O"=P7$)>0 AND POS("R"=P7$)=0 THEN LET P7$=P7$+"R"
0560 IF P9$<>"D" OR M0+M1+7*M2+31<132 THEN GOTO 0600
0570 IF M1>12 THEN CALL "SYC.RM",M1$,M1,12,M1-1
0580 IF M2>9 THEN CALL "SYC.RM",M2$,M2,9,M2-1
0590 GOTO 0560
0600 CALL "SYC.GA",7,1,"","",STATUS
0610 IF STATUS>0 THEN GOTO 9900
0650 IF P9$="D" THEN LET H1$="Detail "+H1$ ELSE LET H1$="Summary "+H1$
0660 LET H2$="Product Selection: "
0670 IF P8$="" THEN LET H2$=H2$+"All" ELSE LET H2$=H2$+P8$
0680 LET HEADINGS$[0]=N1$,HEADINGS$[1]=H1$,HEADINGS$[2]=H2$
0690 LET HEADINGS$[3]=H3$,WHEN$=F0$(7,3),CLOCK$=""
1000 REM " --- Initial Read "
1020 READ (ARW03_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Main Read "
1110 DIM A0$(54)
1120 READ (ARW03_DEV,END=4000)IOL=ARW03A
1130 IF POS(A1$(16,1)=P7$)=0 THEN GOTO 1100
1140 IF WH$<>"" THEN IF A0$(43,2)<>WH$ THEN GOTO 1100
1200 REM " --- Breaks "
1220 IF A0$(1,2+I[0])<>T0$ THEN GOSUB 6000
1240 PRINT @(COLUMN,11),A0$(3,I[0]),
2000 REM " --- Accumulate "
2005 DIM C[5]
2010 LET X=POS(A1$(12,3)>=H4$,-3),X=INT((X+2)/3),C[X]=C[X]+A[0]
2020 IF P9$<>"D" THEN GOTO 2100
2030 IF L+1>L9 THEN GOSUB 5200
2040 GOSUB 6200
2045 IF A1$(21,1)="Y" THEN PRINT (7)@(132-M1-7*M2-30-M0),"Drop",
2050 PRINT (7)@(132-M1-7*M2-25-M0),FNF$(A1$(3,P[0]),P5$),@(132-M1-7*M2-24),A0$(45,7)," ",A1$(16,1),@(132-M1-7*M2-14),FNB$(A1$(12,3)),"  ",A1$(20,1),
2055 FOR X=0 TO 5; IF C[X]<>0 THEN PRINT (7)@(132-M1-(7-X)*M2),C[X]:M2$,
2057 NEXT X
2060 PRINT (7)@(132-M1-M2),A[0]:M2$,@(132-M1),A[1]:M1$
2080 LET L1=L1+1,L2=L2+1,L=L+1
2100 FOR J=0 TO 5; LET T[J]=T[J]+C[J]; NEXT J; LET T[6]=T[6]+A[1],U0=U0+A[1]
2290 GOTO 1100
4000 REM " --- End "
4010 LET T9$="E"
4020 GOSUB 6000
4030 IF H=0 THEN GOSUB 5000
4050 PRINT (7)@(132-M1-M2*7),J$(1,M1+M2*7),'LF',@(132-M1-16),"Report Value: ",@(132-M1),U0:M1$
4100 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1,T3=0
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)@(0),"Item",@(I[0]),"Whse",@(5+I[0]),"Prod",@(10+I[0]),"UM",@(15+I[0]),"Description"
5105 FOR X=1 TO 5; PRINT (7)@(132-M1-(6-X)*M2-8),FNB$(H4$(X*3-2,3)),; NEXT X
5120 PRINT (7)@(132-M1-6*M2-6),"Past",@(132-M1-6),"Total",@(126),"Total"
5150 IF P9$="D" THEN PRINT (7)@(132-M1-7*M2-25-M0),"Cust",@(132-M1-7*M2-24),"Order",@(132-M1-7*M2-14),"Shp-Date Com?",
5155 FOR X=1 TO 5; PRINT (7)@(132-M1-(6-X)*M2-8),FNB$(H5$(X*3-2,3)),; NEXT X
5170 PRINT (7)@(132-M1-6*M2-6),"Due",@(132-M1-4),"Qty",@(126),"Value"
5180 LET L=L+3
5190 RETURN 
5200 REM " --- Print Item "
5220 IF L>L9-2 THEN GOSUB 5000
5230 IF WH$="" THEN LET X$="ALL" ELSE LET X$=A0$(43,2)
5240 PRINT (7)'LF',@(0),A0$(3,I[0]),@(1+I[0]),X$,@(5+I[0]),A1$(9,3),@(10+I[0]),X2$(4,2),@(15+I[0]),X1$
5260 LET L=L+2,L1=0
5290 RETURN 
6000 REM " --- Item Break "
6020 IF T0$="" THEN GOTO 6100
6025 IF P9$="D" THEN IF L2<=1 THEN GOTO 6050
6030 IF L+2>L9 THEN GOSUB 5000
6035 IF P9$="D" THEN PRINT (7)@(132-M1-7*M2),J$(1,M2*6),@(132-M1-M2),J$(1,M1+M2); LET L=L+1
6040 FOR X=0 TO 5; PRINT (7)@(132-M1-(7-X)*M2),T[X]:M2$,; NEXT X
6045 PRINT (7)@(132-M1-M2),T[0]+T[1]+T[2]+T[3]+T[4]+T[5]:M2$,@(132-M1),T[6]:M1$
6047 LET L=L+1
6060 REM " --- Item Total "
6070 GOSUB 6200
6100 IF T9$<>"" THEN RETURN 
6102 DIM T[6]
6110 LET T0$=A0$(1,I[0]+2),L1=0,L2=0
6120 DIM X1$(I[1]+I[2]+I[3]),X2$(50),T[6],C[5]
6140 LET X1$(1)="*Not on File*",X3$=""
6150 FIND (IVM01_DEV,KEY=N0$+A0$(3,20),DOM=6160)IOL=IVM01A
6160 IF A1$(15,1)="N" THEN LET X1$(1)="*Non-Stock Item*",X2$(4,2)="EA"
6180 GOSUB 5200
6190 RETURN 
6200 REM " --- All Warehouses "
6202 LET A3$=X3$
6210 DIM B[16],W[16]
6220 IF A3$="" THEN RETURN 
6230 FOR X=1 TO LEN(A3$) STEP 2
6240 FIND (IVM02_DEV,KEY=N0$+A3$(X,2)+T0$(3,I[0]),DOM=6280)IOL=IVM02A
6250 FOR X1=0 TO 16
6260 LET B[X1]=B[X1]+W[X1]
6270 NEXT X1
6280 NEXT X
6290 RETURN 
8000 REM " --- Functions "
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

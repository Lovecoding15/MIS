0010 REM "DDR - Record Layout Listing (Report)"
0020 REM "Program DDR.CB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLists
0210 DDM01A: IOLIST X0$(1),X1$(1),X[ALL]
0230 DDM03A: IOLIST B0$(1),B1$(1),B[ALL]
0240 DDM04A: IOLIST C0$(1),C1$(1),C[ALL]
0250 DDM12A: IOLIST D0$,D1$
0260 DDM13A: IOLIST D0$,D1$
0270 DDM14A: IOLIST D0$,D1$
0280 DDW01A: IOLIST T0$(1),T0
0290 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0300 SYM04A: IOLIST S0$(1),S1$(1)
0500 REM " --- Initializations
0510 DIM X0$(12),X1$(105),X[2],MILESTONE$(39)
0520 DIM A0$(6),A1$(77),A2$(30),A[2],B0$(7),B1$(64),B[2]
0530 DIM C0$(10),C1$(122),C[2],T0$(6)
0540 IF BEGFILE$<>"" THEN LET BEGFILE$=BEGFILE$+FILL(6-LEN(BEGFILE$))
0545 IF ENDFILE$<>"" THEN LET ENDFILE$=ENDFILE$+FILL(6-LEN(ENDFILE$))
0550 LET HEADINGS=1,PAGE=0,WIDTH=80,WHEN$=F0$(7,3),L9=59,L=L9+1,MODULE$=""
0555 DIM HEADINGS$[HEADINGS],TYPES$[8],DASHES$(WIDTH,"-")
0560 LET TYPES$[1]="Serial",TYPES$[2]="Direct",TYPES$[3]="Global"
0570 LET TYPES$[4]="Index",TYPES$[5]="MKeyed",TYPES$[6]="Ref"
0580 LET TYPES$[7]="Sort",TYPES$[8]="Text",M$="#,###",TOC=0
0590 LET VERSION$="6.0.0",LASTFILE$="",LASTREC$=""
0595 LET VERSION$=STBL("!VERSION",ERR=0600)
0600 LET HEADINGS$[0]="ADD+ON Software - Version "+VERSION$
0700 REM " --- Background
0710 CALL "SYC.NB","Printing",39,COLUMN
0900 REM " --- Position file
0910 IF TOC$="Y" THEN CALL "SYC.FA",DDW01_DEV
0960 LET K$=SYSTEM$
0970 IF SYSTEM$="" THEN LET K$=BEGFILE$
0980 READ (SYM03_DEV,KEY=K$,DOM=1000)IOL=SYM03A
0990 GOTO 1030
1000 REM " --- Read next sort record
1010 LET K$=KEY(SYM03_DEV,END=4000)
1020 READ (SYM03_DEV)IOL=SYM03A
1025 IF BEGFILE$<>"" THEN IF A0$(1,6)<BEGFILE$ THEN GOTO 3900
1030 IF ENDFILE$<>"" THEN IF A0$(1,6)>ENDFILE$ THEN GOTO 4000
1040 IF SYSTEM$<>"" THEN IF A0$(1,LEN(SYSTEM$))<>SYSTEM$ THEN GOTO 4000
1050 IF REVISION$<>"" THEN IF A1$(31,6)<REVISION$ THEN GOTO 3900
1070 LET OLDHDR$="",OLDA0$="",OLDA1$=""
1075 LET PREVMARK$="K"
1080 IF A0$(1,1)="!" THEN IF MODULE$="!" THEN GOTO 1400
1085 IF A0$(1,2)=MODULE$ THEN GOTO 1400
1090 GOSUB 6600
1100 REM " --- Print Index?
1110 IF INDEX$<>"Y" THEN GOTO 1400
1120 LET OLDHDR$=HEADINGS$[1],HEADINGS$[1]=HEADINGS$[1]+" File Index"
1170 PRINT @(COLUMN,11),MODULE$," Index",
1180 LET INDEX=1,COMMENTS=0,L=L9+1,LAST$="",OLDA0$=A0$,OLDA1$=A1$
1190 READ (DDM03_DEV,KEY=A0$,DOM=1200)
1200 REM " --- Read next DDM-03 Record ID record
1210 LET K$=KEY(DDM03_DEV,END=1400)
1220 IF POS(MODULE$=K$)<>1 THEN GOTO 1400
1230 READ (DDM03_DEV)IOL=DDM03A
1240 IF BEGFILE$<>"" THEN IF B0$(1,6)<BEGFILE$ THEN GOTO 1200
1250 IF ENDFILE$<>"" THEN IF B0$(1,6)>ENDFILE$ THEN GOTO 1200
1260 LET FILENAME$="",DESCRIPTION$=""
1270 IF B0$(1,6)=LAST$ THEN GOTO 1300
1280 FIND (SYM03_DEV,KEY=B0$(1,6))IOL=SYM03A
1290 LET FILENAME$=A0$,DESCRIPTION$=A1$(1,30),LAST$=A0$
1300 REM 
1310 IF FILENAME$="" THEN GOTO 1350
1320 IF L+1>L9 THEN GOSUB 5000
1330 PRINT (7)""
1340 LET L=L+1
1350 IF L+1>L9 THEN GOSUB 5000
1355 PRINT (7)FILENAME$,@(7),DESCRIPTION$,@(42),B0$(7,1),@(48),B1$(1,30)
1360 LET L=L+1
1390 GOTO 1200
1400 REM " --- Done with index. Re-position DDM-03
1410 LET INDEX=0,COMMENTS=0
1420 IF OLDHDR$<>"" THEN LET HEADINGS$[1]=OLDHDR$
1430 IF OLDA0$<>"" THEN LET A0$(1)=OLDA0$,A1$(1)=OLDA1$
1460 LET MILESTONE$(1)=A0$+" "+A1$(1,30)
1470 PRINT @(COLUMN,11),MILESTONE$,
1480 IF OLDA0$<>"" THEN READ (SYM03_DEV,KEY=A0$)IOL=SYM03A
1490 READ (DDM03_DEV,KEY=A0$,DOM=1500)
1500 REM " --- Read next DDM-03 Record ID
1510 LET K$=KEY(DDM03_DEV,END=3900)
1515 IF POS(A0$=K$)<>1 THEN GOTO 3900
1520 READ (DDM03_DEV)IOL=DDM03A
1530 IF ID$<>"" THEN IF B0$(7,1)<>ID$ THEN GOTO 3800
1540 IF REVISION$<>"" THEN IF B1$(31,6)<REVISION$ THEN GOTO 3800
1550 LET L=L9+1
1900 REM " --- Position DDM-04 Field Detail file
1910 LET P=1,STRING=-1,NUMBER=-1,SEQ=0,OPTLINE=0
1920 DIM OPTLINES$[999]
1990 READ (DDM04_DEV,KEY=B0$,DOM=2000)
2000 REM " --- Read next DDM-04 detail record
2010 LET K$=KEY(DDM04_DEV,END=3000)
2020 IF POS(B0$=K$)<>1 THEN GOTO 3000
2030 READ (DDM04_DEV)IOL=DDM04A
2040 LET X0$(1)=C1$
2050 FIND (DDM01_DEV,KEY=X0$,DOM=2900)IOL=DDM01A
2060 IF FNP$(C1$(17,30))="" THEN LET C1$(17,30)=X1$(1,30)
2065 IF FNP$(C1$(55,18))="" THEN LET C1$(55,18)=X1$(38,18)
2070 IF FNP$(C1$(73,1))="" THEN LET C1$(73,1)=X1$(80,1)
2075 IF FNP$(C1$(74,6))="" THEN LET C1$(74,6)=X1$(81,6)
2080 IF FNP$(C1$(80,1))="" THEN LET C1$(80,1)=X1$(87,1)
2085 IF FNP$(C1$(81,6))="" THEN LET C1$(81,6)=X1$(88,6)
2100 REM " --- Format detail line fields
2110 LET MARK$="",REPEAT$="",LENGTH=X[0],MAX=1
2120 LET SEQ$=STR(NUM(C0$(8,3)):"##0")
2130 IF C[1]>0 THEN LET LENGTH=LENGTH*C[1]
2140 IF C[2]>1 THEN LET MAX=C[2]
2150 IF X1$(37,1)="L" THEN LET C1$(1,12)="(Literal)"
2200 REM " --- Print a detail line for each occurance
2210 FOR OCCURS=1 TO MAX
2220 IF L+1>L9 THEN GOSUB 5000
2230 LET MARK$="",FIELD$="",SEPARATOR$=""
2240 IF POS(C1$(54,1)="KX")>0 THEN LET MARK$="K"
2250 IF C[2]>1 THEN LET REPEAT$=STR(OCCURS)
2300 REM " --- Field Separator?
2310 IF POS(C1$(54,1)="NK")>0 THEN GOTO 2400
2320 IF C1$(54,1)="E" THEN IF OCCURS<MAX THEN GOTO 2400
2340 LET SEPARATOR$="*"
2350 IF X1$(37,1)<>"N" THEN LET STRING=STRING+1,FIELD$=STR(STRING)+"$"
2355 IF X1$(37,1)="N" THEN LET NUMBER=NUMBER+1,FIELD$="("+STR(NUMBER)+")"
2400 REM " --- Print Detail Line"
2405 IF DATA_REV$<>"" THEN IF X1$(31,6)<DATA_REV$ THEN GOTO 2490
2410 LET POSITION$="("+STR(P)+","+STR(LENGTH)+")",P=P+LENGTH,SEQ=SEQ+1
2420 IF C1$(37,1)="N" THEN LET POSITION$=""
2430 IF SEPARATOR$<>"" THEN LET P=1
2440 IF LEN(FIELD$)>4 THEN LET FIELD$=FIELD$(1,4)
2460 IF L+1>L9 THEN GOSUB 5000
2470 PRINT (7)SEQ$,@(3),MARK$,@(5),C1$(1,12),@(19),FNP$(C1$(17,30))," ",REPEAT$,@(53),X1$(37,1),@(55),LENGTH:M$,@(62),POSITION$,@(71),SEPARATOR$,@(73),FIELD$
2480 LET L=L+1
2490 LET SEQ$=""
2495 NEXT OCCURS
2500 REM " --- Additional Options?
2510 IF ADDOPTS$="N" THEN GOTO 2900
2520 LET MASK$=FNP$(C1$(58,15))
2530 CALL "DDC.MA",N0$,C1$(55,2),C1$(57,1),MASK$,SYS01_DEV
2540 IF POS(" "<>C1$(55,2)+C1$(57,1)+MASK$+C1$(74,6)+C1$(81,6))=0 AND POS("0"<>C1$(73,1)+C1$(80,1))=0 THEN GOTO 2900; REM "No additional options to report
2550 LET SEQ$=STR(NUM(C0$(8,3)):"##0"),RW$=C1$(74,6),RO$=C1$(81,6)
2560 IF FNP$(RW$)="" THEN LET RW$="(None)" ELSE IF PASSWRD$="N" THEN LET RW$="******"
2570 IF FNP$(RO$)="" THEN LET RO$="(None)" ELSE IF PASSWRD$="N" THEN LET RO$="******"
2575 DIM LINE$(80)
2580 LET LINE$(1)=SEQ$,LINE$(6)=C1$(1,12),LINE$(20)=C1$(55,2)+" "+C1$(57,1)+" "+MASK$,LINE$(43)=C1$(73,1)+" "+RW$,LINE$(54)=C1$(80,1)+" "+RO$
2590 LET OPTLINE=OPTLINE+1,OPTLINES$[OPTLINE]=LINE$
2900 REM " --- Loop back for next DDM-04 Detail record
2990 GOTO 2000
3000 REM " --- Check for File Comments
3010 LET COMMENTS=0,COUNT=0
3020 READ (DDM12_DEV,KEY=A0$,DOM=3050)
3050 REM " --- Read next DDM-12 comments record
3060 LET K$=KEY(DDM12_DEV,END=3200)
3065 IF POS(A0$=K$)<>1 THEN GOTO 3200
3070 READ (DDM12_DEV)IOL=DDM12A
3080 LET COMMENTS=COMMENTS+1,COUNT=COUNT+1
3090 IF L+2>L9 OR (COMMENTS=1 AND L+6>L9) THEN GOSUB 5000 ELSE IF COMMENTS=1 THEN GOSUB 5400
3100 REM " --- Print File Comments
3110 IF L+1>L9 THEN GOSUB 5000
3120 IF COUNT=1 THEN PRINT (7)"File ",A0$,@(12),":",
3130 PRINT (7)@(14),D0$(7,3),@(19),D1$
3180 LET L=L+1
3190 GOTO 3050
3200 REM " --- Check for Record Comments
3210 LET COUNT=0
3220 READ (DDM13_DEV,KEY=B0$,DOM=3250)
3250 REM " --- Read next DDM-13 comments record
3260 LET K$=KEY(DDM13_DEV,END=3400)
3265 IF POS(B0$=K$)<>1 THEN GOTO 3400
3270 READ (DDM13_DEV)IOL=DDM13A
3280 LET COMMENTS=COMMENTS+1,COUNT=COUNT+1
3290 IF L+2>L9 OR (COMMENTS=1 AND L+6>L9) THEN GOSUB 5000 ELSE IF COMMENTS=1 THEN GOSUB 5400
3300 REM " --- Print Record Comments
3310 IF L+1>L9 THEN GOSUB 5000
3320 IF COUNT=1 THEN PRINT (7)"Record ",B0$(7,1),@(12),":",
3330 PRINT (7)@(14),D0$(8,3),@(19),D1$
3380 LET L=L+1
3390 GOTO 3250
3400 REM " --- Check for Data Field Comments
3410 LET COUNT=0,LAYOUTSEQ$=""
3420 READ (DDM14_DEV,KEY=B0$,DOM=3450)
3450 REM " --- Read next DDM-14 comments record
3460 LET K$=KEY(DDM14_DEV,END=3600)
3465 IF POS(B0$=K$)<>1 THEN GOTO 3600
3470 READ (DDM14_DEV)IOL=DDM13A
3475 IF D0$(8,3)<>LAYOUTSEQ$ THEN LET LAYOUTSEQ$=D0$(8,3),COUNT=0
3480 LET COMMENTS=COMMENTS+1,COUNT=COUNT+1
3490 IF L+2>L9 OR (COMMENTS=1 AND L+6>L9) THEN GOSUB 5000 ELSE IF COMMENTS=1 THEN GOSUB 5400
3500 REM " --- Print Data Field Comments
3510 IF L+1>L9 THEN GOSUB 5000
3520 IF COUNT<>1 THEN GOTO 3550
3530 FIND (DDM04_DEV,KEY=D0$(1,10),DOM=3550)IOL=DDM04A
3540 PRINT (7)C1$(1,12),@(12),":",
3550 PRINT (7)@(14),D0$(11,3),@(19),D1$
3580 LET L=L+1
3590 GOTO 3450
3600 REM " --- Print Additional Options
3610 LET COMMENTS=0,PRINTOPTS$="Y"
3620 IF ADDOPTS$="N" THEN GOTO 3800
3630 IF OPTLINE=0 THEN GOTO 3800
3640 IF L+8>L9 THEN GOSUB 5000 ELSE GOSUB 5600
3650 FOR I=1 TO OPTLINE
3660 IF L+1>L9 THEN GOSUB 5000
3670 PRINT (7)OPTLINES$[I]
3680 LET L=L+1
3690 NEXT I
3800 REM " --- Loop back for next DDM-03 Record ID record
3810 LET PRINTOPTS$="N"
3890 GOTO 1500
3900 REM " --- Loop back for next SYM-03 Header record
3990 GOTO 1000
4000 REM " --- All done
4010 IF TOC$<>"Y" THEN GOTO 4900
4020 CLOSE (7,ERR=4030)
4030 CALL "SYC.GA",7,1,"","",STATUS
4040 IF STATUS>0 THEN GOTO 9900
4050 LET PAGE=0,L=L9+1,TOC=1,TOC$="N"
4060 PRINT @(COLUMN,11),"Table Of Contents",
4090 READ (DDW01_DEV,KEY="",DOM=4100)
4100 REM 
4110 READ (DDW01_DEV,END=4900)IOL=DDW01A
4120 LET A0$(1)=T0$,X=LEN(FNP$(A0$))
4130 IF X>2 THEN GOTO 4300
4140 GOSUB 6600
4200 REM " --- Module
4210 DIM BUF$(80,".")
4220 LET BUF$(1,LEN(HEADINGS$[1]))=HEADINGS$[1],BUF$(77)=STR(T0:"###0")
4230 LET HEADINGS$[1]="Record Layout Table Of Contents",L=L+1
4240 IF L+1>L9 THEN GOSUB 5000
4250 PRINT (7)BUF$
4290 GOTO 4100
4300 REM " --- File
4310 DIM A1$(77),BUF$(80,".")
4320 LET A1$(1)="Unknown File",L=L+1,BUF$(1,5)="     "
4330 FIND (SYM03_DEV,KEY=A0$,DOM=4340)IOL=SYM03A
4340 LET X$=A0$+" - "+FNP$(A1$(1,30)),BUF$(6,LEN(X$))=X$,BUF$(77)=STR(T0:"###0")
4350 IF L+1>L9 THEN GOSUB 5000
4360 PRINT (7)BUF$
4390 GOTO 4100
4900 REM " --- Return to menu
4990 GOTO 9900
5000 REM " --- Print Report heading
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS THEN GOTO 9900
5040 IF INDEX>0 THEN GOTO 5500
5050 IF COMMENTS>0 THEN GOTO 5400
5060 IF PRINTOPTS$="Y" THEN GOTO 5600
5070 PRINT (7)""
5075 LET L=L+1
5080 IF TOC>0 THEN GOTO 5390
5090 IF TOC$="Y" THEN GOSUB 6000
5100 REM " --- Print File heading
5105 LET FILETYPE$=A1$(37,1),X=POS(FILETYPE$="CDGIMRST")
5110 IF X>0 THEN LET FILETYPE$=TYPES$[X]
5115 LET KEYSIZE$="",LENGTH$="",RECORD$="",EXPORT$="No",PROTECT$="No",L=L+7
5120 IF A[0]>0 THEN LET KEYSIZE$=STR(A[0])
5125 IF A[1]>0 THEN LET RECORD$=STR(A[1])
5130 IF A[2]>0 THEN LET LENGTH$=STR(A[2])
5135 IF A1$(39,1)="Y" THEN LET EXPORT$="Yes"
5140 IF A1$(37,1)="M" THEN IF A[1]=0 THEN LET RECORD$="Dynamic"
5145 LET RW$=A1$(53,6),RO$=A1$(60,6)
5150 IF FNP$(RW$)="" THEN LET RW$="(None)" ELSE IF PASSWRD$="N" THEN LET RW$="******"
5155 IF FNP$(RO$)="" THEN LET RO$="(None)" ELSE IF PASSWRD$="N" THEN LET RO$="******"
5160 PRINT (7)@(0),"File ",A0$," ",A1$(1,30),@(44),"Revised ",FNB6$(A1$(31,6)),@(62),"Alias ",A1$(40,12)
5165 PRINT (7)DASHES$
5170 PRINT (7)@(0),"Mapping Information: ",A2$(1,30),@(62),"File Type: ",FILETYPE$
5175 PRINT (7)@(13),"Export? ",EXPORT$,@(32),"Write Protect? ",PROTECT$,@(63),"Key Size: ",KEYSIZE$
5180 PRINT (7)@(0),"Read/Write Security: ",A1$(52,1),@(26),"Read/Write Password: ",RW$,@(58),"Record Length: ",LENGTH$
5185 PRINT (7)@(1),"Read Only Security: ",A1$(59,1),@(26)," Read Only Password: ",RO$,@(59),"# of Records: ",RECORD$
5190 PRINT (7)DASHES$
5200 REM " --- Print Record heading
5210 PRINT (7)"Record ID: ",B0$(7,1)," ",B1$(1,30),@(44),"Revised ",FNB6$(B1$(31,6)),@(62),"Alias ",B1$(37,12)
5220 PRINT (7)DASHES$
5230 LET L=L+2
5300 REM " --- Print Field heading
5310 PRINT (7)"Seq  Data Name",@(19),"Description",@(53),"T Length Position FS"
5320 PRINT (7)""
5330 LET L=L+2
5390 RETURN 
5400 REM " --- Print Comments heading
5410 PRINT (7)""
5420 PRINT (7)DASHES$
5430 PRINT (7)"Field",@(14),"Seq",@(19),"Comments"
5440 PRINT (7)""
5450 LET L=L+4
5490 RETURN 
5500 REM " --- Print Index heading
5510 PRINT (7)""
5520 PRINT (7)DASHES$
5530 PRINT (7)"File",@(8),"Description",@(40),"Record",@(48),"Description"
5535 PRINT (7)DASHES$
5550 LET L=L+4
5560 IF TOC$<>"Y" THEN GOTO 5590
5570 LET XA0$=A0$,A0$=MODULE$
5575 GOSUB 6000
5580 LET A0$=XA0$
5590 RETURN 
5600 REM " --- Print Comments heading
5610 PRINT (7)""
5620 PRINT (7)DASHES$
5630 PRINT (7)@(41),"  READ",@(52),"  READ"
5640 PRINT (7)@(19),"-------Mask---------",@(41),"  WRITE",@(52),"  ONLY"
5650 PRINT (7)"Seq  Data Name",@(19),"Sy T Appearance",@(41),"SL Paswrd",@(52),"SL Paswrd"
5660 PRINT (7)""
5670 LET L=L+6
5690 RETURN 
6000 REM " --- Update table of contents
6010 IF LASTFILE$=A0$ THEN GOTO 6090
6020 LET T0$(1)=A0$,T0=PAGE,LASTFILE$=A0$
6030 WRITE (DDW01_DEV,KEY=T0$)IOL=DDW01A
6090 RETURN 
6600 REM " --- System break
6610 DIM S0$(2),S1$(35)
6620 LET MODULE$=A0$(1,2),S0$(1)=MODULE$
6625 IF MODULE$(1,1)="!" THEN LET MODULE$="!"
6630 FIND (SYM04_DEV,KEY=S0$,DOM=6660)IOL=SYM04A
6640 LET HEADINGS$[1]=FNP$(S1$)
6650 GOTO 6690
6660 LET HEADINGS$[1]="Unknown Module "+$22$+MODULE$+$22$
6670 IF MODULE$="!" THEN LET HEADINGS$[1]="Global Variable"
6680 IF MODULE$<>"!" AND MODULE$(1,2)="PF" THEN LET HEADINGS$[1]="Print-To-File"
6690 RETURN 
8000 REM " --- Functions
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8090 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM 
9950 RUN "SYS.AA"
9999 END

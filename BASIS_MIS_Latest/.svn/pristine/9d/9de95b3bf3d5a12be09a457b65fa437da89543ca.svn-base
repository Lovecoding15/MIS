0010 REM "BMP - Parameter Maintenance
0020 REM "Program BMP.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="SYS-01",FILES$[2]="GLM-01"
0160 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1]
0200 REM " --- IOLists
0210 BMS01A: IOLIST A0$(1),A1$,A2$,A3$,A4,A5,A6
0220 GLM01A: IOLIST X0$,X1$(1)
0240 GLS01A: IOLIST G0$,G1$,G2$,G3$,G4$,G5$,G6$,G7$,G8$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="BM",N3$=F5$
0450 FIND (SYS01_DEV,KEY=N0$+"GL"+"00",DOM=9800)IOL=GLS01A
0460 LET G0=NUM(G2$(7,2)),G1=NUM(G2$(9,2))
0470 DIM INFO$[20]
0480 CALL "SYC.VA","BM",INFO$[ALL]
0490 LET GL$=INFO$[9]
0500 REM " --- Init Data
0515 DIM A0$(36),X1$(40)
0520 LET I0=0,K9=6
0530 LET A0$(1)=N0$+N2$+"00"
0540 DIM A1$(10),A2$(10),A3$(10)
0600 REM " --- Additional Opens
0610 IF GL$<>"Y" THEN GOTO 0700
0630 CALL "SYC.DA",1,2,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0640 IF STATUS>0 THEN GOTO 9900
0650 LET GLM01_DEV=CHANNELS[2]
0700 REM " --- Background
0710 PRINT 'SB',@(18,5),"Material Factor Mask:",@(18,6),"Overhead Factor Mask:",@(29,7),"Hour Mask:",@(24,8),"Hour Precision:",
0720 IF GL$="Y" THEN PRINT @(15,9),"Work In Process Account:",@(21,10),"Purchases Account:",@(11,11),"Production Variance Account:",
0730 PRINT 'SF',
0740 PRINT 'CF',
1200 REM " --- Find Key
1220 FIND (SYS01_DEV,KEY=A0$(1,K9),DOM=1400)IOL=BMS01A
1300 REM " --- Display 
1320 GOSUB 5000
1390 GOTO DONE
2300 REM " --- Material Factor Mask
2310 LET I0=0
2320 LET V0$="S",V2$=A2$,V3$="",V0=10,V1=40,V2=5
2330 GOSUB 7000
2335 GOSUB TESTMASK
2350 IF V$="" THEN GOTO 2320
2380 LET A2$=V$
2390 GOTO NEXTFIELD
2400 REM " --- Overhead Factor Mask
2420 LET V0$="S",V2$=A3$,V3$="",V0=10,V1=40,V2=6
2440 GOSUB 7000
2445 GOSUB TESTMASK
2450 IF V$="" THEN GOTO 2420
2460 LET A3$=V$
2490 GOTO NEXTFIELD
2700 REM " --- Precision Mask
2720 LET V0$="S",V2$=A1$,V3$="",V0=10,V1=40,V2=7
2730 GOSUB 7000
2740 GOSUB TESTMASK
2750 IF V$="" THEN GOTO 2700
2760 LET A1$=V$
2790 GOTO NEXTFIELD
2800 REM " --- Precision
2820 LET V0$="N",V2$=STR(A6),V3$="#",V1=40,V2=8
2830 GOSUB 7000
2840 LET A6=V
2890 GOTO NEXTFIELD
2900 REM " --- Work In Process Account
2910 IF GL$<>"Y" THEN GOTO 2990
2920 LET V0$="M",V1$="C",V2$=A0$(7,G0),V3$=G1$,V4$="Enter A Valid Account, <F3>=Lookup",V0=10,V1=40,V2=9
2930 GOSUB 7000
2940 ON V3 GOTO 2950,2950,2990,2950,DONE
2950 IF V3=3 THEN CALL "SYC.LG",V$
2960 IF V$="" THEN GOTO 2900
2970 FIND (GLM01_DEV,KEY=N0$+V$,DOM=2900)IOL=GLM01A
2980 LET A0$(7,G1)=V$
2985 PRINT @(V1,V2),FNF$(A0$(7,G0),G5$)," ",X1$(1,35),
2990 GOTO NEXTFIELD
3000 REM " --- Purchases Account
3010 IF GL$<>"Y" THEN GOTO 3090
3020 LET V0$="M",V1$="C",V2$=A0$(17,G0),V3$=G1$,V4$="Enter A Valid Account, <F3>=Lookup",V0=10,V1=40,V2=10
3030 GOSUB 7000
3040 ON V3 GOTO 3050,3050,3090,3050,DONE
3050 IF V3=3 THEN CALL "SYC.LG",V$
3060 IF V$="" THEN GOTO 3000
3070 FIND (GLM01_DEV,KEY=N0$+V$,DOM=3000)IOL=GLM01A
3080 LET A0$(17,G1)=V$
3085 PRINT @(V1,V2),FNF$(A0$(17,G0),G5$)," ",X1$(1,35),
3090 GOTO NEXTFIELD
3100 REM " --- Production Variance Account
3110 IF GL$<>"Y" THEN GOTO 3190
3120 LET V0$="M",V1$="C",V2$=A0$(27,G0),V3$=G1$,V4$="Enter A Valid Account, <F3>=Lookup",V0=10,V1=40,V2=11
3130 GOSUB 7000
3140 ON V3 GOTO 3150,3150,3190,3150,DONE
3150 IF V3=3 THEN CALL "SYC.LG",V$
3160 IF V$="" THEN GOTO 3100
3170 FIND (GLM01_DEV,KEY=N0$+V$,DOM=3100)IOL=GLM01A
3180 LET A0$(27,G1)=V$
3185 PRINT @(V1,V2),FNF$(A0$(27,G0),G5$)," ",X1$(1,35),
3190 GOTO NEXTFIELD
3900 REM " --- Next Field"
3905 NEXTFIELD: 
3910 IF V3=4 THEN GOTO DONE
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2300,2400,2700,2800,2900,3000,3100,3990
3990 LET I0=0
4000 REM " --- Correct?
4005 DONE: 
4010 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4025 IF V3=4 THEN GOTO 9900
4030 IF V$="DELETE" THEN GOTO 4200
4050 ON POS(V$="YN") GOTO DONE,4100,2000
4100 REM " --- Write
4110 LET A4=1,A5=0
4120 WRITE (SYS01_DEV,KEY=A0$(1,K9))IOL=BMS01A
4190 GOTO 9900
4200 REM " --- Remove"
4220 REMOVE (SYS01_DEV,KEY=A0$(1,K9),DOM=4230)
4290 GOTO 9900
5000 REM " --- Display"
5010 PRINT @(40,5),A2$,@(40,6),A3$,@(40,7),A1$,@(40,8),A6,
5020 IF GL$<>"Y" THEN GOTO 5090
5030 FOR X=0 TO 2
5035 DIM X1$(40); LET X1$(1)="Invalid Account"
5040 FIND (GLM01_DEV,KEY=N0$+A0$(X*10+7,10),DOM=5041)IOL=GLM01A
5050 PRINT @(40,X+9),FNF$(A0$(X*10+7,G0),G5$)," ",X1$(1,35),
5060 NEXT X
5090 RETURN 
6100 REM " --- Test Mask"
6105 TESTMASK: 
6130 LET X$=STR(0:V$,ERR=6170)
6140 GOTO 6180
6170 LET V$=""
6190 RETURN 
6200 REM " --- Mask Length"
6210 LET P0=0,A5$=""
6220 FOR X=1 TO LEN(A1$)
6230 IF POS("#"=A1$(X,1))<>0 THEN LET P0=P0+1,A5$=A5$+"0"
6240 IF POS("#"=A1$(X,1))=0 THEN LET A5$=A5$+A1$(X,1)
6280 NEXT X
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "GLR - G/L Summary Report (Report Overlay)"
0020 REM "Program GLR.IB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$(1),A1$(1)
0220 GLM02A: IOLIST X0$(1),A[ALL],U[ALL]
0400 REM " --- Parameters"
0440 LET TM$="##,###,##0-",MM$="###,##0-",TM=LEN(TM$),MM=LEN(MM$)
0450 LET MAX=P[0]+2,RECORDS=LEN(FNP$(ID$))
0500 REM " --- Initializations"
0510 DIM X0$(3+P[4]),O[MAX],A[MP],U[MP]
0570 LET SUBHEADINGS=1,H1$="Account",NUMBER=0,LINES=RECORDS+2
0590 IF P[0]>12 THEN LET H1$="Acct",LINES=RECORDS*2+2
0600 REM " --- Initialize Print Positions"
0610 LET O[MAX]=H0-TM+1
0615 FOR X=MAX-1 TO 2 STEP -1
0620 LET O[X]=O[X+1]-MM
0625 NEXT X
0630 LET O[1]=O[2]-TM
0650 REM " --- Report Subheadings"
0655 DIM SUBHEADINGS$[SUBHEADINGS],BUFFER$(H0)
0660 LET BUFFER$(1)=H1$,BUFFER$(O[1]+TM-10)="Beginning"
0665 FOR X=2 TO MAX-1
0670 LET BUFFER$(O[X]+MM-4)=P3$((X-2)*3+235,3)
0675 NEXT X
0680 LET BUFFER$(H0-6)="Ending",SUBHEADINGS$[SUBHEADINGS]=BUFFER$
0700 REM " --- Background"
0750 CALL "SYC.NB","Printing",M0,COLUMN
0800 REM " --- Retrieve and store titles"
0805 IF RECORDS=0 THEN GOTO 4000
0810 DIM TITLES$[RECORDS]
0820 FOR X=1 TO RECORDS
0830 LET RECORD$=ID$(X,1),FLAG$=AU$(X,1)
0840 CALL "GLC.FA",GLM08_DEV,N0$,RECORD$,FLAG$,X$,TITLE$,STATUS
0850 IF STATUS=0 THEN LET TITLES$[X]=TITLE$
0890 NEXT X
0900 REM " --- Position File"
0970 LET K$=N0$+BEGACCT$
0980 READ (GLM01_DEV,KEY=K$,DOM=1000)
0990 GOTO 1100
1000 REM " --- Get next GLM-01 key"
1010 LET K$=KEY(GLM01_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 IF ENDACCT$<>"" THEN IF K$(3,P[4])>ENDACCT$ THEN GOTO 4000
1100 REM " --- Read GLM-01 record"
1110 READ (GLM01_DEV,KEY=K$)IOL=GLM01A
1120 GOSUB 6800
1130 IF STATUS>0 THEN GOTO 3900
1140 LET ACCT$=FNF$(A0$(3,P[3]),M0$)+" "+A1$(1,35),NUMBER=NUMBER+1
1150 IF L+LINES>L9 THEN GOSUB 5000 ELSE GOSUB 5200
1190 PRINT @(COLUMN,11),FNF$(X0$(3,P[3]),M0$)
2000 REM " --- Retrieve selected GLM-02 records"
2010 FOR R=1 TO RECORDS
2020 DIM X0$(3+P[4]),A[MP],U[MP],X[MAX]
2030 LET RECORD$=ID$(R,1),FLAG$=AU$(R,1),X0$(1)=A0$,X0$(LEN(X0$))=RECORD$
2040 IF POS(RECORD$="012345ABCDEFGHIJKLMNOPQRSTUVWXYZ")=0 THEN GOTO 2900
2050 FIND (GLM02_DEV,KEY=X0$,DOM=2100)IOL=GLM02A
2200 REM " --- Build detail line array"
2210 LET X[0]=A[0],TITLE$=TITLES$[R]
2220 IF FLAG$="U" THEN LET X[0]=U[0]
2230 LET X[MAX]=X[0]
2240 FOR X=1 TO MP
2250 IF FLAG$="A" THEN LET AMOUNT=A[X]
2260 IF FLAG$="U" THEN LET AMOUNT=U[X]
2270 LET X[X]=AMOUNT,X[MAX]=X[MAX]+AMOUNT
2280 NEXT X
2400 REM " --- Print a record"
2410 IF P[0]<13 THEN GOTO 2500
2420 IF L+2>L9 THEN GOSUB 5000
2440 PRINT (7)TITLE$
2450 LET L=L+1
2500 REM " --- Print detail line"
2510 DIM BUFFER$(H0)
2520 LET BUFFER$(O[1])=STR(X[0]:TM$)
2530 FOR X=2 TO MAX-1
2535 LET BUFFER$(O[X])=STR(X[X-1]:MM$)
2540 NEXT X
2550 LET BUFFER$(O[MAX])=STR(X[MAX]:TM$)
2555 IF P[0]<13 THEN LET BUFFER$(1,O[1]-1)=TITLE$
2560 IF L>L9 THEN GOSUB 5000
2570 PRINT (7)BUFFER$
2580 LET L=L+1
2900 REM " --- End of GLM-02 loop"
2990 NEXT R
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All Done"
4010 IF L>L9 THEN GOSUB 5000
4020 PRINT (7)""
4030 PRINT (7)STR(NUMBER)," Accounts Listed"
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 FOR X=0 TO SUBHEADINGS
5050 PRINT (7)SUBHEADINGS$[X]
5060 LET L=L+1
5070 NEXT X
5080 GOSUB 5200
5090 RETURN 
5200 REM " --- Account Heading"
5210 PRINT (7)""
5220 PRINT (7)ACCT$
5230 PRINT (7)""
5240 LET L=L+3
5290 RETURN 
6800 REM " --- Test account number against specified wildcard mask"
6810 LET STATUS=0
6820 IF WILDCARD$="" THEN GOTO 6890
6825 IF WILDCARD<>LEN(WILDCARD$) THEN LET WILDCARD=LEN(WILDCARD$)
6830 LET X$=FNF$(A0$(3,P[3]),M0$)
6840 FOR X=1 TO WILDCARD
6850 IF WILDCARD$(X,1)="?" THEN GOTO 6880
6860 IF X$(X,1)<>WILDCARD$(X,1) THEN LET STATUS=1
6880 NEXT X
6890 RETURN 
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return To Menu"
9910 PRINT @(0,10),'CE',
9920 RUN MENU$,ERR=9950
9950 RUN "SYS.AA"
9999 END

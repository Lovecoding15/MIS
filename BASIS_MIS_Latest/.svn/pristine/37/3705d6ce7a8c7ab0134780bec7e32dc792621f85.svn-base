0010 REM "SYC - Work Order Lookup"
0020 REM "Program SYC.LW"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "21May96 Caj: Lookup by keyword was working incorrectly (61-69,2550,2560-2580)
0040 REM 
0050 REM "SELECTION$   : User selection (returned)
0060 REM "PARAMETER$   : O=Open, C=Closed, B=Both, ?=Ask
0061 REM 
0064 REM "Note: PREFIX$ = W when by WO Number
0065 REM "              = A when by WO Type
0066 REM "              = C when by Inventory Item
0067 REM "              = N when by Customer Name
0068 REM "              = B when by Customer Number
0069 REM "              = * when by Keywork
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER SELECTION$,PARAMETER$
0100 REM " --- Open Files"
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="WOE-01",FILES$[2]="WOE-03",FILES$[3]="WOM-10"
0140 LET FILES$[4]="IVM-01",FILES$[5]="IVM-10",FILES$[6]="SYS-01"
0150 LET FILES$[7]="SYM-06",FILES$[8]="ARM-01",FILES$[9]="ARM-04"
0170 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0175 IF STATUS THEN GOTO 9900
0180 LET WOE01_DEV=CHANNELS[1],WOE03_DEV=CHANNELS[2],WOM10_DEV=CHANNELS[3]
0185 LET IVM01_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0190 LET SYM06_DEV=CHANNELS[7]
0200 REM " --- IOLIST's"
0210 WOE01A: IOLIST A0$(1),A1$(1),A[ALL]
0220 IVM01A: IOLIST B0$(1),B1$(1)
0230 ARM01A: IOLIST C0$(1),C1$(1)
0250 SYM06A: IOLIST *,COMPANY$
0260 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,X$,X$,X$,X$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,X$,X$,X$
0290 WOS01A: IOLIST X$,W1$,W2$,W3$,W4$,X$,M1$,M2$,M3$
0300 IVM10C: IOLIST W0$(1)
0310 WOM10A: IOLIST X0$(1)
0400 REM " --- Parameters"
0405 DIM I[5],P[1],INFO$[20]
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0425 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),SPEEDSEARCH$="N",DISOPTION$=FNP$(PARAMETER$)
0435 IF POS(DISOPTION$="OCB")=0 THEN LET DISOPTION$=""
0440 IF DISOPTION$<>"" THEN IF DISOPTION$="B" THEN LET DISOPTION$="*"
0445 LET X$=STBL("!SPEEDSEARCH",ERR=0455),N0$=X$(1,2),SPEEDSEARCH$="Y"
0450 FIND (SYM06_DEV,KEY=N0$,DOM=9800)IOL=SYM06A
0455 FIND (SYS01_DEV,KEY=N0$+"WO00",DOM=9800)IOL=WOS01A
0460 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0465 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1)),M0$="######",AR$="N"
0470 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0475 IF W3$(2,1)<>"Y" THEN GOTO 0500
0480 CALL "SYC.VA","AR",INFO$[ALL]
0485 LET AR$=INFO$[20]
0487 IF AR$="N" THEN GOTO 0500
0490 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0495 LET P[0]=NUM(P2$(1,2))
0500 REM " --- Initializations"
0505 PRECISION I[1]
0510 DIM OPTLIST$[2,MAXROW,6],PARAM$[2,MAXROW,3],A0$(75),A1$(140),A[11]
0520 DIM W0$(160),X0$(90),B0$(22),B1$(60),C0$(8),C1$(24),DESCRIPTION$(30)
0540 LET WIDTH=80,HEIGHT=12,WIN_X=0,WIN_Y=3,MAX_ROW=HEIGHT-5,K9=11
0545 LET SWIDTH=80,SHEIGHT=6,SWIN_X=WIN_X,SWIN_Y=WIN_Y+HEIGHT
0550 LET STITLE$="Work Order Detail",OTITLE$="Method",DTITLE$="Display"
0555 LET PGM$=FNP$(PGM(-2)),OPTIONS=0,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3
0560 LET NF$="(Not On File)"
0585 CALL "SYC.RM",M1$,M1,10,10
0590 CALL "SYC.RM",M2$,M2,10,10
0600 REM " --- Additional file opens"
0610 IF AR$="N" THEN GOTO 0650
0620 CALL "SYC.DA",1,8,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS THEN GOTO 9900
0640 LET ARM01_DEV=CHANNELS[8],ARM04_DEV=CHANNELS[9]
0650 REM " --- Window arrays"
0655 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0660 LET HEADING$(2)="WO #",HEADING$(10)="Description",HEADING$(41)="Type"
0665 LET HEADING$(57)="Status",FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0670 CALL "SYC.OL",PGM$,OPTIONS,OWIDTH,OPTLIST$[ALL],PARAM$[ALL],TITLE$,MAXROW,M0$,I[0]
0675 IF OPTIONS<1 THEN GOTO 9900
0680 LET OHEIGHT=OPTIONS+4,OWIDTH=OWIDTH+4
0685 IF DISOPTION$="O" THEN LET TITLE$="Open "+TITLE$
0690 IF DISOPTION$="C" THEN LET TITLE$="Closed "+TITLE$
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Possible display choices"
0810 DIM DISLIST$[2,MAXROW,6]
0820 LET DISLIST$[1,1,1]=" All Work Orders",DISLIST$[1,1,2]="*"
0830 LET DISLIST$[1,2,1]=" Open Work Orders",DISLIST$[1,2,2]="O"
0840 LET DISLIST$[1,3,1]=" Planned Work Orders",DISLIST$[1,3,2]="P"
0850 LET DISLIST$[1,4,1]=" Quoted Work Orders",DISLIST$[1,4,2]="Q"
0860 LET DISLIST$[1,5,1]=" Closed Work Orders",DISLIST$[1,5,2]="C"
0870 LET DWIDTH=23,DHEIGHT=7,DWIN_X=OWIN_X+5,DWIN_Y=OWIN_Y+3
1000 REM " --- Lookup using what search method?"
1010 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
1020 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
1030 GOSUB 5000
1040 IF FKEY=4 OR OPTION$="END" OR OPTION$="PRIOR" OR METHOD=0 THEN GOTO 9900
1050 LET V0$=PARAM$[1,METHOD,1],V1$="KCE",V2$="",V4$="",V1=1
1060 LET V3$=PARAM$[1,METHOD,3],V0=NUM(PARAM$[1,METHOD,2],ERR=1000)
1070 LET X$=V3$,V2=OHEIGHT-2,SMETHOD=METHOD,W1=V0
1080 IF V3$<>"" THEN LET V0=P[0]
1090 IF FNP$(OPTION$)="" THEN LET V0$="Z"
1100 GOSUB 7000
1110 IF V3=4 THEN GOTO 1000
1120 IF V3=5 OR V3=6 THEN GOTO 9900
1130 LET PREFIX$=FNP$(OPTION$),START$=V$,V3$=X$,COLUMN$=FNP$(METHOD$)
1200 REM " --- Determine which work orders to include"
1210 GOSUB 5500
1220 IF DNAME$<>"" THEN CALL "SYC.WD",DNAME$
1230 IF FKEY=4 THEN GOTO 1000
1240 IF OPTION$="END" OR OPTION$="PRIOR" OR METHOD=0 THEN GOTO 9900
1250 LET DISPLAY$=FNP$(OPTION$),METHOD=SMETHOD
1290 CALL "SYC.WD",ONAME$
1400 REM " --- Detail Window"
1410 CALL "SYC.WA",0,SWIDTH,SHEIGHT,SWIN_X,SWIN_Y,STITLE$,SNAME$
1420 CALL "SYC.WB",NAME$,0
1430 IF LEN(COLUMN$)>15 THEN LET COLUMN$=COLUMN$(1,15)
1440 LET HEADING$(64)=COLUMN$
1450 PRINT @(0,0),'CF','SB','BR',HEADING$,'ER','SF',
1490 IF SPEEDSEARCH$<>"Y" THEN GOTO 1600
1500 REM " --- Display SpeedSearch Company"
1510 DIM G$(WIDTH-2)
1520 LET G$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
1530 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
1540 CALL "SYC.WA",0,SWIDTH,3,SWIN_X,SWIN_Y+SHEIGHT,"",CNAME$
1550 PRINT @(0,0),'BR',G$,'ER',
1560 CALL "SYC.WB",NAME$,0
1600 REM " --- Position file"
1610 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,SHEIGHT-2]
1620 LET PAGE=1,L=1,X1=1,X0=0
1630 IF PREFIX$="" THEN LET PREFIX$="W"
1690 ON POS(PREFIX$="WABCN*") GOTO 4000,1700,1800,1800,1800,1900,1700
1700 REM " --- Position WOE-01"
1710 LET A0$(1)=N0$
1720 IF PREFIX$<>"*" THEN LET A0$(5)=START$
1730 IF PREFIX$="*" THEN LET START$=FNU$(START$)
1740 READ (WOE01_DEV,KEY=A0$(1,K9),DOM=2000)IOL=WOE01A
1750 LET K$=A0$(1,K9),X$=K$(5,7)
1790 GOTO 2500
1800 REM " --- Position WOE-03"
1810 LET K$=N0$+PREFIX$+W$+START$,RECORD$=PREFIX$
1820 READ (WOE03_DEV,KEY=K$,DOM=2000)
1890 GOTO 2000
1900 REM " --- Position ARM-04"
1910 IF AR$="N" THEN GOTO 3500
1920 LET K$=N0$+"A"+START$,NUMBER$="",RECORD$="B"
1930 READ (WOE03_DEV,KEY=N0$+RECORD$,DOM=1940)
1940 READ (ARM04_DEV,KEY=K$,DOM=2000)
1990 GOTO 2100
2000 REM " --- Read next record"
2010 ON POS(PREFIX$="WABCN*") GOTO 3500,2300,2200,2200,2200,2200,2300
2100 REM " --- Next ARM-04 key"
2110 LET K$=KEY(ARM04_DEV,END=3500)
2120 IF POS(N0$+"A"=K$)<>1 THEN GOTO 3500
2130 READ (ARM04_DEV)
2140 LET X$=K$(4,10),NUMBER$=K$(14),K$=N0$+"B"+K$(14),RECORD$="B"
2150 READ (WOE03_DEV,KEY=K$,DOM=2200)
2190 GOTO 3500
2200 REM " --- Next WOE-03 key"
2210 LET K$=KEY(WOE03_DEV,END=3500)
2220 IF PREFIX$="N" THEN IF K$(4,6)<>NUMBER$ THEN GOTO 2100
2230 IF POS(N0$+RECORD$+W$=K$)<>1 THEN GOTO 3500
2240 READ (WOE03_DEV)
2250 IF PREFIX$="A" THEN LET X$=K$(4,2),K$=K$(1,2)+K$(6)
2260 IF PREFIX$="N" THEN LET K$=K$(1,2)+K$(20)
2270 IF PREFIX$="B" THEN LET X$=FNF$(K$(4,6),M0$),K$=K$(1,2)+K$(20)
2280 IF PREFIX$="C" THEN LET X$=K$(6,I[0]),K$=K$(1,2)+K$(26)
2290 GOTO 2500
2300 REM " --- Next WOE-01 key"
2310 LET K$=KEY(WOE01_DEV,END=3500)
2320 IF POS(N0$=K$)<>1 THEN GOTO 3500
2330 LET X$=K$(5,7)
2390 IF PREFIX$="*" THEN LET X$=START$
2500 REM " --- Read work order master record"
2510 LET LOCKED=0
2520 READ (WOE01_DEV,KEY=K$,ERR=2600)IOL=WOE01A
2530 IF DISPLAY$<>"*" THEN IF A0$(15,1)<>DISPLAY$ THEN GOTO 3400
2540 IF PREFIX$<>"*" THEN GOTO 2700
2550 IF START$="" THEN GOTO 2700
2560 FIND (IVM01_DEV,KEY=N0$+A0$(56),DOM=2600)IOL=IVM01A
2570 IF POS(START$=FNU$(A0$)) THEN GOTO 2700
2580 IF POS(START$=FNU$(B1$)) THEN GOTO 2700
2590 GOTO 3400
2600 REM " --- Error reading inventory master record"
2610 IF ERR=11 THEN GOTO 3400
2620 IF ERR<>0 THEN GOTO 9000
2630 LET LOCKED=1
2700 REM " --- Display page?"
2710 IF L<MAX_ROW THEN GOTO 2800
2720 LET XMODE=5
2730 GOSUB 6200
2740 IF FKEY=1 THEN GOTO 1000
2750 IF FKEY=4 THEN GOTO 9900
2760 IF SELECTION$<>"" THEN GOTO 4000
2800 REM " --- Lookup window record"
2810 DIM G$(WIDTH-2)
2815 IF LOCKED=0 THEN GOTO 2900
2820 DIM A0$(75),A[5]
2830 LET G1$(2)=K$(5,7)+" Record In Use",STATUS$="Unknown",A0$(1)=K$
2890 GOTO 2990
2900 REM " --- Locked record"
2910 LET STATUS$="Open",CUSTOMER$="",ITEM$="",WAREHOUSE$="",ORDER$=""
2915 LET OPENED$="",ACTIVITY$="",COMPLETION$="",CLOSED$=""
2920 IF A0$(15,1)="C" THEN LET STATUS$="Closed"
2925 IF A0$(15,1)="P" THEN LET STATUS$="Planned"
2930 IF A1$(15,1)="Q" THEN LET STATUS$="Quote"
2935 IF FNP$(A0$(16,3))<>"" THEN LET OPENED$=FNB$(A0$(16,3))
2940 IF FNP$(A0$(28,3))<>"" THEN LET ACTIVITY$=FNB$(A0$(28,3))
2945 IF FNP$(A0$(22,3))<>"" THEN LET COMPLETION$=FNB$(A0$(22,3))
2950 IF FNP$(A0$(31,3))<>"" THEN LET CLOSED$=FNB$(A0$(31,3))
2960 LET K$=K$(3),X0$(1)=N0$+"A"+A0$(12,2)+NF$,DESCRIPTION$(1)=A1$(1,30)
2970 FIND (WOM10_DEV,KEY=X0$(1,5),DOM=2980)IOL=WOM10A
2980 LET G$(2)=A0$(5,7),G$(10)=DESCRIPTION$,G$(41)=X0$(4,2)+" "+X0$(6,12)
2990 LET G$(57)=STATUS$,G$(65)=X$,LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=A0$(3)
3000 REM " --- Retrieve inventory item and warehouse"
3010 LET LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=A0$(5,7)
3020 IF A0$(14,1)<>"I" THEN GOTO 3100
3030 LET B0$(1)=N0$+A0$(56),B1$(1)=NF$,W0$(1)=N0$+"C"+A0$(54,2)+NF$
3040 FIND (IVM01_DEV,KEY=B0$,DOM=3050)IOL=IVM01A
3050 FIND (IVM10_DEV,KEY=N0$+"C"+A0$(54,2),DOM=3060)IOL=IVM10C
3060 LET V$=FNITEM$(B1$,I[3],I[4],I[5]),ITEM$="{Item}"+B0$(3,I[0])+" "+V$
3070 LET WHSE$="{Whse}"+W0$(4,2)+" "+W0$(6,20),DESCRIPTION$(1)=V$
3090 LET G$(10,30)=DESCRIPTION$,LLIST$[PAGE,X1,1]=G$
3100 REM " --- Retrieve customer name"
3110 IF AR$="N" THEN GOTO 3200
3120 IF FNP$(A0$(34,6))="" THEN GOTO 3200
3130 LET C0$(1)=N0$+A0$(34,6),C1$(1)="(Not On File)"
3140 FIND (ARM01_DEV,KEY=C0$,DOM=3150)IOL=ARM01A
3150 LET CUSTOMER$="{Cust}"+FNF$(C0$(3,P[0]),M0$)+" "+C1$(1,24)
3160 IF FNP$(A0$(40,7))="" THEN GOTO 3200
3170 LET ORDER$="{Ord#}"+A0$(40,7)
3180 IF FNP$(A0$(47,3))<>"" THEN LET ORDER$=ORDER$+"-"+A0$(47,3)
3200 REM " --- Detail window record"
3210 LET G$(1)="  {Opened}"+OPENED$,G$(21)=ITEM$,MORE$[PAGE,X1,1]=G$
3220 LET G$(1)="{Last Act}"+ACTIVITY$,G$(21)=WHSE$
3230 LET G$(53)="{Production Qty}"+STR(A[0]:M2$),MORE$[PAGE,X1,2]=G$
3240 LET G$(1)="{Est Comp}"+COMPLETION$,G$(21)=CUSTOMER$
3245 IF CLOSED$<>"" THEN LET G$(57)="{Qty Closed}"+STR(A[2]:M2$)
3250 LET MORE$[PAGE,X1,3]=G$,G$(1)=""
3255 IF CLOSED$<>"" THEN LET G$(1)="  {Closed}"+CLOSED$
3260 LET G$(21)=ORDER$
3265 IF CLOSED$<>"" THEN LET G$(55)="{Closed Value}"+STR(A[3]:M1$)
3270 LET MORE$[PAGE,X1,4]=G$
3300 REM " --- Store detail window"
3310 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),LLIST$[PAGE,X1,5]=SNAME$
3320 LET X0=X0+1,X1=X1+1,L=L+1
3400 REM " --- Loop back for next record"
3410 IF LOCKED=0 THEN GOTO 2000
3420 LET LOCKED=0
3430 IF PREFIX$<>"W" AND PREFIX$<>"*" AND PREFIX$<>"" THEN GOTO 2000
3440 LET K$=K$(1,LEN(K$)-1)+CHR(ASC(K$(LEN(K$),1))+1)
3450 READ (WOE01_DEV,KEY=K$,DOM=1400,END=3500)IOL=WOE01A
3490 GOTO 2300
3500 REM " --- End of file"
3510 IF X0>0 THEN GOTO 3900
3520 DIM MESSAGE$[1]
3530 LET MESSAGE$[0]="No Work Orders Found (<Enter>=Continue)"
3540 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3550 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3560 ON V3 GOTO 1000,1000,1000,1000,4000,4000,4000
3590 GOTO 1000
3900 REM " --- Display last page"
3910 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3920 GOSUB 6200
3930 IF FKEY=1 THEN GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
5000 REM " --- Search Method"
5010 DIM MORE$[2,MAXROW,1]
5020 CALL "SYC.WA",0,OWIDTH,OHEIGHT,OWIN_X,OWIN_Y,OTITLE$,ONAME$
5030 CALL "SYC.WC",1,0,OWIDTH-2,0,0,OHEIGHT-4,1
5040 LET OMAX_ROW=OHEIGHT-2,PAGE=1,XMODE=0
5050 CALL "SYC.SA",XMODE,OPTLIST$[ALL],MORE$[ALL],OPTION$,PAGE,OMAX_ROW,ONAME$,OHEIGHT,OWIDTH,FKEY
5100 REM " --- Which method?"
5110 IF FKEY=4 OR OPTION$="END" OR OPTION$="PRIOR" THEN GOTO 5190
5120 LET METHOD$="",METHOD=0
5130 FOR X=1 TO OPTIONS
5140 IF OPTION$=OPTLIST$[1,X,2] THEN LET METHOD$=FNP$(OPTLIST$[1,X,1]),METHOD=X
5150 NEXT X
5190 RETURN 
5500 REM " --- Select which work orders to display. If search method"
5505 REM " --- is by item, user must select which warehouse to search"
5510 LET W$=""
5515 IF PREFIX$<>"C" THEN GOTO 5700
5520 READ (WOE03_DEV,KEY=N0$+"C",DOM=5530)
5530 LET K$=KEY(WOE03_DEV,END=5580)
5540 IF POS(N0$+"C"=K$)<>1 THEN GOTO 5580
5545 LET W$=K$(4,2)
5550 READ (WOE03_DEV,KEY=K$(1,5)+$FF$,DOM=5560)
5560 LET K$=KEY(WOE03_DEV,END=5580)
5570 IF POS(N0$+"C"=K$)=1 THEN GOTO 5600
5580 IF W$="" THEN LET FKEY=4
5590 GOTO 5700
5600 REM " --- Warehouse Lookup"
5610 LET V1=DWIN_X-1,V2=DWIN_Y+3
5620 CALL "IVC.LK",3,V1,V2,V$
5630 LET W$=V$
5640 IF W$<>"" THEN GOTO 5700
5650 LET FKEY=4
5690 GOTO 5800
5700 REM " --- Display Selection"
5710 DIM MORE$[2,MAXROW,1]
5720 LET OPTION$="",DISOPTS=5,FKEY=0,DMAX_ROW=DHEIGHT-1,PAGE=1,XMODE=0
5730 FOR X=1 TO DISOPTS
5740 IF DISOPTION$=DISLIST$[1,X,2] THEN LET OPTION$=DISLIST$[1,X,2]
5750 NEXT X
5760 IF OPTION$<>"" THEN GOTO 5800
5770 CALL "SYC.WA",0,DWIDTH,DHEIGHT,DWIN_X,DWIN_Y,DTITLE$,DNAME$
5780 CALL "SYC.SA",XMODE,DISLIST$[ALL],MORE$[ALL],OPTION$,PAGE,DMAX_ROW,DNAME$,DHEIGHT,DWIDTH,FKEY
5800 REM " --- Which selection?"
5810 IF FKEY=4 OR OPTION$="END" OR OPTION$="PRIOR" THEN GOTO 5190
5820 LET METHOD$="",METHOD=0
5830 FOR X=1 TO DISOPTS
5840 IF OPTION$=DISLIST$[1,X,2] THEN LET METHOD$=FNP$(DISLIST$[1,X,1]),METHOD=X
5850 NEXT X
5890 RETURN 
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 FOR X=1 TO FILES
9915 CLOSE (CHANNELS[X],ERR=9916)
9920 NEXT X
9925 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9930 IF DNAME$<>"" THEN CALL "SYC.WD",DNAME$
9935 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
9937 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
9940 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9945 IF SELECTION$="END" THEN LET SELECTION$=""
9950 EXIT 
9999 END

0010 REM "TMP - TeleMarketing Parameter Maintenance
0020 REM "Program TMP.AA
0030 REM "ADD+ON SOFTWARE VERSION 5.0.2 - 07/24/91"
0035 REM "11/27/91 by Jerry Koch
0040 REM "COPYRIGHT (C) 1991 MicroAccounting Systems, Inc. ALL RIGHTS RESERVED"
0060 BEGIN 
0090 SETERR 9000
0100 REM "OPEN/LOCK FILES"
0110 OPEN (1)"SYS-01"
0160 OPEN (6)"SYS-01"
0200 REM "IOLISTS"
0210 IOLIST A0$(1),A1$(1),A2$(1)
0270 IOLIST X$,F0$,X$,X$,F4$,F5$
0400 REM "PARAMETERS"
0410 FIND (6,KEY="T"+FID(0),DOM=9800)IOL=0270
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="TM",N3$=F5$
0500 REM "INIT DATA"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," "),J3$(10,"1")
0520 LET K0=0,I0=0,K9=6
0530 REM LET P9=24, P8=6
0531 LET P9=21,P8=12
0540 DIM A1$(P9*(P8+1)),A0$(36),A2$(60)
0550 LET L0=14,L9=20
0560 LET A0$(1)=N0$+N2$+"00"
0700 REM "BACKGROUND"
0710 PRINT 'SB',@(4,3),"Next Call Days:",@(44),"Labels Up:",@(3,4),"Swap Name/Firm?:",@(41),"Label Length:",@(3,5),"Search Method 0:",@(42),"Label Width:",@(3,6),"Search Method 1:",@(41),"Post To A/R?:",@(3,7),"Search Method 2:",@(44),"From A/R?:",@(3,8),"Search Method 3:",@(41),"Create Cust?:",@(3,9),"Search Method 4:",
0720 PRINT @(23,12),"Masterfile User Definable Fields",@(3,13),"Type",@(8),"Description",@(28),"Type",@(33),"Description",
0730 LET X0=0,Y0=L0
0740 FOR X=1 TO P9
0750 IF Y0>L9 THEN LET X0=X0+25,Y0=L0
0760 PRINT @(X0,Y0),X:"##.",
0770 LET Y0=Y0+1
0780 NEXT X
0790 PRINT 'SF','CF',
0800 REM "OPTIONS"
0900 REM "INIT FILE"
0920 FIND (1,KEY=N0$+N2$+"00",DOM=1500)IOL=0210
0930 LET NEW$="N"
1000 REM "KEY"
1010 PRINT 'CF',
1300 REM "DISPLAY"
1320 GOSUB 5000
1340 GOTO 4000
1400 REM "NEW RECORD"
1420 LET V0$="Y",V1$="C",V2$="",V3$="",V4$="Is This A New Record? <Y/N>: ",V0=1,V1=55,V2=22
1430 GOSUB 7000
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM "SET-UP"
1510 LET A1$(1)=""
1520 LET NEW$="Y"
1530 DIM A2$(60)
1590 LET A0$(16,1)="N"
1600 REM "--- Default Days
1620 LET V0$="N",V1$="C",V2$=A0$(7,3),V3$="##0",V4$="Enter The Number Of Days For Auto-Callback Date Setting",V0=3,V1=20,V2=3
1640 GOSUB 7000
1680 LET A0$(7,3)=V$
1690 GOTO 3900
1700 REM "--- Search Method 2
1720 LET V0$="S",V1$="C",V2$=A2$(1,20),V3$="",V4$="Enter The Description For Search Method 2",V0=20,V1=20,V2=7
1740 GOSUB 7000
1780 LET A2$(1,20)=V$
1790 GOTO 3900
1800 REM "--- Search Method 3
1820 LET V0$="S",V1$="C",V2$=A2$(21,20),V3$="",V4$="Enter The Description For Search Method 3",V0=20,V1=20,V2=8
1840 GOSUB 7000
1880 LET A2$(21,20)=V$
1890 GOTO 3900
1900 REM "--- Search Method 4
1920 LET V0$="S",V1$="C",V2$=A2$(41,20),V3$="",V4$="Enter The Description For Search Method 4",V0=20,V1=20,V2=9
1940 GOSUB 7000
1980 LET A2$(41,20)=V$
1990 GOTO 3900
2000 REM "FIELDS"
2010 LET X0=0,Y0=L0
2020 FOR N=1 TO P9
2030 IF Y0>L9 THEN LET X0=X0+25,Y0=L0
2040 LET X0$=A1$((N-1)*(P8+1)+1,P8+1)
2060 LET V0$="S",V1$="KECR",V2$=X0$(1,1),V3$=" DSYNdsyn",V4$="S=Comment  D=Date  Y=Yes/No  N=Number",V0=1,V1=X0+4,V2=Y0
2080 GOSUB 7000
2083 LET V$=CVS(V$,4)
2085 IF V3=4 THEN PRINT @(V1,V2),X0$(1,1),
2090 IF V3=4 OR V$="END" THEN EXITTO 3900
2100 IF V3<>2 THEN GOTO 2200
2120 IF N>1 THEN LET N=N-1,Y0=Y0-1 ELSE EXITTO 3900
2130 IF Y0<L0 THEN LET X0=X0-25,Y0=L9
2140 GOTO 2030
2200 REM "--- Don't allow Change/Delete on existing Parameters
2210 IF NEW$="Y" THEN GOTO 2290
2220 IF X0$(1,1)=" " OR X0$(1,1)=V$ THEN GOTO 2290
2230 LET OLDV$=V$
2240 LET V0$="S",V1$="CS",V2$="",V3$="",V4$="Changing Field Types May Destroy Data. Not Allowed. <Cr> ",V0=1,V1=41+INT(LEN(V4$)/2),V2=22
2250 GOSUB 7000
2260 IF V$="SPECIAL" THEN LET V$=OLDV$ ELSE LET V$=X0$(1,1)
2280 PRINT @(X0+4,Y0),V$,
2290 LET X0$(1,1)=V$
2400 REM "TEXT"
2410 LET LEN$="20"
2420 LET V0$="S",V2$=X0$(2,P8),V4$=STR(P8)+" Character Description",V0=P8,V1=X0+8,V2=Y0
2430 IF X0$(1,1)="S" THEN LET V4$=V4$+" (For "+LEN$+" Field)"
2440 GOSUB 7000
2460 IF V3=2 THEN GOTO 2060
2480 LET X0$(2,P8)=V$
2500 LET A1$((N-1)*(P8+1)+1,P8+1)=X0$
2520 LET Y0=Y0+1
2590 NEXT N
2595 GOTO 3900
2700 REM "--- Labels Up
2720 LET V0$="Z",V1$="C",V2$=A0$(10,2),V3$="",V4$="How Many Labels Across To Print?",V0=2,V1=55,V2=3
2740 GOSUB 7000
2780 LET A0$(10,2)=V$
2790 GOTO 3900
2800 REM "--- Label Length
2820 LET V0$="Z",V1$="C",V2$=A0$(12,2),V3$="",V4$="How Many Lines Long Are Your Labels?",V0=2,V1=55,V2=4
2840 GOSUB 7000
2880 LET A0$(12,2)=V$
2890 GOTO 3900
2900 REM "--- Label Width
2910 LET V4$="How Many Characters Wide Are Your Labels?"
2920 LET V0$="Z",V1$="C",V2$=A0$(14,2),V3$="",V0=2,V1=55,V2=5
2940 GOSUB 7000
2960 IF NUM(A0$(10,2))*NUM(V$)>130 THEN LET V4$="'labels Up' Times 'label Width' May Not Exceed 130 Characters."; GOTO 2920
2980 LET A0$(14,2)=V$
2990 GOTO 3900
3000 REM "--- Swap Name/Firm?
3010 IF A0$(16,1)="Y" THEN PRINT @(20,5),"Firm        ",@(20,6),"Contact Name", ELSE PRINT @(20,5),"Contact Name",@(20,6),"Firm        ",
3020 LET V0$="Y",V1$="CR",V2$=A0$(16,1),V3$="",V4$="Make Firm Primary And Contact Name Secondary?",V0=1,V1=20,V2=4
3040 GOSUB 7000
3070 LET A0$(16,1)=V$
3080 IF A0$(16,1)="Y" THEN PRINT @(20,5),"Firm        ",@(20,6),"Contact Name", ELSE PRINT @(20,5),"Contact Name",@(20,6),"Firm        ",
3090 GOTO 3900
3100 REM "--- Post to A/R?
3120 LET V0$="Y",V1$="CR",V2$=A0$(17,1),V3$="",V4$="Automatically Post Address/Phone Changes To Customer Masterfile?",V0=1,V1=55,V2=6
3140 GOSUB 7000
3180 LET A0$(17,1)=V$
3190 GOTO 3900
3200 REM "--- Post from A/R?
3220 LET V0$="Y",V1$="CR",V2$=A0$(18,1),V3$="",V4$="Automatically Post Address/Phone Changes From Customer Masterfile?",V0=1,V1=55,V2=7
3240 GOSUB 7000
3280 LET A0$(18,1)=V$
3290 GOTO 3900
3300 REM "--- Create Customers?
3320 LET V0$="Y",V1$="CR",V2$=A0$(19,1),V3$="",V4$="Allow Customers To Be Created From Contact Masterfile?",V0=1,V1=55,V2=8
3340 GOSUB 7000
3380 LET A0$(19,1)=V$
3390 GOTO 3900
3900 REM "NEXT FIELD"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 IF V3=4 THEN GOTO 4000
3960 ON I0 GOTO 1600,3000,1700,1800,1900,2700,2800,2900,3100,3200,3300,2000,4000
4000 REM "CORRECT?"
4020 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct? <Y/N/Delete>: ",V0=6,V1=64,V2=22,I0=0
4025 GOSUB 7000
4030 LET V$=CVS(V$,4)
4040 IF V$="DELETE" THEN GOTO 4200
4050 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM "WRITE"
4120 WRITE (1,KEY=A0$(1,K9))IOL=0210
4140 IF O0=2 THEN GOTO 5200
4190 GOTO 9900
4200 REM "REMOVE"
4220 REMOVE (1,KEY=A0$(1,K9),DOM=4230)
4290 GOTO 9900
4400 REM "PRINT"
4420 RUN "SYR.AA"
4500 REM "HARD COPY"
4520 LET V0$="Y",V1$="C",V2$="N",V3$="",V4$="Do You Wish To Print A List Of All Records? <Y/'cr'>: ",V0=1,V1=67,V2=22
4525 GOSUB 7000
4530 IF V$="N" THEN GOTO 9900
4540 IF V$<>"Y" THEN GOTO 4500
4590 RUN "JCM.AB"
5000 REM "DISPLAY"
5020 PRINT @(20,3),NUM(A0$(7,3)):"##0",@(55),A0$(10,2),@(20,4),A0$(16,1),@(55),A0$(12,2),
5030 IF A0$(16,1)="Y" THEN PRINT @(20,5),"Firm        ",@(20,6),"Contact Name", ELSE PRINT @(20,5),"Contact Name",@(20,6),"Firm        ",
5040 PRINT @(55,5),A0$(14,2),@(55,6),A0$(17,1),@(20,7),A2$(1,20),@(55),A0$(18,1),@(20,8),A2$(21,20),@(55),A0$(19,1),@(20,9),A2$(41,20),
5110 LET X0=0,Y0=L0
5120 FOR X=1 TO P9
5130 IF Y0>L9 THEN LET X0=X0+25,Y0=L0
5140 LET X0$=A1$((X-1)*(P8+1)+1,P8+1)
5150 PRINT @(X0+4,Y0),X0$(1,1),@(X0+8,Y0),X0$(2,P8),
5160 LET Y0=Y0+1
5180 NEXT X
5185 REM PRINT @(15,L9+3),A2$
5190 RETURN 
6000 REM "CHECK EQUATION"
6005 SETERR 6400
6010 LET Q0=0,T0=0,T1=0,Q$="",Q0$="",Q1$="",V=LEN(V$),V0$=V$
6015 IF V=0 THEN RETURN 
6020 FOR X=1 TO V
6030 IF V$(X,1)="(" THEN LET T0=T0+1
6040 IF V$(X,1)=")" THEN LET T0=T0-1
6050 IF V$(X,1)="[" THEN LET T1=T1+1,Q0$=Q0$+STR(X:"00")
6060 IF V$(X,1)="]" THEN LET T1=T1-1,Q1$=Q1$+STR(X:"00")
6065 IF V$(X,1)=" " THEN EXITTO 6400
6110 LET Q=POS(V$(X,1)="+-*/^()[]")
6120 IF Q<>0 THEN LET Q$=Q$+STR(X:"00")
6140 NEXT X
6160 IF T0<>0 OR T1<>0 THEN GOTO 6400
6170 IF LEN(Q0$)<>LEN(Q1$) THEN GOTO 6400
6180 IF LEN(Q0$)=0 THEN GOTO 6300
6200 FOR X=1 TO LEN(Q0$) STEP 2
6210 LET Q2$=V$(NUM(Q0$(X,2))+1,NUM(Q1$(X,2))-NUM(Q0$(X,2))-1)
6220 IF NUM(Q2$)>P9 OR NUM(Q2$)<1 THEN EXITTO 6400
6230 IF A1$((NUM(Q2$)-1)*(P8+1)+1,1)<>"N" THEN EXITTO 6400
6240 LET V$(NUM(Q0$(X,2)),NUM(Q1$(X,2))-NUM(Q0$(X,2))+1)=J3$(1,NUM(Q1$(X,2))-NUM(Q0$(X,2))+1)
6290 NEXT X
6320 LET V$="LET Q="+V$
6340 EXECUTE V$
6360 LET V$=V0$
6380 SETERR 9000
6390 RETURN 
6400 REM "INVALID EQUATION"
6420 LET V0$="S",V1$="C",V2$="",V3$="",V4$="I'm Sorry But This Is An Invalid Equation <Cr>: ",V0=1,V1=64,V2=22
6430 GOSUB 7000
6470 LET Q0=1,V$=V0$
6480 SETERR 9000
6490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"00")
8020 DEF FNB$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+Q1$(1,2)
8030 DEF FNC$(Q1$)=Q1$(3,2)+Q1$(5,2)+Q1$(1,2)
8040 DEF FND$(Q$)=Q$(5,2)+Q$(1,2)+Q$(3,2)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM " Program BCC.01 - Credit Card info lookup (e-commerce)
0020 BEGIN
0030 SETERR 9000
0040 SETESC 9000
0050 LET SYS01T=UNT; OPEN (SYS01T)"SYS-01"
0060 LET TERMINAL$="T"+FID(0)
0070 READ (SYS01T,KEY=TERMINAL$)*,F0$; CLOSE (SYS01T)
0080 LET FIRM$=F0$(16,2)
0090 call "ec_open::ARE63"
0100 call "ec_open::ART63"
0130 LET ARE64=UNT; OPEN (ARE64)"ARE-64"
0140 LET ART64=UNT; OPEN (ART64)"ART-64"
0150 LET ARE03=UNT; OPEN (ARE03)"ARE-03"
0160 DIM SLIST$[2,7,5],SMORE$[2,7,2]
0165 LET LOG$="Start:",ADATA$=STBL("AON")+"ADATA/"; GOSUB LOGENTRY
0170 LET METHOD$="Customer/Order/Invoice/CreditCard#/Date/Amount/",X1=1
0180 METHOD: LET P=POS("/"=METHOD$); IF P=0 THEN GOTO SELECT_SEARCH
0190 LET SLIST$[1,X1,1]=" "+METHOD$(1,P-1),SLIST$[1,X1,2]=STR(X1),X1=X1+1,METHOD$=METHOD$(P+1)
0200 GOTO METHOD
0210 SELECT_SEARCH: REM PRINT 'CS'
0220 LET CUST$="",ORDER$="",INVOICE$="",TDATE$="",CCNUM$="",TAMT$=""
0230 LET WIDTH=20,HEIGHT=10,WIN_X=7,WIN_Y=3,TITLE$="Search by"
0240 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,SNAME$
0250 CALL "SYC.SA",0,SLIST$[ALL],SMORE$[ALL],SELECTION$,1,7,SNAME$,HEIGHT,WIDTH,FKEY
0260 IF FKEY=4 THEN GOTO END_PROGRAM
0270 LET X=NUM(SELECTION$,ERR=END_PROGRAM)
0280 ON X GOTO END_PROGRAM,CUSTOMER,ORDER,INVOICE,CCNUMBER,DATE,AMOUNT,END_PROGRAM
0290 CUSTOMER: LET M0$="000000"
0300 LET V0$="M",V1$="CE",V2$="",V3$=M0$,V4$="",V0=6,V1=2,V2=7
0310 GOSUB 7000; IF V3=4 THEN GOTO END_PROGRAM
0320 LET CUST$=V$,TITLE$="Credit Card Transactions - Cust# "+V$
0330 GOTO SEARCH
0340 ORDER: LET M0$="0000000"
0350 LET V0$="M",V1$="CE",V2$="",V3$=M0$,V4$="",V0=7,V1=2,V2=7
0360 GOSUB 7000; IF V3=4 THEN GOTO END_PROGRAM
0370 LET ORDER$=V$,TITLE$="Credit Card Transactions - Order# "+V$
0380 GOTO SEARCH
0390 INVOICE: LET M0$="0000000"
0400 LET V0$="M",V1$="CE",V2$="",V3$=M0$,V4$="",V0=7,V1=2,V2=7
0410 GOSUB 7000; IF V3=4 THEN GOTO END_PROGRAM
0420 LET INVOICE$=V$,TITLE$="Credit Card Transactions - Invoice# "+V$
0430 GOTO SEARCH
0440 CCNUMBER: 
0450 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="",V0=16,V1=2,V2=7
0460 GOSUB 7000; IF V3=4 THEN GOTO END_PROGRAM
0470 LET CCNUM$=V$,TITLE$="Credit Card Transactions - Card# "+V$
0480 GOTO SEARCH
0490 DATE: LET M0$="00000000"
0500 LET V0$="M",V1$="CE",V2$="mmddyyyy",V3$=M0$,V4$="",V0=8,V1=2,V2=7
0510 GOSUB 7000; IF V3=4 THEN GOTO END_PROGRAM
0520 LET M=NUM(V$(1,2),ERR=DATE),D=NUM(V$(3,2),ERR=DATE),Y=NUM(V$(5,4),ERR=DATE)
0530 IF M<1 OR M>12 OR D<1 OR D>31 OR Y<2000 THEN GOTO DATE
0540 LET TDATE$=V$(5,4)+V$(1,4),V$=V$(1,2)+"/"+V$(3,2)+"/"+V$(5,4)
0550 LET TITLE$="Credit Card Transactions - "+V$
0560 GOTO SEARCH
0570 AMOUNT: 
0580 LET V0$="N",V1$="CE",V2$="",V3$="",V4$="",V0=10,V1=2,V2=7
0590 GOSUB 7000; IF V3=4 THEN GOTO END_PROGRAM
0600 LET A=NUM(V$,ERR=AMOUNT),TAMT$=V$
0610 LET TITLE$="Credit Card Transactions - Amount "+CVS(STR(A:"###,###.00"),3)
0620 GOTO SEARCH
0630 SEARCH: 
0640 CALL "SYC.WD",SNAME$
0650 LET CCLIST$="",CC_COUNT=0,CC_CHAN=ARE63
0660 GOSUB CREATE_CCLIST
0670 LET CC_CHAN=ART63
0680 GOSUB CREATE_CCLIST
0690 GOSUB DISPLAY_LIST
0700 GOTO SELECT_SEARCH
0710 CREATE_CCLIST: 
0720 READ (CC_CHAN,KEY=FIRM$+CUST$,DOM=MAIN_READ)
0730 MAIN_READ: LET K$=KEY(CC_CHAN,END=CCLIST_END)
0740 READ RECORD (CC_CHAN,KEY=K$)ARE63$
0750 IF CUST$<>"" THEN IF CUST$<>"000000" AND POS(FIRM$+CUST$=K$)<>1 THEN GOTO CCLIST_END ELSE GOTO ADD_TO_CCLIST
0760 IF CC_CHAN<>ARE63 THEN LET ART63$=ARE63$; GOTO METHOD_CHECK
0770 LET ARE63.AVAILABLE$=FILL(50),ART63$=ARE63$,ART63.ORDER_NUMBER$=ART63.AR_INV_NBR$
0780 READ (ARE03,KEY=K$(1,2)+"  "+K$(3)+"000",DOM=METHOD_CHECK)A0$
0790 LET ART63.AR_INV_NBR$=A0$(42,7)
0800 METHOD_CHECK: 
0810 IF CCNUM$<>"" THEN LET CCE$=""; CALL "BCC.11",CCNUM$,CCE$; LET CCNUM$=CCE$
0820 IF ORDER$<>"" AND ART63.ORDER_NUMBER$<>ORDER$ THEN GOTO MAIN_READ
0830 IF INVOICE$<>"" AND ART63.AR_INV_NBR$<>INVOICE$ THEN GOTO MAIN_READ
0840 IF CCNUM$<>"" AND POS(CCNUM$=ART63.CC_NUMBER$)<>1 THEN GOTO MAIN_READ
0850 IF TDATE$<>"" AND POS(TDATE$=ART63.TRANS_DATE$)<>1 THEN GOTO MAIN_READ
0860 IF TAMT$<>"" AND ART63.TRANS_AMOUNT<>NUM(TAMT$) THEN GOTO MAIN_READ
0870 ADD_TO_CCLIST: 
0880 LET CCLIST$=CCLIST$+STR(CC_CHAN:"000")+K$+$0A$,CC_COUNT=CC_COUNT+1
0890 GOTO MAIN_READ
0900 CCLIST_END: RETURN
0910 DISPLAY_LIST: 
0920 LET WIDTH=65,HEIGHT=20,WIN_X=2,WIN_Y=3
0930 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0940 LET HEADING$(2)="Cust   Order   Invoice Date       Credit Card No.     Amount"
0950 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0960 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0970 LET PAGES=INT(CC_COUNT/14)+2,MAX_ROW=HEIGHT-5;rem '14 lines per page shown
0980 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,2]
0990 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-4),FOOTING$,'ER','SF',
1000 LET PAGE=1,L=1,X1=1,SELECTION$="",XMODE=5
1010 IF CC_COUNT=0 THEN LET LLIST$[PAGE,X1,1]=" No transactions"
1020 GET_KEY: LET P=POS($0A$=CCLIST$); IF P<4 THEN LET SELECTION$="|EOF"; GOSUB DISPLAY_PAGE; GOTO DISPLAY_LIST_END
1030 LET X$=CCLIST$(1,P-1),CCLIST$=CCLIST$(P+1),CC_CHAN=NUM(X$(1,3),ERR=GET_KEY),K$=X$(4)
1040 READ RECORD (CC_CHAN,KEY=K$,DOM=GET_KEY)ARE63$
1050 IF CC_CHAN<>ARE63 THEN LET ART63$=ARE63$; GOTO RECORD_SETUP
1060 LET ARE63.AVAILABLE$=FILL(50),ART63$=ARE63$
1070 LET ART63.ORDER_NUMBER$=ART63.AR_INV_NBR$,ART63.AR_INV_NBR$="Deleted"
1080 READ (ARE03,KEY=K$(1,2)+"  "+K$(3)+"000",DOM=RECORD_SETUP)A0$
1090 LET ART63.AR_INV_NBR$=A0$(42,7)
1100 RECORD_SETUP: 
1110 LET LINE$=" "+ART63.CUSTOMER_NBR$+" "+ART63.ORDER_NUMBER$+" "+ART63.AR_INV_NBR$
1120 LET LINE$=LINE$+" "+ART63.TRANS_DATE$(5,2)+"/"+ART63.TRANS_DATE$(7,2)+"/"+ART63.TRANS_DATE$(1,4)
1125 LET LOG$=" Customer "+ART63.CUSTOMER_NBR$+" Order "+ART63.ORDER_NUMBER$+" Invoice "+ART63.AR_INV_NBR$; GOSUB LOGENTRY
1130 LET CCU$=""; CALL "BCC.11",CCU$,ART63.CC_NUMBER$; LET ART63.CC_NUMBER$=CCU$
1140 LET LINE$=LINE$+" "+ART63.CC_NUMBER$+STR(ART63.TRANS_AMOUNT:"###,###.00-")
1150 IF L>=MAX_ROW THEN GOSUB DISPLAY_PAGE
1160 LET LLIST$[PAGE,X1,1]=LINE$,LLIST$[PAGE,X1,2]=X$
1170 LET X1=X1+1,L=L+1
1180 GOTO GET_KEY
1190 DISPLAY_LIST_END: 
1200 CALL "SYC.WD",NAME$
1210 RETURN
1220 DISPLAY_PAGE: 
1230 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
1240 IF FKEY=1 THEN EXITTO DISPLAY_LIST_END
1250 IF FKEY=4 THEN EXITTO END_PROGRAM
1260 IF FKEY=-16 THEN LET PAGE=PAGE+1
1270 IF FKEY=0 THEN GOSUB ALL_INFO; GOTO DISPLAY_PAGE
1280 REM IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,HEIGHT-2
1290 LET L=1,X1=1
1300 RETURN
1310 ALL_INFO: 
1320 IF LEN(SELECTION$)<18 THEN GOTO ALL_INFO_END
1330 LET CC_CHAN=NUM(SELECTION$(1,3),ERR=ALL_INFO_END),K$=SELECTION$(4)
1340 READ RECORD (CC_CHAN,KEY=K$,DOM=ALL_INFO_END)ARE63$
1350 LET MSCHAN=ARE64; IF CC_CHAN<>ARE63 THEN LET ART63$=ARE63$,MSCHAN=ART64; GOTO DISPLAY_INFO
1360 LET ARE63.AVAILABLE$=FILL(50),ART63$=ARE63$
1370 LET ART63.ORDER_NUMBER$=ART63.AR_INV_NBR$,ART63.AR_INV_NBR$="Deleted"
1380 READ (ARE03,KEY=K$(1,2)+"  "+K$(3)+"000",DOM=DISPLAY_INFO)A0$
1390 LET ART63.AR_INV_NBR$=A0$(42,7)
1400 DISPLAY_INFO: 
1410 LET MSG$=""; READ RECORD (MSCHAN,KEY=K$,DOM=MSG_DONE)MSG$; LET MSG$=MSG$(16)
1420 MSG_DONE: LET WIDTH=55,HEIGHT=22,WIN_X=22,WIN_Y=2,TITLE$=""
1430 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,ANAME$
1440 PRINT 'WRAP'("on"),'SB',@(12,0),"Customer:",@(15,1),"Order:",@(13,2),"Invoice:",@(9,3),"Card Number:",@(10,4),"Expiration:",@(16,5),"Name:",@(12,6),"Currency:",@(14,7),"Status:",@(6,8),"Reference Code:",@(2,9),"Authorization Code:",@(6,10),"Transaction ID:",@(9,11),"Customer ID:",@(4,12),"Transaction Date:",@(14,13),"Amount:"
1450 LET CCU$=""; CALL "BCC.11",CCU$,ART63.CC_NUMBER$; LET ART63.CC_NUMBER$=CCU$
1460 LET X=23; PRINT 'SF',@(X,0),ART63.CUSTOMER_NBR$,@(X,1),ART63.ORDER_NUMBER$,@(X,2),ART63.AR_INV_NBR$,@(X,3),ART63.CC_NUMBER$,@(X,4),ART63.CC_EXPIRY_MONTH$," ",ART63.CC_EXPIRY_YEAR$,@(X,5),ART63.CC_NAME$
1470 PRINT @(X,6),ART63.CURRENCY$,@(X,7),ART63.STATUS$,@(X,8),ART63.REF_CODE$,@(X,9),ART63.AUTH_CODE$,@(X,10),ART63.TRANS_ID$,@(X,11),ART63.CUSTOMER_ID$,@(X,12),ART63.TRANS_DATE$,@(X,13),STR(ART63.TRANS_AMOUNT:"###,###.00")
1480 PRINT @(0,14),MSG$
1490 INPUT @(X),XX$,
1500 ALL_INFO_END: 
1510 CALL "SYC.WD",ANAME$
1520 RETURN
6000 LOGENTRY: 
6020 LET LOG$=$22$+LOG$+" <"+INFO(3,2)+"> "+DATE(0:"%Mz/%Dz/%Y - %Hz:%mz:%sz")+$22$
6040 LET A=SCALL("echo "+LOG$+" >> "+ADATA$+"bcc01.log")
6090 RETURN
7000 REM 7000 " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7090
7030 IF V3=127 THEN GOTO 7050
7040 RETURN
7050 REM " --- Escape During Input"
7060 CALL "SYC.ES",ERR=7090,PGM(-2),TCB(8),E$,E2,V3
7070 IF V3<>127 THEN GOTO 7000
7080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7090 REM " --- Error During Input"
7100 ESCAPE
7110 GOTO 7000
9000 REM 9000 " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9060,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9070
9040 IF E1=3 THEN GOTO 9090
9050 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9060 ESCAPE
9070 REM " --- Retry"
9080 RETRY
9090 REM " --- Return"
9100 SETERR 9000
9110 GOTO 9900
9300 REM 9300 " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9340,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9350
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9340 ESCAPE
9350 RETURN
9800 REM 9800 " --- Display Parameter record error"
9810 LET LINE_ERR$=PGM(TCB(5))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM 9900
9910 END_PROGRAM: CALL "SYC.WD",NAME$
9920 CALL "SYC.WD",SNAME$
9930 LET LOG$="End:"; GOSUB LOGENTRY
9940 RUN "SYS.AA"
9950 END

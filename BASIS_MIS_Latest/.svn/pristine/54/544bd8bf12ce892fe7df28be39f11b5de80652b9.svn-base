0010 REM "OPR - Sales Tax Report"
0020 REM "Program OPR.TA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ART-02",FILES$[2]="ARM-10",FILES$[3]="SYS-01"
0140 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET ART02_DEV=CHANNELS[1],ARM10_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 ART02A: IOLIST A0$,A[0],A[1],A[2]
0220 ARM10B: IOLIST X0$(1),X0
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$,M4$,M5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0500 REM " --- Initializations"
0510 DIM HEADINGS$[3],A[2],T[2],G[2],O[1]
0520 LET P8$="",P9$=""
0550 LET H0=132,H=0,HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0555 LET L9=42,L=L9+1,WHEN$=F0$(7,3),CLOCK$="",HEADINGS=3
0570 LET M1=LEN(M1$),M3=LEN(M3$)
0600 REM " --- Print positions"
0610 CALL "SYC.RM",M1$,M1,14,15
0620 LET O[0]=23+M3,O[1]=O[0]+6
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,6,0
0750 PRINT 'SB',@(24,3),"Beginning Date:",@(27,5),"Ending Date:",'SF',
0800 REM " --- Beginning Date"
0805 PRINT 'CF',
0810 LET V0$="D",V1$="EC6N",V2$=P9$,V0=8,V1=40,V2=3,V4$="Enter The Starting Date (<F1>=First)"
0815 GOSUB 7000
0820 ON V3 GOTO 0830,0825,0800,0800,9900
0825 LET V$=""
0830 LET P9$=FNP$(V$)
0835 IF P9$="" THEN PRINT @(V1,V2),"First",'CL',
0850 REM " --- Ending Date"
0855 LET V0$="D",V1$="C6N",V2$=P8$,V0=8,V1=40,V2=5,V4$="Enter Ending Date (<F1>=Last)"
0860 GOSUB 7000
0865 ON V3 GOTO 0875,0870,0800,0850,9900
0870 LET V$=""
0875 LET P8$=FNP$(V$)
0880 IF P8$="" THEN PRINT @(V1,V2),"Last",'CL',
0900 REM " --- Correct?"
0910 LET V4$="Is The Above Information Correct",V0=1,V1=FNV(V4$),V2=22
0915 CALL "SYC.YN",1,V4$,2,V$,V3
0930 ON V3 GOTO 0940,0900,0900,0900,9900
0940 ON POS(V$="YN") GOTO 0900,0950,0800
0950 REM " --- Background"
0953 DIM J$(4*M1,"-")
0955 CALL "SYC.NB","Printing",8,COLUMN
0960 CALL "SYC.GA",7,1,"","",STATUS
0965 IF STATUS>0 THEN GOTO 9900
0970 IF P9$="" THEN LET X0$="First" ELSE LET X0$=FNB6$(P9$)
0975 IF P8$="" THEN LET X1$="Last" ELSE LET X1$=FNB6$(P8$)
0980 LET HEADINGS$[2]="From "+X0$+" To "+X1$
1000 REM " --- Position File"
1090 READ (ART02_DEV,KEY=N0$,DOM=1100)
1100 REM " --- Main Read "
1120 READ (ART02_DEV,END=4000)IOL=ART02A
1130 IF A0$(1,2)<>N0$ THEN GOTO 4000
1160 IF P9$<>"" THEN IF A0$(5,4)<P9$(1,4) THEN GOTO 1100
1170 IF P8$<>"" THEN IF A0$(5,4)>P8$(1,4) THEN GOTO 1100
1180 PRINT @(COLUMN,11),A0$(3,2)," ",A0$(7,2),"/",FNYY21_YY$(A0$(5,2)),
1200 REM " --- Breaks "
1220 IF A0$(3,2)<>T0$ THEN GOSUB 6000
2000 REM " --- Print"
2010 IF L>L9 THEN GOSUB 5000
2020 PRINT (7)H3$,@(O[0]),A0$(7,2),"/",FNYY21_YY$(A0$(5,2)),@(O[1]),A[0]:M1$,A[1]:M1$,A[2]:M1$,A[2]-A[0]:M1$
2060 LET L=L+1,L0=L0+1,H3$=""
2070 FOR X=0 TO 2
2080 LET T[X]=T[X]+A[X],G[X]=G[X]+A[X]
2090 NEXT X
3900 REM " --- Loop back for next record"
3990 GOTO 1100
4000 REM " --- All done"
4010 IF L>L9 THEN GOSUB 5000
4020 LET T9$="E"
4030 GOSUB 6000
4040 PRINT (7)@(O[1]),J$
4050 PRINT (7)@(3),"Totals For Report",@(O[1]),G[0]:M1$,G[1]:M1$,G[2]:M1$,G[2]-G[0]:M1$
4090 GOTO 9900
5000 REM " --- Heading"
5010 LET L=0
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)"Tax"
5050 PRINT (7)"Cd  Description",@(O[0]-5),"Rate",@(O[0]),"Date",@(O[1]+M1-8),"Taxable",@(O[1]+M1*2-4),"Tax",@(O[1]+M1*3-6),"Gross",@(O[1]+M1*4-12),"Non-Taxable"
5060 PRINT (7)""
5090 RETURN 
6000 REM " --- Tax Code Breaks "
6010 IF T0$="" THEN GOTO 6100
6020 IF L0<1 THEN GOTO 6100
6030 PRINT (7)@(O[1]),J$
6040 PRINT (7)@(3),"Totals For ",T0$,@(O[1]),T[0]:M1$,T[1]:M1$,T[2]:M1$,T[2]-T[0]:M1$
6050 PRINT (7)""
6060 LET L=L+3
6100 IF T9$<>"" THEN GOTO 6190
6110 DIM X0$(64),T[2]
6120 LET T0$=A0$(3,2),L0=0,X0$(6,20)="Unknown Tax Code",X0=0
6130 FIND (ARM10_DEV,KEY=N0$+"B"+T0$,DOM=6140)IOL=ARM10B
6140 LET H3$=T0$+" "+X0$(6,20)+STR(X0:M3$)
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "POE - Requisition Entry (Header/Ribbon)"
0020 REM "Program POE.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETERR 9000
0060 SETESC 9000
0070 ON O0 GOTO 0080,1000,4000,4200
0080 BEGIN 
0085 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=28
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="SYS-01",FILES$[2]="POE-01",FILES$[3]="POE-11"
0120 LET FILES$[4]="POE-21",FILES$[5]="POE-31",FILES$[6]="POE-41"
0125 LET FILES$[7]="POM-02",FILES$[8]="POM-04",FILES$[9]="POM-14"
0130 LET FILES$[10]="POS-10",FILES$[11]="APM-01",FILES$[12]="APM-02"
0135 LET FILES$[13]="APM-05",FILES$[14]="APM-09",FILES$[15]="APM-10"
0140 LET FILES$[16]="IVM-01",FILES$[17]="IVM-02",FILES$[18]="IVM-05"
0145 LET FILES$[19]="IVM-10",FILES$[20]="IVM-14"
0155 CALL "SYC.DA",1,1,20,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0165 LET SYS01_DEV=CHANNELS[1],POE01_DEV=CHANNELS[2],POE11_DEV=CHANNELS[3]
0170 LET POE21_DEV=CHANNELS[4],POE31_DEV=CHANNELS[5],POE41_DEV=CHANNELS[6]
0175 LET POM02_DEV=CHANNELS[7],POM04_DEV=CHANNELS[8],POM14_DEV=CHANNELS[9]
0180 LET POS10_DEV=CHANNELS[10],APM01_DEV=CHANNELS[11],APM02_DEV=CHANNELS[12]
0185 LET APM05_DEV=CHANNELS[13],APM09_DEV=CHANNELS[14],APM10_DEV=CHANNELS[15]
0190 LET IVM01_DEV=CHANNELS[16],IVM02_DEV=CHANNELS[17],IVM05_DEV=CHANNELS[18]
0195 LET IVM10_DEV=CHANNELS[19],IVM14_DEV=CHANNELS[20]
0200 REM " --- IOLIST's"
0210 POE01A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0220 APM02A: IOLIST B0$(1),B1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,I5$,I6$,I7$,I8$,I9$,I10$
0280 POS01A: IOLIST P0$,P1$,P2$,P3$,P4$
0285 POS10N: IOLIST P0$,P[ALL]
0290 APS01A: IOLIST X$,P1$,P2$,X$,X$,M0$
0300 ARS01A: IOLIST X$,P5$,P6$,X$,X$,P7$
0310 POE21A: IOLIST X2$(1)
0320 POE31A: IOLIST X3$(1)
0400 REM " --- Parameters"
0405 DIM I[6],P[3],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO",AP$="N",AR$="N",SF$="N"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=POS01A
0423 FIND (POS10_DEV,KEY=N0$+"N",DOM=9900)IOL=POS10N
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 LET VENDLEN=NUM(P2$(1,2))
0435 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1)),I[2]=NUM(I2$(7,2))
0445 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0447 LET I[6]=NUM(I2$(3,2)),M1$=I6$,M2$=I7$,M3$=I8$,M4$=I9$,M5$=I10$
0450 CALL "SYC.VA","AR",INFO$[ALL]
0455 LET AR$=INFO$[20]
0460 IF AR$="Y" THEN CALL "SYC.VA","OP",INFO$[ALL]
0465 LET OP$=INFO$[20]
0470 CALL "SYC.VA","WO",INFO$[ALL]
0475 LET SF$=INFO$[20]
0500 REM " --- Initializations"
0505 PRECISION I[1]
0510 DIM A0$(15),A1$(160),A[8],A$[1],O[5],MESSAGE$[2],VENDOR$(8)
0520 DIM RIBBON[20],RIBBON$[20],RFILES[10],X2$(15),X3$(15),X4$(38)
0550 LET A0$(1)=N0$,L0=12,INCREMENT=P[3],ACTION$="B",TYPE$="R",REQ=0
0560 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),M5=LEN(M5$),P=13+MAX(I[0],5)
0570 LET RIBBON[1]=15,RIBBON[2]=L0,RIBBON$[1]=N0$,RFILES[1]=APM05_DEV
0575 LET RFILES[2]=APM10_DEV,RFILES[3]=IVM10_DEV,RFILES[4]=POM04_DEV
0600 REM " --- Additional File Opens"
0610 IF AR$="N" THEN GOTO 0630
0615 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0620 LET CUSTLEN=NUM(P6$(1,2)),FILES$[21]="ARM-01",FILES$[22]="ARM-03"
0630 IF OP$="N" THEN GOTO 0640
0635 LET FILES$[23]="ARE-03",FILES$[24]="ARE-13",FILES$[25]="ARE-33"
0640 IF SF$="N" THEN GOTO 0650
0645 LET FILES$[26]="WOE-01",FILES$[27]="WOE-22",FILES$[28]="WOE-32"
0650 CALL "SYC.DA",1,21,28,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0655 IF STATUS THEN GOTO 9900
0660 LET ARM01_DEV=CHANNELS[21],ARM03_DEV=CHANNELS[22]
0665 LET ARE03_DEV=CHANNELS[23],ARE13_DEV=CHANNELS[24],ARE33_DEV=CHANNELS[25]
0670 LET WOE01_DEV=CHANNELS[26],WOE22_DEV=CHANNELS[27],WOE32_DEV=CHANNELS[28]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,1,0,L0-3,0
0720 PRINT 'SB',@(1,3),"Vendor:",@(34),"Req #:",@(50),"Purchase Address:",@(3,4),"Name:",@(0,5),"Address:",'SF',
0730 CALL "POC.RA",TYPE$,ACTION$,RIBBON$[ALL],RIBBON[ALL],RFILES[ALL],A$[ALL],A[ALL],V3
0800 REM " --- Resize masks and set print positions"
0810 IF FNP(M1,M2,M3,M5,P)>0 THEN GOTO 0900
0820 CALL "SYC.RM",M5$,M5,5,M5-1
0830 IF FNP(M1,M2,M3,M5,P)>0 THEN GOTO 0900
0840 CALL "SYC.RM",M1$,M1,10,M1-1
0850 IF FNP(M1,M2,M3,M5,P)>0 THEN GOTO 0900
0860 CALL "SYC.RM",M2$,M2,6,M2-1
0870 IF FNP(M1,M2,M3,M5,P)>0 THEN GOTO 0900
0880 CALL "SYC.RM",M3$,M3,6,M3-1
0890 GOTO 0800
0900 REM " --- Position file"
0910 LET O[5]=79-M1,O[4]=O[5]-M3-1,O[2]=O[4]-M2-1,O[1]=O[2]-M5-1,O[0]=O[1]-3
0980 READ (POE31_DEV,KEY=N0$,DOM=0990)
0990 READ (POE01_DEV,KEY=A0$,DOM=1000)
1000 REM " --- Vendor number"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(POE01_DEV,END=1050)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3,6)
1050 LET V0$="M",V1$="KCE",V3$=P1$,V0=6,V1=9,V2=3,REQ=0
1055 LET V4$="Enter A Valid Vendor Number (<F1>=Requisition Number/<F3>=Lookup)"
1060 GOSUB 7000
1070 ON V3 GOTO 1100,2000,1000,1080,9900
1080 CALL "SYC.LV",V$
1090 IF V$="" THEN GOTO 0900
1100 REM " --- Display vendor info"
1110 GOSUB 5200
1120 IF STATUS THEN GOTO 1000
1130 IF POS(VENDOR$=A0$)<>1 THEN LET A0$(1)=VENDOR$
1190 READ (POE01_DEV,KEY=A0$,DOM=1200)
1200 REM " --- Requisition number"
1210 LET V2$="",V2$=KEY(POE01_DEV,END=1240)
1220 IF POS(VENDOR$=V2$)<>1 THEN LET V2$=""
1230 IF V2$<>"" THEN LET V2$=V2$(9)
1240 LET V4$="Enter A Valid Requisition Number (<F1>=Next Available Number/<F3>=Lookup)"
1250 LET V0$="Z",V1$="CKER",V3$="",V0=7,V1=41,V2=3,REQ=1
1260 GOSUB 7000
1270 ON V3 GOTO 1400,1280,1000,1300,9900
1280 CALL "POC.NA","A",TYPE$,POS10_DEV,N0$,V$
1285 PRINT @(41,3),V$,
1290 GOTO 1400
1300 REM " --- <F3> Lookup"
1310 CALL "POC.LA",TYPE$,A0$(3,6),V$
1320 IF V$="" THEN GOTO 1200
1400 REM " --- Display requisition"
1410 GOSUB 5400
1420 IF STATUS THEN GOTO 1600
1440 GOSUB 6000
1480 ON V3 GOTO 1490,1500,1400,1400,1200
1490 ON POS(V$="YN") GOTO 1400,3900,1200
1500 REM " --- Retrieve next requisition for current vendor"
1510 LET K$=KEY(POE01_DEV,END=1200)
1520 IF POS(VENDOR$=K$)<>1 THEN GOTO 1200
1530 LET V$=K$(9)
1590 GOTO 1400
1600 REM " --- New record?"
1610 CALL "SYC.YN",1,"Is This A New Record",2,V$,V3
1620 ON V3 GOTO 1630,1600,1600,1600,4300
1630 ON POS(V$="YN") GOTO 1600,1700,4300
1700 REM " --- Initialize new record"
1710 DIM A1$(160),A[8],B0$(10),B1$(24)
1720 LET A1$(1)=I3$(6,2),A1$(5)=F0$(18,6),A1$(23)=F0$(18,6)
1730 LET A1$(44)=P3$(18,15),A1$(59)=P3$(3,15),A1$(94)=P3$(33,15)
1740 LET A1$(35,1)=P3$(48,1),EXISTS=0,PRINTIT=1
1800 REM " --- Get default terms code from first vendor detail record"
1810 READ (APM02_DEV,KEY=VENDOR$,DOM=1820)
1820 LET K$=KEY(APM02_DEV,END=1900)
1830 IF POS(VENDOR$=K$)<>1 THEN GOTO 1900
1840 READ (APM02_DEV)IOL=APM02A
1850 LET A1$(38,2)=B1$(5,2)
1900 REM " --- Display record and proceed to ribbon"
1910 GOSUB 5000
1990 GOTO 3900
2000 REM " --- Retrieve record using only requisition number"
2005 PRINT 'CF',
2010 LET V2$="",V2$=KEY(POE31_DEV,END=2040)
2020 IF POS(N0$=V2$)<>1 THEN LET V2$=""
2030 IF V2$<>"" THEN LET V2$=V2$(3,7)
2040 LET V4$="Enter A Valid Requisition Number (<F2>=Vendor Number/<F3>=Lookup)"
2050 LET V0$="Z",V1$="CKER",V3$="",V0=7,V1=41,V2=3
2060 GOSUB 7000
2070 ON V3 GOTO 2200,2000,1000,2100,9900
2100 REM " --- Requisition lookup"
2110 CALL "POC.LB",TYPE$,V$
2120 IF V$="" THEN GOTO 2000
2130 LET K$=V$(1,2)+V$(9)+V$(3,6)
2190 GOTO 2400
2200 REM " --- Position requisition xref"
2210 LET X$=N0$+V$
2290 READ (POE31_DEV,KEY=X$,DOM=2300)
2300 REM " --- Get next xref"
2310 LET K$=KEY(POE31_DEV,END=2000)
2320 IF POS(X$=K$)<>1 THEN GOTO 2000
2330 READ (POE31_DEV)
2400 REM " --- Retrieve and display vendor and vendor comments"
2410 LET V$=K$(10)
2420 GOSUB 5200
2430 IF STATUS THEN GOTO 2000
2440 LET V$=K$(3)
2450 GOSUB 5400
2460 IF STATUS THEN GOTO 2000
2500 REM " --- Right requisition?"
2510 GOSUB 6000
2520 ON V3 GOTO 2530,2540,2500,2500,2000
2530 ON POS(V$="YN") GOTO 2500,3900,2000
2540 LET X$=N0$
2590 GOTO 2300
3900 REM " --- Process ribbon"
3910 LET ACTION$="I",A$[0]=A0$,A$[1]=A1$,RIBBON[3]=EXISTS
3920 CALL "POC.RA",TYPE$,ACTION$,RIBBON$[ALL],RIBBON[ALL],RFILES[ALL],A$[ALL],A[ALL],V3
3930 IF V3=2 THEN GOTO 1200
3940 LET A0$(1)=A$[0],A1$(1)=A$[1]
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Reprint/Delete)?",REQ=1
4015 IF EXISTS=0 THEN LET V4$="Is The About Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=7,V2=22,V1=FNV(V4$),EXISTS=0
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4300
4050 IF V$="DELETE" THEN GOTO 4200
4060 IF V$="R" OR V$="REPRINT" THEN GOTO 4400
4090 ON POS(V$="YN") GOTO 4000,4100,3900
4100 REM " --- Write record"
4110 WRITE (POE01_DEV,KEY=A0$)IOL=POE01A
4120 LET X3$(1)=A0$(1,2)+A0$(9)+A0$(3,6)
4130 WRITE (POE31_DEV,KEY=X3$)IOL=POE31A
4180 LET O0=0,LINE=L0-1
4190 RUN "POE.AB"
4200 REM " --- Delete requisition"
4210 DIM FILES[20],PARAMS[10],PARAMS$[10]
4220 LET FILES[1]=POS10_DEV,FILES[2]=POM02_DEV,FILES[3]=POE01_DEV
4230 LET FILES[4]=POE11_DEV,FILES[5]=POE21_DEV,FILES[6]=POE31_DEV
4240 LET FILES[7]=POE41_DEV,FILES[11]=WOE22_DEV,FILES[12]=WOE32_DEV
4250 LET PARAMS$[0]=N0$,PARAMS$[1]=SF$,PARAMS$[2]=I2$
4260 LET PARAMS$[3]=I3$,PARAMS$[4]=I4$,PARAMS[0]=I[1]
4290 CALL "POC.EA",TYPE$,FILES[ALL],PARAMS[ALL],PARAMS$[ALL],A0$,A1$,STATUS
4300 REM " --- Reset number"
4310 CALL "POC.NA","D",TYPE$,POS10_DEV,N0$,A0$(9)
4320 IF REQ THEN GOTO 1200
4390 GOTO 1000
4400 REM " --- Reprint"
4410 LET X2$(1)=A0$
4420 FIND (POE01_DEV,KEY=A0$,DOM=4490)
4430 WRITE (POE21_DEV,KEY=X2$)IOL=POE21A
4440 LET PRINTIT=0
4450 LET MESSAGE$[0]="This Requisition Has Been Queued For Printing (<Enter>=Continue)"
4460 CALL "SYC.XA",3,MESSAGE$[ALL],0,22,-1,V$,V3
4490 GOTO 4000
5000 REM " --- Display record"
5010 PRINT @(41,3),A0$(9),
5020 LET ACTION$="D",A$[0]=A0$,A$[1]=A1$
5030 CALL "POC.RA",TYPE$,ACTION$,RIBBON$[ALL],RIBBON[ALL],RFILES[ALL],A$[ALL],A[ALL],V3
5090 RETURN 
5200 REM " --- Display vendor and comments"
5210 LET VENDOR$(1)=N0$+V$
5220 CALL "POC.VA","VC",VENDOR$,VENDLEN,M0$,APM01_DEV,APM09_DEV,P3$,L0,STATUS
5290 RETURN 
5400 REM " --- Read/display requisition"
5410 LET A0$(1)=VENDOR$+V$,STATUS=11
5420 READ (POE01_DEV,KEY=A0$,DOM=5490)IOL=POE01A
5430 LET STATUS=0
5440 GOSUB 5000
5490 RETURN 
6000 REM " --- Right Requisition?"
6010 LET V4$="Is This The Correct Requisition (Y/N/<F1>=Next Requisition)?"
6020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
6030 GOSUB 7000
6040 LET EXISTS=1,PRINTIT=0
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8070 DEF FNP(Q1,Q2,Q3,Q4,Q5)=(76-(Q1+Q2+Q3+Q4))-Q5
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

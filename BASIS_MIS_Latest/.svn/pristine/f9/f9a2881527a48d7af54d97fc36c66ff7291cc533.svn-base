0010 REM "APR - Invoice Purge Register 2"
0020 REM "Program APR.RB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLISTs"
0210 APT01A1: IOLIST A0$,A[0],A[1],A[2]
0240 APT01A2: IOLIST X0$,X[0],X[1],X[2]
0250 APM03A: IOLIST *,T2$(1)
0260 APM01A: IOLIST *,T2$(1)
0310 APT11A1: IOLIST A0$,A1$,A[0],A[1],A[2]
0340 APT11A2: IOLIST X0$,X1$,X[0],X[1],X[2]
0500 REM " --- Init Data"
0510 LET HEADINGS=3
0520 DIM HEADINGS$[HEADINGS]
0530 LET HEADINGS$[0]=N1$,HEADINGS$[1]="Invoice Purge Register",HEADINGS$[2]="Purge Date: "+FNB$(P9$)
0540 LET H0=80,H=0,WHEN$=F0$(7,3),CLOCK$=""
0550 LET L9=59,L=L9+1,T0=0,T1=0
0560 LET M1=LEN(M1$)
0570 DIM X[2],W[2],B[13],T2$(30)
0610 CALL "SYC.GA",7,1,"","",STATUS
0620 IF STATUS THEN GOTO 9900
1000 REM " --- Init Invoice Read"
1020 READ (APT01_DEV,KEY=N0$,DOM=1030)
1040 CALL "SYC.NB","Printing: ",LEN(M0$)+11,COLUMN
1100 REM " --- Invoice Read"
1120 LET K0$=KEY(APT01_DEV,END=4000)
1140 IF K0$(1,2)<>N0$ THEN GOTO 4000
1260 READ (APT01_DEV)IOL=APT01A1
1270 IF A0$(32,3)>P9$ THEN GOTO 1700
1280 IF A0$(5,4)="TEMP" THEN PRINT @(COLUMN,11),"Temp",J1$(1,15), ELSE PRINT @(COLUMN,11),FNF$(A0$(5,P[0]),M0$)," ",A0$(11,10),
1300 REM " --- Init Totals"
1310 DIM T[2]
1320 LET O9=0
1400 REM " --- Find Total"
1420 FOR X=0 TO 2
1430 LET T[X]=T[X]+A[X]
1440 NEXT X
1490 READ (APT11_DEV,KEY=K0$(1,20),DOM=1500)
1500 REM " --- Invoice Detail"
1510 LET K$=KEY(APT11_DEV,END=1600)
1520 IF K$(1,20)<>K0$(1,20) THEN GOTO 1600
1530 READ (APT11_DEV)IOL=APT11A1
1550 IF A1$(1,1)=" " THEN GOTO 1580
1560 LET T[0]=T[0]+A[0],T[1]=T[1]+A[1],T[2]=T[2]+A[2]
1570 IF A1$(2,3)>P9$ THEN GOTO 1700
1590 GOTO 1500
1600 REM " --- Skip or Print"
1620 IF T[0]<>0 OR T[2]<>0 THEN GOTO 1700
1640 IF O9=0 THEN GOTO 1800
1700 REM " --- Next Invoice"
1720 READ (APT01_DEV,KEY=K0$(1,20)+$FF$,DOM=1100)
1790 GOTO 1100
2000 REM " --- Print Invoice Detail
2020 READ (APT01_DEV,KEY=K0$)IOL=APT01A2
2040 IF K0$(5,P[0])<>T1$ THEN GOSUB 5500
2170 IF L+1>L9 THEN GOSUB 5000
2200 IF P3$(3,1)="Y" THEN PRINT (7)@(44+2*M1),X[2]:M1$,
2210 PRINT (7)@(6),X0$(11,10),"    ",FNB$(X0$(29,3))," ",FNB$(X0$(35,3))," ",X0$(25,2)," ",X0$(67,1),@(44),X[0]:M1$,X[1]:M1$
2220 LET L=L+1
2230 LET W0=X[0],W1=X[1],W2=X[2],Q9=0
2280 LET Q9=1
2290 READ (APT11_DEV,KEY=K0$(1,20),DOM=2300)
2300 LET K$=KEY(APT11_DEV,END=2400)
2310 IF K$(1,20)<>X0$(1,20) THEN GOTO 2400
2320 READ (APT11_DEV)IOL=APT11A2
2335 IF X1$(1,1)=" " THEN GOTO 2400
2341 IF L+1>L9 THEN GOSUB 5000
2342 IF P3$(3,1)="Y" THEN PRINT (7)@(44+2*M1),X[2]:M1$,
2344 PRINT (7)@(10),X1$(1,1)," ",X1$(5,7)," ",FNB$(X1$(2,3)),@(44),X[0]:M1$,X[1]:M1$
2345 LET L=L+1
2350 LET W0=W0+X[0],W1=W1+X[1],W2=W2+X[2]
2370 GOTO 2300
2400 REM " --- Print Total"
2405 IF Q9=0 THEN GOTO 2490
2410 IF P3$(3,1)="Y" THEN PRINT (7)@(44+2*M1),W2:M1$,
2420 PRINT (7)@(21),"Invoice Total:",@(44),W0:M1$,W1:M1$
2440 LET L=L+1
2450 LET T0=T0+W0,T1=T1+W1
2490 GOTO 1100
4000 REM " --- Done"
4010 LET T9$="END"
4020 GOSUB 5500
4025 IF L+1>L9 THEN GOSUB 5000
4030 PRINT (7)@(22),"Report Total:",@(44),T2:M1$
4040 CLOSE (7,ERR=4050)
4050 PRINT 'SF',@(5,5),"*** All Zero Balance Invoices Through ",FNB$(P9$)," Will Be Removed ***",'RB',
4070 CALL "SYC.YN",0,"Are You Ready To Purge The Invoices?",0,V$,V3
4110 PRINT @(0,15),'CL',
4120 IF V$="NO" THEN GOTO 9900
4130 IF V$<>"YES" THEN GOTO 4070
4290 RUN "APU.GA"
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS<>0 THEN EXITTO 9900
5040 IF P3$(3,1)="Y" THEN PRINT (7)@(33+3*M1),"Retention",
5050 PRINT (7)@(0),"Vendor Invoice",@(22),"Date",@(31),"Due",@(38),"Pg H",@(44+M1-7),"Amount",@(44+2*M1-9),"Discount"
5080 LET L=L+1
5090 RETURN 
5500 REM " --- Vendor Break
5510 IF T1$="" THEN GOTO 5600
5520 IF L+1>L9 THEN GOSUB 5000
5530 PRINT (7)@(22),"Vendor Total:",@(44),T0:M1$
5540 LET T2=T2+T0,T3=T3+T1,T0=0,T1=0
5550 LET L=L+1
5610 IF T9$="END" THEN RETURN 
5620 DIM T2$(30)
5630 LET T1$=K0$(5,P[0]),T2$(1)="** Vendor Not On File **"
5640 IF K0$(5,4)="TEMP" THEN FIND (APM03_DEV,KEY=N0$+K0$(3,2)+K0$(11,10),DOM=5651)IOL=APM03A; LET T1$=T2$; GOTO 5651
5650 FIND (APM01_DEV,KEY=N0$+K0$(5,6),DOM=5651)IOL=APM01A
5660 IF L+2>L9 THEN GOSUB 5000
5670 PRINT (7)'LF',@(LEN(M0$)+1),T2$,
5675 IF K0$(5,4)="TEMP" THEN PRINT (7)@(0),"Temp" ELSE PRINT (7)@(0),FNF$(K0$(5,P[0]),M0$)
5680 LET L=L+2
5690 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "Program BSN.05
0050 BEGIN 
0060 SETERR err_rtn
0100 REM "OPEN/LOCK FILES"
0110 OPEN (1)"reset_reason.dat"
0160 OPEN (6)"SYS-01"
0200 REM "templates,iolists
0210 call "templates.pgm::reset_reason"
0220 reset_reason.available$=fill(7)
0270 sys01: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0405 FIND (6,KEY="T"+FID(0),DOM=par_err_rtn)IOL=sys01
0500 REM "INIT DATA"
0700 REM "BACKGROUND"
0720 PRINT 'SB',@(3,3),"Reset Code:",@(3,4),"    Reason:",
0790 PRINT 'SF',
0900 REM "INIT FILE"
0920 READ (1,KEY="",DOM=*next)
1000 main_loop:
1010 PRINT 'CF',
1020 LET V2$="  ",V2$=KEY(1,END=*next)
1050 LET V0$="S",V1$="^",V3$="",V0=2,V1=15,V2=3,V4$="Enter Reset Code, (<F3>=Lookup)"
1060 GOSUB std_input
1070 IF V3=4 OR V$="END" THEN GOTO pgm_exit;rem 'print_rpt
1080 IF V3=3 THEN GOSUB lookup
1090 LET reset_reason.reset_code$=V$
1100 PRINT @(V1,V2),v$; IF cvs(V$,3)="" THEN GOTO main_loop
1200 REM "FIND KEY"
1210 LET O9=0
1220 FIND record(1,KEY=v$,DOM=new_record)reset_reason$
1300 REM "DISPLAY"
1320 GOSUB display_rec
1340 GOTO correct
1400 NEW_RECORD:
1410 LET V0$="Y",V1$="C^",V2$="",V3$="",V4$="Is This A New Record? <Y/N>: ",V0=1,V1=55,V2=22
1420 GOSUB std_input
1440 ON POS(V$="YN") GOTO new_record,setup,main_loop
1500 SETUP:
1510 LET reset_reason.reason$=FILL(30)
2000 DESCR:
2006 GOSUB display_rec
2010 LET O9=1
2020 LET V0$="S",V1$="",V2$=reset_reason.reason$,V3$="",V0=30,V1=15,V2=4,I0=0
2040 GOSUB std_input
2060 LET reset_reason.reason$=v$
2090 GOTO next_field
3900 NEXT_FIELD:
3910 IF V3<>2 THEN GOTO i0_up
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO on_i0
3940 i0_up:
3950 LET I0=I0+1
3960 ON_I0:
3970 ON I0 GOTO descr,next_field,correct
3990 REM
4000 correct:
4010 LET V0$="S",V1$="KC^",V2$="Y",V3$="",V4$="Is The Above Information Correct? <Y/N/Delete>: ",V0=6,V1=64,V2=22
4020 GOSUB std_input
4030 IF V$="DELETE" THEN GOTO delete_rec
4040 IF V$="" THEN IF O9=0 THEN GOTO main_loop
4050 ON POS(V$="YN") GOTO correct,4100,2000
4100 REM "WRITE"
4120 WRITErecord (1,KEY=reset_reason.reset_code$)reset_reason$
4190 GOTO main_loop
4200 DELETE_REC:
4220 REMOVE (1,KEY=reset_reason.reset_code$,DOM=*next)
4290 GOTO main_loop
4500 REM " --- Print?"
4505 print_rpt:
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4590 ON POS(V$="YN") GOTO pgm_exit,print_it,pgm_exit
4900 REM " --- Report Initializations"
4901 print_it:
4905 LET PROGRAM$="",NUMBER=1,HEADINGS=3,WIDTH=80,CHANNEL=1
4915 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4920 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4925 DIM HEADINGS$[HEADINGS]
4930 LET FIELD$[0]="Code",FIELD[0]=1,POSITION[0]=1
4940 LET LENGTH[0]=2,TYPE$[0]="S",MASK$[0]=""
4945 LET FIELD$[1]="Description",FIELD[1]=1,POSITION[1]=3
4950 LET LENGTH[1]=30,TYPE$[1]="S",MASK$[1]="",TOTAL$="State Codes"
4980 LET HEADINGS$[0]="",HEADINGS$[1]="State Listing",RECORD$="",N0$=""
4990 RUN "SYR.AA"
5000 DISPLAY_rec:
5020 PRINT @(15,4),reset_reason.reason$
5090 RETURN 
6000 Lookup:
6005 LET TITLE$="Reset Reasons"
6010 LET CHANNEL=1,NUMBER=2
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Reason"
6040 LET FIELD[0]=1,POSITION[0]=1,LENGTH[0]=2
6050 LET FIELD[1]=1,POSITION[1]=3,LENGTH[1]=30
6060 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6070 LET V$="SKIP",ROW=V2+2
6080 CALL "BTM.10",CHANNEL,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7005 std_input:
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO std_input
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO std_input
9000 REM " --- Standard Error Routine (15May95)"
9005 err_rtn:
9010 IF ERR=127 THEN GOTO esc_rtn
9020 CALL "SYC.EA",ERR=esc_now,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO retry_rtn
9040 IF E1=3 THEN GOTO pgm_exit
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 esc_now:
9091 ESCAPE
9100 REM " --- Retry"
9105 retry_rtn:
9190 RETRY
9300 REM " --- Standard Escape Routine"
9305 esc_rtn:
9310 CALL "SYC.ES",ERR=esc_rtn_1,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN return
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 esc_rtn_1:
9360 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9805 par_err_rtn:
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 pgm_exit:
9905 RUN "SYS.AA"
65000 END

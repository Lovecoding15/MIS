0010 REM "GLR - Journal Entry Register (Report Overlay)"
0020 REM "Program GLR.MB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0051 REM "  NOTE: If this prog is run from GLR.NA, Recurring Journal Register,
0052 REM "        GLE01_DEV & GLE11_DEV are really referring to GLE-02 & GLE-12
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-03",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM03_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 GLE01A: IOLIST A0$(1),A1$(1),A2$,A[ALL]
0220 GLE11A: IOLIST B0$(1),B1$(1),B2$(1),B3$,B[ALL]
0230 GLM01A: IOLIST C0$(1),C1$(1)
0240 GLM03A: IOLIST D0$(1),D1$,D[ALL]
0260 GLS01A: IOLIST X$,P1$,P2$,P3$(1),P4$(1),M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0405 DIM P[4],P3$(325),P4$(32)
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0435 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),K9=17,K8=K9-3
0440 FOR X=0 TO 4
0445 LET P[X]=NUM(P2$(X*2+1,2),ERR=0450)
0450 NEXT X
0455 LET P[2]=FNYY_YEAR(P2$(5,2))
0500 REM " --- Initializations"
0510 DIM A0$(K8),A1$(48),A[2],B0$(K9),B1$(P[4]),B2$(30),B[4]
0520 LET HEADINGS=2,H0=131,H=0,L9=59,L=L9+1,DONE$="N"
0530 DIM C0$(2+P[4]),C1$(40),D0$(32),D[2],J1$(M1-1,"-"),J2$(M2-1,"-")
0540 DIM HEADINGS$[HEADINGS+1],O[6],ENTRY[2],TOTAL[2]
0555 LET H1$=N1$,H2$=N4$,WHEN$=F0$(7,3),CLOCK$="",ERRORS=0,DW=30
0560 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$,UPDATE=0,NUMBER=0
0565 IF CYCLES$="Y" THEN LET HEADINGS=HEADINGS+1,HEADINGS$[2]=H3$
0580 IF P4$(3,1)<>"Y" THEN LET J2$=""
0590 LET A0$(1)=N0$
0600 REM " --- Print positions"
0605 LET M=M0
0610 IF M<8 THEN LET M=8
0620 LET O[0]=3,O[1]=O[0]+4,O[2]=O[1]+M+1,O[3]=58,O[6]=H0
0630 IF P4$(3,1)="Y" THEN LET O[6]=H0-M2,H6=O[6]+M2-6,UNITHDR$="Units"
0640 LET O[5]=O[6]-M1,O[4]=O[5]-M1,DW=MIN(DW,O[4]-O[3]-7)
0645 REM " --- Additional File Opens"
0650 LET GL$="N",STATUS=0
0653 IF JE$<>"Y" THEN GOTO 0700
0655 CALL "GLC.BA",ERR=0660,SOURCE$,N2$,GLW11$,GL$,STATUS
0660 IF STATUS<>0 THEN GOTO 9900
0670 LET FILES$[3]="GLM-01",FILES$[4]=GLW11$,OPTIONS$[4]="CL"
0675 CALL "SYC.DA",1,3,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS>0 THEN GOTO 9900
0690 LET GLM01_DEV=CHANNELS[3],GLW11_DEV=CHANNELS[4]
0700 IF JE$="Y" THEN GOTO 0800
0710 LET FILES$[3]="GLM-01"
0720 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0730 IF STATUS>0 THEN GOTO 9900
0740 LET GLM01_DEV=CHANNELS[3]
0800 REM " --- Run Report?
0810 LET V4$="Do You Want To Print The "+TASK$
0820 CALL "SYC.YN",1,V4$,2,V$,V3
0830 IF V3=4 THEN GOTO 9900
0840 IF V$<>"Y" THEN GOTO 9900
0900 REM " --- Position File"
0910 CALL "SYC.GA",7,1,"","",STATUS
0920 IF STATUS THEN GOTO 9900
0930 CALL "SYC.NB","Printing",13,COLUMN
0990 READ (GLE01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Top Of Loop"
1010 READ (GLE01_DEV,END=4000)IOL=GLE01A
1020 IF POS(N0$=A0$)<>1 THEN GOTO 4000
1030 PRINT @(COLUMN,11),A0$(3,2)," ",A0$(5,10)
1100 REM " --- Check cycle code if indicated"
1110 IF CYCLES$<>"Y" THEN GOTO 1200
1120 IF CYCLE$="" THEN GOTO 1200
1130 IF A1$(37,2)=CYCLE$ THEN GOTO 1200
1190 GOTO 3900
1200 REM " --- Level breaks"
1210 GOSUB 6000
1900 REM " --- Position to first line item""
1910 GOSUB 5200
1920 LET NUMBER=NUMBER+1
1990 READ (GLE11_DEV,KEY=A0$,DOM=2000)
2000 REM " --- Read next line item"
2010 LET K$=KEY(GLE11_DEV,END=3000)
2020 IF POS(A0$=K$)<>1 THEN GOTO 3000
2030 READ (GLE11_DEV)IOL=GLE11A
2100 REM " --- Retrieve GLM-01 Account Master"
2110 LET C0$(1)=N0$+B1$,C1$(1)="(Not On File)"
2120 FIND (GLM01_DEV,KEY=C0$,DOM=2130)IOL=GLM01A
2400 REM " --- Format output fields"
2410 LET DEBIT$="",CREDIT$="",UNITS$=""
2420 IF B[0]<0 THEN LET CREDIT$=STR(ABS(B[0]):M1$)
2430 IF B[0]>0 THEN LET DEBIT$=STR(B[0]:M1$)
2440 IF P4$(3,1)="Y" THEN IF B[1]<>0 THEN LET UNITS$=STR(B[1]:M2$)
2500 REM " --- Print detail line"
2510 IF L+1>L9 THEN GOSUB 5000
2550 PRINT (7)@(O[0]),B0$(K8+1,3),@(O[1]),FNF$(B1$(1,P[3]),M0$),@(O[2]),C1$(1,35),@(O[3]),B2$,@(O[4]),DEBIT$,@(O[5]),CREDIT$,@(O[6]),UNITS$
2560 LET L=L+1
2600 REM " --- Accumulate totals"
2610 LET X=0,ENTRY[2]=ENTRY[2]+B[1]
2620 IF B[0]<0 THEN LET X=1
2630 LET ENTRY[X]=ENTRY[X]+B[0]
2800 REM " --- Generate summary record"
2810 LET ACCOUNT$=B1$(1,P[4]),GLDATE$=A1$(31,3),AMOUNT=B[0],UNITS=B[1]
2820 LET REF1$=A0$(5,10),REF2$=B0$(K8+1,3),MEMO$=B2$
2825 IF A1$(37,2)<>"  " THEN LET REF3$="Cycle "+A1$(37,2) ELSE LET REF3$=""
2830 GOSUB GLRECAP
2840 IF FNP$(A1$(34,3))="" THEN GOTO 2900
2850 LET ACCOUNT$=B1$(1,P[4]),GLDATE$=A1$(34,3),REF3$="Reversal",AMOUNT=-B[0],UNITS=-B[1]
2860 GOSUB GLRECAP
2900 REM " --- Loop back for next line item"
2990 GOTO 2000
3000 REM " --- Done with line items"
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4005 LET DONE$="Y"
4020 GOSUB 6000
4030 LET BALANCE=TOTAL[0]+TOTAL[1],WARNING$="",UNITS$=""
4040 IF BALANCE<>0 THEN LET WARNING$="Register Is Out Of Balance ---->"
4045 IF P4$(3,1)="Y" THEN LET UNITS$=STR(TOTAL[2]:M2$)
4047 IF L+2>L9 THEN GOSUB 5000
4050 PRINT (7)@(O[4]),J1$,@(O[5]),J1$,@(O[6]),J2$
4060 PRINT (7)WARNING$,@(O[4]-20),"Total For Register",@(O[4]),TOTAL[0]:M1$,@(O[5]),ABS(TOTAL[1]):M1$,@(O[6]),UNITS$
4900 REM " --- Run update overlay"
4910 LET PAGE=H,WIDTH=H0,UPDATE$=PROGRAM$,JH$=""
4980 PRINT @(0,10),'CE',
4990 RUN "GLR.XA"
5000 REM " --- Report heading"
5010 LET L=HEADINGS+3
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5050 PRINT (7)"Journal",@(26),"Transaction",@(40),"Date",@(48),"Reversal",@(O[3]),"Memo",@(O[4]-6),"Cycle",@(O[4]+M1-7),"Debits",@(O[5]+M1-8),"Credits",@(H6),UNITHDR$
5060 PRINT (7)""
5090 RETURN 
5200 REM " --- Subheading"
5210 IF L+4>L9 THEN GOSUB 5000
5220 PRINT (7)""
5230 PRINT (7)@(O[0]),"Seq",@(O[1]),"Account",@(O[2]),"Description"
5250 LET L=L+2
5290 RETURN 
6000 REM " --- Transaction Break"
6010 IF T0$="" THEN GOTO 6100
6020 LET BALANCE=ENTRY[0]+ENTRY[1],REVERSAL$="",UNITS$="",WARNING$=""
6030 IF BALANCE<>0 THEN LET WARNING$="Entry Is Out Of Balance ---->"
6035 IF P4$(3,1)="Y" THEN LET UNITS$=STR(ENTRY[2]:M2$)
6040 FOR X=0 TO 2
6045 LET TOTAL[X]=TOTAL[X]+ENTRY[X]
6050 NEXT X
6055 IF L+3>L9 THEN GOSUB 5000
6060 PRINT (7)@(O[4]),J1$,@(O[5]),J1$,@(O[6]),J2$
6070 PRINT (7)WARNING$,@(O[4]-17),"Total For Entry",@(O[4]),ENTRY[0]:M1$,@(O[5]),ABS(ENTRY[1]):M1$,@(O[6]),UNITS$
6080 PRINT (7)""
6090 LET L=L+3
6100 REM 
6105 DIM ENTRY[2]
6110 IF DONE$="Y" THEN GOTO 6390
6120 LET D0$(1)=N0$+A0$(3,2),D0$(5)="(Not On File)",MISSING=0
6130 FIND (GLM03_DEV,KEY=D0$(1,4),DOM=6145)IOL=GLM03A
6140 GOTO 6150
6145 LET ERRORS=ERRORS+1,MISSING=MISSING+1
6150 IF FNP$(A1$(34,3))<>"" THEN LET REVERSAL$=FNB$(A1$(34,3))
6160 IF L+6>L9 THEN GOSUB 5000
6165 PRINT (7)D0$(3,2)," ",D0$(5,20),@(26),A0$(5,10),@(38),FNB$(A1$(31,3)),@(48),REVERSAL$,@(O[3]),A1$(1,DW),@(O[4]-6),A1$(37,2)
6170 LET T0$=A0$(5,10),L=L+1
6200 REM " --- Update global variable"
6210 IF GL$<>"Y" THEN GOTO 6380
6220 IF MISSING>0 THEN GOTO 6380
6230 DIM GLCONTROL$(640)
6240 LET GLCONTROL$(1)=STBL("!GLCONTROL",ERR=6380)
6250 LET GLCONTROL$(18,2)=D0$(3,2),GLCONTROL$(46,2)=D0$(26,2)
6260 IF JE$="Y" THEN LET GLCONTROL$(45,1)=JE$
6270 LET GLCONTROL$(1)=STBL("!GLCONTROL",GLCONTROL$)
6300 REM " --- Check dates"
6310 LET GLDATE$=A1$(31,3)
6320 CALL "GLC.EA",GLDATE$,VERIFY$,PERIOD$,YEAR$,GLSTATUS
6330 IF GLSTATUS>0 THEN CALL "GLC.GA",7,GLDATE$,GLSTATUS,L,ERRORS,STATUS
6340 IF FNP$(A1$(34,3))="" THEN GOTO 6380
6350 LET GLDATE$=A1$(34,3)
6360 CALL "GLC.EA",GLDATE$,VERIFY$,PERIOD$,YEAR$,GLSTATUS
6370 IF GLSTATUS>0 THEN CALL "GLC.GA",7,GLDATE$,GLSTATUS,L,ERRORS,STATUS
6390 RETURN 
6900 REM " --- Standard G/L Recap Routine"
6910 GLRECAP: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLW11_DEV,GLT05_DEV,ACCOUNT$,GLDATE$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return To Menu"
9950 RUN "SYS.AA"
9999 END

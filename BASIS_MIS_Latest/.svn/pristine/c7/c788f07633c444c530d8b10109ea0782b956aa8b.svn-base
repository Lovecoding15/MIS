0010 REM "SYX - Copy System From Firm to Firm"
0020 REM "Program SYX.IA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=6
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="SYM-03"
0130 LET FILES$[3]="SYM-04",FILES$[4]="SYM-06"
0140 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SYM03_DEV=CHANNELS[2]
0170 LET SYM04_DEV=CHANNELS[3],SYM06_DEV=CHANNELS[4]
0200 REM " --- IOLIST's"
0210 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0220 SYM04A: IOLIST B0$(1),B1$(1)
0230 SYM06A: IOLIST C0$(1),C1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="SY"
0500 REM " --- Initializations"
0510 DIM A0$(6),A1$(77),A2$(30),A[2],B0$(2),B1$(64),C0$(2),C1$(40),MESSAGE$[2]
0550 LET SOURCE$=N0$,TARGET$="",M1$="###,##0",L0=9,L9=21
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,6,0
0720 CALL "SYC.WC",1,0,80,0,0,8,0
0750 PRINT 'SB',@(3,3),"System:",@(0,4),"From Firm:",@(2,5),"To Firm:",@(0,7),"File",@(8),"Description",@(40),"Records",@(51),"Copied"
1000 REM " --- System"
1010 PRINT 'CF',
1020 LET V0$="S",V1$="C",V2$="",V0=2,V1=11,V2=3,V2$=KEY(SYM04_DEV,END=1030)
1030 LET V3$="",V4$="Enter A Valid System ID (<F3>=Lookup)",I0=0
1040 GOSUB 7000
1050 ON V3 GOTO 1070,1000,1000,1060,9900
1060 CALL "SYC.LM",V$
1070 LET B0$(1)=V$
1080 FIND (SYM04_DEV,KEY=B0$,DOM=1000)IOL=SYM04A
1090 PRINT @(V1,V2),B0$," ",B1$(1,35),'CL',
1100 REM " --- System not installed"
1110 IF B1$(39,1)="Y" THEN GOTO 1150
1120 LET MESSAGE$[0]=FNP$(B1$(1,35))+" Is Not Installed (<Enter>=Continue)"
1130 CALL "SYC.XA",2,MESSAGE$[ALL],0,-1,-1,V$,V3
1140 IF V$<>"SPECIAL" THEN GOTO 1000
1150 IF B1$(36,1)="Y" THEN GOTO 3900
1160 LET MESSAGE$[0]=FNP$(B1$(1,35))+" Is Not Multi-Firm (<Enter>=Continue)"
1170 CALL "SYC.XA",2,MESSAGE$[ALL],0,-1,-1,V$,V3
1180 IF V$<>"SPECIAL" THEN GOTO 1000
1190 GOTO 3900
1200 REM " --- From Firm"
1210 LET V0$="Z",V1$="C",V2$=SOURCE$,V0=2,V1=11,V2=4
1220 LET V4$="Enter A Valid Firm ID (<F3>=Lookup)"
1230 GOSUB 7000
1240 ON V3 GOTO 1260,1200,3900,1250,9900
1250 CALL "SYC.LX",V$
1260 LET SOURCE$=V$,C0$(1)=SOURCE$
1270 FIND (SYM06_DEV,KEY=SOURCE$,DOM=1200)IOL=SYM06A
1280 PRINT @(V1,V2),SOURCE$," ",C1$,'CL',
1290 GOTO 3900
1400 REM " --- To Firm"
1410 LET V0$="Z",V1$="C",V2$=TARGET$,V0=2,V1=11,V2=5
1420 LET V4$="Enter A Valid Firm ID (<F3>=Lookup)"
1430 GOSUB 7000
1440 ON V3 GOTO 1460,1400,3900,1450,9900
1450 CALL "SYC.LX",V$
1460 IF SOURCE$=V$ THEN GOTO 1400
1465 LET TARGET$=V$,C0$(1)=TARGET$
1470 FIND (SYM06_DEV,KEY=TARGET$,DOM=1400)IOL=SYM06A
1480 PRINT @(V1,V2),TARGET$," ",C1$,'CL',
1490 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,4000
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",1,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4030 ON POS(V$="YN") GOTO 4000,4050,1000
4100 REM " --- Copy records"
4110 LET L=L0
4190 READ (SYM03_DEV,KEY=B0$,DOM=4200)
4200 REM " --- Top of copy loop"
4210 LET K$=KEY(SYM03_DEV,END=4900)
4220 IF POS(B0$=K$)<>1 THEN GOTO 4900
4230 READ (SYM03_DEV)IOL=SYM03A
4240 IF L<=L9 THEN GOTO 4280
4250 PRINT @(0,L0),'LD',
4260 LET L=L9
4280 PRINT @(0,L),'CL',A0$,@(8),A1$(1,30),
4290 IF POS(A1$(37,1)="MDS")=0 THEN GOTO 4200
4300 REM " --- Open file"
4310 CLOSE (SOURCE,ERR=4320)
4320 CLOSE (TARGET,ERR=4330)
4330 LET FILES$[5]=A0$,FILES$[6]=A0$,OPTIONS$[5]="M",OPTIONS$[6]="M"
4340 CALL "SYC.DA",1,5,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
4345 IF STATUS THEN GOTO 4200
4350 LET SOURCE=CHANNELS[5],TARGET=CHANNELS[6]
4500 REM " --- Retrieve file information"
4510 CALL "SYC.JA",SOURCE,FILENAME$,PATH$,TYPE$,KEYLEN,RECORDS,RECLEN,NUMREC,STATUS
4520 PRINT @(40,L),NUMREC:M1$,
4600 REM " --- Perform copy"
4610 GOSUB 6100
4620 LET L=L+1
4690 GOTO 4200
4900 REM " --- Copy completed"
4910 LET V4$=FNP$(B1$(1,35))+" Copied From Firm "+SOURCE$+" (<Enter>=Continue):"
4920 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
4930 GOSUB 7000
4990 GOTO 1000
6100 REM " --- Copy a file"
6170 LET NUMBER=0,TYPE$="D"
6180 IF RECLEN=0 THEN LET TYPE$="S"
6190 READ (SOURCE,KEY=SOURCE$,DOM=6200)
6200 REM " --- Copy loop"
6210 LET K$=KEY(SOURCE,END=6900)
6220 IF POS(SOURCE$=K$)<>1 THEN GOTO 6900
6230 LET NUMBER=NUMBER+1
6240 IF MOD(NUMBER,10)=0 THEN PRINT @(50,L),NUMBER:M1$,
6250 ON POS(TYPE$="DS") GOTO 6900,6300,6400
6300 REM " --- Direct File"
6310 READ RECORD(SOURCE)R$
6320 LET K$(1,2)=TARGET$,R$(1,2)=TARGET$
6330 WRITE RECORD(TARGET,KEY=K$)R$
6340 READ (SOURCE,KEY=SOURCE$+K$(3))
6390 GOTO 6200
6400 REM " --- Sort File"
6410 READ (SOURCE)
6420 LET K$(1,2)=TARGET$
6430 WRITE (TARGET,KEY=K$)
6440 READ (SOURCE,KEY=SOURCE$+K$(3))
6490 GOTO 6200
6900 REM " --- Copy completed"
6910 PRINT @(50,L),NUMBER:M1$,
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

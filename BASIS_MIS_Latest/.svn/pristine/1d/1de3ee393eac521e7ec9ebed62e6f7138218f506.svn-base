0010 REM "POC - Requisition/Purchase Order Lookup By PO/Req Number"
0020 REM "Program POC.LB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "11Jul96 JAL: PO total being incorrectly calculated (2180)"
0032 REM "05Feb97 Caj: By Item and Whse lookups weren't getting all POs when no criterion/start point was entered (1940,2140)
0033 REM "13Feb97 Caj: Possibly using the wrong XREF file (653,1030,1040); PO lookups weren't always starting at correct point in XREF (1080); lookups other than 'By receiver' weren't always using the correct key (1850-1880)
0040 REM 
0050 REM "TYPE$     : R=Requisition/P=Purchase Order/X=PO's with Req (input)"
0060 REM "SELECTION$: Vendor And Req/Purchase Order Number (returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER TYPE$,SELECTION$
0100 REM " --- Open Files"
0105 LET FILES=20
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="SYS-01",FILES$[2]="SYM-06",FILES$[3]="APM-01"
0120 LET FILES$[4]="IVM-10",FILES$[5]="POE-01",FILES$[6]="POE-02"
0125 LET FILES$[7]="POE-03",FILES$[8]="POE-04",FILES$[9]="POE-11"
0130 LET FILES$[10]="POE-12",FILES$[11]="POE-13",FILES$[12]="POE-14"
0135 LET FILES$[13]="POE-31",FILES$[14]="POE-32",FILES$[15]="POE-33"
0140 LET FILES$[16]="POE-34",FILES$[17]="POE-52",FILES$[18]="POE-53"
0145 LET FILES$[19]="POE-54",FILES$[20]="POM-02"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SYM06_DEV=CHANNELS[2],APM01_DEV=CHANNELS[3]
0165 LET IVM10_DEV=CHANNELS[4],POE01_DEV=CHANNELS[5],POE02_DEV=CHANNELS[6]
0170 LET POE03_DEV=CHANNELS[7],POE04_DEV=CHANNELS[8],POE11_DEV=CHANNELS[9]
0175 LET POE12_DEV=CHANNELS[10],POE13_DEV=CHANNELS[11],POE14_DEV=CHANNELS[12]
0180 LET POE31_DEV=CHANNELS[13],POE32_DEV=CHANNELS[14],POE33_DEV=CHANNELS[15]
0185 LET POE34_DEV=CHANNELS[16],POE52_DEV=CHANNELS[17],POE53_DEV=CHANNELS[18]
0190 LET POE54_DEV=CHANNELS[19],POM02_DEV=CHANNELS[20]
0200 REM " --- IOLIST's"
0210 POE01A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0220 APM01A: IOLIST B0$(1),B1$(1)
0230 POM02A: IOLIST Y0$(1),Y1$(1)
0240 IVM10C: IOLIST X0$(1)
0250 SYM06A: IOLIST *,COMPANY$
0260 APS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,X$,M1$,M2$,M3$
0310 POE11A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0400 REM " --- Parameters"
0405 DIM I[1],P[1]
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0425 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),SPEEDSEARCH$="N",M7$="(###)-###-####",KEYLEN=15
0440 LET X$=STBL("!SPEEDSEARCH",ERR=0450),N0$=X$(1,2),SPEEDSEARCH$="Y"
0450 FIND (SYM06_DEV,KEY=N0$,DOM=9800)IOL=SYM06A
0460 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0470 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0480 LET P[0]=NUM(P2$(1,2)),I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1))
0490 IF POS(TYPE$="QC") THEN LET KEYLEN=22
0500 REM " --- Initializations"
0505 PRECISION I[1]
0510 DIM A0$(KEYLEN),A1$(160),A[8],W0$(KEYLEN+3),X0$(160),Y0$(4),Y1$(32),O[5]
0520 DIM B0$(8),B1$(30),W0$(18),W1$(48),W2$(32),W3$(22),W4$(40),W[12]
0540 LET WIDTH=80,HEIGHT=12,WIN_X=0,WIN_Y=5,MAX_ROW=HEIGHT-5,M0=LEN(M0$)
0545 LET SWIDTH=WIDTH,SHEIGHT=4,SWIN_X=WIN_X,SWIN_Y=WIN_Y+HEIGHT
0550 LET OPTIONS=0,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3,M1=LEN(M1$),SELECTION$=""
0555 LET LABEL$="Purchase Order",N$="PO #",D$="Ordered",PGM$=PGM(-2)
0560 LET HEADER_DEV=POE02_DEV,DETAIL_DEV=POE12_DEV,XREF_DEV=POE32_DEV
0565 IF M1<12 THEN CALL "SYC.RM",M1$,M1,12,15
0570 LET O[5]=WIDTH-1-M1,O[0]=2,O[1]=10,O[2]=19,O[3]=O[2]+M0+1
0575 LET O[4]=O[3]+25,DW=O[5]-O[4]-4
0600 REM " --- Initializations based on lookup type"
0605 ON POS(TYPE$="XPRQC") GOTO 9900,0650,0650,0610,0625,0640
0610 LET LABEL$="Requisition",N$="Req #",D$="Req Date",PGM$="POC.LS"
0615 LET HEADER_DEV=POE01_DEV,DETAIL_DEV=POE11_DEV,XREF_DEV=POE31_DEV
0620 GOTO 0650
0625 LET LABEL$="Quality Assurance Receiver"
0627 LET N$="Rec #",D$="Rec Date",PGM$="POC.LZ"
0630 LET HEADER_DEV=POE03_DEV,DETAIL_DEV=POE13_DEV,XREF_DEV=POE33_DEV
0635 GOTO 0650
0640 LET LABEL$="Purchase Order Receiver"
0643 LET N$="Rec #",D$="Rec Date",PGM$="POC.LZ"
0645 LET HEADER_DEV=POE04_DEV,DETAIL_DEV=POE14_DEV,XREF_DEV=POE34_DEV
0650 REM " --- Window arrays"
0653 LET ORIGXREF_DEV=XREF_DEV; REM "Cuz XREF_DEV may be changed later
0655 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0660 LET HEADING$(O[0])=N$,HEADING$(O[1])=D$,HEADING$(O[2])="Vendor"
0665 LET HEADING$(O[4])="Warehouse"
0670 IF TYPE$="X" THEN LET HEADING$(O[4])="Wh Req #"
0675 IF POS(TYPE$="QC") THEN LET HEADING$(O[4])="Wh PO #"
0680 LET HEADING$(WIDTH-13)="Open Amount"
0685 LET TITLE$=LABEL$+" Lookup",STITLE$=LABEL$+" Detail"
0690 IF TYPE$="X" THEN LET TITLE$="Purchase Order/Requisition Lookup"
0695 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Which search option?"
0810 DIM STRINGS$[10],NUMBERS[10]
0820 LET STRINGS$[0]=PGM$,STRINGS$[1]=M0$,NUMBERS[0]=MAXROW
0830 LET NUMBERS[1]=OWIN_X,NUMBERS[2]=OWIN_Y,NUMBERS[3]=I[0]
0840 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
0850 CALL "SYC.SM",STRINGS$[ALL],NUMBERS[ALL],FKEY
0860 IF FKEY THEN GOTO 9900
0870 LET PREFIX$=STRINGS$[2],START$=STRINGS$[3],METHOD$=STRINGS$[4]
0880 LET V3$=STRINGS$[5],V0=NUMBERS[4]
0900 REM " --- Detail Window"
0910 CALL "SYC.WA",0,SWIDTH,SHEIGHT,SWIN_X,SWIN_Y,STITLE$,SNAME$
0920 CALL "SYC.WB",NAME$,0
0940 IF SPEEDSEARCH$<>"Y" THEN GOTO 1000
0950 REM " --- Display SpeedSearch Company"
0955 DIM X$(WIDTH+SWIDTH)
0960 LET X$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
0965 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
0970 CALL "SYC.WA",0,WIDTH+SWIDTH,3,WIN_X,WIN_Y+HEIGHT+SHEIGHT,"",CNAME$
0975 PRINT @(0,0),'BR',X$,'ER',
0980 CALL "SYC.WB",NAME$,0
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,SHEIGHT-2]
1020 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$=""
1030 IF TYPE$="Q" THEN IF PREFIX$="" THEN LET XREF_DEV=POE53_DEV ELSE LET XREF_DEV=ORIGXREF_DEV
1040 IF TYPE$="C" THEN IF PREFIX$="" THEN LET XREF_DEV=POE54_DEV ELSE LET XREF_DEV=ORIGXREF_DEV
1050 IF PREFIX$="" THEN GOTO 1200
1060 IF POS(PREFIX$="*IW") THEN GOTO 1250
1070 IF PREFIX$="V" THEN GOTO 1300
1080 IF PREFIX$="P" THEN GOTO 1150
1100 REM " --- PO's by Requisition"
1110 READ (POE52_DEV,KEY=N0$+START$,DOM=1400)
1140 GOTO 1400
1150 REM " --- PO's by Receiver"
1160 READ (XREF_DEV,KEY=N0$+START$,DOM=1400)
1190 GOTO 1400
1200 REM " --- Req/PO Number"
1210 LET A0$(1)=N0$+START$
1220 READ (XREF_DEV,KEY=N0$+START$,DOM=1400)
1240 GOTO 1400
1250 REM " --- Keyword/Item/Warehouse"
1260 IF PREFIX$="*" THEN LET START$=FNU$(START$)
1270 READ (XREF_DEV,KEY=N0$,DOM=1400)
1290 GOTO 1400
1300 REM " --- Vendor"
1310 READ (HEADER_DEV,KEY=N0$+START$,DOM=1400)
1390 GOTO 1400
1400 REM " --- Read next record"
1410 IF PREFIX$="V" THEN GOTO 1600
1430 IF PREFIX$<>"R" THEN GOTO 1800
1500 REM " --- Next POE-52 sort record"
1510 LET K$=KEY(POE52_DEV,END=3000)
1520 IF POS(N0$+START$=K$)<>1 THEN GOTO 3000
1535 READ (POE52_DEV)
1540 LET K$=N0$+K$(10)
1545 GOTO 1900
1600 REM " --- Next header key"
1610 LET K$=KEY(HEADER_DEV,END=3000)
1620 IF POS(N0$=K$)<>1 THEN GOTO 3000
1690 GOTO 1900
1800 REM " --- Next xref key"
1810 LET K$=KEY(XREF_DEV,END=3000)
1820 IF POS(N0$=K$)<>1 THEN GOTO 3000
1825 READ (XREF_DEV)
1830 IF POS(TYPE$="QC") THEN GOTO 1850
1835 LET K$=K$(1,2)+K$(10)+K$(3,7)
1840 GOTO 1900
1850 IF PREFIX$<>"" THEN GOTO 1880
1860 LET K$=N0$+K$(10,6)+K$(3,7)+K$(16)
1870 GOTO 1900
1880 LET K$=N0$+K$(10,6)+K$(16,7)+K$(3,7)
1900 REM " --- Read header record"
1905 LET LOCKED=0
1910 READ (HEADER_DEV,KEY=K$,ERR=1950)IOL=POE01A
1920 IF TYPE$="X" THEN IF FNP$(A1$(112,7))="" THEN GOTO 2900
1930 IF POS(PREFIX$="*W")=0 THEN GOTO 2000
1935 IF PREFIX$="*" THEN IF POS(START$=FNU$(A1$))=0 THEN GOTO 2900
1940 IF PREFIX$="W" THEN IF START$<>"" AND A1$(1,2)<>START$ THEN GOTO 2900
1945 GOTO 2000
1950 REM " --- Error reading header record"
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1980 LET LOCKED=1
2000 REM " --- Position detail file to accumulate open amount"
2010 LET AMOUNT=0,FOUND=0,Y0$(1)="",Y1$(1)=""
2090 READ (DETAIL_DEV,KEY=A0$,DOM=2100)
2100 REM " --- Read next detail"
2110 LET K$=KEY(DETAIL_DEV,END=2300)
2120 IF POS(A0$=K$)<>1 THEN GOTO 2300
2130 READ (DETAIL_DEV)IOL=POE11A
2140 IF PREFIX$="I" THEN IF START$="" OR FNP$(W3$(3))=START$ THEN LET FOUND=1
2150 IF W0$(1,2)+W1$(1,2)=Y0$ THEN GOTO 2160
2155 FIND (POM02_DEV,KEY=W0$(1,2)+W1$(1,2),DOM=2100)IOL=POM02A
2160 IF POS(Y1$(21,1)="MV") THEN GOTO 2100
2170 LET COST=W[1],QTY=W[2]
2180 IF POS(TYPE$="PX") THEN LET QTY=W[3]-W[7]
2185 IF POS(TYPE$="QC") THEN LET QTY=W[3]-W[4]-W[7]
2190 IF Y1$(21,1)="O" THEN LET QTY=1
2200 PRECISION 2
2210 LET AMOUNT=AMOUNT+(COST*QTY)
2220 PRECISION I[1]
2290 GOTO 2100
2300 REM " --- Display page?"
2310 IF PREFIX$="I" THEN IF FOUND=0 THEN GOTO 2900
2320 IF L<MAX_ROW THEN GOTO 2400
2330 LET XMODE=5
2340 GOSUB 6200
2350 IF FKEY=1 THEN GOTO 0800
2360 IF FKEY=4 THEN GOTO 9900
2370 IF SELECTION$<>"" THEN GOTO 4000
2400 REM " --- Lookup window record"
2410 DIM G$(WIDTH-2)
2415 LET G$(O[0])=A0$(9,7),G$(O[1])="Record In Use"
2425 IF LOCKED THEN GOTO 2490
2430 LET B0$(1)=N0$+A0$(3,6),B1$(1)="(Not On File)"
2435 FIND (APM01_DEV,KEY=B0$,DOM=2440)IOL=APM01A
2440 IF X0$(4,2)=A1$(1,2) THEN GOTO 2470
2450 LET X0$(1)=N0$+"C"+A1$(1,2)+"(Not On File)"
2460 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=2470)IOL=IVM10C
2470 LET G$(O[1])=FNB6$(A1$(5,6)),G$(O[2])=FNF$(B0$(3,P[0]),M0$)+" "+B1$(1,24)
2475 LET G$(O[4])=X0$(4,2)+" "+X0$(6,DW)
2480 IF POS(TYPE$="QC") THEN LET G$(O[4]+3)=A0$(16,7)
2485 IF TYPE$="X" THEN LET G$(O[4]+3)=A1$(112,7)
2490 LET G$(O[5])=STR(AMOUNT:M1$),LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=A0$
2600 REM " --- Detail window record"
2610 LET G$(1)="{Required}",P=21
2613 IF POS(TYPE$="QC") THEN LET P=18
2615 IF FNP$(A1$(23,6))<>"" THEN LET G$(11)=FNB6$(A1$(23,6))
2620 LET G$(P+1)="{Not Before}"
2625 IF FNP$(A1$(17,6))<>"" THEN LET G$(P+14)=FNB6$(A1$(17,6))
2630 LET G$(44)="{Freight Terms}"+A1$(44,15),MORE$[PAGE,X1,1]=G$
2635 LET G$(1)="{Promised}"
2640 IF FNP$(A1$(11,6))<>"" THEN LET G$(11)=FNB6$(A1$(11,6))
2645 IF TYPE$="P" THEN LET G$(P)="{Requisition}"+A1$(112,7)
2650 IF POS(TYPE$="QC") THEN LET G$(P+3)="{Ship Via}"+A1$(59,15)
2660 LET G$(45)="{Acknowledged}"+A1$(74,20),MORE$[PAGE,X1,2]=G$
2680 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),LLIST$[PAGE,X1,5]=SNAME$
2690 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2910 IF LOCKED=0 THEN GOTO 1400
2920 LET LOCKED=0
2950 READ (HEADER_DEV,KEY=K$(1,14)+$FF$,DOM=1400,END=3000)
2990 GOTO 1400
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3035 LET MESSAGE$[0]="No "+LABEL$+"s Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page"
3510 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
8000 REM " --- Functions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNU$(Q$)=CVS(Q$,4)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 FOR X=1 TO FILES
9915 CLOSE (CHANNELS[X],ERR=9916)
9920 NEXT X
9925 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9930 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
9935 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9940 IF SELECTION$="END" THEN LET SELECTION$=""
9945 IF SELECTION$="PRIOR" AND SPEEDSEARCH$="N" THEN LET SELECTION$=""
9950 EXIT 
9999 END

0010 REM "IVR - Inventory Shortage & Reorder Report"
0020 REM "Program IVR.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=8
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0130 LET FILES$[4]="IVM-03",FILES$[5]="IVM-10",FILES$[6]="IVM-12"
0135 LET FILES$[7]="IVM-14",FILES$[8]="IVM-15"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0180 LET IVM03_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],IVM12_DEV=CHANNELS[6]
0190 LET IVM14_DEV=CHANNELS[7],IVM15_DEV=CHANNELS[8]
0200 REM " --- IOLists"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$,A5$,A6$,A[ALL]
0230 IVM10A: IOLIST X0$(1)
0240 IVM10C: IOLIST X0$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0275 IVS01C: IOLIST X$,ABCP[ALL],FACTORS[ALL]
0280 GLS01A: IOLIST G0$,G1$,G2$,G3$
0400 REM " --- Parameters"
0405 DIM P[7],G[1],ABCP[25],FACTORS[25]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="IV"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0425 FIND (SYS01_DEV,KEY=N0$+N2$+"02",DOM=9800)IOL=IVS01C
0430 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0435 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1)),G[0]=NUM(G2$(1,2))
0440 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0445 LET P[6]=NUM(I4$(1,2)),P[7]=FNYY_YEAR(I4$(3,2))
0450 LET PERIOD$=I4$(1,2),YEAR$=I4$(3,2),HEADINGS=7
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM FILES[15],PARAMS$[3],OPTIONS$[7]
0520 DIM HEADINGS$[HEADINGS],O[10],CHANNS[3],PERIOD$[1]
0530 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A[12]
0540 LET FILES[1]=IVM01_DEV,FILES[10]=IVM10_DEV,FILES[12]=IVM12_DEV
0545 LET FILES[14]=IVM14_DEV,FILES[15]=IVM15_DEV,REPORT$="R"
0550 LET PARAMS$[0]=N0$,PARAMS$[1]=I1$,PARAMS$[2]=I2$,PARAMS$[3]=I3$
0560 LET WIDTH=132,PAGE=0,L9=59,L=L9+1,PRODUCT=0,WAREHOUSE=0,REPORT=0
0565 LET CHANNS[2]=IVM12_DEV,CHANNS[3]=IVM03_DEV,HEADINGS$[0]=F4$
0570 LET PERIOD$[0]=YEAR$+PERIOD$,PERIOD$[1]=YEAR$+PERIOD$
0575 LET WHEN$=F0$(7,3),CLOCK$="",H9$="",DONE=0
0580 LET DAY_OF_MTH=NUM(F0$(22,2)),NUMBER=0
0600 REM " --- Resize masks and set print positions"
0610 CALL "SYC.RM",M2$,M2,10,15
0650 LET O[1]=P[0]+1,O[2]=O[1]+12,O[10]=WIDTH-M2-1,O[9]=O[10]-M2-1
0660 LET O[8]=O[9]-M2-1,O[7]=O[8]-M2-1,O[6]=O[7]-M2,O[5]=O[6]-M2
0670 LET O[4]=O[5]-M2,O[3]=O[4]-M2,O[2]=O[3]-2
0700 REM " --- Background"
0710 PRINT 'SB',@(19,3),"Reorder or Shortage:",'SF',@(40),REPORT$,
0720 CALL "SYC.WC",1,0,80,0,0,4,0
0730 CALL "IVC.IB","BDL","",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0800 REM " --- Reorder or Shortage Report?"
0810 LET V4$="R = Reorder Report, S = Shortage Report",V0=1,V1=40,V2=3,I0=0
0820 LET V0$="S",V1$="C",V2$=REPORT$,V3$="RS",V0=1,V1=40,V2=3,I0=0
0830 GOSUB 7000
0835 ON V3 GOTO 0840,0800,0800,0800,0900
0840 LET REPORT$=V$
0850 REM " --- Process input"
0860 CALL "IVC.IB","IL","",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0870 ON V3 GOTO 0900,0850,0800,0850,0900
0900 REM " --- Correct?"
0905 CALL "IVC.IB","DL","",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0910 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
0915 IF V3=4 THEN GOTO 9900
0920 ON POS(V$="YN") GOTO 0900,0925,0800
0925 LET LEVEL$=OPTIONS$[0],BEGWHSE$=OPTIONS$[1],ENDWHSE$=OPTIONS$[2]
0930 LET BEGPROD$=OPTIONS$[3],ENDPROD$=OPTIONS$[4],BEGITEM$=OPTIONS$[5]
0935 LET ENDITEM$=OPTIONS$[6],TYPE$=OPTIONS$[7]
0940 GOSUB 5800
0950 REM " --- Position files"
0960 CALL "SYC.GA",7,1,"","",STATUS
0965 IF STATUS THEN GOTO 9900
0970 CALL "SYC.NB","Printing",7+P[0],COLUMN
0975 LET FILES[2]=IVM02_DEV,FILES[3]=IVM03_DEV
0980 LET FIRST$=N0$+"A",K$=FIRST$+BEGPROD$,P=0
0985 IF LEVEL$="W" THEN LET FIRST$=N0$+"C",K$=FIRST$+BEGWHSE$,P=2
0990 READ (IVM03_DEV,KEY=K$,DOM=1000)
1000 REM " --- Get next sort record"
1010 LET K$=KEY(IVM03_DEV,END=4000)
1020 IF POS(FIRST$=K$)<>1 THEN GOTO 4000
1030 LET WHSE$=K$(4,2),PRODUCT$=K$(4+P,3),ITEM$=K$(7+P)
1040 IF LEVEL$="W" THEN GOTO 1200
1100 REM " --- By Item: Valid Product/Item?"
1110 IF ENDPROD$<>"" THEN IF PRODUCT$>ENDPROD$ THEN GOTO 4000
1120 IF BEGITEM$<>"" THEN IF ITEM$<BEGITEM$ THEN GOTO 1300
1130 IF ENDITEM$<>"" THEN IF ITEM$>ENDITEM$ THEN GOTO 1350
1140 LET X$=PRODUCT$+" "+ITEM$(1,P[0])
1190 GOTO 1500
1200 REM " --- By Warehouse: Valid Warehouse/Product/Item?"
1210 IF ENDWHSE$<>"" THEN IF WHSE$>ENDWHSE$ THEN GOTO 4000
1220 IF BEGPROD$<>"" THEN IF PRODUCT$<BEGPROD$ THEN GOTO 1300
1230 IF ENDPROD$<>"" THEN IF PRODUCT$>ENDPROD$ THEN GOTO 1350
1240 IF BEGITEM$<>"" THEN IF ITEM$<BEGITEM$ THEN GOTO 1400
1250 IF ENDITEM$<>"" THEN IF ITEM$>ENDITEM$ THEN GOTO 1450
1260 LET X$=WHSE$+" "+PRODUCT$+" "+ITEM$(1,P[0])
1290 GOTO 1500
1300 REM " --- Skip to beginning product for this warehouse'
1310 READ (IVM03_DEV,KEY=K$(1,5)+BEGPROD$,DOM=3900)
1340 GOTO 3900
1350 REM " --- Skip to next warehouse"
1360 READ (IVM03_DEV,KEY=K$(1,5)+$FF$,DOM=3900)
1390 GOTO 3900
1400 REM " --- Skip to beginning item"
1410 READ (IVM03_DEV,KEY=K$(1,6+P)+BEGITEM$(1,P[0]-1),DOM=3900)
1440 GOTO 3900
1450 REM " --- Skip to next product"
1460 READ (IVM03_DEV,KEY=K$(1,6+P)+$FF$,DOM=3900)
1490 GOTO 3900
1500 REM " --- Level breaks?"
1510 PRINT @(COLUMN,11),X$,
1520 IF LEVEL$="W" THEN IF WHSE$<>T0$ THEN GOSUB 6000
1530 IF PRODUCT$<>T1$ THEN GOSUB 6200
1540 READ (IVM03_DEV)
2000 REM " --- Retrieve item"
2010 DIM ITEM$[2],WHSE$[2],W[20]
2020 LET A0$(1)=N0$+ITEM$
2030 FIND (IVM01_DEV,KEY=A0$,DOM=3900)IOL=IVM01A
2035 IF LEVEL$="W" AND A2$(24,1)<>"W" THEN GOTO 3900
2040 IF LEVEL$="I" AND A2$(24,1)<>"I" THEN GOTO 3900
2045 IF TYPE$<>"" THEN IF A2$(53,3)<>TYPE$ THEN GOTO 3900
2050 LET ACTION$="A",ITEM$[0]=N0$,ITEM$[2]=ITEM$
2060 IF LEVEL$="W" THEN LET ITEM$[1]=WHSE$,ACTION$=""
2070 CALL "IVC.WA",ACTION$,FILES[ALL],ITEM$[ALL],WHSE$[ALL],W[ALL],VALUE,STATUS
2080 IF REPORT$="R" AND W[0]-W[2]>W[4] THEN GOTO 3900
2090 IF REPORT$="S" AND W[0]-W[2]>W[5] THEN GOTO 3900
2100 CALL "IVC.AU",LEVEL$,FILES[ALL],ITEM$[ALL],FACTORS[ALL],DAY_OF_MTH,PERIOD$+YEAR$,I4$(1,4),G[0],P[1],USAGE,ESTIMATED,FLAG$,STATUS
2105 IF FLAG$<>"" THEN LET USAGE=ESTIMATED
2110 LET AVG_USAGE=USAGE/G[0]
2120 IF LEVEL$="I" THEN LET OP_CODE$=A2$(27,1),EOQ_CODE$=A2$(26,1),SAFE_CODE$=A2$(28,1)
2130 IF LEVEL$="W" THEN LET OP_CODE$=WHSE$[1](58,1),EOQ_CODE$=WHSE$[1](57,1),SAFE_CODE$=WHSE$[1](59,1)
3000 REM " --- Print detail line"
3010 LET NEEDED=1
3020 IF H9$<>"" THEN LET NEEDED=NEEDED+2
3030 IF LEVEL$="W" THEN LET LAST_ISSUE$=FNL$(WHSE$[1](34,3),WHSE$[1](37,3)) ELSE LET LAST_ISSUE$=FNL$(A2$(35,3),A2$(38,3))
3040 LET X$=FNITEM$(A1$,P[3],P[4],P[5])
3045 IF L+NEEDED>L9 THEN GOSUB 5000
3050 IF H9$<>"" THEN GOSUB 5200
3055 IF LEVEL$="W" THEN PRINT (7)@(O[0]),A0$(3),@(O[1]),WHSE$[1](1,10),@(O[2]),WHSE$[1](56,1),@(O[3]),W[0]:M2$,@(O[4]),W[2]:M2$,@(O[5]),W[0]-W[2]:M2$,@(O[6]),W[1]:M2$,@(O[7]),W[4]:M2$,OP_CODE$,@(O[8]),W[6]:M2$,EOQ_CODE$,@(O[9]),W[5]:M2$,SAFE_CODE$,@(O[10]),AVG_USAGE:M2$,FLAG$
3060 IF LEVEL$="I" THEN PRINT (7)@(O[0]),A0$(3),@(O[2]),A2$(25,1),@(O[3]),W[0]:M2$,@(O[4]),W[2]:M2$,@(O[5]),W[0]-W[2]:M2$,@(O[6]),W[1]:M2$,@(O[7]),W[4]:M2$,OP_CODE$,@(O[8]),W[6]:M2$,EOQ_CODE$,@(O[9]),W[5]:M2$,SAFE_CODE$,@(O[10]),AVG_USAGE:M2$,FLAG$
3065 LET L=L+2,NUMBER=NUMBER+1
3090 PRINT (7)@(O[0]+3),X$
3900 REM " --- Loop back for next record"
3910 READ (IVM03_DEV,KEY=K$)
3990 GOTO 1000
4000 REM " --- All done"
4010 IF NUMBER>0 THEN GOTO 9900
4020 GOSUB 5000
4030 PRINT (7)""
4040 PRINT (7)"No Items/Warehouses Found"
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5090 RETURN 
5200 REM " --- Product subheading"
5210 IF L+2>L9 THEN GOSUB 5000
5220 PRINT (7)'LF',H9$
5240 LET L=L+2,H9$=""
5290 RETURN 
5800 REM " --- Initialize HEADINGS$"
5810 DIM H1$(WIDTH),H2$(WIDTH),H6$(WIDTH),H7$(WIDTH)
5812 IF REPORT$="R" THEN LET HEADINGS$[1]="Inventory Reorder Report" ELSE LET HEADINGS$[1]="Inventory Shortage Report"
5815 IF LEVEL$="I" THEN LET HEADINGS$[1]=HEADINGS$[1]+" By Item"
5820 IF LEVEL$="W" THEN LET HEADINGS$[1]=HEADINGS$[1]+" By Warehouse"
5830 LET X1$="First",X2$="First",X2=5
5835 IF BEGITEM$<>"" THEN LET X1$=BEGITEM$
5840 IF BEGPROD$<>"" THEN LET X2$=BEGPROD$,X2=3
5850 IF ENDPROD$="" THEN IF X2=3 THEN LET X2=4
5860 LET H1$(1)="From Item: "+X1$,H1$(WIDTH-X2-13)="From Product: "+X2$
5870 LET X1$="Last",X2$="Last",X3$="All Item Types"
5880 IF ENDITEM$<>"" THEN LET X1$=ENDITEM$
5890 IF ENDPROD$<>"" THEN LET X2$=ENDPROD$
5900 IF TYPE$<>"" THEN LET X3$="Item Type "+TYPE$
5910 LET H2$(1)="  To Item: "+X1$,H2$(FNC(X3$,WIDTH))=X3$
5920 LET H2$(WIDTH-X2-13)="  To Product: "+X2$
5925 LET H6$(O[2])="ABC",H6$(O[3]+M2-8)="Quantity",H6$(O[4]+M2-9)="Committed"
5930 LET H6$(O[5]+M2-8)="Quantity",H6$(O[6]+M2-8)="Quantity",H6$(O[7]+M2-5)="Order"
5935 LET H6$(O[9]+M2-6)="Safety",H6$(O[10]+M2-9)="Avg Usage"
5940 LET H7$(O[0]+1)="Item/Description"
5950 LET H7$(O[2])="Code",H7$(O[3]+M2-7)="On Hand",H7$(O[4]+M2-8)="Quantity"
5960 LET H7$(O[5]+M2-9)="Available",H7$(O[6]+M2-8)="On Order",H7$(O[7]+M2-5)="Point"
5975 LET H7$(O[8]+M2-3)="EOQ",H7$(O[9]+M2-5)="Stock",H7$(O[10]+M2-10)="Per Period"
5980 LET HEADINGS$[3]=H1$,HEADINGS$[4]=H2$,HEADINGS$[6]=H6$,HEADINGS$[7]=H7$
5990 RETURN 
6000 REM " --- Warehouse Break"
6010 IF T0$="" THEN GOTO 6030
6020 GOSUB 6200
6030 DIM X0$(32)
6040 LET T0$=WHSE$,X0$(6,24)="(Not on File)",L=L9+1
6050 FIND (IVM10_DEV,KEY=N0$+"C"+T0$,DOM=6060)IOL=IVM10C
6060 LET X$="Warehouse "+T0$+" "+FNP$(X0$(6,24))
6070 LET HEADINGS$[3]=H1$,HEADINGS$[3](FNC(X$,WIDTH),LEN(X$))=X$
6090 RETURN 
6200 REM " --- Product break"
6210 DIM X0$(32)
6220 LET T1$=PRODUCT$,X0$(7,20)="(Not on File)"
6230 FIND (IVM10_DEV,KEY=N0$+"A"+T1$,DOM=6240)IOL=IVM10A
6240 LET H9$="Product Type "+T1$+" "+X0$(7,20)
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8300 REM " --- Multi-Line Functions"
8310 DEF FNL$(Q1$,Q2$)
8320 IF CVS(Q1$,2)="" THEN LET Q1$="ZZZ"
8330 IF CVS(Q2$,2)="" THEN LET Q2$="ZZZ"
8340 LET Q$=Q1$
8350 IF Q2$>Q1$ THEN LET Q$=Q2$
8360 IF Q$="ZZZ" THEN LET Q$="   "
8370 RETURN Q$
8380 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

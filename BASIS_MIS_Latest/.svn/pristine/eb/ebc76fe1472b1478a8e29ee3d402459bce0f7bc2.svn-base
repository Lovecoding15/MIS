0010 REM "OPR - Customer/Job Report"
0020 REM "Program OPR.FB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files "
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS>0 THEN GOTO 9900
0200 REM " --- IOLISTS "
0210 ARM09A: IOLIST A0$,A1$,A[ALL]
0220 ARM01A: IOLIST B0$,B1$
0500 REM " --- Initialize Data "
0510 DIM HEADINGS$[2],A[1]
0550 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,CLOCK$="",WHEN$=F0$(7,3)
0560 LET H0=132,L9=55,L=L9+1,H=0
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",M0+11,COLUMN
1000 REM " --- Initial Read "
1010 READ (ARM09_DEV,KEY=N0$,DOM=1020)
1100 REM " --- Main Read "
1120 LET K$=KEY(ARM09_DEV,END=4000)
1130 IF POS(N0$=K$)<>1 THEN GOTO 4000
1150 READ (ARM09_DEV,END=4000)IOL=ARM09A
1200 REM " --- Control Breaks "
1220 IF P9$<>"" THEN IF A0$(3,6)<P9$ THEN GOTO 3900
1240 IF P8$<>"" THEN IF A0$(3,6)>P8$ THEN GOTO 3900
1400 REM " --- Break "
1420 IF A0$(3,6)<>T0$ THEN GOSUB 6000
2000 REM " --- Print "
2010 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$)," ",A0$(9,10),
2020 IF L>L9 THEN GOSUB 5200
2040 PRINT (7)@(32+M0),A0$(9,10),@(43+M0),A1$(1,30),@(74+M0),A0$(22,12),@(87+M0),FNB$(A0$(19,3)),@(98+M0),A[0]:M1$
2050 PRINT (7)@(43+M0),A1$(126,20),@(74+M0),FNB$(A0$(34,3)),@(87+M0),FNB$(A0$(37,3)),@(98+M0),A[1]:M1$
2060 LET L=L+2,T0=T0+1
3900 REM 
3990 GOTO 1100
4000 REM " --- End "
4010 IF L>L9 THEN GOSUB 5000
4020 PRINT (7)""
4030 PRINT (7)T0," Total Jobs"
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=0
5020 CALL "SYC.HA",7,HEADINGS$[ALL],2,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)"Customer",@(32+M0),"Job",@(43+M0),"Job Name/",@(74+M0),"Lien Number",@(87+M0),"Lien Date",@(98+M0+M1-12),"Lien Amount"
5050 PRINT (7)"No.",@(1+M0),"Name",@(32+M0),"Number",@(43+M0),"Contact",@(74+M0),"First Ship",@(87+M0),"Last Inv",@(98+M0+M1-12),"Sls-To-Date"
5090 RETURN 
5200 REM " --- Subhead "
5210 IF L>L9-2 THEN GOSUB 5000
5220 PRINT (7)""
5230 PRINT (7)FNF$(T0$(1,P[0]),M0$),@(M0+1),B1$(1,30),
5240 LET L=L+1
5290 RETURN 
6000 REM " --- Customer Break "
6010 IF T9$<>"" THEN GOTO 6090
6020 DIM B1$(30)
6030 LET T0$=A0$(3,6),B1$(1)="(Not On File)"
6040 FIND (ARM01_DEV,KEY=A0$(1,2)+T0$,DOM=6140)IOL=ARM01A
6050 GOSUB 5200
6090 RETURN 
8000 REM " --- Functions "
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

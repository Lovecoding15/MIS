0010 REM "Program BIN.11 based on OPR.CA - email invoices"
0080 SETESC 9000
0090 SETERR 9000
0100 if o9=5 then goto back_from_print_prog
0110 o9$="BIN.11",o9=5
0200 REM " --- IOLists" 
0210 ARE03A: IOLIST A0$(1),A[ALL]
0220 ARE13A: IOLIST W0$,W1$(1),W[ALL]
0250 ARE33A: IOLIST D0$,D1$
0260 ARM01A: IOLIST B0$,B1$
0270 ARM02A: IOLIST B0$,B1$,B[ALL]
0280 ARM03A: IOLIST C0$,C1$
0300 ARM10A: IOLIST X1$
0320 ARM10E: IOLIST Y0$(1)
0330 ARM10F: IOLIST X3$
0340 ARM10G: IOLIST *,X4$
0350 IVM01A: IOLIST *,X2$,X9$(1),*,*,X5$(1)
0370 CSM03A: IOLIST CS0$,CS1$,CS2$
0380 ARE73A: IOLIST WX0$,WX1$,WX[ALL]
0390 ARE83A: IOLIST WY0$,WY1$,WY2$,WY[ALL]
0440 call "templates.pgm::TMM01"
0450 call "templates.pgm::TMM05"
0460 call "templates.pgm::TMM03"
0470 call "ec_open::country"
0500 REM " --- Initialize Data"
0505 DIM WX[4],WY[2],X5$(2)
0510 PRECISION I[2]
0520 LET WHEN$=F0$(7,3)
0540 DIM W1$(64),W[14]
0550 LET LF$=$0A$
0720 DIM A0$(117),A[10]
0800 LET HOLDCUST$="",TODAY$=DATE(0:"%Ms %Dz %Y"),sort$=""
0810 txtchan=unt,emsg$=""
0820 open(txtchan)"invoice_email.txt"
0830 f$=fin(txtchan),sz=dec(f$(1,4))
0840 if sz>0 then readrecord(txtchan,siz=sz)emsg$
0850 close(txtchan)
0860 boundary$="--==========_BASIS"
1000 REM " --- Initial Read"
1010 READ (ARW43_DEV,KEY=N0$,DOM=*next)
1050 main_read: REM " --- Main Read"
1060 LET ARW43_K$=KEY(ARW43_DEV,END=done)
1070 READ (ARW43_DEV)
1080 LET K1$=ARW43_K$(1,2)+"  "+arw43_k$(3,6)+arw43_k$(11,7)+"000"
1090 IF K1$(1,2)<>N0$ THEN GOTO done
1100 READ (ARE03_DEV,KEY=K1$,DOM=main_read)IOL=ARE03A
1110 LET memo_only=(a0$(30,1)="M");rem Print memo lines only on I/C invoice
1120 if holdcust$<>a0$(5,6) then 
1130   gosub tm_address
1140   GOSUB get_email_address
1150   HOLDCUST$=A0$(5,6); if n0$="02" then holdcust$="BSG"
1170   GOSUB EMAIL_WINDOW
1180   IF HEAD$="-Q" THEN GOTO done
1190 fi
1200 IF HEAD$="-S" THEN GOTO main_read 
1220 if pos(sort$=arw43_k$)<>1 then 
1225   if sort$<>"" then gosub send_file
1230   sort$=arw43_k$(1,10)
1235   GOSUB NEWFILE
1240 fi
1400 LET TEMPFILE$=stbl("TEMP")+FID(0)+date(0:"%m%s")+".txt",prtr_dev=unt
1410 ERASE TEMPFILE$,ERR=*next
1415 open(prtr_dev,mode="FILE="+tempfile$)"PX"
1420 rem ' run "OPR.CA";rem 'Run standard invoice print program
1422 call "renderInvoice.src",A0$(1,2), A0$(5,6), A0$(42,7), jasperInvoice$
1425 back_from_print_prog:
1430 PRINT (TXTCHAN)LF$+boundary$+LF$+"Content-Type: application/pdf; name="+$22$+A0$(42,7)+".pdf"+$22$+LF$+"Content-Disposition: attachment; name="+$22$+A0$(42,7)+".pdf"+$22$+LF$+"Content-Transfer-Encoding: base64"+lf$
1440 tries=0
1445 gosub pdf_file
1450 close(tmp,err=*next)
1455 erase tmp$,err=*next
1460 erase tempfile$,err=*next
1470 print(txtchan)pdf$
1480 rem 'skip the text version of the invoice
1485 goto main_read
4000 done: REM " -- Done"
4010 if outfile$<>"" then GOSUB SEND_FILE
4190 GOTO 9900
6010 SEND_FILE: 
6020 PRINT (TXTCHAN,err=*next)boundary$+"--"+LF$
6030 CLOSE (TXTCHAN,ERR=*next)

6035 rem ' escape

6040 LET A=SCALL("/usr/lib/sendmail -t < "+OUTFILE$)
6050 ERASE OUTFILE$,ERR=*next
6060 RETURN 
6100 NEWFILE: 
6110 LET OUTFILE$=STBL("TEMP")+HOLDCUST$+"inv.txt"
6120 ERASE OUTFILE$,ERR=OPEN_OUTFILE
6130 OPEN_OUTFILE: STRING OUTFILE$
6140 LET TXTCHAN=UNT; OPEN (TXTCHAN)OUTFILE$
6150 invoice$=a0$(42,7)
6160 gosub get_email_txt
6170 PRINT (TXTCHAN)"Mime-Version: 1.0"+LF$+"Content-Type: multipart/mixed;"+LF$+" boundary="+$22$+boundary$(3)+$22$

6172 rem ' escape
6174 rem ' print eaddr$
6176 rem ' eaddr$ = "kurt.e.williams@comcast.net"
6177 rem ' print eaddr$
6178 rem ' print fid(txtchan)

6180 PRINT (TXTCHAN)"To: "+EADDR$+LF$+"From: BASIS Customer Service <customer-service@basis.cloud>"+LF$+"Cc: customer-service@basis.cloud"+lf$+"Bcc: kw5121151@gmail.com"+lf$+subject$+lf$
6181 rem ' debug -  PRINT (TXTCHAN)"To: "+EADDR$+LF$+"From: BASIS Customer Service <customer-service@basis.cloud>"+LF$+"Cc: williams.kurt@comcast.net"+lf$+"Bcc: kw5121151@gmail.com"+lf$+subject$+lf$

6182 rem ' escape

6190 PRINT (TXTCHAN)boundary$+LF$+"Content-Type: text/plain;"+LF$+" charset="+$22$+"iso-8859-1"+$22$+LF$+"Content-Transfer-Encoding: 7bit"+LF$+LF$
6200 PRINT (TXTCHAN)msgtext$
6250 RETURN 
6300 EMAIL_WINDOW: 
6305 temp$="Invoice "+a0$(42,7);if holdcust$="BSG" then temp$="All Invoices"
6310 PRINT 'WINDOW'(1,8,78,8,""),'CURSOR'("ON"),
6320 LET HEAD$=FILL(80),emlen=74; IF POS("@"=EADDR$)=0 THEN LET EADDR$=""
6325 IF len(EADDR$)>emlen THEN LET EADDR$=eaddr$(1,emlen)
6330 PRINT_STUFF: PRINT 'CS',@(2,0),temp$,@(2,1),"Customer ",HOLDCUST$," ",N$,@(1,3),"To:",@(1,4),EADDR$,@(1,5),
6340 INPUTE 1,4,emlen,"_",EADDR$
6350 PRINT @(2,5),"OK? CR/N/S(skip this)/Q(quit emailing)",; INPUT @(41,5),I$,; LET I$=CVS(I$,4); IF I$="N" THEN GOTO PRINT_STUFF
6360 PRINT 'POP',
6370 IF POS(I$="SQ") THEN LET HEAD$="-"+I$; RETURN 
6380 IF CVS(EADDR$,3)="" OR POS("@"=EADDR$)=0 THEN GOTO EMAIL_WINDOW
6390 RETURN 
6500 pdf_file:
6510 rem 'call "PDF.01",tempfile$,"","U"; already in pdf form
6511 gosub sign_invoice
6512 wait 2
6515 tmp$=path$ + final_pdfile$
6516 tmp=unt
6520 open(tmp)tmp$
6525 f$=fin(tmp),sz=dec(f$(1,4))
6530 close(tmp,err=*next)
6535 if sz then 
6545  rem ' call "archive_invoice.bbj",n0$,tmp$,invoice$
6546  archiveInvoice$ = A0$(42,7)
6547  call "archive_invoice.bbj",n0$,tmp$,archiveInvoice$
6550  call "PDF.01","",tmp$,"X"
6555  tmp=unt
6560  open(tmp)tmp$
6565  f$=fin(tmp),sz=dec(f$(1,4))
6570  if sz then readrecord(tmp,siz=sz)pdf$
6575  return
6580 fi
6585 if tries=0 then 
6590   tries=1;wait 1;goto pdf_file 
6600 else
6610   head$="-Q",temp$="PDF file failure- Restart with"
6615   PRINT 'WINDOW'(10,8,50,6,""),'CURSOR'("ON"),
6620   PRINT 'CS',@(2,0),temp$,@(2,1),HOLDCUST$," ",N$,
6625   PRINT @(2,3),"Press ENTER to return to Menu",; INPUT @(33,3),I$,
6630   PRINT 'POP',
6635 fi
6690 return
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,LF$
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,LF$
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END
10000 TM_ADDRESS: REM " --- Get 30 character address from Telemaster"
10005 DIM B1$(325)
10010 LET B1$(1)="Not On File",CONTACT$="",n$=""
10015 READ (ARM01_DEV,KEY=N0$+A0$(5,6),DOM=*next)IOL=ARM01A
10020 LET N$=B1$(1,30),CONTACT$=B1$(228,20)
10025 READ (TMM01_DEV,KNUM=0,KEY=A0$(1,2)+A0$(5,6),DOM=*next)
10030 LET TMM01_K$=KEY(TMM01_DEV,END=10090)
10035 IF POS(A0$(1,2)+A0$(5,6)=TMM01_K$)<>1 THEN return
10040 READ record(TMM01_DEV,KEY=TMM01_K$)TMM01$
10090 lang$=cvs(tmm01.lang_code$,7)
10110 if lang$="" then
10120   country.lang_code$="EN"
10130   readrecord(country,key=tmm01.country_code$,dom=*next)country$
10140   lang$=cvs(country.lang_code$,7)
10150   if lang$="" then lang$="EN"
10160 fi
10190 RETURN 
11000 get_email_address:
11010 LET EADDR$=""
11020 if holdcust$="BSG" then n$=""; goto end_email_address
11070 dim email$[4],tmm05$:fattr(tmm05$)
11080 email$[4]="customer-service@basis.cloud",sendto$=""
11090 find record(tmm05_dev,key=tmm01_k$,dom=*next)tmm05$
11100 email$[1]=cvs(tmm05.email$,3)
11110 read(tmm03_dev,key=tmm01_k$,dom=*next)
11120 while 1
11130  read record(tmm03_dev,end=*break)tmm03$
11140  if pos(tmm01_k$=tmm03$)<>1 then break
11150  addr$=cvs(tmm03.e_mail$,3)
11160  if addr$="" then continue
11170  if tmm03.ap_contact$="Y" then email$[0]=addr$;break
11180  if cvs(tmm03.contact_name$,7)=cvs(tmm01.cont_name$,7) then
11190    email$[2]=addr$
11200  else
11210    if email$[3]="" then email$[3]=addr$
11220  fi
11230 wend
11240 i=0
11250 while eaddr$=""
11260   eaddr$=email$[i]
11270   i=i+1
11280 wend
11300 END_email_address: return
14000 get_email_txt:
14010 msgtext$="",subject$="Subject: Invoice(s) "+TODAY$+" -"+holdcust$
14020 sr$=cvs(sort$(9,2),3); if sr$<>"" then sr$=sr$+"_"
14030 sr$=sr$+lang$+":"
14040 while 1
14050   p0=pos(sr$=cvs(emsg$,4));if p0=0 then break
14060   txt$=emsg$(p0)
14070   p0=pos("{"=txt$);if p0=0 then break
14080   txt$=txt$(p0+1),p0=pos("}"=txt$)
14090   if p0=0 then break
14100   msgtext$=txt$(1,p0-1)
14110   break
14120 wend
14130 if msgtext$="" and lang$<>"EN" then lang$="EN";goto get_email_txt
14140 p=pos("SUBJECT:"=cvs(msgtext$,4))
14150 if p then 
14155  txt$=msgtext$(p),p1=pos($0a$=txt$)
14160  if p1 then subject$=cvs(txt$(1,p1-2),3)+" -"+holdcust$,msgtext$=msgtext$(p+p1)
14165 fi
14190 return
14500 REM Date with 4 digit year
14505 DEF FNA4$(Q$,Q2$)=STR(((ASC(Q$)-32)+1900)*POS(" "<>Q2$(2,1)):"0000")
14510 DEF FNB4$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA4$(Q1$(1),Q1$)

15000 sign_invoice:
15010	rem ' sign the invoice
15020	final_pdfile$ = A0$(42,7)+".pdf"
15030	path$ = stbl("TEMP")
15040	if path$(len(path$), 1) <> "/" then let path$ = path$ + "/"
15050	erase path$ + final_pdfile$, err = *next
15060	pwd$=dir("")
15070	chdir("/mnt/data/basisaon")
15080	if A0$(1,2) = "01" then
15090		jv$=" SignBASISPDF"
15100	else
15110		jv$=" SignBASISPDFA4"
15120	fi
15130	com$="/usr/java/latest/bin/java -classpath "+chr(34)+"/basisaon:/basisaon/itext-1.3.jar"+chr(34)+jv$+" "+jasperInvoice$+" "+path$+final_pdfile$
15140	a=scall(com$)
15150  	chdir(pwd$)
15160	erase jasperInvoice$,err=*next
15170 return

20000 END

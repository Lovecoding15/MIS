0010 REM "OPC - Item Price Calculation"
0020 REM "Program OPC.PC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "CHANNELS[n] : [1] = IVM-01"
0051 REM "            : [2] = IVM-02"
0052 REM "            : [6] = IVM-06"
0053 REM "            : [10] = IVM-10"
0054 REM "N0$         : Current Firm"
0055 REM "WHS$        : Warehouse ID"
0056 REM "ITEM$       : Inventory Item Number"
0057 REM "LISTCD$     : List Price Code"
0058 REM "CUST$       : Customer Number"
0059 REM "DATE$       : Date Ordered"
0060 REM "PRICCD$     : Pricing Code"
0061 REM "ORDQTY      : Quantity Ordered"
0062 REM "TYPE_PRICE$ : Type Of Pricing (Returned)"
0063 REM "              C = Mark-Up From Cost"
0064 REM "              L = Mark-Down List Price"
0065 REM "              M = Margin Over Cost"
0066 REM "              N = None"
0067 REM "              P = Prior Price Code"
0068 REM "              X = Contract"
0069 REM "PRICE       : Price (Returned)"
0070 REM "DISC        : Discount (Returned)"
0071 REM "STATUS      : 0=No Error (Returned)"
0075 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER CHANNELS[ALL],N0$,WHS$,ITEM$,LISTCD$,CUST$,DATE$,PRICCD$,ORDQTY,TYPE_PRICE$,PRICE,DISC,STATUS
0100 REM " --- Open Files"
0110 LET IVM01_DEV=CHANNELS[1],IVM02_DEV=CHANNELS[2],IVM06_DEV=CHANNELS[6],IVM10_DEV=CHANNELS[10]
0200 REM " --- IOLists"
0210 IVM01A: IOLIST *,*,D2$(1)
0220 IVM02A: IOLIST *,L1$(1),*,L[ALL]
0230 IVM06A: IOLIST *,CONDATA$(1),CONBRK[ALL],CONPRC[ALL]
0240 IVM10E: IOLIST TABL$(1),TABLBRK[ALL],TABLDISC[ALL]
0500 REM " --- Initialize Data"
0510 LET TYPE_PRICE$="N",PRICE=0,DISC=0,STATUS=0
0520 LET DIGITS=TCB(14); REM "Get current precision
1000 REM " --- Get Item Class"
1010 DIM D2$(62)
1020 READ (IVM01_DEV,KEY=N0$+ITEM$,DOM=4000)IOL=IVM01A
1200 REM " --- Get Item's Warehouse Cost & Prices"
1210 DIM L1$(64),L[20]
1220 READ (IVM02_DEV,KEY=N0$+WHS$+ITEM$,DOM=4000)IOL=IVM02A
1290 GOTO 2000
2000 REM " --- Use Current List Price"
2010 LET PRICE=L[9]
2050 REM " --- Use Prior List Price"
2060 IF FNP$(LISTCD$)="" OR LISTCD$<>L1$(54,2) OR L[10]=0 THEN GOTO 2100
2070 LET PRICE=L[10],TYPE_PRICE$="P"
2090 GOTO 4000
2100 REM " --- Use Contract Pricing"
2105 IF IVM06_DEV=0 THEN GOTO 2200
2110 DIM CONDATA$(48),CONBRK[9],CONPRC[9]
2120 FIND (IVM06_DEV,KEY=N0$+CUST$+ITEM$,DOM=2200)IOL=IVM06A
2130 IF FNP$(CONDATA$(21,3))<>"" AND (DATE$<CONDATA$(21,3) OR DATE$>CONDATA$(24,3)) THEN GOTO 2200
2140 IF ABS(ORDQTY)<CONBRK[0] THEN GOTO 2200
2150 FOR X=0 TO 9
2155 IF CONBRK[X]>0 AND ABS(ORDQTY)>=CONBRK[X] THEN LET PRICE=CONPRC[X]
2160 NEXT X
2180 LET TYPE_PRICE$="X"
2190 GOTO 4000
2200 REM " --- Use Pricing Table"
2205 IF ORDQTY=0 THEN GOTO 4000
2210 DIM TABL$(31),TABLBRK[9],TABLDISC[9]
2220 FIND (IVM10_DEV,KEY=N0$+"E"+D2$(21,3)+PRICCD$,DOM=4000)IOL=IVM10E
2230 IF (TABLBRK[0]=0 AND TABLDISC[0]=0) OR ABS(ORDQTY)<TABLBRK[0] THEN LET PRICE=L[9]; GOTO 4000
2250 FOR X=0 TO 9
2255 IF TABLBRK[X]>0 AND ABS(ORDQTY)>=TABLBRK[X] THEN LET PERC=TABLDISC[X]
2260 NEXT X
2280 LET TYPE_PRICE$=TABL$(31,1)
2290 ON POS(TYPE_PRICE$="CML") GOTO 4000,2300,2400,2500
2300 REM " --- Mark-Up From Cost"
2310 PRECISION DIGITS+3
2320 LET PERCENT=PERC/100
2330 PRECISION DIGITS
2340 LET PRICE=L[11]+L[11]*PERCENT
2350 IF L[9]=0 THEN GOTO 4000
2360 PRECISION DIGITS+3
2370 LET PERCENT=1-PRICE/L[9]
2380 PRECISION DIGITS
2390 LET DISC=100*PERCENT
2395 GOTO 4000
2400 REM " --- Margin Over Cost"
2405 IF PERC>=100 THEN GOTO 4000; REM "No such thing as 100% margin unless cost=0
2420 LET FACTOR=100/(100-PERC)
2440 LET PRICE=L[11]*FACTOR
2450 IF L[9]=0 THEN GOTO 4000
2460 PRECISION DIGITS+3
2470 LET PERCENT=1-PRICE/L[9]
2480 PRECISION DIGITS
2490 LET DISC=100*PERCENT
2495 GOTO 4000
2500 REM " --- Mark-Down List Price"
2510 PRECISION DIGITS+3
2520 LET PERCENT=PERC/100
2530 PRECISION DIGITS
2540 LET PRICE=L[9]-L[9]*PERCENT
2560 LET DISC=PERC
2590 GOTO 4000
4000 REM " --- All Done"
4020 IF PRICE=0 THEN LET TYPE_PRICE$="N"
4090 GOTO 9900
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9800
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Return STATUS = ERR"
9810 LET STATUS=ERR
9820 IF STATUS=0 THEN LET STATUS=999
9830 LET TYPE_PRICE$="N",PRICE=0,DISC=0
9900 REM " --- Return to Caller"
9950 EXIT 
9999 END

0010 REM "PRX - Install New Tax Tables from PRX-10
0020 REM "Program  PRX.TA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Dec98 WGH: Install 'W' W2 Box Definition records from PRX-10 (240,340,450,625,685-695,810,1140,1500-1570)
0040 REM 
0060 BEGIN 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=3
0106 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0110 LET FILES$[1]="PRX-10",FILES$[2]="PRM-10",FILES$[3]="SYS-01"
0120 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0125 IF STATUS>0 THEN GOTO 9900
0130 LET PRX10_DEV=CHANNELS[1],PRM10_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLISTs
0210 PRX10C: IOLIST A0$,A[0],A[1],A[2],A1$,A2$
0220 PRX10I: IOLIST A0$,A[ALL]
0230 PRX10Y: IOLIST X0$,X1$
0240 PRX10W: IOLIST WA0$,WA1$,WA2$,WA3$,WA[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,F4$,F5$
0310 PRM10C: IOLIST B0$,B[0],B[1],B[2],B1$,B2$
0340 PRM10W: IOLIST WB0$,WB1$,WB2$,WB3$,WB[ALL]
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N3$=F4$
0430 FIND (PRX10_DEV,KEY="99",DOM=9900)IOL=PRX10Y
0440 LET C0=0,L0=6,L9=18,N=1,S=1
0450 DIM A[32],B[2],WA[19],WB[19],N[999,1],Y$(999,"Y")
0500 REM " --- Verify Firm ID"
0520 LET V4$="Company "+N0$+" "+F5$+" Will Be Updated."; PRINT @(40-INT(LEN(V4$)/2),21),V4$
0530 LET V0$="S",V1$="C",V2$="",V3$="",V4$="<Enter>=Continue, <F4>=End: ",V0=1,V1=40+INT(LEN(V4$)/2),V2=22
0540 GOSUB 7000
0545 PRINT @(0,21),'CE',
0550 IF V3=4 THEN GOTO 9900
0580 PRINT @(10,4),"* Denotes Update Will Take Place. Type In Number To Change Flag.",
0600 REM " --- Create strings of display records to be installed"
0610 LET K$=KEY(PRX10_DEV,END=0800)
0615 LET K1$=STR(N:"##0.")
0620 IF Y$(N,1)="Y" THEN LET K1$=K1$+"*" ELSE LET K1$=K1$+" "
0625 ON POS(K$(3,1)="CDIW") GOTO 0790,0630,0630,0670,0690,0790
0630 READ (PRX10_DEV,KEY=K$)IOL=PRX10C
0640 IF K$(3,1)="C" THEN LET K1$=K1$+"Tax Code " ELSE LET K1$=K1$+"Cont Code "
0650 LET K1$=K1$+K$(4,2)+" "+A0$(6,16)
0660 GOTO 0700
0670 READ (PRX10_DEV,KEY=K$)IOL=PRX10I
0680 LET K1$=K1$+"Tax Table "+K$(4,3)+" "+A0$(7,20)
0685 GOTO 0700
0690 READ (PRX10_DEV,KEY=K$)IOL=PRX10W
0695 LET K1$=K1$+"W-2 Box "+K$(4,2)+" "+K$(6,1)
0700 REM " --- Display strings"
0710 IF L0>L9 THEN LET C0=C0+40,L0=6
0720 IF C0>79 THEN GOSUB 6000
0730 PRINT @(C0,L0),K1$,
0740 LET N[N,0]=C0,N[N,1]=L0
0750 LET L0=L0+1,N=N+1
0790 GOTO 0600
0800 REM " --- Options
0810 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Do You Wish To Install The Above "+X1$+" Payroll Updates? (Yes/No/List/##): ",V0=4,V1=40+INT(LEN(V4$)/2),V2=22
0820 GOSUB 7000
0830 LET V=NUM(V$,ERR=0840); GOSUB 6100; GOTO 0800
0840 IF V$="LIST" THEN LET C0=0,L0=6,N=1,S=1; PRINT @(C0,L0),'CE',; READ (PRX10_DEV,KEY="99"); GOTO 0600
0850 IF V$="NO" THEN GOTO 9900
0860 IF V$<>"YES" THEN GOTO 0800
1000 REM " --- Initialize for PRM-10 Update
1010 CALL "SYC.NB","Updating:",3,COLUMN
1030 READ (PRX10_DEV,KEY="99")
1040 LET N=0
1100 REM " --- Update Loop"
1110 LET N=N+1
1120 LET K$=KEY(PRX10_DEV,END=4000)
1130 IF Y$(N,1)<>"Y" THEN READ (PRX10_DEV); GOTO 1100
1140 ON POS(K$(3,1)="CDIW") GOTO 1150,1200,1300,1400,1500,1150
1150 READ (PRX10_DEV)
1160 GOTO 1100
1200 REM " --- Tax Code Records"
1210 FIND (PRM10_DEV,KEY=N0$+K$(3),DOM=1400)IOL=PRM10C
1220 READ (PRX10_DEV,KEY=K$)IOL=PRX10C
1230 LET B0$(24,1)=A0$(24,1),B0$(30,1)=A0$(30,1),B0$(60,10)=A0$(60,10)
1240 LET B[0]=A[0],B[1]=A[1],B[2]=A[2]
1250 WRITE (PRM10_DEV,KEY=N0$+K$(3))IOL=PRM10C
1260 PRINT @(COLUMN,11),K$(4),
1270 GOTO 1100
1300 REM " --- Contribution Records"
1310 FIND (PRM10_DEV,KEY=N0$+K$(3),DOM=1400)IOL=PRM10C
1320 READ (PRX10_DEV,KEY=K$)IOL=PRX10C
1330 LET B0$(24,1)=A0$(24,1),B0$(30,1)=A0$(30,1)
1340 LET B[0]=A[0],B[1]=A[1],B[2]=A[2]
1350 WRITE (PRM10_DEV,KEY=N0$+K$(3))IOL=PRM10C
1360 PRINT @(COLUMN,11),K$(4),
1370 GOTO 1100
1400 REM " --- Tax Table and New Records"
1410 READ RECORD(PRX10_DEV,KEY=K$)A$
1430 LET K$(1,2)=N0$,A$(1,2)=N0$
1440 WRITE RECORD(PRM10_DEV,KEY=K$)A$
1450 PRINT @(COLUMN,11),K$(4),
1460 GOTO 1100
1500 REM " --- W-2 Box Definition Records
1510 FIND (PRM10_DEV,KEY=N0$+K$(3),DOM=1400)IOL=PRM10W
1520 READ (PRX10_DEV,KEY=K$)IOL=PRX10W
1530 LET WB0$(1,88)=WA0$(1,88)
1550 WRITE (PRM10_DEV,KEY=N0$+K$(3))IOL=PRM10W
1560 PRINT @(COLUMN,11),K$(4),
1570 GOTO 1100
4000 REM " --- End Of Update
4020 GOTO 9900
6000 REM " --- CR to continue"
6010 LET V0$="S",V1$="C",V2$="",V3$="",V4$="## or <Enter> To Continue. ",V0=3,V1=40+INT(LEN(V4$)/2),V2=22
6020 GOSUB 7000
6030 IF NUM(V$,ERR=6040)>0 THEN LET V=NUM(V$); GOSUB 6100; GOTO 6000
6040 PRINT @(0,6),'CE',
6050 LET C0=0,L0=6
6060 LET S=N
6090 RETURN 
6100 REM " --- Change Update Flag"
6110 IF V=0 THEN GOTO 6190
6120 IF V>N-1 THEN GOTO 6190
6130 IF Y$(V,1)="Y" THEN LET Y$(V,1)="N" ELSE LET Y$(V,1)="Y"
6140 IF V<S THEN GOTO 6190
6150 IF Y$(V,1)="Y" THEN PRINT @(N[V,0]+4,N[V,1]),"*", ELSE PRINT @(N[V,0]+4,N[V,1])," ",
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7290 GOTO 7000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9000
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "WOS - WO Issues - Issue materials                            "
0020 REM "Program:  WOE.DB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 15Dec97 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "10Jul96 JJD: Deleting & changing material issues before register & update causes negative commitments (3444,3445)"
0032 REM "27Jan97 JJD: Adjust item committed qty when qty issued exceeds qty ordered (3446,6000-6090"
0033 REM "20Feb97 JJD: Prevent return qty greater than qty issued (2525)"
0034 REM "20Feb97 JJD: Adjust item committed qty if negative issue, and inform user (3405,6200-6240)"
0035 REM "30Apr97 JJD: Line delete should follow "Issue delete" logic (95,3740,3765,6100-6190)"
0036 REM "11Jun97 WGH: Use WOE-25 Step Number to reference corresponding WOE-23 record (4863,4867,5810,6160,6180)"
0037 REM "27Mar98 KJS: Prevent spurious Issues from being created (520,1610-1660,4860,4870-4885,4930-4960,4980,5800-5870)"
0038 REM "11Nov98 WGH: Jump to detail line # feature doesn't work (4070-4110)
0040 REM 
0055 REM "L9=MAX LINES, L1=FIRST LINES, L0=HEADER LINE, L=CURRENT LINE
0080 SETESC 9000
0085 SETERR 9000
0090 PRECISION IVS01_PRECISION
0095 ON O0 GOTO 0100,3580,2570,3765,4840
0100 REM " --- Open/Lock Files
0200 REM " --- IOLISTs
0210 WOE25A: IOLIST W0$(1),W1$(1),W[ALL]
0220 IVM01A: IOLIST B0$,B1$(1),B2$
0230 IVM02A: IOLIST C0$,C1$,C2$,C[ALL]
0240 WOE15A: IOLIST A0$,A1$
0250 WOE23A: IOLIST R0$,R1$,R[ALL]
0500 REM " --- Init Data
0510 LET L0=10,L1=11,L9=19,K9$=""
0520 DIM W0$(14),W1$(40),W[4],R[4]
0530 LET W0$(1)=A0$(1,11),L=L1
0540 LET H1=77-M2*3-M1-M3
0700 REM " --- Background
0710 PRINT @(0,L0+1),'CE'
0720 PRINT 'SB',@(0,L0),"Seq Wh Item Code",@(H1+M2-4),"Ord",@(H1+M2*2-4),"Bal",@(77-M1-M3-6),"Issue",@(77-M1-5),"Cost",@(77-6),"Value"
0730 PRINT 'SF'
0740 LET K9$=""
0750 GOSUB 5100
0760 IF K9$="" THEN GOTO 1010
0770 GOTO 4000
1000 REM " --- Clear Screen "
1010 IF L<=L9 THEN GOTO 1100
1015 PRINT @(0,21),'CL',@(0,22),'CL',@(0,L1),'LD','LD'
1020 LET L=L-2
1025 GOTO 1010
1100 REM " --- Get Next Key"
1110 LET K$=KEY(WOE25_DEV,END=1300)
1120 IF K$="" THEN GOTO 1300
1130 IF K$(1,2)<>N0$ THEN GOTO 1300
1140 IF K$(1,11)<>A0$(1,11) THEN GOTO 1300
1150 READ (WOE25_DEV)IOL=WOE25A
1160 DIM B1$(DESCLEN)
1170 LET B1$(1)="*** Not On File ***"
1180 FIND (IVM01_DEV,KEY=N0$+W1$(21,20),DOM=1190)IOL=IVM01A
1190 LET B1$=FNITEM$(B1$,DESC[1],DESC[2],DESC[3])
1200 GOTO 1430
1300 REM " --- Seq"
1310 PRINT @(0,21),'CL',@(0,22),'CL',
1320 IF L<=L9 THEN GOTO 1360
1330 PRINT @(0,L1),'LD','LD'
1340 LET L=L-2
1350 GOTO 1320
1360 LET W0=NUM(W0$(12,3))+1
1370 LET V0$="Z",V1$="ER",V2$=STR(W0),V3$="",V0=3,V1=0,V2=L,I0=0
1380 GOSUB 7000
1390 IF V3=4 THEN PRINT @(V1,V2),"   "; GOTO 4000
1400 IF V$="000" THEN GOTO 1300
1410 LET W0$(12,3)=V$,EXIST=1
1420 FIND (WOE25_DEV,KEY=W0$(1,14),DOM=1600)IOL=WOE25A
1430 DIM B1$(DESCLEN),B2$(46)
1435 LET B1$(1)="*** Not On File ***"
1440 FIND (IVM01_DEV,KEY=N0$+W1$(21,20),DOM=1450)IOL=IVM01A
1450 LET B1$=FNITEM$(B1$,DESC[1],DESC[2],DESC[3])
1460 IF W0$(1,14)<=K9$ THEN GOSUB 5400
1470 GOSUB 5000
1480 LET O1$=W1$(19),O1=W[0]-W[1]
1490 LET I0=1
1500 GOTO 3200
1600 REM " --- Setup"
1640 LET EXIST=0
1650 DIM W[4]
1670 LET W1$(1)="",W1$(4,3)=F0$(7,3),O1$="",O1=0,W1$(19,2)=D0$(54,2)
1680 PRINT @(4,L),W1$(19,2)
1690 GOTO 3200
2000 REM " --- Item Code
2005 IF EXIST=0 THEN GOTO 2020
2010 LET V3=0
2015 GOTO 2190
2020 LET V0$="S",V1$="KCR",V2$=W1$(21),V3$="",V4$="Enter Valid Item Number (<F3>=Lookup)",V0=P[0],V1=7,V2=L,I0=1
2040 GOSUB 7000
2050 IF V3=2 OR V3=4 THEN GOTO 3200
2090 IF V3=3 THEN CALL "SYC.LI",V$
2100 IF V$="" THEN GOTO 2000
2110 LET V$=V$+FILL(20-LEN(V$)," "),V$=V$(1,20)
2115 DIM B1$(DESCLEN)
2120 FIND (IVM01_DEV,KEY=N0$+V$,DOM=2000)IOL=IVM01A
2130 LET B1$=FNITEM$(B1$,DESC[1],DESC[2],DESC[3])
2140 LET W1$(21)=V$,W1$(1,2)=B2$(4,2)
2160 FIND (IVM02_DEV,KEY=N0$+W1$(19,22),DOM=2000)IOL=IVM02A
2170 LET W[4]=C[11]
2180 GOSUB 5000
2190 GOTO 3200
2200 REM " --- Unit Of Measure
2210 PRINT 'SB',@(50,L+1),"Date Req'd: ",'SF'
2220 LET V0$="S",V1$="",V2$=W1$(1,2),V3$="",V4$="Enter The Unit of Measure",V0=2,V1=44,V2=L+1
2240 GOSUB 7000
2260 LET W1$(1,2)=V$
2290 GOTO 3200
2300 REM " --- Date Required
2310 PRINT 'SB',@(40,L+1),'SB',"UM: ",'SF'
2320 IF POS(" "<>W1$(4,3))=0 THEN LET W1$(4,3)=F0$(7,3)
2340 LET V0$="D",V1$="C",V2$=W1$(4,3),V3$="",V4$="Enter The Date That This Item Will Be Required",V0=3,V1=62,V2=L+1
2360 GOSUB 7000
2380 LET W1$(4,3)=V$
2390 GOTO 3200
2500 REM " --- Quantity Required
2510 LET V0$="N",V1$="C",V2$=STR(W[3]),V3$=M2$,V4$="Enter Issue Quantity Required",V0=0,V1=H1+M2*2,V2=L
2520 GOSUB 7000
2525 IF W[0]<>0 AND V<0 THEN IF ABS(V)>W[1] THEN GOTO 2500
2530 IF V3<>2 THEN LET I0=I0+2
2540 LET W[3]=V
2550 REM " --- Set Unit Cost "
2560 FIND (IVM02_DEV,KEY=N0$+W1$(19,22),DOM=2570)IOL=IVM02A
2570 IF W[2]=0 THEN LET W[2]=C[11]
2580 LET W[4]=C[11]
2590 GOSUB 5070
2600 GOTO 3200
3200 REM " --- Seq Control"
3210 IF V3=4 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3235 LET O0=0
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1300,2000,2500,2200,2300,3300
3300 REM " --- Correct"
3310 PRINT @(78,L),'CL',@(0,22),'CL'
3315 IF V3=4 THEN IF O1$="" AND W[3]=0 THEN GOTO 4000
3320 LET V0$="S",V1$="C",V2$="",V0=1,V1=78,V2=L,K9$=W0$(1,14),V4$="<Enter>=Continue     <F2>=Change     <D>=Delete",I0=5
3330 GOSUB 7000
3340 IF V3=2 THEN GOTO 3200
3350 IF V$="" THEN GOTO 3390
3360 IF V$="D" THEN GOTO 3700
3370 GOTO 3300
3390 PRINT @(0,21),'LD','LD',
3400 REM " --- Write"
3405 IF W[3]<0 AND W[0]>0 THEN GOSUB 6200
3410 IF O1$=W1$(19) AND O1=W[0]-W[1] THEN GOTO 3430
3425 IF O1$="" THEN GOTO 3440
3430 LET S9$=O1$,S9=O1,S8=-1
3435 GOSUB 5700
3440 LET S9$=W1$(19),S9=W[0]-W[1],S8=1
3444 REM " --- Set default values if item has been previously deleted"
3445 IF W[0]=0 THEN LET S9=W[3],W[0]=W[3],O1$=W1$(19),O1=W[0]-W[1]; GOSUB 5070
3446 IF W[3]>0 THEN IF W[0]-(W[1]+W[3])<0 THEN GOSUB 6000
3450 GOSUB 5700
3460 WRITE (WOE25_DEV,KEY=W0$(1,14))IOL=WOE25A
3500 REM " --- Process Lot/Serial "
3510 IF POS(P3$(17,1)="LS")=0 OR B2$(19,2)<>"YY" OR V3=4 THEN GOTO 3580
3520 IF O0=2 THEN GOTO 3580
3530 IF L<=L1 THEN GOTO 3570
3540 PRINT @(0,L1),'LD',
3550 LET L=L-1
3560 GOTO 3530
3570 RUN "WOE.DC"
3580 REM " --- Return From Lot/Serial "
3590 IF V3=4 THEN GOTO 4000
3600 LET L=L+2,O1$=W1$(19),O1=W[0]-W[1],I0=0,O0=0
3610 GOTO 1010
3700 REM " --- Delete"
3740 IF POS(P3$(17,1)="LS")=0 THEN GOTO 3765
3750 LET O0=3
3760 RUN "WOE.DC"
3765 GOSUB 6100
3770 REMOVE (WOE25_DEV,KEY=W0$(1,14),DOM=3780)
3780 PRINT @(0,L),'LD','LD'
3785 LET I0=0,O0=0
3790 GOTO 1010
4000 REM " --- Options"
4010 LET V0$="",V1$="C",V2$="",V3$="",V4$="Is The Detail Information Correct (Y/N/Review/List/#/Delete)?",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V$="" THEN GOTO 4140
4040 IF V$="DELETE" THEN GOTO 4700
4050 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4060 IF V$="L" OR V$="LIST" THEN GOTO 4400
4070 LET W0=NUM(V$,ERR=4130)
4080 IF W0<1 OR W0>999 THEN GOTO 4000
4090 LET W0$(12,3)=STR(W0-1:"000"),V$="N"
4130 ON POS(V$="YN") GOTO 4000,4140,1300
4140 REM " --- Close & Get Ready
4150 CALL "SYC.WC",1,0,80,0,0,L0-1,0
4160 PRINT @(0,L0),'CE'
4170 LET O9=1
4180 LET O0=0
4190 RUN "WOE.DA"
4400 REM " --- List"
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4610 LET EXIST=1
4620 READ (WOE15_DEV,KEY=A0$(1,11)+"000",DOM=4640)
4630 GOTO 4650
4640 LET EXIST=0
4650 GOSUB 5400
4660 READ (WOE25_DEV,KEY=A0$(1,11),DOM=4670)
4670 LET V3=0
4680 GOTO 1010
4700 REM " --- Delete Here
4710 LET V4$="Delete Issue Only And Retain Commitment"
4720 CALL "SYC.YN",1,V4$,2,V$,V3
4730 LET I9$=V$
4740 IF V3=4 THEN GOTO 4000
4750 IF I9$="YES" THEN LET I9$="Y"
4760 IF I9$="NO" THEN LET I9$="N"
4770 IF POS(I9$="YN")=0 THEN GOTO 4700
4780 READ (WOE25_DEV,KEY=A0$,DOM=4800)
4800 LET K$=KEY(WOE25_DEV,END=4900)
4820 IF K$(1,11)<>A0$(1,11) THEN GOTO 4900
4830 READ (WOE25_DEV)IOL=WOE25A
4832 IF POS(P3$(17,1)="LS")=0 THEN GOTO 4840
4835 LET O0=4; RUN "WOE.DC"
4840 IF I9$="Y" THEN GOTO 4880
4845 LET S9$=W1$(19),S9=W[0]-W[1],S8=-1
4850 GOSUB 5700
4863 READ (WOE23_DEV,KEY=K$(1,11)+W1$(7,3),DOM=4870)IOL=WOE23A
4865 LET R[0]=0
4867 WRITE (WOE23_DEV,KEY=R0$)IOL=WOE23A
4880 REMOVE (WOE25_DEV,KEY=K$)
4890 GOTO 4800
4900 REM " --- Header Here
4920 REMOVE (WOE04_DEV,KEY=A0$(1,4)+"D"+A0$(5,7),DOM=4930)
4970 REMOVE (WOE15_DEV,KEY=A0$,DOM=4980)
4980 IF I9$="N" THEN REMOVE (WOE04_DEV,KEY=A0$(1,4)+"C"+A0$(5,7),DOM=4990)
4990 GOTO 4140
5000 REM " --- Display"
5010 PRINT @(0,L),W0$(12,3),@(4),W1$(19,2),@(7),W1$(21),@(9+P[0]),
5070 PRINT @(H1,L),W[0]:M2$,W[0]-W[1]:M2$,W[3]:M2$,W[4]:M3$,W[3]*W[4]:M1$
5080 PRINT @(7,L+1),B1$,@(40,L+1),'CL','SB',@(40,L+1),"UM: ",'SF',@(44),W1$(1,2),'SB',@(50,L+1),"Date Req'd: ",'SF',@(62),FNB$(W1$(4,3)),
5090 RETURN 
5100 REM " --- List"
5110 GOSUB 5400
5120 READ (WOE25_DEV,KEY=A0$,DOM=5130)
5130 LET K$=KEY(WOE25_DEV,END=5300)
5135 IF K$(1,2)<>N0$ THEN GOTO 5300
5140 IF K$(1,11)<>W0$(1,11) THEN GOTO 5300
5150 READ (WOE25_DEV)IOL=WOE25A
5155 DIM B1$(DESCLEN); LET B1$(1)="*** Not On File ***"
5160 FIND (IVM01_DEV,KEY=W0$(1,2)+W1$(21,20),DOM=5180)IOL=IVM01A
5180 LET B1$=FNITEM$(B1$,DESC[1],DESC[2],DESC[3])
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Press <Enter> To Continue:",V0=1,V1=FNV(V4$),V2=22
5215 GOSUB 7000
5220 IF V3=4 THEN GOTO 5300
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+2,K9$=W0$(1,14)
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5420 PRINT @(0,L1),'CE',
5440 LET L=L1
5490 RETURN 
5700 REM " --- Commit/Uncommit Inventory "
5701 DIM PARAMS[0],PARAMS$[4],ITEMS$[3],REFS$[11],REFS[5],IVFILES[44]
5702 LET PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3),PARAMS$[2]=P2$
5703 LET PARAMS$[3]=P3$,PARAMS$[4]=P4$
5704 LET IVFILES[0]=SYS01_DEV,IVFILES[1]=IVM01_DEV,IVFILES[2]=IVM02_DEV
5705 IF IVM07_DEV THEN LET IVFILES[7]=IVM07_DEV,IVFILES[17]=IVM17_DEV
5710 IF S8=1 THEN LET ACTION$="CO"
5720 IF S8=-1 THEN LET ACTION$="UC"
5730 LET REFS[0]=S9,ITEMS$[0]=N0$,ITEMS$[1]=S9$(1,2),ITEMS$[2]=S9$(3,20)
5740 CALL "IVC.UA",ACTION$,IVFILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEMS$[ALL],REFS$[ALL],REFS[ALL],STATUS
5760 RETURN 
6000 REM " --- Adjust committed when issue qty exceeds ordered qty"
6010 LET COMIT_QTY=-(W[0]-(W[1]+W[3]))
6020 LET S9_HOLD=S9,S8_HOLD=S8,W[0]=W[0]+COMIT_QTY,O1=W[0]-W[1]
6030 LET S8=1,S9=COMIT_QTY
6040 GOSUB 5700
6045 GOSUB 5000
6050 LET S8=S8_HOLD,S9=S9_HOLD
6090 RETURN 
6100 REM " --- Delete Line Item "
6110 LET V4$="Delete Issue Only And Retain Commitment"
6120 CALL "SYC.YN",1,V4$,2,V$,V3
6130 ON POS(V$="YN") GOTO 6100,6190,6140
6140 LET S9$=W1$(19),S9=W[0]-W[1],S8=-1
6150 GOSUB 5700
6160 READ (WOE23_DEV,KEY=W0$(1,11)+W1$(7,3),DOM=6190)IOL=WOE23A
6170 LET R[0]=0
6180 WRITE (WOE23_DEV,KEY=W0$(1,11)+W1$(7,3))IOL=WOE23A
6190 RETURN 
6200 REM " --- Inform User Item qty Is Recommitted"    
6210 DIM MSG$[0]
6220 LET MSG$[0]="Quantity "+STR(ABS(W[3]))+" of item "+CVS(O1$(3),2)+" will be recommitted.  <Enter>."
6230 CALL "SYC.XA",3,MSG$[ALL],0,22,-1,V$,V3
6240 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

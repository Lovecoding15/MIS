0010 REM "IVU - Purge Monthly Inventory Transaction History"
0020 REM "Program IVU.TA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="IVT-04",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET IVT04_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 SYS01T: IOLIST X$,F0$
0220 IVS01A: IOLIST X$,X$,X0$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2)
0430 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET I0=NUM(X0$(1,2))
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 PRINT 'SB',@(5,3),"Purge Transaction History Through:",'SF',@(0,22),'CL',
0800 REM " --- Purge Through Date"
0805 PRINT 'CF',
0810 LET V0$="D",V1$="CER",V2$="",V3$="",V0=6,V1=40,V2=3
0820 LET V4$="Enter The Date To Purge Transaction History Through"
0830 GOSUB 7000
0840 IF V3=4 THEN GOTO 9900
0850 IF V3<>0 THEN GOTO 0800
0890 LET D$=V$
0900 REM " --- OK?"
0920 LET V0$="S",V1$="CE",V2$="N",V3$=" YN",V4$="OK To Purge All History Through This Date (Y/N)?",V0=1,V1=FNV(V4$),V2=22
0930 GOSUB 7000
0940 ON V3 GOTO 0950,0900,0900,0900,9900
0950 IF V$<>"Y" THEN GOTO 9900
0960 CALL "SYC.NB","Updating",12+I0,COLUMN
0970 REM " --- Disallow 'M'enu option in Error Routine
0980 LET EXIT_CTRL=1
0990 READ (IVT04_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Purge loop"
1010 LET K$=KEY(IVT04_DEV,END=4000)
1020 IF K$(1,2)<>N0$ THEN GOTO 4000
1030 PRINT @(COLUMN,11),K$(3,2)," ",K$(5,I0)," ",FNB$(K$(25,3)),
1050 READ (IVT04_DEV,KEY=K$)
1080 IF K$(25,3)<=D$ THEN REMOVE (IVT04_DEV,KEY=K$,DOM=1090)
1090 GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

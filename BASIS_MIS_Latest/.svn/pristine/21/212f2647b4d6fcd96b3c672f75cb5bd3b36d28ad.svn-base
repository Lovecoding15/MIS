0010 REM "GLE - Journal Entry (Heading Overlay)"
0020 REM "Program GLE.CB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "17Apr00 KJS: Display period year correctly in Y2K year (2280,2480,5060,5080)
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0051 REM "  NOTE: If this prog is run from GLE.DA, Recurring Journal Entries,
0052 REM "        GLE01_DEV & GLE11_DEV are really GLE-02 & GLE-12
0060 ON O0 GOTO 70,1000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-01",FILES$[2]="GLM-03",FILES$[3]="SYS-01"
0160 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM01_DEV=CHANNELS[1],GLM03_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 GLE01A: IOLIST A0$(1),A1$(1),A2$,A[ALL]
0240 GLM03A: IOLIST D0$(1),D1$,D[ALL]
0260 GLS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$,M4$,M5$
0270 SYS01T: IOLIST X$,F0$,X$,F2$,X$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM P[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0440 FOR X=0 TO 4
0450 LET P[X]=NUM(P2$(X*2+1,2),ERR=460)
0460 NEXT X
0465 LET P[2]=FNYY_YEAR(P2$(5,2))
0490 LET NUMBER=1,K9=17,K8=K9-3,RECORD$=""
0500 REM " --- Initializations"
0510 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
0515 DIM A0$(K8),A1$(48),A[2],D0$(32),D[2],O[2],J1$(40)
0570 LET VERIFY$="Y",O0=1
0590 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),A0$(1)=N0$,L1=9
0600 REM " --- Set column positions"
0610 LET O[2]=77,COLUMN=80-M1
0620 IF P4$(3,1)="Y" THEN LET O[2]=O[2]-M2
0630 LET O[1]=O[2]-M1,O[0]=O[1]-M1
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,L1-1,0
0720 PRINT 'SB',@(6,3),"Journal ID:",@(COLUMN-12),"Total Debits",@(2,4),"Transaction ID:",@(COLUMN-13),"Total Credits",@(12,5),"Memo:",@(COLUMN-7),"Balance",@(0,6),"Transaction Date:",@(3,7),"Reversal Date:",@(1,L1),"Seq",@(6),"Account/Memo",@(O[0]+M1-6),"Debit",@(O[1]+M1-7),"Credit",'SF',
0730 IF CYCLES$="Y" THEN PRINT 'SB',@(65,L1-2),"Cycle Code:",'SF',
0750 IF P4$(3,1)="Y" THEN PRINT 'SB',@(O[2]+M2-6,L1),"Units",'SF',
0900 REM " --- Position File"
0990 READ (GLE01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Journal ID"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(GLE01_DEV,END=1050)
1030 IF V2$(1,2)<>N0$ THEN LET V2$=""
1040 IF V2$<>"" THEN LET A0$(1)=V2$,V2$=V2$(3,2)
1050 LET V4$="Enter A Valid Journal ID Code (<F3>=Lookup)"
1055 LET V0$="S",V1$="KC",V3$="",V0=2,V1=18,V2=3
1060 GOSUB 7000
1061 IF V3<>3 THEN GOTO 1075
1065 GOSUB 6000
1070 IF V$="" THEN GOTO 0900
1075 IF V3=4 THEN GOTO 9900
1077 IF V$<>A0$(3,2) THEN LET A0$(5)=""
1080 LET D0$(1,4)=N0$+V$,A0$(1,4)=D0$(1,4)
1085 GOSUB 6500
1090 IF STATUS>0 THEN GOTO 1000
1100 REM " --- Transaction ID"
1110 LET V0$="S",V1$="C",V2$=A0$(5,10),V3$="",V0=10,V1=18,V2=4
1120 LET V4$="Enter The Transaction ID For This Journal Entry (<F3>=Lookup)"
1130 GOSUB 7000
1140 ON V3 GOTO 1160,1100,1000,1150,1000
1150 GOSUB 6200
1160 IF FNP$(V$)="" THEN GOTO 1100
1170 LET A0$(5,V0)=V$
1180 PRINT @(V1,V2),A0$(5,V0),
1200 REM " --- Retrieve record"
1210 FIND (GLE01_DEV,KEY=A0$,DOM=1400)IOL=GLE01A
1300 REM " --- Display"
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New record"
1410 LET V4$="Is This A New Record (Y/N)?"
1420 LET V0$="Y",V1$="C",V2$="N",V3$="",V0=1,V1=FNV(V4$),V2=22
1430 GOSUB 7000
1440 ON V3 GOTO 1490,1400,1400,1400,9900
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record"
1510 DIM A1$(48),A[2]
1520 LET A1$(31,3)=F0$(7,3)
1540 GOSUB 5000
2000 REM " --- Memo"
2010 LET V0=30,V0$="S",V1$="C",V2$=A1$(1,V0),V3$="",V1=18,V2=5,I0=0
2020 LET V4$="Enter Up To "+STR(V0)+" Characters"
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,2000,2000,3900
2050 LET A1$(1,V0)=V$
2090 GOTO 3900
2200 REM " --- Transaction Date"
2210 LET V4$="Enter The Transaction Date For This Journal Entry",STATUS=0
2220 LET V0$="D",V1$="R",V2$=A1$(31,3),V3$="",V0=8,V1=18,V2=6
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 IF GL$="Y" THEN CALL "GLC.EA",V$,VERIFY$,PERIOD$,YEAR$,STATUS
2260 IF STATUS>99 THEN GOTO 2200
2265 LET A1$(31,3)=V$
2270 PRINT @(V1,V2),FNB$(A1$(31,3)),
2280 IF GL$="Y" THEN PRINT @(27,6),"- Period ",PERIOD$," (",P3$(235+(NUM(PERIOD$)-1)*3,3)," ",FNYY21_YY$(YEAR$),")"
2290 GOTO 3900
2400 REM " --- Reversal Date"
2410 LET V4$="Enter The (Optional) Reversal Date For This Journal Entry"
2420 LET V0$="D",V1$="C",V2$=A1$(34,3),V3$="",V0=8,V1=18,V2=7,STATUS=0
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 IF FNP$(V$)="" THEN GOTO 2465
2455 IF GL$="Y" THEN CALL "GLC.EA",V$,VERIFY$,PERIOD$,YEAR$,STATUS
2460 IF STATUS>99 THEN GOTO 2400
2465 LET A1$(34,3)=V$
2470 IF FNP$(V$)="" THEN PRINT @(27,7),J1$(1,24),; GOTO 2490
2480 IF GL$="Y" THEN PRINT @(27,7),"- Period ",PERIOD$," (",P3$(235+(NUM(PERIOD$)-1)*3,3)," ",FNYY21_YY$(YEAR$),")"
2490 GOTO 3900
2600 REM " --- Cycle Code"
2605 IF CYCLES$<>"Y" THEN GOTO 3900
2610 LET V4$="Enter The (Optional) Cycle Code For This Journal Entry (<F3>=Lookup)"
2620 LET V0$="S",V1$="C",V2$=A1$(37,2),V3$="",V0=2,V1=77,V2=L1-2
2630 GOSUB 7000
2635 ON V3 GOTO 2650,2600,3900,2640,3900
2640 GOSUB 6100
2650 IF FNP$(V$)<>"" THEN GOTO 2670
2655 PRINT @(V1,V2),'CL',
2660 GOTO 2680
2670 LET V$=V$+"  ",V$=V$(1,2)
2675 FIND (GLM07_DEV,KEY=N0$+V$,DOM=2600)
2680 LET A1$(37,2)=V$
2685 PRINT @(V1,V2),A1$(37,2),
2690 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,2400,2600,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Heading Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write"
4110 WRITE (GLE01_DEV,KEY=A0$)IOL=GLE01A
4190 RUN "GLE.CC"
4200 REM " --- Remove"
4210 READ (GLE11_DEV,KEY=A0$,DOM=4220)
4220 LET K$=KEY(GLE11_DEV,END=4260)
4230 IF POS(A0$=K$)<>1 THEN GOTO 4260
4240 REMOVE (GLE11_DEV,KEY=K$,DOM=4250)
4250 GOTO 4220
4260 REMOVE (GLE01_DEV,KEY=A0$,DOM=4270)
4290 GOTO 1000
5000 REM " --- Display"
5010 PRINT @(18,5),A1$(1,30),@(18,6),FNB$(A1$(31,3)),
5020 IF FNP$(A1$(34,3))<>"" THEN PRINT @(18,7),FNB$(A1$(34,3))
5030 IF CYCLES$="Y" THEN PRINT @(77,L1-2),A1$(37,2),
5040 IF GL$<>"Y" THEN GOTO 5090
5050 CALL "GLC.EA",A1$(31,3),"N",PERIOD$,YEAR$,STATUS
5060 PRINT @(27,6),"- Period ",PERIOD$," (",P3$(235+(NUM(PERIOD$)-1)*3,3)," ",FNYY21_YY$(YEAR$),")"
5065 IF FNP$(A1$(34,3))="" THEN GOTO 5090
5070 CALL "GLC.EA",A1$(34,3),"N",PERIOD$,YEAR$,STATUS
5080 PRINT @(27,7),"- Period ",PERIOD$," (",P3$(235+(NUM(PERIOD$)-1)*3,3)," ",FNYY21_YY$(YEAR$),")"
5090 RETURN
6000 REM " --- Journal ID Lookup"
6010 LET CHANNEL=GLM03_DEV,TITLE$="Journal ID Codes",RECORD$=""
6020 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=2
6030 LET FIELD[1]=1,POSITION[1]=5,LENGTH[1]=20
6040 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6080 LET ROW=V2+1,COL=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,V$="SKIP"
6090 GOTO 6300
6100 REM " --- Cycle Code Lookup"
6110 LET CHANNEL=GLM07_DEV,TITLE$="Cycle Codes",RECORD$=""
6120 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=2
6130 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=20
6140 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6180 LET ROW=V2+1,COL=V1-10-LENGTH[0]-LENGTH[1]-2-NUMBER*4,V$="SKIP"
6190 GOTO 6300
6200 REM " --- Transaction Lookup"
6210 LET CHANNEL=GLE01_DEV,TITLE$=A0$(3,2)+" Journal Entries"
6220 LET FIELD[0]=1,POSITION[0]=5,LENGTH[0]=10,RECORD$=A0$(3,2)
6230 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6240 LET DESCRIPTION$[0]="Transaction",DESCRIPTION$[1]="Memo"
6280 LET ROW=V2+1,COL=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6300 REM " --- Call windowed lookup"
6310 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COL,ROW,V$
6390 RETURN
6500 REM " --- Verify Journal ID Code"
6510 LET STATUS=0
6520 FIND (GLM03_DEV,KEY=D0$(1,4),DOM=6680)IOL=GLM03A
6530 PRINT @(V1,V2),D0$(3,2)," ",D0$(5,20),
6535 IF GL$<>"Y" THEN GOTO 6690
6540 IF D0$(25,1)<>"Y" THEN GOTO 6600
6550 DIM GLCONTROL$(640)
6560 LET GLCONTROL$(1)=STBL("!GLCONTROL",ERR=6600)
6570 LET GLCONTROL$(18,2)=D0$(3,2),GLCONTROL$(46,2)=D0$(26,2)
6575 IF JE$="Y" THEN LET GLCONTROL$(45,1)=JE$
6580 LET GLCONTROL$(1)=STBL("!GLCONTROL",GLCONTROL$)
6590 GOTO 6690
6600 REM 
6610 DIM MESSAGE$[1]
6620 LET MESSAGE$[0]="This Journal ID May Not Be Used For Entry (<Enter>=Continue)"
6630 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
6680 LET STATUS=1
6690 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

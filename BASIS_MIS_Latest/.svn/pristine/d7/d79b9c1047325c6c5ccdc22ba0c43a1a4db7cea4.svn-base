0010 REM "WOR - Work Order Committed Materials Report - Parameters     "
0020 REM "Program:  WOR.MA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "07Apr97 JJD: Delete line using unnecessary 5.2 logic (4240)"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=8
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-23",FILES$[2]="WOW-10",FILES$[3]="WOE-01"
0120 LET FILES$[4]="IVM-01",FILES$[5]="IVM-02",FILES$[6]="SYS-01"
0125 LET FILES$[7]="IVM-10",FILES$[8]="WOM-10"
0130 LET OPTIONS$[2]="C"
0150 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE23_DEV=CHANNELS[1],WOW10_DEV=CHANNELS[2],WOE01_DEV=CHANNELS[3]
0165 LET IVM01_DEV=CHANNELS[4],IVM02_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0170 LET IVM10_DEV=CHANNELS[7],WOM10_DEV=CHANNELS[8]
0200 REM " --- IOLISTs"
0210 WOW10A: IOLIST SORT$
0220 WOE23A: IOLIST W0$,W1$
0230 IVM10A: IOLIST X0$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 WOS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 IVS01A: IOLIST I0$,I1$,I2$,I3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0450 DIM DESC[3]
0460 LET P0=NUM(I2$(1,2)),P1=NUM(I2$(5,1))
0470 FOR X=1 TO 3
0475 LET DESC[X]=NUM(I2$((X-1)*2+9,2)),DESCLEN=DESCLEN+DESC[X]
0480 NEXT X
0500 REM " --- Init Data"
0505 LET H0=132,HEADINGS=4,H=0,WHEN$=F0$(7,3),CLOCK$=""
0510 DIM HEADINGS$[HEADINGS],SORT$(34)
0515 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0520 LET L9=59,L=L9+1
0530 DIM J$(80,"-"),J1$(40," "),J0$(20,"0")
0550 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),G5=LEN(G5$)
0700 REM " --- Background"
0710 FOR X=4 TO 12 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(23,3),"Warehouse Code:",@(25,5),"Product Type:",@(23,7),"Beginning Date:",@(26,9),"Ending Date:"
0760 PRINT 'SF'
1000 REM " --- Options"
1005 LET I0=0
1010 PRINT 'CF'
1020 LET V0$="S",V1$="KEC",V2$=P9$,V3$="",V4$="Enter A Valid Warehouse Code, <F1>=All, <F3>=Lookup",V0=2,V1=40,V2=3
1040 GOSUB 7000
1060 IF V3=4 THEN GOTO 9900
1070 IF V3=1 THEN LET P9$=""; PRINT @(V1,V2),"All Warehouses"; GOTO 1190
1080 IF V3<>3 THEN GOTO 1140
1090 LET V$=V$+"  ",V$=V$(1,2)
1100 CALL "IVC.LK",3,2,5,V$
1140 IF V$="" THEN LET V$=I3$(6,2)
1150 LET V$=V$+"  ",V$=V$(1,2)
1160 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=1020)IOL=IVM10A
1170 PRINT @(V1,V2),X0$(4,2),"  ",X0$(6,20)
1180 LET P9$=V$
1190 GOTO 3900
1200 REM " --- Product Type
1220 LET V0$="S",V1$="CE",V2$=P8$,V3$="",V4$="Enter A Valid Product Type, <F1>=All, <F3>=Lookup",V0=3,V1=40,V2=5
1240 GOSUB 7000
1250 IF V3=4 THEN GOTO 9900
1270 IF V3=2 THEN GOTO 1000
1280 IF V3=1 THEN LET P8$=""; PRINT @(V1,V2),'CL',"All Products"; GOTO 1390
1290 IF V3<>3 THEN GOTO 1340
1300 CALL "IVC.LK",1,3,6,V$
1340 LET V$=V$+"   ",V$=V$(1,3)
1360 FIND (IVM10_DEV,KEY=N0$+"A"+V$,DOM=1220)IOL=IVM10A
1370 PRINT @(V1,V2),X0$(4,3),"  ",X0$(7,20)
1380 LET P8$=V$
1390 GOTO 3900
1400 REM " --- Beginning Date
1420 LET V0$="D",V1$="CE",V2$=P7$,V3$="",V4$="Enter Beginning Required Date, <F1>=First",V0=3,V1=40,V2=7
1440 GOSUB 7000
1445 IF V3=4 THEN GOTO 9900
1450 IF V3=1 THEN LET V$=""
1460 IF V$="" THEN LET P7$=""; PRINT @(V1,V2),'CL',"First"; GOTO 1490
1480 LET P7$=V$
1490 GOTO 3900
1600 REM " --- Ending Date
1620 LET V0$="D",V1$="CE",V2$=P6$,V3$="",V4$="Enter Ending Required Date, <F1>=Last",V0=3,V1=40,V2=9
1640 GOSUB 7000
1645 IF V3=4 THEN GOTO 9900
1650 IF V3=1 THEN LET V$=""
1660 IF V$="" THEN LET P6$=""; PRINT @(V1,V2),'CL',"Last"; GOTO 1690
1670 IF V$<P7$ THEN GOTO 1600
1680 LET P6$=V$
1690 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,4000
4000 REM " --- Overlay"
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct? (Y/N): ",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4060 IF V$="N" THEN GOTO 1000
4080 IF V$<>"Y" THEN GOTO 4000
4100 REM " --- Now Let's Sort It
4190 CALL "SYC.NB","Sorting:",12,COLUMN
4200 READ (WOE23_DEV,KEY=N0$,DOM=4220)
4220 LET K$=KEY(WOE23_DEV,END=4400)
4230 IF POS(N0$=K$)<>1 THEN GOTO 4400
4250 PRINT @(COLUMN,11),K$(5,7),"  ",K$(12,3)
4260 READ (WOE23_DEV)IOL=WOE23A
4280 IF P9$<>"" THEN IF W1$(19,2)<>P9$ THEN GOTO 4220
4300 LET SORT$=W0$(1,2)+W1$(19,22)+W0$(5,10)
4310 WRITE (WOW10_DEV,KEY=SORT$)IOL=WOW10A
4320 GOTO 4220
4400 LET H3$="Warehouse: "; IF P9$="" THEN LET H3$=H3$+"All" ELSE LET H3$=H3$+P9$
4410 LET H3$=H3$+"  Products: "; IF P8$="" THEN LET H3$=H3$+"All" ELSE LET H3$=H3$+P8$
4420 LET H3$=H3$+"  Beginning Date: "; IF P7$="" THEN LET H3$=H3$+"First" ELSE LET H3$=H3$+FNB$(P7$)
4440 LET H3$=H3$+" And Ending Date: "; IF P6$="" THEN LET H3$=H3$+"Last" ELSE LET H3$=H3$+FNB$(P6$)
4490 RUN "WOR.MB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

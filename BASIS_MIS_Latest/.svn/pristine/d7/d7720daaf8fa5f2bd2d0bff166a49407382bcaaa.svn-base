0010 REM "IVX - Rebuild Inventory Committed Quantities"
0020 REM "Program IVX.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=11
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="SYS-01",FILES$[2]="SYM-06"
0135 LET FILES$[3]="IVM-02",FILES$[4]="IVM-07"
0140 LET FILES$[5]="ARE-03",FILES$[6]="ARE-07",FILES$[7]="ARE-13"
0145 LET FILES$[8]="ARE-23",FILES$[9]="ARM-10"
0150 LET FILES$[10]="WOE-01",FILES$[11]="WOE-23"
0155 LET OPTIONS$[3]="L",OPTIONS$[4]="L",OPTIONS$[6]="C"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0190 LET SYS01_DEV=CHANNELS[1],SYM06_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 ARE03A: IOLIST X0$
0220 ARE07A: IOLIST C0$
0230 ARE13A: IOLIST B0$,B1$(1),B[ALL]
0240 ARE23A: IOLIST T0$,T1$,T2$,T[ALL]
0250 ARM10E: IOLIST X0$
0260 IVM02A: IOLIST A0$,A1$,A2$,A[ALL]
0270 IVM07A: IOLIST H0$,H1$,H[ALL]
0280 WOE01A: IOLIST X0$
0290 WOE23A: IOLIST W0$,W1$,W[ALL]
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0320 IVS01A: IOLIST *,*,*,X3$
0330 IVS01A1: IOLIST *,*,IV2$,IV3$
0350 SYM06A: IOLIST FIRM06$(1)
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="IV"
0420 DIM INFO$[20]
0425 CALL "SYC.VA","IV",INFO$[ALL]
0430 IF INFO$[3]<>"Y" THEN GOTO 9900
0450 DIM INFO$[20]
0455 CALL "SYC.VA","OP",INFO$[ALL]
0460 LET OP$=INFO$[3]
0480 DIM INFO$[20]
0485 CALL "SYC.VA","WO",INFO$[ALL]
0490 LET WO$=INFO$[3]
0500 REM " --- Initialize Data
0510 LET WIDTH=47,BLANKS$=FILL(WIDTH)
0530 DIM A[20],ENTRY$[4]
0540 LET ENTRY$[1]="IVE-01",ENTRY$[2]="IVE-02"
0545 IF WO$="Y" THEN LET ENTRY$[3]="WOE-14"
0570 REM " --- Verify Rebuild
0580 LET V4$="Do You Wish To Rebuild Inventory Commitments"
0590 CALL "SYC.YN",0,V4$,0,V$,V3
0595 IF V$<>"YES" THEN GOTO 9900
0600 REM " --- Additional Open/Lock Files
0610 IF OP$="Y" THEN GOTO 0640
0620 LET FILES$[5]="      ",FILES$[6]="      ",FILES$[7]="      "
0630 LET FILES$[8]="      ",FILES$[9]="      "
0640 IF WO$="Y" THEN GOTO 0660
0650 LET FILES$[10]="      ",FILES$[11]="      "
0660 CALL "SYC.DA",1,3,11,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0665 IF STATUS>0 THEN GOTO 9900
0675 LET IVM02_DEV=CHANNELS[3],IVM07_DEV=CHANNELS[4]
0680 LET ARE03_DEV=CHANNELS[5],ARE07_DEV=CHANNELS[6],ARE13_DEV=CHANNELS[7]
0685 LET ARE23_DEV=CHANNELS[8],ARM10_DEV=CHANNELS[9]
0690 LET WOE01_DEV=CHANNELS[10],WOE23_DEV=CHANNELS[11]
0700 REM " --- Verify no IV transactions/transfers & no L/S Materials Issues
0720 GOSUB ENTRYFILES
0730 IF INDEX<>0 THEN GOTO 9900
0950 REM " --- Background
0960 CALL "SYC.NB","Updating",WIDTH,COLUMN
1000 REM " --- Clear IVM-02 Committed
1010 PRINT @(COLUMN,11),BLANKS$,
1015 LET N0$=""
1020 READ (IVM02_DEV,KEY=N0$,DOM=1030)
1030 LET K1$=KEY(IVM02_DEV,END=1200)
1040 IF K1$(1,2)<>N0$ THEN GOTO 1080
1045 READ (IVM02_DEV)IOL=IVM02A
1050 LET A[2]=0
1060 WRITE (IVM02_DEV,KEY=K1$)IOL=IVM02A
1065 PRINT @(COLUMN,11),K1$(1,2)," ",K1$(3,2)," ",K1$(5),
1070 GOTO 1030
1080 LET N0$=K1$(1,2)
1085 GOSUB GETIVPARAMS
1090 IF IV_N0$="Y" THEN GOTO 1020
1095 IF NUM(N0$)>=99 THEN GOTO 1200
1100 READ (IVM02_DEV,KEY=STR(NUM(K1$(1,2))+1:"00"),DOM=1190)
1190 GOTO 1030
1200 REM " --- Clear IVM-07 Committed
1205 IF IVM07_DEV=0 THEN GOTO 2000
1210 PRINT @(COLUMN,11),BLANKS$,
1215 LET N0$=""; DIM H[12]
1220 READ (IVM07_DEV,KEY=N0$,DOM=1230)
1230 LET K07$=KEY(IVM07_DEV,END=2000)
1240 IF K07$(1,2)<>N0$ THEN GOTO 1280
1244 READ (IVM07_DEV)IOL=IVM07A
1250 LET H[1]=0
1260 WRITE (IVM07_DEV,KEY=K07$)IOL=IVM07A
1265 PRINT @(COLUMN,11),K07$(1,2)," ",K07$(3,2)," ",K07$(5,I0)," ",K07$(5+I0),
1270 GOTO 1230
1280 LET N0$=K07$(1,2)
1285 GOSUB GETIVPARAMS
1290 IF IV_N0$="Y" THEN GOTO 1220
1295 IF NUM(N0$)>=99 THEN GOTO 2000
1300 READ (IVM07_DEV,KEY=STR(NUM(K07$(1,2))+1:"00"),DOM=1390)
1390 GOTO 1230
2000 REM " --- Rebuild From ARE-13 Committed
2010 IF OP$<>"Y" THEN GOTO 2500
2020 PRINT @(COLUMN,11),BLANKS$,
2030 DIM B1$(64),B[14]
2040 LET N0$=""
2050 READ (ARE13_DEV,KEY=N0$,DOM=2100)
2100 REM " --- ARE-13 Main Read
2105 LET K13$=KEY(ARE13_DEV,END=2500)
2110 IF K13$(1,2)<>N0$ THEN GOTO 2300
2120 FIND (ARE03_DEV,KEY=K13$(1,17)+"000",DOM=HEADERLESS)IOL=ARE03A; REM "Check header records for quotes
2130 GOTO 2145
2135 HEADERLESS: 
2140 READ (ARE13_DEV,KEY=K13$); REM "Move ptr
2143 GOTO 2290
2145 IF X0$(21,1)="P" THEN LET QOTE$="Y" ELSE LET QOTE$="N"
2150 READ (ARE13_DEV)IOL=ARE13A
2160 FIND (ARM10_DEV,KEY=N0$+"E"+B0$(21,1),DOM=2290)IOL=ARM10E
2170 IF POS(X0$(25,1)="MNO")<>0 THEN GOTO 2290
2180 IF X0$(27,1)="Y" THEN GOTO 2250
2185 IF QOTE$="Y" OR B1$(44,1)="N" THEN GOTO 2250
2190 PRINT @(COLUMN,11),K13$(1,2)," ",K13$(5,6)," ",K13$(11,7)," ",K13$(18,3),
2200 READ (IVM02_DEV,KEY=N0$+B0$(31),DOM=2290)IOL=IVM02A
2220 LET A[2]=A[2]+B[2]
2240 WRITE (IVM02_DEV,KEY=N0$+B0$(31))IOL=IVM02A
2250 LET C0$=N0$+B0$(31)+B0$(3,2)+B0$(11,10)+B0$(5,6)
2260 WRITE (ARE07_DEV,KEY=C0$)IOL=ARE07A
2290 GOTO 2100
2300 REM " --- Next ARE-13 Firm
2310 LET N0$=K13$(1,2)
2320 GOSUB GETIVPARAMS
2330 IF IV_N0$="Y" AND OP_N0$="Y" THEN GOTO 2050
2340 IF NUM(N0$)>=99 THEN GOTO 2500
2350 READ (ARE13_DEV,KEY=STR(NUM(K13$(1,2))+1:"00"),DOM=2390)
2390 GOTO 2100
2500 REM " --- Rebuild From ARE-23 Committed
2505 IF OP$<>"Y" THEN GOTO 3000
2510 IF ARE23_DEV=0 THEN GOTO 3000
2560 PRINT @(COLUMN,11),BLANKS$,
2570 DIM H[12],T[2]
2580 LET N0$="",B0$=""
2590 READ (ARE23_DEV,KEY=N0$,DOM=2600)
2600 REM " --- ARE-23 Main Read
2610 LET K23$=KEY(ARE23_DEV,END=3000)
2630 IF K23$(1,2)<>N0$ THEN GOTO 2800
2640 READ (ARE23_DEV)IOL=ARE23A
2650 PRINT @(COLUMN,11),K23$(1,2)," ",K23$(5,6)," ",K23$(11,7),"-",K23$(18,3)," ",K23$(21,3),
2660 IF POS(K23$(1,20)=B0$)<>1 THEN FIND (ARE13_DEV,KEY=K23$(1,20),DOM=2750)IOL=ARE13A
2670 READ (IVM07_DEV,KEY=N0$+B0$(31)+T1$,DOM=2750)IOL=IVM07A
2680 LET H[1]=H[1]+T[0]
2690 WRITE (IVM07_DEV,KEY=H0$)IOL=IVM07A
2700 GOTO 2600
2750 REM " --- Remove Unmatched ARE-23 Records
2760 REMOVE (ARE23_DEV,KEY=K23$)
2790 GOTO 2600
2800 REM " --- Next ARE-23 Firm
2810 LET N0$=K23$(1,2)
2820 GOSUB GETIVPARAMS
2830 IF IV_N0$="Y" AND LS_N0$="Y" AND OP_N0$="Y" THEN GOTO 2590
2840 IF NUM(N0$)>=99 THEN GOTO 3000
2850 READ (ARE23_DEV,KEY=STR(NUM(K23$(1,2))+1:"00"),DOM=2890)
2890 GOTO 2600
3000 REM " --- Rebuild From WOE-23 Committed
3010 IF WO$<>"Y" THEN GOTO 4000
3020 PRINT @(COLUMN,11),BLANKS$,
3030 DIM W[4]
3040 LET N0$=""
3050 READ (WOE23_DEV,KEY=N0$,DOM=3100)
3100 REM " --- WOE-23 Main Read
3110 LET KW23$=KEY(WOE23_DEV,END=4000)
3120 IF KW23$(1,2)<>N0$ THEN GOTO 3300
3130 READ (WOE23_DEV)IOL=WOE23A
3140 PRINT @(COLUMN,11),KW23$(1,2)," ",KW23$(5,7)," ",KW23$(12,3)," ",W1$(21),
3150 READ (WOE01_DEV,KEY=KW23$(1,11),DOM=3250)IOL=WOE01A
3160 IF X0$(15,1)="C" THEN GOTO 3250; REM "Closed WO
3170 READ (IVM02_DEV,KEY=N0$+W1$(19),DOM=3100)IOL=IVM02A
3180 LET A[2]=A[2]+W[0]-W[1]
3190 WRITE (IVM02_DEV,KEY=A0$)IOL=IVM02A
3200 GOTO 3100
3250 REM " --- Remove Unmatched and Closed WOE-23 Records
3260 REMOVE (WOE23_DEV,KEY=KW23$,DOM=3100)
3290 GOTO 3100
3300 REM " --- Next WOE-23 Firm
3310 LET N0$=KW23$(1,2)
3320 GOSUB 6000
3330 IF IV_N0$="Y" AND WO_N0$="Y" THEN GOTO 3050
3340 IF NUM(N0$)>=99 THEN GOTO 4000
3350 READ (WOE23_DEV,KEY=STR(NUM(KW23$(1,2))+1:"00"),DOM=3390)
3390 GOTO 3100
4000 REM " --- Done
4010 GOTO 9900
6000 REM " --- Make sure appropriate entry files are empty"
6005 ENTRYFILES: 
6020 LET INDEX=0,NUMBER=4
6030 DIM FIRM06$(2),MESSAGE$[NUMBER]
6040 READ (SYM06_DEV,KEY="",DOM=6050)
6050 LET K$=KEY(SYM06_DEV,END=6150)
6060 READ (SYM06_DEV,KEY=K$)IOL=SYM06A
6070 FOR X=1 TO 3
6080 LET FILENAME$=ENTRY$[X],BATCH=0
6090 IF FILENAME$="" THEN GOTO 6130
6100 CALL "SYC.BC",FIRM06$,FILENAME$,DESCRIPTION$,BATCH,STATUS
6110 IF STATUS>0 THEN GOSUB 6200
6120 IF INDEX=NUMBER THEN EXITTO 6150
6130 NEXT X
6140 GOTO 6050
6150 IF INDEX=0 THEN GOTO 6180
6160 LET MESSAGE$[INDEX]=FILL(20)+"Press <Enter> To Continue"
6170 CALL "SYC.XA",3,MESSAGE$[ALL],INDEX,-1,-1,V$,V3
6180 RETURN 
6200 REM " --- Entry file contains records
6210 ON X GOTO 6340,TRANSACTION,TRANSFER,MATERIALS,6340
6220 TRANSACTION: 
6230 LET MESSAGE$[INDEX]="Inventory Transaction Register and Update must be run for Firm "+FIRM06$+".",INDEX=INDEX+1
6240 GOTO 6340
6250 TRANSFER: 
6260 LET MESSAGE$[INDEX]="Inventory Transfer Register and Update must be run for Firm "+FIRM06$+".",INDEX=INDEX+1
6270 GOTO 6340
6280 MATERIALS: 
6290 FIND (SYS01_DEV,KEY=K$(1,2)+"WO00",DOM=6340); REM "Verify WO installed for this Firm
6300 FIND (SYS01_DEV,KEY=K$(1,2)+"IV00",DOM=6340)IOL=IVS01A; REM "Verify IV installed for this Firm
6310 IF POS(X3$(17,1)="LS")=0 THEN GOTO 6340; REM "Verify Serialized/Lotted inventory for this Firm
6320 LET MESSAGE$[INDEX]="Materials Issues Register and Update must be run for Firm "+FIRM06$+".",INDEX=INDEX+1
6340 RETURN 
6400 REM " --- Next Firm's Inventory Parameters
6405 GETIVPARAMS: 
6410 LET IV_N0$="N",LS_N0$="N",OP_N0$="N",WO_N0$="N"
6420 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=6590)IOL=IVS01A1
6430 LET IV_N0$="Y"
6440 IF POS(IV3$(17,1)="LS")>0 THEN LET LS_N0$="Y" ELSE LET LS_N0$="N"
6450 LET I0=NUM(IV2$(1,2))
6460 PRECISION NUM(IV2$(5,1))
6500 REM " --- Next Firm's Order Processing Parameters
6510 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=6590)
6520 LET OP_N0$="Y"
6550 REM " --- Next Firm's Work Order Parameters
6560 FIND (SYS01_DEV,KEY=N0$+"WO00",DOM=6590)
6570 LET WO_N0$="Y"
6590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

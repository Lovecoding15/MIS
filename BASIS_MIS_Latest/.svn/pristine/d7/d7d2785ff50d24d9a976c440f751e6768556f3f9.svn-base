0010 REM "POM - Standard Message Maintenance"
0020 REM "Program POM.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="POM-04",FILES$[3]="POM-14"
0140 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],POM04_DEV=CHANNELS[2],POM14_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 POM04A: IOLIST A0$(1),A1$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N4$=F5$
0500 REM " --- Initializations"
0510 DIM A0$(5),A1$(32),INFO$[5],INFO[5]
0550 LET INFO$[0]="POM-14",INFO[1]=7,INFO[3]=2,INFO[4]=40,INFO[5]=1
0590 LET A0$(1)=N0$,NUMBER=1
0700 REM " --- Background"
0710 PRINT 'SB',@(1,3),"Message Code:",@(2,4),"Description:",@(1,5),"Message Type:",'SF',
0900 REM " --- Position File"
0990 READ (POM04_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Message Code"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(POM04_DEV,END=1050)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3)
1050 LET V0$="S",V1$="KE",V3$="",V4$="Enter A Valid Message Code (<F3>=Lookup)",V0=3,V1=15,V2=3
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN GOSUB 6000
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(3)=V$
1200 REM " --- Retrieve record"
1210 FIND (POM04_DEV,KEY=A0$,DOM=1400)IOL=POM04A
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Initialize new record"
1510 DIM A1$(32)
1520 LET A1$(21,1)="B"
1530 GOSUB 5000
2000 REM " --- Description"
2010 LET V0$="S",V1$="",V2$=A1$(1,20),V3$="",V0=20,V1=15,V2=4,I0=0
2020 LET V4$="Enter The Description For This Message Code"
2040 GOSUB 7000
2050 ON V3 GOTO 2060,2000,3900,2000,3900
2060 LET A1$(1,20)=V$
2090 GOTO 3900
2200 REM " --- Message Type"
2210 LET V4$="Where Should This Message Be Printed (R=Requisition/P=PO/B=Both/N=Neither)"
2220 LET V0$="S",V1$="CR",V2$=A1$(21,1),V3$="RPBN",V0=1,V1=15,V2=5
2230 GOSUB 7000
2250 LET A1$(21,1)=V$
2290 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record"
4110 WRITE (POM04_DEV,KEY=A0$)IOL=POM04A
4115 LET INFO[4]=60; REM "WA:200
4120 LET INFO$[1]=A0$,INFO$[2]="Message "+A0$(3)+" "+A1$(1,20)
4150 CALL "SYC.KA",0,INFO$[ALL],INFO[ALL]
4190 GOTO 1000
4200 REM " --- Position detail file"
4290 REMOVE (POM14_DEV,KEY=A0$,DOM=4300)
4300 REM " --- Remove detail lines"
4310 LET K$=KEY(POM14_DEV,END=4400)
4320 IF POS(A0$=K$)<>1 THEN GOTO 4400
4330 REMOVE (POM14_DEV,KEY=K$,DOM=4400)
4390 GOTO 4300
4400 REM " --- Remove header"
4410 REMOVE (POM04_DEV,KEY=A0$,DOM=1000)
4490 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4900,9900
4900 REM " --- Report Initializations"
4990 RUN "POM.DB",ERR=9900
5000 REM " --- Display"
5010 PRINT @(15,3),A0$(3),@(15,4),A1$(1,20),@(15,5),A1$(21,1),
5090 RETURN 
6000 REM " --- Code Lookup"
6010 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6020 LET TITLE$="Message Code Lookup"
6030 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=3,DESCRIPTION$[0]="Code"
6040 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=20,DESCRIPTION$[1]="Description"
6050 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,V$="SKIP",ROW=V2+2
6060 CALL "SYC.LK",POM04_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

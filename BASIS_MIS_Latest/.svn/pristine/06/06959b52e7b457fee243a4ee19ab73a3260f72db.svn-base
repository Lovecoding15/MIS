0010 REM "APM - DETAIL LISTING"
0020 REM "Program APM.EF"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Jul96 JAL: Possible pagination problem when printing balance summary at end of the detail listing (4010,4320)
0032 REM "19Jan98 WGH: Fix pagination for Checks and Other Transactions (510,550,2320,2820,5180,5405)"
0033 REM "16Sep99 WGH: Correct report line count when there are no checks to report (2510)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0240 APM13A: IOLIST C0$,C1$(1),C[ALL]
0310 APM11A: IOLIST W0$,W1$,W[ALL]
0320 APM12A: IOLIST W0$,W1$,W[ALL]
0400 REM " --- Parameters"
0500 REM " --- Init Data"
0510 LET HEADINGS=2; DIM HEADINGS$[HEADINGS]
0540 LET H0=80,H=0,HEADINGS$[0]=N1$,HEADINGS$[1]=F5$,WHEN$=F0$(7,3)
0550 LET L6=59,L5=L6+1,T0=0,T1=0,T2=0,T3=0,S0=0,S1=0,S2=0,S3=0
0560 LET CLOCK$=""
0800 REM " --- Options"
0820 CALL "SYC.YN",1,"Do You Wish To Print Outstanding Items Only",1,V$,V3
0840 IF V3=4 THEN GOTO 4970
0850 IF V$="Y" OR V$="N" THEN GOTO 0900
0860 GOTO 0800
0900 REM 
0910 CALL "SYC.GA",7,1,"","",STATUS
0920 IF STATUS THEN GOTO 4900
1000 REM " --- Initial Read"
1010 DIM C1$(190),C[3]
1100 REM " --- Main Read"
1150 READ (APM13_DEV,KEY=A0$,DOM=1160)IOL=APM13A
1180 GOSUB 5000
2000 REM " --- Print Checks"
2020 READ (APM11_DEV,KEY=A0$(1,2+P[4]),DOM=2040)
2040 GOSUB 5200
2100 REM " --- Go Next Key"
2120 LET K$=KEY(APM11_DEV,END=2500)
2140 IF K$(1,2+P[4])<>A0$(1,2+P[4]) THEN GOTO 2500
2150 READ (APM11_DEV)IOL=APM11A
2200 REM " --- Print Checks"
2220 IF V$="Y" AND W1$(2,1)<>"O" THEN GOTO 2100
2230 IF W1$(3,6)>C1$(7,6) THEN GOTO 2100
2240 IF W1$(2,1)<>"O" THEN LET T2=T2+W[0],S2=S2+1; GOTO 2300
2290 LET T0=T0+W[0],S0=S0+1
2320 IF L5<L6 THEN GOTO 2350
2330 GOSUB 5000
2340 GOSUB 5200
2350 PRINT (7)@(0),W0$(3+P[4],7),@(11),W1$(1,1),@(16),FNB6$(W1$(3,6)),@(27),W1$(9,30),@(72-M1),W[0]:M1$,@(74),W1$(2,1)
2380 LET L5=L5+1
2490 GOTO 2100
2500 REM " --- Print Transactions"
2510 IF S0+S2=0 THEN PRINT (7)@(20),"Nothing To Report"; LET L5=L5+1
2520 READ (APM12_DEV,KEY=A0$(1,2+P[4]),DOM=2540)
2540 GOSUB 5400
2600 REM " --- Get Next Key"
2620 LET K$=KEY(APM12_DEV,END=3000)
2640 IF K$(1,2+P[4])<>A0$(1,2+P[4]) THEN GOTO 3000
2650 READ (APM12_DEV)IOL=APM12A
2700 REM " --- Print Transactions"
2720 IF V$="Y" AND W1$(2,1)<>"O" THEN GOTO 2600
2730 IF W1$(3,6)>C1$(7,6) THEN GOTO 2600
2740 IF W1$(2,1)<>"O" THEN LET T3=T3+W[0],S3=S3+1; GOTO 2800
2790 LET T1=T1+W[0],S1=S1+1
2820 IF L5<L6 THEN GOTO 2850
2830 GOSUB 5000
2840 GOSUB 5400
2850 PRINT (7)@(0),W0$(3+P[4],7),@(11),W1$(1,1),@(16),FNB6$(W1$(3,6)),@(27),W1$(9,30),@(72-M1),W[0]:M1$,@(74),W1$(2,1)
2880 LET L5=L5+1
2990 GOTO 2600
3000 REM " --- Print Recap"
3010 IF S1+S3=0 THEN PRINT (7)@(20),"Nothing To Report"
4000 REM " --- Options"
4010 IF L5>L6-15 THEN GOSUB 5000
4020 GOSUB 5600
4100 LET T5=C[1]-T0+T1
4120 PRINT (7)@(10),"Statement Balance: ",@(42),C[1]:M1$
4140 PRINT (7)@(10),"Checks Outstanding: ",@(42),T0:M1$,S0:M2$
4160 PRINT (7)@(10),"Transactions Outstanding: ",@(42),T1:M1$,S1:M2$
4180 PRINT (7)@(42),FILL(M1)
4200 PRINT (7)@(10),"Ending Balance:",@(42),T5:M1$
4220 PRINT (7)'LF',@(10),"Check Book Balance",@(42),C[2]:M1$,"  ",FNB6$(C1$(7,6))
4230 IF C[2]=T5 THEN GOTO 4300
4240 LET T9$=" OVER"
4250 IF C[2]>T5 THEN LET T9$="SHORT"
4260 PRINT (7)'LF',@(9),"This Account Is *** ",T9$," *** By ",C[2]-T5:M1$
4300 IF V$<>"N" THEN GOTO 4400
4320 PRINT (7)'LF'
4340 PRINT (7)@(10),"Paid Checks:",@(42),T2:M1$,S2:M2$
4360 PRINT (7)@(10),"Paid Transactions:",@(42),T3:M1$,S3:M2$
4900 REM 
4970 CLOSE (7,ERR=4975)
4975 PRINT @(0,L0+1),'CE',
4980 IF O0=2 THEN RUN "APM.EE"
4985 LET O0=1
4990 RUN "APM.EA"
5000 REM " --- Report Heading"
5010 LET L=0,L5=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],2,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)@(5),"Bank Account Number: ",C1$(43,15),@(45),"Cash Account Number: ",FNF$(A0$(3,P[3]),M0$)
5110 PRINT (7)@(12),"Account Name: ",C1$(13,30)
5120 PRINT (7)@(12),"   Bank Name: ",C1$(58,30)
5180 LET L5=L5+3
5190 RETURN
5200 REM " --- Checks"
5210 PRINT (7)'LF',@(0),J$,@(10),"Checks"
5220 PRINT (7)@(0)," Number  Type  Check Date  Description                          Amount  Paid"
5230 PRINT (7)@(0),J$
5240 LET L5=L5+4
5290 RETURN
5400 REM " --- Transactions"
5405 IF L5>L6-4 THEN GOSUB 5000
5410 PRINT (7)'LF',@(0),J$,@(10),"Other Transactions"
5420 PRINT (7)@(0)," Number  Type  Trans Date  Description                          Amount  Paid"
5430 PRINT (7)@(0),J$
5440 LET L5=L5+4
5490 RETURN
5600 REM " --- Total"
5610 PRINT (7)'LF',@(0),J$,@(10),"Balance Information",'LF'
5690 RETURN
8000 REM " --- Functions"
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
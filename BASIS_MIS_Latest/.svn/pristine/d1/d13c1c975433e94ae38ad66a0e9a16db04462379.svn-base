0010 REM "ARR - Aging Report"
0020 REM "Program ARR.4C"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "27Aug96 WGH: Future agings not printing in summary report (6430)"
0032 REM "29Jan97 JAL: Don't reprint customer subheading after totals (5805,6490)"
0033 REM "03Nov97 KJS: Cosmetic changes (537-543,595-597,5930-5945,6460)
0034 REM "16Jun98 WGH: When aging on Due Date with NO Future Agings, current transactions must still appear as Future when the due date is in the future (525,530,596,4050,5060,5080,5935,6045,6470)"
0035 REM "19May99 KJS: Prevent error one when total values overflow mask (2940,4060,4070,5940,5945,6040,6050,6475,6480,8400-8445)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open Files
0200 REM " --- IOLists
0220 ARM01A: IOLIST B0$,B1$
0230 ARM02A: IOLIST C0$,C1$,C[ALL]
0240 ARM05A: IOLIST D0$,D1$
0250 ARM09A: IOLIST *,JOB$(1)
0270 ARM10F: IOLIST S1$
0280 ARM10A: IOLIST H3$
0300 ART01A: IOLIST A0$,A[ALL]
0310 ART03A: IOLIST Y0$(1)
0320 ART11A: IOLIST W0$,W1$,W[ALL]
0500 REM " --- Init Data
0510 LET HEADINGS=4,CLOCK$="",WIDTH=132,H$=H9$
0515 IF R6$="S" THEN LET HEADINGS=5
0520 DIM K1$(48),S[5],HEADINGS$[HEADINGS]
0525 LET H6$="Based On Due Date",AGEDATE$="  Due"
0530 IF P6$="I" THEN LET H6$="Based On Invoice Date",AGEDATE$="Invoiced"
0535 IF R7$="A" THEN LET H7$="Alpha" ELSE LET H7$="Numeric"
0537 IF R6$="C" THEN GOTO 0545
0540 LET H7$="Salesperson"
0541 IF S3$<>"" THEN LET H8$=S3$ ELSE LET H8$="First"
0542 IF S4$<>"" THEN LET H9$=S4$ ELSE LET H9$="Last"
0543 GOTO 0555
0545 IF R8$<>"" THEN LET H8$=FNF$(R8$(1,P[0]),M0$) ELSE LET H8$="First"
0550 IF R9$<>"" THEN LET H9$=FNF$(R9$(1,P[0]),M0$) ELSE LET H9$="Last"
0555 IF P7$="S" THEN LET H2$="Summary Aging Report" ELSE LET H2$="Detail Aging Report"
0560 LET H2$=H2$+" As Of "+FNB$(P9$)
0565 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$,HEADINGS$[2]=H6$
0570 LET HEADINGS$[3]=H7$+" Sort From "+H8$+" To "+H9$
0585 LET M9$=M1$; IF POS("("=M9$)=1 THEN LET M9$=M9$(1,1)+"#"+M9$(2) ELSE LET M9$="#"+M9$
0590 LET M9=LEN(M9$)
0595 LET UL$=FILL(6*M1+M9,"-")
0596 IF P5$="N" AND P6$="I" THEN LET UL$(1,M1)=""
0597 IF P7$="D" THEN LET UL_TAB=132-6*M1-M9 ELSE LET UL_TAB=32
1000 REM " --- Init Read
1002 CALL "SYC.WC",1,0,80,0,0,4,0
1005 CALL "SYC.NB","Printing:",M0,COLUMN
1010 LET T1$="",T9$=""
1020 READ (ART01_DEV,KEY=N0$+"  "+R8$,DOM=1030)
1030 IF R7$="A" THEN READ (ARM04_DEV,KEY=N0$+"A",DOM=1035)
1035 IF R6$="S" THEN READ (ARM04_DEV,KEY=N0$+"B"+S3$,DOM=1050)
1050 IF R6$<>"S" THEN GOTO 6800
1100 REM " --- Main Read
1110 REM " ---  Sort by Salesperson
1120 IF R6$<>"S" THEN GOTO 1310
1130 LET K2$=KEY(ARM04_DEV,END=4000)
1140 READ (ARM04_DEV)
1150 IF K2$(3,1)<>"B" THEN GOTO 4000
1160 IF S4$<>"" THEN IF K2$(4,3)>S4$ THEN GOTO 4000
1170 READ (ART01_DEV,KEY=N0$+"  "+K2$(7,6),DOM=1180)
1180 IF LEN(O2$)=0 THEN LET O2$=K2$; GOSUB 5700
1190 LET K$=KEY(ART01_DEV,END=1110)
1200 IF K$(1,2)<>N0$ THEN GOTO 1110 ELSE GOTO 1330
1300 REM " ---  Sort by Customer
1310 LET K$=KEY(ART01_DEV,END=6900)
1320 IF N0$<>K$(1,2) THEN GOTO 6900
1330 IF R6$="S" THEN IF K$(5,6)<>K2$(7,6) THEN GOTO 1110
1340 READ (ART01_DEV,KEY=K$)IOL=ART01A
1350 IF LEN(K$)<19 THEN GOTO 1110
1360 IF A0$(1,2)<>N0$ THEN GOTO 1310
1370 IF R8$<>"" THEN IF A0$(5,6)<R8$ THEN GOTO 1310
1380 IF R7$="N" THEN IF R9$<>"" THEN IF A0$(5,6)>R9$ THEN GOTO 4020
1390 IF R7$="A" THEN IF R9$<>"" THEN IF A0$(5,6)>R9$ THEN GOTO 1310
1410 LET Y0=A[0]
1420 IF P5$="N" THEN IF A0$(24,3)>P9$ THEN LET A[0]=0
1430 IF F0$(13,1)<>"T" THEN PRINT @(COLUMN,11),FNF$(A0$(5,P[0]),M0$),
1510 REM " --- Breaks
1520 IF K$(3,2)<>T0$ THEN GOSUB 6000
1530 IF K$(5,6)<>T1$ THEN GOSUB 6400
1540 IF R7$="A" THEN IF K1$(14,6)<>T1$ THEN GOTO 6800
1550 IF R7$="N" OR R6$="S" THEN IF K$(5,6)<>T1$ THEN GOSUB 6400
1560 IF R6$="S" THEN IF K2$(4,3)<>O2$(4,3) THEN GOSUB 5900; GOSUB 5700
2000 REM " --- Determine Inv Amt
2100 REM " --- Check Detail
2110 READ (ART11_DEV,KEY=A0$(1,17),DOM=2120)
2120 LET K11$=KEY(ART11_DEV,END=2200)
2140 IF K11$(1,17)<>A0$(1,17) THEN GOTO 2200
2150 READ (ART11_DEV)IOL=ART11A
2155 IF W1$="" THEN GOTO 2190
2170 IF W1$(1,1)=" " THEN GOTO 2190
2172 LET Y0=Y0+W[0]+W[1]
2175 IF P5$="N" THEN IF W1$(2,3)>P9$ THEN GOTO 2190
2180 LET A[0]=A[0]+W[0]+W[1]
2190 GOTO 2120
2200 REM " --- Zero
2300 REM " --- Date
2310 LET X3$=A0$(27,3)
2320 IF P6$="I" THEN LET X3$=A0$(24,3)
2400 REM " --- Age
2420 LET X0=POS(X3$>H4$,3)
2440 IF X0=0 THEN GOTO 2490
2450 LET X0=INT(X0/3)
2455 IF X0<0 THEN LET X0=0
2460 GOTO 2502
2490 LET X0=5
2502 IF A[0]=0 THEN GOTO 3220
2505 IF L1=0 THEN GOSUB 5800
2510 IF P7$<>"D" THEN GOTO 3000
2520 DIM Y0$(117),JOB$(145)
2530 LET X8$="None      ",X9$="None   "
2600 REM " --- PO # and Order # from ART-04
2620 IF P3$(77,1)<>"Y" OR C1$(38,1)<>"Y" THEN GOTO 2900
2630 FIND (ART03_DEV,KEY=A0$(1,17)+"000",DOM=2900)IOL=ART03A
2640 LET X8$=Y0$(49,10),X9$=Y0$(42,7)
2900 REM " --- Print Detail
2920 IF L+1>L9 THEN GOSUB 5800
2930 LET Q$=FNC$(X3$)
2940 PRINT (7)@(2),A0$(11,7),"  ",Q$(1,2)+"/"+Q$(3,2)+"/"+Q$(5,2),@(20),X8$,@(31),X9$,@(39),A0$(22,2),@(45),A0$(33,1),@(131-(6-X0)*M1-M9),A[0]:M1$,@(131-M9),FNMASK$(A[0],M9$)
2960 LET L=L+1
3000 REM " --- Job Number
3010 IF P3$(12,1)<>"Y" OR P7$<>"D" THEN GOTO 3200
3020 DIM JOB$(145)
3040 READ (ARM09_DEV,KEY=Y0$(1,2)+Y0$(5,6)+Y0$(86,10),DOM=3200)IOL=ARM09A
3070 IF L+1>L9 THEN GOSUB 5800
3080 PRINT (7)@(12),"Job: ",Y0$(86,10),"  ",JOB$(1,30)
3090 LET L=L+1
3200 REM " --- Accumulate
3210 LET L1=L1+1
3220 LET T[X0]=T[X0]+A[0],U[X0]=U[X0]+A[0],X[X0]=X[X0]+A[0],Y[X0]=Y[X0]+Y0,S[X0]=S[X0]+A[0]
3290 GOTO 1310
4000 REM " --- Totals
4020 LET T9$="E"
4040 GOSUB 6000
4043 IF R6$="S" THEN GOSUB 5900
4045 IF H=0 THEN GOSUB 5000
4050 IF P5$="Y" OR P6$="D" THEN LET FUTRU0$=STR(U[0]:M1$) ELSE LET FUTRU0$=""
4060 IF P7$="D" THEN PRINT (7)'LF',@(10),"Grand Total:",@(131-6*M1-M9),FUTRU0$,@(131-5*M1-M9),U[1]:M1$,@(131-4*M1-M9),U[2]:M1$,@(131-3*M1-M9),U[3]:M1$,@(131-2*M1-M9),U[4]:M1$,@(131-M1-M9),U[5]:M1$,@(131-M9),FNMASK$(U[0]+U[1]+U[2]+U[3]+U[4]+U[5],M9$)
4070 IF P7$="S" THEN PRINT (7)'LF',@(10),"Grand Total:",@(32),FUTRU0$,@(32+M1),FNMASK$(U[1],M1$),FNMASK$(U[2],M1$),FNMASK$(U[3],M1$),FNMASK$(U[4],M1$),FNMASK$(U[5],M1$),FNMASK$(U[0]+U[1]+U[2]+U[3]+U[4]+U[5],M9$)
4100 GOTO 9900
5000 REM " --- Heading
5020 LET L=HEADINGS+1
5025 IF R6$="S" THEN LET HEADINGS$[4]=S1$
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS>0 THEN EXITTO 9900
5060 IF P5$="Y" OR P6$="D" THEN LET FUTR$="Future" ELSE LET FUTR$=""
5065 IF P7$="S" THEN GOTO 5100
5070 PRINT (7)@(0),"Customer No. & Name",@(32),"Order",@(53),"Contact"
5080 PRINT (7)@(2),"Invoice  "+AGEDATE$,@(20),"PO Number",@(31),"Number",@(39),"Terms",@(45),"Type",@(131-5*M1-M9-7),FUTR$,@(131-4*M1-M9-8),"Current",@(131-3*M1-M9-8),"30-Days",@(131-2*M1-M9-8),"60-Days",@(131-M1-M9-8),"90-Days",@(131-M9-9),"120-Days",@(131-8),"Balance",'LF'
5085 LET L=L+3
5090 GOTO 5190
5100 PRINT (7)@(0),"Customer No. & Name",@(32+M1-7),FUTR$,@(32+2*M1-8),"Current",@(32+3*M1-8),"30-Days",@(32+4*M1-8),"60-Days",@(32+5*M1-8),"90-Days",@(32+6*M1-9),"120-Days",@(32+6*M1+M9-8),"Balance",'LF',
5110 LET L=L+2
5195 RETURN 
5700 REM " --- Salesperson
5710 LET S1$=K2$(4,3)+": Undefined Salesperson"
5720 READ (ARM10_DEV,KEY=N0$+"F"+K2$(4,3),DOM=5799)IOL=ARM10F
5730 LET S1$=K2$(4,3)+": "+FNP$(S1$(7,20))
5799 RETURN 
5800 REM " --- Customer
5805 IF X0$="" THEN GOTO 5899
5810 IF L+2>L9 THEN GOSUB 5000
5820 IF R8$<>"" THEN IF A0$(5,6)<R8$ THEN GOTO 5850
5830 IF R9$<>"" THEN IF A0$(5,6)>R9$ THEN GOTO 5850
5838 IF P7$<>"D" THEN GOTO 5890
5840 PRINT (7)@(0),X1$," ",X0$,"(",X2$,")",@(53),X4$,'LF',
5845 LET L=L+1,L0=L0+1
5847 IF R4$="Y" THEN GOSUB 6300
5850 RETURN 
5890 PRINT (7)'LF',@(0),X1$," ",X0$,
5895 LET L=L+1,L0=L0+1
5899 RETURN 
5900 REM " --- Salesperson Break
5910 IF L0=0 THEN GOTO 5950
5920 IF LEN(O2$)=0 THEN DIM O2$(19)
5925 IF L+2>L9 THEN GOSUB 5000
5930 PRINT (7)@(UL_TAB),UL$
5935 IF P5$="Y" OR P6$="D" THEN LET FUTRS0$=STR(S[0]:M1$) ELSE LET FUTRS0$=""
5940 IF P7$="D" THEN PRINT (7)@(8),"Total For ",O2$(4,3),@(131-6*M1-M9),FUTRS0$,@(131-5*M1-M9),S[1]:M1$,@(131-4*M1-M9),S[2]:M1$,@(131-3*M1-M9),S[3]:M1$,@(131-2*M1-M9),S[4]:M1$,@(131-M1-M9),S[5]:M1$,@(131-M9),FNMASK$(S[0]+S[1]+S[2]+S[3]+S[4]+S[5],M9$)
5945 IF P7$="S" THEN PRINT (7)@(8),"Total For ",O2$(4,3),@(32),FUTRS0$,@(32+M1),FNMASK$(S[1],M1$),FNMASK$(S[2],M1$),FNMASK$(S[3],M1$),FNMASK$(S[4],M1$),FNMASK$(S[5],M1$),FNMASK$(S[0]+S[1]+S[2]+S[3]+S[4]+S[5],M9$)
5950 DIM S[5]
5960 LET O2$=K2$,L=L9+1
5970 LET L0=0
5990 RETURN 
6000 REM " --- AR Type
6010 IF T0$="" THEN GOTO 6100
6030 GOSUB 6400
6040 IF P3$(1,1)<>"Y" THEN GOTO 6100
6045 IF P5$="Y" OR P6$="D" THEN LET FUTRT0$=STR(T[0]:M1$) ELSE LET FUTRT0$=""
6050 IF P7$="D" THEN PRINT (7)@(131-6*M1-M9),FUTRT0$,@(131-5*M1-M9),T[1]:M1$,@(131-4*M1-M9),T[2]:M1$,@(131-3*M1-M9),T[3]:M1$,@(131-2*M1-M9),T[4]:M1$,@(131-M1-M9),T[5]:M1$,@(131-M9),FNMASK$(T[0]+T[1]+T[2]+T[3]+T[4]+T[5],M9$)
6060 IF P7$="S" THEN PRINT (7)@(32),FUTRT0$,@(32+M1),FNMASK$(T[1],M1$),FNMASK$(T[2],M1$),FNMASK$(T[3],M1$),FNMASK$(T[4],M1$),FNMASK$(T[5],M1$),FNMASK$(T[0]+T[1]+T[2]+T[3]+T[4]+T[5],M9$)
6100 IF T9$="E" THEN RETURN 
6120 DIM T[5]
6140 LET T0$=A0$(3,2),L=L9+1,T9$=""
6150 IF P3$(1,1)<>"Y" THEN RETURN 
6160 LET H3$=""
6170 FIND (ARM10_DEV,KEY=N0$+"A"+T0$,DOM=6290)IOL=ARM10A
6180 LET H3$=FNP$(H3$(6,20))
6290 RETURN 
6300 REM " --- Customer Comments
6310 LET L3=0
6320 READ (ARM05_DEV,KEY=A0$(1,2)+A0$(5,6),DOM=6330)
6330 LET D0$=KEY(ARM05_DEV,END=6390)
6340 IF D0$(1,8)<>A0$(1,2)+A0$(5,6) THEN GOTO 6390
6350 READ (ARM05_DEV,KEY=D0$)IOL=ARM05A
6365 IF POS(" "<>D1$)=0 THEN GOTO 6330
6370 IF L+1>L9 THEN GOSUB 5800
6372 LET L3=L3+1
6375 IF FPT(L3/2)<>0 THEN LET L4=0 ELSE LET L4=54
6380 PRINT (7)@(10+L4),STR(L3:"00")," ",D1$,
6382 IF FPT(L3/2)=0 THEN PRINT (7)'LF',; LET L=L+1
6384 GOTO 6330
6390 IF FPT(L3/2)<>0 THEN PRINT (7)'LF',; LET L=L+1
6392 PRINT (7)""
6394 LET L=L+1
6399 RETURN 
6400 REM " --- Customer Break
6410 IF T1$="" THEN GOTO 6500
6420 IF L1=0 THEN GOTO 6497
6430 IF P7$="S" THEN GOTO 6470
6440 IF L1=1 THEN GOTO 6492
6450 IF L+2>L9 THEN GOSUB 5000
6460 PRINT (7)@(UL_TAB),UL$
6465 LET L=L+1
6470 IF P5$="Y" OR P6$="D" THEN LET FUTRX0$=STR(X[0]:M1$) ELSE LET FUTRX0$=""
6475 IF P7$="D" THEN PRINT (7)@(131-6*M1-M9),FUTRX0$,@(131-5*M1-M9),X[1]:M1$,@(131-4*M1-M9),X[2]:M1$,@(131-3*M1-M9),X[3]:M1$,@(131-2*M1-M9),X[4]:M1$,@(131-M1-M9),X[5]:M1$,@(131-M9),FNMASK$(X[0]+X[1]+X[2]+X[3]+X[4]+X[5],M9$)
6480 IF P7$="S" THEN PRINT (7)@(32),FUTRX0$,@(32+M1),FNMASK$(X[1],M1$),FNMASK$(X[2],M1$),FNMASK$(X[3],M1$),FNMASK$(X[4],M1$),FNMASK$(X[5],M1$),FNMASK$(X[0]+X[1]+X[2]+X[3]+X[4]+X[5],M9$)
6490 LET L=L+1,X0$=""
6492 IF P7$<>"D" THEN GOTO 6497
6493 IF L+1>L9 THEN GOSUB 5800
6494 PRINT (7)'LF',
6496 LET L=L+1
6497 LET C[0]=Y[0],C[1]=Y[1],C[2]=Y[2],C[3]=Y[3],C[4]=Y[4],C[5]=Y[5]
6498 IF R5$="Y" THEN WRITE (ARM02_DEV,KEY=C0$)IOL=ARM02A
6500 IF T9$<>"" THEN RETURN 
6510 DIM X1$(M0),C1$(40),C[10]
6520 LET X0$="Not On File",X2$="",C0$=N0$+A0$(5,6)+A0$(3,2)
6550 FIND (ARM01_DEV,KEY=N0$+A0$(5,6),DOM=6570)IOL=ARM01A
6560 LET X0$=B1$(1,30),X2$=FNF$(B1$(112,10),H$),X4$=B1$(228,20)
6570 LET X1$(1)=FNF$(A0$(5,P[0]),M0$)
6575 EXTRACT (ARM02_DEV,KEY=N0$+A0$(5,6)+A0$(3,2),DOM=6580)IOL=ARM02A
6580 LET T1$=A0$(5,6)
6700 LET L1=0
6710 DIM X[5],Y[5]
6790 RETURN 
6800 REM " --- Alphs Sort Routine
6805 IF R7$="N" THEN GOTO 1310
6810 LET K1$=KEY(ARM04_DEV,END=4020)
6820 READ (ARM04_DEV)
6830 IF N0$+"A"<>K1$(1,3) THEN IF R6$<>"S" THEN GOTO 4020
6835 IF N0$+"A"<>K1$(1,3) THEN GOTO 1110
6850 READ (ART01_DEV,KEY=N0$+"  "+K1$(14,6),DOM=6870)IOL=ART01A
6870 LET K$=KEY(ART01_DEV,END=6810)
6871 IF POS(N0$=K$)<>1 THEN GOTO 6810
6872 IF K$(5,6)<>K1$(14,6) THEN GOTO 6810
6875 READ (ART01_DEV,KEY=K$)IOL=ART01A
6880 GOSUB 6500
6890 GOTO 1370
6900 REM " --- Clean up customer break on Alph sort at EOF"
6910 IF R6$="S" THEN GOSUB 6400; GOTO 1110
6920 IF R7$="N" THEN GOTO 4000
6930 LET T9$="A"
6940 GOSUB 6400
6960 LET T9$=""
6980 GOTO 6800
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
8400 REM " --- Numeric Mask with Overflow"
8410 DEF FNMASK$(Q,Q1$)
8420 LET Q2$=STR(Q:Q1$)
8430 IF LEN(Q2$)>LEN(Q1$) THEN LET Q2$=FILL(LEN(Q1$),"*")
8440 RETURN Q2$
8445 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

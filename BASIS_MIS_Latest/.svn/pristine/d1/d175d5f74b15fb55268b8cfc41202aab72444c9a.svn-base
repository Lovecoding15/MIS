0010 REM "SYC - SAM/Developer Contract Number Lookup - BASIS"
0020 REM "Program SYC.LQ"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "SELECTION$: Contract Number (returned)"
0050 REM 
0060 REM " ** BASIS Copied Serial # Lookup and Modified for SAM/Developer Contract Info   
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER SELECTION$
0100 REM " --- Open Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SMC-01",FILES$[3]="ARM-04"
0135 LET FILES$[4]="SYM-06",FILES$[5]="ARM-01"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1],SMC01_DEV=CHANNELS[2],ARM04_DEV=CHANNELS[3]
0185 LET SYM06_DEV=CHANNELS[4],ARM01_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0230 SYM06A: IOLIST X$,COMPANY$
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$,M4$,M5$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 call "templates.pgm::smc01"
0400 REM " --- Parameters"
0405 DIM P[5],I[2],INFO$[20],SPEED$(128)
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0425 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),SPEEDSEARCH$="N",AP$="N"
0435 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=0445)
0440 LET N0$=SPEED$(1,2),SPEEDSEARCH$="Y"
0445 FIND (SYM06_DEV,KEY=N0$,DOM=9800)IOL=SYM06A
0450 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0455 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[3]=NUM(P2$(7,2))
0460 LET P[2]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2)),P[5]=NUM(P2$(13,2))
0500 REM " --- Initializations"
0505 PRECISION NUM(P2$(5,1))
0510 DIM A0$(20),A1$(60),A2$(18),A3$(35),A4$(60),A[2],S0$(42),W[5]
0540 LET WIDTH=73,HEIGHT=12,WIN_X=1,WIN_Y=3,MAX_ROW=HEIGHT-5,M9$="###0.00-"
0550 LET SWIDTH=73,SHEIGHT=7,SWIN_X=WIN_X,SWIN_Y=WIN_Y+HEIGHT,M9=LEN(M9$)
0560 LET PGM$=PGM(-2),OPTIONS=0,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3
0570 LET STITLE$="Contract Number Detail",W1=15+MAX(M4,M9),W2=20
0600 REM " --- Window arrays"
0610 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0620 LET HEADING$(2)="Number",HEADING$(25)="Active",HEADING$(35)="Customer"
0630 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0640 LET TITLE$="SAM/Developer/Rental Contract Number Lookup"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Which search option?"
0810 DIM STRINGS$[10],NUMBERS[10]
0815 LET STRINGS$[0]=PGM$,STRINGS$[1]="",NUMBERS[0]=MAXROW
0820 LET NUMBERS[1]=OWIN_X,NUMBERS[2]=OWIN_Y,NUMBERS[3]=P[0]
0825 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
0830 CALL "SYC.SM",STRINGS$[ALL],NUMBERS[ALL],FKEY
0835 IF FKEY THEN GOTO 9900
0840 LET PREFIX$=STRINGS$[2],START$=FNU$(STRINGS$[3]),METHOD$=STRINGS$[4]
0845 LET V3$=STRINGS$[5],V0=NUMBERS[4],W=INT(WIDTH/2); REM WIDTH-2-(P[0]+3)
0900 REM " --- Detail Window"
0910 CALL "SYC.WA",0,SWIDTH,SHEIGHT,SWIN_X,SWIN_Y,STITLE$,SNAME$
0915 CALL "SYC.WB",NAME$,0
0920 REM LET HEADING$(W)=METHOD$
0925 REM PRINT @(0,0),'CF','SB','BR',HEADING$,'ER','SF',
0930 IF SPEEDSEARCH$<>"Y" THEN GOTO 1000
0950 REM " --- Display SpeedSearch Company"
0955 DIM X$(WIDTH+SWIDTH)
0960 LET X$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
0965 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
0970 CALL "SYC.WA",0,SWIDTH,3,SWIN_X,SWIN_Y+SHEIGHT,"",CNAME$
0975 PRINT @(0,0),'BR',X$,'ER',
0980 CALL "SYC.WB",NAME$,0
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,SHEIGHT-2]
1020 LET PAGE=1,L=1,X1=1,X0=0,CUST$="",SELECTION$=""
1030 ON POS(PREFIX$="CTA") GOTO CONTRACT_ORDER,CUSTOMER_ORDER,TYPE_ORDER,annual_order
1050 CUSTOMER_ORDER: 
1060 REM " --- Customer Name"
1065 LET V0=6,V9=10,KNBR=1
1080 EXTRACT (ARM04_DEV,KEY=N0$+"A"+START$,DOM=CUSTOMER_LOOP)
1090 CUSTOMER_LOOP: 
1100 LET K$=KEY(ARM04_DEV,END=3000)
1110 IF POS(N0$+"A"=K$)<>1 THEN GOTO 3000
1120 READ (ARM04_DEV,KEY=K$)
1130 LET CUST$=K$(14,V0),X$=K$(4,V9),CONTR_KEY$=N0$+CUST$
1140 GOSUB GET_FIRST_CONTRACT
1150 CUST_CONTRACT_LOOP: 
1160 GOSUB GET_CONTR_INFO
1170 IF CONTR_KEY$<>smc01.FIRM_ID$+smc01.CUSTOMER_NBR$ THEN GOTO CUSTOMER_LOOP
1180 GOTO DO_SCREEN
1190 TYPE_ORDER: 
1200 REM " --- Contract type
1210 LET KNBR=3,CONTR_KEY$=START$
1220 GOSUB GET_FIRST_CONTRACT
1230 TYPE_LOOP: 
1240 GOSUB GET_CONTR_INFO
1250 IF (FINISH) THEN GOTO 3000
1260 GOTO DO_SCREEN
1300 CONTRACT_ORDER: 
1310 REM " --- Contract Number"
1320 LET KNBR=0,CONTR_KEY$=STR(NUM(START$):"000000")
1330 GOSUB GET_FIRST_CONTRACT
1340 CONTRACT_LOOP: 
1350 GOSUB GET_CONTR_INFO
1360 IF (FINISH) THEN GOTO 3000
1370 GOTO DO_SCREEN
1390 annual_order:
1400 rem " --- Annual date order
1410 knbr=2,contr_key$=start$
1420 gosub get_first_contract
1430 annual_loop:
1440 gosub get_contr_info
1450 if (finish) then goto 3000
1460 goto do_screen
1990 DO_SCREEN: 
2000 REM " --- Display page?"
2010 IF L<MAX_ROW THEN GOTO 2100
2020 LET XMODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 0800
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2400 REM " --- Lookup window record"
2410 DIM G1$(WIDTH-2)
2420 IF LOCKED THEN LET G1$(2)=K$(3)+" Record In Use"; GOTO 2490
2425 DIM CUST_NAME$(30); LET CUST_NAME$(1)="** Not on File **"
2430 FIND (ARM01_DEV,KEY=N0$+smc01.CUSTOMER_NBR$,DOM=2431)*,CUST_NAME$
2480 LET G1$(2)=smc01.CONTRACT$,G1$(25)=smc01.SAM_ACTIVE$,G1$(35)=CUST$+" "+CUST_NAME$(1,30)
2490 LET G1$(WIDTH-2)="",LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=smc01.CONTRACT$
2500 REM " --- Format detail window fields"
2505 IF LOCKED THEN GOTO 2680
2600 REM " --- Detail window record"
2610 DIM G2$(SWIDTH-2)
2620 LET G2$(1)="   {Contract}"+smc01.CONTRACT$,G2$(W2)="           {Type}"+TYPE$
2625 LET MORE$[PAGE,X1,1]=G2$
2640 LET G2$(1)="{Annual Date}"+ANNUAL$
2645 LET MORE$[PAGE,X1,2]=G2$
2650 LET G2$(1)=" {Expiration}"+EXPIRE$
2655 LET MORE$[PAGE,X1,3]=G2$
2660 LET G2$(1)="     {E-Mail}"+CVS(smc01.E_MAIL$,1+2)
2670 LET MORE$[PAGE,X1,4]=G2$
2675 let g2$(1)="    {Comment}"+cvs(smc01.comment$,1+2)
2677 let more$[page,x1,5]=g2$
2680 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),LLIST$[PAGE,X1,5]=SNAME$
2690 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2910 ON POS(PREFIX$="CTA") GOTO CONTRACT_LOOP,CUST_CONTRACT_LOOP,TYPE_LOOP,annual_loop
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3035 LET MESSAGE$[0]="No Contract Numbers Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page"
3510 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
3590 GOTO 4000
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
6500 REM " --- Error detected during processing of selection"
6510 DIM MESSAGE$[1]
6520 IF REASON=1 THEN LET MESSAGE$[0]="Lot/Serial Inventory"
6530 IF REASON=2 THEN LET MESSAGE$[0]="Accounts Payable"
6540 LET MESSAGE$[0]=MESSAGE$[0]+" Is Required For This"
6550 LET MESSAGE$[1]="Selection (<Enter>=Continue)"
6560 CALL "SYC.XA",2,MESSAGE$[ALL],1,-1,-1,V$,V3
6590 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNS$(Q$)=CVS(Q$,1)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 FOR X=1 TO FILES
9915 CLOSE (CHANNELS[X],ERR=9916)
9920 NEXT X
9930 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9935 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
9940 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9945 IF SELECTION$="END" THEN LET SELECTION$=""
9950 EXIT 
9999 END
10000 GET_FIRST_CONTRACT: 
10010 EXTRACT RECORD(SMC01_DEV,KEY=CONTR_KEY$,KNUM=KNBR,DOM=FIRST_DONE)
10020 FIRST_DONE: 
10030 RETURN 
11000 GET_CONTR_INFO: 
11020 dim smc01$:fattr(smc01$)
11030 DIM ANNUAL$(8),EXPIRE$(8),TYPE$(3)
11035 LET FINISH=0
11040 READ RECORD(SMC01_DEV,END=END_DONE)smc01$
11050 LET ANNUAL$(1)=smc01.ANNUAL_DT$(5,2)+"/"+smc01.ANNUAL_DT$(7,2)+"/"+smc01.ANNUAL_DT$(3,2)
11060 LET EXPIRE$(1)=smc01.EXPIRE_ON_DT$(5,2)+"/"+smc01.EXPIRE_ON_DT$(7,2)+"/"+smc01.EXPIRE_ON_DT$(3,2)
11070 LET TYPE$=smc01.CONTR_TYPE$
11075 GOTO CONTR_DONE
11080 END_DONE: 
11085 LET FINISH=1
11090 CONTR_DONE: 
11100 RETURN 

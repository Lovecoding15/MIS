0010 REM "DDR - Data Dictionary Where-Used Report"
0020 REM "Program DDR.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="DDM-01",FILES$[2]="DDM-04",FILES$[3]="DDM-06"
0130 LET FILES$[4]="DDM-06",FILES$[5]="SYM-03",FILES$[6]="SYS-01"
0160 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET DDM01_DEV=CHANNELS[1],DDM04_DEV=CHANNELS[2],DDM06_DEV1=CHANNELS[3]
0185 LET DDM06_DEV2=CHANNELS[4],SYM03_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0200 REM " --- IOLists
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="DD",N4$=F5$
0500 REM " --- Initializations"
0550 LET BEGNAME$="",ENDNAME$="",BEGFILE$="",ENDFILE$="",DATATYPE$="SZYMHTDNA"
0590 LET REVISION$="",TIMES=0,I0=0
0700 REM " --- Background
0710 FOR X=4 TO 16 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(20,3),"Beginning Data Name",@(23,5),"Ending Data Name",@(20,7),"Beginning File Name",@(23,9),"Ending File Name",@(30,11),"Data Type",@(27,13),"Last Revised",@(18,15),"Number of Occurrences",'SF',
0790 GOSUB 5000
1000 REM " --- Beginning Data Name
1010 LET V0$="S",V1$="C",V2$=BEGNAME$,V3$="",V0=12,V1=40,V2=3,I0=0
1020 LET V4$="Enter Beginning Data Name (<F1>=First/<F3>=Lookup)"
1030 GOSUB 7000
1040 ON V3 GOTO 1100,1120,1000,1050,4000
1050 CALL "DDC.LA",DDM01_DEV,V2+2,V$
1060 IF V$="" THEN GOTO 1000
1100 REM 
1110 IF V$<>"" THEN GOTO 1150
1120 LET BEGNAME$=""
1130 PRINT @(V1,V2),'CL',"First"
1140 GOTO 1190
1150 LET BEGNAME$=V$
1160 PRINT @(V1,V2),'CL',BEGNAME$
1190 GOTO 3900
1200 REM " --- Ending Data Name
1205 IF ENDNAME$="" THEN IF BEGNAME$<>"" THEN LET ENDNAME$=BEGNAME$
1210 LET V0$="S",V1$="C",V2$=ENDNAME$,V3$="",V0=12,V1=40,V2=5
1220 LET V4$="Enter Ending Data Name (<F1>=Last/<F3>=Lookup)"
1230 GOSUB 7000
1240 ON V3 GOTO 1300,1320,3900,1250,4000
1250 CALL "DDC.LA",DDM01_DEV,V2+2,V$
1260 IF V$="" THEN GOTO 1200
1300 REM 
1310 IF V$<>"" THEN GOTO 1350
1320 LET ENDNAME$=""
1330 PRINT @(V1,V2),'CL',"Last"
1340 GOTO 1390
1350 LET ENDNAME$=V$
1360 PRINT @(V1,V2),'CL',ENDNAME$
1390 GOTO 3900
1400 REM " --- Beginning File Name
1410 LET V0$="S",V1$="C",V2$=BEGFILE$,V3$="",V0=6,V1=40,V2=7
1420 LET V4$="Enter Beginning File Name (<F1>=First/<F3>=Lookup)"
1430 GOSUB 7000
1440 ON V3 GOTO 1500,1520,3900,1450,4000
1450 GOSUB 6100
1460 IF V$="" THEN GOTO 1400
1500 REM 
1510 IF V$<>"" THEN GOTO 1550
1520 LET BEGFILE$=""
1530 PRINT @(V1,V2),'CL',"First"
1540 GOTO 1590
1550 LET BEGFILE$=V$
1560 PRINT @(V1,V2),'CL',BEGFILE$
1590 GOTO 3900
1600 REM " --- Ending File Name
1610 LET V0$="S",V1$="C",V2$=ENDFILE$,V3$="",V0=6,V1=40,V2=9
1620 LET V4$="Enter Ending File Name (<F1>=Last/<F3>=Lookup)"
1630 GOSUB 7000
1640 ON V3 GOTO 1700,1720,3900,1650,4000
1650 GOSUB 6100
1660 IF V$="" THEN GOTO 1600
1700 REM 
1710 IF V$<>"" THEN GOTO 1750
1720 LET ENDFILE$=""
1730 PRINT @(V1,V2),'CL',"Last"
1740 GOTO 1790
1750 LET ENDFILE$=V$
1760 PRINT @(V1,V2),'CL',ENDFILE$
1790 GOTO 3900
1800 REM " --- Data Type
1810 LET V0$="S",V1$="C",V2$=DATATYPE$,V3$="",V0=10,V1=40,V2=11
1820 LET V4$="Enter Data Types to include (S/Z/Y/M/H/T/D/N/A/L/<F1>=All except L)"
1830 GOSUB 7000
1840 ON V3 GOTO 1900,1920,3900,1800,4000
1900 REM 
1910 IF V$<>"" THEN GOTO 1950
1920 LET V$="SZYMHTDNA"
1950 FOR X=1 TO LEN(V$)
1960 IF POS(V$(X,1)="SZYMHTDNAL")=0 THEN EXITTO 1800
1970 NEXT X
1980 LET DATATYPE$=V$
1985 PRINT @(V1,V2),'CL',DATATYPE$
1990 GOTO 3900
2000 REM " --- Last Revision
2010 LET V0$="D",V1$="C6N",V2$=REVISION$,V3$="",V0=8,V1=40,V2=13
2020 LET V4$="Enter the first Revision Date to include (<F1>=First)"
2030 GOSUB 7000
2040 ON V3 GOTO 2100,2120,3900,2000,4000
2100 REM 
2110 IF V$<>"" THEN GOTO 2150
2120 LET REVISION$=""
2130 PRINT @(V1,V2),'CL',"First"
2140 GOTO 2190
2150 LET REVISION$=V$
2160 PRINT @(V1,V2),'CL',FNB6$(REVISION$)
2190 GOTO 3900
2200 REM " --- Number of Occurrences
2210 LET V0$="N",V1$="C",V2$=STR(TIMES),V3$="###",V0=3,V1=40,V2=15
2220 LET V4$="Names used more than this value will be excluded from the report (<F1>=All)"
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2300,3900,2200,3900
2250 IF V<0 THEN GOTO 2200
2260 IF V=0 THEN GOTO 2300
2270 LET TIMES=V
2280 PRINT @(V1,V2),STR(TIMES),'CL',
2290 GOTO 3900
2300 REM 
2310 LET TIMES=0
2320 PRINT @(V1,V2),"All",'CL',
2390 GOTO 3900
3900 REM " --- Next field
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,2200,3990
3990 LET I0=0
4000 REM " --- Correct?
4005 GOSUB 5000
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="S",V1$="CR",V2$="Y",V3$="",V0=1,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4090 ON POS(V$="YN") GOTO 4000,4900,1000
4900 REM " --- Run report overlay
4990 RUN "DDR.DB"
5000 REM " --- Display"
5010 IF BEGNAME$="" THEN PRINT @(40,3),'CL',"First", ELSE PRINT @(40,3),'CL',BEGNAME$,
5020 IF ENDNAME$="" THEN PRINT @(40,5),'CL',"Last", ELSE PRINT @(40,5),'CL',ENDNAME$,
5030 IF BEGFILE$="" THEN PRINT @(40,7),'CL',"First", ELSE PRINT @(40,7),'CL',BEGFILE$,
5040 IF ENDFILE$="" THEN PRINT @(40,9),'CL',"Last", ELSE PRINT @(40,9),'CL',ENDFILE$,
5060 PRINT @(40,11),'CL',DATATYPE$,
5070 IF REVISION$="" THEN PRINT @(40,13),'CL',"First", ELSE PRINT @(40,13),'CL',FNB6$(REVISION$),
5080 IF TIMES=0 THEN PRINT @(40,15),'CL',"All", ELSE PRINT @(40,15),'CL',STR(TIMES),
5090 RETURN 
6100 REM " --- File Name Lookup
6110 LET CHANNEL=SYM03_DEV,RECORD$="",NUMBER=1,TITLE$="ADD+ON System Files"
6120 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6130 LET DESCRIPTION$[0]="File",DESCRIPTION$[1]="Description"
6140 LET FIELD[0]=1,POSITION[0]=1,LENGTH[0]=6
6150 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6160 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6190 GOTO 6500
6500 REM " --- Call Windowed Lookup
6510 LET V$="NO COMPANY",ROW=V2+2
6520 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "SYC - Calculator"
0020 REM "Program SYC.ZC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "COLUMN : Input field column (input)"
0060 REM "ANSWER$: Result of calculation (returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER COLUMN,ANSWER$
0400 REM " --- Parameters"
0410 LET PLACES=FNP(Q$)
0500 REM " --- Initializations"
0510 DIM TAPE$[256,0],MASKS$[20,1],ZERO$(20,"0")
0515 LET TERMINATOR$="+-*/=ASTDXastdxECec",NUMBER$="0123456789.",HNAME$=""
0530 LET TAPENUM=0,MEMORY$="0",FUNCTION$="",DISPLAY$="",L9=59
0535 FOR X=0 TO 19
0540 LET MASKS$[X,0]="-##################"
0545 NEXT X
0550 FOR X=0 TO 18
0555 LET MASKS$[X,0](X+1,1)="."
0560 NEXT X
0605 FOR X=0 TO 19
0610 LET P=POS("."=MASKS$[X,0])
0615 IF P<LEN(MASKS$[X,0]) AND P<>0 THEN LET MASKS$[X,0](P+1,LEN(MASKS$[X,0])-P)=ZERO$(1,LEN(MASKS$[X,0])-P)
0620 IF P=0 THEN GOTO 0645
0625 FOR Y=P TO INT((LEN(MASKS$[X,0])-P)/4) STEP -4
0630 IF Y=P OR Y<3 THEN GOTO 0640
0635 LET MASKS$[X,0](Y,1)=","
0640 NEXT Y
0645 NEXT X
0647 LET MASK$=MASKS$[16,0],MASKS$[16,1]="1"
0650 REM " --- Window sizing"
0655 LET WIDTH=26,HEIGHT=24,WIN_X=1,WIN_Y=0,X=WIDTH-2
0660 IF COLUMN<40 THEN LET WIN_X=50
0665 LET MWIDTH=X,MHEIGHT=3,MWIN_X=WIN_X+1,MWIN_Y=1
0670 LET TWIDTH=X,THEIGHT=11,TWIN_X=WIN_X+1,TWIN_Y=MWIN_Y+MHEIGHT
0675 LET DWIDTH=X,DHEIGHT=3,DWIN_X=WIN_X+1,DWIN_Y=TWIN_Y+THEIGHT
0680 LET EWIDTH=X,EHEIGHT=3,EWIN_X=WIN_X+1,EWIN_Y=DWIN_Y+DHEIGHT
0700 REM " --- Background"
0710 DIM FOOTING$(WIDTH-2)
0720 LET FOOTING$(1)="  <F3>=Help  <F4>=End"
0730 LET TITLE$="ADD+ON Calculator"
0740 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0750 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
0755 LET TITLE$="Memory"
0760 CALL "SYC.WA",0,MWIDTH,MHEIGHT,MWIN_X,MWIN_Y,TITLE$,MNAME$
0765 LET TITLE$="Tape"
0770 CALL "SYC.WA",0,TWIDTH,THEIGHT,TWIN_X,TWIN_Y,TITLE$,TNAME$
0775 LET TITLE$="Display"
0780 CALL "SYC.WA",0,DWIDTH,DHEIGHT,DWIN_X,DWIN_Y,TITLE$,DNAME$
0785 LET TITLE$="Entry"
0790 CALL "SYC.WA",0,EWIDTH,EHEIGHT,EWIN_X,EWIN_Y,TITLE$,ENAME$
1000 REM " --- Get next character"
1010 LET X=LEN(MASK$)-POS("."=MASK$)
1020 IF X>7 THEN LET X=7
1030 PRECISION X
1040 GOSUB 7000
1045 IF V3=0 THEN GOTO 1100
1050 REM " --- Process function keys"
1060 IF V3=-1 THEN GOTO 3500
1070 IF V3=-2 THEN GOTO 3300
1080 IF V3=-5 THEN GOTO 2700
1090 ON V3 GOTO 1100,1700,1800,2800,4000,3000,3100,3400,3600,4000,1150
1100 REM " --- Process other characters"
1110 LET V=POS(V$=TERMINATOR$)
1120 IF V>0 THEN GOTO 1250
1130 IF POS(V$=NUMBER$)>0 THEN GOTO 1200
1140 IF V$="" THEN GOTO 1300
1150 REM " --- Invalid character"
1160 PRINT @(0,0),STR(NUM(DISPLAY$):MASK$),'RB',
1190 GOTO 1000
1200 REM " --- Entered number or decimal point"
1210 GOSUB 5000
1240 GOTO 1000
1250 REM " --- Entered terminator character"
1280 ON V GOTO 1000,1300,1500,2000,1900,2600,1300,1500,2600,1900,2000,1300,1500,2600,1900,2000,2300,2400,2300,2400,1000
1290 GOTO 1000
1300 REM " --- <Aa+> Addition"
1320 LET OPERATOR$="+",FUNCTION$=""
1340 GOSUB 5500
1390 GOTO 1000
1500 REM " --- <Ss-> Subtract"
1520 LET OPERATOR$="-",FUNCTION$=""
1540 GOSUB 5500
1690 GOTO 1000
1700 REM " --- <F1> Print Tape"
1720 GOSUB 6100
1790 GOTO 1000
1800 REM " --- <F2> Scroll Tape"
1820 GOSUB 6300
1890 GOTO 1000
1900 REM " --- <Dd/> Divide"
1910 LET MD$="/"
1990 GOTO 2020
2000 REM " --- <Xx*> Multiply"
2010 LET MD$="*"
2020 IF FUNCTION$=MD$ THEN GOTO 2240
2050 REM 
2055 LET OPERATOR$=MD$
2060 IF TAPENUM=0 THEN GOTO 2080
2065 LET DTEMP$=TAPE$[TAPENUM-1,0],TEMP$=DTEMP$
2070 GOSUB 6000
2075 LET DTEMP=NUM(TEMP$)
2080 LET TEMP$=DISPLAY$+"0"
2085 GOSUB 6000
2090 LET WTEMP$=TEMP$
2100 IF NUM(WTEMP$)=0 THEN GOTO 2150
2105 LET OPERATOR$="+"
2110 GOSUB 5500
2115 GOSUB 5100
2120 LET WTEMP$="0",DTEMP$=TAPE$[TAPENUM-1,0]
2125 GOSUB 6000
2130 LET DTEMP=NUM(WTEMP$)
2150 IF NUM(WTEMP$)<>0 THEN GOTO 2185
2155 IF TOTAL=0 THEN GOTO 2185
2160 GOSUB 5500
2165 GOSUB 5100
2170 LET WTEMP$="0",DTEMP$=TAPE$[TAPENUM-1,0]
2175 GOSUB 6000
2180 LET DTEMP=NUM(TEMP$)
2185 IF NUM(WTEMP$)<>0 THEN GOTO 2215
2190 IF TOTAL<>0 THEN GOTO 2215
2195 IF DTEMP$(LEN(MASK$)+2,1)<>"T" THEN GOTO 2215
2200 LET DISPLAY$=DTEMP$(1,LEN(MASK$)),OPERATOR$=MD$,FUNCTION$=MD$
2205 GOSUB 5500
2210 GOTO 1000
2215 IF NUM(WTEMP$)<>0 THEN GOTO 1000
2220 LET DISPLAY$=DTEMP$,OPERATOR$=MD$,FUNCTION$=MD$
2225 LET TOTAL$=STR(0:MASK$),TOTAL=0
2230 GOSUB 5500
2235 GOTO 1000
2250 REM 
2255 LET OPERATOR$=MD$,FUNCTION$="="
2260 GOSUB 5500
2270 GOSUB 5100
2280 LET FUNCTION$=""
2290 GOTO 1000
2300 REM " --- <Ee> Clear Entry"
2310 LET DISPLAY$=""
2320 GOSUB 5200
2330 CALL "SYC.WB",ENAME$,0
2390 GOTO 1000
2400 REM " --- <Cc> Clear All"
2420 LET DISPLAY$="0",TOTAL$="0",TOTAL=0
2440 GOSUB 5100
2490 GOTO 1000
2600 REM " --- <Tt=> Equals/Totals"
2610 LET OPERATOR$="+"
2620 IF TAPENUM=0 THEN GOTO 2640
2630 IF POS(TAPE$[TAPENUM-1,0](LEN(MASK$)+2,1)="+-")=0 THEN GOTO 2640
2635 LET OPERATOR$=TAPE$[TAPENUM-1,0](LEN(MASK$)+2,1)
2640 LET TEMP$=DISPLAY$+"0"
2650 GOSUB 6000
2660 LET WTEMP$=TEMP$
2670 IF NUM(WTEMP$)>0 THEN GOSUB 5500
2680 GOSUB 5100
2690 GOTO 1000
2700 REM " --- Backspace"
2710 IF LEN(DISPLAY$)=0 THEN GOTO 2790
2720 LET DISPLAY$=DISPLAY$(1,LEN(DISPLAY$)-1)
2730 GOSUB 5200
2790 GOTO 1000
2800 REM " --- <F3> Help"
2805 IF HNAME$<>"" THEN GOTO 1000
2810 LET HWIDTH=48,HHEIGHT=12,HWIN_X=30,HWIN_Y=3
2815 LET TITLE$="ADD+ON Calculator Help"
2820 IF WIN_X>40 THEN LET HWIN_X=0
2840 CALL "SYC.WA",0,HWIDTH,HHEIGHT,HWIN_X,HWIN_Y,TITLE$,HNAME$
2855 PRINT @(1,0),"<Aa+>   = Add          <F1> = Print Tape"
2860 PRINT @(1,1),"<Ss->   = Subtract     <F2> = Scroll Tape"
2865 PRINT @(1,2),"<Xx*>   = Multiply     <F3> = Help"
2870 PRINT @(1,3),"<Dd/>   = Divide       <F4> = Exit"
2875 PRINT @(1,4),"<Cc>    = Clear All    <F5> = Memory +"
2880 PRINT @(1,5),"<Ee>    = Clear Entry  <F6> = Memory -"
2885 PRINT @(1,6),"<Enter> = Repeat       <F7> = Memory Recall"
2890 PRINT @(1,7),"<Tt=>   = Total        <F8> = Memory Clear"
2900 PRINT @(1,8),"                       <F9> = Return Value"
2910 PRINT @(1,9),"----> = Dec Right     <---- = Dec Left",
2980 GOSUB 5200
2990 GOTO 1000
3000 REM " --- <F5> Memory +"
3010 IF TOTAL<>0 THEN GOTO 3060
3015 IF TAPENUM=0 THEN GOTO 3060
3020 IF POS("T"=TAPE$[TAPENUM-1,0])=0 THEN GOTO 3060
3025 LET TEMP$=TAPE$[TAPENUM-1,0](1,LEN(MASK$))
3030 GOSUB 6000
3035 LET MEMADD=NUM(TEMP$)
3040 GOTO 3060
3050 LET MEMADD=TOTAL
3060 LET TEMP$=MEMORY$
3070 GOSUB 6000
3080 LET MEMORY$=STR(NUM(TEMP$)+MEMADD:MASK$)
3090 GOTO 3200
3100 REM " --- <F6> Memory -"
3110 IF TOTAL<>0 THEN GOTO 3145
3115 IF TAPENUM=0 THEN GOTO 3145
3120 IF POS("T"=TAPE$[TAPENUM-1,0])=0 THEN GOTO 3145
3125 LET TEMP$=TAPE$[TAPENUM-1,0](1,LEN(MASK$))
3130 GOSUB 6000
3135 LET MEMSUB=NUM(TEMP$)
3140 GOTO 3150
3145 LET MEMSUB=TOTAL
3150 LET TEMP$=MEMORY$
3160 GOSUB 6000
3165 LET MEMORY$=STR(NUM(TEMP$)-MEMSUB:MASK$)
3200 REM " --- Redisplay all windows"
3210 GOSUB 5400
3220 GOSUB 5300
3230 GOSUB 5200
3290 GOTO 1000
3300 REM " --- <Left Arrow> Change Mask"
3310 FOR X=0 TO 19
3315 IF MASKS$[X,1]="1" THEN EXITTO 3325
3320 NEXT X
3325 IF X=0 THEN GOTO 3340
3330 LET MASKS$[X,1]="",MASKS$[X-1,1]="1",MASK$=MASKS$[X-1,0]
3340 LET TEMP$=DISPLAY$
3345 GOSUB 6000
3350 LET DISPLAY$=TEMP$,TEMP$=TOTAL$
3360 GOSUB 6000
3370 LET TOTAL$=TEMP$
3380 GOSUB 5400
3385 GOSUB 5200
3390 GOTO 1000
3400 REM " --- <F7> Recall Memory"
3410 LET TEMP$=MEMORY$
3420 GOSUB 6000
3430 LET DISPLAY$=STR(NUM(TEMP$)),V$=""
3440 GOSUB 5000
3490 GOTO 1000
3500 REM " --- <Right Arrow> Change Mask"
3520 FOR X=0 TO 19; IF MASKS$[X,1]="1" THEN EXITTO 3530 FI; NEXT X
3530 IF X<19 THEN LET MASKS$[X,1]="",MASKS$[X+1,1]="1",MASK$=MASKS$[X+1,0]
3540 LET TEMP$=DISPLAY$; GOSUB 6000; LET DISPLAY$=TEMP$,TEMP$=TOTAL$; GOSUB 6000; LET TOTAL$=TEMP$
3560 GOSUB 5400
3580 GOSUB 5200
3590 GOTO 1000
3600 REM " --- <F8> Clear Memory"
3620 LET MEMORY$=STR(0:MASK$)
3660 GOSUB 5400
3670 GOSUB 5300
3680 GOSUB 5200
3690 GOTO 1000
4000 REM " --- <F4> Quit/<F9> Return Value"
4010 IF V3=4 THEN GOTO 4090
4020 IF TOTAL<>0 THEN GOTO 4080
4030 IF TAPENUM=0 THEN GOTO 4090
4040 IF POS("T"=TAPE$[TAPENUM-1,0])=0 THEN GOTO 4090
4050 LET TEMP$=TAPE$[TAPENUM-1,0]
4055 GOSUB 6000
4060 LET ANSWER$=TEMP$
4070 GOTO 4090
4080 LET ANSWER$=STR(TOTAL)
4090 GOTO 9900
5000 REM " --- Display entered digit or decimal point"
5010 IF POS("."=DISPLAY$)=0 THEN GOTO 5050
5020 IF V$<>"." THEN GOTO 5050
5030 PRINT 'RB',
5040 GOTO 5080
5050 LET DISPLAY$=DISPLAY$+V$
5060 IF DISPLAY$="." THEN LET DISPLAY$="0."
5080 PRINT @(0,0),STR(NUM(DISPLAY$):MASK$),
5090 RETURN 
5100 REM " --- Display total"
5110 LET DISPLAY$="",TAPE$[TAPENUM,0]=STR(TOTAL:MASK$),TOTAL=0
5120 LET TAPE$[TAPENUM,0]=TAPE$[TAPENUM,0]+" T",TOTAL$=STR(TOTAL:MASK$)
5130 LET TAPENUM=TAPENUM+1
5150 CALL "SYC.WB",TNAME$,0
5160 PRINT @(0,0),'LD',@(0,THEIGHT-3),TAPE$[TAPENUM-1,0],
5170 GOSUB 5400
5180 GOSUB 5200
5190 RETURN 
5200 REM " --- Display entry window"
5210 CALL "SYC.WB",ENAME$,0
5220 PRINT @(0,0),STR(NUM(DISPLAY$):MASK$),
5290 RETURN 
5300 REM " --- Display memory window"
5310 CALL "SYC.WB",MNAME$,0
5320 PRINT @(0,0),MEMORY$,
5390 RETURN 
5400 REM " --- Display total window"
5410 CALL "SYC.WB",DNAME$,0
5420 PRINT @(0,0),TOTAL$,
5490 RETURN 
5500 REM " --- Update tape"
5510 IF LEN(DISPLAY$)=0 THEN GOTO 5690
5520 LET TEMP$=DISPLAY$
5530 GOSUB 6000
5535 LET DISPLAY$=TEMP$
5540 LET TAPE$[TAPENUM,0]=STR(NUM(DISPLAY$):MASK$)+" "+OPERATOR$
5545 IF FUNCTION$="=" THEN LET TAPE$[TAPENUM,0](LEN(MASK$)+2,1)="="
5550 LET DISPLAY$="",TAPENUM=TAPENUM+1
5555 CALL "SYC.WB",TNAME$,0
5560 PRINT @(0,0),'LD',@(0,THEIGHT-3),TAPE$[TAPENUM-1,0],
5565 GOSUB 5200
5570 IF POS(FUNCTION$="/*")=0 THEN GOTO 5600
5575 LET TOTAL$=TAPE$[TAPENUM-1,0],TEMP$=TOTAL$
5580 GOSUB 6000
5585 LET TOTAL=NUM(TEMP$)
5590 GOTO 5690
5600 REM " --- Perform calculation"
5610 LET TEMP$=TOTAL$
5615 GOSUB 6000
5620 LET TOTAL=NUM(TEMP$),TEMP$=TAPE$[TAPENUM-1,0]
5630 GOSUB 6000
5635 LET NUMBER=NUM(TEMP$)
5640 IF OPERATOR$="+" THEN LET TOTAL=TOTAL+NUMBER
5645 IF OPERATOR$="-" THEN LET TOTAL=TOTAL-NUMBER
5650 IF OPERATOR$="*" THEN LET TOTAL=TOTAL*NUMBER
5655 IF OPERATOR$="/" THEN LET TOTAL=TOTAL/NUMBER
5660 LET TOTAL$=STR(TOTAL:MASK$),TEMP$=TOTAL$
5665 GOSUB 6000
5670 GOSUB 5400
5675 GOSUB 5200
5690 RETURN 
6000 REM " --- Strip commas"
6010 IF LEN(TEMP$)=0 THEN RETURN 
6020 LET P0=POS(","=TEMP$)
6030 IF P0=0 THEN GOTO 6060
6040 LET TEMP$=TEMP$(1,P0-1)+TEMP$(P0+1)
6050 GOTO 6020
6060 IF POS(TEMP$(LEN(TEMP$),1)=" +/-*T=")>0 THEN LET TEMP$=TEMP$(1,LEN(TEMP$)-2)
6070 LET TEMP$=FNP$(TEMP$)
6090 RETURN 
6100 REM " --- <F1> Print Tape"
6110 LET PRINTER=UNT,TITLE$="ADD+ON Calculator Tape",L=L9+1,H=0
6120 CALL "SYC.GA",PRINTER,0,TITLE$,"",V
6130 IF V THEN GOTO 6290
6150 FOR X=0 TO 256
6155 IF TAPE$[X,0]="" THEN GOTO 6250
6160 IF FNS$(FNP$(TAPE$[X,0]))="" THEN GOTO 6250
6170 IF L<L9 THEN GOTO 6200
6175 LET L=0,H=H+1
6180 PRINT (PRINTER)'FF',DAY,"   ",TITLE$,"   Page ",H
6185 PRINT (PRINTER)FNE$(Q$)
6200 REM 
6210 PRINT (PRINTER)@(12),TAPE$[X,0]
6220 LET L=L+1
6230 IF POS("T"=TAPE$[X,0])=0 THEN GOTO 6250
6235 PRINT (PRINTER)""
6240 LET L=L+1
6250 NEXT X
6280 CLOSE (PRINTER,ERR=6290)
6290 RETURN 
6300 REM " --- Scroll Tape"
6310 LET LASTLINE=THEIGHT-3,LINES=LASTLINE,LINE=LASTLINE
6315 LET INDEX=TAPENUM-1,LASTINDEX=INDEX
6320 IF INDEX<0 THEN LET INDEX=0
6330 CALL "SYC.WB",TNAME$,0
6340 IF TAPENUM>0 THEN PRINT @(0,LASTLINE),'BR',TAPE$[TAPENUM-1,0],'ER',
6350 GOSUB 7000
6360 IF V3=4 THEN GOTO 6700
6365 IF V3=-3 THEN GOTO 6450
6370 IF V3=2 THEN GOTO 6400
6375 IF V3=-16 THEN GOTO 6550
6380 IF V3=-17 THEN GOTO 6500
6390 GOTO 6350
6400 REM " --- Up Arrow"
6410 GOSUB 6800
6440 GOTO 6600
6450 REM " --- Down Arrow"
6460 GOSUB 6900
6490 GOTO 6600
6500 REM " --- PgUp"
6510 LET STATUS=0,CODE=0
6515 FOR X=1 TO LINES*2
6520 IF STATUS=0 THEN GOSUB 6800
6525 IF X=1 THEN IF STATUS>0 THEN LET CODE=STATUS
6530 NEXT X
6535 LET STATUS=CODE
6540 GOTO 6600
6550 REM " --- PgDn"
6560 LET STATUS=0,CODE=0
6565 FOR X=1 TO LINES*2
6570 IF STATUS=0 THEN GOSUB 6900
6575 IF X=1 THEN IF STATUS>0 THEN LET CODE=STATUS
6580 NEXT X
6585 LET STATUS=CODE
6590 GOTO 6600
6600 REM " --- Done with control key"
6610 IF STATUS>0 THEN PRINT 'RB',
6690 GOTO 6350
6700 REM " --- Restore tape display after scrolling"
6710 FOR X=0 TO 255
6715 IF FNP$(TAPE$[X,0])="" THEN EXITTO 6730
6720 NEXT X
6730 LET LASTINDEX=X-1,INDEX=LASTINDEX-LINES
6735 IF INDEX>=0 THEN GOTO 6750
6740 IF LASTINDEX>=0 THEN PRINT @(0,LASTLINE),TAPE$[LASTINDEX,0],
6745 GOTO 6775
6750 PRINT 'CS',
6755 FOR X=0 TO LINES
6760 PRINT @(0,X),TAPE$[INDEX,0],
6765 LET INDEX=INDEX+1
6770 NEXT X
6775 LET LINE=LASTLINE,INDEX=LASTINDEX
6780 GOSUB 5200
6790 RETURN 
6800 REM " --- Up Arrow"
6810 LET STATUS=1
6820 IF INDEX<1 THEN GOTO 6890
6830 IF LINE=0 THEN GOTO 6850
6840 IF LINE>0 THEN GOTO 6870
6845 GOTO 6890
6850 PRINT @(0,0),TAPE$[INDEX,0],@(0,0),'LI','BR',TAPE$[INDEX-1,0],'ER',
6860 LET INDEX=INDEX-1,STATUS=0
6865 GOTO 5890
6870 PRINT @(0,LINE),TAPE$[INDEX,0],@(0,LINE-1),'BR',TAPE$[INDEX-1,0],'ER',
6880 LET INDEX=INDEX-1,LINE=LINE-1,STATUS=0
6890 RETURN 
6900 REM " --- Down Arrow"
6910 LET STATUS=1
6915 IF FNP$(TAPE$[INDEX+1,0])="" THEN GOTO 6990
6920 IF INDEX>254 THEN GOTO 6990
6930 IF LINE=LASTLINE THEN GOTO 6950
6940 IF LINE<LASTLINE THEN GOTO 6970
6945 GOTO 6990
6950 PRINT @(0,LASTLINE),TAPE$[INDEX,0],@(0,0),'LD',@(0,LASTLINE),'BR',TAPE$[INDEX+1,0],'ER',
6960 LET INDEX=INDEX+1,STATUS=0
6965 GOTO 6990
6970 PRINT @(0,LINE),TAPE$[INDEX,0],@(0,LINE+1),'BR',TAPE$[INDEX+1,0],'ER',
6980 LET INDEX=INDEX+1,LINE=LINE+1,STATUS=0
6990 RETURN 
7000 REM " --- Calculator Routine Input Processing"
7010 LET Q$=PGM(-2),V0$="#",V1$="a",V2$="",V3$="",V4$="",V0=1,V1=0,V2=0
7020 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,Q$,V0,V1,V2,V$,V,V3,VD0
7030 IF V3=0 THEN IF V$="" THEN LET V$="+"
7040 IF POS(V$="AaSs+-")>0 AND DISPLAY$="" AND TAPENUM>0 THEN LET DISPLAY$=TAPE$[TAPENUM-1,0](1,LEN(MASK$))
7050 IF POS(FUNCTION$="/*")>0 AND POS(V$="Tt=")>0 THEN LET V$=FUNCTION$
7090 RETURN 
8000 REM " --- Functions"
8010 DEF FNE$(Q$)=DATE(0:"%hz:%mz %p")
8080 DEF FNS$(Q$)=CVS(Q$,1)
8090 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNP(Q$)=TCB(14)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 IF ENAME$<>"" THEN CALL "SYC.WD",ENAME$
9920 IF DNAME$<>"" THEN CALL "SYC.WD",DNAME$
9930 IF TNAME$<>"" THEN CALL "SYC.WD",TNAME$
9940 IF MNAME$<>"" THEN CALL "SYC.WD",MNAME$
9950 IF HNAME$<>"" THEN CALL "SYC.WD",HNAME$
9960 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9970 PRECISION PLACES
9980 LET X=NUM(ANSWER$,ERR=9990)*1,ANSWER$=STR(X)
9990 EXIT 
9999 END

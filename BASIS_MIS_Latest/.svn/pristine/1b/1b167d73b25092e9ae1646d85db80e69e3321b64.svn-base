0010 REM "ARM - Credit Manager's Review and Release"
0020 REM "Program ARM.QA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "16Jan99 WGH: Manual Ship-To isn't removed when order deleted (4710)
0032 REM "24Jun96 WGH: Held orders may not redisplay after printing a Released order (510,530,920,4930,5025,6620)"
0033 REM "03Jul96 WGH: ARS-10, ARE-23 and IVM-07 assigned to incorrect channels (155)"
0034 REM "03Jul96 WGH: Open IVM-08 when lotted/serialized (110,620,640)
0035 REM "14Aug96 WGH: Error 42 Line 510 of OPR.PA when printing order (455,485-495)"
0036 REM "03Sep96 WGH: Error 13 Line 560 of OPR.PA when printing order (110,155-160,198,620,640)"
0037 REM "23Oct96 JWK: Keep key pointer in sequence (5075,5080)
0038 REM "20Feb97 WGH: Use IV Price Mask for OP price fields (385,560)
0039 REM "17Apr97 WGH: Error 42 Line 6850 of OPR.PA when printing released order (6470)"
0040 REM "21Apr97 JWK: Allow change when entering an existing entry (1845,2005,2160-2190)
0041 REM "14May97 WGH: Delete obsolete code (4500-4530)"
0042 REM "17Jun97 WGH: Display 21st century dates correctly (5040,8145-8165)"
0043 REM "11Nov98 WGH: Stop Order/Invoice display loop when finished (3040,3535,5650)
0044 REM 
0045 SETESC 9000
0050 SETERR 9000
0060 PRECISION 4
0070 ON O9 GOTO 0080,6600
0080 BEGIN 
0083 SETESC 9000
0085 SETERR 9000
0090 PRECISION 4
0100 REM " --- Open/Lock Files
0110 LET FILES=25
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARE-03",FILES$[2]="ARE-04",FILES$[3]="ARE-07"
0130 LET FILES$[4]="ARE-13",FILES$[5]="ARE-33",FILES$[6]="ARM-01"
0135 LET FILES$[7]="ARM-02",FILES$[8]="ARM-03",FILES$[9]="ARM-05"
0140 LET FILES$[10]="ARM-10",FILES$[11]="ARM-14",FILES$[12]="ART-01"
0145 LET FILES$[13]="ART-11",FILES$[14]="IVE-03",FILES$[15]="IVM-01"
0150 LET FILES$[16]="IVM-02",FILES$[17]="ARM-07",FILES$[18]="SYS-01"
0155 LET FILES$[19]="ARS-10",FILES$[20]="IVM-10"
0158 LET FILES$[23]="ARE-23",FILES$[24]="IVM-07",FILES$[25]="IVM-08"
0160 CALL "SYC.DA",1,1,20,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0165 IF STATUS>0 THEN GOTO 9900
0170 LET ARE03_DEV=CHANNELS[1],ARE04_DEV=CHANNELS[2],ARE07_DEV=CHANNELS[3]
0175 LET ARE13_DEV=CHANNELS[4],ARE33_DEV=CHANNELS[5],ARM01_DEV=CHANNELS[6]
0180 LET ARM02_DEV=CHANNELS[7],ARM03_DEV=CHANNELS[8],ARM05_DEV=CHANNELS[9]
0185 LET ARM10_DEV=CHANNELS[10],ARM14_DEV=CHANNELS[11],ART01_DEV=CHANNELS[12]
0190 LET ART11_DEV=CHANNELS[13],IVE03_DEV=CHANNELS[14],IVM01_DEV=CHANNELS[15]
0195 LET IVM02_DEV=CHANNELS[16],ARM07_DEV=CHANNELS[17],SYS01_DEV=CHANNELS[18]
0198 LET ARS10_DEV=CHANNELS[19],IVM10_DEV=CHANNELS[20]
0200 REM " --- IOLists
0210 ARE03A: IOLIST A0$(1)
0220 ARE03A1: IOLIST A0$(1),A[ALL]
0230 ARE13A: IOLIST W0$,W1$(1),W[ALL]
0240 ARE13A2: IOLIST *,*,*,*,*,*,*,*,EXT_PRICE
0250 ARE23A: IOLIST T0$,T1$,T2$,T[ALL]
0260 ARM01A: IOLIST B0$,B1$(1)
0270 ARM02A: IOLIST D0$,D1$,D[ALL]
0280 ARM03A: IOLIST X$,X
0290 ARM05A: IOLIST G0$,G1$(1)
0300 ARM10A: IOLIST X0$(1)
0310 ARM10E: IOLIST Y0$
0320 ARM10F: IOLIST X1$(1)
0325 ARM14C: IOLIST C1$
0330 ARM14D: IOLIST C$
0335 ART01A: IOLIST U0$,U[ALL]
0340 ART11A: IOLIST S0$,S1$,S[ALL]
0350 IVM07A: IOLIST H0$,H1$,H[ALL]
0360 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0365 ARS01C: IOLIST C9$(1)
0370 ARS10N: IOLIST N$,N[ALL]
0380 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0385 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,I5$,M1$,M2$,M3$,M4$
0390 ARM07A: IOLIST R0$,R1$,R2$
0395 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0455 DIM P[2],I[6]
0460 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0465 DIM C9$(36)
0470 FIND (SYS01_DEV,KEY=N0$+N2$+"01",DOM=9800)IOL=ARS01C
0475 IF C9$(7,1)<>"Y" THEN GOTO 9900
0480 GOSUB GET_OP_TERMINAL
0485 FOR X=0 TO 6
0490 LET I[X]=NUM(I2$(X*2+1,2))
0495 NEXT X
0500 REM " --- Init Data
0510 DIM F0[7],U[1],S[1]
0520 LET K0=1,K9=5
0530 LET C0$=N0$+"D"
0540 LET M1=LEN(M1$),M0=LEN(M0$)
0550 LET F2$="O"
0560 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),M4=LEN(M4$)
0570 REM " --- Init ATAMO Data
0575 DIM FILES[44],PARAMS[0],PARAMS$[4],ITEM$[3],REFS$[11],REFS[5]
0580 LET FILES[0]=SYS01_DEV,FILES[1]=IVM01_DEV,FILES[2]=IVM02_DEV
0585 LET PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3),PARAMS$[2]=I2$
0590 LET PARAMS$[3]=I3$,PARAMS$[4]=I4$
0600 REM " --- Additional File Opens
0610 IF POS(I3$(17,1)="LS")<=0 THEN GOTO 0700
0620 CALL "SYC.DA",1,23,25,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS>0 THEN GOTO 9900
0640 LET ARE23_DEV=CHANNELS[23],IVM07_DEV=CHANNELS[24],IVM08_DEV=CHANNELS[25]
0690 LET FILES[7]=IVM07_DEV,FILES[8]=IVM08_DEV
0700 REM " --- Background
0720 PRINT 'SB',@(0,3),"Ln#",@(7),"Date",@(15),"Customer",@(54),"Order",@(62),"Ord Date",@(71),"Ship Date"
0790 PRINT 'SF',
0900 REM " --- Init File
0920 READ (ARM14_DEV,KEY=C0$,DOM=1000)
1000 REM " --- Display a Screen of Dates
1010 PRINT 'CF'
1020 GOSUB 5000
1100 REM " --- Select an Entry
1110 IF F$="END" THEN LET V4$="End Of Data, Select Line # ("
1120 IF F$="" THEN LET V4$="Select Line # (<Enter>=Continue"
1130 IF F2$="O" THEN LET V4$=V4$+"/<F1>=Credit Customers" ELSE LET V4$=V4$+"/<F1>=Held Orders/<F3>=New"
1140 LET V4$=V4$+")",P=POS("(/"=V4$)
1150 IF P>0 THEN LET V4$=V4$(1,P)+V4$(P+2)
1160 LET V0$="S",V1$="KC",V2$="",V3$="",V0=3,V1=FNV(V4$),V2=22
1170 GOSUB 7000
1180 IF V3=3 THEN IF F2$="C" THEN GOTO 1700
1190 IF V3=1 THEN GOTO 4900
1200 IF V3=4 THEN GOTO 9900
1210 IF V$="" THEN LET S2$=""
1220 IF V$="" THEN IF F$="" THEN GOTO 1000 ELSE GOTO 0900
1230 LET I=NUM(V$,ERR=1100)
1240 IF I=0 OR I<>INT(ABS(I)) OR I>L1 THEN GOTO 1100
1250 LET C$=X$(I*22-21,22); REM "Key
1300 REM " --- Scroll up fo the Line
1310 IF I>1 THEN GOTO 1340
1320 PRINT @(0,5),'CE'
1330 GOTO 1400
1340 REM " --- Scroll up if not 1st line
1350 FOR X=1 TO I-1
1360 PRINT @(0,4),'LD'
1370 NEXT X
1380 GOTO 1320
1400 REM " --- Show Detail of Selection
1410 DIM B1$(250),D1$(64),D[6]
1420 LET B0$=N0$+C$(10,6),D0$=B0$+"  "
1430 DIM X0$(28),X1$(28)
1440 LET X0$(1)=N0$+"A   ** Not On File **",X1$(1)=N0$+"F   ** Not On File **"
1450 FIND (ARM01_DEV,KEY=B0$,DOM=1460)IOL=ARM01A
1460 FIND (ARM02_DEV,KEY=D0$,DOM=1470)IOL=ARM02A
1470 FIND (ARM10_DEV,KEY=N0$+"A"+D1$(4,2),DOM=1480)IOL=ARM10A; REM "Terms
1480 FIND (ARM10_DEV,KEY=N0$+"F"+D1$(1,3),DOM=1490)IOL=ARM10F; REM "Salesperson
1490 LET Z$=CVS(B1$(79,24),2)
1500 GOSUB 5100
1510 GOSUB 5200
1520 LET F1=0
1530 GOSUB COMMENTS
1540 DIM A0$(117)
1550 LET A0$(1)=B0$(1,2)+D0$(9,2)+B0$(3,6)+C$(16,7)+"000"
1560 FIND (ARE03_DEV,KEY=A0$(1,20),DOM=4000)IOL=ARE03A
1570 GOSUB ORDER_TOTS
1580 PRINT @(65,20),TOT_PRICE:M1$
1590 GOTO 4000
1700 REM " --- Add a new Entry
1710 DIM C$(22)
1720 PRINT @(0,4),'CE'
1730 LET C$(1)=C0$
1740 LET V0$="D",V1$="ECR6N",V2$=C$(4,6),V3$="",V4$="Enter A Valid Follow-Up Date ",V0=6,V1=4,V2=4,I0=0
1750 GOSUB 7000
1760 IF V3=4 THEN GOTO 1000
1770 LET C$(4,6)=V$
1790 GOTO 3900
1800 REM " --- Cust Number
1810 PRINT @(15,4),'CL'
1820 LET V0$="M",V1$="CR",V2$=C$(10,P[0]),V3$=P1$,V4$="Enter A Valid Customer Number (<F3>=Lookup)",V1=15,V2=4,V0=6
1830 GOSUB 7000
1840 IF V3=4 THEN GOTO 0900
1845 IF V3=2 THEN GOTO 1740
1850 IF V3=3 THEN CALL "SYC.LC",V$
1860 LET C$(10,6)=V$
1865 DIM X$(30),B1$(250)
1870 FIND (ARM01_DEV,KEY=C$(1,2)+C$(10,6),DOM=1800)IOL=ARM01A
1880 PRINT @(V1,V2),FNF$(C$(10,P[0]),M0$),@(V1+M0+1,V2),B1$(1,30),
1890 GOTO 3900
1900 REM " --- Order Number
1910 LET V$=""; GOTO 1960
1920 LET V0$="Z",V1$="C",V2$=C$(16,7),V3$="",V4$="Enter Optional Order Reference Number (<F3>=None)",V1=54,V2=4,V0=7
1930 IF V3=3 THEN LET V$=""; PRINT @(V1,V2),'CL'; GOTO 1960
1940 GOSUB 7000
1950 FIND (ARE03_DEV,KEY=N0$+"  "+C$(10,6)+V$+"000",DOM=1900)IOL=ARE03A1
1960 LET C$(16,7)=V$
1970 GOTO 3900
2000 REM " --- Verify that new entry is ok
2005 FIND (ARM14_DEV,KEY=C$,DOM=2010); GOTO 2160
2010 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
2015 GOSUB 7000
2020 IF V3=4 THEN GOTO 0900
2025 IF C9$=C$ THEN GOTO 2050
2030 IF V$="DELETE" THEN GOSUB 2200; GOTO 0900
2040 IF V$="N" THEN GOTO 1740
2050 IF V$<>"Y" THEN GOTO 2000
2060 GOSUB 2100
2087 LET S2$=""
2090 GOTO 0900
2100 REM " --- Write it -- Subroutine
2110 WRITE (ARM14_DEV,KEY=C$)IOL=ARM14D
2120 LET C1$=C$(1,2)+"C"+C$(10,6)+C$(4,6)+C$(16)
2130 WRITE (ARM14_DEV,KEY=C1$)IOL=ARM14C
2150 RETURN 
2160 REM " --- Exiting Entry
2165 DIM MESSAGE$[1]
2170 LET MESSAGE$[0]="An Entry Already Exists under this Follow-Up Date for",MESSAGE$[1]="This Customer.  <CR> to Change This Entry."
2180 CALL "SYC.XA",0,MESSAGE$[ALL],1,-1,-1,V$,V3
2185 IF V3=4 THEN GOTO 0900
2190 GOTO 1800
2200 REM " --- Delete Follow-up Entry -- Subroutine
2210 REMOVE (ARM14_DEV,KEY=C$,DOM=2220)
2220 REMOVE (ARM14_DEV,KEY=C$(1,2)+"C"+C$(10,6)+C$(4,6)+C$(16),DOM=2230)
2230 RETURN 
2300 REM " --- Modify Credit Limit Here
2310 EXTRACT (ARM02_DEV,KEY=D0$,ERR=2320)IOL=ARM02A; GOTO 2350
2320 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Unable To Extract Record For Changes (<Enter>=Continue)",V0=1,V1=FNV(V4$),V2=22; GOSUB 7000; IF V3=4 THEN GOTO 4000
2350 LET V0$="N",V1$="C",V2$=STR(D[6]),V3$=M1$,V4$="Enter New Credit Limit Amount. ",V0=7,V1=65,V2=19
2360 GOSUB 7000
2370 LET D[6]=V
2380 WRITE (ARM02_DEV,KEY=D0$)IOL=ARM02A
2390 GOTO 4000
2500 REM " --- Change Date on Existing
2510 LET C9$=C$
2520 LET V0$="D",V1$="C6N",V2$=C9$(4,6),V3$="",V4$="",V0=6,V1=5,V2=4
2530 GOSUB 7000
2540 LET C9$(4,6)=V$
2550 IF C9$=C$ THEN GOTO 4000
2560 CALL "SYC.YN",1,"Is This Date Correct?",1,V$,V3
2567 PRINT @(0,23),'CL',@(0,22),
2570 IF V$="N" THEN GOTO 2500
2590 IF V$<>"Y" THEN GOTO 2560
2600 GOSUB 2200; REM "Remove old entry
2610 LET C$=C9$
2620 GOSUB 2100; REM "Write new entry
2630 GOTO 4000
2700 REM " --- Delete Existing Followup Date - From 4000
2710 IF POS(" "<>C$(16,7))=0 THEN LET V4$="Confirm: Do You Want To Remove The Follow-Up Date (Yes/No)?"; GOTO 2730
2720 LET V4$="Confirm: Do You Want To Delete The Order And The Date (Yes/No)?"
2730 LET V0$="S",V1$="KC",V2$="NO",V3$="",V0=3,V1=FNV(V4$),V2=22
2740 GOSUB 7000
2750 IF V$="YES" THEN GOSUB 2200; GOSUB 4200; GOTO 1000
2760 IF V$="NO" OR V3=4 THEN GOTO 4000
2770 GOTO 2700
2800 REM " --- Maintain customer comments"
2810 DIM INFO$[5],INFO[5]
2820 LET X$="Customer "+FNF$(B0$(3,P[0]),M0$)+" "+FNP$(B1$(1,30))
2830 LET INFO$[0]="ARM-05",INFO$[1]=B0$,INFO$[2]=X$,ACTION=0,INFO[1]=10
2850 CALL "SYC.KA",ACTION,INFO$[ALL],INFO[ALL]
2860 GOSUB COMMENTS
2890 GOTO 4000
3000 REM " --- Display/Maintain Order Info ***
3010 LET F1$="NEW",Z1$=""
3020 GOSUB 5600
3030 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Press <Enter> To Continue",V0=3,V1=FNV(V4$),V2=22
3033 GOSUB 7000
3040 IF V3=4 OR F1$="END" THEN GOTO 4000
3050 GOSUB 5600
3090 GOTO 3030
3500 REM " --- Display Invoice Info
3510 IF F1$="NEW" THEN GOTO 3540
3520 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Press <Enter> To Continue",V0=1,V1=FNV(V4$),V2=22
3530 GOSUB 7000
3535 IF V3=4 OR F1$="END" THEN GOTO 4000
3540 FOR I=11 TO 20; PRINT @(0,I),FILL(52),; NEXT I; LET L2=12
3545 PRINT 'SB',@(0,11),"Invoice    Date   Terms         Amount",'SF',
3550 IF F1$="" THEN GOTO 3570
3555 LET F1$=""
3560 READ (ART01_DEV,KEY=B0$(1,2)+D0$(9,2)+B0$(3,6),DOM=3570)
3570 LET K$=KEY(ART01_DEV,END=3850)
3580 IF POS(B0$(1,2)+D0$(9,2)+B0$(3,6)=K$)<>1 THEN GOTO 3850
3590 READ (ART01_DEV,KEY=K$)IOL=ART01A
3600 READ (ART11_DEV,KEY=U0$(1,17),DOM=3700)
3700 REM " --- Check Detail
3720 LET K11$=KEY(ART11_DEV,END=3800)
3740 IF K11$(1,17)<>U0$(1,17) THEN GOTO 3800
3750 READ (ART11_DEV,KEY=K11$)IOL=ART11A
3770 IF S1$(1,1)=" " THEN GOTO 3800
3780 LET U[0]=U[0]+S[0]+S[1]
3795 GOTO 3700
3800 REM " --- Display
3810 PRINT @(0,L2),U0$(11,7),@(9),FNB$(U0$(24,3)),@(20),U0$(22,2),@(25),U[0]:M1$
3820 LET L2=L2+1
3830 IF L2>20 THEN GOTO 3500
3840 GOTO 3570
3850 REM " --- End of File
3860 LET F1$="END"
3870 GOTO 3500
3900 REM " --- Next Field
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1700,1800,1900,2000
4000 REM " --- Options
4010 PRINT 'CI'
4011 PRINT @(0,22),'CL',
4015 PRINT @(2,22),"(",'SF',"R",'SB',"elease/",'SF',"C",'SB',"omments/",'SF',"O",'SB',"rders/",'SF',"I",'SB',"nvoices/",'SF',"L",'SB',"imit/",'SF',"D",'SB',"elete/",'SF',"H",'SB',"old",
4018 PRINT "/",'SF',"M",'SB',"odify Date):"
4020 LET V0$="S",V1$="KC",V2$="",V3$="",V0=1,V1=70,V2=22,V4$=""
4022 GOSUB 7000
4030 IF V$="M" THEN GOTO 2500
4040 IF V3=4 THEN GOTO 0900
4055 IF V$="H" THEN GOTO 4800
4060 IF V$="D" THEN GOTO 2700
4065 IF V$="L" THEN GOTO 2300
4070 IF V$="C" THEN GOTO 2800
4080 IF V$="O" THEN GOTO 3000
4090 IF V$="R" THEN GOTO 6000
4095 IF V$="I" THEN LET F1$="NEW"; GOTO 3500
4100 GOTO 4000
4200 REM " --- Delete the order
4210 DIM A0$(117),A[10],W1$(64),W[14]
4220 LET A0$(1)=N0$+"  "+C$(10)+"000"
4230 READ (ARE03_DEV,KEY=A0$(1,20),DOM=4750)IOL=ARE03A1
4240 LET K$=KEY(ARE03_DEV,END=4700)
4250 IF K$(1,17)<>A0$(1,17) THEN GOTO 4700
4260 READ (ARE13_DEV,KEY=K$(1,17),DOM=4270)IOL=ARE13A
4270 LET K13$=KEY(ARE13_DEV,END=4700)
4280 IF K13$(1,17)<>A0$(1,17) THEN GOTO 4700
4290 FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=4540)IOL=ARM10E
4300 IF POS(Y0$(25,1)="SP")=0 THEN GOTO 4540
4310 IF A0$(21,1)="P" THEN GOTO 4380
4320 IF Y0$(27,1)="Y" OR W1$(44,1)="N" THEN GOTO 4380
4330 REM " --- Uncommit Inventory
4340 LET ITEM$[0]=N0$,ITEM$[1]=W0$(31,2),ITEM$[2]=W0$(33,20)
4350 LET ACTION$="UC",REFS[0]=W[2]
4360 IF POS(I3$(17,1)="LS")=0 THEN GOTO LOT_SERIAL
4370 CALL "IVC.UA",ACTION$,FILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEM$[ALL],REFS$[ALL],REFS[ALL],STATUS
4380 REMOVE (ARE07_DEV,KEY=N0$+W0$(31)+W0$(3,2)+W0$(11,10)+W0$(5,6),DOM=4390)
4390 GOTO 4490
4400 LOT_SERIAL: 
4410 DIM T[2],H[11]
4420 READ (ARE23_DEV,KEY=W0$(1,20),DOM=4430)IOL=ARE23A
4430 LET K9$=KEY(ARE23_DEV,END=4490)
4440 IF K9$(1,20)<>W0$(1,20) THEN GOTO 4490
4450 READ (ARE23_DEV)IOL=ARE23A
4460 LET ITEM$[3]=T1$,REFS[0]=T[0]
4470 CALL "IVC.UA",ACTION$,FILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEM$[ALL],REFS$[ALL],REFS[ALL],STATUS
4480 REMOVE (ARE23_DEV,KEY=K9$); GOTO 4430
4490 IF W0$(26,1)<>"A" THEN GOTO 4540
4540 REMOVE (ARE13_DEV,KEY=K13$,DOM=4550)
4550 GOTO 4270
4700 REM " --- Remove Header
4710 REMOVE (ARE33_DEV,KEY=N0$+A0$(5,13),DOM=4720)
4720 REMOVE (ARE03_DEV,KEY=A0$(1,20))
4730 REMOVE (ARE04_DEV,KEY=N0$+"O"+A0$(3,15),DOM=4750)
4750 REM " --- Reset Next Order Number"
4755 DIM N[4]
4760 LET N$=N0$+"N",N[2]=1000,N[3]=1000
4770 EXTRACT (ARS10_DEV,KEY=N$,DOM=4790)IOL=ARS10N
4780 IF NUM(A0$(11,7))=N[2]-1 THEN LET N[2]=NUM(A0$(11,7))
4790 WRITE (ARS10_DEV,KEY=N$)IOL=ARS10N
4795 RETURN 
4800 REM " --- Hold/Unhold Customers
4810 EXTRACT (ARM02_DEV,KEY=D0$,ERR=4820)IOL=ARM02A; GOTO 4830
4820 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Unable To Extract Record For Changes. <Enter> To Retry, <F4> To Exit. ",V0=1,V1=FNV(V1$),V2=22; GOSUB 7000; IF V3=4 THEN GOTO 4000
4830 LET V0$="S",V1$="C",V2$=D1$(39,1),V3$="YNE",V4$="Y=Yes, N=No, E=Exempt From Credit Hold",V0=1,V1=57,V2=10
4840 GOSUB 7000
4850 LET D1$(39,1)=V$
4860 WRITE (ARM02_DEV,KEY=D0$)IOL=ARM02A
4870 PRINT @(57,10),D1$(39,1)
4880 GOTO 4000
4900 REM " --- Toggle Screen
4910 IF F2$="O" THEN LET F2$="C" ELSE LET F2$="O"
4920 LET S2$=""
4930 READ (ARM14_DEV,KEY=C0$,DOM=4940)
4940 PRINT 'CF'
4950 GOSUB 5000
4990 GOTO 1100
5000 REM " --- Display a screen full
5005 PRINT @(0,4),'CE'
5010 LET L=4,X$="",L1=0,F$=""
5015 IF S2$<>"" THEN READ (ARM14_DEV,KEY=S2$(1,LEN(S2$)-1),DOM=5020)
5020 LET V2$=KEY(ARM14_DEV,END=5090)
5023 READ (ARM14_DEV)
5025 IF POS(C0$=V2$)=0 THEN GOTO 5090
5026 IF F2$="O" AND V2$(16,7)=FILL(7) THEN GOTO 5020
5027 IF F2$="C" AND V2$(16,7)<>FILL(7) THEN GOTO 5020
5028 DIM A0$(117)
5029 IF F2$="O" THEN READ (ARE03_DEV,KEY=V2$(1,2)+"  "+V2$(10)+"000",DOM=5075)IOL=ARE03A
5030 IF L1=0 THEN LET S2$=V2$
5040 PRINT @(0,L),L1+1:"##.B",@(5),V2$(6,2),"/",V2$(8,2),"/",FNYY21_YY$(V2$(4,2)),@(15),FNF$(V2$(10,P[0]),M0$),@(53),V2$(16,7),
5043 IF F2$="O" THEN PRINT @(62,L),FNB$(A0$(77,3)),@(71),FNB$(A0$(27,3))
5045 DIM B1$(250); LET B1$(1)="(Not On File)"
5050 FIND (ARM01_DEV,KEY=V2$(1,2)+V2$(10,6),DOM=5055)IOL=ARM01A
5055 PRINT @(16+M0,L),B1$(1,30),
5060 LET L=L+1,L1=L1+1,X$=X$+V2$
5065 IF L<18 THEN GOTO 5020
5070 RETURN 
5075 REMOVE (ARM14_DEV,KEY=V2$(1,2)+"C"+V2$(10,6)+V2$(4,6)+V2$(16),DOM=5080)
5080 REMOVE (ARM14_DEV,KEY=V2$,DOM=5085)
5085 GOTO 5020
5090 LET F$="END"
5095 RETURN 
5100 REM " --- Display Order/Cust info
5110 PRINT 'SB',@(0,6),"Address:",@(46),"Telephone:",@(52,7),"Fax:",@(44,8),"Salesperson:",@(50,9),"Terms:",@(0,10),"Contact:",@(44),"Credit Hold:",@(57,12),"Future:",@(56,13),"Current:",@(56,14),"30 Days:",@(56,15),"60 Days:",@(56,16),"90 Days:",@(55,17),"120 Days:",@(56,18),"Balance:",@(55,19),"Cr Limit:",@(56,20),"Ord Amt:",'SF',
5190 RETURN 
5200 REM " --- Cust/Ord info
5210 PRINT @(9,6),B1$(31,24),@(57),-NUM(B1$(112,10)):"(###) 000-0000",@(9,7),B1$(55,24),@(57),-NUM(B1$(169,10)):"(###) 000-0000",@(9,8),Z$," ",FNZ$(B1$(103)),@(57),X1$(7,20),@(57,9),X0$(4,2),"-",X0$(6,20),@(9,10),B1$(228,20),@(57),D1$(39,1)
5230 PRINT @(65,12),D[0]:M1$,@(65,13),D[1]:M1$,@(65,14),D[2]:M1$,@(65,15),D[3]:M1$,@(65,16),D[4]:M1$,@(65,17),D[5]:M1$,@(65,18),D[0]+D[1]+D[2]+D[3]+D[4]+D[5]:M1$,@(65,19),D[6]:M1$
5290 RETURN 
5300 REM " --- Display Comments
5305 COMMENTS: 
5310 DIM G1$(48),Z1$(0)
5320 FOR I=11 TO 20; PRINT @(0,I),FILL(52),; NEXT I; LET L2=12
5330 IF F1>99 THEN LET F1=0
5340 READ (ARM05_DEV,KEY=B0$+STR(F1:"00"),DOM=5350)IOL=ARM05A; GOTO 5360
5350 READ (ARM05_DEV,END=5500)IOL=ARM05A
5360 IF POS(B0$=G0$)<>1 THEN GOTO 5500
5365 IF G1$=FILL(LEN(G1$)) THEN GOTO 5350
5370 PRINT @(0,L2),G0$(9)," ",@(3),G1$
5380 LET F1=NUM(G0$(9,2),ERR=5390),Z1$=Z1$+G0$(9,2)
5390 LET L2=L2+1
5400 IF L2<21 THEN GOTO 5350
5410 GOTO 5510
5500 LET F1=100
5510 RETURN 
5600 REM " --- Display Orders
5610 DIM A0$(117),A[10]
5620 FOR I=11 TO 20; PRINT @(0,I),FILL(52),; NEXT I; LET L2=12
5625 PRINT 'SB',@(0,11),"Order #   Date  Terms      Amount",'SF'
5630 IF F1$="" THEN GOTO 5650
5635 LET F1$=""
5640 READ (ARE03_DEV,KEY=B0$(1,2)+D0$(9,2)+B0$(3,6),DOM=5650)
5650 LET K$=KEY(ARE03_DEV,END=5770)
5660 IF POS(B0$(1,2)+D0$(9,2)+B0$(3,6)=K$)=1 THEN GOTO 5680
5670 LET F1$="END"
5675 GOTO 5790
5680 READ (ARE03_DEV,KEY=K$)IOL=ARE03A1
5690 GOSUB ORDER_TOTS
5695 LET A[6]=TOT_PRICE
5700 PRINT @(0,L2),A0$(11,7)," ",FNB$(A0$(77,3))," ",A0$(62,2),"  ",A[6]:M1$
5704 IF A0$(21,1)="P" THEN PRINT @(36,L2),"Quote    "
5705 IF A0$(22,1)="I" THEN PRINT @(36,L2),"Invoice  "
5706 IF A0$(21,1)="V" THEN PRINT @(36,L2),"Void     "; GOTO 5710
5707 IF A0$(101,1)="C" THEN PRINT @(36,L2),"Hold     "
5708 IF A0$(23,1)="B" THEN PRINT @(36,L2),"Backorder"
5709 IF A0$(101,1)="R" THEN PRINT @(36,L2),"Released "
5710 LET L2=L2+1
5720 IF L2<20 THEN GOTO 5650
5790 RETURN 
5900 REM " --- Get Order Totals
5905 ORDER_TOTS: 
5910 LET TOT_PRICE=0
5920 READ (ARE13_DEV,KEY=A0$(1,20),DOM=5930)
5930 LET K1$=KEY(ARE13_DEV,END=5990)
5940 IF K1$(1,17)<>A0$(1,17) THEN GOTO 5990
5945 READ (ARE13_DEV,KEY=K1$)IOL=ARE13A2
5950 LET TOT_PRICE=TOT_PRICE+EXT_PRICE
5960 GOTO 5930
5990 RETURN 
6000 REM " --- Release an Order from Credit Hold
6010 IF POS(" "<>C$(16,7))=0 THEN GOTO 4000
6020 LET V0$="S",V1$="KC",V2$="YES",V3$="",V4$="Confirm: OK To Release Order "+$22$+C$(16,7)+$22$+" (Yes/No)?",V0=3,V1=FNV(V4$),V2=22
6025 GOSUB 7000
6030 IF V$="YES" THEN GOTO 6070
6040 IF V$="N" OR V$="NO" OR V$="" OR V3=4 THEN GOTO 4000
6050 GOTO 6000
6070 DIM A[10],A0$(117),X0$(30); LET X0$(6)="***Not On File***"
6080 EXTRACT (ARE03_DEV,KEY=N0$+"  "+C$(10,13)+"000",DOM=6190)IOL=ARE03A1
6090 FIND (ARM10_DEV,KEY=N0$+"A"+A0$(62,2),DOM=6100)IOL=ARM10A
6100 PRINT @(0,21),'CL',"Current Terms Are: ",A0$(62,2)," ",X0$(6,20),@(0,22),
6105 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Enter New Terms (<Enter>=No Change/<F3>=Lookup)",V0=3,V1=FNV(V4$),V2=22
6110 GOSUB 7000
6115 IF V$="" AND V3<>3 THEN GOTO 6180
6120 IF V3<>3 THEN GOTO 6160
6140 LET V4=1,V5=ARM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="A"
6150 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
6160 LET V$=V$+"  ",V$=V$(1,2); FIND (ARM10_DEV,KEY=N0$+"A"+V$,DOM=6100)IOL=ARM10A
6170 LET A0$(62,2)=V$
6172 LET V0$="S",V1$="KC",V2$="",V3$="",V4$=A0$(62,2)+" "+X0$(6,20)+" (<Enter>=Continue)",V0=1,V1=FNV(V4$),V2=22
6175 GOSUB 7000
6180 PRINT @(0,21),'CL',
6185 LET A0$(101,1)="R"
6187 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A1
6190 GOSUB 2200
6200 REM " --- Print the order?
6230 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Order Has Been Released (<Enter>=Continue"
6235 IF POS(" "<>A0$)>0 AND R0$<>"" THEN LET V4$=V4$+"/<F1>=Print/<F3>=Select Printer"
6240 LET V4$=V4$+")",V0=1,V1=FNV(V4$),V2=22
6250 GOSUB 7000
6260 IF V3=3 THEN GOSUB 6500; GOTO 6200
6280 IF V3=1 THEN GOTO 6400
6300 REM " --- Print the order!!
6330 GOTO 1000
6400 REM " --- Print it here
6410 IF R0$="" THEN GOTO 6200
6420 LET PRTR_DEV=UNT
6430 LET SELECTPR$=R2$(15,2)+FORMAT$
6440 CALL "SYC.GA",PRTR_DEV,0,"Order Pick List Printing",SELECTPR$,STATUS
6450 IF STATUS THEN GOTO 6200
6470 DIM W1$(64),W[14],S$(11)
6480 LET O9=2,O9$="ARM.QA"; RUN "OPR.PA"
6500 REM " --- Printer Select
6510 DIM OPTION$[5]; LET OPTION$[1]="ORDER"
6520 CALL "SYC.QP",STAT,OPTION$[ALL]
6530 IF OPTION$[1]<>"EXIT" THEN LET R2$(15,2)=OPTION$[1]
6540 IF LEN(OPTION$[1])>2 THEN LET FORMAT$=OPTION$[1](3)
6560 PRINT 'SB',@(16,1),R2$(15,2),'SF'
6590 RETURN 
6600 REM " --- All Done
6620 LET C0$=N0$+"D"
6690 IF F$="" THEN GOTO 1000 ELSE GOTO 0900
6900 REM " --- Missing Terminal for Printing
6905 GET_OP_TERMINAL: 
6910 FIND (ARM07_DEV,KEY=N0$+FID(0),DOM=6990)IOL=ARM07A
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

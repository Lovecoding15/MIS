0010 REM "PRM - Deduction Code Maintenance"
0020 REM "Program PRM.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "26Mar97 WGH: Writing too many numeric variables to PRM-10 B (1520)"
0040 REM 
0050 SETESC 9000
0055 SETERR 9000
0060 ON O0 GOTO 0070,4000
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-10",FILES$[2]="SYS-01",FILES$[3]="GLM-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM10_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLISTs"
0210 PRM10B: IOLIST A0$(1),A1$(1),A[ALL],A2$(1),A3$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0280 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[4],G[4]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[3]=NUM(P3$(7,1))
0500 REM " --- Init Data"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 DIM A0$(33),A1$(G[4]),A2$(10),A3$(5),A[1]
0530 LET K0=1,K9=5
0540 LET A0$(1,3)=N0$+"B"
0550 LET M1=LEN(M1$),M2=LEN(M2$),G5=LEN(G5$)
0560 DIM INFO$[20]
0570 CALL "SYC.VA","PR",INFO$[ALL]
0580 LET GL$=INFO$[9]
0590 PRECISION P[3]
0600 REM " --- Additional Opens"
0610 IF GL$<>"Y" THEN GOTO 0700
0620 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS>0 THEN GOTO 9900
0640 LET GLM01_DEV=CHANNELS[3]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,12,0
0720 PRINT 'SB',@(1,3),"Deduction Cd:",@(2,4),"Description:",@(2,5),"Report Code:",@(44),"Special Deduct:",@(2,6),"Deduct Type:",@(44),"Balance/Limit?:",@(0,7),"Periods Taken:",@(3,8),"Total Code:",@(3,9),
0730 IF GL$="Y" THEN PRINT @(3,9),"GL Account:",
0735 PRINT @(2,10),"Deduct Rate:",@(1,11),"Deduct Limit:",@(15,13),"Total To",@(40),"Operator",
0740 FOR X=1 TO 5
0750 PRINT @(12,X+13),X:"#.",
0760 NEXT X
0790 PRINT 'SF',
0900 REM " --- Init File"
0920 READ (PRM10_DEV,KEY=A0$(1,3),DOM=0930)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(PRM10_DEV,END=1050)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4)
1050 LET V0$="S",V1$="CEK",V3$="",V4$="Enter Up To 2 Characters, <F3>=Lookup, <F4>=End",V0=2,V1=15,V2=3,O9=0,O7=0
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN CALL "SYC.LZ",V1,V2,1,PRM10_DEV,4,2,6,16,V$,"B",N0$
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(4)=V$
1200 REM " --- Find Key"
1220 FIND (PRM10_DEV,KEY=A0$(1,K9),DOM=1400)IOL=PRM10B
1230 IF LEN(A0$)>32 THEN IF POS("B"=A0$(33,1))>0 THEN PRINT 'SB',@(8,11),"Balnc",'SF', ELSE PRINT 'SB',@(8,11),"Limit",'SF',
1240 IF GL$="N" THEN LET A1$(1)=""
1300 REM " --- Display"
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record"
1420 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup"
1510 LET A0$(6)="",A1$(1)="",A2$(1)=""
1520 DIM A[1]
1530 PRINT 'SB',@(8,11),"Limit",'SF',
1540 LET A0$(24,1)="F",A0$(25,5)="12345",A0$(32,2)="YL"
1550 GOSUB 5000
2000 REM " --- Overlay"
2020 LET O0=1,O9=1
2040 RUN "PRM.BB"
4000 REM " --- Options"
4005 IF O7=1 THEN GOTO 4200
4010 PRECISION P[3]
4020 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF O9=0 THEN IF V$="" OR V$="Y" THEN GOTO 1000
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 WRITE (PRM10_DEV,KEY=A0$(1,K9))IOL=PRM10B
4190 GOTO 1000
4200 REM " --- Delete"
4220 REMOVE (PRM10_DEV,KEY=A0$(1,K9),DOM=4230)
4290 GOTO 1000
4500 REM " --- Print"
4520 CALL "SYC.YN",1,"Do You Wish To Print A List Of All Records",1,V$,V3
4540 ON POS(V$="YN") GOTO 4500,4550,9900
4560 LET O0=1
4580 RUN "PRM.BC"
5000 REM " --- Display"
5010 PRINT @(15,4),A0$(6,16),@(15,5),A0$(22,2),@(15,6),A0$(24,1),@(15,7),A0$(25,5),@(15,8),A0$(30,2),
5015 IF GL$="Y" THEN PRINT @(15,9),FNF$(A1$(1,G[3]),G5$),
5020 PRINT @(15,10),A[0]:M3$,@(15,11),A[1]:M3$,@(60,5),A0$(32,1),@(60,6),A0$(33,1),
5030 FOR X=0 TO 4
5040 IF A2$(X*2+1,2)="  " THEN GOTO 5060
5050 PRINT @(15,X+14),A2$(X*2+1,2),@(45),A3$(X+1,1),
5060 NEXT X
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

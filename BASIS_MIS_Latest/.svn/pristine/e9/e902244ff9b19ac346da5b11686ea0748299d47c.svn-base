0010 REM "ARM - Customer Maintenance (Job Numbers)"
0020 REM "Program ARM.MJ"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0230 ARM09A: IOLIST C0$,C1$(1),C[ALL]
0500 REM " --- Init Data"
0520 DIM C0$(40),C1$(148),C[2]
0540 LET C0$(1)=A0$
0700 REM " --- Background"
0715 CALL "SYC.WC",1,0,80,0,0,14,0
0740 PRINT @(0,L0),'CE','SB',@(15,5),"Job Number Maintenance",@(3,6),"Job Number:",@(5,7),"Job Name:",@(6,8),"Address:",@(5,11),"Zip Code:",@(6,12),"Contact:",@(4,13),"Telephone:",@(30),"Ext:",@(42),"Retain:",@(5,14),"Preliminary Lien Info",@(24,15),"Date:",@(21,16),"Lien No:",@(22,17),"Amount:",@(6,18),"Date Of First Shipment:",@(9,19),"Total Sales To Date:",@(10,20),"Date Last Invoiced:",'SF',
0900 REM " --- Init File"
0920 READ (ARM09_DEV,KEY=A0$,DOM=0930)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(ARM09_DEV,END=1050)
1030 IF V2$(1,8)<>A0$ THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(9,10)
1050 LET V0$="S",V1$="EC",V0=10,V1=15,V2=6,V4$="Enter Up To "+STR(V0)+" Characters (<F4>=End)"
1060 GOSUB 7000
1070 IF V3=4 THEN RUN "ARM.MA"
1090 IF V$="" THEN GOTO 0900
1100 LET C0$(9,10)=V$
1200 REM " --- Find Key"
1210 LET O9=0
1220 FIND (ARM09_DEV,KEY=C0$(1,18),DOM=1400)IOL=ARM09A
1300 REM " --- Display"
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record"
1420 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-Up"
1510 LET C0$(19,21)="",C1$(1,148)=""
1520 DIM C[2]
2000 REM " --- Name"
2005 EXTRACT (ARM09_DEV,KEY=C0$(1,18),DOM=2006)IOL=ARM09A
2006 GOSUB 5000
2010 LET O9=1
2020 LET V0$="S",V1$="C",V2$=C1$(1,30),V3$="",V0=30,V1=15,V2=7,I0=0,V4$="Enter Up To "+STR(V0)+" Characters"
2040 GOSUB 7000
2060 LET C1$(1,30)=V$
2090 GOTO 3900
2100 REM " --- Address"
2110 LET X=I0-1
2120 LET V0$="S",V1$="C",V2$=C1$(X*24+31,24),V0=24,V1=15,V2=X+8,V4$="Enter Up To "+STR(V0)+" Characters"
2130 GOSUB 7000
2140 LET C1$(X*24+31,24)=V$
2290 GOTO 3900
2300 REM " --- Zip Code"
2320 LET V0$="S",V1$="C",V2$=C1$(103,P[2]),V3$="",V0=P[2],V1=15,V2=11,V4$="Enter (up to) "+STR(V0)+" characters"
2340 GOSUB 7000
2360 LET C1$(103,9)=V$
2390 GOTO 3900
2400 REM " --- Phone"
2420 LET V0$="H",V1$="",V2$=C1$(112,10),V3$=M8$,V0=10,V1=15,V2=13
2440 GOSUB 7000
2460 LET C1$(112,10)=V$
2490 GOTO 3900
2500 REM " --- Ext"
2520 LET V0$="Z",V1$="",V2$=C1$(122,4),V0=4,V1=35,V2=13
2540 GOSUB 7000
2560 LET C1$(122,4)=V$
2590 GOTO 3900
2600 REM " --- Contact"
2620 LET V0$="S",V1$="",V2$=C1$(126,20),V3$="",V0=24,V1=15,V2=12
2650 GOSUB 7000
2660 LET C1$(126,20)=V$
2690 GOTO 3900
2700 REM " --- Retain"
2720 LET V0$="Y",V1$="RC",V2$=C0$(40,1),V3$="YN",V0=1,V1=50,V2=13,V4$="Enter Y To Retain Information For This Job"
2730 GOSUB 7000
2750 LET C0$(40,1)=V$
2790 GOTO 3900
2800 REM " --- Lien Date"
2820 LET V0$="D",V1$="",V2$=C0$(19,3),V3$="",V4$="",V0=8,V1=33,V2=15
2830 GOSUB 7000
2850 LET C0$(19,3)=V$
2890 GOTO 3900
2900 REM " --- Lien #"
2920 LET V0$="S",V1$="C",V2$=C0$(22,12),V0=12,V1=30,V2=16,V4$="Enter Up To "+STR(V0)+" Characters"
2930 GOSUB 7000
2950 LET C0$(22,12)=V$
2990 GOTO 3900
3000 REM " --- Lien Amount"
3020 LET V0$="N",V1$="",V2$=STR(C[0]),V3$=M1$,V4$="",V0=12,V1=30,V2=17
3030 GOSUB 7000
3050 LET C[0]=V
3090 GOTO 3900
3100 REM " --- Date First Ship
3120 LET V0$="D",V1$="",V2$=C0$(34,3),V0=8,V1=33,V2=18
3130 GOSUB 7000
3150 LET C0$(34,3)=V$
3200 REM " --- Total Sales"
3220 LET V0$="N",V2$=STR(C[1]),V3$=M1$,V0=12,V1=30,V2=19
3230 GOSUB 7000
3240 IF V3=2 THEN GOTO 3100
3250 LET C[1]=V
3300 REM " --- Date Last Invoiced"
3320 LET V0$="D",V1$="",V2$=C0$(37,3),V0=8,V1=33,V2=20
3330 GOSUB 7000
3340 IF V3=2 THEN GOTO 3200
3350 LET C0$(37,3)=V$
3390 GOTO 4000
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2100,2100,2300,2600,2400,2500,2700,2800,2900,3000,3990
4000 REM " --- Options"
4010 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4035 IF V$="HISTORY" THEN GOTO 3100
4040 IF V$="" THEN IF O9=0 THEN GOTO 1000
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 WRITE (ARM09_DEV,KEY=C0$(1,18))IOL=ARM09A
4190 GOTO 1000
4200 REM " --- Delete"
4220 REMOVE (ARM09_DEV,KEY=C0$(1,18),DOM=4230)
4290 GOTO 1000
5000 REM " --- Display"
5020 PRINT @(15,7),C1$(1,30),@(15,8),C1$(31,24),@(15,9),C1$(55,24),@(15,10),C1$(79,24),@(15,11),FNZ$(C1$(103)),@(15,12),C1$(126,20),@(15,13),FNF$(C1$(112,10),M8$),@(35,13),C1$(122,4),@(50,13),C0$(40,1),@(33,15),FNB$(C0$(19,3)),@(30,16),C0$(22,12),@(30,17),C[0]:M1$,@(33,18),FNB$(C0$(34,3)),@(30,19),C[1]:M1$,@(33,20),FNB$(C0$(37,3)),
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 RUN "ARM.MA"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "DDS - Export Record Layouts (Input)"
0020 REM "Program DDX.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="DDM-01",FILES$[2]="DDM-03",FILES$[3]="DDM-04"
0130 LET FILES$[4]="DDM-10",FILES$[5]="DDM-20",FILES$[6]="SYM-02"
0135 LET FILES$[7]="SYM-03",FILES$[8]="SYM-04",FILES$[9]="SYS-01"
0160 CALL "SYC.DA",1,1,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9400
0175 LET DDM01_DEV=CHANNELS[1],DDM03_DEV=CHANNELS[2],DDM04_DEV=CHANNELS[3]
0180 LET DDM10_DEV=CHANNELS[4],DDM20_DEV=CHANNELS[5],SYM02_DEV=CHANNELS[6]
0185 LET SYM03_DEV=CHANNELS[7],SYM04_DEV=CHANNELS[8],SYS01_DEV=CHANNELS[9]
0200 REM " --- IOLists
0260 SYM04A: IOLIST S0$(1),S1$(1)
0270 SYS01T: IOLIST TERMINAL$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="DD",N4$=F5$
0500 REM " --- Initializations
0550 LET SYSTEM$="",BEGFILE$="",ENDFILE$=""
0700 REM " --- Background
0710 FOR X=4 TO 8 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(33,3),"System",@(20,5),"Beginning File Name",@(23,7),"Ending File Name",'SF',
1000 REM " --- System
1010 LET V0$="S",V1$="C",V2$=SYSTEM$,V3$="",V0=2,V1=40,V2=3,I0=0
1020 LET V4$="Enter (Optional) System ID (<F1>=All/<F3>=Lookup)"
1030 GOSUB 7000
1040 ON V3 GOTO 1100,1120,1000,1050,4000
1050 CALL "SYC.LM",V$
1100 REM 
1110 IF V$<>"" THEN GOTO 1150
1120 LET SYSTEM$=""
1130 PRINT @(V1,V2),'CL',"All"
1140 GOTO 1190
1150 DIM S0$(2),S1$(35)
1160 LET S0$(1)=V$
1170 FIND (SYM04_DEV,KEY=S0$,DOM=1180)IOL=SYM04A
1180 LET SYSTEM$=FNP$(S0$(1,2))
1185 PRINT @(V1,V2),'CL',S0$(1,2)," ",S1$
1190 GOTO 3900
1400 REM " --- Beginning File Name
1410 LET V0$="S",V1$="C",V2$=BEGFILE$,V3$="",V0=6,V1=40,V2=5
1420 LET V4$="Enter Beginning File Name (<F1>=First/<F3>=Lookup)"
1430 GOSUB 7000
1440 ON V3 GOTO 1500,1520,3900,1450,4000
1450 GOSUB 6100
1500 REM 
1510 IF V$<>"" THEN GOTO 1550
1520 LET BEGFILE$=""
1530 PRINT @(V1,V2),'CL',"First"
1540 GOTO 1590
1550 LET BEGFILE$=V$
1560 PRINT @(V1,V2),'CL',BEGFILE$
1590 GOTO 3900
1600 REM " --- Ending File Name
1605 IF BEGFILE$<>"" THEN IF ENDFILE$="" THEN LET ENDFILE$=BEGFILE$
1610 LET V0$="S",V1$="C",V2$=ENDFILE$,V3$="",V0=6,V1=40,V2=7
1620 LET V4$="Enter Ending File Name (<F1>=Last/<F3>=Lookup)"
1630 GOSUB 7000
1640 ON V3 GOTO 1700,1720,3900,1650,4000
1650 GOSUB 6100
1700 REM 
1710 IF V$<>"" THEN GOTO 1750
1720 LET ENDFILE$=""
1730 PRINT @(V1,V2),'CL',"Last"
1740 GOTO 1790
1750 LET ENDFILE$=V$
1760 PRINT @(V1,V2),'CL',ENDFILE$
1790 GOTO 3900
3900 REM " --- Next field
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1400,1600,3990
3990 LET I0=0
4000 REM " --- Correct?
4010 GOSUB 5000
4020 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4030 ON V3 GOTO 4040,4000,4000,4000,9900
4090 ON POS(V$="YN") GOTO 4000,4900,1000
4900 REM " --- Run export overlay
4990 RUN "DDX.EB"
5000 REM " --- Display
5010 IF SYSTEM$="" THEN PRINT @(40,3),"All" ELSE PRINT @(40,3),SYSTEM$
5020 IF BEGFILE$="" THEN PRINT @(40,5),"First" ELSE PRINT @(40,5),BEGFILE$
5030 IF ENDFILE$="" THEN PRINT @(40,7),"Last" ELSE PRINT @(40,7),ENDFILE$
5090 RETURN 
6100 REM " --- File Name Lookup
6110 LET CHANNEL=SYM03_DEV,RECORD$="",NUMBER=1,TITLE$="ADD+ON System Files"
6120 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6130 LET DESCRIPTION$[0]="File",DESCRIPTION$[1]="Description"
6140 LET FIELD[0]=1,POSITION[0]=1,LENGTH[0]=6
6150 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6160 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6500 REM " --- Call Windowed Lookup
6510 LET V$="NO COMPANY",ROW=V2+2
6520 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9400 REM " --- Missing ADD+ON Data Dictionary"
9410 DIM MESSAGE$[0]
9420 LET MESSAGE$[0]="The ADD+ON Data Dictionary Cannot Be Opened (<Enter>=Continue)"
9430 CALL "SYC.XA",1,MESSAGE$[ALL],0,22,-1,V$,V3
9490 GOTO 9900
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

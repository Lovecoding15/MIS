0010 REM "BMM - Operation Code Maintenance
0020 REM "Program BMM.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="BMM-08",FILES$[2]="SYS-01",FILES$[3]="GLM-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET BMM08_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 BMM08A: IOLIST A0$(1),A[ALL]
0280 GLM01A: IOLIST *,X0$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 BMS01A: IOLIST X$,P1$,P2$,P3$,P4,P5
0320 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,X$,X$,X$
0330 IVS01A: IOLIST X$,X$,P6$,X$,X$,X$,X$,X$,X$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="BM",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=BMS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV"+"00",DOM=9800)IOL=IVS01A
0445 FIND (SYS01_DEV,KEY=N0$+"GL"+"00",DOM=9800)IOL=GLS01A
0450 DIM P[2]
0490 LET P[0]=NUM(P6$(1,2)),G0=NUM(G2$(7,2)),G1=NUM(G2$(9,2))
0500 REM " --- Init Data"
0505 PRECISION NUM(P6$(5,1))
0510 DIM A0$(90),A[13],INFO$[20]
0540 CALL "SYC.RM",P1$,P1,5,8
0545 CALL "SYC.RM",P2$,P2,5,8
0550 CALL "SYC.RM",P3$,P3,5,8
0555 CALL "SYC.VA","BM",INFO$[ALL]
0560 LET GL$=INFO$[9]
0570 CALL "SYC.VA","WO",INFO$[ALL]
0580 LET WO$=INFO$[20],M6$="#0.00",M6=LEN(M6$),K9=5,A0$(1,2)=N0$
0600 REM " --- Additional Opens
0610 IF GL$<>"Y" THEN GOTO 0700
0660 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0670 IF STATUS>0 THEN GOTO 9900
0680 LET GLM01_DEV=CHANNELS[3]
0700 REM " --- Background"
0710 PRINT 'SB',@(8,3),"Operation Code:",@(11,4),"Description:",
0720 IF GL$="Y" THEN PRINT @(2,5),"Direct Labor Account: ",@(0,6),"Overhead Labor Account: ",
0730 PRINT @(12,7),"Labor Rate:",@(7,8),"Pieces Per Hour:",@(7,9),"Overhead Factor:",@(12,10),"Setup Time:",
0740 IF WO$="Y" THEN PRINT @(12,11),"Queue Time:",@(13,12),"Move Time:",
0790 PRINT 'SF',
0900 REM " --- Init File"
0990 READ (BMM08_DEV,KEY=A0$(1,2),DOM=1000)
1000 REM " --- Key"
1005 DIM A[13]
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(BMM08_DEV,END=1050)
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3)
1050 LET V0$="S",V1$="KEC",V3$="",V0=3,V1=24,V2=3
1055 LET V4$="<F3>=Lookup"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN GOSUB 6000
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(3,3)=V$
1200 REM " --- Find Key"
1220 FIND (BMM08_DEV,KEY=A0$(1,K9),DOM=1400)IOL=BMM08A
1300 REM " --- Display"
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-up"
1510 LET A0$(6)="",A[1]=1
2000 REM " --- Description"
2020 LET V0$="S",V1$="",V2$=A0$(6,20),V3$="",V0=20,V1=24,V2=4,I0=0
2040 GOSUB 7000
2050 IF V3=4 THEN GOTO 3900
2060 LET A0$(6,20)=V$
2090 GOTO 3900
2100 REM " --- Operation Rate
2120 LET V0$="N",V1$="",V2$=STR(A[0]),V3$=P1$,V0=0,V1=24,V2=7
2140 GOSUB 7000
2150 LET A[0]=V
2190 GOTO 3900
2300 REM " --- Pieces per Hour
2310 LET V0$="N",V1$="",V2$=STR(A[1]),V3$=P2$,V0=0,V1=24,V2=8
2320 GOSUB 7000
2330 IF V>0 THEN GOTO 2380
2340 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Pieces/Hr Must Be Greater Than Zero! (<Enter>=Continue): ",V0=1,V1=FNV(V4$),V2=22
2350 PRINT @(V1,V2),'RB',
2360 GOSUB 7000
2370 GOTO 2300
2380 LET A[1]=V
2390 GOTO 3900
2400 REM " --- Direct Labor Account
2410 IF GL$<>"Y" THEN GOTO 2490
2420 LET V0$="M",V1$="C",V2$=A0$(31,G0),V3$=G1$,V4$="Enter A Valid G/L Account (<F3>=Lookup)",V0=G1,V1=24,V2=5
2430 GOSUB 7000
2440 IF V3=2 THEN GOTO 3900
2450 IF V3=3 THEN CALL "SYC.LG",V$
2470 FIND (GLM01_DEV,KEY=N0$+V$,DOM=2400)IOL=GLM01A
2480 LET A0$(31,G1)=V$
2485 PRINT @(V1,V2),FNF$(A0$(31,G0),G5$)," ",X0$(1,35),
2490 GOTO 3900
2500 REM " --- Overhead Labor Account
2510 IF GL$<>"Y" THEN GOTO 2590
2520 LET V0$="M",V1$="C",V2$=A0$(31+G1,G0),V3$=G1$,V4$="Enter A Valid G/L Account (<F3>=Lookup)",V0=G1,V1=24,V2=6
2530 GOSUB 7000
2540 IF V3=2 THEN GOTO 3900
2550 IF V3=3 THEN CALL "SYC.LG",V$
2560 IF V$="" THEN GOTO 2500
2570 FIND (GLM01_DEV,KEY=N0$+V$,DOM=2500)IOL=GLM01A
2580 LET A0$(31+G1,G1)=V$
2585 PRINT @(V1,V2),FNF$(A0$(31+G1,G0),G5$)," ",X0$(1,35),
2590 GOTO 3900
2600 REM " --- Overhead Factor
2620 LET V0$="N",V1$="C",V2$=STR(A[2]),V3$=P3$,V4$="Enter An Overhead Factor ",V0=0,V1=24,V2=9
2630 GOSUB 7000
2640 IF V3=2 THEN GOTO 3900
2680 LET A[2]=V
2690 GOTO 3900
2700 REM " --- Queue Time
2710 IF WO$="N" THEN GOTO 2790
2720 LET V0$="N",V1$="C",V2$=STR(A[3]),V3$=M6$,V4$="Enter Queue Time Using Hundredths Of An Hour",V0=0,V1=24,V2=11
2730 GOSUB 7000
2740 IF V3=2 THEN GOTO 3900
2780 LET A[3]=V
2790 GOTO 3900
2800 REM " --- Setup Time
2820 LET V0$="N",V1$="C",V2$=STR(A[4]),V3$=M6$,V4$="Enter Setup Time Using Hundredths Of An Hour",V0=0,V1=24,V2=10
2830 GOSUB 7000
2840 IF V3=2 THEN GOTO 3900
2880 LET A[4]=V
2890 GOTO 3900
2900 REM " --- Move Time
2910 IF WO$="N" THEN GOTO 2990
2920 LET V0$="N",V1$="C",V2$=STR(A[5]),V3$=M6$,V4$="Enter Move Time Using Hundredths Of An Hour",V0=0,V1=24,V2=12
2930 GOSUB 7000
2940 IF V3=2 THEN GOTO 3900
2980 LET A[5]=V
2990 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2400,2500,2100,2300,2600,2800,2700,2900,3990
4000 REM " --- Options"
4010 GOSUB 6100
4020 IF OK$="Y" THEN GOTO 4050
4030 DIM MESSAGE$[1]; LET MESSAGE$[0]="You Must Enter A Valid G/L Account Number For Both Direct",MESSAGE$[1]="And Overhead Labor. <F4> To Delete This Operation Code."
4035 CALL "SYC.XA",1,MESSAGE$[ALL],1,22,-1,V$,V3
4040 IF V3=4 THEN GOTO 4200
4045 GOTO 2000
4050 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4070 GOSUB 7000
4075 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 WRITE (BMM08_DEV,KEY=A0$(1,K9))IOL=BMM08A
4190 GOTO 1000
4200 REM " --- Delete"
4220 REMOVE (BMM08_DEV,KEY=A0$(1,K9),DOM=4230)
4290 GOTO 1000
4500 REM " --- Print"
4520 CALL "SYC.YN",1,"Do You Wish To Print A List Of All Records",1,V$,V3
4540 ON POS(V$="YN") GOTO 4500,4550,9900
4560 LET O0=1
4580 RUN "BMM.AB"
5000 REM " --- Display"
5020 PRINT @(24,3),A0$(3,3),@(24,4),A0$(6,20),
5030 IF GL$="Y" THEN PRINT @(24,5),FNF$(A0$(31,G0),G5$),@(24,6),FNF$(A0$(31+G1,G0),G5$),
5040 PRINT @(24,7),A[0]:P1$,@(24,8),A[1]:P3$,@(24,9),A[2]:P3$,@(24,10),A[4]:M6$,
5060 IF WO$="Y" THEN PRINT @(24,11),A[3]:M6$,@(24,12),A[5]:M6$,
5090 RETURN 
6000 REM " --- Lookup"
6010 LET CHANNEL=BMM08_DEV,RECORD$="",NUMBER=1,TITLE$="Operation Codes"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6025 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6030 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=3
6040 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6050 LET ROW=V2+1,COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6060 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
6100 REM " --- Verify Valid G/L Accounts"
6110 LET OK$="Y"
6120 IF GL$<>"Y" THEN GOTO 6190
6130 FIND (GLM01_DEV,KEY=N0$+A0$(31,G1),DOM=6180)
6140 FIND (GLM01_DEV,KEY=N0$+A0$(41,G1),DOM=6180)
6150 GOTO 6190
6180 LET OK$="N"
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

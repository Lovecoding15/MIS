0010 REM "BMC - Bill of Materials SpeedSearch (Item Where Used)"
0020 REM "Program BMC.QB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0060 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0070 REM "
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open Files"
0105 LET FILES=8
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="IVM-01",FILES$[2]="IVM-02",FILES$[3]="IVM-03"
0130 LET FILES$[4]="IVM-10",FILES$[5]="IVM-14",FILES$[6]="BMM-01"
0140 LET FILES$[7]="BMM-02",FILES$[8]="BMM-04"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET IVM01_DEV=CHANNELS[1],IVM02_DEV=CHANNELS[2],IVM03_DEV=CHANNELS[3]
0180 LET IVM10_DEV=CHANNELS[4],IVM14_DEV=CHANNELS[5],BMM01_DEV=CHANNELS[6]
0190 LET BMM02_DEV=CHANNELS[7],BMM04_DEV=CHANNELS[8]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$,A4$,A5$,A6$,A[ALL]
0220 IVM02A: IOLIST B0$,B1$,B3$,B[ALL]
0230 BMM02A: IOLIST C0$,C1$,C2$,C[ALL]
0240 BMM01A: IOLIST D0$(1),D1$(1),D2$(1),D[ALL]
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,F1$,F2$,F3$,F4$,F5$
0310 IVM10A: IOLIST PRODUCT$(1)
0320 IVM10C: IOLIST WAREHOUSE$(1)
0400 REM " --- Parameters"
0405 DIM P[5],X$(128)
0410 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2),ONAME$=FNP$(X$(27,8))
0415 LET TNAME$=FNP$(X$(3,8)),TWIDTH=NUM(X$(35,2)),THEIGHT=NUM(X$(37,2))
0420 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0425 LET SNAME$=FNP$(X$(19,8)),SWIDTH=NUM(X$(43,2)),SHEIGHT=NUM(X$(45,2))
0430 LET MAXOPT=NUM(X$(57,2)),SYS01_DEV=NUM(STBL("!SYS01"))
0435 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0440 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0445 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0450 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9900)IOL=IVS01A
0455 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2))
0460 LET P[3]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2)),P[5]=NUM(P2$(13,2))
0465 LET PAGES=INT((DSZ-5000)/4000),MAXPAGE=21,M9$="###,##0-",M9=LEN(M2$)
0470 IF PAGES<3 THEN GOTO 9900
0475 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0480 CALL "SYC.RM",M2$,M2,10,11
0500 REM " --- Initializations"
0505 PRECISION NUM(P2$(5,1))
0510 DIM A0$(22),A1$(60),A2$(64),A[12],B[20],C0$(25),C1$(30),C$(114),C[10]
0515 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),BILLDESC$(TWIDTH-9-P[0]),O[1]
0520 DIM OPTLIST$[2,MAXOPT,6],FILES[14],PARAMS[3],ITEM$[6],PRODUCT$(32)
0525 DIM D0$(22),D1$(30),D2$(58),D[11],INFO$[2],WHSE$[3]
0540 LET D_STATUS=0,MAXROW=HEIGHT-5,PGM$=PGM(-2),OPTION=0,W2=WIDTH-M2-13
0545 LET A0$(1)=N0$+OPTION$[3],M2=LEN(M2$),H0=WIDTH-2
0550 LET PARAMS[0]=P[0],PARAMS[1]=P[3],PARAMS[2]=P[4],PARAMS[3]=P[5],DONE=0
0555 LET FILES[1]=IVM01_DEV,FILES[2]=IVM02_DEV,FILES[3]=IVM03_DEV
0560 LET FILES[14]=IVM14_DEV,O[0]=P[0]+1,O[1]=O[0]+4,HEADING$(2)="Bill"
0575 LET HEADING$(O[0])="Seq",HEADING$(O[1])="Description"
0580 LET FOOTING$(2)="BOM Where Used",FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,""
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0720 PRINT 'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",SNAME$,0
0920 PRINT 'CF',
0930 CALL "SYC.WB",TNAME$,0
0940 PRINT 'CS',
0990 GOTO 1200
1000 REM " --- Process key field"
1010 GOSUB 6000
1015 CALL "SYC.WB",TNAME$,0
1020 PRINT @(0,0),'CS',@(0,2),'SB'," Item:",'SF',
1025 LET V2$="",V2$=KEY(IVM01_DEV,END=1040)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1035 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1040 LET ACTION$="V",V1=7,V2=2,V4$=""
1045 CALL "IVC.IA",ACTION$,V1,V2,FILES[ALL],N0$,V2$,V4$,PARAMS[ALL],ITEM$[ALL],A[ALL],V3
1050 ON V3 GOTO 1080,1000,1000,1000,1060,1070,1060
1060 LET OPTION$[1]="EXIT"
1065 GOTO 4000
1070 LET OPTION$[1]="STARTOVER"
1075 GOTO 4000
1080 LET V$=FNP$(ITEM$[0])
1090 IF V$="" THEN GOTO 0900
1095 LET A0$(1)=N0$+V$
1200 REM " --- Retrieve records for display"
1210 LET LINE=1,L=1,X0=0,X1=1,PAGE=1
1220 READ (IVM01_DEV,KEY=A0$,DOM=1000)IOL=IVM01A
1900 REM " --- Retrieve records for display"
1910 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,SHEIGHT-2],X$(WIDTH-2)
1920 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,SELECTION$="",START$=A0$
1930 LET X1$=A0$(3,P[0]),X2$=PARAM$
1940 LET X$(1)='SB'+" Item: "+'SF'+A0$(3,P[0])+" "+FNITEM$(A1$,P[3],P[4],P[5])
1950 CALL "SYC.QB",X$,X1$,X2$
1990 READ (BMM04_DEV,KEY=START$,DOM=2000)
2000 REM " --- For each BMM-04 record"
2010 LET K$=KEY(BMM04_DEV,END=3000)
2020 IF POS(START$=K$)<>1 THEN GOTO 3000
2030 READ (BMM04_DEV)
2200 REM " --- Additional reads"
2210 LET A0$(1)=K$(1,2)+K$(23),C0$(1)=K$(1,2)+K$(23),D0$(1)=A0$
2220 FIND (IVM01_DEV,KEY=A0$,DOM=2900)IOL=IVM01A
2230 FIND (BMM01_DEV,KEY=D0$,DOM=2900)IOL=BMM01A
2240 FIND (BMM02_DEV,KEY=C0$,DOM=2900)IOL=BMM02A
2250 LET PRODUCT$(1)=N0$+"A"+A2$(1,3),PHANTOM$="Assembly"
2260 IF D2$(40,1)="Y" THEN LET PHANTOM$="Phantom"
2270 FIND (IVM10_DEV,KEY=PRODUCT$(1,6),DOM=2400)IOL=IVM10A
2400 REM " --- Accumulate on hand, on order and committed"
2410 LET ACTION$="A",INFO$[0]=N0$,INFO$[1]=WHSE$,INFO$[2]=A0$(3)
2420 CALL "IVC.WA",ACTION$,FILES[ALL],INFO$[ALL],WHSE$[ALL],B[ALL],VALUE,STATUS
2430 LET AVAILABLE=B[0]-B[2],W0=3,W1=22,W4=W1+1
2440 IF FNP$(C2$(5,6))="" THEN LET W0=1,W1=4,W4=2
2500 REM " --- Display page?"
2510 IF L<MAXROW THEN GOTO 2600
2520 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2530 GOSUB 5000
2540 IF DONE THEN GOTO 4000
2570 IF ANSWER$<>"" THEN GOTO 3000
2590 LET L=1,X1=1
2700 REM " --- Middle window record"
2710 DIM G$(WIDTH-2)
2720 LET G$(2)=A0$(3),G$(O[0])=C0$(23,3),G$(O[1])=FNITEM$(A1$,P[3],P[4],P[5])
2790 LET G$(WIDTH-3)="",LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=A0$(3)
2800 REM " --- Bottom window record"
2810 LET G$(1)="",MORE$[PAGE,X1,0]=STR(SHEIGHT-2)
2820 LET G$(W0)="{Qty Req:}"+STR(FNQTY(C[0],C[1],C[3],D[4],C[4])*1:M2$)
2830 LET G$(W2)="  {On Hand:}"+STR(B[0]:M2$),MORE$[PAGE,X1,1]=G$,G$(1)=""
2840 IF FNP$(C2$(5,3))<>"" THEN LET G$(1)="{Effective:}"+FNB$(C2$(5,3))
2845 LET G$(W1)="{Prod:}"+PRODUCT$(4,3)+" "+FNP$(PRODUCT$(7,24))
2850 LET G$(W2)="{Committed:}"+STR(B[2]:M2$),MORE$[PAGE,X1,2]=G$,G$(1)=""
2870 IF FNP$(C2$(8,3))<>"" THEN LET G$(2)="{Obsolete:}"+FNB$(C2$(8,3))
2875 LET G$(W4)=PHANTOM$,G$(W2)="{Available:}"+STR(AVAILABLE:M2$)
2890 LET MORE$[PAGE,X1,3]=G$,LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2990 GOTO 2000
3000 REM " --- No more components"
3010 IF X0>0 THEN GOTO 3100
3020 DIM MESSAGE$[1]
3030 LET MESSAGE$[0]="No Bill Of Materials Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 LET SELECTION$="PRIOR"
3090 GOTO 3500
3100 REM " --- Display last page"
3110 LET ACTION$="M",CHOICE1$=A0$(3),CHOICE2$=""
3120 GOSUB 5000
3130 IF DONE THEN GOTO 4000
3500 REM " --- Process option window"
3510 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3520 LET ACTION$="O",CHOICE1$=A0$(3),CHOICE2$=""
3530 GOSUB 5000
3540 IF CONTROL=1 THEN GOTO 1000
3590 GOTO 4000
3800 REM " --- Exit"
3810 LET OPTION$[1]="EXIT"
3890 GOTO 4000
3900 REM " --- Startover"
3910 LET OPTION$[1]="STARTOVER"
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CS',
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8070 DEF FNQTY(Q1,Q2,Q3,Q4,Q5)=10000*Q1*Q2/(Q2*Q4*(100-Q5))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9921)
9930 NEXT X
9990 EXIT 
9999 END

0010 REM "SHR - UPS Manifest Program"
0020 REM "Program SHRAMA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 07Feb98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Feb97 CAJ: Modified for 1997 SpeedShip Update"
0032 REM "01Feb98 JAL: Modified for 1998 SpeedShip Update"
0033 REM "25Mar98 JAL: UPS 2DA zone is incorrect in manifest summary (6435)"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=1
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SHW-02",OPTIONS$[1]="C"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET SHW02_DEV=CHANNELS[1]
0200 REM " --- IOLIST's"
0210 SHE01A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0220 SHE02A: IOLIST W0$(1),W1$(1),W[ALL]
0240 SHW02A: IOLIST D0$(1),D[ALL]
0250 SHM10A: IOLIST S0$(1),S1$(1)
0260 SHM10P: IOLIST *,OPTIONS$(1)
0350 SHM10C: IOLIST R0$(1),R[ALL]
0400 REM " --- Parameters"
0410 DIM INCLUDE$(22),REVENUE$(22),OPTIONS$(22)
0420 FIND (SHM10_DEV,KEY=N0$+"P"+"UPS FRT",DOM=0430)IOL=SHM10P
0425 LET INCLUDE$(1)=OPTIONS$
0430 FIND (SHM10_DEV,KEY=N0$+"P"+"UPS REV",DOM=0440)IOL=SHM10P
0435 LET REVENUE$(1)=OPTIONS$
0440 LET M1$="#####0.00",M2$="##0",M3$="###,##0",M4$="###,##0.00"
0450 LET DV$=REVENUE$(21,1),PICKUP$=REVENUE$(22,1),M1=LEN(M1$)
0460 LET WIDTH=H0+1,MZ=232
0500 REM " --- Initializations"
0510 DIM A0$(K9),A1$(168),A2$(64),A[8],W0$(K9),W1$(128),W[20],J1$(40),O[14]
0515 DIM S0$(10),S1$(128),HEADINGS$[4],BUFFER$(WIDTH),O[14],R0$(10),R[20]
0520 DIM TOTPKG[2,MZ],TOTWGT[1,MZ],TOTCHG[1,MZ],WHSE$[4]
0525 DIM SUBPKG[2,MZ],SUBWGT[1,MZ],SUBCHG[1,MZ],J$(WIDTH,"-")
0530 LET R$="A",K=17,RECORDS=0,PRINTED=0
0535 IF R6$<>"" THEN LET R$="D",K=13,D$=FNB6$(R9$),CARRIER$(54,12)=""
0540 LET SUBTRAC=0,TOTTRAC=0,SUBREV=0,TOTREV=0,WHEN$=F0$(7,3)
0545 LET SUBUNIT=0,TOTUNIT=0,SUBINS=0,TOTINS=0,CLOCK$=""
0565 LET H8$="Zone      Pieces      Weight        Charges"
0570 LET H9$="         -------     -------     ----------"
0575 LET O[0]=0,O[1]=O[0]+12,O[2]=O[1]+20,O[3]=O[2]+34,O[4]=O[3]+3
0580 LET O[5]=O[4]+8,O[6]=O[5]+5,O[7]=O[6]+5,O[8]=O[7]+5,O[9]=O[8]+M1+1
0585 LET O[10]=O[9]+M1+1,O[11]=O[10]+M1+2,O[12]=O[11],O[13]=O[12]+4
0590 LET O[14]=H0-1,TYPE=0,GRD=0,AIR=1,L9=55,L=L9+1,H9=LEN(H9$)
0600 REM " --- Report headings"
0605 LET HEADINGS$[1]=FNP$(CARRIER$(1,30))+" Shipping Manifest"
0610 LET HEADINGS$[0]=H1$,X0$="For Ship Date "
0615 IF R6$<>"" THEN LET X0$="Reprinted "+X0$,D$=FNB6$(R9$)
0620 LET HEADINGS$[2]=X0$+D$,HEADINGS=2
0625 IF I1$(4,1)<>"Y" THEN GOTO 0650
0630 LET HEADINGS$[3]="*** DEMONSTRATION COPY - NOT VALID FOR ACTUAL SHIPPING ***"
0635 LET HEADINGS=3
0650 REM " --- Build warehouse headings"
0655 LET X$=C1$(31,96)+C1$(137,9)
0660 CALL "SYC.AA",X$,24,4,9,30
0665 LET WHSE$[0]=C1$(1,30),WHSE$[1]=X$(1,30),WHSE=1
0670 FOR X=31 TO 150 STEP 30
0675 IF FNP$(X$(X,30))<>"" THEN LET WHSE=WHSE+1,WHSE$[WHSE]=X$(X,30)
0680 NEXT X
0950 REM " --- Position File"
0960 LET V$=N0$+R$+C$+WHSE$
0970 IF R6$<>"" THEN LET V$=N0$+R$+R9$+C$+WHSE$
0980 LET V=LEN(V$)
0990 READ (SHE04_DEV,KEY=V$,DOM=1000)
1000 REM " --- Read next transaction record"
1010 LET K$=KEY(SHE04_DEV,END=4000)
1020 IF K$(1,V)<>V$ THEN GOTO 4000
1030 READ (SHE04_DEV)
1040 PRINT @(COLUMN,11),K$(K,7),
1100 REM " --- Additional reads"
1110 LET W0$(1,K9)=K$(1,2)+K$(K,15)+K$(K+15,3)+R9$
1120 FIND (SHE02_DEV,KEY=W0$,DOM=3900)IOL=SHE02A
1130 IF W1$(59,2)<>WHSE$ THEN GOTO 3900
1135 IF W1$(56,1)="N" THEN GOTO 3900
1140 IF W1$(28,1)="Y" THEN GOTO 3900
1150 LET A0$(1,K9)=K$(1,2)+K$(K,15)+"000"+R9$
1160 FIND (SHE01_DEV,KEY=A0$,DOM=3900)IOL=SHE01A
1165 IF R6$<>"" THEN IF FNP$(CARRIER$(54,12))="" THEN LET CARRIER$(54,12)=W1$(62,12)
1170 IF W1$(4,7)=S0$(4,7) THEN GOTO 1200
1190 FIND (SHM10_DEV,KEY=W0$(1,2)+"A"+W1$(4,7),DOM=1200)IOL=SHM10A
1500 REM " --- Format address"
1510 IF P9$<>"Y" THEN GOTO 2000
1520 LET ADDRESS$=A1$(31,96)+A1$(144,24)+J1$(1,9)
1530 CALL "SYC.AA",ADDRESS$,24,5,9,24
1590 LET NEEDED=INT(LEN(FNP$(ADDRESS$))/24)+2
2000 REM " --- Format remaining print fields"
2010 LET ZIPCODE$=FNP$(FNZ$(A1$(127))),WEIGHT=W[0],STATE$="",WEIGHT$=""
2015 IF W1$(88,1)="D" THEN LET WEIGHT=W[20]
2020 IF LEN(ZIPCODE$)<7 THEN LET ZIPCODE$=ZIPCODE$+"       "
2025 IF LEN(ZIPCODE$)>7 THEN LET ZIPCODE$=ZIPCODE$(1,7)
2030 IF ZIPCODE$(6,1)="-" THEN LET ZIPCODE$(6)=""
2035 LET WEIGHT$=STR(WEIGHT:M2$)
2040 IF W[0]=0 THEN LET WEIGHT$=" Lt"
2050 LET CHARGES=0,REVENUES=0,Z=NUM(W1$(1,3)),NEEDED=0
2055 IF Z<9 OR (Z>101 AND Z<309) THEN LET STATE$=A1$(142,2)
2060 FOR X=1 TO 20
2070 LET CHARGES=CHARGES+W[X]*(INCLUDE$(X,1)="Y")
2080 LET REVENUES=REVENUES+W[X]*(REVENUE$(X,1)="Y")
2090 NEXT X
2100 REM " --- Set indicator flags"
2110 LET RS$="",AH$="",OS$="",CT$=""
2120 IF W1$(55,1)="Y" THEN LET RS$="X"
2140 IF W1$(16,1)="Y" THEN LET OS$="X"
2150 IF W1$(18,1)="Y" THEN LET CT$="X"
2160 IF W1$(88,1)="D" THEN LET OS$="DW"
2170 IF W1$(126,1)="Y" THEN LET AH$="X"
2300 REM " --- Set optional fields"
2310 LET COD$="",VALUE$=""
2320 IF W[1]<>0 THEN LET VALUE$=STR(W[1]:M1$)
2330 IF W[14]<>0 THEN LET COD$=STR(W[14]:M1$)
2500 REM " --- Print detail line"
2510 IF L+NEEDED>L9 THEN GOSUB 5000
2520 PRINT (7)""
2530 PRINT (7)@(O[0]),FNP$(W0$(3,15)),"-",STR(NUM(W0$(18,3))),@(O[1]),W1$(108,18),@(O[2]),A1$(7,24),A1$(136,6),@(O[3]),STATE$,@(O[4]),ZIPCODE$,@(O[5]),Z:M2$,@(O[6]),WEIGHT$,@(O[7]),RS$,@(O[8]),CHARGES:M1$,@(O[9]),COD$,@(O[10]),VALUE$,@(O[12]),AH$,@(O[13]),OS$,@(O[14]),CT$
2540 LET L=L+2,PRINTED=PRINTED+1
2600 REM " --- Print remainder of address?"
2610 IF P9$<>"Y" THEN GOTO 2800
2620 FOR X=1 TO LEN(ADDRESS$) STEP 24
2630 IF FNP$(ADDRESS$(X,24))="" THEN GOTO 2690
2640 PRINT (7)@(O[2]),ADDRESS$(X,24)
2650 LET L=L+1
2690 NEXT X
2800 REM " --- Generate pickup record?"
2810 IF PICKUP$<>"Y" THEN GOTO 3000
2820 DIM D0$(12),D[2]
2830 LET X$=S1$(79,1),X=POS(W1$(127,1)="PL")-1
2835 IF X<0 THEN LET X=0
2840 IF X$="G" THEN LET X$=" "
2850 LET D0$(1)=N0$+X$+S1$(80,2)+W1$(4,7)
2860 FIND (SHW02_DEV,KEY=D0$,DOM=2870)IOL=SHW02A
2870 LET D[X]=D[X]+1,D[2]=D[2]+CHARGES
2880 WRITE (SHW02_DEV,KEY=D0$)IOL=SHW02A
3000 REM " --- Accumulate subtotals/totals"
3010 LET X=GRD
3020 IF POS(S1$(79,1)="AL")>0 THEN LET X=AIR
3030 IF Z>301 AND Z<309 THEN LET Z=Z-210
3040 IF Z>201 AND Z<249 THEN LET Z=Z-50
3045 IF Z>600 AND Z<609 THEN LET Z=Z-500
3050 IF Z>899 AND Z<909 THEN LET Z=Z-690
3100 REM " --- Total packages by ground/air"
3110 LET SUBPKG[X,Z]=SUBPKG[X,Z]+1,TOTPKG[X,Z]=TOTPKG[X,Z]+1
3120 LET SUBWGT[X,Z]=SUBWGT[X,Z]+WEIGHT,TOTWGT[X,Z]=TOTWGT[X,Z]+WEIGHT
3130 LET SUBCHG[X,Z]=SUBCHG[X,Z]+CHARGES,TOTCHG[X,Z]=TOTCHG[X,Z]+CHARGES
3140 LET SUBREV=SUBREV+REVENUES,TOTREV=TOTREV+REVENUES
3170 LET SUBVAL=SUBVAL+W[5],TOTVAL=TOTVAL+W[5]
3200 REM " --- COD packages"
3210 IF W[3]=0 THEN GOTO 3300
3220 LET SUBPKG[2,Z]=SUBPKG[2,Z]+1,TOTPKG[2,Z]=TOTPKG[2,Z]+1
3230 LET SUBCOD=SUBCOD+W[3],TOTCOD=TOTCOD+W[3]
3400 REM " --- Additional Handling"
3410 IF W1$(126,1)="Y" THEN LET SUBHDL=SUBHDL+1,TOTHDL=TOTHDL+1
3500 REM " --- Declared value summary"
3510 IF DV$<>"Y" THEN GOTO 3600
3520 IF W1$(4,7)=R0$(4,7) THEN GOTO 3550
3530 DIM R0$(10),R[20]
3535 LET R0$(1)=N0$+"C"+W1$(4,7)
3540 FIND (SHM10_DEV,KEY=R0$,DOM=3550)IOL=SHM10C
3550 LET UNITS=0
3555 IF R[5]<>0 THEN LET UNITS=INT(W[5]/R[5])
3560 LET SUBUNITS=SUBUNITS+UNITS,TOTUNITS=TOTUNITS+UNITS
3570 LET SUBINS=SUBINS+W[5],TOTINS=TOTINS+W[5]
3600 REM " --- Print subtotals?"
3610 IF PRINTED<100 THEN GOTO 3900
3620 LET TYPE=1,H$="S u b t o t a l",REVENUES=SUBREV
3630 LET TRAC=SUBTRAC,COD=SUBCOD,HDL=SUBHDL,UNITS=SUBUNITS,INS=SUBINS
3640 GOSUB 6000
3900 REM " --- Loop back for next record"
3910 LET RECORDS=RECORDS+1
3990 GOTO 1000
4000 REM " --- All done"
4010 LET TYPE=2,H$="S u m m a r y",REVENUES=TOTREV
4020 LET TRAC=TOTTRAC,COD=TOTCOD,HDL=TOTHDL,UNITS=TOTUNITS,INS=TOTINS
4030 GOSUB 6000
4900 REM " --- Run update overlay"
4910 IF RECORDS=0 THEN GOTO 9900
4990 RUN "SHRAMB"
5000 REM " --- Report heading"
5010 LET L=HEADINGS
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,H0+1,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 REM " --- Warehouse Heading"
5110 FOR N=0 TO WHSE
5120 LET H0$="",H1$="",L=L+1
5130 IF N=0 THEN LET H1$="UPS Shipper Number: "+CARRIER$(37,10)
5140 IF N=1 THEN LET H1$=" UPS Pickup Number: "+CARRIER$(54,12),H0$=H$
5150 PRINT (7)WHSE$[N],@(FNC(H0$,H0)),H0$,@(H0-31),H1$
5160 NEXT N
5170 PRINT (7)J$
5180 LET L=L+1
5200 REM " --- Manifest Heading"
5210 IF TYPE>0 THEN GOTO 5300
5230 PRINT (7)@(O[2]),"Consignee",@(O[4]),"Zip",@(O[9]+M1-6),"C.O.D.",@(O[10]+M1-6),"Decl'd"
5240 PRINT (7)@(O[0]),"Package ID",@(O[1]),"Tracking #",@(O[2]),"Name/Address/City/State",@(O[3]),"St",@(O[4]),"Code",@(O[5]),"Zone",@(O[6]+1),"Wt",@(O[7]-1),"Res",@(O[8]+M1-7),"Charges",@(O[9]+M1-6),"Amount",@(O[10]+M1-5),"Value",@(O[12]-1),"A/H",@(O[13]-1),"O/S",@(O[14]),"CT"
5250 PRINT (7)J$
5260 LET L=L+3
5290 RETURN 
5300 REM " --- Summary Heading"
5310 PRINT (7)@(26),"Common Carrier",@(95),"Air"
5320 PRINT (7)""
5330 PRINT (7)@(12),H8$,@(75),H8$
5350 LET L=L+3
5390 RETURN 
5400 REM " --- Summary Footing"
5410 PRINT (7)""
5420 PRINT (7)"Effective Date of Rates: 02-07-98 MULTIPLE-ZONES"
5430 PRINT (7)"ADD+ON Software, Inc."
5490 RETURN 
6000 REM " --- Print Subtotals/Totals"
6010 DIM PKG[2,MZ],WGT[1,MZ],CHG[1,MZ]
6020 FOR X=0 TO 2
6030 FOR Y=1 TO MZ
6035 IF TYPE=1 THEN LET PKG[X,Y]=SUBPKG[X,Y]
6040 IF TYPE=2 THEN LET PKG[X,Y]=TOTPKG[X,Y]
6050 IF X>1 THEN GOTO 6080
6060 IF TYPE=1 THEN LET WGT[X,Y]=SUBWGT[X,Y],CHG[X,Y]=SUBCHG[X,Y]
6070 IF TYPE=2 THEN LET WGT[X,Y]=TOTWGT[X,Y],CHG[X,Y]=TOTCHG[X,Y]
6080 NEXT Y
6090 NEXT X
6100 REM " --- Accumulate totals"
6105 DIM SUBPKG[0],SUBWGT[0],SUBCHG[0]
6110 LET FOOTER=0,G=0,A=0,TH$="M a n i f e s t   "+H$
6120 FOR X=1 TO MZ
6130 IF PKG[GRD,X]>0 THEN LET G=G+1
6140 IF PKG[AIR,X]>0 THEN LET A=A+1
6150 FOR Y=0 TO 2
6155 LET PKG[Y,0]=PKG[Y,0]+PKG[Y,X]
6160 IF Y>1 THEN GOTO 6175
6165 LET WGT[Y,0]=WGT[Y,0]+WGT[Y,X]
6170 LET CHG[Y,0]=CHG[Y,0]+CHG[Y,X]
6175 NEXT Y
6180 NEXT X
6190 LET NEEDED=MAX(G,A)
6200 REM " --- Build summary array"
6210 DIM SUMMARY$[NEEDED](WIDTH),X$(H9)
6220 LET L=L9+1,G=0,A=0
6290 IF NEEDED=0 THEN GOTO 6600
6300 REM " --- Ground totals"
6310 FOR X=1 TO MZ
6320 IF PKG[GRD,X]=0 THEN GOTO 6400
6323 LET Z=X
6325 IF Z>91 AND Z<99 THEN LET Z=Z+210
6330 LET X$(1)=STR(Z:M2$),X$(10)=STR(PKG[GRD,X]:M3$)
6340 LET X$(22)=STR(WGT[GRD,X]:M3$),X$(34)=STR(CHG[GRD,X]:M4$)
6350 LET G=G+1,SUMMARY$[G](13,H9)=X$
6400 REM " --- Air totals"
6410 IF PKG[AIR,X]=0 THEN GOTO 6490
6420 LET Z=X
6425 IF Z>209 AND Z<219 THEN LET Z=Z+690
6430 IF Z>200 AND Z<209 THEN LET Z=Z+500
6435 IF Z>150 AND Z<200 THEN LET Z=Z+50
6440 LET X$(1)=STR(Z:M2$),X$(10)=STR(PKG[AIR,X]:M3$)
6450 LET X$(22)=STR(WGT[AIR,X]:M3$),X$(34)=STR(CHG[AIR,X]:M4$)
6460 LET A=A+1,SUMMARY$[A](76,H9)=X$
6490 NEXT X
6500 REM " --- Print 'em"
6510 FOR X=1 TO NEEDED
6515 LET H$=TH$
6520 IF L+3<L9 THEN GOTO 6550
6530 IF FOOTER THEN GOSUB 5400
6540 GOSUB 5000
6550 PRINT (7)""
6560 PRINT (7)SUMMARY$[X]
6570 LET L=L+2,FOOTER=1
6590 NEXT X
6600 REM " --- Print totals"
6610 DIM SUMMARY$[0],X$(H9)
6615 LET BUFFER$(1)="",BUFFER$(13)=H9$,BUFFER$(76)=H9$,X=12,H$=TH$
6620 IF DV$="Y" THEN LET X=13
6625 IF L+X>L9 THEN GOSUB 5000
6630 PRINT (7)""
6640 PRINT (7)BUFFER$
6643 LET BUFFER$(1)=""
6645 FOR X=0 TO 1
6650 LET X$(1)="Totals",X$(10)=STR(PKG[X,0]:M3$),X$(22)=STR(WGT[X,0]:M3$)
6660 LET X$(34)=STR(CHG[X,0]:M4$),BUFFER$(13+X*63)=X$
6680 NEXT X
6690 PRINT (7)""
6700 PRINT (7)BUFFER$
6800 REM " --- Final totals"
6810 PRINT (7)""
6815 PRINT (7)@(9),"Number of Additional Handling Packages",@(48),HDL:M3$
6835 IF DV$="Y" THEN PRINT (7)@(9),"Declared Value Units",@(48),UNITS:M3$,@(75),"Declared Value Charges",@(105),INS:M4$
6840 PRINT (7)@(9),"Number of C.O.D. Packages",@(48),PKG[2,0]:M3$,@(75),"C.O.D. Charges",@(105),COD:M4$
6845 PRINT (7)@(75),"Total UPS Revenue",@(105),REVENUES:M4$
6850 LET L=L+9
6860 FOR X=L TO L9-3
6865 PRINT (7)""
6870 NEXT X
6875 GOSUB 5400
6900 REM " --- Done printing subtotals/totals"
6910 DIM PKG[0],WGT[0],CHG[0],SUMMARY$[0]
6920 DIM SUBPKG[2,MZ],SUBWGT[1,MZ],SUBCHG[1,MZ]
6930 LET PRINTED=0,TYPE=0,L=L9+1,H$="",SUBREV=0,SUBCOD=0,SUBHDL=0
6990 RETURN 
8000 REM " --- Function Definitions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8060 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "SYX - Clear Files"
0020 REM "Program SYX.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=2
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYM-03"
0140 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYM03_DEV=CHANNELS[1]
0200 REM " --- IOLIST's"
0210 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0500 REM " --- Initializations"
0510 DIM A0$(6),A1$(64),A2$(30),A[2],MESSAGE$[1]
0550 LET MESSAGE$[0]="This file is write-protected and should not be"
0560 LET MESSAGE$[1]="cleared under normal circumstances!"
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 PRINT 'SB',@(29,3),"File Name:",'SF',
1000 REM " --- File to clear"
1005 PRINT 'CF',
1010 LET V0$="S",V1$="CE",V2$="",V3$="",V0=6,V1=40,V2=3
1020 LET V4$="Enter the name of the file to be cleared (<F3>=Lookup)"
1030 GOSUB 7000
1040 ON V3 GOTO 1100,1000,1000,1050,9900
1050 GOSUB 6000
1100 REM " --- Retrieve description"
1110 DIM A0$(6),A1$(64),A2$(30),A[2]
1120 LET A0$(1)=V$
1130 FIND (SYM03_DEV,KEY=A0$,DOM=1000)IOL=SYM03A
1140 PRINT @(V1,V2),'CL',A0$,@(V1+V0+1),A1$(1,30),
1200 REM " --- Write Protected?"
1210 IF A1$(38,1)<>"Y" THEN GOTO 4000
1220 CALL "SYC.XA",2,MESSAGE$[ALL],1,-1,-1,V$,V3
1230 IF V$<>"SPECIAL" THEN GOTO 1000
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4030 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Open and clear the file"
4110 LET FILES$[2]=A0$,OPTIONS$[2]="C"
4120 CALL "SYC.DA",1,2,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
4140 CLOSE (CHANNELS[2],ERR=4150)
4190 GOTO 1000
6000 REM " --- Lookup"
6010 LET RECORD$="",NUMBER=1,TITLE$="ADD+ON File Definitions"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6025 LET DESCRIPTION$[0]="File",DESCRIPTION$[1]="Description"
6030 LET FIELD[0]=1,POSITION[0]=1,LENGTH[0]=6
6040 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6050 LET ROW=6,COLUMN=79-LENGTH[0]-LENGTH[1]-NUMBER*4
6060 LET V$="NO COMPANY"
6070 CALL "SYC.LK",SYM03_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "OPR - SERIALIZED LABELS - BASIS"
0020 REM "Program OPR.LA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "MAS * DCF * 9/29/99 * WA# 215
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=12
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARE-03",FILES$[2]="ARE-13",FILES$[3]="ARE-73"
0130 LET FILES$[4]="SYS-01",FILES$[5]="ARE-83",FILES$[6]="ARE-43"
0140 LET FILES$[7]="ARM-01",FILES$[8]="CSM-03",FILES$[9]="CSM-04"
0150 LET FILES$[10]="IVM-01",FILES$[11]="CSM-06",FILES$[12]="CSM-05"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET ARE03_DEV=CHANNELS[1],ARE13_DEV=CHANNELS[2],ARE73_DEV=CHANNELS[3]
0185 LET SYS01_DEV=CHANNELS[4],ARE83_DEV=CHANNELS[5],ARE43_DEV=CHANNELS[6]
0190 LET ARM01_DEV=CHANNELS[7],CSM03_DEV=CHANNELS[8],CSM04_DEV=CHANNELS[9]
0195 LET IVM01_DEV=CHANNELS[10],CSM06_DEV=CHANNELS[11],CSM05_DEV=CHANNELS[12]
0200 REM " --- IOLists
0210 ARE83A: IOLIST Y0$,Y1$,Y2$,Y[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$
0500 REM " --- Init Data
0510 LET H0=65,H1=120,H=0,H1$=N1$,H2$=N3$
0520 DIM Y[2],J1$(30)
0700 REM " --- Background
0710 FOR X=4 TO 8 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(26,3),"Order Number:",@(25,5),"Serial Number:",'SF',
1000 REM " --- Order Number
1010 PRINT 'CF',
1020 LET V0$="Z",V1$="KREC",V2$="",V3$="",V4$="Enter a Valid Open Order Number",V0=7,V1=40,V2=3
1040 GOSUB 7000
1045 IF V3=4 THEN GOTO 9900
1060 LET ORDER$=V$
1070 READ (ARE43_DEV,KEY=N0$+"  "+ORDER$,DOM=1071)
1075 LET ARE43_K$=KEY(ARE43_DEV,END=1090)
1080 IF ARE43_K$(1,11)=N0$+"  "+ORDER$ THEN GOTO 1100
1090 DIM MESSAGE$[1]
1092 LET MESSAGE$[0]="This order number does not exist in the Open Order file"
1093 LET V$=""
1094 CALL "SYC.XA",1,MESSAGE$[ALL],1,-1,-1,V$,V3
1096 GOTO 1000
1100 REM "
1110 LET CUST$=ARE43_K$(12,6)
1120 FIND (ARM01_DEV,KEY=N0$+CUST$,DOM=1200)*,NAME$
1130 PRINT @(50,3),NAME$(1,20)
1200 REM " --- Serial Number
1220 LET V0$="S",V1$="CE",V2$="",V3$="",V0=20,V1=40,V2=5,V4$="Enter a Valid Serial Number, <F1>=All"
1240 GOSUB 7000
1250 IF V3=2 THEN GOTO 1000
1260 IF V3=1 OR V$="" THEN LET SERIAL$=""; PRINT @(V1,V2),'CL',"First",; GOTO 1400
1270 LET SERIAL$=V$+J1$(1,20-LEN(V$))
1300 REM " --- Validate serial number
1310 LET START$=N0$+"  "+CUST$+ORDER$
1320 READ (ARE83_DEV,KEY=START$,DOM=1321)
1330 LET ARE83_K$=KEY(ARE83_DEV,END=1390)
1340 IF POS(START$=ARE83_K$)<>1 THEN GOTO 1390
1350 READ (ARE83_DEV,KEY=ARE83_K$)IOL=ARE83A
1360 IF Y1$(1,1)="D" THEN GOTO 1330
1370 IF Y2$=SERIAL$ THEN GOTO 1400
1390 DIM MESSAGE$[1]
1392 LET MESSAGE$[0]="This serial number does not exist on this order"
1393 LET V$=""
1394 CALL "SYC.XA",1,MESSAGE$[ALL],1,-1,-1,V$,V3
1396 GOTO 1200
4000 REM " --- Correct?
4010 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V3=4 THEN GOTO 9900
4040 IF V$="N" THEN GOTO 1000
4060 IF V$<>"Y" THEN GOTO 4000
4100 RUN "OPR.LB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

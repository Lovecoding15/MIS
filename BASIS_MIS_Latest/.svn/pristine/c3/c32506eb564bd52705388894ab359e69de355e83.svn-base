0010 REM "SYC - On-Line Help Lookup"
0020 REM "Program SYC.HL"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "ACTION     : Action code (input)"
0050 REM "             0=Maintenance Lookup/1=On-Line Help Display"
0055 REM "CHANNELS[n]: Channel numbers (input)"
0060 REM "             [1]=SYS-01     [2]=SYS-11     [5]=SYM-11"
0063 REM "NAME$      : Lookup window name when ACTION=1 (input/returned)"
0065 REM "SELECTION$ : User selection (returned)'
0070 REM "FKEY       : Function key value (returned)"
0075 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER ACTION,CHANNELS[ALL],NAME$,HELPMENU$,SELECTION$,FKEY
0100 REM " --- Open/Lock Files"
0110 LET SYS01_DEV=CHANNELS[1],SYS11_DEV=CHANNELS[2],SYM01_DEV=CHANNELS[4]
0200 REM " --- IOLIST's"
0210 SYS11A: IOLIST A0$(1),A1$(1)
0220 SYM01A: IOLIST B0$(1),B1$(1),B2$(1)
0270 SYS01T: IOLIST TERMINAL$,F0$,F1$,F2$,F3$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM PGM$(6),SPEED$(128)
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0425 LET PGM$(1)=STBL("!HELP",ERR=0430)
0430 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=0470)
0440 LET N0$=SPEED$(1,2),MENU$=SPEED$(51,6),LEAD$="SYC.QA"
0450 GOTO 0490
0470 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0475 LET N0$=F0$(16,2),MENU$=FNP$(F2$),LEAD$=FNP$(F1$)
0480 IF LEN(LEAD$)>6 THEN LET LEAD$=LEAD$(1,6)
0485 IF FNP$(F5$)="" THEN LET LEAD$=""
0490 IF FNP$(LEAD$)=FNP$(PGM$) THEN LET LEAD$=""
0500 REM " --- Initializations"
0510 DIM OPTLIST$[1,MAXROW,5],PARAM$[1,MAXROW,3],TOPIC$(8)
0520 DIM A0$(14),A1$(40),B0$(6),B1$(35),B2$(16)
0540 IF FNP$(HELPMENU$)="" THEN LET HELPMENU$=PGM(-2)
0550 LET WIDTH=62,HEIGHT=12,WIN_Y=3,MAX_ROW=HEIGHT-5,PREFIX$="",FIRST=0
0560 LET TITLE$="ADD+ON On-Line Help Topics",START$=FNP$(PGM$)
0570 IF ACTION>0 THEN LET WIDTH=44,FIRST=1
0580 LET WIN_X=80-WIDTH,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3
0590 LET BEGMODULE$="",BEGMENU$=MENU$,BEGLEAD$=LEAD$,BEGPGM$=PGM$
0610 REM " --- Window arrays"
0620 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0630 LET HEADING$(2)="Subject",HEADING$(12)="Help #",HEADING$(20)="Help Topic"
0635 IF ACTION>0 THEN LET HEADING$(2)="Help Topic"
0640 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn",PREFIX$=""
0650 REM " --- Retrieve menu for on-line help display"
0660 IF ACTION=0 THEN GOTO 0700
0670 LET B0$(1)=MENU$,MODULE$=""
0680 FIND (SYM01_DEV,KEY=B0$,DOM=0700)IOL=SYM01A
0690 LET MODULE$=FNP$(B2$(1,2))
0700 REM " --- Background"
0710 IF ACTION THEN IF NAME$<>"" THEN GOTO 0750
0720 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0730 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0740 GOTO 0800
0750 CALL "SYC.WB",NAME$,0
0800 REM " --- Which search option?"
0810 IF FIRST THEN GOTO 0900
0820 DIM STRINGS$[10],NUMBERS[10]
0830 LET STRINGS$[0]=HELPMENU$,NUMBERS[0]=MAXROW
0840 LET NUMBERS[1]=OWIN_X,NUMBERS[2]=OWIN_Y,KEYWORD$=""
0850 CALL "SYC.SM",STRINGS$[ALL],NUMBERS[ALL],STATUS
0860 IF STATUS>0 THEN GOTO 9900
0870 LET PREFIX$=STRINGS$[2],START$=STRINGS$[3],METHOD$=STRINGS$[4]
0880 IF PREFIX$="*" THEN LET KEYWORD$=FNU$(START$)
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,5]
1020 LET MODULE$=BEGMODULE$,MENU$=BEGMENU$,LEAD$=BEGLEAD$,PGM$=BEGPGM$
1030 LET FIRST=0,PAGE=1,L=1,X1=1,X0=0,SELECTION$="",TOPIC$(1)=PGM$
1040 IF ACTION=0 THEN GOTO 1090
1050 LET START$="*",TOPIC$(1)=START$
1060 IF FNP$(TOPIC$)="" THEN GOTO 3000
1090 IF PREFIX$="*" THEN IF ACTION=0 THEN GOTO 1300
1200 REM " --- Program Name"
1210 READ (SYS11_DEV,KEY=N0$+START$,DOM=1400)IOL=SYS11A
1220 LET K$=A0$
1290 GOTO 1900
1300 REM " --- Keyword"
1310 READ (SYS11_DEV,KEY=N0$,DOM=1400)
1400 REM " --- Get next key"
1410 LET K$=KEY(SYS11_DEV,END=3000)
1420 IF POS(N0$=K$)<>1 THEN GOTO 3000
1430 IF ACTION=0 THEN GOTO 1900
1440 IF TOPIC$(1,1)="*" THEN IF K$(3,1)="*" THEN GOTO 1900
1450 IF K$(3,8)<>TOPIC$ THEN GOTO 3000
1900 REM " --- Read record"
1910 READ (SYS11_DEV,KEY=K$,ERR=1950)IOL=SYS11A
1920 IF PREFIX$<>"*" THEN GOTO 2000
1930 IF KEYWORD$<>"" THEN IF POS(KEYWORD$=FNU$(A1$))=0 THEN GOTO 2900
1940 GOTO 2000
1950 REM " --- Error reading record"
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1990 READ (SYS01_DEV,KEY=K$(1,LEN(K$)-1)+$FF$,DOM=2900)
2000 REM " --- Display page?"
2010 IF L<MAX_ROW THEN GOTO 2400
2020 LET XMODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 0800
2050 IF FKEY=3 THEN GOTO 9900
2060 IF FKEY=4 THEN GOTO 9900
2070 IF SELECTION$<>"" THEN GOTO 4000
2400 REM " --- Lookup window record"
2410 DIM G1$(WIDTH-2)
2420 LET G1$(2)=A0$(3,8),G1$(12,4)=A0$(11,4),G1$(20)=A1$
2430 IF ACTION>0 THEN LET G1$(2)=A1$
2480 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$(3)
2490 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2990 GOTO 1400
3000 REM " --- End of file"
3010 IF ACTION=0 THEN GOTO 3800
3100 REM " --- Help for current system"
3110 IF FNP$(MODULE$)="" THEN GOTO 3200
3120 LET START$=MODULE$,TOPIC$(1)=MODULE$,MODULE$=""
3190 GOTO 1200
3200 REM " --- Help for current menu"
3210 IF FNP$(MENU$)="" THEN GOTO 3300
3220 LET START$=MENU$,TOPIC$(1)=MENU$,MENU$=""
3290 GOTO 1200
3300 REM " --- Help for task (lead overlay)"
3310 IF FNP$(LEAD$)="" THEN GOTO 3400
3320 LET START$=LEAD$,TOPIC$(1)=LEAD$,LEAD$=""
3390 GOTO 1200
3400 REM " --- Help for current program"
3410 IF FNP$(PGM$)="" THEN GOTO 3800
3420 LET START$=PGM$,TOPIC$(1)=PGM$,PGM$=""
3490 GOTO 1200
3800 REM " --- No help found"
3810 IF X0>0 THEN GOTO 3900
3820 DIM MESSAGE$[1]
3830 LET MESSAGE$[0]="No Help Topics Found (<Enter>=Continue)"
3840 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=INT((WIDTH-(LEN(MESSAGE$[0])+9))/2)
3850 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,FKEY
3860 IF ACTION=1 THEN LET FKEY=4
3870 IF FKEY=4 OR FKEY=5 OR FKEY=6 THEN GOTO 4000
3890 GOTO 0800
3900 REM " --- Display last page"
3910 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3920 GOSUB 6200
3930 IF FKEY=1 THEN GOTO 0800
4000 REM " --- All done"
4090 GOTO 9900
5500 REM " --- Search Method"
5510 DIM MORE$[1,MAXROW,1]
5520 CALL "SYC.WA",0,OWIDTH,OHEIGHT,OWIN_X,OWIN_Y,OTITLE$,ONAME$
5530 CALL "SYC.WC",1,0,OWIDTH-2,0,0,OHEIGHT-4,1
5540 LET OMAX_ROW=OHEIGHT-2,PAGE=1,XMODE=0
5600 REM " --- Lightbar selection of search method"
5610 CALL "SYC.SA",XMODE,OPTLIST$[ALL],MORE$[ALL],OPTION$,PAGE,OMAX_ROW,ONAME$,OHEIGHT,OWIDTH,FKEY
5615 IF FKEY=4 THEN GOTO 5690
5620 IF OPTION$="END" THEN GOTO 5690
5630 IF OPTION$="PRIOR" THEN GOTO 5690
5635 LET METHOD$="",METHOD=0
5640 FOR X=1 TO OPTIONS
5650 IF OPTION$<>OPTLIST$[1,X,2] THEN GOTO 5670
5660 LET METHOD$=FNP$(OPTLIST$[1,X,1]),METHOD=X
5670 NEXT X
5690 RETURN 
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6240 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,5
6280 LET L=1,X1=1
6290 RETURN 
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9910 IF NAME$<>"" THEN IF ACTION=0 THEN CALL "SYC.WD",NAME$
9920 IF FKEY<>0 THEN LET SELECTION$=""
9950 EXIT 
9999 END

0010 REM "OPM - Contract Pricing Table Maintenance"
0020 REM "Program OPM.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Oct96 Caj: Error 13 in IVC.IA line 1990 cuz IVM-14 wasn't opened or passed to IVC.IA (110,130,135,140,160,165,407)
0040 REM 
0060 BEGIN 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="IVM-06",FILES$[2]="IVM-01",FILES$[3]="IVM-14"
0135 LET FILES$[4]="ARM-01",FILES$[5]="SYS-01"
0140 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET IVM06_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM14_DEV=CHANNELS[3]
0165 LET ARM01_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5]
0200 REM " --- IOLISTS "
0210 IVM06A: IOLIST A0$(1),A1$,A[ALL]
0220 IVM01A: IOLIST *,X2$(1)
0230 ARM01A: IOLIST X0$(1),X1$(1)
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0290 IVS01A: IOLIST X$,X$,I2$,X$,X$,X$,M1$,M2$,M3$,M4$
0400 REM " --- Parameters "
0405 DIM P[1],I[3]
0406 DIM IV_CHANS[14],PARAMS[3],DESC[3]
0407 LET IV_CHANS[1]=IVM01_DEV,IV_CHANS[14]=IVM14_DEV
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0450 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),I[0]=NUM(I2$(1,2))
0460 LET I[1]=NUM(I2$(9,2)),I[2]=NUM(I2$(11,2)),I[3]=NUM(I2$(13,2))
0465 LET PARAMS[0]=I[0]
0470 FOR X=1 TO 3
0475 LET DESC[X]=NUM(I2$((X-1)*2+9,2))
0480 LET PARAMS[X]=DESC[X]
0485 NEXT X
0500 REM " --- Initialize Data "
0505 PRECISION NUM(I2$(5,1))
0510 DIM A0$(28),A1$(48),A[19],X0$(8),X1$(30),MESSAGE$[1]
0520 LET A0$(1)=N0$
0570 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),M4=LEN(M4$)
0700 REM " --- Background "
0710 PRINT 'SB',@(5,3),"Customer:",@(2,4),"Item Number:",@(5,5),"Comments:",@(3,6),"Start Date:",@(4,7),"Stop Date:",@(15+M2-9,9),"Quantity",@(34+M3-6),"Price",'SF',
0760 FOR X=1 TO 10
0770 PRINT 'SB',@(11,X+9),X:"##:",'SF',
0780 NEXT X
0900 REM " --- Initialize File "
0920 READ (IVM06_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Customer "
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(IVM06_DEV,END=1040)
1025 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1030 IF V2$<>"" THEN LET A0$(3)=V2$(3)
1035 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1040 LET V0$="M",V1$="KCE",V3$=P1$,V0=6,V1=15,V2=3,V4$="Enter A Valid Customer Number (<F3>=Lookup)"
1045 GOSUB 7000
1050 IF V3=4 THEN GOTO 4500
1055 IF V3=3 THEN CALL "SYC.LC",V$
1060 IF V$="" THEN GOTO 0900
1080 LET X0$(1)=N0$+V$,A0$(3,6)=V$
1085 FIND (ARM01_DEV,KEY=X0$,DOM=1040)IOL=ARM01A
1090 PRINT @(V1,V2),FNF$(X0$(3,P[0]),M0$)," ",X1$(1,30),
1100 REM " --- Inventory Item "
1105 DIM ITEM$[6],ITEM[12]
1110 LET DEFAULT$="",DEFAULT$=A0$(9,20)
1115 LET ACTION$="",V1=15,V2=4,V4$="Enter A Valid Item Code"
1120 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1125 IF V3=2 THEN GOTO 1000
1127 IF V3=4 THEN GOTO 4500
1130 LET V$=ITEM$[0]
1135 LET A0$(9)=ITEM$[0]
1160 PRINT @(V1,V2),V$(1,I[0])," ",ITEM$[1],'CL'
1200 REM " --- Find Key "
1210 FIND (IVM06_DEV,KEY=A0$,DOM=1400)IOL=IVM06A
1300 REM " --- Display "
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New Record "
1410 LET V4$="Is This A New Record"
1420 CALL "SYC.YN",1,V4$,1,V$,V3
1430 IF V3=4 THEN GOTO 4500
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set Up "
1510 DIM A1$(48),A[19]
2000 REM " --- Comments "
2010 LET V4$="Enter Any Optional Comments"
2020 LET V0$="S",V1$="C",V2$=A1$(1,20),V3$="",V0=20,V1=15,V2=5,I0=0
2040 GOSUB 7000
2050 ON V3 GOTO 2060,2000,3900,2000,3900
2060 LET A1$(1,20)=V$
2090 GOTO 3900
2100 REM " --- Quantity Breaks "
2110 FOR X=0 TO 9
2120 REM " --- Quantity "
2130 LET V4$="Enter The Quantity For This Break"
2140 LET V0$="N",V1$="C",V2$=STR(A[X]),V3$=M2$,V0=M2,V1=15,V2=X+10
2150 GOSUB 7000
2170 IF V3=4 THEN EXITTO 3900
2180 IF V3=2 THEN GOTO 2210
2190 IF X>0 AND V<A[X-1] THEN GOTO 2130
2195 LET A[X]=V
2200 GOTO 2300
2210 IF X>0 THEN LET X=X-1
2220 IF X=0 THEN EXITTO 3900
2230 GOTO 2140
2300 REM " --- Price "
2310 LET V4$="Enter The Price For This Quantity Break"
2320 LET V0$="N",V1$="C",V2$=STR(A[X+10]),V3$=M3$,V0=M3,V1=33,V2=X+10
2340 GOSUB 7000
2360 LET A[X+10]=V
2370 IF V3=4 THEN EXITTO 3900
2375 IF V3=2 THEN GOTO 2140
2380 NEXT X
2390 GOTO 3900
2400 REM " --- Start Date "
2410 LET V0$="D",V1$="C",V2$=A1$(21,3),V3$="",V4$="Enter The Starting Date For This Contract Pricing",V0=3,V1=15,V2=6
2415 IF V2$="   " THEN LET V2$=F0$(7,3)
2420 GOSUB 7000
2430 LET A1$(21,3)=V$
2440 GOTO 3900
2500 REM " --- Ending Date "
2510 LET V0$="D",V1$="C",V2$=A1$(24,3),V3$="",V4$="Enter The Ending Date For This Contract Pricing",V0=3,V1=15,V2=7
2515 IF V2$="   " THEN LET V2$=""
2520 GOSUB 7000
2530 LET A1$(24,3)=V$
2540 GOTO 3900
3900 REM " --- Next Field "
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2400,2500,2100,4000
4000 REM " --- Options "
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="KC",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4050 IF V$="DELETE" THEN GOTO 4400
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Check Sequence "
4110 FOR X=1 TO 9
4120 IF A[X]=0 THEN EXITTO 4300
4130 IF A[X]>A[X-1] THEN GOTO 4150
4140 EXITTO 4200
4150 NEXT X
4190 GOTO 4300
4200 REM 
4210 LET MESSAGE$[0]="Quantity Breaks Not In Ascending Sequence (<Enter>=Continue)"
4220 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
4290 GOTO 2100
4300 REM " -- Write "
4310 WRITE (IVM06_DEV,KEY=A0$)IOL=IVM06A
4390 GOTO 1000
4400 REM " --- Delete "
4410 REMOVE (IVM06_DEV,KEY=A0$,DOM=4490)
4490 GOTO 1000
4500 REM " --- Print "
4510 LET V4$="Do You Wish To Print A List Of All Records"
4530 CALL "SYC.YN",1,V4$,1,V$,V3
4540 ON POS(V$="YN") GOTO 4500,4560,9900
4560 LET O0=1
4580 RUN "OPM.BB"
5000 REM "DISPLAY"
5020 PRINT @(15,5),A1$(1,20),@(15,6),FNB$(A1$(21,3)),@(15,7),FNB$(A1$(24,3)),
5040 FOR X=0 TO 9
5050 IF A[X]<>0 THEN PRINT @(15,X+10),A[X]:M2$,@(34),A[X+10]:M3$,
5080 NEXT X
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "OPE - Temporary/New Customer Input"
0020 REM "Program OPE.DG"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0210 ARM01A: IOLIST B0$,B1$
0220 ARM02A: IOLIST C0$,C1$,C[ALL]
0230 ARM04A: IOLIST X$
0235 ARM04B: IOLIST X$
0240 ARM04C: IOLIST X$
0245 ARM04D: IOLIST X$
0250 ARM04E: IOLIST X$
0255 ARM04F: IOLIST X$
0260 ARM04G: IOLIST X$
0265 ARM04H: IOLIST X$
0270 ARM04J: IOLIST X$
0275 ARM04K: IOLIST X$
0280 ARM04L: IOLIST X$
0300 ARM10A: IOLIST X$
0305 ARM10B: IOLIST X$
0315 ARM10D: IOLIST X$
0320 ARM10F: IOLIST X$
0325 ARM10H: IOLIST X$
0330 ARM10I: IOLIST X$
0335 ARM10L: IOLIST X$
0340 ARM10M: IOLIST X$
0390 ARS10N: IOLIST N$,N[ALL]
0400 REM " --- Confirm Temporary Customer"
0410 PRINT @(10,3),"       ",@(0,22),'CL',
0420 LET V4$="Do You Wish To Create A New Customer"
0430 CALL "SYC.YN",0,V4$,3,V$,V3
0440 IF V$<>"Y" THEN GOTO 9800
0450 LET S$(1,1)="1"
0500 REM " --- Intialize Data"
0510 DIM B0$(8),B1$(282),C0$(10),C1$(64),C[7]
0520 LET B1$(156,3)=F0$(7,3),B1$(146,10)=B1$(1,10),B1$(227,1)="Y"
0530 LET C1$(6,2)=P3$(50,2),C1$(21,3)=P3$(52,3),C1$(1,3)=P3$(55,3),C1$(32,2)=P3$(58,2),C1$(4,2)=P3$(60,2),C1$(8,2)=P3$(62,2),C1$(38,1)=P3$(78,1),C1$(10,2)="NN",C1$(20,1)="N"
0540 IF C9$(7,1)="Y" THEN IF C9$(25,1)="Y" THEN LET C1$(39,1)="Y"
1000 REM " --- Next Temporary Customer Numbers"
1010 DIM N[4]
1020 LET N$=N0$+"N",N[1]=1000
1030 EXTRACT (ARS10_DEV,KEY=N$,DOM=1040)IOL=ARS10N
1040 IF N[1]>999999 THEN LET N[1]=1000
1050 LET A0$(5,6)=STR(N[1]:FILL(6,"0"))
1060 LET N[1]=N[1]+1
1070 WRITE (ARS10_DEV,KEY=N$)IOL=ARS10N
1080 FIND (ARM01_DEV,KEY=N0$+A0$(5,6),DOM=1200)
1090 GOTO 1000
1200 REM " --- Initialize New Customer"
1210 LET B0$=N0$+A0$(5,6),C0$=B0$+"  "
1220 PRINT @(10,3),FNF$(A0$(5,P[0]),M0$)
2000 REM " --- Name"
2020 LET V0$="S",V1$="C",V2$=B1$(1,30),V3$="",V4$="Enter Billing Name",V0=30,V1=10,V2=4,I0=0
2040 GOSUB 7000
2060 LET B1$(1,30)=V$
2070 IF POS(" "<>B1$(146,10))=0 THEN LET B1$(146,10)=B1$(1,10)
2090 GOTO 3900
2100 REM " --- Address 1"
2120 LET V0$="S",V1$="",V2$=B1$(31,24),V3$="",V0=24,V1=5,V2=5
2140 GOSUB 7000
2150 LET B1$(31,24)=V$
2190 GOTO 3900
2200 REM " --- Address 2"
2220 LET V0$="S",V1$="",V2$=B1$(55,24),V3$="",V0=24,V1=5,V2=6
2240 GOSUB 7000
2250 LET B1$(55,24)=V$
2290 GOTO 3900
2300 REM " --- Address 3"
2320 LET V0$="S",V1$="",V2$=B1$(79,24),V3$="",V0=24,V1=5,V2=7
2340 GOSUB 7000
2350 LET B1$(79,24)=V$
2390 GOTO 3900
2400 REM " --- Zip Code"
2420 LET V0$="S",V1$="CE",V2$=B1$(103,P[2]),V0=P[2],V1=30,V2=7,V3$="",V4$="Enter (up to) "+STR(P[2])+" character zip/postal code"
2440 GOSUB 7000
2450 LET B1$(103,P[2])=V$
2460 PRINT @(V1,V2),FNZ$(B1$(103))
2490 GOTO 3900
2500 REM " --- Telephone"
2510 PRINT @(51,15),"Phone #: "
2520 LET V0$="H",V1$="",V2$=B1$(112,10),V3$=PHONE$,V0=10,V1=60,V2=15
2540 GOSUB 7000
2550 LET B1$(112,10)=V$
2590 GOTO 3900
2600 REM " --- Territory"
2610 PRINT @(12,16),"Territory: ",
2620 LET V0$="S",V1$="KC",V2$=C1$(21,3),V3$="",V0=3,V1=23,V2=16,V4$="Enter A Valid Territory Code, <F3>=Lookup"
2630 GOSUB 7000
2640 IF V3=3 THEN GOSUB 6200
2650 LET C1$(21,3)=V$
2660 FIND (ARM10_DEV,KEY=N0$+"H"+C1$(21,3),DOM=2600)IOL=ARM10H
2670 PRINT @(30,16),X$(7,20),
2690 GOTO 3900
2700 REM " --- Tax Code"
2710 PRINT @(13,18),"Tax Code: ",
2720 LET V0$="S",V1$="KC",V2$=C1$(32,2),V3$="",V0=2,V1=23,V2=18,V4$="Enter A Valid Tax Code, <F3>=Lookup"
2730 GOSUB 7000
2740 IF V3=3 THEN GOSUB 6050
2750 LET C1$(32,2)=V$
2760 FIND (ARM10_DEV,KEY=N0$+"B"+C1$(32,2),DOM=2700)IOL=ARM10B
2770 PRINT @(30,18),X$(6,20),
2790 GOTO 3900
2800 REM " --- Terms"
2810 PRINT @(11,19),"Terms Code: ",
2820 LET V0$="S",V1$="KCR",V2$=C1$(4,2),V3$="",V0=2,V1=23,V2=19,V4$="Enter A Valid Terms Code, <F3>=Lookup"
2830 GOSUB 7000
2840 IF V3=3 THEN GOSUB 6000
2850 LET C1$(4,2)=V$
2860 FIND (ARM10_DEV,KEY=N0$+"A"+C1$(4,2),DOM=2800)IOL=ARM10A
2870 PRINT @(30,19),X$(6,20),
2890 GOTO 3900
2900 REM " --- Distribution"
2910 PRINT @(9,20),"Distribution: ",
2920 LET V0$="S",V1$="KCR",V2$=C1$(8,2),V3$="",V0=2,V1=23,V2=20,V4$="Valid G/L Distribution Code, <F3>=Lookup"
2930 GOSUB 7000
2940 IF V3=3 THEN GOSUB 6100
2950 LET C1$(8,2)=V$
2960 FIND (ARM10_DEV,KEY=N0$+"D"+C1$(8,2),DOM=2900)IOL=ARM10D
2970 PRINT @(30,20),X$(6,20),
2990 GOTO 3900
3000 REM " --- Sales Discount"
3005 LET V0$="S",V1$="C",V2$="",V3$="",V4$="<Enter>=Continue, <F4>=Default Codes: ",V0=1,V1=FNV(V4$),V2=22
3006 GOSUB 7000
3007 IF V3=4 THEN GOTO 4500
3010 PRINT @(8,15),"Sale Discount: ",
3020 LET V0$="S",V1$="KC",V2$=C1$(6,2),V3$="",V0=2,V1=23,V2=15,V4$="Enter A Valid Discount Code, <F3>=Lookup"
3030 GOSUB 7000
3040 IF V3=3 THEN GOSUB 6250
3050 LET C1$(6,2)=V$
3060 FIND (ARM10_DEV,KEY=N0$+"I"+C1$(6,2),DOM=3000)IOL=ARM10I
3080 PRINT @(30,15),X$(6,20),
3090 GOTO 3900
3100 REM " --- Salesperson"
3110 PRINT @(10,17),"Salesperson: ",
3120 LET V0$="S",V1$="KC",V2$=C1$(1,3),V3$="",V0=3,V1=23,V2=17,V4$="Enter A Valid Salesperson Code, <F3>=Lookup"
3130 GOSUB 7000
3140 IF V3=3 THEN GOSUB 6150
3150 LET C1$(1,3)=V$
3160 FIND (ARM10_DEV,KEY=N0$+"F"+C1$(1,3),DOM=3100)IOL=ARM10F
3170 PRINT @(30,17),X$(7,20),
3190 GOTO 3900
3200 REM " --- Price Code"
3210 PRINT @(9,14),"Pricing Code: "
3220 LET V0$="S",V1$="C",V2$=C1$(24,4),V0=4,V1=23,V2=14,V4$="Enter Valid Price Code (<F1>=None/<F3>=Lookup)"
3230 GOSUB 7000
3235 IF V3=1 THEN LET V$=""
3240 IF V3=3 THEN GOSUB 6350
3245 IF V3=4 OR V3=2 THEN GOTO 3900
3250 LET V$=V$+"    ",V$=V$(1,4); DIM X$(30); LET X$(8)="No Price Code"
3255 IF POS(" "<>V$)=0 THEN GOTO 3270
3260 FIND (ARM10_DEV,KEY=N0$+"M"+V$,DOM=3200)IOL=ARM10M
3270 LET C1$(24,4)=V$
3280 PRINT @(0,7),C1$(24,4),@(V1,V2),C1$(24,4),"  ",X$(8,20)
3290 GOTO 3900
3300 REM " --- Alph Search Key"
3310 PRINT @(5,13),"Alpha Search Key: ",
3320 LET V0$="S",V1$="KC",V2$=B1$(146,10),V0=10,V1=23,V2=13,V4$="Enter A 10 Character Alpha Search Key, <F1>=Last Name"
3330 IF POS(" "<>V2$)=0 THEN LET V2$=B1$(1,10)
3340 GOSUB 7000
3345 IF V3=1 THEN CALL "SYC.NA",B1$(1,30),V$
3350 LET B1$(146,10)=V$
3360 PRINT @(V1,V2),B1$(146,10)
3390 GOTO 3900
3400 REM " --- Retain"
3410 PRINT @(51,13),"Retain?:",
3420 LET V0$="Y",V1$="KC",V2$=B1$(227,1),V4$="Y=Retain Customer After Invoice Is Removed, N=Do Not Retain",V1=60,V2=13
3440 GOSUB 7000
3460 LET B1$(227,1)=V$
3490 GOTO 3900
3700 REM " --- Customer Type"
3710 PRINT @(45,14),"Customer Type:",
3720 LET V0$="S",V1$="KC",V2$=C1$(34,3),V4$="Enter A Valid Customer Type, <F3>=Lookup",V0=3,V1=60,V2=14
3730 GOSUB 7000
3740 IF V3=3 THEN GOSUB 6300
3760 LET C1$(34,3)=V$
3770 FIND (ARM10_DEV,KEY=N0$+"L"+C1$(34,3),DOM=3700)IOL=ARM10L
3780 PRINT @(V1+V0+1,V2),X$(7,80-(V1+V0+1))
3790 GOTO 3900
3800 REM " --- Customer Invoice Detail History?"
3810 PRINT @(5,21),"Cust Inv Dtl His: ",
3820 LET V0$="Y",V1$="KC",V2$=C1$(38,1),V0=10,V1=23,V2=21,V4$="Keep Detail History For This Customer (Y/N)? "
3830 GOSUB 7000
3840 LET C1$(38,1)=V$
3850 IF C1$(38,1)="Y" THEN PRINT @(30,21),'CL',"Keep Invoice Detail History", ELSE PRINT @(30,21),"Do Not Keep Invoice Detail History",
3890 GOTO 3900
3900 REM " --- Next Field"
3905 IF S$(1,1)="1" AND V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,2300,2400,3300,3400,3200,3700,2500,3000,2600,3100,2700,2800,2900,3800,3990
4000 REM " --- Correct?"
4020 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4400
4050 ON POS(V$="YN") GOTO 4000,4100,4060
4060 LET S$(1,1)="1"; GOTO 2000
4100 REM " --- Write"
4105 PRINT @(0,13),'CE',
4110 WRITE (ARM01_DEV,KEY=B0$)IOL=ARM01A
4115 WRITE (ARM02_DEV,KEY=C0$)IOL=ARM02A
4120 LET X$=N0$+"A"+B1$(146,10)+B0$(3,6)
4125 WRITE (ARM04_DEV,KEY=X$)IOL=ARM04A; REM "Alternate Sequence
4130 LET X$=N0$+"B"+C1$(1,3)+B0$(3,6)
4135 WRITE (ARM04_DEV,KEY=X$)IOL=ARM04B; REM "Salesperson"
4140 LET X$=N0$+"C"+C1$(21,3)+B0$(3,6)
4145 WRITE (ARM04_DEV,KEY=X$)IOL=ARM04C; REM "Territory"
4150 LET X$=N0$+"D"+B1$(228,10)+B0$(3,6)
4155 WRITE (ARM04_DEV,KEY=X$)IOL=ARM04D; REM "Contact"
4160 LET X$=N0$+"E"+B1$(103,9)+B0$(3,6)
4165 WRITE (ARM04_DEV,KEY=X$)IOL=ARM04E; REM "Zip/Postal Code"
4170 LET X$=N0$+"F"+C1$(4,2)+B0$(3,6)
4175 WRITE (ARM04_DEV,KEY=X$)IOL=ARM04F; REM "Terms Code
4180 LET X$=N0$+"G"+C1$(6,2)+B0$(3,6)
4185 WRITE (ARM04_DEV,KEY=X$)IOL=ARM04G; REM "Sales Discount
4190 LET X$=N0$+"H"+C1$(8,2)+B0$(3,6)
4195 WRITE (ARM04_DEV,KEY=X$)IOL=ARM04H; REM "Distribution
4200 LET X$=N0$+"J"+C1$(24,4)+B0$(3,6)
4205 WRITE (ARM04_DEV,KEY=X$)IOL=ARM04J; REM "Pricing Code
4210 LET X$=N0$+"K"+C1$(32,2)+B0$(3,6)
4215 WRITE (ARM04_DEV,KEY=X$)IOL=ARM04K; REM "Tax Code
4220 LET X$=N0$+"L"+B1$(112,10)+B0$(3,6)
4225 WRITE (ARM04_DEV,KEY=X$)IOL=ARM04L; REM "Telephone
4290 GOTO 9900
4400 REM " --- Decrement Temporary Customer Number"
4410 DIM N[4]; LET N$=N0$+"N",N[1]=1000
4420 EXTRACT (ARS10_DEV,KEY=N$,DOM=4440)IOL=ARS10N
4430 IF NUM(A0$(5,6))=N[1]-1 THEN LET N[1]=NUM(A0$(5,6))
4440 WRITE (ARS10_DEV,KEY=N$)IOL=ARS10N
4450 GOTO 9800
4500 REM " --- Defaults"
4510 PRINT @(8,15),"Sale Discount: ",@(23,15),P3$(50,2),
4520 FIND (ARM10_DEV,KEY=N0$+"I"+P3$(50,2),DOM=4700)IOL=ARM10I
4530 PRINT @(30,15),X$(6,20),
4540 PRINT @(12,16),"Territory: ",@(23,16),P3$(52,3),
4550 FIND (ARM10_DEV,KEY=N0$+"H"+P3$(52,3),DOM=4700)IOL=ARM10H
4560 PRINT @(30,16),X$(7,20),
4570 PRINT @(10,17),"Salesperson: ",@(23,17),P3$(55,3),
4580 FIND (ARM10_DEV,KEY=N0$+"F"+P3$(55,3),DOM=4700)IOL=ARM10F
4590 PRINT @(30,17),X$(7,20),
4600 PRINT @(13,18),"Tax Code: ",@(23,18),P3$(58,2),
4610 FIND (ARM10_DEV,KEY=N0$+"B"+P3$(58,2),DOM=4700)IOL=ARM10B
4620 PRINT @(30,18),X$(6,20),
4630 PRINT @(11,19),"Terms Code: ",@(23,19),P3$(60,2),
4640 FIND (ARM10_DEV,KEY=N0$+"A"+P3$(60,2),DOM=4700)IOL=ARM10A
4650 PRINT @(30,19),X$(6,20),
4660 PRINT @(9,20),"Distribution: ",@(23,20),P3$(62,2),
4670 FIND (ARM10_DEV,KEY=N0$+"D"+P3$(62,2),DOM=4700)IOL=ARM10D
4680 PRINT @(30,20),X$(6,20),
4685 PRINT @(5,21),"Cust Inv Dtl His: ",@(23,21),'CL',C1$(38,1),
4690 IF C1$(38,1)="Y" THEN PRINT @(30,21),"Keep Invoice Detail History", ELSE PRINT @(30,21),"Do Not Keep Invoice Detail History",
4695 GOTO 4000
4700 REM " --- Missing Default"
4720 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Default Value Is Missing, <Enter> To Continue",V0=1,V1=FNV(V4$),V2=22
4730 GOSUB 7000
4740 LET I0=8
4790 GOTO 3000
6000 REM " --- Terms Lookup"
6005 LET NMBR=1
6010 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6020 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6025 LET DESCRIPT$[1]="Terms",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6030 CALL "SYC.LK",ARM10_DEV,"A",NMBR,"Terms Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6040 RETURN 
6050 REM " --- Tax Lookup"
6055 LET NMBR=1
6060 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6070 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6075 LET DESCRIPT$[1]="Tax",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6080 CALL "SYC.LK",ARM10_DEV,"B",NMBR,"Tax Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6090 RETURN 
6100 REM " --- Distribution Lookup"
6105 LET NMBR=1
6110 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6120 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6125 LET DESCRIPT$[1]="Distribution",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6130 CALL "SYC.LK",ARM10_DEV,"D",NMBR,"Distribution Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6140 RETURN 
6150 REM " --- Salesperson Lookup"
6155 LET NMBR=1
6160 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6170 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3
6175 LET DESCRIPT$[1]="Salesperson",FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
6180 CALL "SYC.LK",ARM10_DEV,"F",NMBR,"Salesperson Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6190 RETURN 
6200 REM " --- Territory Lookup"
6205 LET NMBR=1
6210 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6220 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3
6225 LET DESCRIPT$[1]="Territory",FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
6230 CALL "SYC.LK",ARM10_DEV,"H",NMBR,"Territory Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6240 RETURN 
6250 REM " --- Sales Discount Lookup"
6255 LET NMBR=1
6260 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6270 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6275 LET DESCRIPT$[1]="Sales Discount",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6280 CALL "SYC.LK",ARM10_DEV,"I",NMBR,"Sales Discount Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6290 RETURN 
6300 REM " --- Customer Type Lookup"
6305 LET NMBR=1
6310 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6320 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3
6325 LET DESCRIPT$[1]="Customer Type",FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
6330 CALL "SYC.LK",ARM10_DEV,"L",NMBR,"Customer Type Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6340 RETURN 
6350 REM " --- Pricing Code Lookup"
6355 LET NMBR=1
6360 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6370 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=4
6375 LET DESCRIPT$[1]="Pricing Code",FIELD[1]=1,POSITION[1]=8,LENGTH[1]=20
6380 CALL "SYC.LK",ARM10_DEV,"M",NMBR,"Pricing Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6390 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9800
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 LET O0=1
9810 RUN O0$
9900 LET O0=2,S$(12,1)="1"
9910 RUN O0$
9999 END

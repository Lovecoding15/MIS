0010 REM "ARR - Customer Ranking Report"
0020 REM "Program ARR.KA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ARM-02",FILES$[3]="ARM-04"
0130 LET FILES$[4]="ARM-06",FILES$[5]="ARW-06",FILES$[6]="ARM-10"
0135 LET FILES$[7]="SYS-01"
0140 LET OPTIONS$[5]="C"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET ARM01_DEV=CHANNELS[1],ARM02_DEV=CHANNELS[2],ARM04_DEV=CHANNELS[3]
0185 LET ARM06_DEV=CHANNELS[4],ARW06_DEV=CHANNELS[5],ARM10_DEV=CHANNELS[6]
0190 LET SYS01_DEV=CHANNELS[7]
0200 REM " --- IOLists
0210 ARM02A: IOLIST B0$,B1$
0220 ARM06A: IOLIST A0$,A1$,A[ALL]
0230 ARW06A: IOLIST W0$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[2],G[4]
0460 FOR X=0 TO 4
0470 LET G[X]=NUM(G2$(X*2+1,2),ERR=0480)
0480 NEXT X
0485 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=FNYY_YEAR(P4$(3,2))
0500 REM " --- Init Data"
0510 DIM J$(80,"-"),J0$(10,"0"),J1$(40," "),J2$(H1,"-"),A[21]
0520 LET M0=LEN(M0$),M1=LEN(M1$),CODES$=""
0700 REM " --- Background"
0710 FOR X=4 TO 6 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(2,3),"Ranking Method (1, 2, 3, 4, 5, 6, or 7):",@(2,5),"Sort by Territory/Salesperson/Type/None:",'SF',
1000 REM " --- Ranking Method"
1020 LET V0$="S",V1$="RCE",V2$=R7$,V3$="1234567",V0=1,V1=43,V2=3,V4$="1=MTD Sales, 2=MTD GP, 3=MTD %, 4=YTD Sales, 5=YTD GP, 6=YTD %, 7=Cust #",I0=0
1030 PRINT @(0,21),"Rank by:"
1040 GOSUB 7000
1042 IF V3=4 THEN GOTO 9900
1045 PRINT @(0,21),'CL'
1050 LET R7$=V$
1060 IF R7$="7" THEN LET X$="Customer Number" ELSE IF R7$="1" THEN LET X$="MTD Sales" ELSE IF R7$="2" THEN LET X$="MTD Gross Profit $" ELSE IF R7$="3" THEN LET X$="MTD Gross Profit %" ELSE IF R7$="6" THEN LET X$="YTD Gross Profit %" ELSE IF R7$="4" THEN LET X$="YTD Sales" ELSE IF R7$="5" THEN LET X$="YTD Gross Profit $" ELSE GOTO 1000
1065 LET H6$=X$
1070 PRINT 'SF',@(V1+3,V2),X$,
1090 GOTO 3900
1100 REM " --- Alternate Sort?
1110 LET V0$="S",V1$="ER",V2$=R6$,V3$="STNY",V4$="Rank customers within T=Territory, S=Salesperson, Y=Type, N=None",V0=1,V1=43,V2=5
1120 GOSUB 7000
1130 LET R6$=V$
1140 IF R6$="T" THEN LET H7$="Sorted within Territory",H9$="Territory",X9$="H" ELSE IF R6$="S" THEN LET H7$="Sorted within Salesperson",H9$="Salesperson",X9$="F" ELSE IF R6$="Y" THEN LET H7$="Sorted within Customer Type",H9$="Customer Type",X9$="L" ELSE LET H7$="",H9$="",X9$=""
1160 PRINT @(V1+2,V2),H7$
1190 GOTO 3900
3900 REM " --- New Field"
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 0700,1100,4000
4000 REM " --- Correct?"
4010 LET V0$="S",V1$="EC",V2$="Y",V3$="YNR",V4$="Is The Above Information Correct (Y/N/Reprint)? ",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V3=4 THEN GOTO 9900
4045 IF V$="R" OR V$="REPRINT" THEN GOTO 4110
4050 ON POS(V$="YN") GOTO 4000,4100,0700
4100 GOSUB 6000
4200 RUN "ARR.KB"
6000 REM " --- Build ARW-06 Work File"
6040 DIM A[21]
6095 LET R1=0; REM "Rec Count
6100 REM " --- Read ARM-06 And Sort To ARW-06
6110 CALL "SYC.NB","Sorting",12,COLUMN
6120 READ (ARM06_DEV,KEY=N0$,DOM=6121)
6130 LET K$=KEY(ARM06_DEV,END=6490)
6140 IF POS(N0$=K$)<>1 THEN GOTO 6490
6150 READ (ARM06_DEV)IOL=ARM06A
6200 LET X=0,R7=NUM(R7$)
6210 ON R7-1 GOTO 6230,6240,6250,6260,6270,6280,6320
6230 LET X=A[0]; GOTO 6300
6240 LET X=A[0]-A[3]; GOTO 6300
6250 IF A[0]>0 THEN LET X=(A[0]-A[3])*100/A[0] FI; GOTO 6300
6260 LET X=A[1]; GOTO 6300
6270 LET X=A[1]-A[4]; GOTO 6300
6280 IF A[1]>0 THEN LET X=(A[1]-A[4])*100/A[1] FI; GOTO 6300
6300 REM " --- Write It Here"
6305 IF X=0 THEN GOTO 6440
6310 LET X=X-99999999
6320 LET K9$="   "
6340 IF R6$="N" THEN GOTO 6400
6350 IF LEN(A0$)>8 THEN LET A0$=A0$(1,8)
6370 FIND (ARM02_DEV,KEY=A0$+"  ",DOM=6400)IOL=ARM02A
6380 IF R6$="T" THEN LET K9$=B1$(21,3) ELSE IF R6$="S" THEN LET K9$=B1$(1,3) ELSE IF R6$="Y" THEN LET K9$=B1$(34,3)
6390 IF POS(K9$=CODES$,3)=0 THEN LET CODES$=CODES$+K9$
6410 LET W0$=N0$+K9$+STR(X:"000000000.00-")+A0$(3,6)
6420 WRITE (ARW06_DEV,KEY=W0$)IOL=ARW06A
6430 LET R1=R1+1
6440 PRINT @(COLUMN,11),K9$,"  ",A0$(3,6),
6480 GOTO 6130
6499 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "DDR - File Definition Listing (Input)"
0020 REM "Program DDR.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYM-03",FILES$[2]="SYS-01",FILES$[3]="DDM-03"
0140 LET FILES$[4]="DDM-07"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS<>0 THEN GOTO 9900
0180 LET SYM03_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2],DDM03_DEV=CHANNELS[3]
0190 LET DDM07_DEV=CHANNELS[4]
0200 REM " --- IOLists
0210 SYM03A: IOLIST A0$(1),A1$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="DD",N4$=F5$
0500 REM " --- Initializations
0510 DIM A0$(6),A1$(64)
0550 LET BEGFILE$="",ENDFILE$="",REVISION$=""
0560 LET MAPPING$="N",RECORDS$="N",SORTBY$="F",FILETYPE$="CDGIMRST"
0700 REM " --- Background
0705 PRINT @(0,3),'CE',
0710 FOR X=4 TO 16 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(31,3),"Sequence",@(14,5),"Beginning File Name/Alias",@(17,7),"Ending File Name/Alias",@(30,9),"File Type",@(27,11),"Last Revised",@(12,13),"Include Mapping Information",@(13,15),"Include Record Information",'SF',
0790 GOSUB 5000
1000 REM " --- Report Sequence"
1010 LET V0$="S",V1$="C",V2$=SORTBY$,V3$="FA",V0=1,V1=40,V2=3,I0=0
1020 LET V4$="Select Report Sequence (F=By File Name/A=By Alias)"
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1000,3900
1050 IF POS(V$="FA")=0 THEN GOTO 1000
1060 LET SORTBY$=V$
1090 GOTO 3900
1200 REM " --- Beginning File Name
1210 LET V0$="S",V1$="C",V2$=BEGFILE$,V3$="",V0=6,V1=40,V2=5
1215 IF SORTBY$="A" THEN LET V0=12
1220 LET V4$="Enter Beginning File Name Or Alias (<F1>=First/<F3>=Lookup)"
1230 GOSUB 7000
1240 ON V3 GOTO 1300,1320,1200,1250,4000
1250 CALL "SYC.LD","",V1,V2,V$
1260 IF SORTBY$="F" THEN GOTO 1300
1270 LET A0$(1)=V$
1280 FIND (SYM03_DEV,KEY=A0$,DOM=1290)IOL=SYM03A
1290 LET V$=A1$(40,12)
1300 REM 
1310 IF V$<>"" THEN GOTO 1350
1320 LET BEGFILE$=""
1330 PRINT @(V1,V2),'CL',"First"
1340 GOTO 1390
1350 LET BEGFILE$=V$
1360 PRINT @(V1,V2),'CL',BEGFILE$
1390 GOTO 3900
1400 REM " --- Ending Data Name
1410 LET V0$="S",V1$="C",V2$=ENDFILE$,V3$="",V0=6,V1=40,V2=7
1415 IF SORTBY$="A" THEN LET V0=12
1420 LET V4$="Enter Ending File Name Or Alias (<F1>=Last/<F3>=Lookup)"
1430 GOSUB 7000
1440 ON V3 GOTO 1500,1520,3900,1450,4000
1450 CALL "SYC.LD","",V1,V2,V$
1460 IF SORTBY$="F" THEN GOTO 1500
1470 LET A0$(1)=V$
1480 FIND (SYM03_DEV,KEY=A0$,DOM=1490)IOL=SYM03A
1490 LET V$=A1$(40,12)
1500 REM 
1510 IF V$<>"" THEN GOTO 1550
1520 LET ENDFILE$=""
1530 PRINT @(V1,V2),'CL',"Last"
1540 GOTO 1590
1550 LET ENDFILE$=V$
1560 PRINT @(V1,V2),'CL',ENDFILE$
1590 GOTO 3900
1800 REM " --- File Type
1810 LET V0$="S",V1$="C",V2$=FILETYPE$,V3$="",V0=10,V1=40,V2=9
1820 LET V4$="Enter File Types To Include (C/D/G/I/M/R/S/T/<F1>=All)"
1830 GOSUB 7000
1840 ON V3 GOTO 1900,1920,3900,1800,4000
1900 REM 
1910 IF V$<>"" THEN GOTO 1950
1920 LET V$="CDGIMRST"
1950 FOR X=1 TO LEN(V$)
1960 IF POS(V$(X,1)="CDGIMRST")=0 THEN EXITTO 1800
1970 NEXT X
1980 LET FILETYPE$=V$
1985 PRINT @(V1,V2),'CL',FILETYPE$
1990 GOTO 3900
2000 REM " --- Last Revision
2010 LET V0$="D",V1$="C6N",V2$=REVISION$,V3$="",V0=8,V1=40,V2=11
2020 LET V4$="Enter The First Revision Date To Include (<F1>=First)"
2030 GOSUB 7000
2040 ON V3 GOTO 2100,2120,3900,2000,4000
2100 REM 
2110 IF V$<>"" THEN GOTO 2150
2120 LET REVISION$=""
2130 PRINT @(V1,V2),'CL',"First"
2140 GOTO 2190
2150 LET REVISION$=V$
2160 PRINT @(V1,V2),'CL',FNB6$(REVISION$)
2190 GOTO 3900
2200 REM " --- Include Mapping Information?"
2210 LET V0$="Y",V1$="C",V2$=MAPPING$,V3$="",V0=1,V1=40,V2=13
2220 LET V4$="Include File Mapping Information On Report (Y/N)?"
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 LET MAPPING$=V$
2290 GOTO 3900
2400 REM " --- Include Record Information?"
2405 IF SORTBY$="A" THEN LET RECORDS$="Y"; GOTO 2460
2410 LET V0$="Y",V1$="C",V2$=RECORDS$,V3$="",V0=1,V1=40,V2=15
2420 LET V4$="Include Record Information On Report (Y/N)?"
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 LET RECORDS$=V$
2460 PRINT @(V1,V2),RECORDS$,
2490 GOTO 3900
3900 REM " --- Next field
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1800,2000,2200,2400,3990
3990 LET I0=0
4000 REM " --- Correct?
4005 GOSUB 5000
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="S",V1$="CR",V2$="Y",V3$="",V0=1,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4090 ON POS(V$="YN") GOTO 4000,4900,1000
4900 REM " --- Run report overlay
4990 RUN "SYM.FB"
5000 REM " --- Display"
5010 PRINT @(40,3),'CL',SORTBY$,
5020 IF BEGFILE$="" THEN PRINT @(40,5),'CL',"First", ELSE PRINT @(40,5),'CL',BEGFILE$,
5030 IF ENDFILE$="" THEN PRINT @(40,7),'CL',"Last", ELSE PRINT @(40,7),'CL',ENDFILE$,
5040 PRINT @(40,9),'CL',FILETYPE$,
5050 IF REVIONS$="" THEN PRINT @(40,11),'CL',"First", ELSE PRINT @(40,11),'CL',FNB6$(REVISION$),
5060 PRINT @(40,13),'CL',MAPPING$,
5070 PRINT @(40,15),'CL',RECORDS$,
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

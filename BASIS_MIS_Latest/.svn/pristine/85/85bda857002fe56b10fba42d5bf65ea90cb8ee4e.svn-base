0010 REM "ARR - MONTHLY CASH RECEIPTS JOURNAL PURGE"
0020 REM "Program ARU.XA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ARM-10",FILES$[3]="ART-06"
0130 LET FILES$[4]="ART-16",FILES$[5]="ART-26",FILES$[6]="SYS-01"
0160 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET ARM01_DEV=CHANNELS[1],ARM10_DEV=CHANNELS[2],ART06_DEV=CHANNELS[3]
0185 LET ART16_DEV=CHANNELS[4],ART26_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0200 REM " --- IOLists
0220 ARM01A: IOLIST *,X0$
0230 ARM10C: IOLIST X0$
0360 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0370 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0380 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[3]
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=FNYY_YEAR(P4$(3,2))
0500 REM " --- Init Data
0530 DIM J$(80,"-")
0700 REM " --- Background
0710 FOR X=4 TO 12 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'CF','SB',@(20,3),"Beginning Customer:",@(23,5),"Ending Customer:",@(16,7),"Beginning Receipt Date:",@(19,9),"Ending Receipt Date:",@(21,11),"Cash Receipt Code:",'SF',@(0,22),'CL',
1005 PRINT 'CF',
1200 REM " --- Beginning Customer
1210 LET V0$="M",V1$="KC",V2$=R8$,V3$=P1$,V4$="Enter A Valid Customer Number (<F1>=First/<F3>=Lookup)",V0=6,V1=40,V2=3,I0=0
1220 GOSUB 7000
1240 IF V3<>3 THEN GOTO 1250
1242 GOSUB 6400
1244 IF V$="" THEN GOTO 1200
1250 IF V3=1 THEN LET V$=""
1260 IF V$="" THEN PRINT @(V1,V2),"First",'CL',; GOTO 1280
1265 READ (ARM01_DEV,KEY=N0$+V$,DOM=1200)IOL=ARM01A
1270 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$),"  ",X0$(1,30),'CL'
1280 LET R1$=V$
1290 GOTO 3900
1400 REM " --- Ending Customer
1410 LET V0$="M",V1$="KC",V2$=R2$,V3$=P1$,V4$="Enter A Valid Customer Number (<F1>=Last/<F3>=Lookup)",V0=6,V1=40,V2=5
1415 PRINT @(V1,V2),'CL',
1420 GOSUB 7000
1430 IF V3=2 THEN GOTO 3900
1440 IF V3<>3 THEN GOTO 1450
1442 GOSUB 6400
1444 IF V$="" THEN GOTO 1800
1450 IF V3=1 THEN LET V$=""
1460 IF V$="" THEN PRINT @(V1,V2),"Last",'CL'; GOTO 1480
1465 READ (ARM01_DEV,KEY=N0$+V$,DOM=1400)IOL=ARM01A
1470 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$),"  ",X0$(1,30),'CL'
1480 LET R2$=V$
1485 IF R2$<R1$ THEN GOTO 1400
1490 GOTO 3900
1600 REM " --- Beginning Date
1620 LET V0$="D",V1$="",V2$=R3$,V0=8,V1=40,V2=7,V4$="Enter Starting Receipt Date (<F1>=First)"
1640 GOSUB 7000
1645 IF V3=2 THEN GOTO 3900
1650 IF V3=1 THEN LET V$=""
1660 IF V$="" THEN PRINT @(V1,V2),"First",'CL',
1680 LET R3$=V$
1690 GOTO 3900
1800 REM " --- Ending Date
1820 LET V0$="D",V1$="",V2$=R4$,V0=8,V1=40,V2=9,V4$="Enter Ending Receipt Date (<F1>=Last)"
1840 GOSUB 7000
1845 IF V3=2 THEN GOTO 3900
1850 IF V3=1 THEN LET V$=""
1860 IF V$="" THEN PRINT @(V1,V2),"Last",'CL',
1880 LET R4$=V$
1890 GOTO 3900
1900 REM " --- Cash Receipt Code
1920 LET V0$="S",V1$="C",V2$=R5$,V3$="",V0=3,V1=40,V2=11,V4$="Enter A Valid Cash Receipt Code (<F1>=All/<F3>=Lookup)"
1930 GOSUB 7000
1931 IF V3=2 THEN GOTO 3900
1932 IF V3<>3 THEN GOTO 1940
1934 GOSUB 6100
1936 IF V$="" THEN GOTO 1900
1940 IF V3=1 THEN LET V$=""
1950 IF V$="" THEN PRINT @(V1,V2),"All",'CL',; GOTO 1980
1955 LET V$=V$+"   ",V$=V$(1,3)
1960 FIND (ARM10_DEV,KEY=N0$+"C"+V$,DOM=1900)IOL=ARM10C
1970 PRINT @(V1+5,V2),X0$(7,20)
1980 LET R5$=V$
1990 GOTO 3900
3900 REM " --- New Field
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1200,1400,1600,1800,1900,4000
4000 REM " --- Correct?
4010 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct (Yes/No)?",V0=3,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 IF V$="YES" THEN GOTO 4100
4060 IF V$="NO" THEN GOTO 1000 ELSE GOTO 4000
4100 RUN "ARU.XB"
6100 REM " --- Cash Receipt Lookup
6110 LET V4=1,V5=ARM10_DEV,V6=4,V7=3,V8=7,V9=20,V5$="C"
6120 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
6190 RETURN 
6400 REM " --- Alpha Search
6405 PRINT @(0,16),'CL',@(0,18),'CL'
6440 CALL "SYC.LC",V$
6490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "SYC - PF Reports Display (Control Program)"
0020 REM "Program SYC.QR"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open Files"
0105 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYM-02",PRINTER$=""
0140 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYM02_DEV=CHANNELS[1]
0200 REM " --- IOLIST's"
0210 PFXXXA: IOLIST A0$(1),A1$(1)
0270 SYS01T: IOLIST TERMINAL$,F0$,F1$,F2$,F3$,F4$,F5$
0280 SYM02A: IOLIST C0$(1),C1$(1),C[ALL]
0400 REM " --- Parameters"
0410 DIM SPEED$(128),C0$(3),C1$(128),C[4]
0415 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=9900)
0420 LET SYS01_DEV=NUM(STBL("!SYS01",ERR=9900),ERR=9900)
0425 LET SYM01_DEV=NUM(STBL("!SYM01",ERR=9900),ERR=9900)
0430 LET SYM11_DEV=NUM(STBL("!SYM11",ERR=9900),ERR=9900)
0440 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0450 LET PRINTER$=F0$(13,2)+F0$(25,1),MAXOPT=15,MAXPF=99
0460 FIND (SYM02_DEV,KEY=F0$(4,3),DOM=9900)IOL=SYM02A
0470 IF C[0]<1 THEN LET C[0]=10
0475 IF FNP$(C1$(101,3))="" THEN LET C1$(101,3)=F0$(4,3)
0480 FOR X=101 TO 103
0485 IF POS(C1$(X,1)="01234567890ABCDEFGHIJKLMNOPQRSTUVWXYZ")=0 THEN EXITTO 4900
0490 NEXT X
0500 REM " --- Initializations"
0505 LET PFNAME$="PF"+C1$(101,3),D_STATUS=0,SECURITY$=SPEED$(65,1)
0510 LET WIDTH=80,HEIGHT=14,WIN_X=0,WIN_Y=3,MAXROW=HEIGHT-5
0515 LET OWIDTH=20,OHEIGHT=6,OWIN_X=WIN_X,OWIN_Y=WIN_Y+HEIGHT
0520 LET HWIDTH=WIDTH-OWIDTH,HHEIGHT=6,HWIN_X=OWIN_X+OWIDTH,HWIN_Y=OWIN_Y
0525 LET PAGES=INT(MAXPF/(MAXROW-1))+1; REM "Only MAXROW-1 PF reports per page
0530 DIM OPTLIST$[2,MAXOPT,6],OPTMORE$[PAGES,MAXOPT,4],INFO$[10],INFO[16]
0535 DIM SECLIST$[MAXOPT],MESSAGE$[1],SIZES[99],A0$(2),A1$(64)
0545 LET OTITLE$="Options",HTITLE$="Information",INFO$[1]=PFNAME$
0550 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),BUF$(WIDTH-2)
0555 LET HEADING$(2)="Report Description",HEADING$(50)="Date"
0560 LET HEADING$(60)="Time",HEADING$(WIDTH-6)="Size",REPORTS=PAGES*MAXROW
0565 LET FOOTING$(2)="F1=Select/Unselect for Batch  F4=End  PgUp  PgDn"
0570 LET INFO[0]=REPORTS,INFO[1]=WIDTH,INFO[2]=HEIGHT,INFO[3]=OWIDTH
0575 LET INFO[4]=OHEIGHT,INFO[5]=HWIDTH,INFO[6]=HHEIGHT,INFO[16]=SYS01_DEV
0580 LET XWIDTH=60,XHEIGHT=6,XWIN_X=9,XWIN_Y=OWIN_Y+2-XHEIGHT
0585 LET XNAME$="ASCII",XTITLE$="Print to File"
0590 LET PAGE=1
0600 REM " --- Other file opens"
0605 LET FILES$[2]=PFNAME$+"A",OPTIONS$[2]="F"
0610 CALL "SYC.DA",1,2,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0613 IF STATUS>0 THEN GOTO 9900
0615 LET PFILE_DEV=CHANNELS[2]
0620 IF PFILE_DEV>0 THEN GOTO 0650
0625 CALL "SYC.MA","M",PFNAME$+"A","SY","2",0,128,STATUS
0630 IF STATUS=0 THEN GOTO 0600
0635 LET MESSAGE$[0]="Unable to Define PF Index File (<Enter>=Continue)"
0640 CALL "SYC.XA",1,MESSAGE$[ALL],1,22,-1,V$,V3
0645 GOTO 9900
0650 REM " --- Load options"
0660 LET PGM$=PGM(-2),OPTION=0
0670 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,MENU$,""
0690 IF OPTION=0 THEN GOTO 9900
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 CALL "SYC.WA",0,OWIDTH,OHEIGHT,OWIN_X,OWIN_Y,OTITLE$,ONAME$
0730 CALL "SYC.WA",0,HWIDTH,HHEIGHT,HWIN_X,HWIN_Y,HTITLE$,HNAME$
0740 LET INFO$[2]=NAME$,INFO$[3]=ONAME$,INFO$[4]=HNAME$
0750 CALL "SYC.WB",ONAME$,0
0760 PRINT 'CS',
0765 FOR X=1 TO OMAXROW
0770 LET SECLIST$[X]=OPTLIST$[1,X,5],X$=OPTLIST$[1,X,3]
0775 LET OPTLIST$[1,X,3]=OPTLIST$[1,X,4],OPTLIST$[1,X,5]=HNAME$
0780 LET OPTLIST$[1,X,4]=X$,OPTLIST$[1,X,5]=HNAME$
0785 PRINT @(0,X-1),OPTLIST$[1,X,1],
0790 NEXT X
0800 REM " --- Load option descriptions"
0810 FOR X=1 TO OMAXROW
0815 LET OPTMORE$[PAGE,X,0]="3"
0820 NEXT X
0830 LET OPTMORE$[1,1,2]=" This option will display the selected report(s) on your"
0840 LET OPTMORE$[1,1,3]=" terminal."
0850 LET OPTMORE$[1,2,2]=" This option will send the selected report(s) to a"
0860 LET OPTMORE$[1,2,3]=" printer."
0870 LET OPTMORE$[1,3,2]=" This will copy the selected report(s) to ASCII and pdf"
0880 LET OPTMORE$[1,3,3]=" files in your home directory."
0890 LET OPTMORE$[1,4,2]=" This option will delete the selected report(s)."
0900 REM " --- Final initializations"
0910 CALL "SYC.WB",HNAME$,0
0920 PRINT 'CS',@(1,1),"Select A Report Or Press <F4> To Exit."
0930 CALL "SYC.WB",NAME$,0
0940 DIM LLIST$[PAGES,MAXROW,5],REPORTS$[REPORTS](1),MORE$[PAGES,MAXROW,1]
0960 LET L=1,X0=0,X1=1,PICKS$="N",EOF$="N",MAXPAGE=PAGE,PAGE=1
0970 PRINT @(0,0),'CF','SB','BR',HEADING$,'ER',@(0,HEIGHT-4),'CL',@(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
0990 READ (PFILE_DEV,KEY="",DOM=1000)
1000 REM " --- Read next index record"
1010 LET K$=KEY(PFILE_DEV,END=2000)
1020 READ (PFILE_DEV)IOL=PFXXXA
1200 REM " --- Determine file size"
1210 LET INDEX=NUM(K$),FILES$[3]=PFNAME$+STR(INDEX),OPTIONS$[3]="F"
1220 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
1230 IF STATUS>0 THEN GOTO 1800
1240 LET REPORT_DEV=CHANNELS[3]
1250 IF REPORT_DEV=0 THEN GOTO 1800
1260 CALL "SYC.JA",REPORT_DEV,Y0$,Y1$,Y2$,Y0,Y1,BYTES,Y3,STATUS
1270 LET SIZES[INDEX]=BYTES
1290 CLOSE (REPORT_DEV,ERR=2000)
1400 REM " --- Store report information"
1410 LET XMODE=5
1420 IF L<MAXROW THEN GOTO 1600
1425 IF PAGE<MAXPAGE THEN GOTO 1500
1430 GOSUB 5000
1440 IF OPTION$[1]="EXIT" THEN GOTO 9900
1450 IF OPTION$[1]="STARTOVER" THEN GOTO 9900
1460 IF OPTION$[1]="PRIOR" THEN GOTO 9900
1470 IF SELECTION$<>"" THEN GOTO 3000
1490 GOTO 1550
1500 REM " --- Increment LLIST$[] Page"
1510 LET PAGE=PAGE+1
1550 REM " --- Initialize For Next LLIST$[] Page"
1560 LET L=1,X1=1
1600 REM " --- Format data for display"
1610 LET BUF$(2)=A1$(1,40),BUF$(48)=FNB6$(A1$(41,6))
1620 LET BUF$(58)=A1$(47,8),BUF$(68)=STR(BYTES:"##,###,##0")
1640 LET LLIST$[PAGE,X1,1]=BUF$,LLIST$[PAGE,X1,3]=STR(INDEX)
1650 LET LLIST$[PAGE,X1,2]=STR(X1),X0=X0+1,X1=X1+1,L=L+1
1690 GOTO 1900
1800 REM " --- Remove bad index"
1810 REMOVE (PFILE_DEV,KEY=STR(INDEX:"00"),DOM=1900)
1900 REM " --- Next report"
1990 GOTO 1000
2000 REM " --- End of file"
2010 CALL "SYC.WB",NAME$,0
2020 IF X0>0 THEN GOTO 2200
2030 PRINT @(0,0),'CF',
2040 LET MESSAGE$[0]="No Reports Found (<Enter>=Continue)"
2050 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
2090 GOTO 9900
2200 REM " --- Process last page"
2210 LET EOF$="Y",XMODE=5
2220 GOSUB 5000
2230 IF OPTION$[1]="EXIT" THEN GOTO 9900
2240 IF OPTION$[1]="STARTOVER" THEN GOTO 9900
2250 IF OPTION$[1]="PRIOR" THEN GOTO 9900
2260 IF SELECTION$="" THEN GOTO 9900
3000 REM " --- Process option window for selected reports"
3010 GOSUB 6000
3020 IF FKEY=-2 THEN GOTO 0900
3030 IF FKEY=4 THEN GOTO 9900
3040 IF ANSWER$="END" THEN GOTO 9900
3050 ON POS(CHOICE$="SPFD") GOTO 3090,4000,3200,3400,4000
3090 GOTO 9900
3200 REM " --- Select printer to print report to"
3210 DIM ARRAY$[3]
3220 CALL "SYC.QP",STATUS,ARRAY$[ALL]
3230 LET PRINTER=UNT,MESSAGE=0,NEWPFNAME$="",RESERVED$=""
3240 CALL "SYC.GA",PRINTER,MESSAGE,NEWPFNAME$,RESERVED$,STATUS
3250 IF STATUS THEN GOTO 0900
3270 IF ARRAY$[1]="STARTOVER" THEN GOTO 0900
3290 GOTO 4000
3400 REM " --- Enter the name of the file to print report to"
3410 CALL "SYC.WA",0,XWIDTH,XHEIGHT,XWIN_X,XWIN_Y,XTITLE$,XNAME$
3420 PRINT @(1,0),'SB',"Output File Name:",'SF',
3430 LET V0$="S",V1$="E",V2$="",V3$="",V4$="",V0=30,V1=19,V2=0
3440 GOSUB 7000
3450 IF V3=0 THEN GOTO 3500
3480 CALL "SYC.WD",XNAME$
3490 GOTO 0900
3500 REM " --- Create text file for output"
3505 if pos(".txt"=v$)=0 then v$=v$+".txt"
3510 LET TEMPFILE$=stbl("TEMP")+FID(0)+date(0:"%m%s")+".tmp",info$[10]=V$
3515 LET PRINTER=UNT,INFO$[9]=TEMPFILE$
3520 ERASE TEMPFILE$,ERR=open_printer
3530 GOTO 3520
3535 open_printer:
3540 open(printer,mode="FILE="+tempfile$,err=open_printfile)"PX"
3545 option$[1]="SYC.QT";goto 4000
3550 open_printfile: OPEN (PRINTER)TEMPFILE$
3560 CALL "SYC.MA","T",TEMPFILE$,"SY","",0,0,STATUS
4000 REM " --- Initialize remaining parameters"
4010 IF PICKS$="Y" THEN GOTO 4050
4030 LET REPORTS$[REPORT]="Y"
4050 LET INFO$[5]=XNAME$,INFO$[6]=PICKS$,INFO[7]=XWIDTH,INFO[8]=XHEIGHT
4060 LET INFO[9]=0,INFO[10]=0,INFO[11]=PAGES,INFO[12]=MAXROW
4070 LET INFO$[0]=CHOICE$,INFO[13]=PRINTER,INFO[14]=PFILE_DEV
4100 REM " --- Call selected subtask"
4110 CALL "SYC.QY",D_STATUS,OPTION$[ALL],REPORTS$[ALL],INFO$[ALL],INFO[ALL],SIZES[ALL]
4190 GOTO 0900
4900 REM " --- Invalid PF File ID"
4910 LET MESSAGE$[0]="A Valid PF File ID Is Required For This Operator (<Enter>=Continue)"
4920 CALL "SYC.XA",1,MESSAGE$[ALL],0,-1,-1,V$,V3
4990 GOTO 9900
5000 REM " --- Middle window processing"
5010 LET OPTION$[1]=""
5020 CALL "SYC.WB",NAME$,0
5200 REM " --- EOF?"
5210 IF EOF$<>"Y" THEN GOTO 5400
5230 LET SELECTION$=SELECTION$+"|EOF"
5400 REM " --- Lightbar selection"
5410 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAXROW,NAME$,HEIGHT,WIDTH,FKEY
5420 IF FKEY=-1 THEN GOTO 6850
5430 IF FKEY=-2 THEN GOTO 6850
5440 IF FKEY=4 THEN GOTO 6700
5450 IF FKEY=5 THEN GOTO 6800
5460 IF SELECTION$="END" THEN GOTO 6700
5470 IF FKEY=-16 THEN IF EOF$="Y" THEN IF PAGE=MAXPAGE THEN LET XMODE=5; GOTO 5200
5480 IF FKEY=-16 THEN LET PAGE=PAGE+1,MAXPAGE=PAGE; GOTO 6900
5600 REM " --- Which report selected?"
5610 LET PICK=NUM(SELECTION$,ERR=5000)
5620 LET REPORT=NUM(LLIST$[PAGE,PICK,3],ERR=5000)
5630 IF FKEY<>1 THEN GOTO 5800
5700 REM " --- Process REPORTS$ selection"
5710 IF REPORTS$[REPORT]="Y" THEN GOTO 5740
5720 LET REPORTS$[REPORT]="Y",LLIST$[PAGE,PICK,1](1,1)="*"
5730 GOTO 5750
5740 LET REPORTS$[REPORT]="N",LLIST$[PAGE,PICK,1](1,1)=" "
5750 PRINT @(0,PICK+1),LLIST$[PAGE,PICK,1](1,1),
5755 LET PICKS$="N"
5760 FOR X=1 TO REPORTS
5765 IF REPORTS$[X]="Y" THEN LET PICKS$="Y"
5770 NEXT X
5780 LET XMODE=15
5790 GOTO 5200
5800 REM " --- Return selection"
5810 LET SELECTION$=LLIST$[PAGE,PICK,2],REPORT=NUM(LLIST$[PAGE,PICK,3])
5820 LET REPORTS$[REPORT]="Y",LLIST$[PAGE,PICK,1](1,1)="*"
5890 GOTO 6900
6000 REM " --- Option window processing"
6010 LET OPTPAGE=1,OPTION$[1]=""
6020 CALL "SYC.WB",ONAME$,0
6030 PRINT 'CS',
6200 REM 
6210 CALL "SYC.SA",0,OPTLIST$[ALL],OPTMORE$[ALL],ANSWER$,OPTPAGE,OMAXROW,ONAME$,OHEIGHT,OWIDTH,FKEY
6220 IF FKEY=-16 THEN GOTO 6000
6230 IF FKEY=-1 THEN GOTO 6850
6240 IF FKEY=-2 THEN GOTO 6850
6250 IF FKEY=4 THEN GOTO 6700
6260 IF FKEY=5 THEN GOTO 6800
6270 IF ANSWER$="END" THEN GOTO 6700
6400 REM " --- What was selected?"
6410 FOR X=1 TO OPTION
6420 IF OPTLIST$[1,X,2]<>ANSWER$ THEN GOTO 6490
6430 LET OPTION$[2]=OPTLIST$[1,X,3],SELECTION=X
6450 LET SECURITY$=FNP$(SECLIST$[X]),PASSWORD$=FNP$(OPTLIST$[1,X,6])
6490 NEXT X
6500 REM " --- Check security"
6510 IF SECURITY$="" AND PASSWORD$="" THEN GOTO 6600
6520 CALL "SYC.QX","M",SECURITY$,SELECTION,OPTLIST$[ALL],STATUS
6530 IF STATUS>0 THEN GOTO 5600
6600 REM " --- Store selection"
6610 LET OPTION$[1]=ANSWER$,OPTION$[3]="",CHOICE$=OPTLIST$[1,SELECTION,4](1,1)
6690 GOTO 6900
6700 REM " --- <F4> Exit"
6710 LET OPTION$[1]="EXIT"
6790 GOTO 6900
6800 REM " --- <F5> SpeedSearch"
6810 LET OPTION$[1]="STARTOVER"
6840 GOTO 6900
6850 REM " --- Arrows"
6860 LET OPTION$[1]="PRIOR"
6900 REM " --- Option processing completed"
6910 CALL "SYC.WB",NAME$,0
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8070 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9910 IF PRINTER$="" THEN GOTO 9930
9915 LET F0$(13,2)=PRINTER$(1,2),F0$(25,1)=PRINTER$(3,1)
9920 WRITE (SYS01_DEV,KEY=TERMINAL$)IOL=SYS01T
9930 CLOSE (PFILE_DEV,ERR=9931)
9940 CLOSE (SYM02_DEV,ERR=9941)
9950 CLOSE (PRINTER,ERR=9951)
9960 IF HNAME$<>"" THEN CALL "SYC.WD",HNAME$
9965 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
9970 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9975 LET OPTION$[1]="",OPTION$[2]="",OPTION$[3]=""
9980 EXIT 
9999 END

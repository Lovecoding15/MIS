0010 REM "SYX - Remove Firm From System"
0020 REM "Program SYX.HA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=8
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="SYM-03",FILES$[3]="SYM-04"
0125 LET FILES$[4]="SYM-06",FILES$[5]="SYM-39",FILES$[6]="SYM-49"
0130 LET FILES$[7]="SYM-59"
0140 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SYM03_DEV=CHANNELS[2],SYM04_DEV=CHANNELS[3]
0170 LET SYM06_DEV=CHANNELS[4],SYM39_DEV=CHANNELS[5],SYM49_DEV=CHANNELS[6]
0180 LET SYM59_DEV=CHANNELS[7]
0200 REM " --- IOLIST's"
0210 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0220 SYM04A: IOLIST B0$(1),B1$(1)
0230 SYM06A: IOLIST C0$(1),C1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="SY"
0500 REM " --- Initializations"
0510 DIM A0$(6),A1$(64),A2$(30),A[2],B0$(2),B1$(64),C0$(2),C1$(40)
0550 LET C0$(1)=N0$,CONFIRM$="Y",M1$="###,##0",L0=9,L9=21,CHANNEL=0
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,6,0
0720 CALL "SYC.WC",1,0,80,0,0,8,0
0750 PRINT 'SB',@(1,3),"System:",@(3,4),"Firm:",@(0,5),"Confirm:",@(0,7),"File",@(8),"Description",@(40),"Records",@(50),"Removed"
0900 REM " --- Position file"
0990 READ (SYM04_DEV,KEY="",DOM=1000)
1000 REM " --- System"
1010 PRINT 'CF',
1020 LET V0$="S",V1$="C",V2$="",V0=2,V1=9,V2=3,V2$=KEY(SYM04_DEV,END=1030)
1030 LET V3$="",V4$="Enter A Valid System ID (<F3>=Lookup)",I0=0
1040 GOSUB 7000
1050 ON V3 GOTO 1070,1000,1000,1060,9900
1060 CALL "SYC.LM",V$
1070 LET B0$(1)=V$
1080 FIND (SYM04_DEV,KEY=B0$,DOM=1000)IOL=SYM04A
1090 PRINT @(V1,V2),B0$," ",B1$(1,35),'CL',
1100 REM " --- System not installed"
1110 IF B1$(39,1)="Y" THEN GOTO 3900
1120 LET MESSAGE$[0]=FNP$(B1$(1,35))+" Is Not Installed (<Enter>=Continue)"
1130 CALL "SYC.XA",2,MESSAGE$[ALL],0,-1,-1,V$,V3
1140 IF V$<>"SPECIAL" THEN GOTO 1000
1190 GOTO 3900
1200 REM " --- Firm"
1210 LET V0$="Z",V1$="C",V2$=C0$,V0=2,V1=9,V2=4
1220 LET V4$="Enter A Valid Firm (<F3>=Lookup)"
1230 GOSUB 7000
1240 ON V3 GOTO 1260,1200,3900,1250,9900
1250 CALL "SYC.LX",V$
1260 LET C0$(1)=V$
1270 FIND (SYM06_DEV,KEY=C0$,DOM=1200)IOL=SYM06A
1280 PRINT @(V1,V2),C0$," ",C1$,'CL',
1290 GOTO 3900
1400 REM " --- Confirm?"
1410 LET V0$="Y",V1$="C",V2$=CONFIRM$,V3$="",V0=1,V1=9,V2=5
1420 LET V4$="Confirm Clearing Of Each Individual File (Y/N)?"
1430 GOSUB 7000
1440 ON V3 GOTO 1450,1400,3900,1400,9900
1450 LET CONFIRM$=V$
1480 PRINT @(V1,V2),CONFIRM$,'CL',
1490 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,4000
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",1,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4030 ON POS(V$="YN") GOTO 4000,4050,1000
4100 REM " --- Remove records"
4110 LET L=L0
4190 READ (SYM03_DEV,KEY=B0$,DOM=4200)
4200 REM " --- Top of remove loop"
4210 LET K$=KEY(SYM03_DEV,END=4900)
4220 IF POS(B0$=K$)<>1 THEN GOTO 4900
4230 READ (SYM03_DEV)IOL=SYM03A
4240 IF L<=L9 THEN GOTO 4280
4250 PRINT @(0,L0),'LD',
4260 LET L=L9
4280 PRINT @(0,L),'CL',A0$,@(8),A1$(1,30),
4290 IF POS(A1$(37,1)="DIMS")=0 THEN GOTO 4200
4300 REM " --- Confirm?"
4310 LET NUMREC=0,NUMBER=0,CLEAR$="Y"
4315 IF CONFIRM$="N" THEN GOTO 4400
4320 LET V4$="Clear Firm "+C0$+" Records From This File"
4330 CALL "SYC.YN",1,V4$,2,V$,V3
4350 ON V3 GOTO 4360,4300,4300,4300,1000
4360 LET CLEAR$=V$
4390 ON POS(V$="YN") GOTO 4300,4400,4400
4400 REM " --- Open file"
4410 IF A1$(38,1)<>"Y" THEN GOTO 4450
4420 IF CLEAR$<>"Y" THEN GOTO 4450
4430 GOSUB 5500
4440 IF V$="SPECIAL" THEN LET CLEAR$="Y"
4450 LET FILES$[8]=A0$,OPTIONS$[8]="F"
4460 CLOSE (CHANNEL,ERR=4470)
4470 CALL "SYC.DA",1,8,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
4480 IF STATUS>0 THEN GOTO 4550
4485 LET CHANNEL=CHANNELS[8]
4490 IF CHANNEL=0 THEN GOTO 4550
4500 REM " --- Retrieve file information"
4510 CALL "SYC.JA",CHANNEL,FILENAME$,PATH$,TYPE$,KEYLEN,RECORDS,RECLEN,NUMREC,STATUS
4550 PRINT @(40,L),NUMREC:M1$,
4560 IF NUMREC>0 THEN GOTO 4600
4590 GOTO 4650
4600 REM " --- Perform removal"
4610 IF CLEAR$="Y" THEN GOSUB 6100
4650 PRINT @(50,L),NUMBER:M1$,
4660 LET L=L+1
4700 REM " --- Erase Batched Files"
4705 IF CLEAR$<>"Y" THEN GOTO 4890
4710 READ (SYM39_DEV,KEY="",DOM=4720)
4720 LET SYM39_K$=KEY(SYM39_DEV,END=4890)
4730 READ (SYM39_DEV)
4740 IF POS(A0$=SYM39_K$)<>11 THEN GOTO 4720
4750 REM " --- This File Is Batched"
4760 READ (SYM59_DEV,KEY=SYM39_K$(1,10)+C0$,DOM=4770)
4770 LET SYM59_K$=KEY(SYM59_DEV,END=4890)
4780 READ (SYM59_DEV)
4790 IF POS(SYM39_K$(1,10)+C0$=SYM59_K$)<>1 THEN GOTO 4890
4800 REM " --- Got A Batch To Remove"
4810 CLOSE (CHANNEL,ERR=4820)
4820 ERASE A0$+"."+SYM59_K$(13,3),ERR=4830
4825 GOTO 4820
4830 REMOVE (SYM49_DEV,KEY=SYM59_K$(13,3),DOM=4840)
4840 REMOVE (SYM59_DEV,KEY=SYM59_K$,DOM=4850)
4850 REM " --- Check For Another Batch"
4860 GOTO 4770
4890 REM " --- Get The Next File"
4895 GOTO 4200
4900 REM " --- Removal completed"
4910 LET V4$="Firm "+C0$+" Removed From "+FNP$(B1$(1,35))+" (<Enter>=Continue):"
4920 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
4930 GOSUB 7000
4990 GOTO 1000
5500 REM " --- Display windowed message and prompt for input"
5510 DIM MESSAGE$[1]
5530 LET MESSAGE$[0]="This file is write-protected and should not be cleared"
5540 LET MESSAGE$[1]="under normal circumstances (<Enter>=Continue)"
5560 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
5590 RETURN 
6100 REM " --- Remove firm from file"
6190 READ (CHANNEL,KEY=C0$,DOM=6200)
6200 REM " --- Remove Loop"
6210 LET K$=KEY(CHANNEL,END=6900)
6220 IF POS(C0$=K$)<>1 THEN GOTO 6900
6230 LET NUMBER=NUMBER+1
6240 IF MOD(NUMBER,10)=0 THEN PRINT @(50,L),NUMBER:M1$,
6300 REM " --- Remove record"
6310 REMOVE (CHANNEL,KEY=K$)
6390 GOTO 6200
6900 REM " --- Removal completed"
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

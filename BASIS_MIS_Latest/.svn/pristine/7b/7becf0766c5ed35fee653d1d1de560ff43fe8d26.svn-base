0010 REM "IVU - Inventory Transaction Update
0020 REM "Program IVU.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "14May97 WGH: May update wrong GL account (2570)"
0032 REM "09Jun97 JWK: Close Printer Before Asking Update Question (100,110)
0040 REM 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Close Printer Before Asking Update Question
0110 CLOSE (7,ERR=0111)
0200 REM " --- IOLists
0210 IVE01A: IOLIST A0$(1)
0220 IVM01A: IOLIST B0$(1),B1$(1),B2$,B3$,B4$,B5$,Z0$(1)
0230 IVM02A: IOLIST C0$(1),C1$(1)
0240 IVM10B: IOLIST Y0$(1),Y1$(1)
0290 ARM10D: IOLIST X0$(1),X1$(1)
0310 IVE11A: IOLIST W0$(1),W1$(1),W2$(1),W[ALL]
0500 REM " --- Init Data"
0510 PRECISION P[2]
0515 DIM FILES[44],PARAMS[10],PARAMS$[10],ITEM$[3],A0$(50),B0$(22),B1$(60)
0520 DIM C0$(24),C1$(64),Y0$(32),Y1$(G[4]),X0$(32),X1$(8*G[4])
0530 DIM W0$(53),W1$(22),W2$(67),W[2]
0550 LET FILES[0]=SYS01_DEV,FILES[1]=IVM01_DEV,FILES[2]=IVM02_DEV
0555 LET FILES[4]=IVM04_DEV,FILES[5]=IVM05_DEV,FILES[7]=IVM07_DEV
0560 LET FILES[8]=IVM08_DEV,FILES[12]=IVM12_DEV,FILES[17]=IVM17_DEV
0565 LET FILES[41]=IVT01_DEV,FILES[42]=IVT02_DEV,FILES[43]=IVT03_DEV
0570 LET FILES[44]=IVT04_DEV
0575 LET PARAMS[0]=G[0],PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3)
0580 LET PARAMS$[2]=P2$,PARAMS$[3]=P3$,PARAMS$[4]=P4$
0600 REM " --- Additional File Opens"
0610 IF GL$<>"Y" THEN GOTO 0650
0620 LET FILES$[20]="GLT-04",FILES$[21]="GLT-05"
0630 CALL "SYC.DA",1,20,21,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0640 IF STATUS>0 THEN GOTO 9900
0650 LET GLT04_DEV=CHANNELS[20],GLT05_DEV=CHANNELS[21]
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0800 REM " --- Run Update?"
0810 LET V4$="Are You Ready To Update The "+F5$
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0910 CALL "SYC.NB","Updating",11,COLUMN
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
0970 REM " --- File initializations"
0990 READ (IVE01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next header record"
1010 LET K$=KEY(IVE01_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 READ (IVE01_DEV)IOL=IVE01A
1200 REM " --- Transaction Code"
1210 DIM Y1$(G[4],"0")
1220 FIND (IVM10_DEV,KEY=N0$+"B"+A0$(13,2),DOM=1230)IOL=IVM10B
1900 REM " --- Position detail file"
1990 READ (IVE11_DEV,KEY=A0$(1,9),DOM=2000)
2000 REM " --- Read next detail"
2010 LET K$=KEY(IVE11_DEV,END=3900)
2020 IF POS(A0$(1,9)=K$)<>1 THEN GOTO 3900
2030 READ (IVE11_DEV)IOL=IVE11A
2040 PRINT @(COLUMN,11),W0$(3,7),"-",W0$(10,3)
2050 IF POS(Y0$(26,1)="AIR")=0 THEN GOTO 2900
2200 REM " --- Item Number"
2210 DIM ITEM$[3],REF$[11],REF[3],Z0$(G[4])
2220 FIND (IVM01_DEV,KEY=N0$+W1$(3),DOM=2900)IOL=IVM01A
2230 LET ITEM$[0]=W0$(1,2),ITEM$[1]=W1$(1,2),ITEM$[2]=W1$(3,20)
2240 LET ITEM$[3]=W2$(1,20),ACTION$="I"+Y0$(26,1),REF$[0]=A0$(15,3)
2250 LET REF$[2]=A0$(3,7),REF$[4]=A0$(13,2),REF[0]=W[0],REF[1]=W[1]
2260 LET REF$[5]=W0$(10,3),REF$[7]=W0$(28,10),REF$[8]=A0$(18,20)
2270 IF LS$="Y" THEN LET REF$[10]=W2$(21,7),REF$[11]=W2$(28,40)
2280 IF Y0$(26,1)="I" THEN LET REF[0]=-REF[0]
2300 REM " --- Inventory updates"
2310 CALL "IVC.UA",ACTION$,FILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEM$[ALL],REF$[ALL],REF[ALL],STATUS
2400 REM " --- Distribute by Item?"
2410 IF GL$<>"Y" THEN GOTO 2900
2420 IF Y0$(27,1)="N" THEN GOTO 2900
2430 IF POS(Y0$(26,1)="C")>0 THEN GOTO 2900
2440 IF A9$="N" THEN GOTO 2500
2450 DIM X1$(G[4]*8,"0")
2460 FIND (IVM02_DEV,KEY=W0$(1,2)+W1$,DOM=2500)IOL=IVM02A
2470 FIND (ARM10_DEV,KEY=N0$+"D"+C1$(13,2),DOM=2500)IOL=ARM10D
2480 LET Z0$(1)=X1$(61,G[4])
2500 REM " --- GL Distribution"
2510 PRECISION 2
2520 LET ACCOUNT$=Z0$,WHEN$=A0$(15,3),AMOUNT=W[2]
2530 LET REF1$="Inv Trn "+A0$(13,2),REF2$=A0$(3,7),REF3$=A0$(10,3)
2540 LET MEMO$=W1$(3,P[0])+" "+FNITEM$(B1$,P[3],P[4],P[5])
2550 IF Y0$(26,1)="I" THEN LET AMOUNT=-AMOUNT
2560 GOSUB GLPOST
2570 LET ACCOUNT$=W0$(28,G[4]),AMOUNT=-AMOUNT
2580 GOSUB GLPOST
2590 PRECISION P[2]
2900 REM " --- Remove detail and loop back for next"
2910 REMOVE (IVE11_DEV,KEY=W0$(1,12),DOM=2920)
2990 GOTO 2000
3900 REM " --- Remove Header Here"
3910 REMOVE (IVE01_DEV,KEY=A0$(1,12),DOM=3920)
3990 GOTO 1000
4000 REM " --- End"
4010 IF GL$="Y" THEN CALL "GLC.CA",STATUS
4020 CALL "SYC.BB",STATUS
4090 GOTO 9900
6900 REM " --- Standard G/L Posting Routine"
6910 GLPOST: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,WHEN$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions"
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "POC - Purchase Order SpeedSearch (QA Work In Process By Item)"
0020 REM "Program POC.QJ"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "24Apr00 KJS: Display extension correctly (1710,1730-1740,1780,1930)
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open/Lock Files"
0110 LET FILES=8
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="IVM-01",FILES$[2]="IVM-10",FILES$[3]="IVM-14"
0130 LET FILES$[4]="POE-03",FILES$[5]="POE-13",FILES$[6]="POE-43"
0135 LET FILES$[7]="POM-02",FILES$[8]="APM-01"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET IVM01_DEV=CHANNELS[1],IVM10_DEV=CHANNELS[2],IVM14_DEV=CHANNELS[3]
0170 LET POE03_DEV=CHANNELS[4],POE13_DEV=CHANNELS[5],POE43_DEV=CHANNELS[6]
0180 LET POM02_DEV=CHANNELS[7],APM01_DEV=CHANNELS[8]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1)
0220 POE03A: IOLIST B0$(1),B1$(1),B2$,B3$,B[ALL]
0230 APM01A: IOLIST C0$(1),C1$(1)
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,U0$,M1$,M2$,M3$
0270 APS01A: IOLIST X$,U1$,U2$,U3$,U4$,M0$,M1$,M2$,M3$
0310 POE13A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0320 POM02A: IOLIST Y0$(1),Y1$(1)
0330 IVM10C: IOLIST X0$(1)
0400 REM " --- Parameters"
0410 DIM I[5],P[1],X$(128)
0420 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2),MAXPAGE=NUM(X$(66,2))
0425 LET TNAME$=FNP$(X$(3,8)),ONAME$=FNP$(X$(27,8)),SNAME$=FNP$(X$(19,8))
0430 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0445 LET SHEIGHT=NUM(X$(45,2)),MAXOPT=NUM(X$(57,2))
0450 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0455 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0460 LET PAGES=INT((DSZ-5000)/4000),SYS01_DEV=NUM(STBL("!SYS01"))
0465 IF PAGES<3 THEN GOTO 9900
0470 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0475 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9900)IOL=APS01A
0480 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9900)IOL=IVS01A
0490 LET I[0]=NUM(P2$(1,2)),I[1]=NUM(P2$(5,1)),P[0]=NUM(U2$(1,2))
0495 LET I[3]=NUM(P2$(9,2)),I[4]=NUM(P2$(11,2)),I[5]=NUM(P2$(13,2))
0500 REM " --- Initializations"
0510 DIM A0$(22),A1$(60),A2$(64),B0$(22),B1$(160),B[8],OPTLIST$[1,MAXOPT,6]
0520 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),X0$(32),Y0$(4),Y1$(32)
0530 DIM FILES[14],ITEM$[6],ITEM[12],PARAMS[3],C0$(8),C1$(30),VENDOR$(25)
0535 DIM W0$(25),W1$(48),W2$(32),W3$(22),W4$(40),W[12]
0540 LET D_STATUS=0,MAXROW=HEIGHT-5,A0$(1)=N0$+OPTION$[3],DONE=0
0550 LET FILES[1]=IVM01_DEV,FILES[14]=IVM14_DEV,NF$="(Not On File)"
0560 LET PARAMS[0]=I[0],PARAMS[1]=I[3],PARAMS[2]=I[4],PARAMS[3]=I[5]
0570 LET PGM$=PGM(-2),OPTION=0
0600 REM " --- Resize masks"
0610 CALL "SYC.RM",M1$,M1,10,10
0620 CALL "SYC.RM",M2$,M2,9,9
0630 CALL "SYC.RM",M3$,M3,10,10
0650 REM " --- Option Window"
0660 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,""
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0720 LET HEADING$(2)="Rec #   Seq PO #    Vendor",HEADING$(48)="Received"
0730 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0740 PRINT @(0,0),'CS','SB','BR',HEADING$,'ER','SF',@(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",TNAME$,0
0920 PRINT @(0,1),'CL',@(0,2),'CL',@(0,3),'CL',
0930 CALL "SYC.WB",SNAME$,0
0940 PRINT 'CF',
0980 READ (IVM01_DEV,KEY=A0$,DOM=1000)IOL=IVM01A
0990 GOTO 1100
1000 REM " --- Process key field"
1010 GOSUB 6000
1015 CALL "SYC.WB",TNAME$,0
1020 PRINT @(0,0),'CS',@(0,2),'SB'," Item:",'SF',
1025 LET V2$="",V2$=KEY(IVM01_DEV,END=1040)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1035 IF V2$<>"" THEN LET V2$=V2$(3,I[0])
1040 LET ACTION$="V",V1=7,V2=2,V4$=""
1045 CALL "IVC.IA",ACTION$,V1,V2,FILES[ALL],N0$,V2$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1050 ON V3 GOTO 1080,1000,1000,1000,1060,1070,1060
1060 LET OPTION$[1]="EXIT"
1065 GOTO 4000
1070 LET OPTION$[1]="STARTOVER"
1075 GOTO 4000
1080 LET V$=FNP$(ITEM$[0])
1090 IF V$="" THEN GOTO 0900
1095 LET A0$(3)=V$
1200 REM " --- Retrieve records for display"
1210 DIM LLIST$[PAGES,MAXROW,5],X0$(32),DESCRIPTION$(WIDTH-10-I[0])
1220 DIM MORE$[PAGES,MAXROW,SHEIGHT-2]
1230 LET LINE=1,L=1,X0=0,X1=1,PAGE=1
1240 READ (IVM01_DEV,KEY=A0$,DOM=1000)IOL=IVM01A
1250 LET DESCRIPTION$(1)=FNITEM$(A1$,I[3],I[4],I[5])
1260 LET X$='SB'+" Item: "+'SF'+A0$(3,I[0])+" "+DESCRIPTION$
1265 LET X1$=A0$(3,I[0]),X2$=PARAM$
1270 CALL "SYC.QB",X$,X1$,X2$
1280 PRINT @(0,2),X$,
1390 READ (POE43_DEV,KEY=A0$,DOM=1400)
1400 REM " --- Read next line item"
1410 LET K$=KEY(POE43_DEV,END=1900)
1420 IF POS(A0$=K$)<>1 THEN GOTO 1900
1430 READ (POE43_DEV)
1435 LET B0$(1)=N0$+K$(33,13)+K$(23,7)
1440 LET W0$(1)=B0$+K$(30,3),C0$(1)=B0$,C1$(1)=NF$
1445 FIND (POE03_DEV,KEY=B0$,DOM=1800)IOL=POE03A
1450 FIND (POE13_DEV,KEY=W0$,DOM=1800)IOL=POE13A
1455 LET Y0$(1)=N0$+W1$(1,2),Y1$(1)="",X0$(1)=N0$+"C"+W3$(1,2)
1460 FIND (POM02_DEV,KEY=Y0$,DOM=1470)IOL=POM02A
1470 IF Y1$(21,1)<>"S" THEN GOTO 1800
1480 FIND (APM01_DEV,KEY=C0$,DOM=1490)IOL=APM01A
1490 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=1500)IOL=IVM10C
1500 REM " --- Display page?"
1510 IF L<MAXROW THEN GOTO 1600
1520 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
1530 GOSUB 5000
1540 IF DONE THEN GOTO 4000
1570 IF ANSWER$<>"" THEN GOTO 3000
1590 LET L=1,X1=1
1600 REM " --- Middle window record"
1610 DIM G$(WIDTH-2)
1620 LET VENDOR$(1)=FNF$(C0$(3,P[0]),M0$)+" "+C1$
1630 LET G$(2)=W0$(9,7)+" "+W0$(23,3)+" "+W0$(16,7)+" "+VENDOR$
1690 LET G$(48)=FNB6$(B1$(29,6)),LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=C0$(3)
1700 REM " --- Bottom window record"
1710 LET G$(1)="",MORE$[PAGE,X1,0]=STR(SHEIGHT-2),QTY=W[4]
1715 LET EXTENSION=(QTY*W[1])*1,G$(1)="{UM}"+W1$(22,2)
1720 LET G$(16)="{Order Qty}"+STR(W[3]:M2$)
1725 LET G$(36)="{Qty App'd}"+STR(W[5]:M2$),MORE$[PAGE,X1,1]=G$
1730 LET G$(1)="{Cd}"+Y0$(3,2)+" "+Y1$(1,11)
1740 LET G$(18)="{WIP Qty}"+STR(W[4]:M2$)
1750 LET G$(36)="{Unit Cost}"+STR(W[1]:M3$),MORE$[PAGE,X1,2]=G$
1760 LET G$(1)="{Wh}"+X0$(4,2)+" "+X0$(6,24)
1770 LET G$(16)="{Qty Rej'd}"+STR(W[6]:M2$)
1780 LET G$(36)="{WIP Total}"+STR(EXTENSION:M1$),MORE$[PAGE,X1,3]=G$
1790 LET LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
1800 REM " --- Loop back for next record"
1890 GOTO 1400
1900 REM " --- No more line items"
1910 IF X0>0 THEN GOTO 2000
1920 DIM MESSAGE$[1]
1930 LET MESSAGE$[0]="No QA Receipts Found (<Enter>=Continue)"
1940 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
1960 IF V3=5 THEN GOTO 4000
1970 IF V3=6 THEN GOTO 4000
1990 GOTO 3000
2000 REM " --- Display last page"
2010 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2020 GOSUB 5000
2030 IF DONE THEN GOTO 4000
2040 IF ANSWER$<>"" THEN GOTO 3000
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$=A0$(3),CHOICE2$=""
3030 GOSUB 5000
3040 IF CONTROL=1 THEN GOTO 1000
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=(OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR")
5090 RETURN
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CS',
6090 RETURN
8000 REM " --- Functions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9950 EXIT
9999 END
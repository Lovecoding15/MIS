0010 REM "IVM - Item Maintenance (Lot/Serial Transaction History)"
0020 REM "Program IVM.MO"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 PRECISION P[1]
0200 REM " --- IOLIST's"
0230 IVM10C: IOLIST X0$(1)
0500 REM " --- Initializations"
0510 DIM B0$(52),X0$(32),FILES[5],PARAMS[13],PARAMS$[13],O[2],I[3]
0520 DIM D0$(44),D1$(96),D[12],SAVED0$(44)
0545 LET I[3]=H0-9,I[2]=I[3]-13,I[1]=I[2]-P[2]-7,DW=((H0-M1-M2-M3)-16)+1
0565 LET X0=MAX(MIN(24,I[1]-(P[2]+20)),0),HEADING$(1)="   Date   Type Reference"
0570 LET HEADING$(H0-M1-M3-9)="Quantity",HEADING$(H0-M1-5)="Cost"
0580 LET HEADING$(H0-10)="Extension",FOOTING$(1)=" <F1>=Next Lot/Serial Number"
0585 LET FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0590 LET O[2]=H0-M1,O[1]=O[2]-M3,O[0]=O[1]-M2
0600 REM " --- Additional parameters"
0610 IF AR$="Y" THEN LET PARAMS[1]=CUSTLEN,PARAMS$[1]=CUSTMASK$,FILES[2]=ARM01_DEV
0620 IF AP$="Y" THEN LET PARAMS[2]=VENDLEN,PARAMS$[2]=VENDMASK$,FILES[3]=APM01_DEV
0630 IF GL$="Y" THEN LET PARAMS[3]=G[3],PARAMS$[3]=G5$,FILES[4]=GLM01_DEV
0640 LET FILES[0]=IVM01_DEV,FILES[1]=IVM10_DEV,FILES[5]=IVT01_DEV
0645 LET PARAMS[0]=P[0],PARAMS[4]=P[3],PARAMS[5]=P[4],PARAMS[6]=P[5]
0650 LET PARAMS[7]=31,PARAMS[8]=MAXROW,PARAMS[9]=WIDTH,PARAMS[10]=HEIGHT
0660 LET PARAMS[11]=52,PARAMS[12]=P[1],PARAMS$[0]=N0$,PARAMS$[9]=M1$
0670 LET PARAMS$[10]=M2$,PARAMS$[11]=M3$
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT 'CS','SB',@(1,0),"Warehouse:",@(I[1]-14),"Lot/Serial #:",@(I[2]-6),"From:",@(I[3]-4),"To:",'SF',
0730 CALL "SYC.WC",1,0,WIDTH-2,1,0,1,0
0740 PRINT @(0,2),'SB','BR',HEADING$,'ER','SF',
0750 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 IF PAGES<3 THEN GOTO 4000
0990 READ (IVM03_DEV,KEY=A0$(1,2)+"G"+A0$(3),DOM=1000)
1000 REM " --- Warehouse ID"
1005 PRINT 'CF',
1010 LET V2$="",V2$=KEY(IVM03_DEV,END=1040)
1020 IF POS(A0$(1,2)+"G"+A0$(3)=V2$)<>1 THEN LET V2$=""
1030 IF V2$<>"" THEN LET V2$=V2$(24)
1040 LET V0$="S",V1$="KE",V3$="",V4$="",V0=2,V1=12,V2=0
1050 GOSUB 7000
1055 ON V3 GOTO 1065,1000,1000,1060,4000
1060 CALL "IVC.LK",3,V1,V2,V$
1065 IF V$="" THEN GOTO 0900
1070 LET V$=V$+"  ",V$=V$(1,2)
1200 REM " --- Retrieve record"
1210 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=1000)IOL=IVM10C
1220 LET K$=A0$(1,2)+"G"+A0$(3)+V$,D0$(1)=A0$(1,2)+V$+A0$(3)
1230 READ (IVM03_DEV,KEY=K$,DOM=1240)
1240 READ (IVT01_DEV,KEY=D0$,DOM=1300)
1300 REM " --- Display record"
1310 GOSUB 5000
1340 GOTO 1400
1350 REM " --- Position file to next lot/serial number"
1360 FOR X=3 TO MAXROW
1370 PRINT @(0,X),'CL',
1380 NEXT X
1390 READ (IVT01_DEV,KEY=D0$(1,44)+$FF$,DOM=1400)
1400 REM " --- Lot/Serial Number"
1410 LET V2$="",V2$=KEY(IVT01_DEV,END=1440)
1420 IF POS(D0$(1,24)=V2$)<>1 THEN LET V2$=""
1430 IF V2$<>"" THEN LET V2$=V2$(25)
1440 LET V0$="S",V1$="KE",V3$="",V4$="",V0=P[2],V1=I[1],V2=0
1450 GOSUB 7000
1455 ON V3 GOTO 1500,1400,1000,1460,4000
1460 LET ACTION$=""
1465 LET SAVED0$(1)=D0$,SAVED0$(25)=V$
1470 CALL "SYC.LL",ACTION$,D0$,D1$,D[ALL]
1480 IF D0$=FILL(44) THEN LET D0$(1)=SAVED0$
1490 LET V$=D0$(25)
1495 PRINT @(V1,V2),D0$(25,10),
1500 REM " --- Retrieve record"
1510 LET X$=D0$(1,24)+V$
1520 READ (IVT01_DEV,KEY=X$,DOM=1530)
1530 LET K$=KEY(IVT01_DEV,END=1400)
1540 IF POS(X$=K$)<>1 THEN GOTO 1400
1550 LET D0$(1)=K$(1,44)
1600 REM " --- Beginning Date"
1610 LET V0$="D",V1$="E",V2$=BEGDATE$,V3$="",V4$="",V0=3,V1=I[2],V2=0
1620 GOSUB 7000
1630 ON V3 GOTO 1650,1640,1400,1600,4000
1640 LET V$=""
1650 IF V$="" THEN PRINT @(V1,V2),"First   ",
1690 LET BEGDATE$=V$
1700 REM " --- Ending Date"
1710 LET V0$="D",V1$="E",V2$=ENDDATE$,V3$="",V4$="",V0=3,V1=I[3],V2=0
1720 GOSUB 7000
1730 ON V3 GOTO 1750,1740,1600,1700,4000
1740 LET V$=""
1750 IF V$="" THEN PRINT @(V1,V2),"Last    ",
1760 IF V$<>"" THEN IF V$<BEGDATE$ THEN GOTO 1700
1790 LET ENDDATE$=V$
3000 REM " --- Call display routine"
3010 LET ACTION$="",PARAMS$[4]=D0$,PARAMS$[5]=BEGDATE$
3020 LET PARAMS$[6]=ENDDATE$,PARAMS$[7]=NAME$
3030 CALL "SYC.WB",NAME$,0
3040 CALL "IVC.TA",ACTION$,FILES[ALL],PARAMS[ALL],PARAMS$[ALL],SELECTION$,FKEY
3050 IF FKEY=1 THEN GOTO 1350
3060 IF FKEY=4 THEN GOTO 4000
3090 GOTO 1000
4000 REM " --- Run menu overlay"
4010 CALL "SYC.WD",NAME$
4090 RUN "IVM.MA"
5000 REM " --- Display record"
5010 LET X1$="First   ",X2$="Last    "
5020 IF BEGDATE$<>"" THEN LET X1$=FNB$(BEGDATE$)
5030 IF ENDDATE$<>"" THEN LET X2$=FNB$(ENDDATE$)
5050 PRINT @(12,0),X0$(4,2)," ",X0$(6,X0),@(I[1]),D0$(25,P[2]),@(I[2]),X1$,@(I[3]),X2$,
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9910 CALL "SYC.WD",NAME$
9950 RUN "SYS.AA"
9999 END

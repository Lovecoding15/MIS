0010 REM "SHR - Print Selected SHM-10 Records"
0020 REM "Program SHR.BB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 07Feb98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "19Dec97 JAL: Prevent Error 47/Line 710 (520)"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 SHM10A: IOLIST B0$(1),B1$(1),B[ALL]
0220 SHM10C: IOLIST A0$(1),A[ALL]
0230 SHM10D: IOLIST D0$(1),D1$(1),D[ALL]
0240 SHM10F: IOLIST W0$(1),W1$(1)
0250 SHM10G: IOLIST A0$(1),A1$(1)
0400 REM " --- Parameters"
0410 LET I0$=A0$,I1$=A1$
0500 REM " --- Initializations"
0510 DIM A0$(10),A1$(128),A[20],B0$(10),B1$(128),B[4],D0$(4),D1$(128),D[4]
0520 DIM W0$(5),W1$(160),J$(WIDTH,"-"),HEADINGS$[6],X$(WIDTH),J9$(80)
0530 DIM QUADRANT$[24,4]
0550 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$,L=L9+1,NUMBER=0
0560 LET M5$="(###)-###-####",QUADRANT=0
0700 REM " --- Background"
0710 PRINT @(COLUMN,11),J9$(1,7),
0800 REM " --- Record initializations"
0805 ON O0 GOTO 4900,0810,0830,0840,0850,0860,0870
0810 LET X$(1)="Whse  Name/Address",X$(39)="Telephone",X$(56)="State"
0815 LET HEADINGS$[4]=J$,HEADINGS$[5]=X$,HEADINGS$[6]=J$
0820 LET TITLE$="Warehouses",HEADINGS=6
0825 GOTO 0950
0830 LET TITLE$="Carriers"
0835 GOTO 0900
0840 LET TITLE$="Services"
0845 GOTO 0900
0850 LET TITLE$="Package Defaults"
0855 GOTO 0900
0860 LET TITLE$="Optional Charges"
0865 GOTO 0900
0870 LET TITLE$="Program Options"
0900 REM 
0910 LET HEADINGS$[4]=J$,HEADINGS=4
0950 REM " --- Position file"
0960 LET HEADINGS$[2]="SpeedShip "+TITLE$,X$=FIRST$
0980 IF X$<>"" THEN LET X$(LEN(X$))=CHR(ASC(X$(LEN(X$)))-1)
0990 READ (SHM10_DEV,KEY=N0$+RECORD$+X$,DOM=1000)
1000 REM " --- Next key"
1010 LET K$=KEY(SHM10_DEV,END=4000)
1020 IF POS(N0$+RECORD$=K$)<>1 THEN GOTO 4000
1030 IF LAST$<>"" THEN IF K$(4,LEN(LAST$))>LAST$ THEN GOTO 4000
1040 PRINT @(COLUMN,11),K$(4),
1050 LET NUMBER=NUMBER+1
1090 ON O0 GOTO 4000,1100,1500,2000,2500,3000,2600
1100 REM " --- Warehouses"
1110 READ (SHM10_DEV)IOL=SHM10F
1120 IF L+7>L9 THEN GOSUB 5000
1130 PRINT (7)""
1140 PRINT (7)W0$(4,2),@(6),W1$(1,30),@(38),FNF$(W1$(127,10),M5$),@(55),W1$(146,2)
1160 LET W$=W1$(31,96)+W1$(137,9)
1170 CALL "SYC.AA",W$,24,4,9,30
1180 FOR X=1 TO 150 STEP 30
1190 IF FNP$(W$(X,30))="" THEN GOTO 1220
1200 PRINT (7)@(6),W$(X,30)
1210 LET L=L+1
1220 NEXT X
1490 GOTO 3900
1500 REM " --- Carriers"
1510 READ (SHM10_DEV)IOL=SHM10D
1515 LET L=L+8
1520 IF L>L9 THEN GOSUB 5000
1530 PRINT (7)""
1540 PRINT (7)"Carrier: ",D0$(4,1)," ",D1$(1,30),@(58),"Type: ",D1$(112,1),@(68),"Installed: ",D1$(97,1)
1550 PRINT (7)""
1560 PRINT (7)"       Sample Manifest: ",D1$(91,6),@(55),"Lookup Fkey: ",D1$(35,2)
1570 PRINT (7)"              Manifest: ",D1$(47,6),@(62),"Text: ",D1$(66,5)
1580 PRINT (7)"      Reprint Manifest: ",D1$(119,6),@(44),"Default Shipper Number: ",D1$(37,10)
1590 PRINT (7)"Default Shipping Label: ",D1$(71,6),@(38),"Default Manifest Book Number: ",D1$(54,12)
1600 PRINT (7)"     Default COD Label: ",D1$(77,6),@(41),"Use Manifest Summary Book: ",D1$(53,1)
1610 PRINT (7)""
1620 PRINT (7)J$
1990 GOTO 3900
2000 REM " --- Services"
2010 DIM D0$(4),D1$(128),D[4]
2020 READ (SHM10_DEV)IOL=SHM10A
2030 LET D0$(1)=N0$+"D"+B1$(1,1)
2040 FIND (SHM10_DEV2,KEY=D0$,DOM=2050)IOL=SHM10D
2100 REM 
2105 LET L=L+13
2110 IF L>L9 THEN GOSUB 5000
2120 PRINT (7)""
2130 PRINT (7)"Service: ",B0$(4,7)," ",B1$(2,24),@(45),"Carrier: ",B1$(1,1)," ",D1$(1,24)
2140 PRINT (7)""
2150 PRINT (7)"      Package Entry: ",B1$(61,6),@(44),"Opt Charges Maintenance Screen: ",B1$(28,1)
2160 PRINT (7)"    Display Charges: ",B1$(67,6),@(62),"Service Type: ",B1$(79,1)
2170 PRINT (7)"   Optional Charges: ",B1$(55,6),@(52),"Transactional manifest: ",B1$(26,1)
2180 PRINT (7)"Freight Calculation: ",B1$(30,6),@(59),"Weighing Method: ",B1$(48,1)
2190 PRINT (7)" Zone Determination: ",B1$(49,6),@(63),"Weight Mask: ",STR(B[0])
2200 PRINT (7)@(51),"Intrastate Restrictions: ",B1$(73,1)
2210 PRINT (7)"         Rate Chart: ",B1$(36,6),@(51),"Intrastate Weight Limit: ",STR(B[1])
2220 PRINT (7)"         Zone Chart: ",B1$(42,6),@(55),"Delivery Commitment: ",B1$(80,2)
2230 PRINT (7)""
2240 PRINT (7)J$
2490 GOTO 3900
2500 REM " --- Service Defaults"
2510 READ (SHM10_DEV)IOL=SHM10G
2520 GOSUB 6000
2550 LET ID$="D"
2590 GOTO 3100
2600 REM " --- Program Options"
2610 READ (SHM10_DEV)IOL=SHM10G
2620 GOSUB 6000
2630 LET ID$="P"
2690 GOTO 3100
3000 REM " --- Optional Charges"
3010 READ (SHM10_DEV)IOL=SHM10C
3020 GOSUB 6000
3030 LET ID$=B1$(28,1)
3100 REM " --- Convert record information"
3110 LET VARIABLE$="A",SERVICE$=K$(4)
3120 CALL "SHC.FA",SHM03_DEV,ID$,SERVICE$,COMMAND$,N0$,VARLEN,I1$,VARIABLE$
3130 IF LEN(COMMAND$)=0 THEN GOTO 3900
3140 DIM A$(VARLEN),PRINT$[24](80)
3150 EXECUTE "3160 "+COMMAND$
3200 REM " --- Convert to print buffer"
3210 CALL "SHC.FE",SHM03_DEV,ID$,SERVICE$,A$,N0$,I1$,I5,0,PRINT$[ALL],L0,L5
3220 IF QUADRANT>3 THEN GOSUB 5500
3230 LET QUADRANT=QUADRANT+1,L=2,X=24
3235 IF QUADRANT>2 THEN LET X=17
3240 LET QUADRANT$[2,QUADRANT]="   Service ID: "+SERVICE$+" "+B1$(2,X)
3250 FOR X=L0 TO L5
3260 LET L=L+1,X$=PRINT$[X](25)
3270 IF LEN(X$)>40 THEN LET X$=X$(1,40)
3280 LET QUADRANT$[L,QUADRANT]=X$
3290 NEXT X
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF QUADRANT>0 THEN GOSUB 5500
4020 IF L>L9 THEN GOSUB 5000
4030 LET X$=STR(NUMBER)
4040 IF NUMBER=0 THEN LET X$="No"
4050 PRINT (7)""
4060 PRINT (7)X$," ",TITLE$
4900 REM " --- Run next overlay"
4910 LET A0$=I0$,A1$=I1$
4990 RUN O0$,ERR=9900
5000 REM " --- Heading"
5010 LET L=HEADINGS
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5090 RETURN 
5500 REM " --- Print page of defaults or optional charges"
5510 GOSUB 5000
5520 LET MAXQUAD=1
5530 IF QUADRANT>1 THEN LET MAXQUAD=2
5540 FOR X=1 TO MAXQUAD
5550 FOR L=1 TO 24
5560 IF X>1 THEN IF QUADRANT$[L,X]="" AND QUADRANT$[L,X+2]="" THEN GOTO 5580
5570 PRINT (7)QUADRANT$[L,X],@(40),QUADRANT$[L,X+2]
5580 NEXT L
5590 NEXT X
5900 REM 
5910 DIM QUADRANT$[24,4]
5920 LET QUADRANT=0,L=0
5990 RETURN 
6000 REM " --- Retrieve service"
6010 DIM B0$(10),B1$(128),B[4]
6020 LET B0$(1)=N0$+"A"+K$(4)
6030 FIND (SHM10_DEV2,KEY=B0$,DOM=6040)IOL=SHM10A
6090 RETURN 
8000 REM " --- Function Definitions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "WO - Schedule: Days Forecast Ahead/Behind (Public/Called)"
0020 REM "Program WOC.AA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "30Oct98 WGH: Close WOE-32 channel (9970-9975)
0032 REM "31Oct98 WGH: Zero hour operations not scheduled correctly (1960,3040)
0040 REM 
0050 REM "WO_NUM$   : Work Order Number                           (Entered)
0051 REM "V0$       : From Date 'YYMMDD' or Packed 'YMD'          (Entered)
0052 REM "V1$       : Calculated Date                             (Returned)
0053 REM "METHOD$   : Scheduling Method, F=Foreward, B=Backward   (Entered)
0054 REM "OPCODE_DEV: Channel WOM-02 or BMM-08 is open on         (Entered)
0055 REM "STATUS$   : ?0 = Successful Calculation                 (Returned)
0056 REM "          : ?1 = Operations Calendar Found Unscheduled Date
0057 REM "          : ?2 = Invalid From Date
0058 REM "          : ?3 = File Missing
0059 REM "          : ?4 = Invalid Scheduling Method
0060 REM "          : ?5 = Purchase Calendar Found Unsceduled Date
0061 REM "          : ?6 = Undetermined error
0062 REM "          : 1? = Date Required changed for a Subcontract with
0063 REM "          :      a non-received associated Purch. Ord/Req.
0070 SETERR 9000
0075 SETESC 9000
0080 ENTER WO_NUM$,V0$,V1$,METHOD$,OPCODE_DEV,STATUS$
0090 LET STATUS$="03"
0100 REM " --- Open Files"
0105 LET FILES=8
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOM-04",FILES$[2]="SYS-01",FILES$[3]="WOE-02"
0120 LET FILES$[4]="WOE-32",FILES$[5]="WOE-03",FILES$[6]="WOM-05"
0125 LET FILES$[7]="WOM-06",FILES$[8]="POM-01"
0150 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOM04_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2],WOE02_DEV=CHANNELS[3]
0165 LET WOE32_DEV=CHANNELS[4],WOE03_DEV=CHANNELS[5],WOM05_DEV=CHANNELS[6]
0170 LET WOM06_DEV=CHANNELS[7]
0200 REM " --- IOLISTs"
0210 WOM04A: IOLIST A0$,A1$,A2$,A[ALL]
0220 WOE02A: IOLIST B0$,B1$,B[ALL]
0225 WOE32A: IOLIST B0$,B1$,B[ALL]
0230 OPCODE: IOLIST C0$,C[ALL]
0235 WOM06A: IOLIST SCHEDSORT$
0240 POM01A: IOLIST L0$,L1$,L0
0245 WOE03D: IOLIST OPNSSORT$
0250 WOM05A: IOLIST W0$,W1$(1),W2$(1),W[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 WOS01A: IOLIST X$,X$,X$,P9$
0425 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2)
0440 FIND (SYS01_DEV,KEY=N0$+"WO00",DOM=9800)IOL=WOS01A
0500 REM " --- Init Data"
0510 LET V1$=V0$,FROM_DATE$=V0$
0520 LET STATUS$(2,1)="0",DIRECTION$=METHOD$
0530 LET V$=V0$
0540 DIM A[31],B[13],C[13],W1$(20),W2$(20),W[9],SCHEDSORT$(18),OPNSSORT$(21)
0550 LET PASS=1
0560 IF LEN(V0$)=3 THEN LET ORIG_DATE$=FNYY_YY21$(FNM$(FNC$(V0$))),V0_LEN=3 ELSE LET ORIG_DATE$=V0$,V0_LEN=6
0570 IF METHOD$="B" THEN LET END_DATE$=ORIG_DATE$
0600 REM " --- Open Additional Files"
0610 IF P9$(3,1)<>"Y" THEN GOTO 0800
0620 CALL "SYC.DA",1,8,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0625 IF STATUS THEN GOTO 9900
0630 LET POM01_DEV=CHANNELS[8]
0800 REM " --- Fix Data"
0820 IF LEN(V$)=3 THEN LET V$=FNYY_YY21$(FNM$(FNC$(V$))),V1$=V$
0850 REM " --- Check Validity of Data"
0860 LET X$="",X0=0
0870 CALL "SYC.CA",V$,X$,X0
0880 IF V$="N" THEN LET STATUS$(2,1)="2"; GOTO 4100
0890 IF POS(DIRECTION$="BF")=0 THEN LET STATUS$(2,1)="4"; GOTO 4100
0900 REM " --- Clear Schedule Detail Files"
0910 READ (WOM06_DEV,KEY=N0$+WO_NUM$,DOM=0920)
0920 LET K$=KEY(WOM06_DEV,END=1000)
0930 IF POS(N0$+WO_NUM$=K$)<>1 THEN GOTO 1000
0940 REMOVE (WOM06_DEV,KEY=K$)
0950 REMOVE (WOM05_DEV,KEY=K$(1,2)+K$(13,6)+K$(3,10),DOM=0960)
0960 GOTO 0920
1000 REM " --- Capture Subcontract Lead Time information"
1010 IF P9$(3,1)<>"Y" THEN GOTO 1800
1020 READ (WOE32_DEV,KEY=N0$+"  "+WO_NUM$+"C",DOM=1030)
1030 REM " --- Read Loop"
1040 LET K$=KEY(WOE32_DEV,END=1200)
1050 IF POS(N0$+"  "+WO_NUM$+"C"=K$)<>1 THEN GOTO 1200
1060 READ (WOE32_DEV)IOL=WOE32A
1070 IF B1$(91,1)<>"S" THEN GOTO 1030
1080 LET SUBS$=SUBS$+B1$(53,3)+B0$(13,3)+STR(B[5]:"000")+"      "
1090 GOTO 1030
1200 REM " --- Set Required Date in Subcontracts for Forward method"
1210 IF DIRECTION$<>"F" THEN GOTO 1800
1220 LET F_YR=FNYY_YEAR(V$(1,2)),F_MTH=NUM(V$(3,2)),F_DAY=NUM(V$(5,2)),OCC=1
1230 LET LEADPOS=POS("   "=SUBS$,15,OCC)
1240 IF LEADPOS=0 THEN GOTO 1800
1250 LET LEAD=NUM(SUBS$(LEADPOS+6,3))
1260 FIND (POM01_DEV,KEY=N0$+FNYEAR_YY21$(F_YR)+STR(F_MTH:"00"),DOM=1550)IOL=POM01A
1270 FOR X=F_DAY TO L0
1280 IF LEAD<=0 AND L1$(X,1)="W" THEN EXITTO 1500
1290 IF L1$(X,1)="W" THEN LET LEAD=LEAD-1
1300 LET F_DAY=F_DAY+1
1310 NEXT X
1320 IF LEAD<=0 AND L1$(L0,1)="W" AND F_DAY<=L0 THEN GOTO 1500
1330 LET F_MTH=F_MTH+1,F_DAY=1
1340 IF F_MTH>12 THEN LET F_MTH=1,F_YR=F_YR+1
1350 GOTO 1260
1500 REM " --- Save date for Subcontracts without op seq and try again"
1510 LET SUBS$(LEADPOS+9,6)=STR(F_MTH:"00")+STR(F_DAY:"00")+FNYEAR_YY21$(F_YR)
1520 LET V1$=FNYEAR_YY21$(F_YR)+STR(F_MTH:"00")+STR(F_DAY:"00"),V$=V1$
1530 LET OCC=OCC+1
1540 GOTO 1230
1550 LET STATUS$(2,1)="5"
1560 GOTO 4100
1800 REM " --- Get Operation record from WOE-02
1810 LET FIRST_TIME$="Y"
1820 IF DIRECTION$="F" THEN READ (WOE02_DEV,KEY=N0$+"  "+WO_NUM$,DOM=1830)
1830 IF DIRECTION$<>"F" THEN READ (WOE02_DEV,KEY=N0$+"  "+WO_NUM$+"B",DOM=1840)
1840 REM " --- Read Loop
1850 IF DIRECTION$="F" THEN LET K$=KEY(WOE02_DEV,END=2300) ELSE LET K$=KEYP(WOE02_DEV,END=2300)
1860 IF POS(N0$+"  "+WO_NUM$=K$)<>1 THEN GOTO 2300
1870 IF K$(12,1)<>"A" THEN GOTO 2300
1880 GOSUB 4500
1890 IF B1$(91,1)="M" THEN GOTO 2070
1900 LET OP_CODE$=B1$(1,3)
1910 DIM C[13]
1920 FIND (OPCODE_DEV,KEY=N0$+OP_CODE$,DOM=1930)IOL=OPCODE
1930 LET V=B[2]+B[9]+C[3]
1940 REM " --- Calculate Date
1950 LET ODATE$=B1$(4,3)
1960 IF DIRECTION$="F" THEN LET V=C[3],ELE=0; IF V>=0 THEN GOSUB 6000; REM "Checking for >=0 forces re-initialization required if all B[] vars here are zero.
1970 IF DIRECTION$="F" THEN LET V=B[8],ELE=1; IF V>0 THEN GOSUB 6020
1980 IF DIRECTION$="F" THEN LET V=B[2]-B[8],ELE=2; IF V>0 THEN GOSUB 6020
1990 IF DIRECTION$="F" THEN LET V=B[9],ELE=3; IF V>0 THEN GOSUB 6015
2000 IF DIRECTION$="B" THEN GOSUB 5000
2010 LET V1$=FNYEAR_YY21$(F_YR)+STR(F_MTH:"00")+STR(F_DAY:"00"),V$=V1$
2020 IF STATUS$(2,1)<>"0" THEN GOTO 4100
2030 LET OPNSSORT$=B0$(1,2)+"D"+B0$(3,2)+B1$(1,3)+ODATE$+B0$(5,7)+B0$(13,3)
2040 REMOVE (WOE03_DEV,KEY=OPNSSORT$,DOM=2050)
2050 LET OPNSSORT$=B0$(1,2)+"D"+B0$(3,2)+B1$(1,6)+B0$(5,7)+B0$(13,3)
2060 WRITE (WOE03_DEV,KEY=OPNSSORT$)IOL=WOE03D
2070 WRITE (WOE02_DEV,KEY=K$)IOL=WOE02A
2080 IF DIRECTION$="B" THEN GOSUB 4500
2090 GOTO 1840
2300 REM " --- Set Required Date in Subcontracts for Backward method"
2310 IF DIRECTION$<>"B" THEN GOTO 3000
2320 LET F_YR=FNYY_YEAR(V$(1,2)),F_MTH=NUM(V$(3,2)),F_DAY=NUM(V$(5,2)),OCC=1
2330 LET LEADPOS=POS("   "=SUBS$,-15,OCC)
2340 IF LEADPOS=0 THEN GOTO 3000
2350 LET LEAD=NUM(SUBS$(LEADPOS+6,3))
2360 FIND (POM01_DEV,KEY=N0$+FNYEAR_YY21$(F_YR)+STR(F_MTH:"00"),DOM=2750)IOL=POM01A
2370 IF F_DAY<1 THEN GOTO 2450
2380 IF F_DAY>L0 THEN LET F_DAY=L0
2390 FOR X=F_DAY TO 1 STEP -1
2400 IF LEAD<=0 AND L1$(X,1)="W" THEN EXITTO 2700
2410 IF L1$(X,1)="W" THEN LET LEAD=LEAD-1
2420 LET F_DAY=F_DAY-1
2430 NEXT X
2440 IF LEAD<=0 AND F_DAY>0 THEN GOTO 2700
2450 LET F_MTH=F_MTH-1,F_DAY=31
2460 IF F_MTH<1 THEN LET F_MTH=12,F_YR=F_YR-1
2470 GOTO 2360
2700 REM " --- Save date for Subcontracts without op seq and try again"
2710 LET SUBS$(LEADPOS+9,6)=STR(F_MTH:"00")+STR(F_DAY:"00")+FNYEAR_YY21$(F_YR)
2720 LET V1$=FNYEAR_YY21$(F_YR)+STR(F_MTH:"00")+STR(F_DAY:"00"),V$=V1$
2730 LET OCC=OCC+1
2740 GOTO 2330
2750 LET STATUS$(2,1)="5"
2760 GOTO 4100
3000 REM " --- Rewrite Subcontract records with operation sequences"
3010 IF P9$(3,1)<>"Y" THEN GOTO 4000
3020 IF LEN(SUBS$)=0 OR DIRECTION$="B" THEN GOTO 4000
3030 FOR X=1 TO LEN(SUBS$) STEP 15
3040 EXTRACT (WOE32_DEV,KEY=N0$+"  "+WO_NUM$+"C"+SUBS$(X+3,3),DOM=3080)IOL=WOE32A
3050 IF B1$(4,3)<>"   " AND FND$(SUBS$(X+9,6))<>B1$(4,3) AND POS(B1$(52,1)="RP")>0 THEN LET STATUS$(1,1)="1"
3060 LET B1$(4,3)=FND$(SUBS$(X+9,6))
3070 WRITE (WOE32_DEV,KEY=N0$+"  "+WO_NUM$+"C"+SUBS$(X+3,3))IOL=WOE32A
3080 NEXT X
4000 REM " --- Done - Successful"
4005 IF LEN(V1$)=0 THEN LET V1$=V0$
4010 IF V0_LEN=3 THEN LET V1$=FND$(FNN$(V1$))
4015 IF DIRECTION$="B" THEN LET DIRECTION$="F",RET_DATE$=V1$,V$=V1$,USED_HRS=0,SUBS$=""; GOTO 0800
4020 IF METHOD$="F" THEN GOTO 4070
4025 IF ORIG_DATE$>=V$ THEN GOTO 4065
4030 LET V0$=""
4035 CALL "SYC.CA",END_DATE$,V0$,-1
4040 LET DIRECTION$="B",V1$=V0$,V$=V0$,END_DATE$=V0$,PASS=PASS+1
4045 IF PASS>99 THEN LET STATUS$(2,1)="6"; GOTO 9900
4050 IF PASS=2 THEN CALL "SYC.WA",0,12,3,35,18,"",PASS_NAME$
4055 PRINT @(1,0),'SB',"Pass: ",'SF',PASS:"#0",
4060 GOTO 0800
4065 LET V1$=RET_DATE$
4070 LET V0$=FROM_DATE$
4075 GOTO 9950
4100 REM " --- Done - Unsuccessful"
4110 GOTO 9900
4500 REM " --- Read backwards allowing for different language platforms
4510 EXTRACT (WOE02_DEV,KEY=K$)IOL=WOE02A
4590 RETURN 
5000 REM " --- Calculate Past Date"
5010 LET F_YR=FNYY_YEAR(V$(1,2)),F_MTH=NUM(V$(3,2)),F_DAY=NUM(V$(5,2)),OCC=1
5020 IF P9$(3,1)<>"Y" THEN GOTO 5300
5030 LET LEADPOS=POS(K$(13,3)=SUBS$,-15,OCC)
5040 IF LEADPOS=0 THEN GOTO 5300
5045 LET SUBS$(LEADPOS+9,6)=STR(F_MTH:"00")+STR(F_DAY:"00")+FNYEAR_YY21$(F_YR)
5050 LET LEAD=NUM(SUBS$(LEADPOS+6,3))
5060 FIND (POM01_DEV,KEY=N0$+FNYEAR_YY21$(F_YR)+STR(F_MTH:"00"),DOM=5950)IOL=POM01A
5065 IF F_DAY>L0 THEN LET F_DAY=L0
5070 FOR X=F_DAY TO 1 STEP -1
5080 IF LEAD<=0 AND L1$(X,1)="W" THEN EXITTO 5200
5090 IF L1$(X,1)="W" THEN LET LEAD=LEAD-1
5100 LET F_DAY=F_DAY-1
5110 NEXT X
5130 LET F_MTH=F_MTH-1,F_DAY=31
5140 IF F_MTH<1 THEN LET F_MTH=12,F_YR=F_YR-1
5150 GOTO 5060
5200 REM " --- Save Date for Subcontracts and try again
5210 LET OCC=OCC+1
5220 GOTO 5030
5300 REM " --- Do Operations"
5305 IF B1$(91,1)="M" THEN GOTO 5490
5310 FIND (WOM04_DEV,KEY=N0$+OP_CODE$+FNYEAR_YY21$(F_YR)+STR(F_MTH:"00"),DOM=5900)IOL=WOM04A
5320 FOR X=F_DAY TO 1 STEP -1
5330 IF A[X]<0 THEN EXITTO 5900
5335 IF A[X]>0 AND USED_HRS>A[X] THEN GOTO 5350
5340 IF A[X]>0 THEN IF USED_HRS+V>=A[X] THEN LET V=V-(A[X]-USED_HRS),USED_HRS=USED_HRS+(A[X]-USED_HRS) ELSE LET USED_HRS=USED_HRS+V,V=0
5350 IF V<=0 THEN IF A[X]>0 THEN LET F_DAY=X; EXITTO 5480
5360 LET USED_HRS=0
5390 NEXT X
5400 LET X$=""
5410 CALL "SYC.CA",FNYEAR_YY21$(F_YR)+STR(F_MTH:"00")+"01",X$,-1
5420 LET F_YR=FNYY_YEAR(X$(1,2)),F_MTH=NUM(X$(3,2)),F_DAY=NUM(X$(5,2))
5430 GOTO 5310
5480 LET B1$(4,3)=FND$(FNN$(V$))
5490 RETURN 
5900 REM " --- Operation Date Not Scheduled"
5910 LET STATUS$(2,1)="1"
5940 RETURN 
5950 REM " --- Purchase Date Not Scheduled"
5960 LET STATUS$(2,1)="5"
5990 RETURN 
6000 REM " --- Calculate Future Date"
6010 LET F_YR=FNYY_YEAR(V$(1,2)),F_MTH=NUM(V$(3,2)),F_DAY=NUM(V$(5,2)),OCC=1
6015 IF B1$(91,1)="M" THEN GOTO 6200
6020 FIND (WOM04_DEV,KEY=N0$+OP_CODE$+FNYEAR_YY21$(F_YR)+STR(F_MTH:"00"),DOM=6500)IOL=WOM04A
6030 IF A[0]=0 THEN GOTO 6500
6040 FOR X=F_DAY TO A[0]
6045 IF A[X]<0 THEN EXITTO 6500
6046 IF A[X]>0 AND USED_HRS>A[X] THEN GOTO 6065
6050 IF A[X]>0 THEN GOSUB 6600
6055 IF A[X]>0 THEN IF USED_HRS+V>=A[X] THEN LET V=V-(A[X]-USED_HRS),USED_HRS=USED_HRS+(A[X]-USED_HRS) ELSE LET USED_HRS=USED_HRS+V,V=0
6065 LET B1$(4,3)=FND$(FNN$(V$))
6070 IF V<=0 THEN LET F_DAY=X,B1$(4,3)=FND$(STR(F_MTH:"00")+STR(F_DAY:"00")+FNYEAR_YY21$(F_YR)); EXITTO 6200
6075 LET USED_HRS=0
6077 LET FIRST_TIME$="N"
6080 NEXT X
6100 LET F_MTH=F_MTH+1,F_DAY=1
6110 IF F_MTH>12 THEN LET F_YR=F_YR+1,F_MTH=1
6120 GOTO 6020
6200 REM " --- Do Subcontracts"
6210 IF P9$(3,1)<>"Y" OR ELE<>3 THEN GOTO 6490
6220 LET LEADPOS=POS(K$(13,3)=SUBS$,15,OCC)
6230 IF LEADPOS=0 THEN GOTO 6490
6240 LET LEAD=NUM(SUBS$(LEADPOS+6,3))
6250 FIND (POM01_DEV,KEY=N0$+FNYEAR_YY21$(F_YR)+STR(F_MTH:"00"),DOM=6550)IOL=POM01A
6260 FOR X=F_DAY TO L0
6270 IF LEAD<=0 AND L1$(X,1)="W" THEN EXITTO 6400
6280 IF L1$(X,1)="W" THEN LET LEAD=LEAD-1
6290 LET F_DAY=F_DAY+1
6300 NEXT X
6310 IF LEAD<=0 AND L1$(L0,1)="W" AND F_DAY<=L0 THEN GOTO 6400
6320 LET F_MTH=F_MTH+1,F_DAY=1
6330 IF F_MTH>12 THEN LET F_MTH=1,F_YR=F_YR+1
6340 GOTO 6250
6400 REM " --- Save Date for Subcontracts and try again
6410 LET SUBS$(LEADPOS+9,6)=STR(F_MTH:"00")+STR(F_DAY:"00")+FNYEAR_YY21$(F_YR)
6420 LET OCC=OCC+1
6430 GOTO 6220
6490 RETURN 
6500 REM " --- Date Not Scheduled"
6510 LET STATUS$(2,1)="1"
6540 RETURN 
6550 REM " --- Purchase Date Not Scheduled"
6560 LET STATUS$(2,1)="5"
6590 RETURN 
6600 REM " --- Write Schedule Detail Files
6610 LET W0$=N0$+OP_CODE$+FND$(STR(F_MTH:"00")+STR(X:"00")+FNYEAR_YY21$(F_YR))+WO_NUM$+B0$(13,3)
6620 DIM W[9]
6630 IF USED_HRS+V>A[X] THEN LET Z=A[X]-USED_HRS ELSE LET Z=V
6640 EXTRACT (WOM05_DEV,KEY=W0$,DOM=6650)IOL=WOM05A
6650 LET W[ELE]=W[ELE]+Z
6660 WRITE (WOM05_DEV,KEY=W0$)IOL=WOM05A
6670 LET SCHEDSORT$=W0$(1,2)+W0$(9,10)+W0$(3,6)
6680 WRITE (WOM06_DEV,KEY=SCHEDSORT$)IOL=WOM06A
6690 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8080 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8090 DEF FNN$(Q$)=Q$(3,4)+Q$(1,2)
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 LET STATUS$(2,1)="6"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Exit - Unsuccessful"
9920 LET V0$="N",V1$="N"
9930 IF STATUS$(2,1)="0" THEN LET STATUS$(2,1)="6"
9950 REM " --- Return to caller"
9955 IF LEN(P9$)>=3 THEN IF P9$(3,1)="Y" THEN CLOSE (POM01_DEV,ERR=9960); LET POM01_DEV=0
9960 CLOSE (WOM04_DEV,ERR=9965); LET WOM04_DEV=0
9965 CLOSE (SYS01_DEV,ERR=9970); LET SYS01_DEV=0
9970 CLOSE (WOE02_DEV,ERR=9975); LET WOE02_DEV=0
9975 CLOSE (WOE32_DEV,ERR=9980); LET WOE32_DEV=0
9980 CLOSE (WOE03_DEV,ERR=9985); LET WOE03_DEV=0
9985 CLOSE (WOM05_DEV,ERR=9990); LET WOM05_DEV=0
9990 CLOSE (WOM06_DEV,ERR=9992); LET WOM06_DEV=0
9992 CALL "SYC.WD",PASS_NAME$
9995 EXIT 
9999 END

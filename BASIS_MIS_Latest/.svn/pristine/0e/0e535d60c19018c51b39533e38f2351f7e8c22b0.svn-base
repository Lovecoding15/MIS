0010 REM "APE - INVOICE ENTRY"
0020 REM "Program APE.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "04Dec97 KJS: Can't do Retentions on a Cash Basis (5570)"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0220 APM01A: IOLIST B0$,B1$
0230 APM09A: IOLIST K0$,K1$
0510 LET L1=L0
0520 LET M0=LEN(M0$),M1=LEN(M1$),G6=LEN(G6$)
0720 CALL "SYC.WC",1,0,80,0,0,13,0
0900 PRINT 'CF',
0910 PRINT @(0,15),'CE'
1000 REM " --- Vendor
1010 GOSUB 6000
1020 LET V2$=A0$(5,P[0])
1030 IF LEN(V2$)<4 THEN GOTO 1050
1040 IF V2$(1,4)="TEMP" THEN LET V2$=""
1050 LET V0$="M",V1$="ECK",V3$=P1$,V0=6,V1=15,V2=3,V4$="Valid Vendor Number   <F2>=Temporary Vendor   <F3>=Lookup"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 9900
1075 IF V3=2 THEN GOTO 1400
1080 IF V3=3 THEN v$="";GOTO 3000
1090 IF V$="" THEN GOTO 1000
1100 LET A0$(5)=V$
1110 if num(v$,err=*next) then goto 1200
1120 goto 3000
1200 REM " --- Find Key
1220 FIND (APM01_DEV,KEY=N0$+A0$(5,6),DOM=1000)IOL=APM01A
1300 REM " --- Display
1310 GOSUB 5000
1320 PRINT 'CF',
1330 READ (APM09_DEV,KEY=B0$+P3$(22,2),DOM=1350)
1340 GOTO 1380
1350 LET APM09_K$=KEY(APM09_DEV,END=1390)
1360 IF POS(B0$=APM09_K$)<>1 THEN GOTO 1390
1370 IF NUM(P3$(24,2))<>0 OR APM09_K$(9,2)>P3$(24,2) THEN GOTO 1390
1380 CALL "SYC.CM","W",B0$(1,2),B0$(3,6),P3$(22,2),P3$(24,2),APM09_DEV,48,12,1,11
1390 GOTO 4000
1400 REM " --- New Record
1410 PRINT @(15,3),'CL',
1420 CALL "SYC.YN",1,"Is This A Temporary Vendor?",2,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-up
1520 PRINT 'SB',@(15,3),"Temporary",'CL','SF',
2000 REM " --- Temp Vendor
2020 LET O0=1,O1=0
2100 RUN "APE.AF"
3100 REM " --- Alpha Search
3140 CALL "SYC.LV",V$
3160 IF V$="" THEN GOTO 1000
3170 LET X$=FNF$(V$(1,P[0]),M0$)
3180 PRINT X$
3190 GOTO 1100
3900 GOSUB 6400
3990 GOTO 1000
4000 REM " --- Invoice
4020 LET O0=1,O1=0
4100 RUN "APE.AC"
5000 REM " --- Display
5020 PRINT 'SB',@(15,3),FNF$(A0$(5,P[0]),M0$),@(15,4),B1$(1,30),'SF',
5090 RETURN 
5500 REM " --- Background
5505 CALL "SYC.WC",1,0,80,0,0,5,0
5510 PRINT 'SB',
5520 IF P3$(1,1)="Y" THEN PRINT @(6,6),"AP Type:",
5530 PRINT @(3,7),"Invoice No:",
5535 IF P3$(2,1)="Y" THEN PRINT @(4,8),"Dist Code:",
5540 PRINT @(4,9),"Pmt Group:",@(3,10),"Terms Code:",@(9,11),"Hold:",
5550 PRINT @(23,8),"Invoice Date:",@(25,9),"Acctg Date:",@(27,10),"Due Date:",@(22,11),"Discount Date:",
5560 PRINT @(44,6),"Total Invoice Amt:",J1$(1,M1+1),@(46,7),"Net Invoice Amt:",@(47,8),"Adjustment Amt:",@(49,9),"Discount Amt:",@(52,11),"Reference:",@(57,12),"Memo:",
5570 IF P3$(3,1)="Y" THEN PRINT @(46,10),"Retention Amt:",
5580 CALL "SYC.WC",1,0,80,0,0,13,0
5585 PRINT 'SB',@(0,14),"Seq",@(5),"Account",@(47+M1),"Amount",
5590 IF G4$(3,1)="Y" THEN PRINT @(64+G7),"Units",
5595 IF P3$(16,1)="Y" THEN PRINT @(20),"Miscellaneous",
5600 PRINT 'SF',
5690 RETURN 
6000 REM " --- Clear Screen
6020 PRINT @(15,6),J1$(1,25),@(15,4),'CL',@(15,3),'CL',
6090 RETURN 
6200 REM " --- Clear Lower Screen
6220 PRINT @(0,L1),'CE',
6290 RETURN 
6400 REM " --- Full Screen
6410 PRINT 'RB',
6420 LET V0$="S",V1$="EC",V2$="",V3$="",V4$="Press <Enter> To Continue",V0=1,V1=FNV(V4$),V2=22
6430 GOSUB 7000
6450 LET L=L1
6490 RETURN 
6500 REM " --- Comments
6510 LET L2=15
6515 PRINT 'SB',
6520 READ (APM09_DEV,KEY=N0$+A0$(5,6),DOM=6530)
6530 LET K$=KEY(APM09_DEV,END=6590)
6535 IF K$(1,8)<>A0$(1,2)+A0$(5,6) THEN GOTO 6590
6540 READ (APM09_DEV,KEY=K$)IOL=APM09A
6550 IF L2>20 THEN GOTO 6600
6560 PRINT @(14,L2),">",K1$,"<"
6570 LET L2=L2+1
6580 GOTO 6530
6590 PRINT 'SF',
6595 RETURN 
6600 REM " --- Comments Continued
6620 LET V0$="S",V1$="C",V2$="",V3$="",V4$="<Enter> For More Comments:",V0=1,V1=FNV(V4$),V2=22
6630 GOSUB 7000
6640 IF V3=4 THEN GOTO 6690
6650 PRINT @(0,15),'CE'
6660 LET L2=15
6670 GOTO 6560
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- F4/End Option for Locked Record
9220 IF ERR=0 THEN GOTO 0001
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

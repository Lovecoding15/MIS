0010 REM "POS - Replenishment Selection Data Entry
0020 REM "Program:  POE.FA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Oct96 CAJ: Program rewritten for 6.0.2 replenishment
0032 REM "29May99 WGH: Can't enter single character warehouse code (2505,2715)"
0040 REM 
0070 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=16
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="APM-01",FILES$[2]="APM-06",FILES$[3]="IVM-01"
0130 LET FILES$[4]="IVM-02",FILES$[5]="IVM-03",FILES$[6]="IVM-03"
0135 LET FILES$[7]="IVM-05",FILES$[8]="IVM-10",FILES$[9]="IVM-12"
0140 LET FILES$[10]="POE-06",FILES$[11]="POE-07",FILES$[12]="POE-16"
0145 LET FILES$[13]="POE-17",FILES$[14]="POE-26",FILES$[15]="POE-36"
0150 LET FILES$[16]="SYS-01"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET APM01_DEV=CHANNELS[1],APM06_DEV=CHANNELS[2],IVM01_DEV=CHANNELS[3]
0180 LET IVM02_DEV=CHANNELS[4],IVM03_DEV=CHANNELS[5],IVM03_DEV2=CHANNELS[6]
0185 LET IVM05_DEV=CHANNELS[7],IVM10_DEV=CHANNELS[8],IVM12_DEV=CHANNELS[9]
0190 LET POE06_DEV=CHANNELS[10],POE07_DEV=CHANNELS[11],POE16_DEV=CHANNELS[12]
0195 LET POE17_DEV=CHANNELS[13],POE26_DEV=CHANNELS[14],POE36_DEV=CHANNELS[15]
0197 LET SYS01_DEV=CHANNELS[16]
0200 REM " --- IOLists
0240 APM01A: IOLIST X0$,X1$(1)
0260 IVM10C: IOLIST X1$(1)
0270 IVM10F: IOLIST X0$(1)
0290 POE07A: IOLIST W0$(1),W1$(1)
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 APS01A: IOLIST U0$,VENDIMASK$,U2$,U3$,U4$,VENDOMASK$
0330 GLS01A: IOLIST G0$,G1$,G2$,G3$
0340 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0350 IVS01C: IOLIST *,ABCP[ALL],FACTORS[ALL]
0360 POS01A: IOLIST P0$,P1$,P2$,P3$,P4$
0400 REM " --- Parameters
0405 DIM INFO$[20],DESC[3],DESCLEN[3],ABCP[25],FACTORS[25]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=POS01A
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 LET VENDLEN=NUM(U2$(1,2))
0435 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET IVPRECISION=NUM(I2$(5,1)),ITEMLEN=NUM(I2$(1,2))
0445 LET CURPER$=I4$(1,2),CURYR$=I4$(3,2),CURPER=NUM(CURPER$),CURYR=FNYY_YEAR(CURYR$)
0450 FOR X=1 TO 3
0455 LET DESCLEN[X]=NUM(I2$((X-1)*2+9,2))
0460 NEXT X
0465 CALL "SYC.PA",SYS01_DEV,CURPER,CURYR,BEGPER$,ENDPER$,STATUS
0470 LET DAYOFMONTH=NUM(F0$(22,2))
0475 FIND (SYS01_DEV,KEY=N0$+"IV02",DOM=9800)IOL=IVS01C
0490 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0495 LET TOT_PERS=NUM(G2$(1,2))
0500 REM " --- Init Data
0510 PRECISION IVPRECISION
0530 DIM COMMENT$(30),W0$(4),W1$(75)
0540 LET TERMDATE$=F0$(7,3),DATETHRU$=TERMDATE$
0700 REM " --- Background
0760 GOSUB REFRESH
0900 REM " --- Check for previous selections
0910 LET I0=0
0920 READ (POE07_DEV,KEY=N0$,DOM=0930,END=NOPREV)
0930 LET K$=KEY(POE07_DEV,END=NOPREV)
0940 IF POS(N0$=K$)<>1 THEN GOTO NOPREV
0950 GOTO 1000
0960 NOPREV: 
0970 LET W0$=N0$+"01"
0990 GOTO 3900
1000 REM " --- Clear previous selections?
1010 LET V0$="S",V1$="^",V2$="N",V3$="",V4$="Clear All Previous Selections For Firm (Y/N)?",V0=1,V1=40,V2=3,I0=0
1020 GOSUB 7000
1030 ON V3 GOTO 1060,1060,1060,1060,4000
1060 IF V$<>"Y" THEN GOTO 1100
1070 GOSUB CLEARTHEM
1100 REM " --- Get next replenishment seq #
1105 LET FIRST$=N0$+"00",LAST$=N0$+$FF$,V3=0,K$=""
1110 READ (POE07_DEV,KEY=LAST$,DOM=1120)
1120 LET K$=KEYP(POE07_DEV,END=1130)
1130 IF POS(N0$=K$)<>1 THEN LET K$=FIRST$
1140 IF K$(3,2)<"99" THEN GOTO 1170
1145 DIM MESSAGE$[1]
1150 LET MESSAGE$[0]="No more selections can be made for this firm until you run the"
1155 LET MESSAGE$[1]="Replenishment Register and then Clear All Previous Selections."
1160 CALL "SYC.XA",0,MESSAGE$[ALL],1,-1,-1,V$,V3
1165 GOTO 9900
1170 LET K$(3,2)=STR(NUM(K$(3,2))+1:"00")
1180 LET W0$=K$,W1$(1)=""
1190 GOTO 3900
1200 REM " --- Beginning Buyer Code
1210 LET V0$="S",V1$="KCE",V2$=BUYERFROM$,V3$="",V4$="Enter A Valid Buyer Code (<F1>=First/<F3>=Lookup)",V0=3,V1=40,V2=4
1220 GOSUB 7000
1230 ON V3 GOTO 1290,1240,1390,1270,4000
1240 LET BUYERFROM$=""
1250 PRINT @(V1,V2),'CL',@(V1,V2),"First",
1260 GOTO 1390
1270 GOSUB KODELOOKUP
1280 IF V$="" THEN GOTO 1200
1290 IF V$="" THEN GOTO 1240
1295 LET V$=V$+FILL(V0-LEN(V$))
1300 DIM X0$(38),BUYERFNAME$(20)
1310 FIND (IVM10_DEV,KEY=N0$+"F"+V$,DOM=1200)IOL=IVM10F
1320 LET BUYERFROM$=V$,BUYERFNAME$(1)=X0$(7,20)
1330 PRINT @(V1,V2),BUYERFROM$," ",BUYERFNAME$,'CL',
1390 GOTO 3900
1400 REM " --- Ending Buyer Code
1410 LET V0$="S",V1$="KCE",V2$=BUYERTHRU$,V3$="",V4$="Enter A Valid Buyer Code (<F1>=Last/<F3>=Lookup)",V0=3,V1=40,V2=5
1420 GOSUB 7000
1430 ON V3 GOTO 1490,1440,1590,1470,4000
1440 LET BUYERTHRU$=""
1450 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
1460 GOTO 1590
1470 GOSUB KODELOOKUP
1480 IF V$="" THEN GOTO 1400
1490 IF V$="" THEN GOTO 1440
1495 LET V$=V$+FILL(V0-LEN(V$))
1500 IF BUYERFROM$<>"" THEN IF V$<BUYERFROM$ THEN GOTO 1400
1510 DIM X0$(38),BUYERTNAME$(20)
1520 FIND (IVM10_DEV,KEY=N0$+"F"+V$,DOM=1400)IOL=IVM10F
1530 LET BUYERTHRU$=V$,BUYERTNAME$(1)=X0$(7,20)
1540 PRINT @(V1,V2),BUYERTHRU$," ",BUYERTNAME$,'CL',
1590 GOTO 3900
1600 REM " --- Beginning Vendor
1610 LET V0$="M",V1$="KCE",V2$=VENDFROM$,V3$=VENDIMASK$,V4$="Enter A Valid Vendor Number (<F1>=First/<F3>=Lookup)",V0=6,V1=40,V2=6
1620 GOSUB 7000
1630 ON V3 GOTO 1690,1640,1790,1670,4000
1640 LET VENDFROM$=""
1650 PRINT @(V1,V2),'CL',@(V1,V2),"First",
1660 GOTO 1790
1670 CALL "SYC.LV",V$
1680 IF V$="" THEN GOTO 1600
1690 IF V$="" THEN GOTO 1640
1700 DIM X1$(195),VENDFNAME$(30)
1710 FIND (APM01_DEV,KEY=N0$+V$,DOM=1600)IOL=APM01A
1720 LET VENDFROM$=V$,VENDFNAME$(1)=X1$(1,30)
1730 PRINT @(V1,V2),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
1790 GOTO 3900
1800 REM " --- Ending Vendor
1810 LET V0$="M",V1$="KCE",V2$=VENDTHRU$,V3$=VENDIMASK$,V4$="Enter A Valid Vendor Number (<F1>=Last/<F3>=Lookup)",V0=6,V1=40,V2=7
1820 GOSUB 7000
1830 ON V3 GOTO 1890,1840,1990,1870,4000
1840 LET VENDTHRU$=""
1850 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
1860 GOTO 1990
1870 CALL "SYC.LV",V$
1880 IF V$="" THEN GOTO 1800
1890 IF V$="" THEN GOTO 1840
1900 IF VENDFROM$<>"" THEN IF V$<VENDFROM$ THEN GOTO 1800
1910 DIM X1$(195),VENDTNAME$(30)
1920 FIND (APM01_DEV,KEY=N0$+V$,DOM=1800)IOL=APM01A
1930 LET VENDTHRU$=V$,VENDTNAME$(1)=X1$(1,30)
1940 PRINT @(V1,V2),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
1990 GOTO 3900
2000 REM " --- Beginning Review Date
2010 LET V0$="D",V1$="",V2$=DATEFROM$,V3$="",V4$="Enter Beginning Review Date (<F1>=First/<F3>=Terminal Date)",V0=3,V1=40,V2=8
2020 GOSUB 7000
2030 ON V3 GOTO 2090,2040,2190,2070,4000
2040 LET DATEFROM$=""
2050 PRINT @(V1,V2),"First   ",
2060 GOTO 2190
2070 LET DATEFROM$=TERMDATE$
2080 GOTO 2110
2090 IF V$="" THEN GOTO 2040
2100 LET DATEFROM$=V$
2110 PRINT @(V1,V2),FNB$(DATEFROM$)
2190 GOTO 3900
2200 REM " --- Ending Review Date
2210 LET V0$="D",V1$="",V2$=DATETHRU$,V3$="",V4$="Enter The Review Through Date (<F3>=Terminal Date)",V0=3,V1=40,V2=9
2220 GOSUB 7000
2230 ON V3 GOTO 2290,2270,2390,2270,4000
2270 IF TERMDATE$<DATEFROM$ THEN GOTO 2200
2275 LET DATETHRU$=TERMDATE$
2280 GOTO 2320
2290 IF V$="" THEN GOTO 2270
2300 IF V$<>"" THEN IF V$<DATEFROM$ THEN GOTO 2200
2310 LET DATETHRU$=V$
2320 PRINT @(V1,V2),FNB$(DATETHRU$)
2390 GOTO 3900
2400 REM " --- Warehouse From
2410 LET V0$="S",V1$="",V2$=WHSEFROM$,V3$="",V4$="Enter A Valid Warehouse ID  (<F1>=First/<F3>=Lookup)",V0=2,V1=40,V2=10
2420 GOSUB 7000
2430 ON V3 GOTO 2490,2440,2590,2470,4000
2440 LET WHSEFROM$=""
2450 PRINT @(V1,V2),'CL',@(V1,V2),"First",
2460 GOTO 2590
2470 CALL "IVC.LK",3,V1,V2,V$
2480 IF V$="" THEN GOTO 2400
2490 IF V$="" THEN GOTO 2440
2500 DIM X1$(160),WHSEFNAME$(24)
2505 LET V$=V$+FILL(2),V$=V$(1,2)
2510 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=2400)IOL=IVM10C
2520 LET WHSEFROM$=V$,WHSEFNAME$(1)=X1$(6,24)
2530 PRINT @(V1,V2),FNF$(WHSEFROM$,"00")," ",WHSEFNAME$,'CL',
2590 GOTO 3900
2600 REM " --- Warehouse Thru
2610 LET V0$="S",V1$="",V2$=WHSETHRU$,V3$="",V4$="Enter A Valid Warehouse ID  (<F1>=Last/<F3>=Lookup)",V0=2,V1=40,V2=11
2620 GOSUB 7000
2630 ON V3 GOTO 2690,2640,2790,2670,4000
2640 LET WHSETHRU$=""
2650 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
2660 GOTO 2790
2670 CALL "IVC.LK",3,V1,V2,V$
2680 IF V$="" THEN GOTO 2600
2690 IF V$="" THEN GOTO 2640
2700 IF WHSEFROM$<>"" AND V$<WHSEFROM$ THEN GOTO 2600
2710 DIM X1$(160),WHSETNAME$(24)
2715 LET V$=V$+FILL(2),V$=V$(1,2)
2720 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=2600)IOL=IVM10C
2730 LET WHSETHRU$=V$,WHSETNAME$(1)=X1$(6,24)
2740 PRINT @(V1,V2),FNF$(WHSETHRU$,"00")," ",WHSETNAME$,'CL',
2790 GOTO 3900
2900 REM " --- Comment
2910 LET V0$="S",V1$="",V2$=COMMENT$,V3$="",V4$="Enter Comments Regarding This Selection",V0=30,V1=40,V2=12
2920 GOSUB 7000
2930 ON V3 GOTO 2940,2940,2950,2940,4000
2940 LET COMMENT$(1)=V$
2950 GOTO 3900
3900 REM " --- Next Field
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,2200,2400,2600,2900,4000
4000 REM " --- Correct
4010 GOSUB REFRESH
4020 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22,I0=1
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4060 ON POS(V$="YN") GOTO 4000,4100,1200
4100 REM " --- Write selection data to POE-07
4120 IF VENDFROM$<>"" THEN LET W1$(1,6)=VENDFROM$
4130 IF VENDTHRU$<>"" THEN LET W1$(7,6)=VENDTHRU$
4140 IF BUYERFROM$<>"" THEN LET W1$(13,3)=BUYERFROM$
4150 IF BUYERTHRU$<>"" THEN LET W1$(16,3)=BUYERTHRU$
4160 IF DATEFROM$<>"" THEN LET W1$(19,3)=DATEFROM$
4170 IF DATETHRU$<>"" THEN LET W1$(22,3)=DATETHRU$
4180 LET W1$(26,30)=COMMENT$
4190 IF WHSEFROM$<>"" THEN LET W1$(56,2)=WHSEFROM$
4200 IF WHSETHRU$<>"" THEN LET W1$(58,2)=WHSETHRU$
4220 WRITE (POE07_DEV,KEY=W0$(1,4))IOL=POE07A
4250 REM " --- Run Selection Update
4260 LET REPLEN_SEQ$=W0$(3,2)
4290 RUN "POE.FB"
5000 REM " --- Refresh screen data
5005 REFRESH: 
5010 PRINT 'SB',@(13,3),"Clear Previous Selections:",@(18,4),"Beginning Buyer Code:",@(21,5),"Ending Buyer Code:",@(22,6),"Beginning Vendor:",@(25,7),"Ending Vendor:",@(17,8),"Beginning Review Date:",@(20,9),"Ending Review Date:",@(19,10),"Beginning Warehouse:",@(22,11),"Ending Warehouse:",@(31,12),"Comment:",
5020 PRINT 'CF',
5030 IF BUYERFROM$="" THEN PRINT @(40,4),"First" ELSE PRINT @(40,4),BUYERFROM$," ",BUYERFNAME$,'CL',
5040 IF BUYERTHRU$="" THEN PRINT @(40,5),"Last" ELSE PRINT @(40,5),BUYERTHRU$," ",BUYERTNAME$,'CL',
5050 IF VENDFROM$="" THEN PRINT @(40,6),"First",'CL', ELSE PRINT @(40,6),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5060 IF VENDTHRU$="" THEN PRINT @(40,7),"Last",'CL', ELSE PRINT @(40,7),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5080 IF DATEFROM$="" THEN PRINT @(40,8),"First   ", ELSE PRINT @(40,8),FNB$(DATEFROM$)
5090 IF DATETHRU$="" THEN PRINT @(40,9),"Last    ", ELSE PRINT @(40,9),FNB$(DATETHRU$)
5100 IF WHSEFROM$="" THEN PRINT @(40,10),"First" ELSE PRINT @(40,10),WHSEFROM$," ",WHSEFNAME$,'CL',
5110 IF WHSETHRU$="" THEN PRINT @(40,11),"Last" ELSE PRINT @(40,11),WHSETHRU$," ",WHSETNAME$,'CL',
5130 PRINT @(40,12),COMMENT$,'CL'
5190 RETURN 
6000 REM " --- Code Lookup"
6005 KODELOOKUP: 
6010 LET RECORD$="F",NUMBER=1,TITLE$="Buyer Codes"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3
6050 LET FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
6060 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6070 LET V$="SKIP",ROW=V2+2
6080 CALL "SYC.LK",IVM10_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
6100 REM " --- Clear selections
6110 CLEARTHEM: 
6120 CALL "SYC.NB","Clearing .....",1,COLUMN
6130 READ (POE06_DEV,KEY=N0$,DOM=CLEARPOE06)
6140 CLEARPOE06: 
6150 LET K06$=KEY(POE06_DEV,END=INITPOE07)
6160 IF POS(N0$=K06$)<>1 THEN GOTO INITPOE07
6170 REMOVE (POE06_DEV,KEY=K06$)
6180 GOTO CLEARPOE06
6200 REM " --- POE-07
6210 INITPOE07: 
6220 READ (POE07_DEV,KEY=N0$,DOM=CLEARPOE07)
6230 CLEARPOE07: 
6240 LET K07$=KEY(POE07_DEV,END=INITPOE16)
6250 IF POS(N0$=K07$)<>1 THEN GOTO INITPOE16
6260 REMOVE (POE07_DEV,KEY=K07$)
6270 GOTO CLEARPOE07
6300 REM " --- POE-16
6310 INITPOE16: 
6320 READ (POE16_DEV,KEY=N0$,DOM=CLEARPOE16)
6330 CLEARPOE16: 
6340 LET K16$=KEY(POE16_DEV,END=INITPOE17)
6350 IF POS(N0$=K16$)<>1 THEN GOTO INITPOE17
6360 REMOVE (POE16_DEV,KEY=K16$)
6370 GOTO CLEARPOE16
6400 REM " --- POE-17
6410 INITPOE17: 
6420 READ (POE17_DEV,KEY=N0$,DOM=CLEARPOE17)
6430 CLEARPOE17: 
6440 LET K17$=KEY(POE17_DEV,END=INITPOE26)
6450 IF POS(N0$=K17$)<>1 THEN GOTO INITPOE26
6460 REMOVE (POE17_DEV,KEY=K17$)
6470 GOTO CLEARPOE17
6500 REM " --- POE-26
6510 INITPOE26: 
6520 READ (POE26_DEV,KEY=N0$,DOM=CLEARPOE26)
6530 CLEARPOE26: 
6540 LET K26$=KEY(POE26_DEV,END=INITPOE36)
6550 IF POS(N0$=K26$)<>1 THEN GOTO INITPOE36
6560 REMOVE (POE26_DEV,KEY=K26$)
6570 GOTO CLEARPOE26
6600 REM " --- POE-36
6610 INITPOE36: 
6620 READ (POE36_DEV,KEY=N0$,DOM=CLEARPOE36)
6630 CLEARPOE36: 
6640 LET K26$=KEY(POE36_DEV,END=DONECLEAR)
6650 IF POS(N0$=K26$)<>1 THEN GOTO DONECLEAR
6660 REMOVE (POE36_DEV,KEY=K26$)
6670 GOTO CLEARPOE36
6680 DONECLEAR: 
6690 PRINT @(0,10),'CL',@(0,11),'CL',@(0,12),'CL'
6700 GOSUB REFRESH
6790 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

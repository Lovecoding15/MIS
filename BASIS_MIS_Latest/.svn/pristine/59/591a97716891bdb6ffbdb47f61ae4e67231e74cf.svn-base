0010 REM "WOE - WO Issues Entry (Header Overlay)"
0020 REM "Program WOE.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "19Jun96 WGH: Prevent possible PRECISION error (460)"
0032 REM "26Mar97 JJD: When previous issue is deleted and committment is retained, Issue Complete needs to reset WOE-25 detail file (3065)"
0033 REM "11Jun97 WGH: Zero cost for non-stock items (52,3095,3435-3440)"
0034 REM "12Jun97 WGH: Production Quantity Issued default entry only works for work order processed in sequence (2075,4040-4110)"
0040 REM 
0045 REM "O9$="0" - New Issue,  "1" -Recall Existing Issue
0050 SETERR 9000
0052 SETESC 9000
0055 PRECISION IVS01_PRECISION
0060 ON O9 GOTO 0070,1000
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=16
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-15",FILES$[2]="WOE-25",FILES$[3]="WOE-01"
0120 LET FILES$[4]="IVM-01",FILES$[5]="WOE-04",FILES$[6]="SYS-01"
0125 LET FILES$[7]="IVM-02",FILES$[8]="WOE-13",FILES$[9]="WOE-23"
0130 LET FILES$[10]="WOE-22",FILES$[11]="WOM-10",FILES$[12]="WOE-02"
0135 LET FILES$[16]="WOM-02"
0150 CALL "SYC.DA",1,1,12,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE15_DEV=CHANNELS[1],WOE25_DEV=CHANNELS[2],WOE01_DEV=CHANNELS[3]
0165 LET IVM01_DEV=CHANNELS[4],WOE04_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0170 LET IVM02_DEV=CHANNELS[7],WOE13_DEV=CHANNELS[8],WOE23_DEV=CHANNELS[9]
0175 LET WOE22_DEV=CHANNELS[10],WOM10_DEV=CHANNELS[11],WOE02_DEV=CHANNELS[12]
0200 REM " --- IOLISTs"
0205 WOE04D: IOLIST S0$
0210 WOE15A: IOLIST A0$,A1$
0220 WOE25A: IOLIST W0$,W1$,W[ALL]
0230 WOE23A: IOLIST R0$,R1$,R[ALL]
0240 WOE01A: IOLIST D0$,D1$,D[ALL]
0250 IVM01A: IOLIST B0$,B1$(1),B2$,B3$,B4$,B5$,B6$,B[ALL]
0260 WOS01A: IOLIST X$,P1$,P2$,P9$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 IVS01A: IOLIST X$,X$,P2$,P3$
0290 BMS01A: IOLIST *,*,M4$,M5$,M4,M5
0300 ARS01A: IOLIST *,*,X$,*,*,P7$
0310 WOE22A: IOLIST MAT0$,MAT1$
0320 WOM10A: IOLIST X1$
0330 IVM02A: IOLIST C0$,C1$,C2$,C[ALL]
0400 REM " --- Parameters"
0405 DIM P[1],C[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N4$=F5$
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0425 LET BM$=P9$(1,1),AR$=P9$(2,1),PO$=P9$(3,1)
0430 IF BM$<>"Y" THEN GOTO 0440
0435 FIND (SYS01_DEV,KEY=N0$+"BM00",DOM=0440)IOL=BMS01A
0440 IF AR$<>"Y" THEN GOTO 0455
0445 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=0455)IOL=ARS01A
0450 LET P7=NUM(X$(1,2))
0455 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0460 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(5,1)),IVS01_PRECISION=P[1]
0465 LET P0=P[0],P1=P[1],P2=NUM(P2$(7,2))
0470 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$)
0475 LET GL$="N",STATUS=0,SOURCE$=PGM(-2)
0480 CALL "GLC.BA",ERR=0485,SOURCE$,N2$,GLW11$,GL$,STATUS
0485 IF STATUS<>0 THEN GOTO 9900
0500 REM " --- Init Data"
0505 PRECISION IVS01_PRECISION
0520 DIM A0$(75),A1$(110),A[5],B1$(60),R[4],W[4],B[8],D[11],DESC[3]
0530 LET K0=1
0540 LET A0$(1)=N0$
0550 FOR X=1 TO 3
0555 LET DESC[X]=NUM(P2$((X-1)*2+9,2)),DESCLEN=DESCLEN+DESC[X]
0560 NEXT X
0600 REM " --- Additional File Opens "
0610 IF POS(P3$(17,1)="LS")=0 THEN GOTO 0630
0620 LET FILES$[13]="IVM-07",FILES$[14]="WOE-14",FILES$[15]="IVM-17"
0630 IF BM$="Y" THEN LET FILES$[16]="BMM-08"
0640 CALL "SYC.DA",1,13,16,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0650 IF STATUS THEN GOTO 9900
0660 LET IVM07_DEV=CHANNELS[13],WOE14_DEV=CHANNELS[14],IVM17_DEV=CHANNELS[15]
0670 LET OPCODE_DEV=CHANNELS[16]
0700 REM " --- Background"
0710 FOR X=7 TO 9 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(2,3),"WO Number:",@(21),"Type:",@(55),"Status:",@(0,5),"Bill Number:",@(0,6),"Description:",@(53,4),"Category:",@(52,5),"Warehouse:",@(58,6),"U/M:",@(1,8),"Issue Date:",'SF',
0900 REM " --- Initialize File "
0990 READ (WOE15_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(WOE15_DEV,END=1050)
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF LEN(V2$)>5 THEN LET V2$=V2$(5,7) ELSE LET V2$=""
1050 LET V0$="Z",V1$="CE",V3$="",V0=7,V1=13,V2=3,V4$="Enter Work Order (<F3>=Lookup)"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 9900
1080 IF V3=3 THEN GOSUB 6600
1180 IF V$="" OR POS("0"<>V$)=0 THEN GOTO 0900
1190 LET A0$(5)=V$,O9$="0"
1200 REM " --- Find Work Order Information "
1205 FIND (WOE01_DEV,KEY=A0$(1,11),DOM=1000)IOL=WOE01A
1210 GOSUB 5000
1215 IF POS(D0$(15,1)="C")=0 THEN GOTO 1240
1220 DIM MESS$[1]
1225 LET MESS$[0]="This Work Order Is Closed! (<Enter>=Continue)"
1230 CALL "SYC.XA",0,MESS$[ALL],0,22,-1,V$,V3
1235 GOTO 1000
1240 IF POS(D0$(15,1)="PQ")=0 THEN GOTO 1265
1245 DIM MESS$[1]
1247 LET MESS$[0]="Work Order Has Not Been Released. You Must Release It"
1250 LET MESS$[1]="From Work Order Entry (<Enter>=Continue)"
1255 CALL "SYC.XA",0,MESS$[ALL],1,22,-1,V$,V3
1260 GOTO 1000
1265 READ (WOE15_DEV,KEY=A0$(1,11)+"000",DOM=1400)IOL=WOE15A
1270 PRINT @(13,8),FNB$(A1$(7,3)),
1300 REM " --- Process Valid Work Order "
1310 FIND (WOE04_DEV,KEY=A0$(1,4)+"D"+A0$(5,7),DOM=1350)
1320 LET O9$="1"
1340 GOTO 4000
1350 FIND (WOE04_DEV,KEY=A0$(1,4)+"C"+A0$(5,7),DOM=2000)
1355 DIM MESS$[1]
1360 LET MESS$[0]="Pick List Has Not Been Printed (<Enter>=Continue)"
1370 CALL "SYC.XA",0,MESS$[ALL],0,22,-1,V$,V3
1380 GOTO 2000
1400 REM " --- New Entry???
1410 LET V4$="Is This A New Record"
1420 CALL "SYC.YN",1,V4$,1,V$,V3
1430 IF V$="N" THEN GOTO 4080
1440 IF V$<>"Y" THEN GOTO 1410
1500 REM " --- Write Header
1510 DIM A1$(70)
1520 LET A1$(1)=D0$(12,4),A1$(49,22)=D0$(54,22),A0$=D0$(1,11)+"000"
1530 LET A1$(7,3)=F0$(7,3)
1540 WRITE (WOE15_DEV,KEY=A0$)IOL=WOE15A
1600 REM " --- Write Details To Material Issues File "
1605 READ (WOE23_DEV,KEY=A0$,DOM=1610)
1610 LET K13$=KEY(WOE23_DEV,END=1700)
1615 IF POS(A0$(1,11)=K13$)<>1 THEN GOTO 1700
1620 READ (WOE23_DEV)IOL=WOE23A
1625 LET W0$=R0$
1630 LET W1$=R1$
1635 LET W[ALL]=R[ALL]
1640 LET W1$(7,3)=R0$(12,3)
1645 WRITE (WOE25_DEV,KEY=R0$)IOL=WOE25A
1650 GOTO 1610
1700 REM " --- Reset Pointer"
1710 READ (WOE25_DEV,KEY=A0$,DOM=2000)
2000 REM " --- Issue Date "    
2010 IF POS(" "<>A1$(7,3))=0 THEN LET A1$(7,3)=F0$(7,3)
2020 LET V0$="D",V1$="CRE",V2$=A1$(7,3),V3$="",V4$="Enter Issue Date (<Enter>="+FNB$(V2$)+")",V0=3,V1=13,V2=8
2030 GOSUB 7000
2040 IF V3=4 THEN GOTO 1000
2050 GOSUB 6900
2060 IF STATUS>99 THEN GOTO 2000
2070 LET A1$(7,3)=V$
2075 GOTO 4100
3000 REM " --- Issue All???
3005 READ (WOE15_DEV,KEY=A0$(1,11),DOM=3010)
3010 LET K$=KEY(WOE15_DEV,END=3530)
3020 IF K$(1,11)<>A0$(1,11) THEN GOTO 3530
3030 LET V4$="Was This Work Order Pulled Complete"
3040 CALL "SYC.YN",1,V4$,1,V$,V3
3050 IF V$="N" THEN GOTO 3300
3060 IF V$<>"Y" THEN GOTO 3000
3065 READ (WOE25_DEV,KEY=K$,DOM=3070)
3070 LET K$=KEY(WOE25_DEV,END=3530)
3080 IF K$(1,11)<>A0$(1,11) THEN GOTO 3530
3090 READ (WOE25_DEV)IOL=WOE25A
3095 DIM C[20]
3100 FIND (IVM02_DEV,KEY=N0$+W1$(19,22),DOM=3110)IOL=IVM02A
3110 LET W[3]=W[0]-W[1],W[4]=C[11]
3120 PRINT @(30,22),"Now Issuing Step ",W0$(12,3)
3130 WRITE (WOE25_DEV,KEY=W0$)IOL=WOE25A
3140 GOTO 3070
3300 REM " --- Partial Issue "    
3305 GOSUB 6000
3310 IF OPSEQ$="NONE" THEN GOTO 3530
3315 IF D[0]-D[2]=0 THEN GOTO 3530
3320 LET V4$="Enter Portion Of Scheduled Production Quantity Issued (<F3>="+STR(D[0]-D[2])+"):"
3325 LET V0$="S",V1$="C",V2$="",V3$="",V0=6,V1=FNV(V4$),V2=22
3330 GOSUB 7000
3335 IF V3=3 THEN LET V$=STR(D[0]-D[2])
3340 LET I=NUM(V$,ERR=3320)
3345 IF I=0 THEN GOTO 3530
3350 IF I<=D[0]-D[2] THEN GOTO 3400
3355 PRINT @(5,22),'RB',"Are You Sure ",I," Is Right?  Only ",D[0]-D[2]," Are In Production? (Y/N): "
3360 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="",V0=1,V1=65+LEN(STR(I))+LEN(STR(D[0]-D[2])),V2=22
3365 GOSUB 7000
3370 IF V$="N" THEN GOTO 3315
3400 REM " --- Calculate Quantities     
3410 LET K$=KEY(WOE25_DEV,END=3530)
3420 IF K$(1,11)<>A0$(1,11) THEN GOTO 3530
3430 READ (WOE25_DEV)IOL=WOE25A
3435 DIM C[20]
3440 FIND (IVM02_DEV,KEY=N0$+W1$(19,22),DOM=3450)IOL=IVM02A
3450 IF OPSEQ$="" THEN GOTO 3480
3460 FIND (WOE22_DEV,KEY=N0$+"  "+A0$(5,7)+"B"+W1$(7,3),DOM=3400)IOL=WOE22A
3470 IF MAT1$(47,3)<>OPSEQ$ THEN GOTO 3400
3480 IF D[0]=0 THEN LET D[0]=1
3490 LET W[3]=W[0]*I/D[0],W[4]=C[11]
3500 PRINT @(30,22),"Now Issuing Step ",W0$(12,3)
3510 WRITE (WOE25_DEV,KEY=W0$)IOL=WOE25A
3520 GOTO 3400
3530 READ (WOE25_DEV,KEY=A0$,DOM=3540)
3540 RETURN 
4000 REM " --- Run Overlay
4020 LET V4$="Is This The Correct Work Order"
4030 CALL "SYC.YN",1,V4$,1,V$,V3
4040 IF V$="Y" OR V$="YES" THEN GOTO 4100
4080 READ (WOE25_DEV,KEY=A0$(1,11),DOM=4090)
4090 GOTO 1000
4100 REM " --- Issue Date Correct?"
4110 READ (WOE25_DEV,KEY=A0$(1,11),DOM=4120)
4120 LET V4$="Is The Above Issue Date Correct"
4125 CALL "SYC.YN",1,V4$,2,V$,V3
4130 IF V$="N" THEN GOTO 2000
4135 IF POS(" "<>A1$(7,3))=0 THEN GOTO 2000
4140 WRITE (WOE15_DEV,KEY=A0$)IOL=WOE15A
4150 IF O9$<>"0" THEN GOTO 4190
4160 GOSUB 3000
4170 LET S0$=A0$(1,4)+"D"+A0$(5,7)
4180 WRITE (WOE04_DEV,KEY=S0$)IOL=WOE04D
4190 IF O0=0 THEN GOTO 4900
4200 REM " --- Check For More Issues"
4210 GOSUB 6100
4220 IF ISS$<>"Y" THEN GOTO 4900
4230 GOSUB 6000
4240 IF OPSEQ$="NONE" THEN GOTO 4900
4250 GOSUB 3400
4900 RUN "WOE.DB"
5000 REM " --- Display"
5005 DIM B1$(60)
5010 FIND (IVM01_DEV,KEY=N0$+D0$(56,20),DOM=5020)IOL=IVM01A
5020 DIM X1$(30)
5030 LET X1$(6,20)="** Not On File **",I9$="N"
5040 FIND (WOM10_DEV,KEY=N0$+"A"+D0$(12,2),DOM=5060)IOL=WOM10A
5050 LET I9$=D0$(14,1)
5060 PRINT @(13,3),A0$(5,7),@(27,3),D0$(12,2),"  ",X1$(6,20),@(63),D0$(15,1),@(63,4),D0$(14,1),@(63,5),D0$(54,2),@(63,6),D0$(50,2)
5070 IF I9$="I" THEN PRINT @(65,4),"Inventory",@(13,5),D0$(56) ELSE IF I9$="N" THEN PRINT @(65,4),"Non-Stocking", ELSE IF I9$="R" THEN PRINT @(65,4),"Recurring"
5080 IF I9$<>"I" THEN PRINT @(13,6),D1$(1,30)
5085 IF I9$="I" THEN PRINT @(13,6),B1$(1,MIN(DESC[1],42))
5100 RETURN 
6000 REM " --- Get Operation Sequence to Issue"
6005 LET V0$="Z",V1$="CE",V2$="",V3$="",V4$="Enter Operation Sequence To Issue (<F1>=All/<F3>=Lookup/<F4>=None)",V0=3,V1=FNV(V4$),V2=22
6010 GOSUB 7000
6015 ON V3 GOTO 6055,6035,6000,6020,6045
6020 LET V$=A0$(5,7)
6025 CALL "WOC.DA",V$,SYS01_DEV,OPCODE_DEV,WOE02_DEV
6030 GOTO 6055
6035 LET OPSEQ$=""
6040 GOTO 6065
6045 LET OPSEQ$="NONE"
6050 GOTO 6065
6055 FIND (WOE02_DEV,KEY=N0$+"  "+A0$(5,7)+"A"+V$,DOM=6000)
6060 LET OPSEQ$=V$
6065 RETURN 
6100 REM " --- See if any issued quantities are 0"
6105 LET ISS$=""
6110 LET K$=KEY(WOE25_DEV,END=6135)
6115 IF K$(1,11)<>A0$(1,11) THEN GOTO 6135
6120 READ (WOE25_DEV)IOL=WOE25A
6125 IF W[1]+W[3]=0 THEN LET ISS$="Y"; GOTO 6135
6130 GOTO 6110
6135 READ (WOE25_DEV,KEY=A0$(1,11)+"000")
6140 RETURN 
6600 REM " --- W/O Lookup
6640 CALL "SYC.LW",V$,"O"
6690 RETURN 
6900 REM " --- Date verification"
6910 LET STATUS=0
6920 IF GL$="Y" THEN CALL "GLC.EA",V$,"Y",PERIOD$,YEAR$,STATUS
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

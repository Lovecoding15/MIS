0010 REM "ARR - SIMPLE INVOICE PRINTING/DETAIL"
0020 REM "Program ARR.UB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Aug98 WGH: Prevent Error 47 on Line 5072 when customer not on file (910,1310)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0095 PRECISION 5
0200 REM " --- IOLists
0210 ARE05A: IOLIST A0$,A1$
0230 ARM01A: IOLIST C0$,C1$
0250 ARM10A: IOLIST X2$
0260 ARM10D: IOLIST X1$
0310 ARE15A: IOLIST W0$,W1$,W[ALL]
0500 REM " --- Init Data
0510 LET H0=0,WIDTH=80
0600 REM " -- Printer
0620 CALL "SYC.GA",7,1,"","",STATUS
0625 IF STATUS THEN GOTO 9900
0800 REM " --- Test Pattern
0810 LET V4$="Do You Wish To Print A Test Pattern"
0830 CALL "SYC.YN",1,V4$,1,V$,V3
0840 IF V3=4 THEN GOTO 9900
0860 ON POS(V$="YN") GOTO 0800,0900,1000
0910 DIM A0$(14,"9"),A1$(30,"9"),C1$(305,"X"),X1$(30,"X"),W1$(100,"X"),C9$(P9,"9"),W0$(12,"9")
0920 LET X2$=X1$,X3$=X2$,C1$(103,9)="999999999",W[2]=9999999.99,W1$(1+G[4],6)="999999",W0$(10,3)="999"
0930 LET L=0
0940 GOSUB 5000
0950 FOR X=1 TO 6
0960 GOSUB 5200
0970 NEXT X
0980 GOSUB 5600
0990 GOTO 0800
1000 REM " --- Init File
1020 READ (ARE05_DEV,KEY=N0$,DOM=1030)
1040 PRINT @(0,11),'CL','SB',@(30,11),"Printing: ",'SF',
1050 IF P9$="N" THEN GOTO 1100
1060 READ (ARE05_DEV,KEY=N0$+P8$,DOM=1100)IOL=ARE05A
1070 GOTO 1200
1080 IF P9$="N" THEN GOTO 1100
1100 REM " --- Main Read
1120 READ (ARE05_DEV,END=4000)IOL=ARE05A
1140 IF A0$(1,2)<>N0$ THEN GOTO 4000
1200 REM " --- Break
1220 IF A0$(10,3)<>"000" THEN GOTO 1100
1240 IF A1$(17,1)="V" THEN GOTO 1100
1280 PRINT @(40,11),A0$(3,7),
1290 LET T0=0
1300 REM " --- Get Customer
1305 FOR X0=1 TO X9
1310 DIM C1$(305)
1320 LET C1$(1)="***Not On File***"
1340 FIND (ARM01_DEV,KEY=N0$+A1$(7,6),DOM=1400)IOL=ARM01A
1400 REM " --- Get Dist
1410 LET X1$="**Not On File**"
1420 FIND (ARM10_DEV,KEY=N0$+"D"+A1$(13,2),DOM=1500)IOL=ARM10D
1440 LET X1$=X1$(6,20)
1500 REM " --- Terms
1510 LET X2$="**Not On File**"
1520 FIND (ARM10_DEV,KEY=N0$+"A"+A1$(15,2),DOM=1600)IOL=ARM10A
1540 LET X2$=X2$(6,20)
1560 IF POS("  "=X2$)<>0 THEN LET X2$=X2$(1,POS("  "=X2$))
2000 REM " --- Detail
2010 LET L=0,L1=0,H=0
2090 GOSUB 5000
2100 REM " --- Main Read
2110 READ (ARE15_DEV,KEY=A0$(1,9),DOM=2120)
2120 LET K$=KEY(ARE15_DEV,END=3000)
2140 IF K$(1,9)<>A0$(1,9) THEN GOTO 3000
2160 READ (ARE15_DEV)IOL=ARE15A
2500 REM " --- Print Detail
2520 IF L+2>L9 THEN GOSUB 5800
2540 GOSUB 5200
2560 LET T0=T0+W[2]
2900 GOTO 2120
3000 REM " --- Totals
3020 GOSUB 5600
3030 IF X9>1 AND X9<>X0 THEN READ (ARE05_DEV,KEY=A0$(1,9)+"000",DOM=3033)
3040 LET T0=0
3050 NEXT X0
3090 GOTO 1100
4000 REM " --- End
4100 GOTO 9900
5000 REM " --- Heading"
5002 LET H=H+1,L=HEADINGS+1
5010 PRINT (7)'FF',@(35+H0),"*************",'LF',@(35+H0),"** Invoice **",'LF',@(35+H0),"*************"
5020 PRINT (7)'LF',@(H0),R1$(1,30),@(H0+60),"Page:",@(H0+71),STR(H)
5030 PRINT (7)@(H0),R1$(31,24)
5040 PRINT (7)@(H0),R1$(55,24),@(H0+60),"Date:",@(H0+71),FNB6$(A1$(1,6))
5050 PRINT (7)@(H0),R1$(79,24)
5060 PRINT (7)@(H0+60),"Cust #:",@(H0+71),FNF$(A1$(7,P[0]),M0$)
5070 PRINT (7)'LF',@(H0),"Sold To:",@(H0+10),C1$(1,30),@(H0+60),"Invoice #:",@(H0+71),A0$(3,7)
5071 PRINT (7)@(H0+10),C1$(31,24),'LF',@(H0+10),C1$(55,24),'LF',@(H0+10),C1$(79,24)
5072 IF C1$(179,24)<>FILL(24) THEN PRINT (7)@(H0+10),C1$(179,24); LET L=L+1
5073 IF C1$(203,24)<>FILL(24) THEN PRINT (7)@(H0+10),C1$(203,24); LET L=L+1
5074 IF C1$(103,P[6])>J0$(1,P[6]) THEN PRINT (7)@(H0+10),FNZ$(C1$(103)),
5076 PRINT (7)'LF','LF',@(H0+10),"*** Terms: ",X2$,"***"
5080 PRINT (7)'LF',@(H0),"Seq",@(H0+7),"Date",@(H0+14),"Memo",@(WIDTH-M1-M3-4),"Qty",@(WIDTH-M1-6),"Price",@(WIDTH-10),"Extension"
5085 LET L=L+19
5090 RETURN 
5200 REM " --- Detail
5210 IF W[0]=0 THEN IF W[2]=0 THEN GOTO 5240
5220 PRINT (7)""
5230 LET L=L+1
5240 PRINT (7)@(H0),W0$(10,3),@(H0+5),FNB6$(W1$(1+G[4],6)),@(H0+14),W1$(7+G[4],30),
5250 IF W[0]=0 THEN IF W[2]=0 THEN GOTO 5270
5260 PRINT (7)@(WIDTH-M1-M2-M3),W[0]:M2$,W[1]:M3$,W[2]:M1$,
5270 LET L=L+1
5280 PRINT (7)'LF',
5290 RETURN 
5600 REM " --- Total
5630 PRINT (7)@(WIDTH-M1),J$(1,M1)
5640 PRINT (7)@(WIDTH-M1-M2-M3),"Total:",@(WIDTH-M1),T0:M1$
5650 PRINT (7)'LF',@(H0),P7$
5690 RETURN 
5800 REM " --- Overflow
5820 PRINT (7)'LF',@(WIDTH-M1-M2-M3),"Continued"
5840 GOSUB 5000
5890 RETURN 
6000 REM " --- Balance Forward
6005 DIM T[3]
6010 LET T0=0,T1=0,T2=0,T3=0,T9=0
6020 READ (ARM01_DEV,KEY=B0$,DOM=6030)
6040 READ (ARM01_DEV,END=6190)IOL=ARM01A
6050 IF C0$(1,15)<>B0$ THEN GOTO 6190
6070 ON POS(C0$(22,1)="ABC") GOTO 6040,6080,6100,6040
6080 IF C0$(16,6)>=P8$ THEN GOTO 6040
6086 LET V0=C[2]-C[3]-C[4]-C[5],V0$=C0$(16,6),T9=T9+V0
6088 GOSUB 5600
6090 GOTO 6040
6100 IF C0$(29,6)>=P8$ THEN GOTO 6040
6110 LET V0=(-C[0])-C[1],V0$=C0$(16,6),T9=T9+V0
6120 GOSUB 5600
6180 GOTO 6040
6190 RETURN 
6200 REM " --- Reverse Name
6220 LET V0=POS("  "=V1$),V$=V1$
6230 IF V0=0 THEN IF V1$(LEN(V1$),1)=" " THEN LET V0=LEN(V1$)
6240 IF V0>1 THEN LET V$=V1$(1,V0-1)
6250 LET V$=V$+" "+V0$
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

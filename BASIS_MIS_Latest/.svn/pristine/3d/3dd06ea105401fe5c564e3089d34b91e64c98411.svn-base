0010 REM "SHX - Install Carrier Upgrade"
0020 REM "Program SHX.UA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Feb97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Feb97 CAJ: Modified for 1997 SpeedShip Update"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SYM-03",FILES$[3]="SHM-10"
0160 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1],SYM03_DEV=CHANNELS[2],SHM10_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0240 SYM03A: IOLIST D0$(1),D1$(1)
0250 SHM10D: IOLIST *,I8$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="SH"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)
0500 REM " --- Initializations"
0510 DIM D0$(6),D1$(30),I8$(128),SOURCE$[6],TARGET$[6]
0540 LET SOURCE$[0]="_SH%GONE",F5$="Install Carrier Upgrade"
0550 LET SOURCE$[1]="_SH%MENU",TARGET$[1]="SYM-01"
0555 LET SOURCE$[2]="_SH%FILE",TARGET$[2]="SYM-03"
0560 LET SOURCE$[3]="_SH%DICT",TARGET$[3]="DDM-01"
0565 LET SOURCE$[4]="_SH%RECS",TARGET$[4]="DDM-03"
0570 LET SOURCE$[5]="_SH%LAYS",TARGET$[5]="DDM-04"
0575 LET SOURCE$[6]="_SH%REMS",TARGET$[6]="DDM-05"
0700 REM " --- Background"
0710 PRINT 'SB',@(28,4),"Carrier ID:",@(30,5),"Updating:",'SF',
0800 REM " --- Carrier ID"
0805 LET V0$="S",V1$="CR",V2$="",V3$="",V0=1,V1=40,V2=4
0810 LET V4$="Enter Carrier ID (<F3>=Lookup)"
0815 GOSUB 7000
0820 ON V3 GOTO 0845,0800,0800,0825,9900
0825 CALL "SHC.LK",V1,V2,"D",0,V$
0840 IF V$="" THEN GOTO 8000
0845 FIND (SHM10_DEV,KEY=N0$+"D"+V$,DOM=0800)IOL=SHM10D
0850 LET C$=V$
0855 PRINT @(V1,V2),C$,"  ",I8$(1,30),
0900 REM " --- Correct?"
0910 CALL "SYC.YN",1,"Is The Above Information Correct",1,V$,V3
0920 ON V3 GOTO 0925,0800,0800,0800,9900
0925 ON POS(V$="YN") GOTO 0900,0930,0800
1000 REM " --- For each possible source file"
1010 FOR X=1 TO 6
1020 DIM D0$(6),D1$(30)
1030 LET P=POS("%"=SOURCE$[X]),D0$(1)=TARGET$[X]
1040 IF P>0 THEN LET SOURCE$[X](P,1)=C$
1050 FIND (SYM03_DEV,KEY=D0$,DOM=1060)IOL=SYM03A
1060 PRINT @(40,5),D0$," ",D1$,'CL',
1100 REM " --- Open source and target files"
1110 LET FILES$[4]=SOURCE$[X],FILES$[5]=TARGET$[X],OPTIONS$[4]="R"
1120 CALL "SYC.DA",1,4,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
1130 IF STATUS THEN GOTO 1300
1140 LET SOURCE_DEV=CHANNELS[4],TARGET_DEV=CHANNELS[5]
1200 REM " --- Merge new records"
1210 IF POS("DDM-04"=SOURCE$[X]) THEN GOSUB 6500
1220 GOSUB 6000
1300 REM " --- Close files and erase source file"
1310 CLOSE (SOURCE_DEV,ERR=1320)
1320 CLOSE (TARGET_DEV,ERR=1330)
1340 ERASE SOURCE$[X],ERR=1390
1390 NEXT X
1600 REM " --- Delete any necessary files"
1610 LET P=POS("%"=SOURCE$[0])
1620 IF P>0 THEN LET SOURCE$[0](P,1)=C$
1630 LET FILES$[4]=SOURCE$[0],OPTIONS$[4]="R"
1640 CALL "SYC.DA",1,4,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
1650 IF STATUS THEN GOTO 1900
1700 REM " --- Next record"
1710 LET K$=KEY(SOURCE_DEV,END=1800)
1720 READ (SOURCE_DEV)
1730 ERASE K$,ERR=1740
1790 GOTO 1700
1800 REM 
1810 CLOSE (SOURCE_DEV,ERR=1820)
1890 ERASE SOURCE$[0],ERR=1900
1900 REM " --- Position SYM-03"
1910 DIM D0$(6),D1$(30)
1990 READ (SYM03_DEV,KEY="SH",DOM=2000)
2000 REM " --- Read next SYM-03 Record"
2010 LET K$=KEY(SYM03_DEV,END=3900)
2020 IF POS("SH"=K$)<>1 THEN GOTO 3900
2030 READ (SYM03_DEV)IOL=SYM03A
2040 PRINT @(40,5),D0$," ",D1$
2050 LET FILENAME$="_"+D0$(1,3)+C$+D0$(5)
2100 REM " --- Open source and target files"
2110 LET FILES$[4]=FILENAME$,FILES$[5]=D0$,OPTIONS$[4]="R"
2120 CALL "SYC.DA",1,4,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
2130 LET SOURCE_DEV=CHANNELS[4],TARGET_DEV=CHANNELS[5]
2200 REM " --- Merge new records"
2210 IF STATUS THEN GOTO 2300
2220 LET DOINGSH=1
2230 GOSUB 6000
2300 REM " --- Close files and erase source file"
2310 CLOSE (SOURCE_DEV,ERR=2320)
2320 CLOSE (TARGET_DEV,ERR=2330)
2340 ERASE FILENAME$,ERR=2400
2900 REM " --- Loop back for next record"
2990 GOTO 2000
3900 REM " --- Process completed"
3910 DIM MESSAGE$[1]
3920 LET MESSAGE$[0]="Upgrade Installed For "+FNP$(I8$(1,30))+" (<Enter>=Continue)"
3930 CALL "SYC.XA",0,MESSAGE$[ALL],0,22,-1,V$,V3
4000 REM " --- Exit from upgrade procedure"
4090 GOTO 9900
6000 REM " --- Copy from File 1 to File 2"
6010 PRINT @(0,6),'CE',
6090 LET R=0
6100 REM 
6110 LET K$=KEY(SOURCE_DEV,END=6200)
6120 IF R>0 THEN GOTO 6150
6130 LET R=LEN(K$)
6140 CALL "SYC.NB","Copying",R+1,COLUMN
6150 PRINT @(COLUMN,11),K$,
6160 READ RECORD(SOURCE_DEV)RECORD$
6170 IF DOINGSH AND K$(1,2)="99" THEN LET K$=N0$+K$(3),RECORD$=N0$+RECORD$(3)
6180 WRITE RECORD(TARGET_DEV,KEY=K$)RECORD$
6190 GOTO 6100
6200 REM " --- Transfer completed"
6210 LET DOINGSH=0
6290 RETURN 
6500 REM " --- Remove old record layout before merging new one"
6510 LET X$=KEY(SOURCE_DEV,END=6800)
6520 LET X$=X$(1,7)
6590 READ (TARGET_DEV,KEY=X$,DOM=6600)
6600 REM " --- Remove each data element"
6610 LET K$=KEY(TARGET_DEV,END=6700)
6620 IF POS(X$=K$)<>1 THEN GOTO 6700
6630 REMOVE (TARGET_DEV,KEY=K$)
6690 GOTO 6600
6700 REM " --- Position to next record layout"
6710 READ (SOURCE_DEV,KEY=X$+$FF$,DOM=6500)
6790 GOTO 6500
6800 REM " --- Removal completed"
6810 READ (SOURCE_DEV,KEY="",DOM=6820)
6890 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

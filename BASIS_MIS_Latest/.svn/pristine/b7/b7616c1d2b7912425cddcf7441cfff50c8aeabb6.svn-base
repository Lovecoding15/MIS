0010 REM "POC - Print Calendar"
0020 REM "Program POC.CP"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "SYS01_DEV: SYS-01 channel number (input)"
0050 REM "POM01_DEV: POM-01 channel number (input)"
0055 REM "PARAMS[n]: Beginning/ending month/year (returned)"
0065 REM "STATUS   : 0=No error (returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER SYS01_DEV,POM01_DEV,PARAMS[ALL],STATUS
0100 REM " --- Open/Lock Files"
0110 CALL "SYC.GA",7,1,"","",STATUS
0120 IF STATUS THEN GOTO 9900
0200 REM " --- IOLISTs"
0210 POM01A: IOLIST A0$(1),A1$(1),A0
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),WHEN$=F0$(7,3),HEADINGS=2,WIDTH=80,L9=46
0500 REM " --- Initializations"
0510 DIM L[12],HEADINGS$[HEADINGS],A0$(6),A1$(31)
0520 DIM PAGE$[L9],BUF$(WIDTH),MILESTONE$(14)
0540 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$,CLOCK$="",L=0
0550 LET DAYS$="Sunday   Monday   Tuesday  WednesdayThursday Friday   Saturday "
0560 LET CODES$="           Closed    Holiday   Work Day "
0565 LET L[1]=31,L[2]=28,L[3]=31,L[4]=30,L[5]=31,L[6]=30
0570 LET L[7]=31,L[8]=31,L[9]=30,L[10]=31,L[11]=30,L[12]=31
0575 LET BEGMONTH=PARAMS[0],BEGYEAR=PARAMS[1]
0580 LET ENDMONTH=PARAMS[2],ENDYEAR=PARAMS[3]
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",14,COLUMN
0800 REM " --- Get Last Day Scheduled"
0810 CALL "POC.CE",POM01_DEV,N0$,BEGDATE$,ENDDATE$,STATUS
0820 LET BEGDATE$=FNYY_YY21$(FNM$(FNC$(BEGDATE$))),ENDDATE$=FNYY_YY21$(FNM$(FNC$(ENDDATE$)))
0830 IF BEGMONTH=0 THEN LET BEGMONTH=NUM(BEGDATE$(3,2))
0840 IF ENDMONTH=0 THEN LET ENDMONTH=NUM(ENDDATE$(3,2))
0850 IF BEGYEAR=0 THEN LET BEGYEAR=FNYY_YEAR(BEGDATE$(1,2))
0860 IF ENDYEAR=0 THEN LET ENDYEAR=FNYY_YEAR(ENDDATE$(1,2))
0870 LET MONTH=BEGMONTH,YEAR=BEGYEAR
0880 LET LAST$=FNYEAR_YY21$(ENDYEAR)+STR(ENDMONTH:"00")
1000 REM " --- Read next record"
1010 LET A0$(1)=N0$+FNYEAR_YY21$(YEAR)+STR(MONTH:"00"),A1$(1)=""
1020 IF A0$(3)>LAST$ THEN GOTO 4000
1030 FIND (POM01_DEV,KEY=A0$,DOM=1040)IOL=POM01A
1100 REM " --- Set calendar title"
1110 LET X$=DATE(JUL(1900+YEAR,MONTH,1):"%Ml %Y"),MILESTONE$(1)=X$
1120 PRINT @(COLUMN,11),MILESTONE$,
1130 LET BUF$(1)="",BUF$(FNC(X$,WIDTH))=X$,L=L+1,NUMBER=NUMBER+1
1140 IF L>L9 THEN GOSUB 5000
1150 LET PAGE$[L]=BUF$
1200 REM " --- Print Days of the Week"
1210 LET BUF$(2)=" "+FILL(76,"-")+" ",P=2,L=L+1,PAGE$[L]=BUF$
1220 FOR X=1 TO 7
1230 LET BUF$(P)="|"+DAYS$((X-1)*9+1,9),P=P+11
1240 NEXT X
1250 LET BUF$(P)="|",L=L+1,PAGE$[L]=BUF$
1260 LET BUF$(2)="|"+FILL(76,"-")+"|",L=L+1,PAGE$[L]=BUF$
1500 REM " --- Determine starting day of the month"
1510 DIM DAYS[42]
1515 LET FIRST$=A0$(3)+"01",X$="",FIRST=0,D=0,DATE=0
1520 CALL "SYC.CB",FIRST$,X$,FIRST
1530 LET FIRST=FIRST+1,L[2]=28
1540 IF FIRST>7 THEN LET FIRST=1
1550 IF MOD(YEAR,4)=0 THEN LET L[2]=29
1560 FOR X=FIRST TO 42
1570 LET DATE=DATE+1
1580 IF DATE<=L[MONTH] THEN LET D=D+1,DAYS[X]=D
1590 NEXT X
1900 REM " --- Display dates on calendar"
1910 LET DATE=1
1920 FOR WEEK=0 TO 5
1930 LET P=2
1940 FOR DOW=1 TO 7
1950 LET BUF$(P)="|",X=WEEK*7+DOW
1960 IF DAYS[X]>0 THEN LET BUF$(P+2)=STR(DAYS[X]:"#0")
1970 LET P=P+11
1980 NEXT DOW
1990 LET BUF$(P)="|",L=L+1,PAGE$[L]=BUF$,P=2
2000 FOR DOW=1 TO 7
2010 LET BUF$(P)="|"
2020 IF DAYS[WEEK*7+DOW]=0 THEN GOTO 2050
2030 LET X$=CODES$((POS(A1$(DATE,1)=" CHW")-1)*10+1,10),BUF$(P+2)=X$
2040 LET BUF$(P+2)=X$,DATE=DATE+1
2050 LET P=P+11
2060 NEXT DOW
2070 LET BUF$(P)="|",L=L+1,PAGE$[L]=BUF$
2080 LET BUF$(2)="|"+FILL(76,"-")+"|",L=L+1,PAGE$[L]=BUF$
2090 NEXT WEEK
3900 REM " --- Loop back for next month"
3910 LET MONTH=MONTH+1
3920 IF MONTH>12 THEN LET MONTH=1,YEAR=YEAR+1
3930 LET L=L+2
3990 GOTO 1000
4000 REM " --- All done"
4010 IF L>0 THEN GOSUB 5000
4020 IF NUMBER>0 THEN GOTO 9900
4030 GOSUB 5000
4040 PRINT (7)"No Dates Scheduled"
4090 GOTO 9900
5000 REM " --- Report heading"
5010 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5020 IF STATUS THEN EXITTO 9900
5030 IF L=0 THEN GOTO 5090
5040 FOR X=1 TO MIN(L,L9)
5050 PRINT (7)PAGE$[X]
5060 NEXT X
5070 DIM PAGE$[L9]
5080 LET L=1
5090 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8035 DEF FNC(Q$,Q)=INT((WIDTH-LEN(Q$))/2)
8080 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9950 EXIT 
9999 END

0010 REM "IVR - Inventory Analysis Report (Report Overlay)"
0020 REM "Program IVR.HB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1)
0220 IVM02A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0230 IVW01A: IOLIST C0$(1),C[ALL]
0235 IVW02A: IOLIST D0$(1),D[ALL]
0240 IVM10A: IOLIST X0$(1)
0250 IVM10C: IOLIST X0$(1)
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM A0$(22),A1$(60),A2$(64),HEADINGS$[6],O[9]
0520 DIM FILES[3],ITEMS$[2],PERIODS$[1],ACTIVITY[12],B0$(24),B1$(64),B[20]
0530 DIM C0$(40),C[2],D0$(4),D[1],WHSE[1],REPORT[1]
0540 LET M7$="-###0.0000%",M8$="###########.0000-",M7=LEN(M7$),M8=LEN(M8$)
0550 LET FILES[2]=IVM12_DEV,FILES[3]=IVM03_DEV2,HEADINGS=6,T0$=""
0555 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$,WHEN$=F0$(7,3),CLOCK$="",H9$=""
0560 LET WIDTH=132,PAGE=0,L9=59,L=L9+1,X$="Sales",H7$="Sold",H8$="Sales"
0565 LET BEGWHSE$=OPTIONS$[1],ENDWHSE$=OPTIONS$[2],BEGPROD$=OPTIONS$[3]
0570 LET ENDPROD$=OPTIONS$[4],BEGITEM$=OPTIONS$[5],ENDITEM$=OPTIONS$[6]
0575 LET TYPE$=OPTIONS$[7],LEVEL$=OPTIONS$[0],DONE=0,ACTION$="T"
0580 LET PERIODS$[0]=BEGYEAR$+BEGPER$,PERIODS$[1]=ENDYEAR$+ENDPER$
0585 IF BASE$="U" THEN LET X$="Usage",H7$="Used",H8$="Usage"
0590 LET HEADINGS$[2]="Based On Total "+X$+" For Period "+BEGPER$+" "+FNYY21_YY$(BEGYEAR$)+" Through Period "+ENDPER$+" "+FNYY21_YY$(ENDYEAR$)
0600 REM " --- Print positions"
0610 CALL "SYC.RM",M1$,M1,12,15
0620 CALL "SYC.RM",M2$,M2,11,15
0630 CALL "SYC.RM",M3$,M3,11,15
0640 LET O[9]=WIDTH-M7,O[8]=O[9]-M1,O[7]=O[8]-M2
0650 LET O[6]=O[7]-M7,O[5]=O[6]-M1,O[4]=O[5],CH$=""
0660 IF LEVEL$="W" THEN LET O[4]=O[5]-M3,CH$="Unit Cost"
0670 LET O[3]=O[4]-M2,O[2]=O[3]-3,O[1]=O[2]-6,DW=O[3]-3
0700 REM " --- Background"
0710 CALL "SYC.NB"," Sorting",7+P[0],COLUMN
0800 REM " --- Report headings"
0805 DIM H1$(WIDTH),H2$(WIDTH)
0810 IF LEVEL$="I" THEN LET HEADINGS$[1]=HEADINGS$[1]+" By Item"
0815 IF LEVEL$="W" THEN LET HEADINGS$[1]=HEADINGS$[1]+" By Warehouse"
0820 LET X1$="First",X2$="First",X2=5
0825 IF BEGITEM$<>"" THEN LET X1$=BEGITEM$
0830 IF BEGPROD$<>"" THEN LET X2$=BEGPROD$,X2=3
0835 IF ENDPROD$="" THEN IF X2=3 THEN LET X2=4
0840 LET H1$(1)="From Item: "+X1$,H1$(WIDTH-X2-13)="From Product: "+X2$
0845 LET X1$="Last",X2$="Last",X3$="All Item Types"
0850 IF ENDITEM$<>"" THEN LET X1$=ENDITEM$
0855 IF ENDPROD$<>"" THEN LET X2$=ENDPROD$
0860 IF TYPE$<>"" THEN LET X3$="Item Type "+TYPE$
0865 LET H2$(1)="  To Item: "+X1$,H2$(FNC(X3$,WIDTH))=X3$
0870 LET H2$(WIDTH-X2-13)="  To Product: "+X2$
0875 LET HEADINGS$[4]=H1$,HEADINGS$[5]=H2$
0900 REM " --- Position files"
0970 LET FIRST$=N0$+"A",K$=FIRST$+BEGPROD$,P=0
0980 IF LEVEL$="W" THEN LET FIRST$=N0$+"C",K$=FIRST$+BEGWHSE$,P=2
0990 READ (IVM03_DEV,KEY=K$,DOM=1000)
1000 REM " --- Get next sort record"
1010 LET K$=KEY(IVM03_DEV,END=1900)
1020 IF POS(FIRST$=K$)<>1 THEN GOTO 1900
1030 LET WHSE$=K$(4,2),PRODUCT$=K$(4+P,3),ITEM$=K$(7+P)
1040 IF LEVEL$="W" THEN GOTO 1200
1100 REM " --- By Item: Valid Product/Item?"
1110 IF ENDPROD$<>"" THEN IF PRODUCT$>ENDPROD$ THEN GOTO 1900
1120 IF BEGITEM$<>"" THEN IF ITEM$<BEGITEM$ THEN GOTO 1400
1130 IF ENDITEM$<>"" THEN IF ITEM$>ENDITEM$ THEN GOTO 1450
1140 LET X$=PRODUCT$+" "+ITEM$(1,P[0])
1190 GOTO 1500
1200 REM " --- By Warehouse: Valid Warehouse/Product/Item?"
1210 IF ENDWHSE$<>"" THEN IF WHSE$>ENDWHSE$ THEN GOTO 1900
1220 IF BEGPROD$<>"" THEN IF PRODUCT$<BEGPROD$ THEN GOTO 1300
1230 IF ENDPROD$<>"" THEN IF PRODUCT$>ENDPROD$ THEN GOTO 1350
1240 IF BEGITEM$<>"" THEN IF ITEM$<BEGITEM$ THEN GOTO 1400
1250 IF ENDITEM$<>"" THEN IF ITEM$>ENDITEM$ THEN GOTO 1450
1260 LET X$=WHSE$+" "+PRODUCT$+" "+ITEM$(1,P[0])
1290 GOTO 1500
1300 REM " --- Skip to beginning product for this warehouse'
1310 READ (IVM03_DEV,KEY=K$(1,5)+BEGPROD$,DOM=1800)
1340 GOTO 1800
1350 REM " --- Skip to next warehouse"
1360 READ (IVM03_DEV,KEY=K$(1,5)+$FF$,DOM=1800)
1390 GOTO 1800
1400 REM " --- Skip to beginning item"
1410 READ (IVM03_DEV,KEY=K$(1,6+P)+BEGITEM$(1,P[0]-1),DOM=1800)
1440 GOTO 1800
1450 REM " --- Skip to next product"
1460 READ (IVM03_DEV,KEY=K$(1,6+P)+$FF$,DOM=1800)
1490 GOTO 1800
1500 REM " --- Accumulate totals"
1510 PRINT @(COLUMN,11),X$,
1520 READ (IVM03_DEV)
1530 IF TYPE$="" THEN GOTO 1580
1540 LET A0$(1)=N0$+ITEM$
1550 FIND (IVM01_DEV,KEY=A0$,DOM=1800)IOL=IVM01A
1560 IF A2$(53,3)<>TYPE$ THEN GOTO 1800
1580 IF LEVEL$="W" THEN GOSUB 6800
1590 IF LEVEL$="I" THEN GOSUB 6500
1600 REM " --- Write IVW-01 item record"
1610 LET C0$(1)=N0$+WHSE$,C0$(5)=SIGN$,C0$(6)=AMOUNT$,C0$(23)=ITEM$
1620 LET C[0]=COST,C[1]=USAGE,C[2]=ONHAND
1690 WRITE (IVW01_DEV,KEY=C0$)IOL=IVW01A
1700 REM " --- Write IVW-02 totals record"
1710 DIM D0$(4),D[1]
1720 LET D0$(1)=N0$+WHSE$
1730 FIND (IVW02_DEV,KEY=D0$,DOM=1740)IOL=IVW02A
1740 LET D[0]=D[0]+COST*USAGE,D[1]=D[1]+COST*ONHAND
1790 WRITE (IVW02_DEV,KEY=D0$)IOL=IVW02A
1800 REM " --- Loop back for next record"
1890 GOTO 1000
1900 REM " --- Position file"
1910 DIM BAR$(WIDTH,"-"),D0$(4),D[1]
1920 LET D0$(1)=N0$
1930 IF LEVEL$="I" THEN FIND (IVW02_DEV,KEY=D0$,DOM=1940)IOL=IVW02A
1940 LET TOTAL_USAGE=D[0],TOTAL_VALUE=D[1],TOTAL_PERCENT=0,BAR=0
1950 PRINT @(COLUMN-9,11),FILL(16+P[0]),@(COLUMN-9),'SB',"Printing",'SF',
1990 READ (IVW01_DEV,KEY="",DOM=2000)
2000 REM " --- Read next work record"
2010 READ (IVW01_DEV,END=4000)IOL=IVW01A
2020 PRINT @(COLUMN,11),C0$(3,2)," ",C0$(23,P[0]),
2030 LET COST$="",COST=C[0],USAGE=C[1],ONHAND=C[2]
2200 REM " --- Level break?"
2210 IF LEVEL$="W" THEN IF C0$(3,2)<>T0$ THEN GOSUB 6000
2400 REM " --- Additional reads"
2410 DIM A0$(22),A1$(60),A2$(64),DESCRIPTION$(DW)
2420 LET A0$(1)=N0$+C0$(23)
2430 FIND (IVM01_DEV,KEY=A0$)IOL=IVM01A
2440 LET DESCRIPTION$(1)=FNITEM$(A1$,P[3],P[4],P[5])
2450 LET PRODUCT$=A2$(1,3),UM$=A2$(4,2)
2600 REM " --- Perform detail line calculations"
2610 LET ITEM_USAGE=COST*USAGE,ITEM_VALUE=ONHAND*COST
2620 IF LEVEL$="W" THEN LET COST$=STR(COST:M3$)
2630 LET USAGE_PERCENT=0,VALUE_PERCENT=0,PERCENT$=""
2640 IF TOTAL_USAGE<>0 THEN LET USAGE_PERCENT=(ITEM_USAGE*100)/TOTAL_USAGE
2650 IF TOTAL_VALUE<>0 THEN LET VALUE_PERCENT=(ITEM_VALUE*100)/TOTAL_VALUE
2660 IF TOTAL_PERCENT<100 THEN LET PERCENT$=STR(USAGE_PERCENT:M7$)
3000 REM " --- Print detail line"
3010 IF L+2>L9 THEN GOSUB 5000
3020 PRINT (7)@(O[0]),A0$(3,P[0]),@(O[1]),PRODUCT$,@(O[2]),UM$,@(O[3]),USAGE:M2$,@(O[4]),COST$,@(O[5]),ITEM_USAGE:M1$,@(O[6]),PERCENT$,@(O[7]),ONHAND:M2$,@(O[8]),ITEM_VALUE:M1$,@(O[9]),VALUE_PERCENT:M7$
3030 PRINT (7)@(O[0]+3),DESCRIPTION$
3040 LET L=L+2,TOTAL_PERCENT=TOTAL_PERCENT+USAGE_PERCENT
3200 REM " --- Accumulate totals"
3210 LET WHSE[0]=WHSE[0]+ITEM_USAGE,WHSE[1]=WHSE[1]+ITEM_VALUE
3220 LET REPORT[0]=REPORT[0]+ITEM_USAGE,REPORT[1]=REPORT[1]+ITEM_VALUE
3400 REM " --- Reached 100% of usage yet?"
3410 IF TOTAL_PERCENT<100 THEN GOTO 3900
3420 IF BAR THEN GOTO 3900
3430 IF L+3>L9 THEN GOSUB 5000
3440 PRINT (7)""
3450 PRINT (7)BAR$
3460 PRINT (7)""
3470 LET L=L+3,BAR=1
3900 REM " --- Loop back for next record"
3990 GOTO 2000
4000 REM " --- All done"
4010 LET DONE=1
4020 IF LEVEL$="W" THEN GOSUB 6000
4030 IF L+2>L9 THEN GOSUB 5000
4040 PRINT (7)""
4050 PRINT (7)@(O[5]-16),"Total For Report",@(O[5]),REPORT[0]:M1$,@(O[8]),REPORT[1]:M1$
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+4
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)@(O[1]),"Prod",@(O[6]+M7-10),"Cumulative",@(O[7]+M2-9),"Quantity",@(O[9]+M7-10),"Cumulative"
5050 PRINT (7)@(O[0]),"Item/Description",@(O[1]),"Type",@(O[2]),"UM",@(O[3]+M2-10),"Units ",H7$,@(O[4]+M3-(LEN(CH$)+1)),CH$,@(O[5]+M1-12),"Total ",H8$,@(O[6]+M7-10),"% Of ",H8$,@(O[7]+M2-9),"On Hand",@(O[8]+M1-12),"Total Value",@(O[9]+M7-10),"% Of Value"
5060 PRINT (7)""
5090 RETURN 
6000 REM " --- Warehouse Break"
6010 IF T0$="" THEN GOTO 6100
6020 LET X$="Total For "+H9$
6030 IF L+2>L9 THEN GOSUB 5000
6040 PRINT (7)""
6050 PRINT (7)@(O[5]-LEN(X$)),X$,@(O[5]),WHSE[0]:M1$,@(O[8]),WHSE[1]:M1$
6060 LET L=L+2
6100 IF DONE THEN GOTO 6190
6110 DIM X0$(32),D0$(4),D[1],WHSE[1]
6120 LET T0$=C0$(3,2),X0$(6,24)="(Not on File)",L=L9+1,D0$(1)=N0$+T0$
6130 FIND (IVM10_DEV,KEY=N0$+"C"+T0$,DOM=6140)IOL=IVM10C
6140 LET H9$="Warehouse "+T0$+" "+FNP$(X0$(6,24)),BAR=0
6150 LET HEADINGS$[4]=H1$,HEADINGS$[4](FNC(H9$,WIDTH),LEN(H9$))=H9$
6160 FIND (IVW02_DEV,KEY=D0$,DOM=6070)IOL=IVW02A
6170 LET TOTAL_USAGE=D[0],TOTAL_VALUE=D[1],TOTAL_PERCENT=0
6190 RETURN 
6500 REM " --- Accumulate totals for item"
6510 DIM I[2]
6580 LET X$=N0$+"G"+ITEM$
6590 READ (IVM03_DEV2,KEY=X$,DOM=6600)
6600 REM " --- Read next item/warehouse xref"
6610 LET K$=KEY(IVM03_DEV2,END=6700)
6620 IF POS(X$=K$)<>1 THEN GOTO 6700
6630 READ (IVM03_DEV2)
6640 LET WHSE$=K$(24)
6650 GOSUB 6800
6660 LET AMOUNT=(I[0]*I[2])+(COST*ONHAND),NUMBER=I[2]+ONHAND
6670 IF NUMBER>0 THEN LET I[0]=AMOUNT/NUMBER
6680 LET I[1]=I[1]+USAGE,I[2]=I[2]+ONHAND
6690 GOTO 6600
6700 REM " --- Done with item"
6710 LET COST=I[0],USAGE=I[1],ONHAND=I[2],WHSE$="  "
6720 GOSUB 6900
6790 RETURN 
6800 REM " --- Accumulate warehouse totals"
6810 DIM B0$(24),B1$(64),B[20],ACTIVITY[12]
6820 LET B0$(1)=N0$+WHSE$+ITEM$,ITEMS$[0]=N0$,ITEMS$[1]=WHSE$,ITEMS$[2]=ITEM$
6830 LET COST=0,USAGE=0,ONHAND=0
6840 FIND (IVM02_DEV,KEY=B0$,DOM=6860)IOL=IVM02A
6850 LET ONHAND=B[0],COST=B[11]
6860 CALL "IVC.WB",ACTION$,FILES[ALL],ITEMS$[ALL],PERIODS$[ALL],ACTIVITY[ALL],STATUS
6870 IF BASE$="S" THEN LET USAGE=ACTIVITY[3]-ACTIVITY[8]
6880 IF BASE$="U" THEN LET USAGE=(ACTIVITY[3]+ACTIVITY[4])-ACTIVITY[8]
6900 REM " --- Calculate total amount of usage"
6910 LET AMOUNT=USAGE*COST,SIGN$="",AMOUNT$=STR(100000000000-AMOUNT:M8$)
6920 IF AMOUNT<=0 THEN LET SIGN$="-",AMOUNT$=STR(AMOUNT:M8$)
6990 RETURN 
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

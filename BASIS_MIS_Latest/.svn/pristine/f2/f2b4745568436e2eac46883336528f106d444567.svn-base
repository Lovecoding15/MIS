0010 REM "BME - Inventory Production Entry I
0020 REM "Program BME.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "19Jun96 JJD: With records in BME-01, F2 from "Bill/Item No:" field does not return curser to the previous field (1430).
0032 REM "31Mar97 WGH: Initialize !GLCONTROL based on current program (655)"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="BME-01",FILES$[2]="BMM-09",FILES$[3]="IVM-01"
0130 LET FILES$[4]="IVM-02",FILES$[5]="IVM-10",FILES$[6]="IVM-14"
0135 LET FILES$[7]="SYS-01"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS<>0 THEN GOTO 9900
0175 LET BME01_DEV=CHANNELS[1],BMM09_DEV=CHANNELS[2],IVM01_DEV=CHANNELS[3]
0180 LET IVM02_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],IVM14_DEV=CHANNELS[6]
0185 LET SYS01_DEV=CHANNELS[7]
0200 REM " --- IOLists
0210 BME01A: IOLIST A0$,A1$(1),A[0],A[1]
0230 BMM09A: IOLIST K0$,K1$
0250 IVM01A: IOLIST B0$,B1$(1),B2$
0260 IVM10C: IOLIST X0$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 BMS01A: IOLIST X$,M1$,M2$,M3$
0320 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,X$,X$,X$,X$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0435 FIND (SYS01_DEV,KEY=N0$+"BM00",DOM=9800)IOL=BMS01A
0440 DIM P[4],DESC[3],INFO$[20],PARAMS[3]
0445 FOR X=1 TO 3
0450 LET DESC[X]=NUM(P2$((X-1)*2+9,2))
0453 LET PARAMS[X]=DESC[X]
0455 NEXT X
0460 LET P[0]=NUM(P2$(1,2))
0465 LET PARAMS[0]=P[0]
0500 REM " --- Init Data
0520 DIM A0$(34),A[1],A1$(8)
0540 LET A0$(1)=N0$,A0$(5,3)=F0$(7,3)
0550 DIM IV_CHANS[14]
0560 LET IV_CHANS[1]=IVM01_DEV,IV_CHANS[14]=IVM14_DEV
0600 REM " --- Additional File Opens
0650 LET GL$="N",STATUS=0
0655 CALL "GLC.BA",ERR=0660,PGM(-2),N2$,GLW11$,GL$,STATUS
0660 IF STATUS<>0 THEN GOTO 9900
0665 IF GL$<>"Y" THEN GOTO 0700
0670 LET FILES$[8]="GLM-01",FILES$[9]=GLW11$,OPTIONS$[9]="C"
0675 CALL "SYC.DA",1,8,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS>0 THEN GOTO 9900
0690 LET GLM01_DEV=CHANNELS[8],GLW11_DEV=CHANNELS[9]
0700 REM " --- Background
0740 PRINT 'SB',@(3,3),"Warehouse ID:",@(0,4),"Production Date:",@(6,5),"Reference:",@(3,7),"Bill/Item No:",@(3,8),"Unit-Of-Sale:",@(2,9),"Item Quantity:",@(3,10),"Update Item?:"
0790 PRINT 'SF'
1000 REM " --- Key
1010 PRINT 'CF',
1020 LET I0=0
1030 LET A0$(3,2)=""
1040 READ (BME01_DEV,KEY=A0$(1,2),DOM=1050)
1050 GOSUB 6400
1100 REM " --- Warehouse ID
1110 IF A0$(3,2)="  " THEN LET A0$(3,2)=P3$(6,2)
1120 LET V0$="S",V1$="KREC",V2$=A0$(3,2),V0=2,V1=17,V2=3,V4$="Enter Warehouse Code, <F3>=Lookup, <Enter> For Default"
1130 GOSUB 7000
1135 IF V3=4 THEN GOTO 9900
1140 DIM DESCRIPTION$[1],FIELD[1],POSITION[1],LENGTH[1]
1145 LET DESCRIPTION$[0]="Warehouse",DESCRIPTION$[1]="Name",FIELD[0]=1,FIELD[1]=1,POSITION[0]=4,POSITION[1]=6,LENGTH[0]=2,LENGTH[1]=24
1150 IF V3=3 THEN CALL "SYC.LK",IVM10_DEV,"C",1,"Warehouse Lookup",DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],50,7,V$
1160 LET A0$(3,2)=V$
1170 GOSUB 6200
1180 IF X0$="" THEN GOTO 1100
1190 GOTO 3900
1200 REM " --- Date
1220 LET V0$="D",V1$="RE",V2$=A0$(5,3),V0=8,V1=17,V2=4,STATUS=0
1240 GOSUB 7000
1250 IF V3=4 THEN GOTO 9900
1260 IF GL$="Y" THEN CALL "GLC.EA",V$,"Y",PERIOD$,YEAR$,STATUS
1265 IF STATUS>99 THEN GOTO 1200
1270 LET A0$(5)=V$
1280 READ (BME01_DEV,KEY=A0$(1,7),DOM=1290)
1290 GOTO 3900
1300 REM " --- Reference Number
1305 LET O1=0,V8=7
1310 GOSUB 6400
1320 LET V0$="S",V1$="CE",V2$=A0$(8,7),V0=7,V1=17,V2=5,V4$="Enter A 7 Character Production Reference"
1330 GOSUB 7000
1335 IF V3=4 THEN GOTO 9900
1340 LET A0$(8,7)=V$,A0$(15,20)=FILL(20),X1$=A0$(8,7)
1342 READ (BME01_DEV,KEY=A0$(1,14),DOM=1345)
1345 GOTO 3900
1350 REM " --- Update Item?
1360 LET V0$="Y",V2$=A1$(8,1),V0=1,V1=17,V2=10,V3$="YN",V4$="Y=Add Finished Item To Inventory,  N=Relieve Components Only",V1$="R",V1$="C"
1365 IF V2$=" " THEN LET V2$="Y"
1370 GOSUB 7000
1380 LET A1$(8,1)=V$
1385 GOTO 3900
1400 REM " --- Item
1410 PRINT @(0,15),'CE'
1420 GOSUB 6300
1430 LET V8=14,I0=3; GOSUB 6400
1440 IF X1$<>A0$(8,7) THEN LET O1=1,A0$(8,7)=X1$
1450 IF O1=1 THEN LET O1=0,A0$(15,20)=""
1460 PRINT @(17,3),A0$(3,2),@(17,4),FNB$(A0$(5,3)),@(17,5),A0$(8,7)
1470 DIM ITEM$[6],ITEM[12],B1$(64)
1480 LET DEFAULT$=A0$(15,20),ACTION$="N",V1=17,V2=7,V4$="Enter A Valid Bill Or Inventory Item # (<F1>=Item Lookup/<F3>=Bill Lookup)"
1490 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1500 IF V3=4 THEN GOTO 1000
1510 IF V3=2 THEN PRINT @(V1,V2),'CL',; GOTO 3900
1520 IF V3<>1 THEN GOTO 1550
1530 CALL "SYC.LI",V$
1540 GOTO 1570
1550 IF V3<>3 THEN GOTO 1620
1560 CALL "SYC.LB",V$
1570 LET A0$(15,20)=V$+FILL(20-LEN(V$))
1590 FIND (IVM01_DEV,KEY=N0$+A0$(15,20),DOM=1400)IOL=IVM01A
1600 LET B1$(1)=FNITEM$(B1$,DESC[1],DESC[2],DESC[3])
1610 GOTO 1640
1620 LET A0$(15,20)=ITEM$[0]+FILL(20-LEN(ITEM$[0]))
1630 LET B1$(1)=ITEM$[1],B2$=ITEM$[2]
1640 PRINT @(V1,V2),A0$(15,P[0])," ",B1$(1,61-P[0]),
1650 FIND (IVM02_DEV,KEY=N0$+A0$(3,2)+A0$(15,20),DOM=1700)
1660 PRINT @(17,8),B2$(4,2),
1670 IF P3$(1,1)="Y" THEN PRINT @(17,3),A0$(3,2)
1680 PRINT @(17,4),FNB$(A0$(5,3))
1690 GOTO 1800
1700 PRINT 'RB',
1710 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Item Not Set Up In Warehouse - <Enter> To Continue: ",V0=1,V1=FNV(V4$),V2=22
1720 GOSUB 7000
1730 GOTO 1400
1800 REM " --- Get Record
1810 GOSUB 6500
1820 FIND (BME01_DEV,KEY=A0$,DOM=1900)IOL=BME01A
1890 GOTO 4000
1900 REM " --- New Record
1910 DIM A[1]
1920 LET A[0]=1
1930 LET A1$(8,1)="Y"
1940 GOSUB 5000
2200 REM " --- Item Qty
2220 LET V0$="N",V2$=STR(A[0]),V3$=M2$,V1=17,V2=9,I0=4,V1$="C",V4$="Enter Quantity To Be Produced"
2240 GOSUB 7000
2250 IF V=0 THEN GOTO 2200
2260 LET A[0]=V
2290 GOTO 3900
3900 REM " --- Next Field
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1300,1400,2200,1350,4000
4000 REM " --- Options
4010 GOSUB 5000
4020 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)? ",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V$="DELETE" THEN GOTO 4200
4050 ON POS(V$="YN") GOTO 4000,4100,2200
4100 REM " --- Write
4110 WRITE (BME01_DEV,KEY=A0$)IOL=BME01A
4190 GOTO 1400
4200 REM " --- Delete
4210 REMOVE (BME01_DEV,KEY=A0$,DOM=4290)
4290 GOTO 1400
5000 REM " --- Display
5020 PRINT @(17,9),A[0]:M2$,
5040 PRINT @(17,10),A1$(8,1),
5190 RETURN 
6200 REM " --- Type
6210 LET X0$=""
6220 FIND (IVM10_DEV,KEY=N0$+"C"+A0$(3,2),DOM=6230)IOL=IVM10C
6230 IF X0$="" THEN RETURN 
6240 PRINT @(V1+3,V2),X0$(6,20),
6290 RETURN 
6300 REM " --- Clear Data
6320 FOR X=7 TO 12
6330 PRINT @(17,X),'CL',
6340 NEXT X
6390 RETURN 
6400 REM " --- Sequential Read
6410 READ (BME01_DEV,KEY=A0$(1,V8),DOM=6420)
6420 LET K1$=KEY(BME01_DEV,END=6480)
6430 IF LEN(K1$)<2 THEN GOTO 6480
6440 IF K1$(1,2)<>A0$(1,2) THEN LET K1$=""
6450 IF K1$<>"" THEN IF A0$(1,V8)=K1$(1,V8) THEN LET A0$=K1$ ELSE LET A0$(V8+1)=""
6470 GOTO 6490
6480 LET O1=1
6490 RETURN 
6500 REM " --- Comments
6510 LET L2=15
6515 PRINT 'SB',
6520 READ (BMM09_DEV,KEY=N0$+A0$(15,20),DOM=6530)
6530 LET K$=KEY(BMM09_DEV,END=6590)
6535 IF POS(A0$(1,2)+A0$(15,20)=K$)<>1 THEN GOTO 6590
6540 READ (BMM09_DEV,KEY=K$)IOL=BMM09A
6550 IF L2>20 THEN GOTO 6600
6560 PRINT @(14,L2),">",K1$,"<"
6570 LET L2=L2+1
6580 GOTO 6530
6590 PRINT 'SF',
6595 RETURN 
6600 REM " --- Comments Continued
6620 LET V0$="S",V1$="C",V2$="",V3$="",V4$="<Enter> For More Comments, <F4>=End:",V0=1,V1=58,V2=22
6630 GOSUB 7000
6640 IF V3=4 THEN GOTO 6690
6650 PRINT @(0,15),'CE'
6660 LET L2=15
6670 GOTO 6560
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

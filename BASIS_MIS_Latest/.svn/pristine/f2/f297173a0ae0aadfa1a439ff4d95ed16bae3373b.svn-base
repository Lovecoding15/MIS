0010 REM "SHE - Package Processing (Package Entry)"
0020 REM "Program SHE.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Feb97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Feb97 CAJ: Modified for 1997 SpeedShip Update"
0032 REM "21Oct98 WGH: Prevent Error 12 when opening PF print (6305-6325)
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 SHE01A: IOLIST A0$,A1$(1),A2$(1),A[ALL]
0220 SHE02A: IOLIST W0$,W1$(1),W[ALL]
0230 SHE04A: IOLIST K$
0240 ARE03A: IOLIST ORDER$
0260 SHM10A: IOLIST *,SERVICE$(1),DIGITS,XLIMIT
0275 SHM10D: IOLIST *,CARRIER$(1)
0280 SHM10E: IOLIST *,TERMINAL$(1)
0285 SHM10F: IOLIST *,WAREHOUSE$(1)
0290 SHM10G: IOLIST *,PKGDEF$(1)
0300 SHM10N: IOLIST T0$(1),T1$(1)
0310 SHM10P: IOLIST *,OPTIONS$(1)
0320 SHM10T: IOLIST H0$(1),H[0],H[1]
0500 REM " --- Initializations"
0550 LET STATES$="ALAKASAZARCACCCOCTDEDCFLGAGUHIIDILINIAKSKYLAMEMDMAMIMNMS"
0560 LET STATES$=STATES$+"MOMTNENVNHNJNMNYNCNDOHOKORPAPRRISCSDTNTXUTVTVAVI"
0570 LET STATES$=STATES$+"WAWVWIWY",N=1,M6$="##0",WARNED=0,XLIMIT=0,LAST$=""
0800 REM " --- Establish default service"
0810 IF DEFAULT$<>"" THEN GOTO 0900
0820 IF A0$(3,1)="M" THEN GOTO 0890
0830 IF OP$<>"Y" THEN GOTO 0890
0840 FIND (ARE03_DEV,KEY=N0$+"  "+A1$(1,6)+A0$(3,7)+"000",DOM=0890)IOL=ARE03A
0850 LET DEFAULT$=ORDER$(32,7)
0860 IF FNP$(DEFAULT$)<>"" THEN GOTO 0900
0890 LET DEFAULT$=SPEEDSHIP$(55,7)
1000 REM " --- Setup for next carton"
1020 LET F0=0,O1$="",L9$="",V0=11,V1=11,V2=11
1030 GOSUB 5500
1040 IF W0$="" THEN GOTO 1100
1050 IF W0$(1,17)=A0$(1,17) THEN LET L9$=W0$(18,3)
1100 REM 
1110 DIM W0$(20),W1$(128),W[20],CARRIER$(128)
1120 LET W0$(1,17)=A0$(1,17)
1130 IF LEN(L9$)>0 THEN LET W0$(18,3)=STR(NUM(L9$)+N:"000")
1500 REM " --- Process next carton"
1510 IF W0$(18,3)<"001" THEN LET W0$(18,3)="001"
1530 LET V0$="Z",V1$="CK",V2$=W0$(18,3),V3$=""
1540 LET V4$="Enter the Package Number (<F1>=Next/<F2>=Previous/<F3>=Lookup)"
1550 LET V0=3,V1=11,V2=12,I0=0
1560 PRINT @(V1,V2),W0$(18,3),
1570 GOSUB 7000
1590 ON V3 GOTO 1800,1600,1650,1700,9900
1600 REM " --- <F1> Next Package/<F2> Previous Package"
1610 IF NUM(W0$(18,3))+N<1000 THEN LET W0$(18,3)=STR(NUM(W0$(18,3))+N:"000")
1620 GOTO 1500
1650 IF NUM(W0$(18,3))+N>0 THEN LET W0$(18,3)=STR(NUM(W0$(18,3))-N:"000")
1660 GOTO 1500
1700 REM " --- <F3> Package Lookup"
1710 DIM DESCRIPTION$[4],FIELD[4],POSITION[4],LENGTH[4]
1715 LET NUMBER=4,RECORD$=A0$(3,15),TITLE$="This Shipment's Packages"
1720 LET DESCRIPTION$[0]="Pkg",FIELD[0]=1,POSITION[0]=18,LENGTH[0]=3
1725 LET DESCRIPTION$[1]="Service",FIELD[1]=2,POSITION[1]=4,LENGTH[1]=7
1730 LET DESCRIPTION$[2]="Tracking #",FIELD[2]=2,POSITION[2]=108,LENGTH[2]=18
1735 LET DESCRIPTION$[3]="Weight",FIELD[3]=3,POSITION[3]=1,LENGTH[3]=0
1740 LET DESCRIPTION$[4]="Memo",FIELD[4]=2,POSITION[4]=29,LENGTH[4]=24
1745 LET ROW=3,COLUMN=79-LENGTH[0]-LENGTH[1]-LENGTH[2]-6-LENGTH[4]-NUMBER*4
1750 LET V$="SKIP"
1755 CALL "SYC.LK",2,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
1760 IF V$="" THEN GOTO 1500
1800 REM " --- Retrieve entered package"
1810 LET W0$(18,3)=V$
1820 IF NUM(W0$(18,3))<1 THEN GOTO 1500
1830 PRINT @(V1,V2),W0$(18,3),
1840 FIND (SHE02_DEV,KEY=W0$,DOM=2000)IOL=SHE02A
1850 LET O1$=W1$
1900 REM " --- Retrieve parameters and display record"
1910 PRINT @(11,13),W1$(4,7),@(11,14),FILL(3),@(11,14),W[0]:M6$,
1920 GOSUB 6500
1930 IF STATUS THEN GOTO 2000
1940 CALL ENTPGM$,ERR=1950,A0$,A1$,W0$,W1$,W[ALL],OPTIONS$,SPEEDSHIP$,1
1950 GOSUB 5000
1990 GOTO 4000
2000 REM " --- Service ID"
2010 LET V1=11,V2=13,I0=1
2015 IF FNP$(W1$(4,7))<>"" THEN GOTO 2030
2020 LET W1$(4,7)=DEFAULT$
2025 IF FNP$(W1$(4,7))="" THEN LET W1$(4,7)=SPEEDSHIP$(55,7)
2030 LET V0$="S",V1$="CK",V$=W1$(4,7),V3$="",V5$="D",V0=7
2035 CALL "SHC.KA",SHM10_DEV,N0$,F$,V0,V1,V2,V0$,V1$,V$,V3
2040 IF V3>4 THEN LET V3=0
2045 IF V3=4 THEN LET V3=2
2050 ON V3 GOTO 2080,2080,3900,2060,3900
2060 CALL "SHC.LK",V1,V2,"A",0,V$
2070 IF V$="" THEN GOTO 2000
2080 LET W1$(4,7)=V$
2090 PRINT @(V1,V2),V$,
2100 REM " --- Initializations for selected service"
2110 GOSUB 6500
2115 IF STATUS<>0 THEN GOTO 2000
2200 REM " --- Pickup number"
2210 IF CARRIER$(53,1)<>"Y" THEN GOTO 2280
2215 LET V$=FNP$(CARRIER$(54,12))
2220 IF V$<>"" THEN GOTO 2290
2230 LET V4$="Enter a summary book number for this carrier"
2235 LET V0$="S",V1$="C",V2$="",V3$="",V0=12,V1=39,V2=3
2240 GOSUB 7000
2245 IF V3=4 THEN GOTO 2000
2250 IF V$="" THEN GOTO 2230
2255 LET T0$=N0$+"N"+W1$(11,1)+W1$(59,2),T1$(11,12)=V$,CARRIER$(54,12)=V$
2260 WRITE (SHM10_DEV,KEY=T0$)IOL=SHM10N
2265 GOTO 2290
2280 LET V$="None",CARRIER$(54,12)=""
2290 PRINT @(39,3),FILL(12),@(39,3),V$,@(39,4),FILL(8),@(39,4),"0",
2295 LET W1$(62,12)=CARRIER$(54,12)
2300 REM " --- Determine Zone"
2310 LET ZIPCODE$=A1$(127,P[2]),ZONE$=""
2320 CALL ZONEPGM$,ERR=2330,A0$,A1$,W0$,W1$,W[ALL],P[2],ZIPCODE$,ZONE$,SERVICE$,RESERVED$,ZONEFILE$,SPEEDSHIP$
2330 IF ZONE$="EXIT" THEN GOTO 2000
2340 LET W1$(1,3)=ZONE$,A1$(127,P[2])=ZIPCODE$
2400 REM " --- Default values for new record"
2410 LET L=LEN(FNP$(PKGDEF$))
2420 IF L=0 THEN GOTO 2490
2430 FOR X=1 TO L
2440 IF PKGDEF$(X,1)<>" " THEN IF W1$(X,1)=" " THEN LET W1$(X,1)=PKGDEF$(X,1)
2450 NEXT X
2490 GOTO 3900
2500 REM " --- Display record"
2510 LET V0=14,V1=18,V2=16
2520 GOSUB 5500
2530 CALL ENTPGM$,ERR=2540,A0$,A1$,W0$,W1$,W[ALL],OPTIONS$,SPEEDSHIP$,1
2590 GOTO 3900
2600 REM " --- Package Weight"
2610 LET V1=11,V2=14
2620 IF SERVICE$(48,1)<>"N" THEN GOTO 2650
2630 LET W[0]=0
2640 PRINT @(V1,V2),J1$(1,3),
2645 GOTO 2990
2650 IF SERVICE$(48,1)="M" THEN GOTO 2800
2660 IF TERMINAL$(1,1)<>"Y" THEN GOTO 2800
2670 IF SERVICE$(48,1)<>"S" THEN GOTO 2800
2700 REM " --- Get weight from scale"
2710 LET SCALE_DEV=UNT,V3=0
2720 CALL "SHC.GS",SCALE_DEV,TERMINAL$(4,2),STATUS
2730 IF STATUS THEN GOTO 2790
2740 CALL SCALEPGM$,ERR=2750,SCALE_DEV,V$
2750 CLOSE (SCALE_DEV,ERR=2760)
2760 IF SCALE_DEV<>99 THEN GOTO 2780
2770 LET I0=I0-3
2775 GOTO 2990
2780 LET W[0]=NUM(V$,ERR=2790)
2790 GOTO 2900
2800 REM " --- Get weight from user"
2810 LET V0$="N",V1$="R",V2$=STR(W[0]:M6$),V3$=M6$,V0=LEN(V3$)
2820 LET V4$="Enter the weight of this package"
2830 GOSUB 7000
2840 PRINT @(V1-1,V2),FILL(M6+2),
2850 IF V3=4 THEN LET V3=2
2860 ON V3 GOTO 2870,2600,3900,2600,2600
2870 LET W[0]=V
2900 REM 
2910 IF INT(W[0])<>W[0] THEN LET W[0]=INT(W[0])+1
2920 PRINT @(V1,V2),W[0]:M6$,
2930 IF W[0]=-1 THEN LET I0=I0-2
2940 IF W[0]=-2 THEN LET W[0]=0
2950 IF W[0]<1 THEN GOTO 2800
2990 GOTO 3900
3000 REM " --- Are there intrastate restrictions?"
3010 IF SERVICE$(73,1)<>"Y" THEN GOTO 3900
3020 IF T1$(35,1)<>"Y" THEN GOTO 3900
3050 REM " --- Valid state abbreviation?"
3055 IF POS(A1$(142,2)=STATES$,2)>0 THEN GOTO 3100
3060 IF WARNED>0 THEN GOTO 3900
3065 LET V0$="S",V1$="C",V2$=A1$(142,2),V3$="",V0=2,V1=45,V2=7,WARNED=WARNED+1
3070 LET V4$=$22$+V2$+$22$+" is not a valid state for intrastate verification (<Enter>=Continue)"
3075 GOSUB 7000
3080 ON V3 GOTO 3085,3050,3900,3050,3900
3085 LET A1$(142,2)=V$
3090 GOTO 3050
3100 REM " --- Test weight against intrastate limit"
3110 IF A1$(142,2)<>WAREHOUSE$(146,2) THEN GOTO 3900
3120 IF W[0]<=XLIMIT THEN GOTO 3900
3130 LET V4$="Package weight exceeds intrastate limits for this service"
3140 GOSUB 6600
3150 LET I0=0,K9=0
3190 GOTO 3900
3200 REM " --- Package entry"
3210 CALL ENTPGM$,ERR=3220,A0$,A1$,W0$,W1$,W[ALL],OPTIONS$,SPEEDSHIP$,0
3290 GOTO 3900
3400 REM " --- Freight calculation"
3410 IF FRTPGM$="" THEN GOTO 3900
3415 LET ZONE$=FNP$(W1$(1,3)),F0=0
3420 IF SERVICE$(48,1)="S" THEN IF W[0]=0 THEN GOTO 3480
3430 CALL FRTPGM$,ERR=3440,A0$,A1$,W0$,W1$,W[ALL],F0,P[2],ZIPCODE$,ZONE$,SERVICE$,RESERVED$,RATEFILE$,ZONEFILE$,SPEEDSHIP$
3440 IF F0>0 THEN GOTO 3480
3445 LET V4$="Invalid package weight! (<Enter>=Retry)"
3450 GOSUB 6900
3455 IF V3=0 THEN LET I0=1
3460 IF V3=4 THEN LET I0=0
3465 LET V3=0
3470 GOTO 3900
3480 LET W[4]=F0
3490 GOTO 3900
3600 REM " --- Optional charges"
3610 CALL CHGPGM$,ERR=3620,SHM10_DEV,W[ALL],W1$,N0$,SPEEDSHIP$
3690 GOTO 3900
3800 REM " --- Display charges"
3810 GOSUB 5000
3890 GOTO 3900
3900 REM " --- Next field"
3930 IF V3<>2 THEN GOTO 3950
3940 IF I0>0 THEN LET I0=I0-1
3945 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,2000,2500,2600,3400,3000,3800,3200,3400,3600,3800,4000
4000 REM " --- Correct?"
4010 LET V4$="Is this correct (Y/N/Delete/S=Shipping Label/C=COD Tag)?"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22,I0=0
4025 GOSUB 7000
4030 ON V3 GOTO 4040,4000,4000,4000,9900
4040 IF V$="DELETE" THEN GOTO 4500
4050 IF LEN(V$)>0 THEN LET V$=V$(1,1)
4070 IF V$="C" THEN IF W[2]>0 THEN GOSUB 6200
4080 IF V$="S" THEN GOSUB 6100
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Get next transaction number"
4110 IF O1$="" THEN GOTO 4145
4120 IF W1$(11,1)<>O1$(11,1) THEN GOTO 4145
4130 IF FNP$(W1$(12,4))="" THEN GOTO 4145
4140 GOTO 4200
4145 FIND (SHE02_DEV,KEY=W0$,DOM=4150)
4147 GOTO 4200
4150 LET H0$(1)=N0$+"T"+W1$(11,1)+W1$(59,2),H[0]=0,H[1]=0
4155 FIND (SHM10_DEV,KEY=H0$,DOM=4160)IOL=SHM10T
4160 LET H[0]=H[0]+1,W1$(12,4)=J1$(1,4)
4170 IF SERVICE$(26,1)<>"Y" THEN GOTO 4190
4180 IF FNP$(W1$(12,4))="" THEN LET H[1]=H[1]+1,W1$(12,4)=STR(H[1]:"0000")
4190 WRITE (SHM10_DEV,KEY=H0$)IOL=SHM10T
4200 REM " --- Update package detail"
4210 WRITE (SHE02_DEV,KEY=W0$)IOL=SHE02A
4230 IF W1$=O1$ THEN GOTO 4900
4240 GOSUB 6100
4250 IF W[2]=0 THEN GOTO 4300
4260 GOSUB 6200
4270 WRITE (SHE02_DEV,KEY=W0$)IOL=SHE02A
4300 REM " --- Update xref"
4310 IF O1$="" THEN GOTO 4350
4320 LET K$=N0$+"A"+O1$(11,1)+O1$(59,2)+O2$+O1$(12,4)+W0$(3,15)+W0$(18,3)
4330 REMOVE (SHE04_DEV,KEY=K$,DOM=4340)
4350 LET K$=N0$+"A"+W1$(11,1)+W1$(59,2)+A2$(1,6)+W1$(12,4)+W0$(3,15)+W0$(18,3)
4360 WRITE (SHE04_DEV,KEY=K$)IOL=SHE04A
4390 GOTO 4900
4500 REM " --- Delete package"
4510 REMOVE (SHE02_DEV,KEY=W0$,DOM=4900)
4520 LET H0$(1)=N0$+"T"+W1$(11,1)+W1$(59,2),H[0]=0
4530 FIND (SHM10_DEV,KEY=H0$,DOM=4900)IOL=SHM10T
4540 LET H[0]=H[0]-1
4550 WRITE (SHM10_DEV,KEY=H0$)IOL=SHM10T
4600 REM " --- Delete xref"
4610 LET K$=N0$+"A"+W1$(11,1)+W1$(59,2)+A2$(1,6)+W1$(12,4)+W0$(3,15)+W0$(18,3)
4620 REMOVE (SHE04_DEV,KEY=K$,DOM=4630)
4900 REM " --- Update shipment header totals"
4910 CALL "SHC.HA",SHE01_DEV,SHE02_DEV,A0$,A[ALL]
4920 PRINT @(39,4),FILL(8),@(39,4),H[0],@(66,5),A[0]:M1$,@(66,6),A[1]:M1$,@(66,7),A[0]+A[1]:M1$,@(66,9),A[2],@(66,10),A[3],
4930 IF OP$="Y" THEN CALL ORDPGM$,ERR=4940,SHE01_DEV,SHE02_DEV,ARE03_DEV,A0$
4990 GOTO 1000
5000 REM " --- Display charges"
5010 LET L=19,W=W[0],W[10]=0,X$="   "
5020 FOR X=3 TO 16
5030 IF X<>10 THEN IF X<>14 THEN LET W[10]=W[10]+W[X]
5040 NEXT X
5050 IF W1$(88,1)="D" THEN LET W=W[20],X$=" DW"
5075 PRINT @(1,L),W0$(18,3),@(6),W1$(4,7),@(17-LEN(M6$)),W:M6$,X$,@(21),W[4]:M2$,@(64),W[9]:M3$,@(71),W[10]:M2$,@(6,L+1),SERVICE$(2,24),@(11,14),W[0]:M6$,
5080 CALL DISPGM$,ERR=5090,W1$,W[ALL]
5090 RETURN 
5500 REM " --- Clear package entry area"
5510 PRINT @(V0,12),'CL',@(V1,13),'CL',@(V2,14),'CL',@(1,15),'CL',@(28,17),FILL(30),@(1,19),'CL',@(1,20),'CL',
5590 RETURN 
6100 REM " --- Print shipping label"
6110 IF TERMINAL$(2,1)<>"Y" THEN GOTO 6190
6120 LET LPRINTER$=TERMINAL$(36,2),LPROGRAM$=LABELPGM$
6130 IF A1$(127,5)="00000" THEN LET A1$(127,5)=FILL(5)
6140 GOSUB 6300
6190 RETURN 
6200 REM " --- Print COD Label"
6210 IF TERMINAL$(3,1)<>"Y" THEN GOTO 6280
6220 LET LPRINTER$=TERMINAL$(38,2),LPROGRAM$=CODPGM$,W[14]=W[10]+W[2]
6230 GOSUB 6300
6280 LET W[14]=W[10]+W[2]
6290 RETURN 
6300 REM " --- Open label printer and print label"
6305 LET LABEL_DEV=UNT
6310 OPEN (LABEL_DEV,ERR=6320)LPRINTER$
6315 GOTO 6330
6320 CALL "SYC.GA",LABEL_DEV,0,"",LPRINTER$,PSTATUS
6325 IF PSTATUS THEN GOTO 6390
6330 CALL LPROGRAM$,ERR=6340,A1$,A2$,W0$,W1$,W[ALL],LABEL_DEV,WAREHOUSE$,CARRIER$,P[0]
6340 CLOSE (LABEL_DEV,ERR=6390)
6390 RETURN 
6500 REM " --- Additional reads for selected service"
6505 IF W1$(4,7)=LAST$ THEN GOTO 6790
6510 DIM WAREHOUSE$(160),PKGDEF$(128),T0$(6),T1$(48),SERVICE$(128)
6515 DIM CARRIER$(128),TERMINAL$(50),OPTIONS$(128)
6520 LET STATUS=0,S$=W1$(4,7)
6530 FIND (SHM10_DEV,KEY=N0$+"A"+S$,DOM=6740)IOL=SHM10A
6535 LET C$=SERVICE$(1,1)
6540 FIND (SHM10_DEV,KEY=N0$+"E"+C$+FID(0)+FILL(4-LEN(FID(0))),DOM=6700)IOL=SHM10E
6545 LET W$=TERMINAL$(40,2)
6550 FIND (SHM10_DEV,KEY=N0$+"D"+C$,DOM=6720)IOL=SHM10D
6560 IF CARRIER$(97,1)<>"Y" THEN GOTO 6720
6570 FIND (SHM10_DEV,KEY=N0$+"F"+W$,DOM=6575)IOL=SHM10F
6575 FIND (SHM10_DEV,KEY=N0$+"G"+S$,DOM=6580)IOL=SHM10G
6580 FIND (SHM10_DEV,KEY=N0$+"P"+S$,DOM=6590)IOL=SHM10P
6590 FIND (SHM10_DEV,KEY=N0$+"N"+C$+W$,DOM=6600)IOL=SHM10N
6600 REM " --- Working variables"
6610 LET CARRIER$(37,10)=T1$(1,10),CARRIER$(54,12)=T1$(11,12)
6615 LET CARRIER$(71,6)=T1$(23,6),CARRIER$(77,6)=T1$(29,6)
6620 LET SPEEDSHIP$(70,2)=W$,W1$(59,2)=W$,W1$(61,1)=PKGDEF$(61,1)
6625 LET W1$(11,1)=C$,DEFAULT$=S$,M6$=FNM$(DIGITS,M4$,M5$),M6=LEN(M6$)
6630 IF M6$(M6)="." THEN LET M6=M6-1,M6$=M6$(1,M6)
6635 LET ENTPGM$=FNN$(SERVICE$(61,6)),ZONEPGM$=FNN$(SERVICE$(49,6))
6640 LET CHGPGM$=FNN$(SERVICE$(55,6)),DISPGM$=FNN$(SERVICE$(67,6))
6650 LET FRTPGM$=FNN$(SERVICE$(30,6)),SCALEPGM$=FNN$(TERMINAL$(30,6))
6660 LET RATEFILE$=FNN$(SERVICE$(36,6)),ZONEFILE$=FNN$(SERVICE$(42,6))
6670 LET LABELPGM$=FNN$(CARRIER$(71,6)),CODPGM$=FNN$(CARRIER$(77,6))
6690 GOTO 6790
6700 REM " --- No terminal record for carrier"
6710 LET V4$="There is no terminal record for this service"
6715 GOTO 6770
6720 REM " --- Carrier not installed"
6725 LET V4$="This carrier is not installed"
6730 GOTO 6770
6740 REM " --- Service Not Found"
6745 LET V4$="This service does not exist"
6770 GOSUB 6800
6780 LET STATUS=1
6790 RETURN 
6800 REM " --- Message + <CR>=Continue"
6810 LET V4$=V4$+" (<Enter>=Continue)"
6900 REM " --- Display standard message and prompt for response"
6910 DIM MESSAGE$[1]
6920 LET MESSAGE$[0]=V4$
6930 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8065 DEF FNN$(Q$)=FNP$(Q$(POS("NONE"=Q$)*4+1))
8070 DEF FNM$(Q,Q1$,Q2$)=Q1$(1,Q-FPT(Q))+"."+Q2$(1,FPT(Q)*10)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to header program"
9910 LET O0=1,V0=11,V1=11,V2=11
9920 GOSUB 5500
9950 RUN "SHE.AA"
9999 END

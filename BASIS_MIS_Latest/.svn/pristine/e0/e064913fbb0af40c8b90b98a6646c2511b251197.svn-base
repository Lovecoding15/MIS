0010 REM "SAU - Purge Sales Analysis (User Interface Overlay)
0020 REM "Program SAU.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=21
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ARM-02",FILES$[3]="ARM-10"
0130 LET FILES$[4]="ART-03",FILES$[5]="ART-13",FILES$[6]="IVM-01"
0135 LET FILES$[7]="IVM-02",FILES$[8]="IVW-05",FILES$[9]="SAM-01"
0140 LET FILES$[10]="SAM-02",FILES$[11]="SAM-03",FILES$[12]="SAM-04"
0145 LET FILES$[13]="SAM-05",FILES$[14]="SAM-06",FILES$[15]="SAM-07"
0150 LET FILES$[16]="SAM-08",FILES$[17]="SAM-09",FILES$[18]="SAM-10"
0155 LET FILES$[19]="SAM-11",FILES$[20]="SAM-21",FILES$[21]="SYS-01"
0160 CALL "SYC.DA",1,1,21,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0165 IF STATUS>0 THEN GOTO 9900
0170 LET ARM01_DEV=CHANNELS[1],ARM02_DEV=CHANNELS[2],ARM10_DEV=CHANNELS[3]
0175 LET ART03_DEV=CHANNELS[4],ART13_DEV=CHANNELS[5],IVM01_DEV=CHANNELS[6]
0180 LET IVM02_DEV=CHANNELS[7],IVW05_DEV=CHANNELS[8],SAM01_DEV=CHANNELS[9]
0185 LET SAM02_DEV=CHANNELS[10],SAM03_DEV=CHANNELS[11],SAM04_DEV=CHANNELS[12]
0190 LET SAM05_DEV=CHANNELS[13],SAM06_DEV=CHANNELS[14],SAM07_DEV=CHANNELS[15]
0195 LET SAM08_DEV=CHANNELS[16],SAM09_DEV=CHANNELS[17],SAM10_DEV=CHANNELS[18]
0197 LET SAM11_DEV=CHANNELS[19],SAM21_DEV=CHANNELS[20],SYS01_DEV=CHANNELS[21]
0200 REM " --- IOLists
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 ARS01A: IOLIST U0$,CUSTIMASK$,U2$,U3$,U4$,CUSTOMASK$
0320 SAS01A: IOLIST *,OPTPARAMS$(1),LEVPARAMS$(1)
0330 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="SA",N4$=F5$
0435 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET IVPRECISION=NUM(I2$(5,1)),ITEMLEN=NUM(I2$(1,2))
0444 IF POS(I3$(3,1)="LF")>0 THEN LET LF$="Y"
0450 DIM OPTPARAMS$(11),LEVPARAMS$(11)
0460 FIND (SYS01_DEV,KEY=N0$+"SA00",DOM=9800)IOL=SAS01A
0470 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0480 LET CUSTLEN=NUM(U2$(1,2))
0500 REM " --- Initializations
0510 DIM MESSAGE$[5]
0520 LET MESSAGE$[0]="This Task Permanently Deletes Sales Analysis Records Based On The"
0530 LET MESSAGE$[1]="Selected Criteria. Please Review The Options Presented Before You"
0540 LET MESSAGE$[2]="Begin The Purge Process. You May Wish To Make A Backup Copy Of"
0550 LET MESSAGE$[3]="Your Data Before Proceeding (<Enter>=Continue):"
0560 CALL "SYC.XA",2,MESSAGE$[ALL],3,-1,-1,V$,V3
0570 IF V3=2 OR V3=4 THEN GOTO 9900
0580 DIM OPROMPTS$[11],OPTIONS$(11)
0590 LET OPTIONS$(1)=OPTPARAMS$,I0=0,PURGERECS$="Z"
0600 REM " --- Prompts
0605 LET OPROMPTS$[1]="By Customer",OPROMPTS$[2]="By Territory"
0610 LET OPROMPTS$[3]="By Salesperson",OPROMPTS$[4]="By Customer Type"
0615 LET OPROMPTS$[5]="By Customer Ship-To",OPROMPTS$[6]="By Customer SIC"
0620 LET OPROMPTS$[7]="By Product Type",OPROMPTS$[8]="By Warehouse"
0625 LET OPROMPTS$[9]="By Stocking Vendor",OPROMPTS$[10]="By Distribution Code"
0630 LET OPROMPTS$[11]="By Non-Stock Item"
0700 REM " --- Background
0710 PRINT 'SB',@(17,4),"Purge Through Year:",'SF',
0720 FOR X=1 TO 11
0730 PRINT 'SB',@(35-LEN(OPROMPTS$[X]),X+5),OPROMPTS$[X],":",'SF',
0740 PRINT @(37,X+5),OPTPARAMS$(X,1),'CL',
0760 NEXT X
0770 PRINT 'SB',@(16,18),"Purge Which Records:",'SF',@(37,18),PURGERECS$," Zero Balances",'CL',
1000 REM " --- Purge Through Year
1010 IF PURGEYEAR$="" THEN LET CURDATE$=F0$(18,6),PURGEYEAR$=CURDATE$(1,2)
1040 LET V0$="D",V1$="CY",V2$=PURGEYEAR$,V3$="",V4$="Enter The Year To Purge Through",V0=2,V1=37,V2=4
1050 GOSUB 7000
1060 IF V3=4 THEN GOTO 9900
1070 IF V$="" OR V$=FILL(V0) THEN GOTO 1000
1080 LET PURGEYEAR$=V$
1090 GOTO 3900
1100 REM " --- Sales Analysis Options
1110 LET X=I0,V0$="Y",V1$="",V2$=OPTIONS$(X,1),V3$="",V0=1,V1=37,V2=X+5
1120 LET V4$="Rebuild Sales Analysis "+OPROMPTS$[X]+" (Y/N)?"
1130 IF OPTPARAMS$(X,1)="N" THEN GOTO 3900; REM "Can't rebuild if not using
1140 GOSUB 7000
1150 ON V3 GOTO 1160,1100,3900,1100,3900
1160 LET OPTIONS$(X,1)=V$
1170 GOTO 3900
1200 REM " --- Sales Analysis Options
1220 LET V0$="S",V1$="C",V2$=PURGERECS$,V3$="ZA",V4$="Z=Purge Zero Balance Records Only     A=All Records For The Purge Year",V0=1,V1=37,V2=18
1230 GOSUB 7000
1240 IF V3=4 THEN GOTO 3900
1250 IF V$="" OR V$=FILL(V0) THEN GOTO 1200
1260 LET PURGERECS$=V$
1270 IF V$="Z" THEN PRINT @(V1+2,V2),"Zero Balance Records"
1280 IF V$="A" THEN PRINT @(V1+2,V2),"All Records For Purge Year"
1290 GOTO 3900
1300 REM " --- Get detailed segment info if only one 'Y'
1310 IF POS("Y"=OPTIONS$,1,0)>1 THEN GOTO 3900
1320 LET NMBR=4,ACTION=POS("Y"=OPTIONS$)
1330 DIM LEVELS$[NMBR],LITERALS$[NMBR],TYPES$[NMBR],IMASKS$[NMBR],OMASKS$[NMBR],PROMPTS$[NMBR],INLEN[NMBR],FLDLEN[NMBR]
1340 DIM DESCCHAN[NMBR],DESCREC$[NMBR],DESCSTR[NMBR],DESCPOS[NMBR],DESCLEN[NMBR]
1350 DIM SA_CHANNELS[2],XREFS[NMBR],PERIODS$[13],IV_MASKS$[3],IV_DESC[3]
1360 CALL "SAC.PA",ACTION,SYS01_DEV,N0$,SEGMENTS,LEVELS$[ALL],LITERALS$[ALL],TYPES$[ALL],IMASKS$[ALL],OMASKS$[ALL],PROMPTS$[ALL],INLEN[ALL],FLDLEN[ALL],DESCCHAN[ALL],DESCREC$[ALL],DESCSTR[ALL],DESCPOS[ALL],DESCLEN[ALL],SA_CHANNELS[ALL],KEYLEN,XREFS[ALL],FY$,PERIODS,PERIODS$[ALL],IV_MASKS$[ALL],IV_DESC[ALL],IVM14_DEV,STATUS
1370 IF STATUS OR SEGMENTS<1 THEN GOTO 9900
1380 LET SAM00_DEV=SA_CHANNELS[1],SAM_DEV=SA_CHANNELS[1]
1390 LET CURYEAR=1900+FNYY_YEAR(FY$),PRIYEAR=CURYEAR-1,PRIFY$=FNYEAR_YY21$(PRIYEAR)
1400 GOSUB SHOWLITERALS
1500 REM " --- Initialize for next segment selection
1510 DIM FIELDS$[SEGMENTS]
1520 LET FIELD=1,V1=37
1550 REM " --- Initialize For Next Field
1560 LET THISPROMPT$=PROMPTS$[FIELD]+" (<F1>=All/<F3>=Lookup)"
1570 LET V2=FIELD+18
1600 REM " --- Get Field Selection for segments
1610 CALL "SAC.IA","R",TYPES$[FIELD],FIELDS$[FIELD],IMASKS$[FIELD],OMASKS$[FIELD],THISPROMPT$,FLDLEN[FIELD],V1,V2,INLEN[FIELD],N0$,DESCCHAN[FIELD],DESCREC$[FIELD],DESCSTR[FIELD],DESCPOS[FIELD],DESCLEN[FIELD],IV_DESC[ALL],IVM14_DEV,FY$,FIELD,SAM_DEV,LITERALS$[ALL],FIELDS$[ALL],V3
1620 IF V3=4 THEN GOTO 1550
1630 IF V3=1 THEN PRINT @(V1,V2),"All",'CL',; LET FIELDS$[FIELD]=""
2000 REM " --- Next Field for segments
2010 IF V3=4 THEN GOTO 4000
2020 IF V3<>2 THEN GOTO 2060
2030 IF FIELD=1 THEN GOSUB CLRLITERALS; GOTO 3900
2040 IF FIELD>1 THEN LET FIELD=FIELD-1
2050 GOTO 2070
2060 LET FIELD=FIELD+1
2070 IF FIELD>SEGMENTS THEN GOTO 4000
2080 GOTO 1550
3900 REM " --- Next Field
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1 ELSE LET V3=0
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,1100,1100,1100,1100,1100,1100,1100,1100,1100,1100,1200,1300,3990
3990 IF POS("Y"=OPTIONS$,1,0)=1 THEN GOTO 1300
4000 REM " --- Correct?
4010 LET V4$="Is The Above Information Correct (Yes/No)?",I0=0
4020 LET V0$="S",V1$="CER",V2$="",V3$="",V0=3,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4060,4000,4000,4000,9900
4060 IF V$="YES" THEN GOTO 4100
4070 IF V$<>"NO" AND V$(1,1)<>"N" THEN GOTO 4000
4080 GOSUB CLRLITERALS
4090 GOTO 1000
4100 REM " --- Run Overlay
4190 RUN "SAU.AB"
5000 REM " --- Display
5010 FOR X=1 TO 11
5020 LET X$=""
5050 PRINT @(37,X+5),OPTPARAMS$(X,1),'CL',
5080 NEXT X
5090 RETURN 
6100 REM " --- Display Literals for segments
6105 SHOWLITERALS: 
6120 FOR CNT=1 TO SEGMENTS
6130 PRINT 'SB',@(35-LEN(LITERALS$[CNT]),CNT+18),LITERALS$[CNT]+":",'SF',
6160 NEXT CNT
6190 RETURN 
6200 REM " --- Clear segment literals section
6205 CLRLITERALS: 
6220 FOR CNT=1 TO SEGMENTS
6230 PRINT @(0,CNT+18),'CL',
6260 NEXT CNT
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8090 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

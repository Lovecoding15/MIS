0010 REM "ARM - Customer Maintenance (Name & Address Overlay)"
0020 REM "Program ARM.MB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0060 ON O0 GOTO 0070,0070,0070,DEL_CUST
0200 REM " --- IOLists"
0210 ARM01A: IOLIST A0$(1),A1$(1)
0220 ARM02A: IOLIST B0$,B1$
0230 ARM04A: IOLIST C0$
0240 ARM04D: IOLIST C0$
0250 ARM04E: IOLIST C0$
0260 ARM04L: IOLIST C0$
0700 REM " --- Background"
0720 PRINT 'SB',@(15,5),"Name & Address",@(6,6),"Address:",@(3,11),"Zip/Postal:",@(4,12),"Telephone:",@(30),"Ext:",@(44,9),"Resale No:",@(46,6),"Alt Seq:",@(42,7),"Date Opened:",@(45,8),"Ship-Via:",@(44,10),"Fax Phone:",@(46,11),"Contact:",@(47,12),"Retain:",@(3,13),"D&B Number:",@(45,13),"SIC Code:",
0790 PRINT 'SF',
0900 REM " --- Init File"
0920 IF V$<>"R" AND O0=0 THEN GOTO 2000
0940 GOSUB 5000
0960 GOTO 4000
1000 REM " --- Remove X-Ref"
1010 GOSUB 6700
2000 REM " --- Name"
2020 LET V0$="S",V1$="",V2$=A1$(1,30),V3$="",V0=30,V1=15,V2=4,I0=0
2040 GOSUB 7000
2060 LET A1$(1,30)=V$
2080 PRINT 'SB',@(15,3),FNF$(A0$(3,P[0]),M0$),@(V1,V2),A1$(1,30),'SF',
2090 GOTO 3900
2100 REM " --- Address"
2110 LET X=I0-1
2120 LET V0$="S",V1$="",V2$=A1$(X*24+31,24),V0=24,V1=15,V2=X+6
2130 GOSUB 7000
2150 IF V3=4 OR V3=2 THEN GOTO 3900
2180 LET A1$(X*24+31,24)=V$
2190 GOTO 3900
2300 REM " --- Zip Code"
2320 LET V0$="S",V1$="",V2$=A1$(103,P[2]),V3$="",V0=P[2],V1=15,V2=11
2340 GOSUB 7000
2350 IF LEN(V$)<9 THEN LET V$=V$+FILL(9-LEN(V$))
2360 LET A1$(103,9)=V$
2370 PRINT @(V1,V2),FNZ$(A1$(103))
2390 GOTO 3900
2400 REM " --- Phone"
2420 LET V0$="H",V1$="",V2$=A1$(112,10),V3$=M8$,V0=10,V1=15,V2=12
2440 GOSUB 7000
2460 LET A1$(112,10)=V$
2490 GOTO 3900
2500 REM " --- Ext"
2510 LET V0$="Z",V1$="",V2$=A1$(122,4),V0=4,V1=35,V2=12
2520 GOSUB 7000
2530 LET A1$(122,4)=V$
2540 GOTO 3900
2550 REM " --- D&B Number "
2560 LET V0$="S",V1$="",V2$=A1$(248,9),V0=9,V1=15,V2=13,V4$="Enter Dun & Bradstreet Number"
2570 GOSUB 7000
2580 LET A1$(248,9)=V$
2590 GOTO 3900
2600 REM " --- Resale #"
2620 LET V0$="S",V1$="",V2$=A1$(126,20),V0=20,V1=55,V2=9
2640 GOSUB 7000
2660 LET A1$(126,20)=V$
2690 GOTO 3900
2700 REM " --- Alt Sort"
2710 IF O8=1 THEN LET A1$(146,10)=A1$(1,10)
2720 LET V0$="S",V1$="C",V2$=A1$(146,10),V0=10,V1=55,V2=6,V4$="Enter Alternate Sequence (<F1>=Last Name)"
2740 GOSUB 7000
2750 IF V3=1 THEN CALL "SYC.NA",A1$(1,30),V$
2760 LET A1$(146,10)=V$
2765 PRINT @(V1,V2),A1$(146,10)
2770 LET O8=0
2790 GOTO 3900
2800 REM " --- Date Open"
2820 LET V0$="D",V1$="",V2$=A1$(156,3),V0=3,V1=55,V2=7
2830 IF V2$="   " THEN LET V2$=F0$(7,3)
2840 GOSUB 7000
2860 LET A1$(156,3)=V$
2890 GOTO 3900
2900 REM " --- Ship Via"
2910 LET V0$="S",V2$=A1$(159,10),V0=10,V1=55,V2=8
2920 LET V1$="C",V4$="Enter Preferred Shipping Method"
2930 IF SHIP$="Y" THEN LET V4$=V4$+" (<F3>=SpeedShip Service Lookup)"
2940 GOSUB 7000
2950 IF SHIP$="Y" THEN IF V3=3 THEN CALL "SHC.LK",V1,V2,"A",0,V$
2960 LET A1$(159,10)=V$
2970 PRINT @(V1,V2),A1$(159,10)
2990 GOTO 3900
3000 REM " --- Alt Phone or Fax Number"
3020 LET V0$="H",V1$="",V2$=A1$(169,10),V3$=M8$,V0=10,V1=55,V2=10
3040 GOSUB 7000
3060 LET A1$(169,10)=V$
3090 GOTO 3900
3100 REM " --- Address 4"
3120 LET V0$="S",V1$="",V2$=A1$(179,24),V0=24,V1=15,V2=9
3130 GOSUB 7000
3140 LET A1$(179,24)=V$
3190 GOTO 3900
3200 REM " --- Address 5"
3220 LET V0$="S",V1$="",V2$=A1$(203,24),V0=24,V1=15,V2=10
3230 GOSUB 7000
3240 LET A1$(203,24)=V$
3290 GOTO 3900
3300 REM " --- Retain?
3310 LET V0$="Y",V1$="",V2$=A1$(227,1),V0=1,V1=55,V2=12,V4$="Y=Yes    N=No"
3320 GOSUB 7000
3330 LET A1$(227,1)=V$
3340 GOTO 3900
3350 REM " --- SIC Code "
3360 LET V0$="S",V1$="",V2$=A1$(257,8),V0=8,V1=55,V2=13,V4$="Enter Standard Industry Code"
3370 GOSUB 7000
3380 LET A1$(257,8)=V$
3390 GOTO 3900
3400 REM " --- Contact"
3420 LET V0$="S",V1$="C",V2$=A1$(228,20),V4$="Enter Contact Name (NOTE: 1st 10 Characters Are Used For Lookups)",V0=20,V1=55,V2=11
3430 GOSUB 7000
3440 LET A1$(228,20)=V$
3490 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 IF ABS(I0)>1000 THEN GOTO 3990
3960 ON I0 GOTO 2000,2100,2100,2100,3100,3200,2300,2400,2500,2550,2700,2800,2900,2600,3000,3400,3300,3350,3990
4000 REM " --- Options"
4010 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/##/Restore/Delete)?",V0=7,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4026 PRINT @(0,16),'CL',@(0,17),'CL',@(0,22),'CL',
4030 LET I=NUM(V$,ERR=4040)
4035 IF I>0 THEN GOTO 4800
4040 IF V$="DELETE" THEN GOTO DEL_CUST
4045 IF V$="R" OR V$="RESTORE" THEN FIND (ARM01_DEV,KEY=A0$(1,K9),DOM=4010)IOL=ARM01A; GOTO 0700
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Write"
4120 WRITE (ARM01_DEV,KEY=A0$(1,K9))IOL=ARM01A
4135 LET C0$=N0$+"A"+A1$(146,10)+A0$(3,6)
4140 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04A
4145 LET C0$=N0$+"E"+A1$(103,9)+A0$(3,6)
4150 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04E
4155 LET C0$=N0$+"D"+A1$(228,10)+A0$(3,6)
4160 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04D
4165 LET C0$=N0$+"L"+A1$(112,10)+A0$(3,6)
4170 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04L
4172 LET C0$=N0$+"M"+A1$(248,9)+A0$(3,6)
4173 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04L
4174 LET C0$=N0$+"N"+A1$(257,8)+A0$(3,6)
4175 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04L
4180 IF NEW_CUST$="YES" THEN GOTO 4190
4185 LET O0=1
4186 RUN "ARM.MA"
4190 RUN "ARM.MC"
4200 REM " --- Delete
4205 DEL_CUST: 
4210 GOSUB 6000
4220 REMOVE (ARM01_DEV,KEY=A0$(1,K9),DOM=4230)
4230 REMOVE (ARM04_DEV,KEY=N0$+"A"+A1$(146,10)+A0$(3,6),DOM=4233); REM "Alt Seq"
4233 REMOVE (ARM04_DEV,KEY=N0$+"E"+A1$(103,9)+A0$(3,6),DOM=4236); REM "Zip Code"
4236 REMOVE (ARM04_DEV,KEY=N0$+"D"+A1$(228,10)+A0$(3,6),DOM=4238); REM "Contact"
4238 REMOVE (ARM04_DEV,KEY=N0$+"L"+A1$(112,10)+A0$(3,6),DOM=4239); REM "Phone #"
4239 REMOVE (ARM04_DEV,KEY=N0$+"M"+A1$(248,9)+A0$(3,6),DOM=4240); REM "D&B #"
4240 REMOVE (ARM04_DEV,KEY=N0$+"N"+A1$(257,8)+A0$(3,6),DOM=4241); REM "SIC Code"
4245 READ (ARM02_DEV,KEY=A0$,DOM=4250)
4250 LET K$=KEY(ARM02_DEV,END=4350)
4260 IF K$(1,8)<>A0$ THEN GOTO 4350
4265 READ (ARM02_DEV)IOL=ARM02A
4270 REMOVE (ARM02_DEV,KEY=K$)
4274 REMOVE (ARM04_DEV,KEY=N0$+"B"+B1$(1,3)+A0$(3),DOM=4278); REM "Salesperson"
4278 REMOVE (ARM04_DEV,KEY=N0$+"C"+B1$(21,3)+A0$(3),DOM=4282); REM "Territory"
4282 REMOVE (ARM04_DEV,KEY=N0$+"F"+B1$(4,2)+A0$(3),DOM=4286); REM "Terms"
4286 REMOVE (ARM04_DEV,KEY=N0$+"G"+B1$(6,2)+A0$(3),DOM=4290); REM "Discount"
4290 REMOVE (ARM04_DEV,KEY=N0$+"H"+B1$(8,2)+A0$(3),DOM=4294); REM "Distribution"
4294 REMOVE (ARM04_DEV,KEY=N0$+"J"+B1$(24,4)+A0$(3),DOM=4298); REM "Pricing"
4298 REMOVE (ARM04_DEV,KEY=N0$+"K"+B1$(32,2)+A0$(3),DOM=4300); REM "Tax"
4320 GOTO 4250
4350 READ (ARM05_DEV,KEY=A0$,DOM=4360)
4360 LET K$=KEY(ARM05_DEV,END=4400)
4370 IF K$(1,8)<>A0$ THEN GOTO 4400
4380 REMOVE (ARM05_DEV,KEY=K$)
4390 GOTO 4360
4400 READ (ARM03_DEV,KEY=A0$,DOM=4410)
4410 LET K$=KEY(ARM03_DEV,END=4450)
4420 IF K$(1,8)<>A0$ THEN GOTO 4450
4430 REMOVE (ARM03_DEV,KEY=K$,DOM=4450)
4440 GOTO 4410
4455 READ (ARM09_DEV,KEY=A0$,DOM=4460)
4460 LET K$=KEY(ARM09_DEV,END=4480)
4465 IF K$(1,8)<>A0$ THEN GOTO 4480
4470 REMOVE (ARM09_DEV,KEY=K$,DOM=4480)
4475 GOTO 4455
4485 IF O0=3 THEN GOTO 6500 ELSE LET O0=2
4490 RUN "ARM.MA"
4700 REM " --- Restore
4800 REM " --- ##
4820 LET I0=I-2
4830 GOSUB 6700
4890 GOTO 3900
4985 IF O0=3 THEN GOTO 6500 ELSE LET O0=2
4990 RUN "ARM.MA"
5000 REM " --- Display
5010 PRINT 'SB',@(15,4),A1$(1,30),'SF',
5020 PRINT @(15,6),A1$(31,24),@(15,7),A1$(55,24),@(15,8),A1$(79,24),@(15,9),A1$(179,24),@(15,10),A1$(203,24),@(15,11),FNZ$(A1$(103)),@(15,12),FNF$(A1$(112,10),M8$),@(35,12),A1$(122,4),@(55,9),A1$(126,20),@(55,6),A1$(146,10),@(55,7),FNB$(A1$(156,3)),@(55,8),A1$(159,10),@(55,10),FNF$(A1$(169,10),M8$),@(55,11),A1$(228,20),@(55,12),A1$(227,1),@(15,13),A1$(248,9),@(55,13),A1$(257,8),
5090 RETURN 
6000 REM " --- Check for Open Invoices
6030 READ (ART01_DEV,KEY=N0$+"  "+A0$(3,6),DOM=6040)
6040 LET K0$=KEY(ART01_DEV,END=6200)
6050 IF K0$(1,2)<>N0$ THEN GOTO 6200
6060 IF K0$(5,6)<>A0$(3,6) THEN GOTO 6200
6100 REM " --- Open Invoices --  No Customer Deletion Allowed
6110 LET V4$="Open Invoices Exist - Cannot Delete Customer"
6120 LET TITLE=2
6130 GOSUB SHOW_MESSAGE
6150 IF O0=3 THEN EXITTO 6500
6190 EXITTO 4000
6200 REM " --- No Open Invoices
6210 IF OP$<>"Y" THEN GOTO 6240
6220 GOSUB 6300
6230 GOSUB 6600
6240 LET V$="DELETE"
6250 RETURN 
6300 REM " --- Check for Open Orders
6305 LET Q9=0
6330 READ (ARE03_DEV,KEY=N0$+"  "+A0$(3,6),DOM=6340)
6340 LET K0$=KEY(ARE03_DEV,END=6460)
6350 IF K0$(1,2)<>N0$ THEN GOTO 6460
6360 IF K0$(5,6)<>A0$(3,6) THEN GOTO 6460
6370 LET V4$="Open Orders Exist - Cannot Delete Customer"
6375 LET TITLE=2
6380 GOSUB SHOW_MESSAGE
6390 LET Q9=1
6480 IF Q9=1 THEN IF O0=3 THEN EXITTO 6500 ELSE EXITTO 4000
6490 RETURN 
6500 REM " --- No Distribution Codes. Return to menu
6510 LET V4$="Distribution Codes Must Be Defined Before Customer Entry"
6520 LET TITLE=1
6530 GOSUB SHOW_MESSAGE
6590 GOTO 9900
6600 REM " --- Check for Historical Invoice
6620 READ (ART03_DEV,KEY=N0$+"  "+A0$(3,6),DOM=6630)
6630 LET K1$=KEY(ART03_DEV,END=6690)
6640 IF POS(A0$(3,6)=K1$)<>5 THEN GOTO 6690
6645 REM " NOTE: We know it's historical (not open) cuz already checked ART-01
6650 CALL "SYC.YN",1,"Historical Invoices Exist.  OK to Delete",1,V$,V3
6670 IF V$="N" THEN EXITTO 4000
6695 RETURN 
6700 REM " --- Extract and Remove X-References
6710 LET O0=0
6720 EXTRACT (ARM01_DEV,KEY=A0$(1,K9),DOM=6730)
6730 REMOVE (ARM04_DEV,KEY=N0$+"A"+A1$(146,10)+A0$(3,6),DOM=6740); REM "Alt Seq"
6740 REMOVE (ARM04_DEV,KEY=N0$+"E"+A1$(103,9)+A0$(3,6),DOM=6750); REM "Zip Code"
6750 REMOVE (ARM04_DEV,KEY=N0$+"D"+A1$(228,10)+A0$(3,6),DOM=6760); REM "Contact"
6760 REMOVE (ARM04_DEV,KEY=N0$+"L"+A1$(112,10)+A0$(3,6),DOM=6770); REM "Phone #"
6770 REMOVE (ARM04_DEV,KEY=N0$+"M"+A1$(248,9)+A0$(3,6),DOM=6775); REM "D&B #"
6775 REMOVE (ARM04_DEV,KEY=N0$+"N"+A1$(257,8)+A0$(3,6),DOM=6776); REM "SIC Code"
6790 RETURN 
6900 REM " --- Standard Warning/Error"
6905 SHOW_MESSAGE: 
6910 DIM MESSAGE$[1]
6920 LET MESSAGE$[0]=V4$+" (<Enter>=Continue)"
6930 CALL "SYC.XA",TITLE,MESSAGE$[ALL],0,22,-1,V$,V3
6940 IF TITLE=1 THEN EXITTO 9900
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8090 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9220 LET O0=0; RUN "ARM.MA"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

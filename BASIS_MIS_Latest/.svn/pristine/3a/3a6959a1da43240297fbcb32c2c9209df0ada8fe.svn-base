0001 SETERR ERRMSG
0010 REM <rev21_updt.bbx>
0020 SETERR INVOKED_PGM
0030 LET CALLED_PGM=0
0040 ENTER SERIALNUM_IN$,KEY_IN$,AUTHNUM$; LET CALLED_PGM=1
0050 LET SERIALNUM_IN$=CVS(SERIALNUM_IN$,7)+FILL(20),SERIALNUM_IN$=SERIALNUM_I
0050:N$(1,20),KEY_IN$=CVS(KEY_IN$,11)
0060 GOTO END_INPUT
0070 INVOKED_PGM: 
0080 LET ARGNUM=1
0090 GETARGV: 
0100 LET ARG$=ARGV(ARGNUM,ERR=END_INPUT)
0110 SWITCH ARGNUM
0120 CASE 1; LET SERIALNUM_IN$=CVS(ARG$,7)+FILL(20),SERIALNUM_IN$=SERIALNUM_IN
0120:$(1,20); BREAK
0130 CASE 2; LET KEY_IN$=CVS(ARG$,11); BREAK
0140 CASE DEFAULT; BREAK
0150 SWEND
0160 LET ARGNUM=ARGNUM+1
0170 GOTO GETARGV
0180 END_INPUT: 
0190 SETERR ERRMSG
0200 LET AUTHNUM$="",MSG$="",HIREV=2.9,LOWREV=2
0210 LET ADATA$=STBL("AON")+"ADATA/"
0220 IF CVS(SERIALNUM_IN$,3)="" THEN LET MSG$="No serial number"; GOTO ERRMSG
0230 IF LEN(KEY_IN$)<>13 OR KEY_IN$=FILL(13,"x") THEN LET MSG$="Invalid activa
0230:tion key"; GOTO ERRMSG
0240 DIM SNM01$:"SERIAL_NBR:C(20*),SERIAL_DESC:C(60*),PRODUCT:C(3),PLATFORM:C(
0240:3),OS_LEVEL:C(3),PRODUCT_REV:C(3),LICENSE_TYPE:C(3),MEDIA_TYPE:C(3*),ACTI
0240:VE_FLAG:C(1),ORIG_SALE:C(3),CUSTOMER_NBR:C(6),PORT_ID:C(5),FIRM_ID:C(2),D
0240:EALER_STR:C(18*),COMMENT:C(60*),USERS:N(5*),DEALER_NUM_1:N(1*),DEALER_NUM
0240:_2:N(1*)"
0250 DIM SNT01$:"SERIAL_NBR:C(20),TRANS_DATE:C(3),SN_DET_SEQ:C(5*),CUSTOMER_NB
0250:R:C(6),INV_NBR:C(7),SEQUENCE_NBR:C(3),TYPE_OF_SALE:C(3),PRODUCT_REV:C(3),
0250:AUTH_CODE:C(15),SERVER_NBR:C(20*),FIRM_ID:C(2),ACTION:C(1),FIXED_FLOAT:C(
0250:1),DEALER_STR:C(36*),USERS:N(5*),EXT_PRICE:N(7*),LICENSE_CNT:N(7*),NUM1:N
0250:(1*),NUM2:N(1*),NUM3:N(1*)"
0260 LET OK=0,SNM01=UNT
0270 OPEN (SNM01)ADATA$+"SNM-01"
0280 EXTRACT RECORD (SNM01,KEY=SERIALNUM_IN$,ERR=SNM01END)SNM01$; LET OK=1
0290 SNM01END: 
0300 IF OK=0 THEN GOTO FIND_OEM
0310 IF SNM01.ACTIVE_FLAG$="N" THEN LET MSG$="Inactive serial number"; GOTO ER
0310:RMSG
0320 LET FIRM$=SNM01.FIRM_ID$,RK$=FIRM$+SNM01.PRODUCT_REV$,CSM04=UNT
0330 LET MSG$="Invalid revision"
0340 OPEN (CSM04)ADATA$+"CSM-04"
0350 LET CSMKEY$=""; READ (CSM04,KEY=FIRM$,DOM=CSMKEY)
0360 CSMKEY: READ (CSM04,END=CSMKEY_END)R0$,R1$,R2$; IF R0$(1,2)<>FIRM$ THEN G
0360:OTO CSMKEY_END
0370 IF POS("2."=R1$) AND R2$(1,1)="L" THEN LET CSMKEY$=CSMKEY$+R0$(3,3)
0380 GOTO CSMKEY
0390 CSMKEY_END: READ (CSM04,KEY=RK$,DOM=ERRMSG)*,RD$
0400 LET RD$=CVS(RD$,3),P=POS(" "=RD$); IF P=0 THEN LET P=LEN(RD$)
0410 LET RV$=CVS(RD$(1,P),2)
0420 GOSUB CHECK_REV; IF WRONG_REV THEN GOTO ERRMSG
0430 LET CSM13=UNT; OPEN (CSM13)ADATA$+"CSM-13"
0440 LET MSG$="Invalid port"
0450 READ (CSM13,KEY=FIRM$+"C"+SNM01.OS_LEVEL$,DOM=RCSM13)
0460 RCSM13: READ (CSM13,END=ERRMSG)K1$,R1$
0470 IF POS(FIRM$+"C"+SNM01.OS_LEVEL$=K1$)=0 THEN GOTO ERRMSG
0480 LET P=POS(R1$(1,3)=CSMKEY$,3); IF P=0 THEN GOTO RCSM13
0490 LET KEYMATCH=0,SNT01=UNT,CSMKEY$=CSMKEY$(P,3)
0500 OPEN (SNT01)ADATA$+"SNT-01"
0510 DIM HOLD_DETAIL$:FATTR(SNT01$)
0520 READ (SNT01,KEY=SERIALNUM_IN$,ERR=READ_SNT01)
0530 READ_SNT01: 
0540 LET SNT01KEY$=KEY(SNT01,END=SNT01END)
0550 IF SNT01KEY$(1,20)<>SERIALNUM_IN$ THEN GOTO SNT01END
0560 READ RECORD (SNT01,KEY=SNT01KEY$)SNT01$
0570 IF SNT01.ACTION$="D" THEN GOTO READ_SNT01
0580 LET HOLD_DETAIL$=FIELD(SNT01$)
0590 IF CVS(SNT01.AUTH_CODE$,2)=KEY_IN$ THEN LET KEYMATCH=1 ELSE IF LEN(CVS(SN
0590:T01.AUTH_CODE$,2))=13 THEN LET KEYMATCH=0
0600 GOTO READ_SNT01
0610 SNT01END: 
0620 IF HOLD_DETAIL.SERIAL_NBR$<>SERIALNUM_IN$ THEN LET MSG$="Serial number de
0620:tail record is missing! Call MIS."; GOTO ERRMSG
0630 LET SNT01KEY$=HOLD_DETAIL$(1,28)
0640 EXTRACT RECORD (SNT01,KEY=SNT01KEY$,ERR=ERRMSG)SNT01$
0650 REM Is this already at new rev? If so, get the auth# and quit.
0660 IF KEYMATCH AND NUM(RV$)>=2.1 THEN LET AUTHNUM$=CVS(SNT01.AUTH_CODE$,2); 
0660:IF LEN(AUTHNUM$)=10 THEN GOTO EXIT
0670 IF KEYMATCH=0 THEN LET MSG$="Activation key not found"; GOTO ERRMSG
0690 LET X$="0000000"+CSMKEY$,X$=X$(LEN(X$)-6,7),SNT01.INV_NBR$=X$
0700 LET D$=DATE(0:"%Mz%Dz%Y"),YRA=NUM(D$(7,2)); IF YRA<10 THEN LET YRA=YRA+10
0700:0
0710 LET SNT01.TRANS_DATE$=CHR(YRA+32)+CHR(NUM(D$(1,2))+32)+CHR(NUM(D$(3,2))+3
0710:2)
0720 LET SNT01.TYPE_OF_SALE$="UNC",SNT01.PRODUCT_REV$=CSMKEY$
0730 LET L4$=CVS(SERIALNUM_IN$,2),L4$=L4$(LEN(L4$)-3,4)
0740 LET AUTHNUM$=HOLD_DETAIL.INV_NBR$(3,5)+L4$
0750 LET FLOAT=1; IF POS(SNM01.PRODUCT$(1,2)="VPOD",2) AND SNT01.FIXED_FLOAT$<
0750:>"F" AND SNM01.USERS<2 THEN LET FLOAT=0
0760 IF FLOAT THEN LET SNT01.FIXED_FLOAT$="F" ELSE LET SNT01.FIXED_FLOAT$="X"
0770 IF SNT01.FIXED_FLOAT$="F" THEN LET AUTHNUM$=AUTHNUM$+"1" ELSE LET AUTHNUM
0770:$=AUTHNUM$+"0"
0780 LET SNT01.AUTH_CODE$=AUTHNUM$,SNT01.EXT_PRICE=0,SNT01.USERS=0,SNT01.LICEN
0780:SE_CNT=0
0790 LET SNT01.FIRM_ID$=FIRM$
0800 WRITE RECORD (SNT01,KEY=SNT01$(1,28))SNT01$
0810 LET SNM01.PRODUCT_REV$=CSMKEY$
0820 WRITE RECORD (SNM01,KEY=SNM01$(1,20))SNM01$
0830 GOTO EXIT
0840 FIND_OEM: 
0860 LET MSG$="Serial number not found"
0870 GOTO ERRMSG; REM no oem exchange
1260 CHECK_REV: 
1270 LET R$="",WRONG_REV=1
1280 FOR R=1 TO LEN(RV$); IF POS(RV$(R,1)=".0123456789") THEN LET R$=R$+RV$(R,
1280:1) FI ; NEXT R
1290 LET R=NUM(R$,ERR=END_CHECK_REV)
1300 IF R>=LOWREV AND R<HIREV THEN LET WRONG_REV=0
1310 END_CHECK_REV: 
1320 RETURN
1330 ERRMSG: 
1340 IF MSG$="" THEN LET MSG$="Err "+STR(ERR)+" in "+STR(TCB(5))
1350 LET AUTHNUM$="ERROR "+MSG$
1360 EXIT: 
1370 SETERR EXIT2
1380 IF SNM01 THEN CLOSE (SNM01)
1390 IF SNT01 THEN CLOSE (SNT01)
1400 IF CSM04 THEN CLOSE (CSM04)
1410 IF CSM13 THEN CLOSE (CSM13)
1420 IF OEM THEN CLOSE (OEM)
1430 IF AN THEN CLOSE (AN)
1440 LET LOGENTRY$=$22$+CVS(SERIALNUM_IN$,2)+" "+KEY_IN$+" "+AUTHNUM$+" "+DATE
1440:(0)+$22$
1450 LET A=SCALL("echo "+LOGENTRY$+" >> "+ADATA$+"rev21_updt.log")
1460 EXIT2: 
1470 IF CALLED_PGM THEN EXIT
1480 PRINT CVS(SERIALNUM_IN$,2); PRINT AUTHNUM$
1490 RELEASE

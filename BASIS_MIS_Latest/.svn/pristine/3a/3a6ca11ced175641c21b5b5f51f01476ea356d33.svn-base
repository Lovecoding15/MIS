0010 REM "WOS - Work Order Type Code Maintenance"
0020 REM "Program WOM.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Mar99 KJS: Y2K year processing (470,485,8145-8190)
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="WOM-10",FILES$[2]="SYS-01",FILES$[3]="GLM-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET WOM10_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 WOM10A: IOLIST A0$(1),A1$(1)
0220 GLM01A: IOLIST X$,G1$(1),X$,X$
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N4$=F5$
0425 FIND (SYS01_DEV,KEY=N0$+"WO00",DOM=9800)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[1],G[4]
0460 FOR X=0 TO 4
0470 LET G[X]=NUM(G2$(X*2+1,2),ERR=0471)
0480 NEXT X
0485 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2))
0500 REM " --- Init Data"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 DIM A0$(39),A1$(10*G[4],"0")
0530 LET K0=1,K9=5,G5=LEN(G5$)
0540 PRINT @(0,X),'CL'
0550 LET A0$(1)=N0$+"A"
0560 DIM INFO$[20]
0570 CALL "SYC.VA","WO",INFO$[ALL]
0580 LET GL$=INFO$[9]
0600 REM " --- Additional File Opens"
0610 IF GL$="N" THEN GOTO 0700
0620 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS THEN GOTO 9900
0640 LET GLM01_DEV=CHANNELS[3]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,7,0
0720 PRINT 'SB',@(4,3),"Work Order Type Code:",@(13,5),"Description:",@(0,9),"WO Category (I/N/R):",@(32,9),"Close At Standards/Actuals?:",
0740 IF GL$="Y" THEN PRINT @(6,11),"Work In Process:",@(5,12),"Close To Account:",@(9,13),"Direct Labor: ",@(7,14),"Overhead Labor: ",@(12,15),"Purchases:",@(2,16),"Direct Ops Variance:",@(0,17),"Overhead Ops Variance:",@(3,18),"Materials Variance:",@(0,19),"Subcontracts Variance:",'SF',
0800 REM " --- Options"
0900 REM " --- Init File"
0920 READ (WOM10_DEV,KEY=A0$(1,3),DOM=0930)
1000 REM " --- Key"
1010 PRINT 'CF',
1015 LET V2$="",V2$=KEY(WOM10_DEV,END=1030)
1020 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1025 IF V2$<>"" THEN LET V2$=V2$(4)
1030 LET V0$="S",V1$="KEC",V3$="",V0=2,V1=26,V2=3,V4$="Enter Up To 2 Characters (<F3>=Lookup)"
1032 LET HOLD$=V2$
1035 GOSUB 7000
1040 IF V3<>3 THEN GOTO 1080
1045 LET CHANNEL=WOM10_DEV,RECORD$="A",NUMBER=2,TITLE$="Work Order Type Lookup",COLUMN=5,ROW=5,SEL$=""
1050 DIM DESC$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
1055 LET DESC$[0]="Code",DESC$[1]="Description",DESC$[2]="Cat",FIELD[0]=1,FIELD[1]=1,FIELD[2]=1,POSITION[0]=4,POSITION[1]=6,POSITION[2]=26,LENGTH[0]=2,LENGTH[1]=20,LENGTH[2]=1
1060 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESC$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,SEL$
1065 IF SEL$="" THEN LET V2$=HOLD$; GOTO 1035
1070 LET V$=SEL$
1075 PRINT @(V1,V2),V$
1080 IF V3=4 THEN GOTO 4500
1085 IF V$="" THEN GOTO 0900
1090 LET A0$(4)=V$
1200 REM " --- Find Key"
1210 LET O9=0
1220 FIND (WOM10_DEV,KEY=A0$(1,K9),DOM=1400)IOL=WOM10A
1300 REM " --- Display"
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record"
1420 LET V4$="Is This A New Record:"
1430 CALL "SYC.YN",1,V4$,1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-up"
1510 LET A0$(6)="",A1$(1)=""
2000 REM " --- Description"
2010 LET O9=1
2020 LET V0$="S",V1$="",V2$=A0$(6,20),V3$="",V4$="",V0=20,V1=26,V2=5,I0=0
2040 GOSUB 7000
2060 LET A0$(6,20)=V$
2090 GOTO 3900
2200 REM " --- W/O Category
2220 LET V0$="S",V1$="C",V2$=A0$(26,1),V3$="IRN",V4$="I=Inventory Item   N=Non-Stock Item   R=Recurring (Permanent)",V0=1,V1=21,V2=9
2240 GOSUB 7000
2280 LET A0$(26,1)=V$
2290 GOTO 3900
2400 REM " --- Accounts
2405 IF GL$="N" THEN GOTO 2590
2410 LET X=I0-3
2420 LET V0=G[4],V1=23,V2=8+I0; DIM G1$(35)
2440 LET V0$="M",V1$="RC",V2$=A1$(X*G[4]+1,G[3]),V3$=G5$,V4$="Enter A Valid G/L Account Number (<F3>=Lookup)"
2450 IF A0$(26,1)="I" AND X=1 THEN LET A1$(X*G[4]+1,G[3])=J0$,G1$(1)="From Appropriate Inventory Account"; GOTO 2520
2460 GOSUB 7000
2470 IF V3=4 OR V3=2 THEN GOTO 3900
2480 IF V3=3 THEN CALL "SYC.LG",V$; LET V3=1
2500 FIND (GLM01_DEV,KEY=N0$+V$,DOM=2420)IOL=GLM01A
2510 LET A1$(X*G[4]+1,G[4])=V$
2520 PRINT @(V1,V2),FNF$(A1$(X*G[4]+1,G[3]),G5$)," ",G1$(1,35)
2590 GOTO 3900
2600 REM " --- Standards?
2620 LET V0$="S",V1$="RC",V2$=A0$(27,1),V3$="SA",V4$="S=Receive At Standard Cost   A=Receive At Work Order Cost",V0=1,V1=62,V2=9
2630 IF P3$(8,1)="S" AND A0$(26,1)="I" THEN LET A0$(27,1)="S"; PRINT @(V1,V2),A0$(27,1),; GOTO 2690
2640 GOSUB 7000
2680 LET A0$(27,1)=V$
2690 GOTO 3900
3000 REM " --- G/L Accts"
3005 IF GL$="N" THEN GOTO 3190
3010 IF A0$(27,1)<>"S" THEN LET A1$(51)=""; GOTO 3240
3020 LET X=I0-8
3040 LET V0$="M",V1$="RC",V2$=A1$(X*G[4]+51,G[3]),V3$=G5$,V0=G[4],V1=23,V2=8+I0,V4$="Enter A Valid G/L Account Number, (<F3>=Lookup)"
3060 GOSUB 7000
3080 IF V3=4 OR V3=2 THEN GOTO 3900
3090 IF V3=3 THEN CALL "SYC.LG",V$; LET V3=1
3110 DIM G1$(35)
3120 FIND (GLM01_DEV,KEY=N0$+V$,DOM=3040)IOL=GLM01A
3130 LET A1$(X*G[4]+51,G[4])=V$
3140 PRINT @(23,8+I0),FNF$(A1$(X*G[4]+51,G[3]),G5$)," ",G1$(1,35)
3190 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 IF ABS(I0)>1000 THEN GOTO 3990
3960 ON I0 GOTO 2000,2200,2600,2400,2400,2400,2400,2400,3000,3000,3000,3000,3990
3990 LET I0=K0
4000 REM " --- Options"
4020 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N/##/Delete)?",V0=6,V1=66,V2=22
4025 GOSUB 7000
4027 LET I=INT(NUM(V$,ERR=4030))
4028 IF I>0 THEN LET I0=I-2; GOTO 3900
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF O9=0 THEN IF V$="" THEN GOTO 1000
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 WRITE (WOM10_DEV,KEY=A0$(1,K9))IOL=WOM10A
4190 GOTO 1000
4200 REM " --- Delete"
4220 REMOVE (WOM10_DEV,KEY=A0$(1,K9),DOM=4230)
4290 GOTO 1000
4500 REM " --- Print"
4520 LET V4$="Do You Wish To Print A List Of All Records"
4530 CALL "SYC.YN",1,V4$,1,V$,V3
4540 ON POS(V$="YN") GOTO 4500,4550,9900
4560 LET O0=1
4580 RUN "WOM.AB"
5000 REM " --- Display"
5010 PRINT @(26,5),A0$(6,20),@(21,9),A0$(26,1),@(62,9),A0$(27,1),
5015 IF GL$="N" THEN GOTO 5090
5020 FOR X=0 TO 8
5030 DIM G1$(35)
5040 IF A0$(26,1)="I" AND X=1 THEN LET A1$(X*G[4]+1,G[3])=J0$,G1$(1)="From Appropriate Inventory Account"; GOTO 5060
5045 IF A0$(27,1)="A" AND X>4 THEN GOTO 5085
5050 DIM G1$(35); LET G1$(1)="*** Missing Account Number ***"
5055 FIND (GLM01_DEV,KEY=N0$+A1$(X*G[4]+1,G[4]),DOM=5060)IOL=GLM01A
5060 PRINT @(23,X+11),FNF$(A1$(X*G[4]+1,G[3]),G5$)," ",G1$(1,35)
5085 NEXT X
5090 RETURN 
5400 REM " --- Clear Descriptions"
5420 FOR X=20 TO 12 STEP -2
5430 PRINT @(0,X),'CL'
5440 NEXT X
5490 RETURN 
6000 REM " --- Call Lookup for G/L Acct
6020 LET V4=2,V5=2,V6=3,V7=G[4],V8=1,V9=30,V$="",V5$=""
6040 CALL "SYC.LZ",V1,V2,V4,GLM01_DEV,V6,V7,V8,V9,V$,V5$,N0$
6060 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

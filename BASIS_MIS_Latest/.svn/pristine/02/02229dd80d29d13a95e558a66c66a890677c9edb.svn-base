0010 REM "WOS - Closed Work Order Purge - Date Entry                   "
0020 REM "Program WOU.FA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Mar99 KJS: Y2K year processing (435,443)
0032 REM "01May99 WGH: Remove references to obsolete file WOT-02 (130,175)"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=13
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-01",FILES$[2]="WOE-02",FILES$[3]="WOE-22"
0120 LET FILES$[4]="WOE-32",FILES$[5]="WOE-03",FILES$[6]="WOT-03"
0125 LET FILES$[7]="WOT-23",FILES$[8]="WOT-33",FILES$[9]="WOM-10"
0130 LET FILES$[10]="SYS-01",FILES$[11]="WOE-07",FILES$[12]=""
0150 CALL "SYC.DA",1,1,12,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE01_DEV=CHANNELS[1],WOE02_DEV=CHANNELS[2],WOE22_DEV=CHANNELS[3]
0165 LET WOE32_DEV=CHANNELS[4],WOE03_DEV=CHANNELS[5],WOT03_DEV=CHANNELS[6]
0170 LET WOT23_DEV=CHANNELS[7],WOT33_DEV=CHANNELS[8],WOM10_DEV=CHANNELS[9]
0175 LET SYS01_DEV=CHANNELS[10],WOE07_DEV=CHANNELS[11]
0200 REM " --- IOLISTs"
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 WOS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0290 IVS01A: IOLIST X$,X$,I2$,I3$
0400 REM " --- Parameters"
0405 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0410 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$
0415 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0420 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0425 DIM P[2],G[4]
0430 FOR X=0 TO 4
0435 LET G[X]=NUM(G2$(X*2+1,2),ERR=0436)
0440 NEXT X
0443 LET G[2]=FNYY_YEAR(G2$(5,2))
0445 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0450 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(P4$(1,2)),P[2]=FNYY_YEAR(P4$(3,2))
0500 REM " --- Init Data"
0510 LET H0=60,H1=120,H=0,H1$=N1$,H2$=N3$
0520 LET L9=50,L=L9+1
0530 DIM J$(80,"-"),J1$(80),J0$(30,"0")
0550 LET M0=LEN(M0$),M1=LEN(M1$),G5=LEN(G5$)
0600 REM " --- Additional File Opens "
0610 IF POS(I3$(17,1)="LS")=0 THEN GOTO 0700
0620 LET FILES$[13]="WOT-12"
0630 CALL "SYC.DA",1,13,13,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0640 IF STATUS THEN GOTO 9900
0650 LET WOT12_DEV=CHANNELS[13]
0700 REM " --- Background"
0710 FOR X=4 TO 6 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(8,3),"Purge Closed Work Orders As Of: ",'SF',@(0,22),'CL'
1000 REM " --- Options"
1010 SETERR 9000
1020 LET V0$="D",V1$="REC",V2$=P9$,V3$="",V4$="Enter Closed Date To Purge Work Orders",V0=3,V1=40,V2=3
1030 GOSUB 7000
1040 IF V3=4 THEN GOTO 9900
1050 LET P9$=V$
2000 REM " --- Verify Period To Be Current - 1
2010 LET V=P[1]
2020 CALL "SYC.PA",SYS01_DEV,V,P[2],V0$,V9$,STATUS
2030 IF STATUS THEN GOTO 9900
2040 IF P9$<=V0$ THEN GOTO 3900
2050 GOSUB 6000
2060 GOTO 1000
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,4000
4000 REM " --- Overlay"
4020 LET V0$="Y",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct? (Y/N): ",V0=1,V1=63,V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4060 IF V$="N" THEN GOTO 1000
4080 IF V$<>"Y" THEN GOTO 4000
4100 RUN "WOU.FB"
6000 REM " --- Can Not Purge For This Period
6020 PRINT 'RB',@(10,21),"Closed Work Orders May Not Be Purged For The Current Period",@(10,22),"And The Prior Period, Press <Enter> To Re-Enter Purge Date: "
6040 LET V0$="S",V1$="C",V2$="",V3$="",V4$="",V0=1,V1=67,V2=22
6050 GOSUB 7000
6060 PRINT @(0,21),'CL',@(0,22),'CL'
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

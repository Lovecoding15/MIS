0010 REM "IVR - Inventory Valuation Report (Report Overlay)"
0020 REM "Program IVR.NB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "FORMAT values:"
0055 REM 
0060 REM "1=Product Totals        4=Detail (with LS AND LF)"
0065 REM "2=Item Summary          5=Detail (with LF/No LS)"
0070 REM "3=Detail (No LS/LF)     6=Detail (with LS/No LF)"
0075 REM 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 CALL "SYC.GA",7,1,"","",STATUS
0120 IF STATUS THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$,A6$,A[ALL]
0220 IVM02A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0230 IVM04A: IOLIST C0$(1),C1$(1),C2$,C[ALL]
0240 IVM07A: IOLIST D0$(1),D1$(1),D[ALL]
0260 IVM12A: IOLIST W0$(1),W1$,W2$,W[ALL]
0270 IVM17A: IOLIST W0$(1),W1$,W2$,W[ALL]
0300 ARM10D: IOLIST Y0$(1),Y1$(1)
0310 IVM10A: IOLIST X0$(1)
0320 IVM10C: IOLIST X0$(1)
0340 GLW04A: IOLIST Z0$(1),Z[0]
0400 REM " --- Parameters"
0410 CALL "SYC.RM",M1$,M1,6,15
0420 CALL "SYC.RM",M2$,M2,9,12
0430 CALL "SYC.RM",M3$,M3,10,12
0440 LET CH$="Average",INDEX=11+POS(COST$="ASRL")
0450 IF COST$="S" THEN LET CH$="Standard"
0460 IF COST$="R" THEN LET CH$="Replacmnt"
0470 IF COST$="L" THEN LET CH$="Landed"
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A6$(5*G[4]),A[12],T[2]
0520 DIM B0$(24),B1$(64),B[20],C0$(30),C1$(16),C[5],D0$(44),D1$(96),D[12]
0530 DIM O[12],H[12],ITEM[2],TYPES$[11]
0550 LET WHEN$=F0$(7,3),CLOCK$="",WIDTH=132,PAGE=0,L9=59,L=L9+1,H9$=""
0555 LET SELECTION$="END",T0$="",T1$="",T2$="",PERIOD$=I4$(3,2)+I4$(1,2)
0560 LET WAREHOUSE=0,PRODUCT=0,REPORT=0,L1=0,L2=0,L3=0
0575 LET SOURCE$="BBBOTAOPRCTIWOIAIIIRPH"
0580 LET TYPES$[0]="????",TYPES$[1]="Conv",TYPES$[2]="BOM ",TYPES$[3]="Tier"
0585 LET TYPES$[4]="Sale",TYPES$[5]="P.O.",TYPES$[6]="Xfer",TYPES$[7]="W.O."
0590 LET TYPES$[8]="Adj ",TYPES$[9]="Iss ",TYPES$[10]="Rec ",TYPES$[11]="Phys"
0600 REM " --- Print positions"
0605 IF FORMAT=1 THEN GOTO 0690
0610 LET O[12]=WIDTH-M1,O[11]=O[12]-M2,O[10]=O[11]-M3,O[5]=O[10]-M2
0615 LET O[3]=O[5]-11,O[2]=O[3]-4,O[1]=P[0]+1,DW=O[2]-O[1]
0620 IF FORMAT<4 THEN GOTO 0700
0625 LET O[9]=O[10]-M2,O[8]=O[9]-24,O[7]=O[8]-M3
0630 LET O[1]=O[0]+20,O[2]=O[1]+3,DW=60
0685 GOTO 0700
0690 LET O[1]=O[0]+28,O[9]=O[1]+25,O[10]=O[9]+M2,O[11]=O[10]+M3,O[12]=O[11]+M2
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",7+P[0],COLUMN
0800 REM " --- Report headings"
0805 DIM H1$(WIDTH),H2$(WIDTH)
0810 IF FORMAT=1 THEN LET WHSE=INT((WIDTH-WHSE)/2)
0815 LET X1$="First",X2$="First",X2=5
0820 IF BEGITEM$<>"" THEN LET X1$=BEGITEM$
0825 IF BEGPROD$<>"" THEN LET X2$=BEGPROD$,X2=3
0830 IF ENDPROD$="" THEN IF X2=3 THEN LET X2=4
0835 LET H1$(1)="From Item: "+X1$,H1$(WIDTH-X2-13)="From Product: "+X2$
0840 IF FORMAT=1 THEN LET H1$(WHSE,LEN(WHSE1$))=WHSE1$
0845 LET X1$="Last",X2$="Last",X3$=H3$
0850 IF ENDITEM$<>"" THEN LET X1$=ENDITEM$
0855 IF ENDPROD$<>"" THEN LET X2$=ENDPROD$
0865 LET H2$(1)="  To Item: "+X1$,X3=FNC(X3$,WIDTH)
0870 IF FORMAT=1 THEN LET X3=WHSE,X3$=WHSE2$
0875 LET H2$(X3)=X3$,H2$(WIDTH-X2-13)="  To Product: "+X2$
0880 LET HEADINGS$[H1]=H1$,HEADINGS$[H2]=H2$
0900 REM " --- Position file"
0910 LET H[5]=O[5]+M2-9,H[7]=O[7]+M3-10,H[8]=O[8]
0950 LET H[9]=O[9]+M2-9,H[10]=O[10]+M3-10,H[11]=O[11]+M2-8,H[12]=O[12]+M1-6
0970 LET FIRST$=N0$+"C",K$=FIRST$+BEGWHSE$
0975 IF BEGWHSE$<>"" THEN LET K$=K$+BEGPROD$
0980 IF K$<>FIRST$ THEN LET K$=K$+BEGITEM$
0990 READ (IVM03_DEV,KEY=K$,DOM=1000)
0995 GOTO 1020
1000 REM " --- Get next sort record"
1010 LET K$=KEY(IVM03_DEV,END=4000)
1020 IF POS(FIRST$=K$)<>1 THEN GOTO 4000
1030 READ (IVM03_DEV,KEY=K$)
1040 LET WHSE$=K$(4,2),PRODUCT$=K$(6,3),ITEM$=K$(9)
1100 REM " --- Valid Warehouse/Product/Item?"
1110 IF ENDWHSE$<>"" THEN IF WHSE$>ENDWHSE$ THEN GOTO 4000
1120 IF BEGPROD$<>"" THEN IF PRODUCT$<BEGPROD$ THEN GOTO 1200
1130 IF ENDPROD$<>"" THEN IF PRODUCT$>ENDPROD$ THEN GOTO 1250
1140 IF BEGITEM$<>"" THEN IF ITEM$<BEGITEM$ THEN GOTO 1300
1150 IF ENDITEM$<>"" THEN IF ITEM$>ENDITEM$ THEN GOTO 1350
1160 LET WHSE$=K$(4,2),PRODUCT$=K$(6,3),ITEM$=K$(9)
1190 GOTO 1400
1200 REM " --- Skip to beginning product for this warehouse'
1210 READ (IVM03_DEV,KEY=K$(1,5)+BEGPROD$,DOM=3900)
1240 GOTO 3900
1250 REM " --- Skip to next warehouse"
1260 READ (IVM03_DEV,KEY=K$(1,5)+$FF$,DOM=3900)
1290 GOTO 3900
1300 REM " --- Skip to beginning item"
1310 READ (IVM03_DEV,KEY=K$(1,8)+BEGITEM$(1,P[0]-1),DOM=3900)
1340 GOTO 3900
1350 REM " --- Skip to next product"
1360 READ (IVM03_DEV,KEY=K$(1,8)+$FF$,DOM=3900)
1390 GOTO 3900
1400 REM " --- Level breaks?"
1410 PRINT @(COLUMN,11),WHSE$," ",PRODUCT$," ",ITEM$(1,P[0]),
1420 IF WHSE$<>T0$ THEN GOSUB 6000
1430 IF PRODUCT$<>T1$ THEN GOSUB 6200
1440 IF FORMAT>3 AND ITEM$<>T2$ THEN GOSUB 6400
1500 REM " --- Retrieve item, warehouse and activity"
1510 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A6$(5*G[4]),A[12],ITEM[2]
1520 DIM B0$(24),B1$(64),B[20],W0$(28),W[12],DESCRIPTION$(DW)
1530 LET A0$(1)=N0$+ITEM$,B0$(1)=N0$+WHSE$+ITEM$,W0$(1)=B0$+PERIOD$
1540 FIND (IVM01_DEV,KEY=A0$,DOM=3900)IOL=IVM01A
1550 IF TYPE$<>"" THEN IF A2$(53,3)<>TYPE$ THEN GOTO 3900
1560 FIND (IVM02_DEV,KEY=B0$,DOM=3900)IOL=IVM02A
1570 FIND (IVM12_DEV,KEY=W0$,DOM=1580)IOL=IVM12A
1580 LET DESCRIPTION$(1)=FNITEM$(A1$,P[3],P[4],P[5]),UM$=A2$(4,2)
1590 LET LOCATION$=B1$(1,10),ONHAND=B[0],COST=B[INDEX],UNITCOST=B[11],VALUE=0
1600 IF FORMAT>3 THEN LET H11$(O[1]+1)=UM$,H11$(O[2]+1)=LOCATION$,H11$(O[7]+1)=STR(COST:M3$)
1610 LET ACCOUNT$=A6$(1,G[4]),L3=L3+1
1620 CALL "IVC.PA",SELECTION$,W[ALL],BALANCE
1630 IF FORMAT<=3 AND INCLUDE$="N" AND BALANCE=0 THEN GOTO 3900
1700 REM " --- Retrieve GL account if distribute by item"
1710 IF DI$="N" THEN GOTO 1800
1730 DIM Y0$(32),Y1$(7*G[4],"0")
1740 LET Y0$(1)=N0$+"D"+B1$(13,2)
1750 FIND (ARM10_DEV,KEY=Y0$(1,5),DOM=1760)IOL=ARM10D
1760 LET ACCOUNT$=Y1$(6*G[4]+1,G[4])
1800 REM " --- Product total or summary report?"
1810 ON FORMAT GOTO 9900,3000,3000,3000,1900,1900,1900
1900 REM " --- Detail report"
1910 IF LS$<>"Y" THEN GOTO 2400
1950 IF A2$(19,1)<>"Y" THEN GOTO 2400
1970 IF INCLUDE$="N" AND BALANCE=0 THEN GOTO 3900
1990 READ (IVM07_DEV,KEY=B0$,DOM=2000)
2000 REM " --- Read next lot/serial master"
2005 DIM D[12]
2010 LET K07$=KEY(IVM07_DEV,END=3900)
2020 IF K07$(1,24)<>B0$ THEN GOTO 3900
2030 READ (IVM07_DEV,KEY=K07$)IOL=IVM07A
2040 LET ONHAND=D[0],UNITCOST=D[3],LS_ORTIER$=D0$(25,20),W0$=D0$+PERIOD$
2050 IF ONHAND=0 THEN GOTO 2290
2060 DIM W[12]
2070 FIND (IVM17_DEV,KEY=W0$,DOM=2100)IOL=IVM17A
2080 CALL "IVC.PA",SELECTION$,W[ALL],BALANCE
2090 GOTO 2200
2100 REM " --- Using IVM-12 End Bal cuz no IVM-17
2110 LET USEIVM12=1
2120 IF L2>1 THEN GOTO 2210; REM "Have artificially 0'd Balance, so skip test
2200 IF INCLUDE$="N" AND BALANCE=0 THEN GOTO 2290
2210 GOSUB 6600; REM "Get total value
2220 GOSUB 5900; REM "Print line
2280 GOSUB 6700; REM "Accum Totals
2290 GOTO 2000
2400 REM " --- Process LIFO/FIFO Tiers"
2410 IF LF$<>"Y" THEN GOTO 3000
2430 READ (IVM04_DEV,KEY=B0$,DOM=2500)
2500 REM " --- Read next LIFO/FIFO master
2510 LET K04$=KEY(IVM04_DEV,END=3900)
2520 IF K04$(1,24)<>B0$ THEN GOTO 3900
2530 READ (IVM04_DEV,KEY=K04$)IOL=IVM04A
2540 LET ONHAND=C[0],UNITCOST=C[1]
2550 IF ONHAND=0 THEN GOTO 2790
2620 LET REF$=C1$(3,7),TIERTYPE$=C1$(1,2),P=INT(1+POS(TIERTYPE$=SOURCE$,2)/2)
2630 IF P>0 THEN LET TIERTYPE$=TYPES$[P]
2640 LET LS_ORTIER$=C0$(28,3)+" "+FNB$(C0$(25,3))+" "+TIERTYPE$+" "+REF$
2650 LET BALANCE=0,FROMLF=1
2700 GOSUB 6600; REM "Get total value
2710 GOSUB 5900; REM "Print line
2720 GOSUB 6700; REM "Accum Totals
2790 GOTO 2500
2890 GOTO 3900
3000 REM " --- Go Calculate total value 
3020 GOSUB 6600
3080 IF FORMAT=1 THEN GOTO 3800
3090 IF FORMAT>3 THEN GOTO 3500
3200 REM " --- Print line for formats 2 & 3 (summary & detail no LS or LF)
3210 IF H9$<>"" THEN GOSUB 5800
3220 IF L+1>L9 THEN GOSUB 5000
3230 PRINT (7)@(O[0]),A0$(3,P[0]),@(O[1]),DESCRIPTION$,@(O[2]),UM$,@(O[3]),LOCATION$,@(O[5]),ONHAND:M2$,@(O[10]),COST:M3$,@(O[11]),BALANCE:M2$,@(O[12]),AMOUNT$
3240 LET L=L+1,L1=L1+1
3290 GOTO 3800
3500 REM " --- Print line for non(LS/LF) items w/formats 4, 5 & 6
3510 IF H9$<>"" THEN GOSUB 5800
3520 IF L+2>L9 THEN GOSUB 5000
3530 IF INCLUDE$="N" AND BALANCE=0 THEN GOTO 3900
3540 PRINT (7)@(O[0]),H11$,@(O[9]),ONHAND:M2$,@(O[10]),UNITCOST:M3$,@(O[11]),BALANCE:M2$,@(O[12]),AMOUNT$
3550 IF L2=0 THEN LET H11$=DESCRIPTION$ ELSE LET H11$=""
3570 LET L=L+1,L1=L1+1,L2=L2+1
3800 REM " --- Go Accumulate Totals
3810 GOSUB 6700
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 LET DONE=1
4020 GOSUB 6000
4030 LET TOTAL$="Total For Report",TOTAL=REPORT
4040 GOSUB 6900
4080 IF GL$="Y" THEN GOTO 4900
4090 GOTO 9900
4900 REM " --- Run GL summary overlay"
4910 DIM WORK$[2]
4920 LET WORK$[0]=HEADINGS$[0],WORK$[1]=HEADINGS$[1],WORK$[2]=HEADINGS$[2]
4930 DIM HEADINGS$[2]
4940 LET HEADINGS$[0]=WORK$[0],HEADINGS$[1]=WORK$[1],HEADINGS=2
4950 IF FORMAT=1 THEN LET HEADINGS$[2]=WORK$[2],HEADINGS=2
4990 RUN "GLR.XB"
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+4
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 ON FORMAT GOSUB 5090,5100,5200,5200,5300,5300,5300
5050 PRINT (7)""
5090 RETURN 
5100 REM " --- Product subheading (format 1)
5110 PRINT (7)@(H[9]),"Quantity",@(H[10]),CH$,@(H[11]),"Ending",@(H[12]),"Total"
5120 PRINT (7)@(O[0]),"Warehouse",@(O[1]),"Product",@(H[9]),"On Hand",@(H[10]),"Unit Cost",@(H[11]),"Balance",@(H[12]),"Value"
5190 RETURN 
5200 REM " --- Summary subheading (formats 2 & 3)
5210 PRINT (7)@(H[5]),"Quantity",@(H[10]),CH$,@(H[11]),"Ending",@(H[12]),"Total"
5220 PRINT (7)@(O[0]),"Item",@(O[1]),"Description",@(O[2]),"UM",@(O[3]),"Location",@(H[5]),"On Hand",@(H[10]),"Unit Cost",@(H[11]),"Balance",@(H[12]),"Value"
5290 RETURN 
5300 REM " --- Detail subheading (formats 4, 5 & 6)
5310 LET LINE1LS$="  Lot/Serial Number or",LINE2LS$="Tier  Date   Type Ref #"
5320 IF FORMAT=5 THEN LET LINE1LS$=""
5330 IF FORMAT=6 THEN LET LINE1LS$="",LINE2LS$="Lot/Serial Number"
5350 PRINT (7)@(H[7]),CH$,@(H[8]),LINE1LS$,@(H[9]),"Quantity",@(H[11]),"Ending",@(H[12]),"Total"
5360 PRINT (7)@(O[0]),"Item/Description",@(O[1]),"UM",@(O[2]),"Location",@(H[7]),"Cost",@(H[8]),LINE2LS$,@(H[9]),"On Hand",@(H[10]),"Unit Cost",@(H[11]),"Balance",@(H[12]),"Value"
5390 RETURN 
5800 REM " --- Product subheading"
5810 IF L+4>L9 THEN GOSUB 5000
5830 PRINT (7)H9$
5850 LET L=L+1,H9$=""
5890 RETURN 
5900 REM " --- Print line for formats 4, 5 & 6 (LS &/or LF)
5910 IF H9$<>"" THEN GOSUB 5800
5920 IF FROMLF THEN LET BALANCE$="" ELSE LET BALANCE$=STR(BALANCE:M2$)
5930 IF USEIVM12 AND L2>0 THEN LET BALANCE$="",BALANCE=0,USEIVM12=0
5940 IF L+2>L9 THEN GOSUB 5000
5960 PRINT (7)@(O[0]),H11$,@(O[8]),LS_ORTIER$,@(O[9]),ONHAND:M2$,@(O[10]),UNITCOST:M3$,@(O[11]),BALANCE$,@(O[12]),AMOUNT$
5970 IF L2=0 THEN LET H11$=DESCRIPTION$ ELSE LET H11$=""
5980 LET L=L+1,L1=L1+1,L2=L2+1
5990 RETURN 
6000 REM " --- Warehouse Break"
6010 IF T0$="" THEN GOTO 6100
6020 GOSUB 6200
6030 LET TOTAL$="Total For Warehouse "+H5$,TOTAL=WAREHOUSE
6040 IF WAREHOUSE>0 THEN GOSUB 6900; GOTO 6100
6050 IF L+1<=L9 THEN PRINT (7)""; LET L=L+1,L1=L1+1
6100 IF DONE THEN GOTO 6190
6110 DIM X0$(32)
6120 LET T0$=WHSE$,X0$(6,24)="(Not on File)",L1=0,L2=0,T2$="",WAREHOUSE=0
6140 FIND (IVM10_DEV,KEY=N0$+"C"+T0$,DOM=6150)IOL=IVM10C
6150 LET H5$=FNP$(T0$+" "+X0$(6,24)),X$="Warehouse "+H5$,H6$=H5$
6160 IF FORMAT=1 THEN GOTO 6190
6170 LET HEADINGS$[4]=H1$,HEADINGS$[4](FNC(X$,WIDTH),LEN(X$))=X$,L=L9+1
6190 RETURN 
6200 REM " --- Product break"
6210 IF T1$="" THEN GOTO 6300
6220 IF FORMAT>3 THEN GOSUB 6400
6230 IF FORMAT>1 THEN GOTO 6300
6240 IF L3=0 THEN GOTO 6330
6250 IF L+1>L9 THEN GOSUB 5000
6255 IF WHICHQTY$="B" THEN IF T[1]<>0 THEN LET T[0]=T[2]/T[1]
6260 IF WHICHQTY$="O" THEN IF TOTONHAND<>0 THEN LET T[0]=T[2]/TOTONHAND
6270 PRINT (7)@(O[0]),H6$,@(O[1]),H7$,@(O[9]),TOTONHAND:M2$,@(O[10]),T[0]:M3$,@(O[11]),T[1]:M2$,@(O[12]),T[2]:M1$
6280 LET L=L+1,H6$=""
6290 GOTO 6330
6300 LET TOTAL$="Total For Product Type "+H7$,TOTAL=PRODUCT
6310 IF L1>1 THEN GOSUB 6900; GOTO 6330
6320 IF L+1<=L9 THEN PRINT (7)""; LET L=L+1,L1=L1+1
6330 IF DONE THEN GOTO 6390
6340 DIM X0$(32),T[2]
6345 LET T1$=PRODUCT$,X0$(7,20)="(Not on File)",T2$=""
6350 LET L1=0,L2=0,L3=0,PRODUCT=0,TOTONHAND=0
6360 FIND (IVM10_DEV,KEY=N0$+"A"+T1$,DOM=6370)IOL=IVM10A
6370 LET H7$=FNP$(T1$+" "+X0$(7,20)),H9$="Product Type "+H7$
6380 IF PAGEBREAK$="Y" THEN LET L=L9+1
6390 RETURN 
6400 REM " --- Item break"
6410 IF T2$="" OR L2<1 THEN GOTO 6500
6415 IF L2=1 THEN GOTO 6460
6420 LET TOTAL$="Total For Item "+H10$,ITEMBREAK=1
6430 LET TOTALBAL$=STR(ITEM[1]:M2$),TOTALVAL$=STR(ITEM[2]:M1$)
6435 LET TOTALONHAND$=STR(ITEM[0]:M2$)
6440 IF FROMLF THEN LET FROMLF=0,TOTALBAL$=""
6450 IF L2>1 THEN GOSUB 6900; GOTO 6500
6460 LET ITEMBREAK=0
6470 IF L+1>L9 THEN GOSUB 5000
6480 IF POS(" "<>H11$)>0 THEN PRINT (7)H11$; LET L=L+1,L1=L1+1
6490 IF L+1<=L9 THEN PRINT (7)""; LET L=L+1,L1=L1+1
6500 IF DONE THEN GOTO 6590
6510 DIM ITEM[2],H11$(O[8])
6520 LET T2$=ITEM$(1,P[0]),L2=0,L3=0
6530 LET H10$=FNP$(T2$)+" ",H11$(1)=H10$
6590 RETURN 
6600 REM " --- Calculate total value 
6610 PRECISION 2
6620 IF WHICHQTY$="B" THEN LET AMOUNT=BALANCE*COST ELSE LET AMOUNT=ONHAND*COST
6630 PRECISION P[1]
6640 LET AMOUNT$=STR(AMOUNT:M1$)
6650 IF USEIVM12 AND L2>0 THEN LET AMOUNT=0,AMOUNT$=""
6660 GOSUB 6800
6690 RETURN 
6700 REM " --- Accumulate Totals"
6710 LET PRODUCT=PRODUCT+AMOUNT,WAREHOUSE=WAREHOUSE+AMOUNT,REPORT=REPORT+AMOUNT
6720 LET T[1]=T[1]+BALANCE,T[2]=T[2]+AMOUNT
6730 LET TOTONHAND=TOTONHAND+ONHAND
6740 LET ITEM[0]=ITEM[0]+ONHAND,ITEM[1]=ITEM[1]+BALANCE,ITEM[2]=ITEM[2]+AMOUNT
6790 RETURN 
6800 REM " --- Update G/L Summary"
6810 IF GL$<>"Y" THEN GOTO 6890
6820 IF AMOUNT=0 THEN GOTO 6890
6830 DIM Z0$(2+G[4]),Z[1]
6850 LET Z0$(1)=N0$+ACCOUNT$
6860 FIND (GLW04_DEV,KEY=Z0$,DOM=6870)IOL=GLW04A
6870 LET Z[0]=Z[0]+AMOUNT
6880 WRITE (GLW04_DEV,KEY=Z0$)IOL=GLW04A
6890 RETURN 
6900 REM " --- Print total line"
6910 IF L+2>L9 THEN GOSUB 5000
6920 IF ITEMBREAK THEN GOTO 6950
6930 PRINT (7)@(O[12]-LEN(TOTAL$)),TOTAL$,@(O[12]),TOTAL:M1$
6940 GOTO 6970
6950 PRINT (7)@(O[9]-LEN(TOTAL$)),TOTAL$,@(O[9]),TOTALONHAND$,@(O[11]),TOTALBAL$,@(O[12]),TOTALVAL$
6960 LET ITEMBREAK=0
6970 PRINT (7)""
6980 LET L=L+2
6990 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

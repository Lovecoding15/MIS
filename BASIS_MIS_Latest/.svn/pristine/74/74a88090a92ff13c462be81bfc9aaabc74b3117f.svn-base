0010 REM "APC - Vendor Purchase Address Maintenance"
0020 REM "Program APC.PA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "15Mar00 KJS: Restore option doesn't work (4050)
0032 REM "15Mar00 KJS: New record doesn't clear old values (1510)
0040 REM 
0045 REM "ACTION$  : "W" for window/"" for no window (input)"
0050 REM "APM05_DEV: APM-05 channel number (input)"
0055 REM "VENDOR$  : Firm + Vendor + Optional Address (input/returned)"
0060 REM "WIN_X    : Window column (input)"
0065 REM "WIN_Y    : Window row (input)"
0070 REM "OFFSET   : Screen offset if non-windowed (input)"
0075 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER ACTION$,APM05_DEV,VENDOR$,WIN_X,WIN_Y,OFFSET
0200 REM " --- IOLIST's"
0210 APM05A: IOLIST A0$(1),A1$(1)
0500 REM " --- Init Data
0510 DIM A0$(10),A1$(160)
0550 LET A0$(1)=VENDOR$,HEIGHT=12,WIDTH=72,PA$=" For This Purchase Address"
0560 LET TITLE$="Purchase Address Maintenance",M8$="(###) ###-####"
0570 LET ADDRESS$=A0$(9),V$=ADDRESS$
0580 IF POS("W"=ACTION$)=0 THEN LET WIDTH=80,OFFSET=11,ADDRESS$="",V$=""
0700 REM " --- Background
0710 IF POS("W"=ACTION$) THEN CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT 'SB',@(1,OFFSET),"Purchase Number:",@(3,OFFSET+1),"Purchase Name:",@(9,OFFSET+2),"Address:",@(6,OFFSET+5),"Zip/Postal:",@(7,OFFSET+6),"Telephone:",@(33),"Ext:",@(7,OFFSET+7),"Fax Phone:",@(9,OFFSET+8),"Contact:",'SF',
0900 REM " --- Position file"
0980 READ (APM05_DEV,KEY=A0$,DOM=990)
0990 IF FNP$(ADDRESS$)<>"" THEN GOTO 1200
1000 REM " --- Purchase Address"
1010 GOSUB 6000
1020 LET V2$="",V2$=KEY(APM05_DEV,END=1050)
1030 IF POS(VENDOR$=K$)<>1 THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(9,2)
1050 LET V4$="Enter A Valid Purchase Address (<F3>=Lookup)"
1055 LET V0$="Z",V1$="CKE",V0=2,V1=18,V2=OFFSET
1060 GOSUB 7000
1070 ON V3 GOTO 1200,1000,1000,1080,9900
1080 CALL "SYC.LT","P",A0$(3,6),V$
1090 IF V$="" THEN GOTO 0900
1200 REM " --- Find Key
1210 LET A0$(9)=V$
1220 PRINT @(18,OFFSET),A0$(9,2)
1230 FIND (APM05_DEV,KEY=A0$,DOM=1400)IOL=APM05A
1300 REM " --- Display
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New Record
1405 IF FNP$(ADDRESS$)<>"" THEN GOTO 1500
1410 LET V4$="Is This A New Record (Y/N)?"
1420 LET V0$="Y",V1$="C",V2$="N",V3$="",V0=1,V1=FNV1(V4$,WIDTH),V2=22
1430 GOSUB 7000
1440 ON V3 GOTO 1450,1400,1000,1400,1000
1450 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-up
1510 LET A1$(1)=""
2000 REM " --- Name
2010 LET V4$="Enter The Name"+PA$
2020 LET V0$="S",V1$="C",V2$=A1$(1,30),V3$="",V0=30,V1=18,V2=OFFSET+1,I0=0
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 LET A1$(1,30)=V$
2090 GOTO 3900
2100 REM " --- Address
2110 LET X=I0-1,V4$="Enter Address Line "+STR(I0)+PA$
2120 LET V0$="S",V1$="C",V2$=A1$(X*24+31,24),V3$="",V0=24,V1=18,V2=OFFSET+I0+1
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2150 LET A1$(X*24+31,24)=V$
2190 GOTO 3900
2200 REM " --- Zip Code
2210 LET V4$="Enter The Zip Code"+PA$
2220 LET V0$="S",V1$="C",V2$=A1$(103,9),V3$="",V0=9,V1=18,V2=OFFSET+5
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 LET A1$(103,9)=V$
2260 PRINT @(V1,V2),FNZ$(A1$(103)),
2290 GOTO 3900
2300 REM " --- Phone
2310 LET V4$="Enter The Telephone Number"+PA$
2320 LET V0$="H",V1$="C",V2$=A1$(112,10),V3$=M8$,V0=10,V1=18,V2=OFFSET+6
2330 GOSUB 7000
2340 ON V3 GOTO 2350,2300,3900,2300,3900
2350 LET A1$(112,10)=V$
2360 PRINT @(V1,V2),FNF$(A1$(112,10),M8$)
2390 GOTO 3900
2400 REM " --- Ext
2410 LET V4$="Enter An Optional Extension For This Telephone Number"
2420 LET V0$="Z",V1$="C",V2$=A1$(122,4),V3$="",V0=4,V1=38,V2=OFFSET+6
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 LET A1$(122,4)=V$
2490 GOTO 3900
2500 REM " --- FAX Phone
2510 LET V4$="Enter A Fax Telephone Number"+PA$
2520 LET V0$="H",V1$="C",V2$=A1$(146,10),V3$=M8$,V0=10,V1=18,V2=OFFSET+7
2530 GOSUB 7000
2540 ON V3 GOTO 2550,2500,3900,2500,3900
2550 LET A1$(146,10)=V$
2560 PRINT @(V1,V2),FNF$(A1$(146,10),M8$)
2590 GOTO 3900
2600 REM " --- Contact
2610 LET V4$="Enter The Contact Name"+PA$
2620 LET V0$="S",V1$="C",V2$=A1$(126,20),V3$="",V0=20,V1=18,V2=OFFSET+8
2630 GOSUB 7000
2640 ON V3 GOTO 2650,2600,3900,2600,3900
2650 LET A1$(126,20)=V$
2690 GOTO 3900
3900 REM " --- Next Field
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2100,2100,2200,2300,2400,2500,2600,3990
3990 GOSUB 5000
4000 REM " --- Options
4010 LET V4$="Is The Above Information Correct (Y/N/Delete/Restore)?"
4020 LET V0$="S",V1$="KC",V2$="Y",V3$="",V0=7,V1=FNV1(V4$,WIDTH-2),V2=22
4030 GOSUB 7000
4040 IF V$="DELETE" THEN GOTO 4200
4050 IF V$="R" OR V$="RESTORE" THEN GOTO 1220
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write
4110 WRITE (APM05_DEV,KEY=A0$)IOL=APM05A
4120 IF FNP$(ADDRESS$)<>"" THEN GOTO 9900
4190 GOTO 1000
4200 REM " --- Delete
4210 REMOVE (APM05_DEV,KEY=A0$,DOM=4220)
4220 LET A0$(9)=""
4290 GOTO 1000
5000 REM " --- Display
5010 PRINT @(18,OFFSET+1),A1$(1,30),@(18,OFFSET+2),A1$(31,24),@(18,OFFSET+3),A1$(55,24),@(18,OFFSET+4),A1$(79,24),@(18,OFFSET+5),FNZ$(A1$(103)),@(18,OFFSET+6),FNF$(A1$(112,10),M8$),@(38),A1$(122,4),@(18,OFFSET+7),FNF$(A1$(146,10),M8$),@(18,OFFSET+8),A1$(126,20),
5090 RETURN
6000 REM " --- Clear screen"
6010 FOR X=OFFSET TO OFFSET+8
6020 PRINT @(18,X),'CL',
6030 IF X=OFFSET+6 THEN PRINT @(33,X),'SB',"Ext:",'SF',
6040 NEXT X
6090 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8090 DEF FNV1(Q$,Q)=FNC(Q$,Q)+LEN(Q$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 REM " --- Return to caller"
9910 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9920 LET VENDOR$=A0$
9950 EXIT
9999 END
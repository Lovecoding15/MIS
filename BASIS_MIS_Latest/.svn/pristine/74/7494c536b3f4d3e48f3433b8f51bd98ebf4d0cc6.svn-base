0010 REM "CSC - Configurator Matinenance - Bill of Materials Info
0020 REM "Program CSC.BM"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "MAS * DCF * 4/28/99 * WA# 204 * BASIS
0040 REM 
0050 REM "RECORD$   : Record ID
0051 REM "CSM12_DEV : Channel for CSM-12
0052 REM "LINE_NO   : Starting Line Number
0053 REM "BMM01_DEV : Channel for BMM-01
0054 REM "IVM01_DEV : Channel for IVM-01
0055 REM "IVM02_DEV : Channel for IVM-02
0056 REM "SYS01_DEV : Channel for SYS-01
0085 SETESC 9000
0090 SETERR 9000
0095 ENTER RECORD$,CSM12_DEV,LINE_NO,BMM01_DEV,IVM01_DEV,IVM02_DEV,SYS01_DEV
0200 REM " --- IOLists
0210 CSM12A: IOLIST B0$,B1$,B2$,B[ALL]
0240 IVM01A: IOLIST C0$,C1$(1),C2$,C3$,C4$,C5$,C6$,C[ALL]
0250 IVM02A: IOLIST *,*,*,JUNK[ALL],UNITCOST
0310 BMS01A: IOLIST X$,P1$,P2$,P3$,P4,X5,BOMP6
0320 IVS01A: IOLIST X$,P5$,P6$,WH$,X$,X$,P7$,P8$,P9$
0400 REM " --- Parameters
0410 LET N0$=RECORD$(1,2)
0430 FIND (SYS01_DEV,KEY=N0$+"BM"+"00",DOM=9800)IOL=BMS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV"+"00",DOM=9800)IOL=IVS01A
0445 LET WH$=WH$(6,2)
0450 DIM P[2],DESC[3],PARAMS[3]
0455 LET P[0]=NUM(P6$(1,2))
0460 FOR X=1 TO 3
0470 LET DESC[X]=NUM(P6$((X-1)*2+9,2))
0475 LET PARAMS[X]=DESC[X]
0480 NEXT X
0485 LET PARAMS[0]=P[0]
0492 IF NUM(P6$(5,1))>BOMP6 THEN LET P6=NUM(P6$(5,1)) ELSE LET P6=BOMP6
0495 PRECISION P6
0500 REM " --- Init Data
0510 LET L9=20,L0=LINE_NO+3,L=L0,L2=L,MESS$="",FIRST_PASS$="Y"
0515 LET RECLEN=LEN(RECORD$),A4=100
0520 DIM C1$(60),B0$(RECLEN+3),B1$(20),B2$(114),B[10],C[12],JUNK[10]
0540 LET B0$(1)=RECORD$
0555 CALL "SYC.RM",P1$,P1,9,9
0560 CALL "SYC.RM",P2$,P2,7,7
0565 CALL "SYC.RM",P8$,P8,5,8
0570 CALL "SYC.RM",P9$,P9,6,8
0575 LET M2$=P8$,M9$="#####0-",M5$="####.00-"
0580 LET M7$=P9$,M9$="#####0-",M0=P[0],M7=LEN(M7$),P7=LEN(P7$),M2=LEN(M2$),M9=LEN(M9$)
0585 LET M6$="###.00",P4$="###0"
0700 REM " --- Background
0705 PRINT @(0,L-3),'CE',
0710 CALL "SYC.WC",1,0,80,0,0,L-3,0
0720 PRINT 'SB',@(10,L-3),"Material Information",
0730 PRINT @(38,L-2),"Qty",@(46),"Alt",@(55),"Scrap",@(65),"Unit",@(73),"Total",
0740 PRINT @(0,L-1),"Seq",@(4),"Item",@(26),"Divisor",@(38),"Reqd",@(45),"Factor",@(54),"Factor",@(65),"Cost",@(74),"Cost",
0750 IF O2=0 THEN GOSUB CLRSCRN
0790 GOSUB LIST
0800 IF FIRST_PASS$="Y" THEN LET FIRST_PASS$="N"; GOTO 4000
1040 IF L>L9 THEN PRINT @(0,L2),'LD','LD'; LET L=L9
1100 REM " --- Get Next Key
1120 LET K$=KEY(CSM12_DEV,END=1400)
1140 IF POS(RECORD$=K$)<>1 THEN GOTO 1400
1150 READ (CSM12_DEV)IOL=CSM12A
1160 GOTO 1490
1400 REM " --- Sequence
1405 IF L>L9 THEN PRINT @(0,L2),'LD','LD',; LET L=L9
1410 IF V3<>2 THEN LET B0=NUM(B0$(7,3))+5
1420 LET V0$="Z",V1$="KER",V2$=STR(B0),V3$="",V0=3,V1=0,V2=L,I0=1
1430 GOSUB 7000
1440 IF V3=4 THEN PRINT @(V1,V2),"    "; GOTO 4000
1450 IF V$="000" OR V$="" THEN GOTO 1400
1460 LET B0$(7,3)=V$
1470 DIM B1$(20),B2$(114)
1475 LET B[0]=1,B[1]=1,B[2]=0,B[3]=1
1480 FIND (CSM12_DEV,KEY=B0$(1,9),DOM=1600)IOL=CSM12A
1485 PRINT @(0,L),'CL',@(0,L+1),'CL',
1490 IF B0$(1,9)<=SAVEB0$ THEN GOSUB CLRSCRN
1492 LET SUBBILL$=" "
1494 FIND (BMM01_DEV,KEY=N0$+B1$,DOM=1500)
1496 LET SUBBILL$="*"
1500 GOSUB DISPLAY
1520 LET I0=6
1590 GOTO 3000
1600 REM " --- New Line
1610 DIM B[10]
1620 LET B[1]=1,B[3]=1
1630 PRINT @(0,L),'CL',@(0,L+1),'CL',
1640 PRINT @(0,L),B0$(7,3),
1650 IF MESS$="Y" THEN LET B2$(4,1)="M" ELSE LET B2$(4,1)="S"
2000 REM " --- Item Code
2005 PRINT @(3,L),'CL',@(0,L+1),'CL',
2015 IF B2$(4,1)="M" THEN LET MESS$="Y"; GOTO 3500
2020 LET V0$="S",V1$="KCRE",V2$=B1$,V4$="<F3>=Item Lookup",V0=20,V1=4,V2=L,I0=1
2022 FIND (CSM12_DEV,KEY=B0$(1,9),DOM=2023)IOL=CSM12A; LET EXIST$="Y"; GOTO 2025
2023 LET V4$="<F1>=Message Line, "+V4$,EXIST$="N"
2025 GOSUB 7000
2027 IF EXIST$="N" THEN IF V3=1 THEN DIM B[10]; LET MESS$="Y",B2$(4,1)="M",B1$(1,20)="",B[1]=1,B[3]=1; GOTO 3500
2031 IF V3=4 THEN GOTO 4000
2033 IF V3=3 THEN CALL "SYC.LI",V$
2034 IF V$="" THEN GOTO 2020
2036 IF V3=2 THEN GOTO 2095
2038 IF FNP$(B0$(3,7))=V$ THEN GOSUB 6000; GOTO 2020
2040 LET B1$(1,20)=V$,B2$(4,1)="S"
2041 REM " --- I/C Interface
2042 READ (IVM01_DEV,KEY=N0$+B1$(1,20),DOM=2020)IOL=IVM01A
2046 LET SUBBILL$=" "
2048 FIND (BMM01_DEV,KEY=N0$+B1$,DOM=2052)
2050 LET SUBBILL$="*"
2052 GOSUB DISPLAY
2054 GOTO 2900
2060 REM " --- Description
2065 GOTO 2095
2070 LET V0$="S",V1$="",V2$=B2$(1,32),V0=32,V1=4,V2=L+1
2075 GOSUB 7000
2090 LET B2$(1,32)=V$
2095 GOTO 2900
2100 REM " --- U/M
2105 IF B2$(4,1)="M" THEN GOTO 2190
2110 READ (IVM01_DEV,KEY=N0$+B1$(1,20),DOM=2020)IOL=IVM01A
2120 IF B2$(33,2)="  " THEN LET B2$(33,2)=C2$(4,2)
2140 LET V0$="S",V1$="C",V2$=B2$(33,2),V3$="",V4$="",V0=2,V1=55,V2=L+1
2150 GOSUB 7000
2160 LET B2$(33,2)=V$
2190 GOTO 2900
2300 REM " --- Divisor
2310 IF B2$(4,1)="M" THEN GOTO 2390
2340 LET V0$="N",V2$=STR(B[3]),V3$=P1$,V0=0,V1=25,V2=L
2350 GOSUB 7000
2355 IF V=0 THEN GOTO 2300
2360 LET B[3]=V
2380 GOSUB 5500
2390 GOTO 2900
2400 REM " --- Qty Required
2410 IF B2$(4,1)="M" THEN GOTO 2490
2440 LET V0$="N",V2$=STR(B[0]),V3$=P1$,V0=0,V1=34,V2=L
2450 GOSUB 7000
2455 IF V=0 THEN GOTO 2400
2460 LET B[0]=V
2480 GOSUB 5500
2490 GOTO 2900
2500 REM " --- Alternate Factor
2510 IF B2$(4,1)="M" THEN GOTO 2590
2540 LET V0$="N",V2$=STR(B[1]),V3$=P2$,V0=0,V1=43,V2=L
2550 GOSUB 7000
2555 IF V=0 THEN GOTO 2500
2560 LET B[1]=V
2570 GOSUB 5500
2590 GOTO 2900
2600 REM " --- Unit Cost
2690 GOTO 2900
2700 REM " --- Scrap Factor
2710 IF B2$(4,1)="M" THEN GOTO 2790
2740 LET V0$="N",V1$="C",V2$=STR(B[4]),V3$=P2$,V4$="Enter Positive Percentage Of Scrap You Expect (5 = 5%)",V0=0,V1=52,V2=L
2750 GOSUB 7000
2755 IF V>=100 OR V<0 THEN GOTO 2700
2760 LET B[4]=V
2770 GOSUB 5500
2790 GOTO 2900
2800 REM " --- Op Code
2810 IF B2$(4,1)="M" THEN GOTO 2890
2820 LET V0$="Z",V1$="C",V2$=B2$(1,3),V3$="",V4$="Enter Op Code Sequence, <F1>=None, <F3>=Lookup",V0=3,V1=46,V2=L+1
2830 GOSUB 7000
2835 IF V3=2 THEN GOTO 2890
2840 IF V3=3 THEN LET V$=A0$(3,20); CALL "BMC.DA",V$; GOTO 2870
2850 IF V3=1 OR LEN(V$)=0 THEN LET V$="   "; GOTO 2880
2870 FIND (BMM03_DEV,KEY=N0$+A0$(3,20)+V$,DOM=2800)
2880 LET B2$(1,3)=V$
2885 PRINT @(V1,V2),B2$(1,3)
2890 GOTO 2900
2900 REM " --- Seq Control
2910 IF V3=4 THEN IF I0>1 THEN GOTO 3000
2920 IF V3<>2 THEN GOTO 2950
2930 IF I0>0 THEN LET I0=I0-1
2940 GOTO 2960
2950 LET I0=I0+1
2960 ON I0 GOTO 1400,2000,2060,2300,2400,2500,2700,2600,2800,2100,3000
3000 REM " --- Correct
3010 LET B0=NUM(B0$(7,3))
3020 LET V0$="S",V1$="C^",V2$="",V3$="",V0=1,V1=78,V2=L+1,SAVEB0$=B0$(1,9)
3025 LET V4$="<Enter>=OK, <F1>=Additional Options, <F2>=Change, D=Delete"
3030 GOSUB 7000
3040 LET I0=10
3045 IF V3=1 THEN LET X0$=B2$(5,3),X1$=B2$(8,3); CALL "CSC.BN",X0$,X1$; LET B2$(5,3)=X0$,B2$(8,3)=X1$; GOTO 3000
3050 IF V3=2 THEN GOTO 2900
3080 IF V$="D" THEN GOTO 3200
3100 REM " --- Write
3110 WRITE (CSM12_DEV,KEY=B0$(1,9))IOL=CSM12A
3150 LET L=L+2,SAVEB0$=B0$(1,9)
3160 IF V3=4 THEN GOTO 4000
3190 GOTO 1000
3200 REM " --- Delete
3220 REMOVE (CSM12_DEV,KEY=B0$(1,9),DOM=3230)
3240 PRINT @(0,L),'CL',@(0,L+1),'CL'
3290 GOTO 1000
3500 REM " --- Message
3510 PRINT @(3,L),'CL',@(0,L+1),'CL',
3520 LET V0$="S",V1$="C",V2$=B2$(35,60),V3$="",V4$="Enter Message",V0=60,V1=4,V2=L+1
3522 FIND (CSM12_DEV,KEY=B0$(1,9),DOM=3523)IOL=CSM12A; LET EXIST$="Y"; GOTO 3525
3523 LET V4$=V4$+", <F1>=Standard Line",EXIST$="N"
3540 GOSUB 7000
3550 IF EXIST$="N" THEN IF V3=1 THEN LET MESS$="N",B2$(4,1)="S",B2$(35,60)=""; GOTO 2000
3560 LET B2$(35,60)=V$
3590 GOTO 2900
4000 REM " --- Options
4010 LET V0$="S",V1$="KC^",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/#/Review/List)? ",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4025 LET I=NUM(V$,ERR=4030); LET V$=STR(I:"000")
4026 IF V$="000" THEN GOTO 4030
4027 IF L>L9 THEN PRINT @(0,L2),'LD','LD',; LET L=L9
4028 GOTO 1450
4030 IF V$="R" OR V$="Review" THEN LET V$="REVIEW" ELSE IF V$="L" OR V$="List" THEN LET V$="LIST"
4040 IF V$="REVIEW" THEN GOTO 4300
4050 IF V$="LIST" THEN GOTO 4400
4070 ON POS(V$="YN") GOTO 4000,4100,1400
4290 PRINT @(0,L-3),'CE',
4295 GOTO 9900
4300 REM " --- Review
4320 READ (CSM12_DEV,KEY=B0$(1,6),DOM=4330)
4340 GOSUB CLRSCRN
4390 GOTO 1000
4400 REM " --- Call LIST
4420 GOSUB LIST
4490 GOTO 4000
5000 REM " --- Display
5003 DISPLAY: 
5005 IF B2$(4,1)="M" THEN PRINT @(0,L),B0$(7,3),@(4,L+1),B2$(35,60); GOTO 5090
5020 DIM C1$(60)
5025 LET UNITCOST=0
5030 FIND (IVM01_DEV,KEY=N0$+B1$(1,20),DOM=5035)IOL=IVM01A
5035 FIND (IVM02_DEV,KEY=N0$+WH$+B1$(1,20),DOM=5040)IOL=IVM02A
5040 LET C1$(1)=FNITEM$(C1$,DESC[1],DESC[2],DESC[3])+FILL(60-LEN(C1$))
5050 PRINT @(0,L),B0$(7,3),SUBBILL$,@(4),B1$(1,P[0]),@(25),B[3]:P1$,@(34),B[0]:P1$,@(43),B[1]:P2$,@(52),B[4]:P2$,@(61),UNITCOST:P9$,@(70),UNITCOST*FNQTY(B[0],B[1],B[3],A4,B[4]):P9$,
5060 PRINT @(4,L+1),C1$(1,32)
5070 PRINT @(38,L+1),'SB',"Op Seq:",'SF',@(46),B2$(1,3),@(50),'SB',"U/M:",'SF',@(55),B2$(33,2),'SB',@(58),"Net Qty:",'SF',FNQTY(B[0],B[1],B[3],A4,B[4]):P1$
5080 LET SUBBILL$=" "
5090 RETURN 
5100 REM " --- List
5105 LIST: 
5110 GOSUB CLRSCRN
5130 READ (CSM12_DEV,KEY=RECORD$,DOM=5140)
5140 LET K$=KEY(CSM12_DEV,END=5300)
5150 IF POS(RECORD$=K$)<>1 THEN GOTO 5300
5160 READ (CSM12_DEV)IOL=CSM12A
5170 LET SUBBILL$=" "
5180 FIND (BMM01_DEV,KEY=N0$+B1$,DOM=5200)
5190 LET SUBBILL$="*"
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="C",V2$="",V3$="",V4$="<Enter>=Continue, <F4>=End: ",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5225 IF V3=4 THEN EXITTO 4000
5230 GOSUB CLRSCRN
5250 GOSUB DISPLAY
5260 LET L=L+2,SAVEB0$=B0$(1,9)
5290 GOTO 5140
5300 RETURN 
5400 REM " --- Clear Screen
5405 CLRSCRN: 
5420 PRINT @(0,L2),'CE',
5440 LET L=L2
5490 RETURN 
5500 REM " --- Display Calculated Fields
5505 SHOW_CALCD: 
5520 PRINT @(70,L),'CL',UNITCOST*FNQTY(B[0],B[1],B[3],A4,B[4]):P9$
5540 PRINT @(58,L+1),'SB',"Net Qty:",'SF',FNQTY(B[0],B[1],B[3],A4,B[4]):P1$
5590 RETURN 
6000 REM " --- Verify Item<>Bill Number
6030 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Item # May Not Be The Same As The Bill #, <Enter> To Continue: ",V0=1,V1=FNV(V4$),V2=22
6035 PRINT @(V1,V2),'RB',
6040 GOSUB 7000
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNQTY(Q1,Q2,Q3,Q4,Q5)=10000*Q1*Q2/(Q3*Q4*(100-Q5))
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 4290
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 EXIT 
9999 END

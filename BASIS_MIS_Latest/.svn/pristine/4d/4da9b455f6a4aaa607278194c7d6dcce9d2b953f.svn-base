0010 REM "GLR - Allocation Entry Register (Report Overlay)"
0020 REM "Program GLR.QB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$(1),A1$(1)
0220 GLM02A: IOLIST X0$(1),A[ALL],U[ALL]
0230 GLM03A: IOLIST C0$(1),C1$,C[ALL]
0290 GLE03A: IOLIST B0$(1),B1$,B[ALL]
0300 GLE13A: IOLIST W0$(1),W1$(1),W2$,W[ALL]
0400 REM " --- Parameters"
0410 DIM GLCONTROL$(640)
0420 LET GLCONTROL$(1)=STBL("!GLCONTROL",ERR=9900)
0450 LET M4$="##0.00%",M4=LEN(M4$),K9=5+P[4],K8=K9-3
0490 CALL "SYC.RM",M2$,M2,7,10
0500 REM " --- Initializations"
0510 DIM A0$(2+P[4]),A1$(35),B0$(K8),B[4],X0$(3+P[4]),O[5],A[MP],U[MP],S[2]
0520 DIM W0$(K9),W1$(P[4]),W[4],U4$(M4,"-"),U1$(M1-1,"-"),U2$(M2-1,"-")
0530 DIM C0$(32),C[2]
0550 LET H0=80,UNITHDR$="",BALANCED=0,PERIOD=NUM(BEGPER$,ERR=9900)
0560 IF P4$(3,1)<>"Y" THEN LET U2$=""
0590 LET C0$(1)=N0$+GLCONTROL$(18,2),C0$(5)="Unknown Journal ID"
0600 REM " --- Initialize print positions"
0610 LET O[5]=H0
0620 IF P4$(3,1)="Y" THEN LET O[5]=H0-M2,UNITHDR$="Units"
0630 LET O[4]=O[5]-M1,O[3]=O[4]-M4-1,X1=M0,X2=M0
0640 IF X1<9 THEN LET X1=9
0645 IF X2<12 THEN LET X2=12
0650 LET O[1]=O[0]+X1,O[2]=O[1]+X2,WIDTH=O[3]-O[2]
0660 IF WIDTH>35 THEN LET WIDTH=35
0700 REM " --- Background"
0750 CALL "SYC.NB","Printing",M0,COLUMN
0800 REM " --- Which GLM-02 record?"
0810 LET RECORD$="0"
0820 IF P4$(6,1)<>"Y" THEN LET RECORD$="4"
0900 REM " --- Position File"
0910 CALL "SYC.PA",SYS01_DEV,NUM(BEGPER$),FNYY_YEAR(BEGYEAR$),X$,GLDATE$,STATUS
0920 FIND (GLM03_DEV,KEY=C0$(1,4),DOM=0930)IOL=GLM03A
0990 READ (GLE03_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Get next GLE-03 record"
1010 LET K$=KEY(GLE03_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 READ (GLE03_DEV)IOL=GLE03A
1050 PRINT @(COLUMN,11),FNF$(B0$(3,P[3]),M0$)
1200 REM " --- Additional File Reads"
1210 DIM A0$(2+P[4]),A1$(WIDTH),X0$(3+P[4]),A[MP],U[MP],B[4],S[2]
1220 LET A0$(1)=B0$(1,2+P[4]),A1$(1)="(Not On File)"
1230 LET X0$(1)=B0$(1,2+P[4])+RECORD$
1240 FIND (GLM01_DEV,KEY=A0$,DOM=1250)IOL=GLM01A
1250 FIND (GLM02_DEV,KEY=X0$,DOM=1260)IOL=GLM02A
1500 REM " --- Accumulate total to allocate"
1510 LET B[0]=A[PERIOD],B[1]=0
1520 IF P4$(3,1)="Y" THEN LET B[1]=U[PERIOD]
1600 REM " --- Generate recap"
1610 LET ACCOUNT$=A0$(3,P[4]),AMOUNT=-B[0],UNITS=0
1620 LET REF1$="Source",REF2$="",REF3$=""
1630 IF P4$(3,1)="Y" THEN LET UNITS=-B[1]
1640 GOSUB GLRECAP
1650 LET REF2$=FNF$(A0$(3,P[3]),M0$)
1800 REM " --- Print header"
1810 IF L+4>L9 THEN GOSUB 5000
1820 LET UNITS$=""
1830 IF P4$(3,1)="Y" THEN LET UNITS$=STR(B[1]:M2$)
1850 PRINT (7)""
1860 PRINT (7)@(O[0]),FNF$(A0$(3,P[3]),M0$),@(O[2]),A1$,@(O[4]),B[0]:M1$,@(O[5]),UNITS$
1870 PRINT (7)""
1890 LET L=L+3
1900 REM " --- Position GLE-13"
1990 READ (GLE13_DEV,KEY=B0$,DOM=2000)
2000 REM " --- Read next detail record"
2010 LET K$=KEY(GLE13_DEV,END=3000)
2020 IF POS(B0$=K$)<>1 THEN GOTO 3000
2030 READ (GLE13_DEV)IOL=GLE13A
2100 REM " --- Last detail?"
2110 LET LAST=1
2120 LET K$=KEY(GLE13_DEV,END=2200)
2130 IF POS(B0$=K$)=1 THEN LET LAST=0
2200 REM " --- Additional File Reads"
2210 DIM A0$(2+P[4]),A1$(WIDTH)
2220 LET A0$(1)=N0$+W1$,A1$(1)="(Not On File)"
2230 FIND (GLM01_DEV,KEY=A0$,DOM=2240)IOL=GLM01A
2300 REM " --- Calculate amount to be allocated to this account"
2310 LET W[1]=B[0]*W[0]/100
2320 IF P4$(3,1)="Y" THEN LET W[2]=B[1]*W[0]/100
2400 REM " --- If last detail, check for rounding problems"
2410 IF LAST=0 THEN GOTO 2500
2420 LET X=B[0]-(S[1]+W[1])
2430 IF ABS(X)>0 THEN LET W[1]=W[1]+X
2440 IF P4$(3,1)<>"Y" THEN GOTO 2500
2450 LET X=B[1]-(S[2]+W[2])
2460 IF ABS(X)<=1 THEN LET W[2]=W[2]+X
2500 REM " --- Print a record"
2510 IF L+1>L9 THEN GOSUB 5000
2520 LET UNITS$="",UNITS=0
2530 IF P4$(3,1)="Y" THEN LET UNITS$=STR(W[2]:M2$),UNITS=W[2]
2550 PRINT (7)@(O[1]),FNF$(A0$(3,P[3]),M0$),@(O[2]),A1$,@(O[3]),W[0]:M4$,@(O[4]),W[1]:M1$,@(O[5]),UNITS$
2560 LET L=L+1
2600 REM " --- Accumulate totals"
2610 FOR X=0 TO 2
2620 LET S[X]=S[X]+W[X]
2630 NEXT X
2700 REM " --- Update GLE-13 record"
2710 WRITE (GLE13_DEV,KEY=W0$)IOL=GLE13A
2800 REM " --- Generate recap"
2810 LET ACCOUNT$=W1$(1,P[4]),MEMO$=C0$(5,20),AMOUNT=W[1]
2820 LET REF1$="Target",REF3$="Line "+W0$(3+P[4],3)
2830 GOSUB GLRECAP
2900 REM " --- Loop back for next detail"
2990 GOTO 2000
3000 REM " --- Print transaction totals"
3005 IF L+2>L9 THEN GOSUB 5000
3010 LET WARNING$=""
3020 IF S[0]<>100 THEN LET WARNING$="Entry Must Equal 100% ---->",BALANCED=1
3030 IF P4$(3,1)="Y" THEN LET UNITS$=STR(S[2]:M2$)
3040 PRINT (7)@(O[3]),U4$,@(O[4]),U1$,@(O[5]),U2$
3050 PRINT (7)WARNING$,@(O[3]-17),"Total For Entry",@(O[3]),S[0]:M4$,@(O[4]),S[1]:M1$,@(O[5]),UNITS$
3060 LET L=L+2
3500 REM " --- Update GLE-03 record"
3510 WRITE (GLE03_DEV,KEY=B0$)IOL=GLE03A
3900 REM " --- Loop back for next header"
3990 GOTO 1000
4000 REM " --- All Done"
4010 LET WIDTH=H0,UPDATE$="GLU.IA"
4090 RUN "GLR.XA"
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)""
5050 PRINT (7)@(O[0]),"Source",@(O[1]),"Destination"
5060 PRINT (7)@(O[0]),"Account",@(O[1]),"Account",@(O[2]),"Description",@(O[3]),"Percent",@(O[4]+M1-7),"Amount",@(O[5]+M2-6),UNITHDR$
5070 LET L=L+3
5090 RETURN 
6900 REM " --- Standard G/L Recap Routine"
6910 GLRECAP: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLW11_DEV,GLT05_DEV,ACCOUNT$,GLDATE$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions"
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return To Menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "Replenishment Report - Print Overlay
0020 REM "Program: POR.FB
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "28Oct96 CAJ: Program rewritten for 6.0.2 replenishment
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 APM01A: IOLIST *,VDATA$(1)
0220 APM05A: IOLIST *,VDATA2$(1)
0230 APM06A: IOLIST S0$,S1$(1),S[ALL]
0240 IVM01A: IOLIST *,ITEMDESC$(1)
0250 IVM10F: IOLIST R0$
0260 IVM10A: IOLIST PROD$
0270 POE06A: IOLIST A0$,A1$
0280 POE16A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0290 POE26A: IOLIST C0$(1),C1$(1),C2$,C[ALL]
0300 POE36A: IOLIST D0$(1),D1$(1),D[ALL]
0310 POE07A: IOLIST W0$(1),W1$(1)
0500 REM " --- Init data
0510 LET FOOTNUM=5,L9=59-FOOTNUM,HEADINGS=3,PAGE=0,WIDTH=132,WHEN$=F0$(7,3)
0520 DIM A0$(14),VENDOR$(6),PREVVEND$(6),S[11],B[16],HEADINGS$[HEADINGS]
0530 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,HEADINGS1LEN=LEN(HEADINGS$[1])
0540 DIM LF$(60,$0A$)
0550 LET PRINTBTOT$="N",PRINTVTOT$="N",PRINTPTOT$="N"
0560 LET DOPAGEBRK$="N",PRINTTOTLINE$="N"
0570 DIM BUYERTOTS[2],VENDTOTS[2],PRODTOTS[2]
0580 DIM REVIEW$(30)
0590 LET THEREAREBUYERS=1; REM "Used to print sel criteria when no POE-17
0600 REM " --- Open printer
0610 CALL "SYC.GA",7,1,"","",STATUS
0620 IF V1=1 THEN GOTO 9900
0700 REM " --- Background
0720 CALL "SYC.NB","Printing:",5+VENDLEN,COLUMN
1000 REM " --- Init Read
1020 READ (POE06_DEV,KEY=N0$,DOM=1100)
1100 REM " --- Main Read
1105 READPOE06: 
1120 LET K$=KEY(POE06_DEV,END=4000)
1140 IF POS(N0$=K$)<>1 THEN GOTO 4000
1210 READ (POE06_DEV,END=4000)IOL=POE06A
1220 LET BUYER$=A0$(3,3),VENDOR$=A0$(6,6)
1230 LET REVIEW$="Through Review Date: "+FNB$(A1$(3))
1240 LET COMMENT$=A1$(7,30)
1250 PRINT @(COLUMN,11),BUYER$,"  ",FNF$(VENDOR$(1,VENDLEN),VENDOMASK$),
1260 GOSUB TESTBREAKS
1500 REM " --- Init detail ptr
1505 INITPOE16: 
1520 READ (POE16_DEV,KEY=A0$(1,11),DOM=READPOE16)
1600 REM " --- Line item details
1605 READPOE16: 
1620 DIM B0$(36),B1$(20),B[16],LINEPOINT$(0),ORDPOINT$(0),AVAIL$(0),ONORDER$(0)
1630 DIM EOQ$(0),SAFETY$(0),MAX$(0),LEAD$(0),WEIGHT$(0),COST$(0),SUGG$(0)
1640 DIM ANNUALUSAGE$(0),AVGUSAGE$(0),EXTENSION$(0)
1650 LET K16$=KEY(POE16_DEV,END=NEXTPOE06)
1660 IF POS(A0$(1,11)=K16$)<>1 THEN GOTO NEXTPOE06
1670 READ (POE16_DEV)IOL=POE16A
1680 LET PRODTYPE$=K16$(12,3)
1690 GOSUB TESTBREAKS
1700 LET ITEMNUM$=B0$(15,20),WHSE$=B0$(35,2),STOCKLEVEL$=B1$(1,1)
1710 IF B1$(2,1)="Y" THEN LET ESTIMATED$="**"
1720 LET ANNUALUSAGE$=STR(B[12]:M2$),AVGUSAGE$=STR(B[13]:M2$)
1730 LET AVAIL$=STR(B[2]:M2$),ONORDER$=STR(B[3]:M2$)
1740 IF STOCKLEVEL$="I" THEN GOTO CHECKTOT4I
1745 IF SUGGONLY$<>"Y" THEN GOTO ASSIGNSUGG
1750 IF ITEMNUM$=PREVWITEM$ THEN GOTO ASSIGNSUGG
1760 LET PREVMINUSANNUSE=MINUSANNUSE,PREVMINUSAVGUSE=MINUSAVGUSE
1770 LET PREVMINUSAVAIL=MINUSAVAIL,PREVMINUSONORD=MINUSONORD
1790 LET MINUSANNUSE=0,MINUSAVGUSE=0,MINUSAVAIL=0,MINUSONORD=0
1795 LET PREVWITEM$=ITEMNUM$
1800 ASSIGNSUGG: 
1810 LET SUGG=B[9]
1820 REM " --- Skip whse detail if NOT INCLUDEWHS$
1830 IF INCLUDEWHSE$="Y" THEN GOTO INCLUDEWHSES
1840 GOSUB NEWITEM
1850 READ (POE16_DEV,KEY=B0$(1,34)+$FF$,DOM=NEXTPOE16)
1890 INCLUDEWHSES: 
1900 REM " --- Accum amts to subtract from total line for sugg=0 'W' items
1910 IF SUGGONLY$<>"Y" OR SUGG<>0 THEN GOTO ASSIGN4W
1920 LET MINUSANNUSE=MINUSANNUSE+B[12],MINUSAVGUSE=MINUSAVGUSE+B[13]
1930 LET MINUSAVAIL=MINUSAVAIL+B[2],MINUSONORD=MINUSONORD+B[3]
1935 IF ITEMNUM$<>PREVITEM$ THEN GOSUB NEWITEM
1940 GOTO NEXTPOE16
1950 ASSIGN4W: 
1960 LET LINEPOINT$=STR(B[0]:M2$),ORDPOINT$=STR(B[1]:M2$),EOQ$=STR(B[4]:M2$)
1970 LET SAFETY$=STR(B[5]:M2$),MAX$=STR(B[6]:M2$),LEAD$=STR(B[7]:M9$)
1980 LET SUGG$=STR(SUGG:M2$),WEIGHT$=STR(B[8]:M6$),COST$=STR(B[10]:M3$)
1990 LET EXTENSION$=STR(B[14]:M1$)
2000 GOTO TESTNEWITEM
2100 REM " --- Check suggested qty of total for stocked by 'I'
2105 CHECKTOT4I: 
2110 DIM C[16]
2120 LET MINUSANNUSE=0,MINUSAVGUSE=0,MINUSAVAIL=0,MINUSONORD=0
2125 LET PREVMINUSANNUSE=0,PREVMINUSAVGUSE=0,PREVMINUSAVAIL=0,PREVMINUSONORD=0
2130 FIND (POE26_DEV,KEY=A0$(1,11)+PRODTYPE$+ITEMNUM$+"  ",DOM=TESTINCLUDE)IOL=POE26A
2140 LET SUGG=C[9]
2145 TESTINCLUDE: 
2150 IF INCLUDEWHSE$="Y" THEN GOTO TESTSUGG
2160 GOSUB NEWITEM
2170 READ (POE16_DEV,KEY=B0$(1,34)+$FF$,DOM=NEXTPOE16)
2180 REM " --- Skip all whses for items by 'I' w/no sugg qty
2190 TESTSUGG: 
2200 IF SUGGONLY$<>"Y" THEN GOTO TESTNEWITEM
2210 IF SUGG=0 THEN READ (POE16_DEV,KEY=B0$(1,34)+$FF$,DOM=NEXTPOE16)
2220 REM " --- Test for new item
2230 TESTNEWITEM: 
2240 IF ITEMNUM$<>PREVITEM$ THEN GOSUB NEWITEM
2250 REM " --- Print
2260 IF L+1>L9 THEN LET PRINTVTOT$="N"; GOSUB NEWVENDOR
2270 PRINT (7)@(O[0]),WHSE$," ",@(O[1]),ANNUALUSAGE$,@(O[2]),AVGUSAGE$,@(O[3]),SAFETY$,@(O[4]),LEAD$,@(O[5]),AVAIL$,@(O[6]),ONORDER$,@(O[7]),ORDPOINT$,@(O[8]),MAX$,@(O[9]),EOQ$,@(O[10]),LINEPOINT$,@(O[11]),COST$,@(O[12]),SUGG$,@(O[13]),WEIGHT$,@(O[14]),EXTENSION$
2280 LET L=L+1
2290 IF STOCKLEVEL$="W" THEN GOSUB DOSURPLUS
2900 REM " --- Loop up for next detail
2905 NEXTPOE16: 
2920 GOTO READPOE16
3000 REM " --- Loop up for next Suggested Order header
3005 NEXTPOE06: 
3010 GOTO READPOE06
4000 REM " --- Done
4010 IF PAGE<>0 THEN GOTO SOMETHINGSPRINTED
4020 GOSUB 5000
4030 LET THEREAREBUYERS=0
4040 SOMETHINGSPRINTED: 
4060 IF PRINTTOTLINE$="Y" THEN GOSUB TOTALLINE
4070 GOSUB VENDTOTALS
4080 GOSUB BUYERTOTALS
4090 GOSUB PRINTFOOTER
4100 RUN "POU.BA"
5000 REM " --- Heading
5010 IF PAGE>0 THEN GOSUB PRINTFOOTER
5020 LET L=HEADINGS+1
5030 IF REVIEW$<>FILL(30) AND POS(REVIEW$=HEADINGS$[1])=0 THEN LET HEADINGS$[1]=HEADINGS$[1](1,HEADINGS1LEN)+" "+REVIEW$
5035 LET HEADINGS$[2]=BUYERHEAD$
5040 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5050 IF STATUS<>0 THEN EXITTO 9900
5060 IF COMMENT$=FILL(30) THEN GOTO 5090
5065 LET COMMENT$=CVS(COMMENT$,3); REM "Strip leading & trailing spaces
5070 PRINT (7)@(FNC(COMMENT$,WIDTH)),COMMENT$
5085 LET L=L+1
5090 RETURN 
5100 REM " --- New Vendor 
5105 NEWVENDOR: 
5110 IF PRINTVTOT$="Y" THEN GOSUB VENDTOTALS
5120 IF DOPAGEBRK$="Y" THEN LET DOPAGEBRK$="N"; GOSUB 5000
5130 IF L+12>L9 THEN GOSUB 5000
5140 GOSUB VADDRESS
5150 DIM VENDLINE$(132),TARGETTYPE$(10)
5160 LET VENDLINE$(1)="Vendor: "+FNF$(VENDOR$(1,VENDLEN),VENDOMASK$)
5170 LET VENDLINE$(17)=NAME$
5180 IF POS(" "<>VENDOR$)=0 THEN LET VENDLINE$(17)="No Stocking Vendor"
5190 IF S1$(7,2)<>"  " THEN LET VENDLINE$(48)="Addr: "+S1$(7,2)
5200 IF S1$(1,3)=FILL(3) THEN LET PRIREV$="None" ELSE LET PRIREV$=FNB$(S1$(1))
5210 IF S1$(4,3)=FILL(3) THEN LET NXTREV$="None" ELSE LET NXTREV$=FNB$(S1$(4))
5220 ON POS(S1$(9,1)="NDWU") GOTO DONEPOS,NONE,DOLLARS,WEIGHT,UNITS
5225 NONE: 
5230 LET TARGETTYPE$="N None"
5240 GOTO DONEPOS
5245 DOLLARS: 
5250 LET TARGETTYPE$="D Dollars"
5260 GOTO DONEPOS
5265 WEIGHT: 
5270 LET TARGETTYPE$="W Weight"
5280 GOTO DONEPOS
5285 UNITS: 
5290 LET TARGETTYPE$="U Units"
5300 GOTO DONEPOS
5305 DONEPOS: 
5310 IF S1$(10,3)=FILL(3) THEN LET LASTRCPT$="None" ELSE LET LASTRCPT$=FNB$(S1$(10))
5320 LET VENDLINE$(59)="Next Review: "+NXTREV$,VENDLINE$(83)="Target Type: "+TARGETTYPE$,VENDLINE$(107)="Target Units: "+STR(S[3]:M2$)
5330 IF S1$(10,3)=FILL(3) THEN LET LASTRCPT$="None" ELSE LET LASTRCPT$=FNB$(S1$(10))
5340 PRINT (7)""
5350 PRINT (7)@(0),VENDLINE$
5360 PRINT (7)@(16),ADDR1$,@(57),"Prior Review: ",PRIREV$,@(83),"Cycle Days: ",S[0]:"##0",@(105),"Target Weight: ",S[2]:M6$
5370 PRINT (7)@(16),ADDR2$,@(57),"Last Receipt: ",LASTRCPT$,@(104),"Target Dollars: ",S[1]:M1$
5380 IF POS(" "<>ADDR3$)<>0 THEN PRINT (7)@(16),ADDR3$; LET L=L+1
5390 PRINT (7)@(0),"Contact: ",CONTACT$,@(57),"Phone:  ",FNF$(PHONE$,PHNMSK$),@(90),"Fax:  ",FNF$(FAX$,PHNMSK$)
5400 PRINT (7)DASHES$
5410 PRINT (7)@(H[1]),"Act Annl",@(H[2]),"Wgt Per",@(H[3]),"Safety",@(H[4]),"Lead",@(H[6]),"On",@(H[7]),"Order",@(H[8]),"Max",@(H[10]),"Line",@(H[11]),"Unit",@(H[12]),"Suggested",@(H[13]),"Total"
5430 PRINT (7)@(H[0]),"Whs",@(H[1]),"Usage",@(H[2]),"Usage",@(H[3]),"Stock",@(H[4]),"Time",@(H[5]),"Avail",@(H[6]),"Order",@(H[7]),"Point",@(H[8]),"Qty",@(H[9]),"EOQ",@(H[10]),"Point",@(H[11]),"Cost",@(H[12]),"Order Qty",@(H[13]),"Weight",@(H[14]),"Extension"
5440 PRINT (7)DASHES$
5450 LET L=L+9
5460 LET PRINTVTOT$="Y",PREVVEND$=VENDOR$
5490 RETURN 
5500 REM " --- Get Vendor's address etc
5505 VADDRESS: 
5510 DIM VDATA$(195),VDATA2$(160),S1$(25),S[11]
5520 READ (APM01_DEV,KEY=N0$+VENDOR$,DOM=5530)IOL=APM01A
5530 READ (APM06_DEV,KEY=N0$+VENDOR$+BUYER$,DOM=5550)IOL=APM06A
5540 IF S1$(7,2)="  " THEN GOTO USEAPM01ADDR
5550 READ (APM05_DEV,KEY=N0$+VENDOR$+S1$(7,2),DOM=USEAPM01ADDR)IOL=APM05A
5560 USEAPM05ADRR: 
5570 LET VDATA$(1,125)=VDATA2$(1,125),VDATA$(186,10)=VDATA2$(146,10)
5580 LET VDATA$(126,20)=VDATA2$(126,20)
5590 USEAPM01ADDR: 
5600 LET NAME$=VDATA$(1,30),ADDR1$=VDATA$(31,24),ADDR2$=VDATA$(55,24)
5610 LET ADDR3$=VDATA$(79,24),ZIP$=FNZ$(VDATA$(103)),PHONE$=VDATA$(112,10)
5620 LET EXT$=VDATA$(122,4),FAX$=VDATA$(186,10),CONTACT$=VDATA$(126,20)
5630 IF POS(" "<>ADDR3$)>0 THEN LET ADDR3$=FNP$(ADDR3$)+"  "+ZIP$ ELSE LET ADDR2$=FNP$(ADDR2$)+"  "+ZIP$
5690 RETURN 
5700 REM " --- New Product Type
5705 NEWPRODTYPE: 
5710 IF PRINTPTOT$="Y" THEN GOSUB PRODTOTALS
5720 DIM PRODTOTS[2],PROD$(30)
5730 LET PROD$(7,20)="**Not On File**"
5740 FIND (IVM10_DEV,KEY=N0$+"A"+PRODTYPE$,DOM=5760)IOL=IVM10A
5750 LET PRODDESC$=PROD$(7,20)
5760 IF L+2>L9 THEN LET PRINTVTOT$="N"; GOSUB NEWVENDOR
5770 PRINT (7)""
5775 PRINT (7)@(0),"Product: ",PRODTYPE$," ",PRODDESC$
5780 LET L=L+2
5785 LET PRINTPTOT$="Y",PREVPRODTYPE$=PRODTYPE$
5790 RETURN 
5800 REM " --- New Item
5805 NEWITEM: 
5810 IF PRINTTOTLINE$="Y" THEN GOSUB TOTALLINE
5815 LET PREVMINUSANNUSE=0,PREVMINUSAVGUSE=0,PREVMINUSAVAIL=0,PREVMINUSONORD=0
5820 DIM ITEMDESC$(DESCLEN)
5830 LET ITEMDESC$(1)="** Not on File **"
5840 FIND (IVM01_DEV,KEY=N0$+ITEMNUM$,DOM=5860)IOL=IVM01A
5850 LET ITEMDESC$=FNITEM$(ITEMDESC$,DESC[1],DESC[2],DESC[3])
5855 IF SUGGONLY$="Y" AND SUGG=0 THEN GOTO 5880
5860 IF L+1>L9 THEN LET PRINTPTOT$="N"; GOSUB NEWPRODTYPE
5870 PRINT (7)ESTIMATED$,"  ",ITEMNUM$(1,ITEMLEN),"  ",ITEMDESC$,@(50),"Stocked by: ",STOCKLEVEL$
5875 LET L=L+1,PRINTTOTLINE$="Y"
5880 LET PREVITEM$=ITEMNUM$
5890 RETURN 
5900 REM " --- Print the 'footers' here
5905 PRINTFOOTER: 
5920 IF L<L9 THEN PRINT (7)LF$(1,L9-L)
5930 PRINT (7)DASHES$
5940 PRINT (7)@(0),"** Indicates period usage is estimated",@(40),"M - Suggested Buy Based on Maximum",@(85),"R - Suggested Buy Based on Reorder Point"
5950 PRINT (7)@(0),"O - Indicates On Hand Greater than Max",@(40),"E - Suggested Buy Rounded to EOQ Amount",@(85),"Y - Suggested Buy Limited to One Year's Usage"
5960 PRINT (7)@(0),"U - Indicates Unusual Monthly Usage   ",@(40),"L - Suggested Buy Based on Line Point",@(85),"2 - Suggested Buy Increased 1/2 Month's Usage"
5990 RETURN 
6000 REM " --- Test for Breaks
6005 TESTBREAKS: 
6010 IF BUYER$=PREVBUYER$ THEN GOTO TESTVENDOR
6015 IF PRINTPTOT$="Y" THEN GOSUB PRODTOTALS
6020 IF PRINTVTOT$="Y" THEN GOSUB VENDTOTALS
6025 IF PRINTBTOT$="Y" THEN GOSUB BUYERTOTALS
6030 GOSUB NEWBUYER
6035 GOSUB NEWVENDOR
6045 GOTO 6090
6050 TESTVENDOR: 
6055 IF VENDOR$=PREVVEND$ THEN GOTO TESTPROD
6060 GOSUB NEWVENDOR
6065 GOSUB NEWPRODTYPE
6070 GOTO 6090
6075 TESTPROD: 
6080 IF PRODTYPE$=PREVPRODTYPE$ THEN GOTO 6090
6085 GOSUB NEWPRODTYPE
6090 RETURN 
6100 REM " --- New Buyer
6110 NEWBUYER: 
6130 DIM BUYERTOTS[2],BUYERHEAD$(30),R0$(30)
6150 FIND (IVM10_DEV,KEY=N0$+"F"+BUYER$,DOM=6160)IOL=IVM10F
6160 LET BUYERHEAD$="Buyer Code: "+BUYER$+" "+R0$(7,20),BUYERHEAD$=BUYERHEAD$(1,POS(" "<>BUYERHEAD$,-1))
6170 GOSUB 5000
6180 LET PRINTBTOT$="Y",PREVBUYER$=BUYER$,DOPAGEBRK$="N"
6190 RETURN 
6200 REM " --- Print buyer totals
6205 BUYERTOTALS: 
6210 GOSUB 5000
6215 PRINT (7)""
6220 PRINT (7)""
6225 PRINT (7)@(O[12]-21),"Total For Buyer ",PREVBUYER$,@(O[12]),BUYERTOTS[0]:M2$,@(O[13]-1),BUYERTOTS[1]:"#"+M6$,@(O[14]-1),BUYERTOTS[2]:"#"+M1$
6230 PRINT (7)""
6235 LET L=L+4
6240 REM " --- Print selection critera for this buyer
6245 PRINT (7)"Selection Criteria:",@(HSEL[1]),"Beg",@(HSEL[2]),"End",@(HSEL[3]),"Beg",@(HSEL[4]),"End",@(HSEL[5]),"Beg",@(HSEL[6]),"End",@(HSEL[7]),"Beg",@(HSEL[8]),"End"
6250 PRINT (7)@(HSEL[0]),"Seq",@(HSEL[1]),"Buyer",@(HSEL[2]),"Buyer",@(HSEL[3]),"Vendor",@(HSEL[4]),"Vendor",@(HSEL[5]),"Date",@(HSEL[6]),"Date",@(HSEL[7]),"Whs",@(HSEL[8]),"Whs",@(HSEL[9]),"Comment"
6255 LET L=L+2
6260 DIM W0$(4),W1$(75)
6265 READ (POE17_DEV,KEY=N0$+PREVBUYER$,DOM=CRITERIALOOP)
6270 CRITERIALOOP: 
6275 IF THEREAREBUYERS THEN GOTO READPOE17
6280 LET W0$=KEY(POE07_DEV,END=DONEBUYERTOT)
6285 IF POS(N0$=W0$)<>1 THEN GOTO DONEBUYERTOT
6290 GOTO READPOE07
6295 READPOE17: 
6300 LET K17$=KEY(POE17_DEV,END=DONEBUYERTOT)
6305 IF POS(N0$+PREVBUYER$=K17$)<>1 THEN GOTO DONEBUYERTOT
6310 READ (POE17_DEV)
6315 LET W0$=K17$(1,2)+K17$(6,2)
6320 READPOE07: READ (POE07_DEV,KEY=W0$,DOM=NEXTPOE17)IOL=POE07A
6325 LET SEQNUM$=STR(W0$(3,2):"00"),COMMENT$=W1$(26,30)
6330 IF W1$(13,3)=FILL(3) THEN LET BUYERFROM$="First" ELSE LET BUYERFROM$=W1$(13,3)
6335 IF W1$(16,3)=FILL(3) THEN LET BUYERTHRU$="Last" ELSE LET BUYERTHRU$=W1$(16,3)
6340 IF W1$(1,6)=FILL(6) THEN LET VENDFROM$="First" ELSE LET VENDFROM$=FNF$(W1$(1,VENDLEN),VENDOMASK$)
6345 IF W1$(7,6)=FILL(6) THEN LET VENDTHRU$="Last" ELSE LET VENDTHRU$=FNF$(W1$(7,VENDLEN),VENDOMASK$)
6350 IF W1$(56,2)=FILL(2) THEN LET WHSFROM$="First" ELSE LET WHSFROM$=W1$(56,2)
6355 IF W1$(58,2)=FILL(2) THEN LET WHSTHRU$="Last" ELSE LET WHSTHRU$=W1$(58,2)
6360 IF W1$(19,3)=FILL(3) THEN LET DATEFROM$="First" ELSE LET DATEFROM$=FNB$(W1$(19,3))
6365 IF W1$(22,3)=FILL(3) THEN LET DATETHRU$="Last" ELSE LET DATETHRU$=FNB$(W1$(22,3))
6370 IF L+1>L9 THEN GOSUB 5000
6375 PRINT (7)@(HSEL[0]),SEQNUM$,@(HSEL[1]),BUYERFROM$,@(HSEL[2]),BUYERTHRU$,@(HSEL[3]),VENDFROM$,@(HSEL[4]),VENDTHRU$,@(HSEL[5]),DATEFROM$,@(HSEL[6]),DATETHRU$,@(HSEL[7]),WHSFROM$,@(HSEL[8]),WHSTHRU$,@(HSEL[9]),COMMENT$
6380 LET L=L+1
6385 NEXTPOE17: GOTO CRITERIALOOP
6390 DONEBUYERTOT: 
6395 RETURN 
6400 REM " --- Print Vendor Totals
6405 VENDTOTALS: 
6410 IF PRINTTOTLINE$="Y" THEN GOSUB TOTALLINE
6415 LET PRINTTOTLINE$="N"
6420 LET VENDTOTWT$=STR(VENDTOTS[1]:"#"+M6$),MINIMUMWT$=STR(S[2]:"#"+M6$)
6425 LET TOTWTDIFF$=STR(VENDTOTS[1]-S[2]:"#"+M6$)
6430 IF PRINTPTOT$="Y" THEN GOSUB PRODTOTALS
6435 IF L+4>L9 THEN LET PRINTVTOT$="N"; GOSUB NEWVENDOR
6440 PRINT (7)""
6445 PRINT (7)@(O[12]-20-VENDLEN),"Total For Vendor ",FNF$(PREVVEND$(1,VENDLEN),VENDOMASK$),@(O[12]),VENDTOTS[0]:M2$,@(O[13]-1),VENDTOTWT$,@(O[14]-1),VENDTOTS[2]:"#"+M1$
6450 PRINT (7)@(O[12]-17),"Target Minimum ",@(O[12]),S[3]:M2$,@(O[13]-1),MINIMUMWT$,@(O[14]-1),S[1]:"#"+M1$
6455 PRINT (7)""
6460 PRINT (7)@(O[12]-13),"Over/Under",@(O[12]),VENDTOTS[1]-S[3]:M2$,@(O[13]-1),TOTWTDIFF$,@(O[14]-1),VENDTOTS[2]-S[1]:"#"+M1$
6465 PRINT (7)""
6470 LET L=L+6
6475 DIM VENDTOTS[2]
6480 LET PRINTVTOT$="N",DOPAGEBRK$="Y"
6490 RETURN 
6500 REM " --- Print product type Subtotal
6505 PRODTOTALS: 
6520 IF PRINTTOTLINE$="Y" THEN GOSUB TOTALLINE
6525 LET PRINTTOTLINE$="N"
6530 LET TOTPRODWT$=STR(PRODTOTS[1]:"#"+M6$)
6540 PRINT (7)""
6550 PRINT (7)@(O[12]-29),"Total For Product Type ",PREVPRODTYPE$,@(O[12]),PRODTOTS[0]:M2$,@(O[13]-1),TOTPRODWT$,@(O[14]-1),PRODTOTS[2]:"#"+M1$
6560 LET L=L+2
6570 LET PRINTPTOT$="N"
6580 RETURN 
6600 REM " --- Print surplus of other whses for items stocked by 'W'
6605 DOSURPLUS: 
6610 DIM D0$(24),D[2],SURPLUS$(132)
6620 LET SURPLUS$(O[1])="Surplus Other Whses: ",COL=4
6630 READ (POE36_DEV,KEY=N0$(1,2)+ITEMNUM$,DOM=READPOE36)IOL=POE36A
6640 READPOE36: 
6650 LET K36$=KEY(POE36_DEV,END=PRINTSURPLUS)
6660 IF POS(N0$(1,2)+ITEMNUM$=K36$)<>1 THEN GOTO PRINTSURPLUS
6670 IF COL>12 THEN GOTO PRINTSURPLUS
6680 READ (POE36_DEV)IOL=POE36A
6690 IF D0$(23,2)=WHSE$ THEN GOTO NEXTPOE36
6700 LET SURPLUS$(O[COL])=D0$(23,2)+": "+STR(D[0]:M2$)
6710 LET COL=COL+2
6715 NEXTPOE36: 
6720 GOTO READPOE36
6740 PRINTSURPLUS: 
6750 IF COL<=4 THEN GOTO DONESURPLUS
6760 PRINT (7)SURPLUS$
6770 LET L=L+1
6780 DONESURPLUS: 
6790 RETURN 
6800 REM " --- Print total line
6805 TOTALLINE: 
6810 IF L+1>L9 THEN LET PRINTVTOT$="N"; GOSUB NEWVENDOR
6815 DIM C0$(36),C1$(20),C[16],ADJUSTCODES$(8),TLINEPOINT$(0),TORDPOINT$(0)
6820 DIM TAVAIL$(0),TONORDER$(0),TEOQ$(0),TSAFETY$(0),TMAX$(0),TLEAD$(0)
6825 DIM TSUGG$(0),TWEIGHT$(0),TCOST$(0),TANNUALUSAGE$(0),TAVGUSAGE$(0)
6830 DIM TEXTENSION$(0)
6840 FIND (POE26_DEV,KEY=N0$+PREVBUYER$+PREVVEND$+PREVPRODTYPE$+PREVITEM$+"  ",DOM=NOTOTAL)IOL=POE26A
6845 LET TSTOCKLEVEL$=C1$(1,1),ADJUSTCODES$=C1$(3,8),TEXTENSION$=STR(C[14]:M1$)
6850 LET TAVAIL$=STR(C[2]-PREVMINUSAVAIL:M2$),TWEIGHT$=STR(C[8]:M6$)
6855 LET TONORDER$=STR(C[3]-PREVMINUSONORD:M2$),TSUGG$=STR(C[9]:M2$)
6860 LET TANNUALUSAGE$=STR(C[12]-PREVMINUSANNUSE:M2$)
6865 LET TAVGUSAGE$=STR(C[13]-PREVMINUSAVGUSE:M2$)
6870 IF TSTOCKLEVEL$="W" THEN GOTO 6900
6880 LET TLINEPOINT$=STR(C[0]:M2$),TORDPOINT$=STR(C[1]:M2$),TEOQ$=STR(C[4]:M2$)
6890 LET TSAFETY$=STR(C[5]:M2$),TMAX$=STR(C[6]:M2$),TLEAD$=STR(C[7]:M9$)
6895 LET TCOST$=STR(C[10]:M3$)
6900 PRINT (7)@(0),ADJUSTCODES$,@(O[1]),TANNUALUSAGE$,@(O[2]),TAVGUSAGE$,@(O[3]),TSAFETY$,@(O[4]),TLEAD$,@(O[5]),TAVAIL$,@(O[6]),TONORDER$,@(O[7]),TORDPOINT$,@(O[8]),TMAX$,@(O[9]),TEOQ$,@(O[10]),TLINEPOINT$,@(O[11]),TCOST$,@(O[12]),TSUGG$,@(O[13]),TWEIGHT$,@(O[14]),TEXTENSION$
6910 LET TSUGG=C[9],TWEIGHT=C[8],TEXTENSION=C[14]
6920 LET BUYERTOTS[0]=BUYERTOTS[0]+TSUGG,BUYERTOTS[1]=BUYERTOTS[1]+TWEIGHT
6925 LET BUYERTOTS[2]=BUYERTOTS[2]+TEXTENSION,VENDTOTS[0]=VENDTOTS[0]+TSUGG
6935 LET VENDTOTS[1]=VENDTOTS[1]+TWEIGHT,VENDTOTS[2]=VENDTOTS[2]+TEXTENSION
6940 LET PRODTOTS[0]=PRODTOTS[0]+TSUGG,PRODTOTS[1]=PRODTOTS[1]+TWEIGHT
6945 LET PRODTOTS[2]=PRODTOTS[2]+TEXTENSION
6950 GOTO DONETOTAL
6955 NOTOTAL: 
6960 PRINT (7)@(1),"*** Total Line Not On File ***"
6965 DONETOTAL: 
6985 LET PRINTTOTLINE$="N",L=L+1
6990 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

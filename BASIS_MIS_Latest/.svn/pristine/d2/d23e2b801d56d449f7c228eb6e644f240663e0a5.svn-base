0010 REM BLK.20 - AUTHORIZATION/ACTIVATION FORM REPRINT FROM HISTORY
0020 LET SYS01T=UNT; OPEN (SYS01T)"SYS-01"
0030 LET TERMINAL$="T"+FID(0)
0040 READ (SYS01T,KEY=TERMINAL$)*,F0$; CLOSE (SYS01T)
0050 DIM CSM03$:"FIRM_ID:C(2),OS_LEVEL:C(3*),DESC:C(30*),PORT_ID:C(5),DLR1:C(15*),NUM1:N(1*),NUM2:N(1*)"
0060 DIM CSM04$:"FIRM_ID:C(2),PRODUCT_REV:C(3*),DESC:C(20*),KLN:C(1),DLR1:C(19*),NUM1:N(1*),NUM2:N(1*)"
0070 DIM SNM01$:"SERIAL_NBR:C(20*),SERIAL_DESC:C(60*),PRODUCT:C(3),PLATFORM:C(3),OS_LEVEL:C(3),PRODUCT_REV:C(3),LICENSE_TYPE:C(3),MEDIA_TYPE:C(3*),ACTIVE_FLAG:C(1),ORIG_SALE:C(3),CUSTOMER_NBR:C(6),PORT_ID:C(5),FIRM_ID:C(2),DEALER_STR:C(18*),COMMENT:C(60*),USERS:N(5*),DEALER_NUM_1:N(1*),DEALER_NUM_2:N(1*)"
0080 DIM SNT01$:"SERIAL_NBR:C(20),TRANS_DATE:C(3),SN_DET_SEQ:C(5*),CUSTOMER_NBR:C(6),AR_INV_NBR:C(7),SEQUENCE_NBR:C(3),TYPE_OF_SALE:C(3),PRODUCT_REV:C(3),AUTH_CODE:C(15),SERVER_NBR:C(20*),FIRM_ID:C(2),ACTION:C(1),FIXED_FLOAT:C(1),DEALER_STR:C(36*),USERS:N(5*),EXT_PRICE:N(7*),LICENSE_CNT:N(7*),NUM1:N(1*),NUM2:N(1*),NUM3:N(1*)"
0090 DIM SNT01TMP$:FATTR(SNT01$)
0100 LET TMPDIR$=STBL("AON")+"tmp/"
0110 LET SNM01CHAN=UNT; OPEN (SNM01CHAN)"SNM-01"
0120 LET SNT01CHAN=UNT; OPEN (SNT01CHAN)"SNT-01"
0130 LET CSM01CHAN=UNT; OPEN (CSM01CHAN)"CSM-01"
0140 LET CSM02CHAN=UNT; OPEN (CSM02CHAN)"CSM-02"
0150 LET CSM03CHAN=UNT; OPEN (CSM03CHAN)"CSM-03"
0160 LET CSM04CHAN=UNT; OPEN (CSM04CHAN)"CSM-04"
0170 GOSUB BUILD_PRINT_SORT
0180 IF RECORD_COUNT=0 THEN GOTO END_SORT
0190 GOSUB CREATE_OUTFILE
0200 LET TODAY$=DATE(0:"%Mz/%Dz/%Y")
0210 READ (E1,KEY="",DOM=READ_SORT)
0220 READ_SORT: 
0230 LET K$=KEY(E1,END=END_SORT); LET KL=LEN(K$)
0240 READ RECORD (E1,KEY=K$)SORT_REC$; LET SN$=SORT_REC$(KL+1,20)
0250 READ RECORD (SNM01CHAN,KEY=SN$)SNM01$
0260 READ (SNT01CHAN,KEY=SN$,DOM=READ_SNT01)
0270 READ_SNT01: 
0280 LET K$=KEY(SNT01CHAN,END=END_SNT01); IF POS(SN$=K$)=0 THEN GOTO END_SNT01
0290 READ RECORD (SNT01CHAN,KEY=K$)SNT01TMP$
0300 IF CVS(SNT01TMP.AUTH_CODE$,2)<>"" THEN LET SNT01$=FIELD(SNT01TMP$)
0310 GOTO READ_SNT01
0320 END_SNT01: 
0330 LET USERS=SNM01.USERS,FLOAT$=SNT01.FIXED_FLOAT$
0340 LET ITEM$=SNM01.PRODUCT$+SNM01.PLATFORM$+SNM01.OS_LEVEL$+SNM01.PRODUCT_REV$+SNM01.LICENSE_TYPE$
0350 IF POS(FLOAT$="FX")=0 THEN IF USERS<2 AND POS(ITEM$(1,2)="VPOD",2) THEN LET FLOAT$="X"
0360 READ RECORD (CSM04CHAN,KEY=F0$(16,2)+ITEM$(10,3))CSM04$
0370 LET RV$=CVS(CSM04.DESC$,3),P=POS(" "=RV$); IF P=0 THEN LET P=LEN(RV$)
0380 LET PROD_TYPE$=ITEM$(13,3),REV_LEVEL$=CVS(RV$(1,P),2)
0390 READ RECORD (CSM03CHAN,KEY=F0$(16,2)+ITEM$(7,3))CSM03$; LET PORT$=CSM03.PORT_ID$
0400 LET PORT_REV$=PORT$+"."+RV$,P=POS("."=RV$); IF P THEN LET PORT_REV$=PORT_REV$(1,6)+RV$(1,P-1)+RV$(P+1)
0410 READ (CSM01CHAN,KEY=F0$(16,2)+ITEM$(1,3))*,PRDDESC$
0420 READ (CSM02CHAN,KEY=F0$(16,2)+ITEM$(4,3))*,PLTDESC$
0430 rem LET PRINT_DESC$=CVS(PRDDESC$,2)+" "+CVS(PLTDESC$,2)
0432 LET PRINT_DESC$=CVS(PRDDESC$,2)
0440 rem IF CSM04.KLN$="L" THEN LET OSLINE$="Port ID "+PORT_REV$+"   "+CVS(CSM03.DESC$,2) ELSE LET OSLINE$=CVS(CSM03.DESC$,2),ITEM$=CVS(ITEM$,2)+"  "+PORT$
0442 IF CSM04.KLN$="L" THEN LET OSLINE$=CVS(REV_DESC$,2)+" Port ID "+PORT_REV$+"  "+cvs(pltdesc$,2) ELSE LET OSLINE$=CVS(pltdesc$,2),ITEM$=CVS(ITEM$,2)+"  "+PORT$
0450 IF LEN(PRINT_DESC$)>30 THEN LET PRINT_DESC$=PRINT_DESC$(1,30); if len(CVS(PRDDESC$,2))>20 then LET PRINT_DESC$=CVS(PRDDESC$,2)
0460 IF LEN(OSLINE$)>48 THEN LET OSLINE$=OSLINE$(1,48)
0470 PRINT 'SB',@(20,20),"SERIAL NO: ",'SF',
0480 LET LICTEXT$=""; PRINT @(33,20),SNM01.SERIAL_NBR$,
0490 IF CSM04.KLN$<>"L" THEN GOTO NOT_FLEX
0500 LET TMPSN$=CVS(SN$,2)
0520 hd$="REPRINT"
0530 call "temp_license.pgm",tmpsn$,lictext$
0540 p=pos(","=lictext$,1,4)
0550 if p then lictext$=lictext$(1,p)+hd$+lictext$(p+1)
0650 NOT_FLEX: 
0660 GOSUB WRITE_OUTREC
0670 GOTO READ_SORT
0680 END_SORT: 
0690 LET ENDCHAN=UNT
0700 IF E1 THEN CLOSE (E1); ERASE E1$,ERR=THE_END
0710 THE_END: 
0720 FOR ANYCHAN=1 TO ENDCHAN; CLOSE (ANYCHAN,ERR=NXTCHAN)
0730 NXTCHAN: NEXT ANYCHAN
0740 IF RECORD_COUNT=0 THEN GOTO NO_RECORDS
0750 IF CHOICE$="P" THEN RUN "BLK.12"
0760 IF CHOICE$="E" OR CHOICE$="F" THEN RUN "BLK.16"
0770 NO_RECORDS: 
0780 PRINT 'WINDOW'(5,5,70,8,"Key/License Form Printing"),'CS',"No serial numbers selected."; WAIT 4; PRINT 'POP'
0790 RUN "SYS.AA"
0850 BUILD_PRINT_SORT: 
0860 LET RECORD_COUNT=0
0870 LET E1$=TMPDIR$+"BLK"+FID(0)+".TMP"; ERASE E1$,ERR=MAKE_SORT
0880 MAKE_SORT: MKEYED E1$,[1:26],0,60
0890 LET E1=UNT; OPEN (E1)E1$
0900 READ (SNM01CHAN,KEY=START_SNUM$,DOM=SNM01_CHECK)
0910 SNM01_CHECK: 
0920 READ RECORD (SNM01CHAN,END=END_SNM01_CHECK)SNM01$
0930 IF SNM01.ACTIVE_FLAG$="N" THEN GOTO SNM01_CHECK
0940 IF CVS(SNM01.SERIAL_NBR$,2)<CVS(START_SNUM$,2) THEN GOTO SNM01_CHECK
0950 IF CVS(SNM01.SERIAL_NBR$,2)>CVS(END_SNUM$,2) THEN GOTO END_SNM01_CHECK
0960 LET SORT_REC$=SNM01.CUSTOMER_NBR$+SNM01.SERIAL_NBR$+SNM01.SERIAL_NBR$
0970 WRITE RECORD (E1)SORT_REC$; LET RECORD_COUNT=RECORD_COUNT+1
0980 GOTO SNM01_CHECK
0990 END_SNM01_CHECK: RETURN
1000 CREATE_OUTFILE: 
1010 LET OUTCHAN=UNT,OUTFILE$=TMPDIR$+FID(0)+"BLK10.txt"
1020 ERASE OUTFILE$,ERR=MAKE_OUTFILE
1030 MAKE_OUTFILE: STRING OUTFILE$
1040 OPEN (OUTCHAN)OUTFILE$
1050 LET DSBCHAN=UNT,DSBFILE$=TMPDIR$+FID(0)+"BLK10dsb.txt"
1060 ERASE DSBFILE$,ERR=MAKE_DSBFILE
1070 MAKE_DSBFILE: STRING DSBFILE$
1080 OPEN (DSBCHAN)DSBFILE$
1090 RETURN
1100 WRITE_OUTREC: 
1110 REM Add purchase order # and cust #
1120 LET LICTEXT$=LICTEXT$+",,"+SNM01.CUSTOMER_NBR$
1130 LET COMMA_POS=POS(","=PRINT_DESC$); IF COMMA_POS THEN LET PRINT_DESC$(COMMA_POS,1)=" "; GOTO 1130
1140 LET COMMA_POS=POS(","=OSLINE$); IF COMMA_POS THEN LET OSLINE$(COMMA_POS,1)=" "; GOTO 1140
1150 LET OUTREC$=CVS(SNM01.SERIAL_NBR$,2)+","+CVS(SNT01.AUTH_CODE$,2)+","+STR(USERS)+","+SNT01.AR_INV_NBR$+","+TODAY$+","+CVS(ITEM$,2)+","+CVS(PRINT_DESC$,35)+","+CVS(REV_LEVEL$,2)+","+CVS(OSLINE$,35)+LICTEXT$
1160 PRINT (OUTCHAN)OUTREC$
1170 PRINT (DSBCHAN)OUTREC$
1180 RETURN

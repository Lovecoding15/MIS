0010 REM "APR - Recurring Invoice Register"
0020 REM "Program APR.NA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=8
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APE-03",FILES$[2]="APE-13",FILES$[3]="APM-01"
0140 LET FILES$[4]="APM-03",FILES$[5]="APM-10",FILES$[6]="SYS-01"
0160 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET APE03_DEV=CHANNELS[1],APE13_DEV=CHANNELS[2],APM01_DEV=CHANNELS[3]
0185 LET APM03_DEV=CHANNELS[4],APM10_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0200 REM " --- IOLIST's"
0210 APE03A: IOLIST A0$,A[0],A[1],A[2]
0220 APM01A: IOLIST B0$,B1$(1)
0230 APM03A: IOLIST B0$,B1$(1)
0240 APM10A: IOLIST H9$(1)
0245 APM10B: IOLIST *,Z0$
0250 GLM01A: IOLIST C0$(1),C1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0310 APE13A: IOLIST W0$,W1$(1),W2$,W[0],W[1]
0400 REM " --- Parameters"
0405 DIM P[2],G[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N3$=F5$,SOURCE$=PGM(-2),WHEN$=F0$(7,3)
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 FOR X=0 TO 4
0435 LET G[X]=NUM(G2$(X*2+1,2),ERR=0440)
0440 NEXT X
0445 LET G[2]=FNYY_YEAR(G2$(5,2))
0450 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(9,1))
0500 REM " --- Initializations"
0510 DIM J$(80,"-"),W1$(G[4]+1),HEADINGS$[1],B1$(105)
0520 DIM U[2],T[2],A[2],W[1],O[8],Z[3]
0530 LET M0=LEN(M0$),M1=LEN(M1$),G5=LEN(G5$),H0$="",H1$="",H2$=""
0535 LET WIDTH=132,PAGE=0,L9=59,L=L9+1,UNITS=0,ERRORS=0
0540 LET HEADINGS=1,HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,H3$="Discount"
0550 LET O[0]=0,O[1]=O[0]+M0+1,O[8]=WIDTH-M1
0555 IF P3$(3,1)="Y" THEN LET H2$=H3$+"/",H3$="Retention"
0560 LET O[7]=O[8]-M1,O[6]=O[7]-16,O[5]=O[6],O[5]=O[6]-10
0565 LET O[4]=O[5]-21,O[3]=O[4]-12,O[2]=O[3]-11,Z[0]=O[2]
0570 LET Z[3]=O[7],Z[2]=Z[3]-M1,Z[1]=Z[0]+G5+1,GW=Z[2]-Z[1]-1
0575 IF GW>35 THEN LET GW=35
0600 REM " --- Additional File Opens "
0650 LET GL$="N",STATUS=0
0655 CALL "GLC.BA",ERR=0660,SOURCE$,N2$,GLW11$,GL$,STATUS
0660 IF STATUS<>0 THEN GOTO 9900
0665 IF GL$<>"Y" THEN GOTO 0700
0670 LET FILES$[7]="GLM-01",FILES$[8]=GLW11$,OPTIONS$[8]="C"
0675 CALL "SYC.DA",1,7,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS>0 THEN GOTO 9900
0690 LET GLM01_DEV=CHANNELS[7],GLW11_DEV=CHANNELS[8]
0800 REM " --- Run Report?
0810 LET V4$="Do You Want To Print The "+N3$
0820 CALL "SYC.YN",1,V4$,2,V$,V3
0830 IF V3=4 THEN GOTO 9900
0840 IF V$<>"Y" THEN GOTO 9900
0900 REM " --- File Initializations"
0910 LET W=M0+7+(P3$(1,1)="Y")*3
0920 CALL "SYC.NB","Printing",W,COLUMN
0930 CALL "SYC.GA",7,1,"","",STATUS
0940 IF STATUS THEN GOTO 9900
0990 READ (APE03_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next invoice header"
1010 DIM VENDOR$(M0)
1020 LET K$=KEY(APE03_DEV,END=4000)
1030 IF POS(N0$=K$)<>1 THEN GOTO 4000
1040 READ (APE03_DEV)IOL=APE03A
1050 IF K$(21,2)<>"00" THEN GOTO 3900
1060 LET VENDOR$(1)=A0$(5,6),X$=""
1070 IF A0$(5,4)<>"TEMP" THEN LET VENDOR$(1)=FNF$(A0$(5,P[0]),M0$)
1080 IF P3$(1,1)="Y" THEN LET X$=A0$(3,2)+" "
1085 LET X$=X$+VENDOR$+" "+A0$(11,6)
1090 PRINT @(COLUMN,11),X$,
1200 REM " --- Level Breaks"
1220 IF K$(3,2)<>T0$ THEN GOSUB 6000
1240 IF A0$(5,6)<>T1$ THEN GOSUB 6400
1300 REM " --- Regular vendor"
1310 DIM VENDNAME$(30)
1320 LET B1$(1)="Unknown Vendor",MISSING=1
1330 IF A0$(5,4)="TEMP" THEN GOTO 1400
1350 FIND (APM01_DEV,KEY=N0$+A0$(5,6),DOM=1450)IOL=APM01A
1360 LET VENDNAME$(1)=B1$(1,30),MISSING=0
1390 GOTO 1450
1400 REM " --- Temporary vendor"
1410 FIND (APM03_DEV,KEY=A0$(1,4)+A0$(11,10),DOM=1450)IOL=APM03A
1420 LET VENDNAME$(1)=B1$(1,30),MISSING=0
1450 REM " --- Set G/L reference fields"
1460 LET REF1$=VENDNUMB$,REF2$=A0$(11,10),REF3$="",MEMO$=VENDNAME$
1470 IF P3$(1,1)="Y" THEN LET REF3$="AP Type "+A0$(3,2)
1500 REM " --- Distribution Code"
1510 IF P3$(4,1)="Y" THEN GOTO 1600
1520 DIM Z0$(4*G[4],"0")
1530 FIND (APM10_DEV,KEY=N0$+"B"+A0$(23,2),DOM=1600)IOL=APM10B
1540 LET ACCOUNT$=Z0$(1,G[4]),GLDATE$=A0$(32,3),AMOUNT=A[2]-A[0],UNITS=0
1550 GOSUB GLRECAP
1560 IF P3$(3,1)="N" THEN GOTO 1600
1570 LET ACCOUNT$=Z0$(3*G[4]+1,G[4]),AMOUNT=-A[2]
1580 GOSUB GLRECAP
1600 REM " --- Print invoice heading"
1610 LET ERRORS=ERRORS+MISSING
1615 IF L+3>L9 THEN GOSUB 5000
1635 IF P3$(3,1)="Y" THEN LET RETENTION$=STR(A[2]:M1$)
1640 PRINT (7)""
1650 PRINT (7)@(O[0]),VENDNUMB$,@(O[1]),VENDNAME$,@(O[2]),A0$(11,10),@(O[3]),A0$(23,2)," ",A0$(25,2)," ",A0$(27,2)," ",A0$(67,1),@(O[4]),FNB$(A0$(29,3)),"  ",FNB$(A0$(32,3)),@(O[6]),A0$(41,10),@(O[7]),A[0]:M1$,@(O[8]),A[1]:M1$
1660 PRINT (7)@(O[4]),FNB$(A0$(35,3)),"  ",FNB$(A0$(38,3)),@(O[6]),A0$(51,16),@(O[8]),RETENTION$
1690 LET L=L+3,L2=L2+1
1700 REM " --- Accumulate totals"
1710 FOR X=0 TO 2
1720 LET T[X]=T[X]+A[X],U[X]=U[X]+A[X],S[X]=S[X]+A[X]
1730 NEXT X
1800 REM " --- Subheading"
1810 IF GL$<>"Y" THEN GOTO 1900
1815 IF L+2>L9 THEN GOSUB 5000
1820 PRINT (7)""
1830 PRINT (7)@(Z[0]),"Account",@(Z[1]),"Description",@(Z[2]+M1-7),"Amount",@(Z[3]),H4$
1840 LET L=L+2
1900 REM 
1910 LET W0=0,L1=0
2000 REM " --- Print detail lines"
2005 READ (APE13_DEV,KEY=A0$(1,20),DOM=2006)
2010 LET K$=KEY(APE13_DEV,END=3000)
2020 IF K$(1,20)<>A0$(1,20) THEN GOTO 3000
2030 READ (APE13_DEV)IOL=APE13A
2200 REM " --- Print G/L Distribution"
2210 IF GL$<>"Y" THEN GOTO 2900
2220 IF L1>0 THEN IF L+1>L9 THEN GOSUB 5000
2230 DIM C0$(2+G[4]),C1$(GW)
2240 LET C0$(1)=N0$+W1$(1,G[4]),C1$(1)="Unknown G/L Account",POCOST$=""
2250 FIND (GLM01_DEV,KEY=C0$,DOM=2260)IOL=GLM01A
2270 PRINT (7)@(Z[0]),FNF$(W1$(1,G[3]),G5$),@(Z[1]),C1$,@(Z[2]),W[0]:M1$
2290 LET L=L+1,L1=L1+1,W0=W0+W[0],T0=T0+W[0],T1=T1+W[0]
2300 REM " --- G/L Summary"
2310 LET ACCOUNT$=W1$(1,G[4]),GLDATE$=A0$(32,3),AMOUNT=W[0],UNITS=W[1]
2320 LET REF3$="Line "+W0$(21,2)
2380 IF P3$(4,1)<>"Y" THEN GOSUB GLRECAP
2900 REM " --- Loop back for next detail line"
2990 GOTO 2010
3000 REM " --- Line Feed?"
3010 IF L1<>0 THEN GOTO 3100
3015 IF L+1>L9 THEN GOSUB 5000
3020 PRINT (7)""
3030 LET L=L+1
3100 REM " --- Check balance"
3110 IF GL$="N" OR W0=A[0] THEN GOTO 3900
3115 IF L+1>L9 THEN GOSUB 5000
3120 PRINT (7)@(Z[0]),"---> WARNING: This Transaction Is Out Of Balance"
3130 LET L=L+1,ERRORS=ERRORS+1
3900 REM " --- Loop back for next header"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF L+2>L9 THEN GOSUB 5000
4020 LET T9$="E"
4030 GOSUB 6000
4035 IF P3$(1,1)="Y" THEN LET H9$=""; GOSUB 5000
4040 LET TOTAL$="Total For Report",T0=U[0],T1=U[1],T2=U[2]
4050 GOSUB 6800
4080 IF ERRORS=0 THEN GOTO 4100
4085 PRINT (7)""
4090 PRINT (7)@(O[1]),"--> WARNING: This Report Is Out Of Balance Or Missing Information"
4100 REM " --- Run next overlay"
4110 LET UPDATE$="APU.FA"
4180 IF GL$="Y" THEN RUN "GLR.XA"
4185 RUN UPDATE$
4190 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 IF H9$<>"" THEN PRINT (7)@(FNC(H9$,WIDTH)),H9$; LET L=L+1
5050 PRINT (7)""
5070 PRINT (7)@(O[4]),"Inv Date  Acct Date",@(O[5]),H0$,@(O[6]),"Reference/",@(O[7]+M1-8),"Invoice",@(O[8]+M1-(LEN(H2$)+1)),H2$
5080 PRINT (7)@(O[0]),"Vendor",@(O[1]),"Name",@(O[2]),"Invoice",@(O[3]),"DC PG TC HD",@(O[4]),"Due Date  Disc Date",@(O[5]),H1$,@(O[6]),"Memo",@(O[7]+M1-7),"Amount",@(O[8]+M1-(LEN(H3$)+1)),H3$
5085 LET L=L+3
5090 RETURN 
6000 REM "AP TYPE BREAK"
6010 IF T9$="" THEN LET T9$="H"
6020 GOSUB 6400
6030 IF T9$="H" THEN LET T9$=""
6040 IF T0$="" THEN GOTO 6100
6050 IF P3$(1,1)<>"Y" THEN GOTO 6100
6080 LET TOTAL$=H9$+" Total",T0=T[0],T1=T[1],T2=T[2]
6090 GOSUB 6800
6100 IF T9$<>"" THEN GOTO 6190
6120 DIM T[2],H9$(32)
6140 LET T0$=A0$(3,2),L=L9+1,T1$=""
6150 IF P3$(1,1)<>"Y" THEN GOTO 6190
6160 LET H9$(6,20)="Unknown AP Type",MISSING=1
6170 FIND (APM10_DEV,KEY=N0$+"A"+T0$,DOM=6180)IOL=APM10A
6175 LET MISSING=0
6180 LET H9$="AP Type "+T0$+" "+FNP$(H9$(6,20)),ERRORS=ERRORS+MISSING
6190 RETURN 
6400 REM "VENDOR SUBTOTAL"
6420 IF T1$="" THEN GOTO 6500
6430 IF L2=1 THEN GOTO 6500
6440 LET TOTAL$="Vendor "+VENDNUMB$+" "+VENDNAME$+" Total"
6450 LET T0=S[0],T1=S[1],T2=S[2]
6460 GOSUB 6800
6500 IF T9$<>"" THEN RETURN 
6520 DIM S[2]
6540 LET T1$=A0$(5,6),L2=0
6590 RETURN 
6800 REM " --- Print totals"
6805 IF L+3>L9 THEN GOSUB 5000
6810 PRINT (7)@(O[7]),J$(1,2*M1)
6820 PRINT (7)@(O[7]-LEN(TOTAL$)),TOTAL$,@(O[7]),T0:M1$,@(O[8]),T1:M1$
6830 IF P3$(3,1)="Y" THEN PRINT (7)@(O[8]),T2:M1$
6840 IF P3$(3,1)<>"Y" THEN PRINT (7)""
6850 LET L=L+3
6890 RETURN 
6900 REM " --- Standard G/L Recap Routine"
6910 GLRECAP: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLW11_DEV,GLT05_DEV,ACCOUNT$,GLDATE$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

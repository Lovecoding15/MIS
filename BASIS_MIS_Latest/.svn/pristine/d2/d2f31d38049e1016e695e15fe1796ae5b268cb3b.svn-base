0010 REM "DDR - Data Dictionary Where-Used Report"
0020 REM "Program DDR.DB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS=1 THEN GOTO 9900
0200 REM " --- IOLists
0220 DDM04A: IOLIST A0$(1),A1$(1),A[ALL]
0500 REM " --- Initializations
0510 DIM A0$(10),A1$(122),A[2],H3$(70,"-"),MILESTONE$(39)
0530 LET LAST$="",ROW=0,COLUMN=49,ELEMENTS=0,MAX=52
0540 LET HEADINGS=4,PAGE=0,WIDTH=132,WHEN$=F0$(7,3)
0550 DIM HEADINGS$[HEADINGS],BUFFER$[MAX](WIDTH),H6$(WIDTH)
0560 LET VERSION$="6.0.0",VERSION$=STBL("!VERSION",ERR=0565)
0565 LET HEADINGS$[0]="ADD+ON Software - Version "+VERSION$
0570 LET HEADINGS$[1]=N4$,H3$(30,12)=" Where Used "
0580 IF BEGNAME$<>"" THEN LET BEGNAME$=BEGNAME$+FILL(12-LEN(BEGNAME$))
0590 IF ENDNAME$<>"" THEN LET ENDNAME$=ENDNAME$+FILL(12-LEN(ENDNAME$))
0600 REM 
0605 LET H6$(1)="Includes Data Types "+DATATYPE$,X$="All Revisions"
0610 IF REVISION$<>"" THEN LET X$="Revised On or After "+FNB6$(REVISION$)
0615 LET H6$(WIDTH-LEN(X$))=X$,H4$="From ",H5$="From "
0620 IF BEGFILE$<>"" THEN LET H4$=H4$+BEGFILE$ ELSE LET H4$=H4$+"First Data File"
0625 IF BEGNAME$<>"" THEN LET H5$=H5$+BEGNAME$ ELSE LET H5$=H5$+"First Data Name"
0630 LET H4$=H4$+" Through ",H5$=H5$+" Through "
0635 IF ENDFILE$<>"" THEN LET H4$=H4$+ENDFILE$ ELSE LET H4$=H4$+"Last Data File"
0645 IF ENDNAME$<>"" THEN LET H5$=H5$+ENDNAME$ ELSE LET H5$=H5$+"Last Data Name"
0650 LET P=FNC(H5$,WIDTH)+1,H6$(P,LEN(H5$))=H5$
0660 LET HEADINGS$[2]=H4$,HEADINGS$[3]=H6$
0700 REM " --- Background
0710 CALL "SYC.WC",1,0,80,0,0,18,0
0720 CALL "SYC.NB","Printing",39,COL
0900 REM " --- Position file
0990 READ (DDM06_DEV1,KEY=BEGNAME$,DOM=1000)
1000 REM " --- Read next sort record
1010 LET K$=KEY(DDM06_DEV1,END=4000)
1020 IF ENDNAME$<>"" THEN IF K$(1,12)>ENDNAME$ THEN GOTO 4000
1030 READ (DDM06_DEV1)
1100 REM " --- Range/Edit Tests
1110 IF BEGFILE$<>"" THEN IF K$(48,6)<BEGFILE$ THEN GOTO 3900
1120 IF ENDFILE$<>"" THEN IF K$(48,6)>ENDFILE$ THEN GOTO 3900
1130 IF DATATYPE$<>"" THEN IF POS(K$(43,1)=DATATYPE$)=0 THEN GOTO 3900
1140 LET X0$=K$
1150 IF TIMES=0 THEN GOTO 1200
1160 GOSUB 6200
1170 IF OCCURS<=TIMES THEN GOTO 1200
1180 READ (DDM06_DEV1,KEY=K$(1,42)+$FF$,DOM=1190)
1190 GOTO 3900
1200 REM " --- Revision Date?
1210 IF REVISION$="" THEN GOTO 1300
1220 LET A0$(1)=X0$(48)
1230 FIND (DDM04_DEV,KEY=A0$,DOM=3900)IOL=DDM04A
1240 IF A1$(31,6)<REVISION$ THEN GOTO 3900
1300 REM " --- Level breaks?
1310 IF LAST$="" THEN GOSUB 6000
1320 IF LAST$<>X0$(1,42) THEN GOSUB 6000
1330 IF TYPE$<>X0$(43,5) THEN GOSUB 6000
1340 LET COLUMN=COLUMN+14
1350 IF COLUMN+12<WIDTH THEN GOTO 1400
1360 LET ROW=ROW+1,COLUMN=63
1370 IF ROW>MAX THEN GOSUB 5000
1400 REM " --- Store information in page buffer
1410 LET BUFFER$[ROW](COLUMN)=K$(48,6)+" "+K$(54,1)+" "+K$(55,3)
3900 REM " --- Loop back for next record
3990 GOTO 1000
4000 REM " --- All done
4010 LET ROW=ROW+2
4020 IF ROW>MAX THEN GOSUB 5000
4030 LET BUFFER$[ROW](1)=STR(ELEMENTS)+" Element(s) Reported"
4050 GOSUB 5000
4090 GOTO 9900
5000 REM " --- Print report heading
5010 LET ROW=1,COLUMN=63,NUMBER=MAX,L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS<>0 THEN GOTO 9900
5050 PRINT (7)"Data Name",@(13),"Description",@(50),"Type Length ",H3$
5060 PRINT (7)""
5100 REM " --- Print report page
5110 FOR X=MAX TO 1 STEP -1
5120 LET NUMBER=X
5130 IF FNP$(BUFFER$[X])<>"" THEN EXITTO 5150
5140 NEXT X
5150 FOR X=1 TO NUMBER
5160 PRINT (7)BUFFER$[X]
5170 NEXT X
5180 DIM BUFFER$[MAX](WIDTH)
5190 RETURN 
6000 REM " --- Element Break
6010 LET ROW=ROW+2,ELEMENTS=ELEMENTS+1
6020 IF ROW>MAX THEN GOSUB 5000
6030 LET LAST$=X0$(1,42),TYPE$=X0$(43,5),COLUMN=49
6040 LET BUFFER$[ROW](1)=X0$(1,12)+" "+X0$(13,30)
6050 LET BUFFER$[ROW](51)=X0$(43,1)
6060 LET BUFFER$[ROW](57)=STR(NUM(X0$(44,4)):"#,##0")
6070 LET MILESTONE$(1)=LAST$(1,12)+" "+LAST$(13)
6080 PRINT @(COL,11),MILESTONE$
6090 RETURN 
6200 REM " --- Count number of occurences
6210 LET OCCURS=1
6220 READ (DDM06_DEV2,KEY=X0$)
6230 LET X$=KEY(DDM06_DEV2,END=6290)
6240 IF X$(1,43)<>X0$(1,43) THEN GOTO 6290
6250 READ (DDM06_DEV2)
6260 LET OCCURS=OCCURS+1
6270 GOTO 6230
6290 RETURN 
8000 REM " --- Functions
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM 
9950 RUN "SYS.AA"
9999 END

0010 REM "POR - Purchase Order Expediting Report
0020 REM "Program POR.KA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=11
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="APM-01",FILES$[2]="APM-10",FILES$[3]="IVM-01"
0130 LET FILES$[4]="IVM-10",FILES$[5]="POE-02",FILES$[6]="POE-12"
0135 LET FILES$[7]="POE-32",FILES$[8]="POM-02",FILES$[9]="SYS-01"
0145 LET FILES$[10]="ARM-01",FILES$[11]="ARM-03"
0160 CALL "SYC.DA",1,1,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET APM01_DEV=CHANNELS[1],APM10_DEV=CHANNELS[2],IVM01_DEV=CHANNELS[3]
0180 LET IVM10_DEV=CHANNELS[4],POE02_DEV=CHANNELS[5],POE12_DEV=CHANNELS[6]
0185 LET POE32_DEV=CHANNELS[7],POM02_DEV=CHANNELS[8],SYS01_DEV=CHANNELS[9]
0200 REM " --- IOLists
0240 APM01A: IOLIST X0$,X1$(1)
0280 IVM10C: IOLIST X1$(1)
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 APS01A: IOLIST U0$,VENDIMASK$,U2$,U3$,U4$,VENDOMASK$
0320 ARS01A: IOLIST U0$,CUSTIMASK$,U2$,U3$,U4$,CUSTOMASK$
0330 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0340 POS01A: IOLIST P0$,P1$,P2$,P3$,P4$
0400 REM " --- Parameters
0405 DIM INFO$[20],DESCLEN[3]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO",AP$="N",AR$="N",WHEN$=F0$(7,3)
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=POS01A
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 LET VENDLEN=NUM(U2$(1,2))
0435 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET IVPRECISION=NUM(I2$(5,1)),ITEMLEN=NUM(I2$(1,2))
0445 FOR X=1 TO 3
0450 LET DESCLEN[X]=NUM(I2$((X-1)*2+9,2))
0455 NEXT X
0460 CALL "SYC.VA","AR",INFO$[ALL]
0465 LET AR$=INFO$[20]
0500 REM " --- Init Data
0510 PRECISION IVPRECISION
0520 LET WHSE$=I3$(6,2)
0530 LET DATETHRU$=F0$(18,6)
0540 LET TYPE$="P"; REM "Purchase Order -- POC.LB
0550 CALL "SYC.RM",M1$,M1,9,13
0560 CALL "SYC.RM",M2$,M2,9,10
0570 CALL "SYC.RM",M3$,M3,5,12
0590 DIM VENDFNAME$(30),VENDTNAME$(30),WHSENAME$(24)
0600 REM " --- Additional File Opens
0610 IF AR$<>"Y" THEN GOTO 0700
0620 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0630 LET CUSTLEN=NUM(U2$(1,2))
0640 CALL "SYC.DA",1,10,11,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0650 IF STATUS>0 THEN GOTO 9900
0660 LET ARM01_DEV=CHANNELS[10],ARM03_DEV=CHANNELS[11]
0700 REM " --- Background
0710 FOR X=4 TO 14 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(14,3),"Beginning Purchase Order:",@(17,5),"Ending Purchase Order:",@(22,7),"Beginning Vendor:",@(25,9),"Ending Vendor:",@(20,11),"Expedite Thru Date:",@(29,13),"Warehouse:",
0760 GOSUB REFRESH
1000 REM " --- Beginning Purchase Order
1020 LET V0$="Z",V1$="KCE",V2$=POFROM$,V3$="",V4$="Enter A Valid Purchase Order Number (<F1>=First/<F3>=Lookup)",V0=7,V1=40,V2=3,I0=0
1030 GOSUB 7000
1040 ON V3 GOTO 1100,1050,1130,1080,4000
1050 LET POFROM$=""
1060 PRINT @(V1,V2),'CL',@(V1,V2),"First",
1070 GOTO 1130
1080 CALL "POC.LB",TYPE$,VENDPO$
1090 IF LEN(VENDPO$)<15 THEN GOTO 1050 ELSE LET V$=VENDPO$(9,7)
1100 IF V$="" THEN GOTO 1050
1110 LET POFROM$=V$
1120 PRINT @(40,3),POFROM$,'CL',
1130 GOTO 3900
1200 REM " --- Ending Purchase Order
1210 LET V0$="Z",V1$="KCE",V2$=POTHRU$,V3$="",V4$="Enter A Valid Purchase Order Number (<F1>=Last/<F3>=Lookup)",V0=7,V1=40,V2=5
1220 GOSUB 7000
1230 ON V3 GOTO 1290,1240,1330,1270,4000
1240 LET POTHRU$=""
1250 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
1260 GOTO 1330
1270 CALL "POC.LB",TYPE$,VENDPO$
1280 IF LEN(VENDPO$)<15 THEN GOTO 1240 ELSE LET V$=VENDPO$(9,7)
1290 IF V$="" THEN GOTO 1240
1300 IF POFROM$<>"" THEN IF V$<POFROM$ THEN GOTO 1200
1310 LET POTHRU$=V$
1320 PRINT @(40,5),POTHRU$,'CL',
1330 GOTO 3900
1600 REM " --- Beginning Vendor
1610 LET V0$="M",V1$="KCE",V2$=VENDFROM$,V3$=VENDIMASK$,V4$="Enter A Valid Vendor Number (<F1>=First/<F3>=Lookup)",V0=6,V1=40,V2=7
1620 GOSUB 7000
1630 ON V3 GOTO 1690,1640,1790,1670,4000
1640 LET VENDFROM$=""
1650 PRINT @(V1,V2),'CL',@(V1,V2),"First",
1660 GOTO 1790
1670 CALL "SYC.LV",V$
1680 IF V$="" THEN GOTO 1600
1690 IF V$="" THEN GOTO 1640
1700 DIM X1$(195)
1710 FIND (APM01_DEV,KEY=N0$+V$,DOM=1600)IOL=APM01A
1720 LET VENDFROM$=V$,VENDFNAME$(1)=X1$(1,30)
1730 PRINT @(V1,V2),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
1790 GOTO 3900
1800 REM " --- Ending Vendor
1810 LET V0$="M",V1$="KCE",V2$=VENDTHRU$,V3$=VENDIMASK$,V4$="Enter A Valid Vendor Number (<F1>=Last/<F3>=Lookup)",V0=6,V1=40,V2=9
1820 GOSUB 7000
1830 ON V3 GOTO 1890,1840,1990,1870,4000
1840 LET VENDTHRU$=""
1850 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
1860 GOTO 1990
1870 CALL "SYC.LV",V$
1880 IF V$="" THEN GOTO 1800
1890 IF V$="" THEN GOTO 1840
1900 IF VENDFROM$<>"" THEN IF V$<VENDFROM$ THEN GOTO 1800
1910 DIM X1$(195)
1920 FIND (APM01_DEV,KEY=N0$+V$,DOM=1800)IOL=APM01A
1930 LET VENDTHRU$=V$,VENDTNAME$(1)=X1$(1,30)
1940 PRINT @(V1,V2),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
1990 GOTO 3900
2400 REM " --- Expedite Through Date
2405 LET V4$="Select PO's With Items Due Before This Date"
2410 LET V0$="D",V1$="6N",V2$=DATETHRU$,V3$="",V0=6,V1=40,V2=11
2420 GOSUB 7000
2430 ON V3 GOTO 2450,2450,2480,2450,4000
2450 IF V$="" THEN LET V$=F0$(18,6)
2460 PRINT @(V1,V2),FNB6$(V$),
2470 LET DATETHRU$=V$
2480 GOTO 3900
2600 REM " --- Warehouse
2610 LET V0$="S",V1$="",V2$=WHSE$,V3$="",V4$="Enter A Valid Warehouse ID  (<F1>=All, <F3>=Lookup)",V0=2,V1=40,V2=13
2620 GOSUB 7000
2630 ON V3 GOTO 2660,2720,2790,2650,4000
2650 CALL "IVC.LK",3,V1,V2,V$
2660 IF V$="" THEN GOTO 2600
2670 DIM X1$(160)
2680 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=2600)IOL=IVM10C
2690 LET WHSE$=V$,WHSENAME$(1)=X1$(6,24)
2700 PRINT @(V1,V2),FNF$(WHSE$,"00")," ",WHSENAME$(1,24),'CL',
2710 GOTO 2790
2720 LET WHSE$=""
2730 PRINT @(V1,V2),"All",'CL'
2790 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1600,1800,2400,2600,4000
4000 REM " --- Correct
4010 GOSUB REFRESH
4020 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run print overlay
4110 IF POFROM$="" THEN LET POFROM$=FILL(7)
4120 IF POTHRU$="" THEN LET POTHRU$=FILL(7)
4130 IF VENDFROM$="" THEN LET VENDFROM$=FILL(6),VENDFNAME$=FILL(30)
4140 IF VENDTHRU$="" THEN LET VENDTHRU$=FILL(6),VENDTNAME$=FILL(30)
4150 IF WHSE$="" THEN LET WHSENAME$=""
4190 RUN "POR.KB"
5000 REM " --- Refresh screen data
5005 REFRESH: 
5010 PRINT 'CF',
5020 IF POFROM$="" THEN PRINT @(40,3),"First" ELSE PRINT @(40,3),POFROM$,'CL',
5030 IF POTHRU$="" THEN PRINT @(40,5),"Last" ELSE PRINT @(40,5),POTHRU$,'CL',
5050 IF VENDFROM$="" THEN PRINT @(40,7),"First",'CL', ELSE PRINT @(40,7),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5060 IF VENDTHRU$="" THEN PRINT @(40,9),"Last",'CL', ELSE PRINT @(40,9),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5090 PRINT @(40,11),FNB6$(DATETHRU$)
5100 IF WHSE$="" THEN PRINT @(40,13),"All",'CL', ELSE PRINT @(40,13),FNF$(WHSE$,"00")," ",WHSENAME$(1,24),'CL',
5190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

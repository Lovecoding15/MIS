REM ========================================================================
REM Lead in to the sales drill down reports
REM ========================================================================

REM ========================================================================
REM USE and Declares
REM ========================================================================

	use ::bbjasper.bbj::BBJasperReport
	use ::bbjasper.bbj::BBJasperViewerWindow

	declare BBJasperReport BBjReport!
	declare BBJasperViewerWindow BBjReportWindow!

	declare BBjSysGui sysgui!
	declare BBjTopLevelWindow win!

REM ========================================================================
REM Common information
REM ========================================================================

	rem ' set the environment
	chdir stbl("E_COMM")
	addPrefix$=stbl("BBJHOME")+"utils/reporting/bbjasper/"
	oldPrefix$=pfx
	newPrefix$=oldPrefix$+" "+addPrefix$
	prefix newPrefix$

	REM Open the BBj SysGUI Channel
	sysgui!=BBjAPI().openSysGui("X0") 

	REM ' Initialization
	rem ' for bambam3 and production
	server$ = "localhost"
	
	rem ' for laptop
	rem ' server$ = "bambam3"
	
	uid$ = "ec"
	rem ' for bambam3
	rem ' pwd$ = "newone"
	rem ' for bbjserver.basis.cloud
	pwd$ = "rEd|4pHhxFWrf6BK$^"
	
	dbName$ = "AddOnData"

	connectString$ = "jdbc:basis:" + server$ + ":2001?database=" + dbName$ + "&user=" + uid$ + "&pwd=" + pwd$ + "&ssl=true"

	REM ' get firm and firm name
	sysinfo_template$=stbl("+SYSINFO_TPL",err=*next)
	dim sysinfo$:sysinfo_template$
	sysinfo$=stbl("+SYSINFO",err=*next)
	firm_id$=sysinfo.firm_id$
	firm_name$=sysinfo.firm_name$

	REM set current month and year
	year$ = date(0:"%Y")
	year = num(year$)
        month$ = date(0:"%M")
        month = num(month$)-1
    
	
REM ========================================================================
REM create the window
REM ========================================================================

	winWidth = 380
	winHeight = 220

	win!=sysgui!.addWindow(50, 150, winWidth, winHeight, firm_name$ + " Sales by Cust Type",$12$)
	font! = sysgui!.makeFont("Ariel", 10, sysgui!.BOLD)

	labelM! = win!.addStaticText(win!.getAvailableControlID(), 10,10,50,20,"Month:", $8000$)
	labelM!.setFont(font!)

	listbuttonM! = win!.addListButton(win!.getAvailableControlID(), 70, 10,120,80, "")
	listbuttonM!.addItem("January")
	listbuttonM!.addItem("February")
	listbuttonM!.addItem("March")
	listbuttonM!.addItem("April")
	listbuttonM!.addItem("May")
	listbuttonM!.addItem("June")
	listbuttonM!.addItem("July")
	listbuttonM!.addItem("August")
	listbuttonM!.addItem("September")
	listbuttonM!.addItem("October")
	listbuttonM!.addItem("November")
	listbuttonM!.addItem("December")
	listbuttonM!.selectIndex(month)
	
	labelY! = win!.addStaticText(win!.getAvailableControlID(), 10,50,50,20,"Year:", $8000$)
	labelY!.setFont(font!)

	listbuttonY! = win!.addListButton(win!.getAvailableControlID(), 70, 50,120,80, "")
	index = 0
	for y = 1999 to year	
		yearStr$ = yearStr$ + str(y) + $0A$
		index = index + 1
	next y
	listbuttonY!.insertItems(0, yearStr$)
	listbuttonY!.selectIndex(index - 1)
	
	reportButton! = win!.addButton(win!.getAvailableControlID(), 250,10,95,30,"Run Report")
	doneButton! = win!.addButton(win!.getAvailableControlID(), 250,50,95,30,"Done")
	
	instructionLabel! = win!.addStaticText(win!.getAvailableControlID(),10,90,360,20,"Use these inputs to enter the drill down stack at a lower level.")
	font1! = sysgui!.makeFont("Ariel", 10, sysgui!.PLAIN)
	instructionLabel!.setFont(font1!)
	
	listButtonT! = win!.addListButton(win!.getAvailableControlID(), 120, 120, 60,80, "")
	gosub load_cust_type_list

	labelT! = win!.addStaticText(win!.getAvailableControlID(), 10,120,100,20,"Customer Type:", $8000$)
	labelT!.setFont(font!)
	
	listButtonC! = win!.addListButton(win!.getAvailableControlID(), 120, 150, 250,80, "", $0001$)
	labelC! = win!.addStaticText(win!.getAvailableControlID(), 10,150,100,20,"Customer:", $8000$)
	labelC!.setFont(font!)

	listButtonI! = win!.addListButton(win!.getAvailableControlID(), 120, 180, 150,80, "", $0001$)
	labelI! = win!.addStaticText(win!.getAvailableControlID(), 10,180,100,20,"Invoice:", $8000$)
	labelI!.setFont(font!)

	doneButton!.setCallback(doneButton!.ON_BUTTON_PUSH, "DONE")
	reportButton!.setCallback(reportButton!.ON_BUTTON_PUSH, "DEPLOY_REPORT")
	listButtonT!.setCallback(listButtonT!.ON_LIST_SELECT,"HOUSEKEEP_CUSTOMER")
	listButtonC!.setCallback(listButtonC!.ON_LIST_SELECT,"HOUSEKEEP_INVOICES")
	listButtonM!.setCallback(listButtonM!.ON_LIST_SELECT,"HOUSEKEEP_INVOICES")
	listButtonY!.setCallback(listbuttonY!.ON_LIST_SELECT,"HOUSEKEEP_INVOICES")
	
	win!.setVisible(1)

	process_events

DONE:
	release
	

REM ========================================================================
REM deploy the report
REM ========================================================================

DEPLOY_REPORT:
	
	rem ' what report are we running

	cust_type = listbuttonT!.getSelectedIndex()
	
	if cust_type = 0 then
		gosub startSalesByCustType
	else
		customer = listbuttonC!.getSelectedIndex()
		
		if customer = 0 then
			gosub startSalesCustTypeCust
		else
			invoice = listbuttonI!.getSelectedIndex()
			
			if invoice = 0 then
				gosub startSaleByCustomer
			else
				gosub startSalesInvoiceDetails
			fi
		fi
	fi
	
	return


REM ========================================================================
REM startSalesByCustType
REM ========================================================================

startSalesByCustType:

	reportName$ = "SalesByCustType.jasper"

	BBjReport! = new BBJasperReport(reportName$, connectString$)

	BBjReport!.putParam("FIRM_ID","01")

	rem ' BBjReport!.putParam("MONTH","12")
	rem ' BBjReport!.putParam("YEAR","2007")
	rem ' BBjReport!.putParam("CUST_NAME","Everest Industries")
	rem ' BBjReport!.putParam("CUSTOMER_NBR","000100")

	month = listbuttonM!.getSelectedIndex()
	month$ = str(month + 1:"00")
	year = listbuttonY!.getSelectedIndex()
	year$ = listbuttonY!.getItemAt(year)
	
	BBjReport!.putParam("FIRM_ID",firm_id$)
	BBjReport!.putParam("FIRM_NAME",firm_name$)
	BBjReport!.putParam("MONTH",month$)
	BBjReport!.putParam("YEAR",year$)

	BBjReport!.fill()

	BBjReportWindow! = new BBJasperViewerWindow(BBjReport!, 0, 0, 920, 600,"BASIS International, Ltd.", $93$)
	BBjReportWindow!.center()
	BBjReportWindow!.show(1)

	return

REM ========================================================================
REM startSalesByCustTypeCust
REM ========================================================================

startSalesCustTypeCust:

	reportName$ = "SalesCustTypeCust.jasper"

	BBjReport! = new BBJasperReport(reportName$, connectString$)

	month = listbuttonM!.getSelectedIndex()
	month$ = str(month + 1:"00")
	year = listbuttonY!.getSelectedIndex()
	year$ = listbuttonY!.getItemAt(year)

	cust_type$ = cvs(listButtonT!.getItemAt(listbuttonT!.getSelectedIndex()),3)
		
	BBjReport!.putParam("FIRM_ID",firm_id$)
	BBjReport!.putParam("FIRM_NAME",firm_name$)
	BBjReport!.putParam("CUST_TYPE",cust_type$)
	BBjReport!.putParam("MONTH",month$)
	BBjReport!.putParam("YEAR",year$)

	BBjReport!.fill()

	BBjReportWindow! = new BBJasperViewerWindow(BBjReport!, 0, 0, 920, 600,"BASIS International, Ltd.", $93$)
	BBjReportWindow!.center()
	BBjReportWindow!.show(1)

	return

REM ========================================================================
REM startSalesByCustTypeCust
REM ========================================================================

startSaleByCustomer:

	reportName$ = "SalesByCustomer.jasper"

	BBjReport! = new BBJasperReport(reportName$, connectString$)

	month = listbuttonM!.getSelectedIndex()
	month$ = str(month + 1:"00")
	year = listbuttonY!.getSelectedIndex()
	year$ = listbuttonY!.getItemAt(year)

	cust_type$ = cvs(listButtonT!.getItemAt(listbuttonT!.getSelectedIndex()),3)
	customer$ = cvs(listbuttonC!.getItemAt(listbuttonC!.getSelectedIndex()),3)
	customer_nbr$ = customer$(1,6)
	cust_name$ = customer$(8)
		
	BBjReport!.putParam("FIRM_ID",firm_id$)
	BBjReport!.putParam("FIRM_NAME",firm_name$)
	BBjReport!.putParam("CUST_TYPE",cust_type$)
	BBjReport!.putParam("CUSTOMER_NBR",customer_nbr$)
	BBjReport!.putParam("CUST_NAME",cust_name$)
	BBjReport!.putParam("MONTH",month$)
	BBjReport!.putParam("YEAR",year$)

	BBjReport!.fill()

	BBjReportWindow! = new BBJasperViewerWindow(BBjReport!, 0, 0, 920, 600,"BASIS International, Ltd.", $93$)
	BBjReportWindow!.center()
	BBjReportWindow!.show(1)

	return

REM ========================================================================
REM startSalesInvoiceDetails
REM ========================================================================

startSalesInvoiceDetails:

	reportName$ = "SalesInvoiceDetails.jasper"

	BBjReport! = new BBJasperReport(reportName$, connectString$)

	month = listbuttonM!.getSelectedIndex()
	month$ = str(month + 1:"00")
	year = listbuttonY!.getSelectedIndex()
	year$ = listbuttonY!.getItemAt(year)

	cust_type$ = cvs(listButtonT!.getItemAt(listbuttonT!.getSelectedIndex()),3)
	customer$ = cvs(listbuttonC!.getItemAt(listbuttonC!.getSelectedIndex()),3)
	customer_nbr$ = customer$(1,6)
	cust_name$ = customer$(8)

	invoice$ = listbuttonI!.getItemAt(listbuttonI!.getSelectedIndex())
	inv_nbr$ = invoice$(1,7)
	inv_date$ = invoice$(9)
	
	BBjReport!.putParam("FIRM_ID",firm_id$)
	BBjReport!.putParam("FIRM_NAME",firm_name$)
	BBjReport!.putParam("CUST_TYPE",cust_type$)
	BBjReport!.putParam("CUSTOMER_NBR",customer_nbr$)
	BBjReport!.putParam("CUST_NAME",cust_name$)
	BBjReport!.putParam("MONTH",month$)
	BBjReport!.putParam("YEAR",year$)
	BBjReport!.putParam("AR_INV_NBR",inv_nbr$)
	BBjReport!.putParam("INVOICE_DATE",inv_date$)
	BBjReport!.putParam("WindowWidth","1250")

	BBjReport!.fill()

	BBjReportWindow! = new BBJasperViewerWindow(BBjReport!, 0, 0, 920, 600,"BASIS International, Ltd.", $93$)
	BBjReportWindow!.center()
	BBjReportWindow!.show(1)

	return


REM ========================================================================
REM load customer type list button listbuttonT!
REM ========================================================================

load_cust_type_list:

	listbuttonT!.removeAllItems()

	sql$ = "SELECT * FROM ARM10L t1 "
	sql$ = sql$ + "WHERE t1.record_id_l = 'L' and t1.firm_id = '" + firm_id$ + "' "
	sql$ = sql$ + "ORDER BY t1.cust_type"
	
	rs! = BBJAPI().createSQLRecordSet(connectString$,"",sql$)
	
	count = 0
	listbuttonT!.insertItemAt(count, "None")
	count = count + 1
	
	while 1
		rec! = rs!.getCurrentRecordData()
	
		if cvs(rec!.getFieldValue("CUST_TYPE"),3)<>"" then 
			listbuttonT!.insertItemAt(count, rec!.getFieldValue("CUST_TYPE"))
			count = count + 1
		fi

		rs!.next(err=*break)	
	
	wend

	listbuttonT!.selectIndex(0)

	return
	
REM ========================================================================
REM housekeep the customer listbutton based on the selection in the customer
REM type listbutton
REM ========================================================================

housekeep_customer:

	cust_type = listbuttonT!.getSelectedIndex()

	rem ' at the point the invoice listbutton should always be disable and emptey
	listbuttonI!.removeAllItems()
	listbuttonI!.setEnabled(0)

	if cust_type = 0 then
		listbuttonC!.removeAllItems()
		listbuttonC!.setEnabled(0)
	else
		cust_type$ = listbuttonT!.getItemAt(cust_type)
		
		listbuttonC!.setEnabled(1)
		listbuttonC!.removeAllItems()
			
		sql$ = "SELECT t1.customer_nbr, t1.cust_name FROM ARM01 t1 "
		sql$ = sql$ + "INNER JOIN ARM02 t2 ON t1.firm_id = t2.firm_id AND t1.customer_nbr = t2.customer_nbr "
		sql$ = sql$ + "WHERE t1.firm_id = '" + firm_id$ + "' AND t2.cust_type = '" + cust_type$ + "' "
		sql$ = sql$ + "ORDER BY t1.customer_nbr"
		
		rs! = BBJAPI().createSQLRecordSet(connectString$,"",sql$)
	
		count = 0
		listbuttonC!.insertItemAt(count, "None")
		count = count + 1
		
		if !(rs!.isEmpty()) then
			while 1
				rec! = rs!.getCurrentRecordData()

				cust_nbr$ = rec!.getFieldValue("CUSTOMER_NBR")
				cust_name$ = rec!.getFieldValue("CUST_NAME")

				listbuttonC!.insertItemAt(count, cust_nbr$ + " " + cust_name$)
				count = count + 1

				rs!.next(err=*break)	

			wend
		fi

		listbuttonC!.selectIndex(0)
	
	fi
	
	return
	
REM ========================================================================
REM housekeep the invoices listbutton based on the selection in the customer
REM listbutton
REM ========================================================================
	
housekeep_invoices:

	customer = listbuttonC!.getSelectedIndex()

	rem ' if listbuttonC! is empty customer will be -1
	if customer > -1 then
		
		if customer = 0 then
			listbuttonI!.removeAllItems()
			listbuttonI!.setEnabled(0)
		else
			customer$ = listbuttonC!.getItemAt(customer)
			customer$ = customer$(1,6)

			listbuttonI!.setEnabled(1)
			listbuttonI!.removeAllItems()

			month = listbuttonM!.getSelectedIndex()
			month$ = str(month + 1:"00")
			year = listbuttonY!.getSelectedIndex()
			year$ = listbuttonY!.getItemAt(year)

			sql$ = "SELECT t1.ar_inv_nbr, date(t1.invoice_date, '%Mz/%Dz/%Y') AS inv_date FROM ART03 t1 "
			sql$ = sql$ + "WHERE t1.firm_id = '" + firm_id$ + "' AND t1.customer_nbr = '" + customer$ + "' AND MONTH(t1.invoice_date) = '" + month$ + "' AND YEAR(t1.invoice_date) = '" + year$ + "' "
			sql$ = sql$ + "ORDER BY t1.ar_inv_nbr"

			rs! = BBJAPI().createSQLRecordSet(connectString$,"",sql$)

			count = 0
			listbuttonI!.insertItemAt(count, "None")
			count = count + 1

			if !(rs!.isEmpty())
				while 1
					rec! = rs!.getCurrentRecordData()

					inv_nbr$ = rec!.getFieldValue("AR_INV_NBR")
					invoice_date$ = rec!.getFieldValue("INV_DATE")

					listbuttonI!.insertItemAt(count, inv_nbr$ + " " + invoice_date$)
					count = count + 1

					rs!.next(err=*break)	

				wend
			fi

			listbuttonI!.selectIndex(0)

		fi
	fi

	return
	
0010 REM "POX - Remove Unlinked PO Detail Lines"
0020 REM "Program POX.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "17Jul96 JAL: New program for 6.0.1"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files
0105 LET FILES=11
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="SYS-01",FILES$[2]="POE-01",FILES$[3]="POE-02"
0120 LET FILES$[4]="POE-03",FILES$[5]="POE-04",FILES$[6]="POE-11"
0125 LET FILES$[7]="POE-12",FILES$[8]="POE-13",FILES$[9]="POE-14"
0130 LET FILES$[10]="POE-23",FILES$[11]="POE-24"
0150 CALL "SYC.DA",1,1,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],POE01_DEV=CHANNELS[2],POE02_DEV=CHANNELS[3]
0165 LET POE03_DEV=CHANNELS[4],POE04_DEV=CHANNELS[5],POE11_DEV=CHANNELS[6]
0170 LET POE12_DEV=CHANNELS[7],POE13_DEV=CHANNELS[8],POE14_DEV=CHANNELS[9]
0175 LET POE23_DEV=CHANNELS[10],POE24_DEV=CHANNELS[11]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,1,0,4,0
0720 PRINT 'SB',@(28,3),"Processing:",'SF',
0800 REM " --- Options
0810 LET V4$="Do You Wish To Remove Unlinked Purchase Order Detail Lines"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0990 CALL "SYC.NB","Updating",25,COLUMN
1000 REM " --- Requisitions"
1010 LET K=15,RECEIPT=0,X$="Requisitions"
1020 LET HEADER_DEV=POE01_DEV,DETAIL_DEV=POE11_DEV
1030 GOSUB 6000
1500 REM " --- Purchase Orders"
1510 LET K=15,RECEIPT=0,X$="Purchase Orders"
1520 LET HEADER_DEV=POE02_DEV,DETAIL_DEV=POE12_DEV
1530 GOSUB 6000
2000 REM " --- QA Receipts"
2010 LET K=22,RECEIPT=1,X$="Quality Assurance Receipts"
2020 LET HEADER_DEV=POE03_DEV,DETAIL_DEV=POE13_DEV
2030 GOSUB 6000
2500 REM " --- QA Receipts Lot/Serial Detail"
2510 LET K=22,RECEIPT=1,X$="Quality Assurance Lot/Serial Detail"
2520 LET HEADER_DEV=POE03_DEV,DETAIL_DEV=POE23_DEV
2530 GOSUB 6000
3000 REM " --- PO Receipts"
3010 LET K=22,RECEIPT=1,X$="Purchase Order Receipts"
3020 LET HEADER_DEV=POE04_DEV,DETAIL_DEV=POE14_DEV
3030 GOSUB 6000
3500 REM " --- PO Receipts"
3510 LET K=22,RECEIPT=1,X$="Purchase Order Receipts"
3520 LET HEADER_DEV=POE04_DEV,DETAIL_DEV=POE14_DEV
3530 GOSUB 6000
4000 REM " --- All done"
4090 GOTO 9900
6000 REM " --- Check for unlinked detail lines"
6010 PRINT @(40,3),X$,'CL',
6090 READ (DETAIL_DEV,KEY="",DOM=6100)
6100 REM " --- Read next detail"
6110 LET K$=KEY(DETAIL_DEV,END=6300)
6120 READ (DETAIL_DEV)
6130 PRINT @(COLUMN,11),K$(1,2)," ",K$(3,6)," ",K$(9,7),
6140 IF RECEIPT THEN PRINT " ",K$(16,7),
6150 FIND (HEADER_DEV,KEY=K$(1,K),DOM=6200)
6190 GOTO 6100
6200 REM " --- Remove unlinked detail line"
6210 REMOVE (DETAIL_DEV,KEY=K$)
6290 GOTO 6100
6300 REM " --- Done with current file set"
6390 RETURN 
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "IVM - Item Maintenance (Stocking Information)"
0020 REM "Program IVM.MV"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 PRECISION P[1]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$,A6$(1),A[ALL]
0220 IVM02A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0230 APM01A: IOLIST C0$,C1$(1)
0290 IVS10D: IOLIST X$,I5$(1)
0300 IVM10C: IOLIST W0$(1)
0310 IVM10F: IOLIST X0$(1)
0320 IVM03F: IOLIST X0$(1)
0500 REM " --- Initializations"
0510 DIM B0$(24),B1$(64),B[20]
0550 LET M6$="##0-",M6=LEN(M6$),O=19+M2+1
0570 LET DNE$="Enter A Valid Code (D=Derived/E=Entered/N=No Recalculation)"
0700 REM " --- Background"
0710 PRINT 'SB',@(3,7),"Stocking Level:",@(5,8),"Warehouse ID:",@(9,10),"ABC Code:",@(7,11),"Buyer Code:",@(2,12),"Stocking Vendor:",@(8,13),"Lead Time:",@(1,14),"Maximum Quantity:",@(6,15),"Order Point:",@(14,16),"EOQ:",@(5,17),"Safety Stock:",'SF',
0900 REM " --- Position file"
0990 READ (IVM03_DEV,KEY=A0$,DOM=1000)
1000 REM " --- Stocking Level"
1005 PRINT 'CF',
1010 LET V4$="Are Stocking Decisions For This Item Made At The Item Or Warehouse Level (I/W)?"
1020 LET V0$="S",V1$="C",V2$=A2$(24,1),V3$="IW",V0=1,V1=19,V2=7
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1000,4900
1050 IF POS(V$="IW")=0 THEN GOTO 1000
1060 LET A2$(24,1)=V$
1100 REM " --- Warehouse ID"
1105 IF A2$(24,1)="I" THEN GOTO 1200
1120 LET V2$="",V2$=KEY(IVM03_DEV,END=1140)
1125 IF POS(A0$(1,2)+"G"+A0$(3)=V2$)<>1 THEN LET V2$=""
1130 IF V2$<>"" THEN LET V2$=V2$(24)
1140 LET V0$="S",V1$="KCE",V3$="",V0=2,V1=19,V2=8
1145 LET V4$="Enter A Valid Warehouse ID <F3>=Lookup)"
1150 GOSUB 7000
1155 ON V3 GOTO 1170,1100,1000,1160,4900
1160 CALL "IVC.LK",3,V1,V2,V$
1165 IF V$="" THEN GOTO 1100
1170 LET V$=V$+"  ",V$=V$(1,2)
1190 LET B0$(1)=A0$(1,2)+V$+A0$(3)
1200 REM " --- Retrieve record"
1210 IF A2$(24,1)="W" THEN GOTO 1250
1220 LET ABC$=A2$(25,1),BUYER$=A2$(9,3),VENDOR$=A2$(12,6),ORDER$=A2$(27,1)
1230 LET EOQ$=A2$(26,1),SAFETY$=A2$(28,1),LEADTIME=A[7],MAXQTY=A[3]
1235 LET ORDER=A[4],EOQ=A[6],SAFETY=A[5],L4$="Item"
1240 GOTO 1300
1250 LET B0$(1)=N0$+V$+A0$(3)
1260 FIND (IVM02_DEV,KEY=B0$,DOM=1100)IOL=IVM02A
1270 LET ABC$=B1$(56,1),BUYER$=B1$(19,3),VENDOR$=B1$(22,6),ORDER$=B1$(58,1)
1280 LET EOQ$=B1$(57,1),SAFETY$=B1$(59,1),LEADTIME=B[7],MAXQTY=B[3]
1290 LET ORDER=B[4],EOQ=B[6],SAFETY=B[5],L4$="Warehouse"
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
2000 REM " --- ABC Code"
2010 LET V4$="Enter A Valid ABC Code",V3$="ABCDEFGHIJKLMNOPQRSTUVWXYZ "
2020 LET V0$="S",V1$="C",V2$=ABC$,V3$="",V0=1,V1=19,V2=10,I0=0
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 LET ABC$=V$
2090 GOTO 3900
2100 REM " --- Buyer Code"
2105 IF PO$<>"Y" THEN GOTO 3900
2110 LET V4$="Enter A Valid Buyer Code (<F1>=None/<F3>=Lookup)"
2120 LET V0$="S",V1$="C",V2$=BUYER$,V3$="",V0=3,V1=19,V2=11
2130 GOSUB 7000
2140 ON V3 GOTO 2160,2145,3900,2155,3900
2145 LET BUYER$="   "
2147 PRINT @(V1,V2),'CL',
2150 GOTO 3900
2155 CALL "IVC.LK",4,V1,V2,V$
2160 LET BUYER$=V$+"   ",BUYER$=BUYER$(1,3)
2170 FIND (IVM10_DEV,KEY=N0$+"F"+BUYER$,DOM=2100)IOL=IVM10F
2180 PRINT @(V1,V2),BUYER$," ",X0$(7,20),
2190 GOTO 3900
2200 REM " --- Stocking Vendor"
2205 IF AP$<>"Y" THEN GOTO 3900
2210 LET V4$="Enter A Valid Vendor Code (<F1>=None/<F3>=Lookup)",V2=12,X=1
2215 LET V0$="M",V1$="KCE",V2$=VENDOR$(1,VENDLEN),V3$=VENDMASK$,V0=6,V1=19
2220 GOSUB 7000
2225 ON V3 GOTO 2250,2230,3900,2240,3900
2230 LET VENDOR$(1)="",X=0
2235 GOTO 2255
2240 CALL "SYC.LV",V$
2250 LET VENDOR$(1)=V$
2255 GOSUB 6500
2260 IF STATUS>0 THEN IF X>0 THEN GOTO 2200
2265 DIM X$(60)
2270 IF FNP$(VENDOR$)<>"" THEN LET X$(1)=FNF$(VENDOR$(1,VENDLEN),VENDMASK$)+" "+C1$
2280 PRINT @(V1,V2),X$,
2290 GOTO 3900
2300 REM " --- Lead Time"
2310 LET V4$="Enter The Lead Time In Days For This "+L4$
2320 LET V0$="N",V1$="C",V2$=STR(LEADTIME),V3$=M6$,V0=M6,V1=19,V2=13
2330 GOSUB 7000
2340 ON V3 GOTO 2350,2300,3900,2300,3900
2350 IF V<0 OR FPT(V) THEN GOTO 2300
2360 LET LEADTIME=V
2390 GOTO 3900
2400 REM " --- Maximum Quantity"
2410 LET V4$="Enter The Maximum Quantity For This "+L4$
2420 LET V0$="N",V1$="C",V2$=STR(MAXQTY),V3$=M2$,V0=M2,V1=19,V2=14
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 IF V<0 THEN GOTO 2400
2460 LET MAXQTY=V
2490 GOTO 3900
2500 REM " --- Order Point"
2510 LET V4$="Enter The Order Point For This "+L4$
2520 LET V0$="N",V1$="C",V2$=STR(ORDER),V3$=M2$,V0=M2,V1=19,V2=15
2530 GOSUB 7000
2540 ON V3 GOTO 2550,2500,3900,2500,3900
2550 IF V<0 THEN GOTO 2500
2560 LET ORDER=V
2590 GOTO 3900
2600 REM " --- Order Point Code"
2620 LET V0$="S",V1$="C",V2$=ORDER$,V3$="DNE",V4$=DNE$,V0=1,V1=O,V2=15
2630 GOSUB 7000
2640 ON V3 GOTO 2650,2600,3900,2600,3900
2650 LET ORDER$=V$
2690 GOTO 3900
2700 REM " --- EOQ"
2710 LET V4$="Enter The Economic Order Quantity (EOQ) For This "+L4$
2720 LET V0$="N",V1$="C",V2$=STR(EOQ),V3$=M2$,V0=M2,V1=19,V2=16
2730 GOSUB 7000
2740 ON V3 GOTO 2750,2700,3900,2700,3900
2750 IF V<0 THEN GOTO 2700
2760 LET EOQ=V
2790 GOTO 3900
2800 REM " --- EOQ Code"
2820 LET V0$="S",V1$="C",V2$=EOQ$,V3$="DNE",V4$=DNE$,V0=1,V1=O,V2=16
2830 GOSUB 7000
2840 ON V3 GOTO 2850,2800,3900,2800,3900
2850 LET EOQ$=V$
2890 GOTO 3900
2900 REM " --- Safety Stock"
2910 LET V4$="Enter The Safety Stock Quantity For This "+L4$
2920 LET V0$="N",V1$="C",V2$=STR(SAFETY),V3$=M2$,V0=M2,V1=19,V2=17
2930 GOSUB 7000
2940 ON V3 GOTO 2950,2900,3900,2900,3900
2950 IF V<0 THEN GOTO 2900
2960 LET SAFETY=V
2990 GOTO 3900
3000 REM " --- Safety Stock Code"
3020 LET V0$="S",V1$="C",V2$=SAFETY$,V3$="DNE",V4$=DNE$,V0=1,V1=O,V2=17
3030 GOSUB 7000
3040 ON V3 GOTO 3050,3000,3900,3000,3900
3050 LET SAFETY$=V$
3090 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,2300,2400,2500,2600,2700,2800,2900,3000,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Update Item"
4105 DIM X0$(37)
4110 LET X0$(1)=N0$+"F"+A2$(9,3)+A2$(12,6)+A2$(1,3)+A0$(3)
4120 REMOVE (IVM03_DEV,KEY=X0$,DOM=4130)
4130 IF A2$(24,1)="W" THEN GOTO 4300
4140 LET A2$(25,1)=ABC$,A2$(9,3)=BUYER$,A2$(12,6)=VENDOR$,A2$(27,1)=ORDER$
4150 LET A2$(26,1)=EOQ$,A2$(28,1)=SAFETY$,A[7]=LEADTIME,A[3]=MAXQTY
4160 LET A[4]=ORDER,A[6]=EOQ,A[5]=SAFETY,FIRST$=N0$+"G"+A0$(3)
4170 WRITE (IVM01_DEV,KEY=A0$)IOL=IVM01A
4180 LET X0$(1)=N0$+"F"+A2$(9,3)+A2$(12,6)+A2$(1,3)+A0$(3)
4185 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03F
4190 READ (IVM03_DEV,KEY=FIRST$,DOM=4400)
4200 REM " --- Clear warehouses"
4210 LET K$=KEY(IVM03_DEV,END=4300)
4220 IF POS(FIRST$=K$)<>1 THEN GOTO 4300
4225 READ (IVM03_DEV)
4230 LET B0$(1)=N0$+K$(24)+K$(4,20)
4235 EXTRACT (IVM02_DEV,KEY=B0$,DOM=4270)IOL=IVM02A
4240 LET B1$(19,3)="",B1$(22,6)=""
4245 LET X0$(1)=N0$+"F"+B1$(19,3)+B1$(22,6)+A2$(1,3)+A0$(3)+B0$(3,2)
4250 REMOVE (IVM03_DEV,KEY=X0$,DOM=4360)
4260 WRITE (IVM02_DEV,KEY=B0$)IOL=IVM02A
4290 GOTO 4200
4300 REM " --- Update Warehouse"
4310 LET X0$(1)=N0$+"F"+B1$(19,3)+B1$(22,6)+A2$(1,3)+A0$(3)+B0$(3,2)
4320 REMOVE (IVM03_DEV,KEY=X0$,DOM=4330)
4330 LET B1$(56,1)=ABC$,B1$(19,3)=BUYER$,B1$(22,6)=VENDOR$,B1$(58,1)=ORDER$
4340 LET B1$(57,1)=EOQ$,B1$(59,1)=SAFETY$,B[7]=LEADTIME,B[3]=MAXQTY
4350 LET B[4]=ORDER,B[6]=EOQ,B[5]=SAFETY
4360 WRITE (IVM02_DEV,KEY=B0$)IOL=IVM02A
4370 LET X0$(1)=N0$+"F"+A2$(9,3)+A2$(12,6)+A2$(1,3)+A0$(3)
4380 REMOVE (IVM03_DEV,KEY=X0$,DOM=4390)
4390 LET X0$(1)=N0$+"F"+B1$(19,3)+B1$(22,6)+A2$(1,3)+A0$(3)+B0$(3,2)
4400 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03F
4410 LET A2$(9,3)="",A2$(12,6)=""
4420 WRITE (IVM01_DEV,KEY=A0$)IOL=IVM01A
4800 REM " --- Run next overlay"
4810 IF A2$(24,1)="W" THEN GOTO 1000
4900 REM " --- Run menu overlay"
4990 RUN "IVM.MA"
5000 REM " --- Display record"
5010 DIM W0$(30),X0$(32),X$(60),WHSE$(27)
5015 IF A2$(24,1)<>"W" THEN GOTO 5040
5020 FIND (IVM10_DEV,KEY=N0$+"C"+B0$(3,2),DOM=5030)IOL=IVM10C
5030 LET WHSE$=B0$(3,2)+" "+W0$(6,24)
5040 IF PO$="Y" THEN FIND (IVM10_DEV,KEY=N0$+"F"+BUYER$,DOM=5055)IOL=IVM10F
5050 IF AP$<>"Y" THEN GOTO 5080
5055 IF FNP$(VENDOR$)="" THEN GOTO 5080
5060 GOSUB 6500
5070 LET X$(1)=FNF$(C0$(3,VENDLEN),VENDMASK$)+" "+C1$
5080 PRINT @(19,7),A2$(24,1),@(19,8),WHSE$,@(19,10),ABC$,@(19,11),BUYER$," ",X0$(7,20),@(19,12),X$,@(19,13),LEADTIME:M6$,@(19,14),MAXQTY:M2$,@(19,15),ORDER:M2$,@(O),ORDER$,@(19,16),EOQ:M2$,@(O),EOQ$,@(19,17),SAFETY:M2$,@(O),SAFETY$,
5090 RETURN 
6500 REM " --- Retrieve stocking vendor"
6510 DIM C0$(8),C1$(30)
6520 LET C0$(1)=N0$+VENDOR$,STATUS=1
6530 IF AP$<>"Y" THEN GOTO 6590
6540 FIND (APM01_DEV,KEY=C0$,DOM=6590)IOL=APM01A
6550 LET STATUS=0
6590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

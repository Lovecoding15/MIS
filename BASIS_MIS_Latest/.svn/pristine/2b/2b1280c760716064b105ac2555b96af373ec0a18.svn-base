0010 REM "IVX - LIFO/FIFO Maintenance (Input Overlay)"
0020 REM "Program IVX.FA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETERR 9000
0055 SETESC 9000
0060 ON O0 GOTO 0070,4000
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=5
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0130 LET FILES$[4]="IVM-10",FILES$[5]="IVM-04"
0150 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2]
0180 LET IVM02_DEV=CHANNELS[3],IVM10_DEV=CHANNELS[4]
0200 REM " --- IOLIST's"
0250 IVM10C: IOLIST X0$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0290 IVS01A: IOLIST X$,X$,P2$,P3$,X$,X$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 DIM P[5]
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),H1$=F4$
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0450 IF POS(P3$(3,1)="LF")=0 THEN GOTO 4900
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(5,1))
0495 LET P[3]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2)),P[5]=NUM(P2$(13,2))
0500 REM " --- Initializations"
0510 DIM X0$(32),HEADINGS$[5]
0550 LET WHSE$=P3$(6,2),WIDTH=80,HEADINGS=5,HEADINGS$[0]=F4$
0600 REM " --- Additional File Opens"
0610 CALL "SYC.DA",1,5,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS>0 THEN GOTO 9900
0630 LET IVM04_DEV=CHANNELS[5]
0640 CALL "SYC.RM",M1$,M1,10,15
0650 CALL "SYC.RM",M2$,M2,12,15
0660 CALL "SYC.RM",M3$,M3,10,15
0700 REM " --- Background"
0710 PRINT @(0,3),'SB',"Warehouse ID:",'SF',
0720 CALL "SYC.WC",1,0,80,0,0,4,0
1000 REM " --- Warehouse"
1010 LET V4$="Enter A Valid Warehouse (<F3>=Lookup)"
1020 LET V0$="S",V1$="RC",V2$=WHSE$,V0=2,V1=14,V2=3
1030 GOSUB 7000
1040 ON V3 GOTO 1070,1000,1000,1050,9900
1050 CALL "IVC.LK",3,V1,V2,V$
1060 IF V$="" THEN GOTO 1000
1070 LET V$=V$+"  ",V$=V$(1,2)
1080 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=1000)IOL=IVM10C
1090 LET H3$=X0$(4,2)+" "+FNP$(X0$(6,24))
1100 PRINT @(V1,V2),H3$,'CL'
1110 LET WHSE$=X0$(4,2),HEADINGS$[2]="Warehouse "+H3$
4000 REM " --- Submenu"
4010 DIM MENUS$[1]
4020 CLOSE (7,ERR=4030)
4030 LET MENUS=1,MENUS$[1]="IVX.FA",WIN_X=0,WIN_Y=6,V3=0,O0=1
4040 PRINT @(0,5),'CE',
4050 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4060 CALL "SYC.WC",1,0,80,0,0,4,0
4070 IF SELECTION$="" THEN GOTO 1000
4080 PRINT 'SB',@(9,4)," ",DESCRIPTION$," ",'SF',
4100 REM " --- Report initializations"
4110 IF SELECTION$="DIAGNOSIS" THEN LET O0=1
4120 IF SELECTION$="ADJUSTMENT" THEN LET O0=2
4130 IF SELECTION$="REBUILD" THEN LET O0=3
4190 RUN "IVX.FB"
4900 REM " --- LIFO/FIFO not set in parameter record"
4910 CALL "IVC.SB","F",STATUS
4990 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "ARR - Customer Name & Address Listing"
0020 REM "Program ARR.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ARM-04",FILES$[3]="SYS-01"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET ARM01_DEV=CHANNELS[1],ARM04_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLists
0210 ARM01A: IOLIST A0$(1),A1$(1)
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0405 DIM P[2]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$,WHEN$=F0$(7,3)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0500 REM " --- Init Data
0510 DIM A0$(8),A1$(230),HEADINGS$[3]
0530 LET H=0,H1$=N1$,H2$=N3$,L9=59,L=L9+1,A0$(1)=N0$
0540 LET HEADINGS=3,WIDTH=132,CLOCK$="",M0=LEN(M0$)
0550 LET M8$="(###)-###-####",M7$="00000",HEADINGS$[0]=H1$,HEADINGS$[1]=H2$
0570 IF P[2]=9 THEN LET M7$="00000-0000"
0590 LET P8$="",P7$="",P9$="C"
0700 REM " --- Background
0710 PRINT 'CF'
0720 FOR X=4 TO 8 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0750 PRINT 'SB',@(15,3),"Customer/Alpha Sequence:",@(29,5),"Beginning:",@(32,7),"Ending:",'SF',
0800 REM " --- Options
0805 PRINT 'CF',
0810 LET V0$="S",V1$="C",V2$=P9$,V3$="AC",V4$="Customer Number Or Alphabetic Sequence (C=Customer Number/A=Alphabetic)",V0=1,V1=40,V2=3
0820 GOSUB 7000
0830 IF V3=4 THEN GOTO 9900
0840 LET P9$=V$
0845 IF P9$="C" THEN LET V0$="M",V1$="",V2$=P8$,V3$=P1$,V4$="Enter Beginning Customer Number (<F1>=First/<F3>=Lookup)",V0=6,V1=40,V2=5,FILE_DEV=ARM01_DEV
0850 IF P9$="A" THEN LET V0$="S",V1$="",V2$=P8$,V3$="",V4$="Enter Beginning Alpha Selection (<F1>=First)",V0=8,V1=40,V2=5,FILE_DEV=ARM04_DEV
0855 GOSUB 7000
0856 IF V3=1 THEN LET V$=""
0857 IF P9$="C" THEN IF V3=3 THEN CALL "SYC.LC",V$
0858 IF V$="" THEN PRINT @(V1,V2),'CL',"First"; GOTO 0860
0859 IF P9$="C" THEN LET V$=V$+"000000"; LET V$=V$(1,P[0]); PRINT @(V1,V2),V$
0860 IF V3=2 THEN GOTO 0800
0862 LET P8$=V$
0870 IF P9$="C" THEN LET V1$="",V2$=P7$,V4$="Enter Ending Customer Number (<F1>=Last/<F3>=Lookup)",V2=7,V0$="M",V0=6,V3$=P1$
0872 IF P9$="A" THEN LET V1$="",V2$=P7$,V4$="Enter Ending Alpha Selection (<F1>=Last)",V2=7,V0$="S",V0=8
0873 GOSUB 7000
0874 IF V3=2 THEN GOTO 0845
0875 IF V3=1 THEN LET V$=""
0876 IF P9$="C" THEN IF V3=3 THEN CALL "SYC.LC",V$
0877 IF V$="" THEN PRINT @(V1,V2),'CL',"Last"; GOTO 0879
0878 IF P9$="C" THEN LET V$=V$+"000000"; LET V$=V$(1,P[0]); PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$)
0879 LET P7$=V$
0880 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
0883 GOSUB 7000
0885 IF V3=4 THEN GOTO 9900
0887 IF V$="Y" THEN GOTO 0900
0890 IF V$="N" THEN GOTO 0800
0895 GOTO 0880
0900 REM " --- Init File
0902 CALL "SYC.GA",7,1,"","",STATUS
0903 IF STATUS THEN GOTO 9900
0905 CALL "SYC.NB","Printing",MAX(M0,10),COLUMN
0910 IF P9$="A" THEN LET P8$="A"+P8$; IF P7$<>"" THEN LET P7$="A"+P7$
0920 READ (FILE_DEV,KEY=N0$+P8$,DOM=0940)
0930 LET K0$=N0$+P8$; GOTO 1100
1000 REM " --- Next One
1020 LET K0$=KEY(FILE_DEV,END=4000)
1030 IF POS(N0$=K0$)<>1 THEN GOTO 4000
1040 IF P7$<>"" THEN IF K0$(3,LEN(P7$))>P7$ THEN GOTO 4000
1050 IF P9$<>"A" THEN GOTO 1100
1060 IF K0$(3,1)<>"A" THEN GOTO 4000
1070 PRINT @(COLUMN,11),K0$(4,10),
1080 LET K0$=K0$(1,2)+K0$(14,6)
1090 READ (FILE_DEV)
1100 REM " --- Read customer master"
1110 READ (ARM01_DEV,KEY=K0$,DOM=1000)IOL=ARM01A
1120 IF P9$<>"A" THEN PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$),
1140 IF L+1>L9 THEN GOSUB 5000
1160 LET A9$=A1$(79,24),A8$=A1$(55,24)
1170 IF POS(" "<>A9$)=0 THEN LET A8$=A8$(1,POS("   "=A8$+"   ")-1)+" "+FNZ$(A1$(103)),A9$=""
1175 IF POS(" "<>A9$)>0 THEN LET A9$=A9$(1,POS("   "=A9$+"   ")-1)+" "+FNZ$(A1$(103))
1177 IF LEN(A8$)+LEN(A9$)>53 THEN LET A9$=A9$(1,53-LEN(A8$))
1180 PRINT (7)FNF$(A0$(3,P[0]),M0$)," ",FNF$(A1$(112,10),M8$)," ",A1$(1,28)," ",A1$(31,24)," ",A8$," ",A9$
1190 LET L=L+1
3900 REM 
3990 GOTO 1000
4000 REM " --- End of File
4090 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1,HEADINGS$[2]="Customer Number Sequence"
5020 IF P9$="A" THEN LET HEADINGS$[2]="Alphabetical Sort Sequence"
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,WIDTH,WHEN$,CLOCK$,STATUS
5040 PRINT (7)"Customer"
5050 PRINT (7)"Number",@(M0+1),"Telephone      Name",@(M0+45),"Address",'LF'
5060 LET L=L+2
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8075 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

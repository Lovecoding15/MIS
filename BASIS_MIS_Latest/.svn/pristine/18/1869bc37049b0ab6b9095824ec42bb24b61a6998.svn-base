0010 REM "GLR - Financial Statements (Report Overlay)"
0020 REM "Program GLR.FB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0260 GLM22A: IOLIST C0$(1),C1$(1),C2$(1),C3$(1)
0270 GLM32A: IOLIST R0$,R1$(1)
0280 GLE06A: IOLIST Y0$(1),Y[ALL]
0290 GLE07A: IOLIST D0$(1),D[ALL]
0400 REM " --- Parameters"
0490 LET MC=7,MO=5,MX=8,MY=1,MZ=1,MT=3
0500 REM " --- Initializations"
0510 LET L9=59,L=L9+1,H=0
0520 DIM C0$(8),C1$(56),C2$(MO*4),C3$(COLMAX*3),D0$(9),D[MT],R1$(7),Y0$(10),Y[MT]
0530 DIM COLINFO$((COLMAX+1)*MC),S[MX,MY,MZ],T[MX,MY,MZ]
0531 REM " --- Array definition for I[X,Y,Z],J[X,Y,Z],T[X,Y,Z],S[X,Y,Z]"
0532 REM " --- X: 0=C-PERIOD, 1=C-QTR, 2=C-YEAR
0533 REM " ---    3=P-PERIOD, 4=P-QTR, 5=P-YEAR
0534 REM " ---    6=N-PERIOD, 7=N-QTR, 8=N-YEAR"
0535 REM " --- Y: 0=Actual, 1=Budget"
0536 REM " --- Z: 0=Amount, 1=Units"
0540 LET CPN$="CPN",PQY$="PQY",LAST$=""
0900 REM " --- Get GLM-32 Column Info For Report"
0910 LET COL=0
0920 READ (GLM32_DEV,KEY=A0$(1,4),DOM=0930)
0930 LET K32$=KEY(GLM32_DEV,END=0990)
0940 IF K32$(1,4)<>A0$(1,4) OR COL>=COLMAX THEN GOTO 0990
0950 READ (GLM32_DEV,KEY=K32$)IOL=GLM32A
0960 LET COLINFO$(COL*MC+1,MC)=R1$,COL=COL+1
0980 GOTO 0930
0990 READ (GLM22_DEV,KEY=A0$(1,4),DOM=1000)
1000 REM " --- Next GLM-22 Detail Record For Report"
1010 LET K$=KEY(GLM22_DEV,END=4000)
1020 IF K$(1,4)<>A0$(1,4) THEN GOTO 4000
1030 READ (GLM22_DEV)IOL=GLM22A
1040 PRINT @(COLUMN+24,11),C0$(5,4)
1100 REM " --- Xfer IOLIST to working array"
1110 DIM S[MX,MY,MZ]
1120 READ (GLE06_DEV,KEY=C0$,DOM=1130)
1130 LET K06$=KEY(GLE06_DEV,END=1300)
1140 IF POS(C0$=K06$)<>1 THEN GOTO 1300
1150 READ (GLE06_DEV,KEY=K06$)IOL=GLE06A
1160 LET N=0,X=(POS(Y0$(9,1)=CPN$)*MT-MT)+(POS(Y0$(10,1)=PQY$)-1)
1170 FOR Y=0 TO MY
1180 FOR Z=0 TO MZ
1190 LET S[X,Y,Z]=Y[N],N=N+1
1200 NEXT Z
1210 NEXT Y
1220 GOTO 1130
1300 IF C1$(1,1)="H" THEN GOTO 3000
1320 IF C1$(52,1)=SEQUENCE$ THEN GOTO 1380
1330 DIM Y[MT],S[MX,MY,MZ]
1380 DIM W[7]
1400 REM " --- Accumulate Detail"
1410 FOR N=0 TO 7
1420 IF C1$(1,1)="T" THEN GOTO 1470
1425 LET X=A[N,0],Y=A[N,1],Z=A[N,2]
1430 IF X=9 THEN GOTO 1470
1440 IF Y<=1 THEN LET W[N]=S[X,Y,Z]
1450 IF Y>1 THEN LET W[N]=S[X,1,Z]-S[X,0,Z]
1470 LET A[N,3]=NUM(C3$(N*3+1,3))
1490 NEXT N
2000 REM " --- Check for total-in"
2020 LET W0$=C1$(45,3)
2040 IF POS(W0$="   000",3) THEN GOTO 2100
2050 LET W0$="T"+C1$(45,3),U0$=PRINTFLAG$
2060 GOSUB 6000
2080 GOSUB 6200
2090 GOSUB 6400
2100 REM " --- Check for ratio/percent"
2120 LET W0$=C1$(48,3)
2140 IF POS(W0$="   000",3) THEN GOTO 2200
2150 LET W0$="P"+C1$(48,3),U0$=SEQUENCE$
2160 GOSUB 6000
2200 REM " --- Calculate ratio/percent"
2220 FOR X=0 TO 7
2230 LET W0$=COLINFO$(X*MC+4,1)
2240 IF W0$=" " THEN GOTO 2390
2250 LET W0=A[X,0],W1=A[X,1],W2=A[X,2],W3=A[X,3],W4=0,W5=0,W6=W2
2260 IF W0$="R" THEN LET W6=ABS(SGN(W6)-1)
2270 IF W0=9 THEN LET W5=0; GOTO 2300
2280 IF W1<=1 THEN LET W5=T[W0,W1,W6]
2300 IF W1>1 AND W0$="P" THEN LET W5=S[W0,1,W6]
2350 IF W0$="P" THEN LET W[X]=W[X]*100
2360 IF W5<>0 THEN LET W4=W[X]/W5
2380 LET W[X]=W4
2382 IF W0$="%" THEN IF X>1 AND W[X-1]<>0 THEN LET W[X]=W[X-2]*100/W[X-1] ELSE LET W[X]=0
2384 IF W0$="$" THEN IF X>1 THEN LET W[X]=W[X-1]-W[X-2] ELSE LET W[X]=0
2390 NEXT X
2400 REM " --- Total out"
2410 IF POS(" "<>C2$)=0 THEN GOTO 3000
2420 FOR W=0 TO 4
2430 LET W0$=C2$(W*3+1,3),W1$=C2$(W+16,1),W1=1
2440 IF POS(W0$="   000",3) THEN GOTO 2690
2460 LET W0$="T"+W0$,U0$=PRINTFLAG$
2470 IF W1$="-" THEN LET W1=-1
2480 GOSUB 6000
2490 IF POS("C"=C1$(40,5))<>0 THEN DIM T[MX,MY,MZ]
2500 REM " --- Accumulate to total"
2520 FOR X=0 TO MX
2530 LET N=0,D0$(7,1)=CPN$(INT(X/MT)+1,1),D0$(8,1)=PQY$(MOD(X,MT)+1,1)
2540 FOR Y=0 TO MY
2550 FOR Z=0 TO MZ
2560 LET T[X,Y,Z]=T[X,Y,Z]+S[X,Y,Z]*W1
2570 LET D[N]=T[X,Y,Z],N=N+1
2580 NEXT Z
2590 NEXT Y
2600 WRITE (GLE07_DEV,KEY=D0$(1,8))IOL=GLE07A
2610 NEXT X
2690 NEXT W
3000 REM " --- Print"
3010 IF POS("P"=C1$(40,5))<>0 THEN GOSUB 5000
3020 IF L+1>L9 THEN GOSUB 5000
3100 REM " --- Editing"
3120 IF POS("S"=C1$(40,5))=0 THEN GOTO 3150
3130 PRINT (7)""
3140 LET L=L+1
3150 IF POS("D"=C1$(40,5))=0 AND POS("U"=C1$(40,5))=0 THEN GOTO 3300
3160 LET W1$="-"
3170 IF POS("D"=C1$(40,5))<>0 THEN LET W1$="="
3175 IF L+1>L9 THEN GOSUB 5000
3180 FOR X=0 TO 7
3190 IF A[X,3]=0 THEN GOTO 3250
3200 IF COLINFO$(X*MC+3,1)="U" THEN DIM W$(LEN(M2$),W1$)
3210 IF COLINFO$(X*MC+3,1)="A" THEN DIM W$(LEN(M1$),W1$)
3220 IF POS(COLINFO$(X*MC+4,1)="P%")>0 THEN DIM W$(LEN(M4$),W1$)
3230 IF COLINFO$(X*MC+4,1)="R" THEN DIM W$(LEN(M3$),W1$)
3235 IF COLINFO$(X*MC+4,1)="$" THEN IF X<>0 AND COLINFO$((X-1)*MC+3,1)="U" THEN DIM W$(LEN(M2$),W1$) ELSE DIM W$(LEN(M1$),W1$)
3240 PRINT (7)@(A[X,3]),W$,
3250 NEXT X
3260 PRINT (7)""
3270 LET L=L+1
3300 IF POS("-"=C1$(40,5))=0 THEN GOTO 3350
3320 FOR X=0 TO 7
3330 LET W[X]=-W[X]
3340 NEXT X
3350 IF C1$(1,1)="N" THEN GOTO 3900
3352 LET X$=FNP$(C1$(2,35))
3354 IF L+1>L9 THEN GOSUB 5000
3355 IF X$<>"" THEN PRINT (7)@(NUM(C1$(37,3))),X$,
3360 IF C1$(1,1)="H" THEN GOTO 3500
3400 REM " --- Print amounts"
3410 FOR X=0 TO 7
3415 IF W[X]=0 OR A[X,0]=9 OR A[X,3]=0 THEN GOTO 3490
3420 IF A[X,1]>1 THEN IF POS("F"=C1$(40,5))<>0 THEN LET W[X]=-W[X]
3425 IF POS(COLINFO$(X*MC+4,1)="BC$%")<>0 THEN GOTO 3440
3430 IF COLINFO$(X*MC+4,1)<>" " THEN IF POS(C1$(48,3)="   000",3) THEN GOTO 3490
3435 IF W[X]=0 OR A[X,0]=9 OR A[X,3]=0 THEN GOTO 3490
3440 IF COLINFO$(X*MC+3,1)="A" THEN LET MASK$=M1$
3445 IF COLINFO$(X*MC+3,1)="U" THEN LET MASK$=M2$
3450 IF COLINFO$(X*MC+4,1)="R" THEN LET MASK$=M3$
3460 IF POS(COLINFO$(X*MC+4,1)="P%")>0 THEN LET MASK$=M4$
3465 IF COLINFO$(X*MC+4,1)="$" THEN IF X<>0 AND COLINFO$((X-1)*MC+3,1)="U" THEN LET MASK$=M2$ ELSE LET MASK$=M1$
3472 IF POS(COLINFO$(X*MC+4,1)="PR%")>0 THEN GOTO 3485
3474 IF POS(COLINFO$(X*MC+3,1)="AR")=0 THEN GOTO 3485
3475 IF POS("$"=C1$(40,5))=0 THEN GOTO 3485
3477 LET XPOS=POS("#"=MASK$)
3480 IF XPOS>0 THEN IF XPOS>1 THEN LET MASK$=MASK$(1,XPOS-1)+"$"+MASK$(XPOS) ELSE LET MASK$="$"+MASK$
3485 PRINT (7)@(FNPPOS(A[X,3],MASK$)),W[X]:MASK$,
3490 NEXT X
3500 PRINT (7)""
3520 LET L=L+1
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- Run lead overlay"
4010 LET O0=1
4090 RUN "GLR.FA"
5000 REM " --- Print report heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS<>0 THEN GOTO 9900
5090 RETURN 
6000 REM " --- Fetch GLE-07 totals record"
6010 IF W0$=LAST$ THEN GOTO 6190
6020 LET D0$(1)=N0$+W0$,FOUND=0
6030 READ (GLE07_DEV,KEY=D0$(1,6),DOM=6040)
6040 LET K07$=KEY(GLE07_DEV,END=6170)
6050 IF K07$(1,6)<>N0$+W0$ THEN GOTO 6170
6060 READ (GLE07_DEV,KEY=K07$)IOL=GLE07A
6070 IF D0$(9,1)<>U0$ THEN GOTO 6040
6080 LET N=0,X=(POS(D0$(7,1)=CPN$)*MT-MT)+(POS(D0$(8,1)=PQY$)-1),FOUND=FOUND+1
6090 FOR Y=0 TO MY
6100 FOR Z=0 TO MZ
6110 LET T[X,Y,Z]=D[N],N=N+1
6120 NEXT Z
6130 NEXT Y
6140 LET N=0
6150 GOTO 6040
6160 GOTO 6180
6170 IF FOUND=0 THEN DIM T[MX,MY,MZ]
6180 LET LAST$=W0$,D0$(9,1)=U0$
6190 RETURN 
6200 REM " --- Accumulate from total"
6220 FOR N=0 TO 7
6230 LET X=A[N,0],Y=A[N,1],Z=A[N,2]
6240 IF X=9 THEN GOTO 6270
6250 IF Y<=1 THEN LET W[N]=W[N]+T[X,Y,Z]
6260 IF Y>1 THEN LET W[N]=W[N]+T[X,1,Z]-T[X,0,Z]
6270 NEXT N
6280 RETURN 
6400 REM " --- Retrieve total"
6420 FOR X=0 TO MX
6430 FOR Y=0 TO MY
6440 FOR Z=0 TO MZ
6450 LET S[X,Y,Z]=S[X,Y,Z]+T[X,Y,Z]
6460 NEXT Z
6470 NEXT Y
6480 NEXT X
6490 RETURN 
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNPPOS(Q,Q$)=Q-MIN(1,POS("$"=Q$))
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

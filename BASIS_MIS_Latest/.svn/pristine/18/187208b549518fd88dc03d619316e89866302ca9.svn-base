0010 REM "GLE - Journal Entry (Detail Lines)"
0020 REM "Program GLE.CC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0051 REM "  NOTE: If .CB was run from GLE.DA, Recurring Journal Entries,
0052 REM "        GLE01_DEV & GLE11_DEV are really GLE-02 & GLE-12
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0220 GLE11A: IOLIST B0$(1),B1$(1),B2$(1),B3$,B[ALL]
0230 GLM01A: IOLIST C0$(1),C1$(1)
0500 REM " --- Initializations"
0510 DIM B0$(K9),B1$(P[4]),B2$(30),B[4],C0$(2+P[4]),C1$(35)
0520 DIM J0$(P[4],"0"),J1$(40)
0590 LET B0$(1)=A0$,L9=20,L=L1+1,K9$=""
0900 REM " --- Display any existing lines"
0910 GOSUB 5500
0920 GOSUB 5100
0930 IF Q1=0 THEN GOTO 1400
0990 GOTO 4000
1000 REM " --- Overflow"
1010 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L1+1),'LD','LD',
1020 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1120 LET K$=KEY(GLE11_DEV,END=1400)
1140 IF POS(A0$=K$)<>1 THEN GOTO 1400
1150 READ (GLE11_DEV)IOL=GLE11A
1190 GOTO 1490
1400 REM " --- Sequence Number"
1410 LET W0=NUM(B0$(K8+1))+10
1415 LET V4$="Enter A 3 Digit Sequence Number"
1420 LET V0$="Z",V1$="KERC",V2$=STR(W0),V3$="",V0=3,V1=1,V2=L,I0=0
1430 GOSUB 7000
1440 IF V3=4 THEN PRINT @(V1,L),J1$(1,V0); GOTO 4000
1450 IF V$=J0$(1,V0) OR V$="" THEN GOTO 1400
1460 LET B0$(K8+1)=V$
1480 FIND (GLE11_DEV,KEY=B0$,DOM=1600)IOL=GLE11A
1490 IF B0$(1,K9)<=K9$ THEN GOSUB 5400
1500 GOSUB 5000
1520 LET I0=5
1530 GOTO 3300
1600 REM " --- New Record"
1610 DIM B1$(P[4]),B2$(30),B[4]
1620 PRINT @(6,L),'CL',@(6,L+1),'CL',
2000 REM " --- G/L Account"
2010 LET V4$="Enter A Valid G/L Account (<F3>=Lookup)",I0=1
2020 LET V0$="M",V1$="C",V2$=B1$(1,P[3]),V3$=P1$,V0=P[4],V1=6,V2=L
2030 GOSUB 7000
2040 ON V3 GOTO 2100,2000,3200,2050,2100
2050 CALL "SYC.LG",V$
2060 IF V$="" THEN GOTO 2000
2100 REM 
2110 LET C0$(1)=N0$+V$
2120 FIND (GLM01_DEV,KEY=C0$,DOM=2000)IOL=GLM01A
2130 PRINT @(V1,V2),FNF$(C0$(3,P[3]),M0$)," ",C1$,
2140 LET B1$(1)=C0$(3)
2190 GOTO 3200
2200 REM " --- Memo"
2210 LET V0$="S",V1$="C",V2$=B2$,V3$="",V0=30,V1=6,V2=L+1
2220 LET V4$="Enter Up To "+STR(V0)+" Characters (<F1>=Copy Header Memo)"
2230 GOSUB 7000
2240 ON V3 GOTO 2260,2250,3200,2200,3200
2250 LET V$=A1$(1,30)
2260 LET B2$(1)=V$
2270 PRINT @(V1,V2),B2$,
2290 GOTO 3200
2400 REM " --- Debit Amount"
2410 IF B[0]<0 THEN GOTO 3200
2420 LET V4$="Enter The Debit Amount For This Entry (<F1>=Change To Credit)"
2430 LET V0$="N",V1$="C",V2$=STR(B[0]),V3$=M1$,V0=M1,V1=O[0],V2=L+1
2440 GOSUB 7000
2450 ON V3 GOTO 2500,2460,3200,2400,3200
2460 LET B[0]=(-B[0])*1
2470 GOSUB 5050
2490 GOTO 3200
2500 REM 
2510 LET B[0]=V
2520 GOSUB 5050
2590 GOTO 3200
2600 REM " --- Credit Amount"
2610 IF B[0]>0 THEN GOTO 3200
2620 LET V4$="Enter The Credit Amount For This Entry (<F1>=Change To Debit)"
2630 LET V0$="N",V1$="C",V2$=STR(-B[0]),V3$=M1$,V0=M1,V1=O[1],V2=L+1
2640 GOSUB 7000
2650 ON V3 GOTO 2700,2660,3200,2600,3200
2660 LET B[0]=(-B[0])*1
2670 GOSUB 5050
2690 GOTO 3200
2700 REM 
2710 LET B[0]=(-V)*1
2720 GOSUB 5050
2790 GOTO 3200
2800 REM " --- Units"
2810 IF P4$(3,1)<>"Y" THEN GOTO 3200
2820 LET V4$="Enter Any (Optional) Units For This Entry"
2830 LET V0$="N",V1$="C",V2$=STR(B[1]),V3$=M2$,V0=M2,V1=O[2],V2=L+1
2840 GOSUB 7000
2850 ON V3 GOTO 2860,2800,3200,2800,3200
2860 LET B[1]=V
2870 PRINT @(V1,V2),B[1]:M2$,
2890 GOTO 3200
3200 REM " --- Sequence Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,2000,2200,2400,2600,2800,3300
3300 REM " --- Line Correct?"
3310 LET V4$="<Enter>=Correct     <D>=Delete"
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=B0$(1,K9)
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3300,3200,3300,3350
3350 IF V$="" THEN GOTO 3400
3360 IF V$="D" THEN GOTO 3700
3390 GOTO 3300
3400 REM " --- Write"
3420 WRITE (GLE11_DEV,KEY=B0$)IOL=GLE11A
3450 LET L=L+2
3460 IF V3=4 THEN GOTO 4000
3470 GOSUB 5500
3490 GOTO 1000
3700 REM " --- Remove"
3710 REMOVE (GLE11_DEV,KEY=B0$,DOM=3720)
3750 PRINT @(0,L),'CL',@(0,L+1),'CL',
3760 IF V3=4 THEN GOTO 4000
3770 GOSUB 5500
3790 GOTO 1000
4000 REM " --- Options"
4010 GOSUB 5500
4015 PRINT @(0,22),'CL',@(3),"Is The Above Information Correct (Y/N/List/Review/Delete/Total)?",
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="",V0=6,V1=68,V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4100
4050 IF V$="L" OR V$="LIST" THEN GOTO 4400
4060 IF V$="T" OR V$="TOTAL" THEN GOTO 4700
4070 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4080 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run Header Overlay"
4115 IF TOTAL=0 THEN GOTO 4190
4120 DIM MESSAGE$[1]
4125 LET X=TOTAL
4130 IF TOTAL<0 THEN LET X=-X
4135 LET MESSAGE$[0]="This Entry Is Out Of Balance By "+FNS$(STR(TOTAL:M1$))
4140 DIM X$(LEN(MESSAGE$[0]))
4150 LET X=MAX(INT((LEN(MESSAGE$[0])-34)/2),1)
4160 LET X$(X)="Press <Enter> To Continue",MESSAGE$[1]=X$
4170 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
4180 IF V$<>"SPECIAL" THEN GOTO 1000
4190 RUN "GLE.CB"
4200 REM " --- Remove"
4210 READ (GLE11_DEV,KEY=A0$,DOM=4220)
4220 LET K$=KEY(GLE11_DEV,END=4260)
4230 IF POS(A0$=K$)<>1 THEN GOTO 4260
4240 REMOVE (GLE11_DEV,KEY=K$,DOM=4250)
4250 GOTO 4220
4260 REMOVE (GLE01_DEV,KEY=A0$,DOM=4270)
4290 RUN "GLE.CB"
4400 REM " --- List"
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4620 READ (GLE11_DEV,KEY=A0$,DOM=4630)
4640 GOSUB 5400
4690 GOTO 1000
4700 REM " --- Total"
4710 GOSUB 6000
4720 PRINT @(0,22),'CL',"Totals For This Journal Entry",@(O[0]),DEBITS:M1$,@(O[1]),-CREDITS:M1$,
4730 IF P4$(3,1)="Y" THEN PRINT @(O[2]),UNITS:M2$,
4740 LET V0$="S",V1$="C",V2$="",V3$="",V4$="",V0=1,V1=78,V2=22
4750 GOSUB 7000
4790 GOTO 4000
5000 REM " --- Display"
5010 DIM C0$(2+P[4]),C1$(35)
5020 LET C0$(1)=N0$+B1$,Q1=1
5030 FIND (GLM01_DEV,KEY=C0$,DOM=5040)IOL=GLM01A
5040 PRINT @(0,L),'CL',@(1),B0$(K8+1,3),@(6),FNF$(B1$(1,P[3]),M0$)," ",C1$,@(6,L+1),B2$,
5050 PRINT @(O[0],L+1),'CL',
5060 IF B[0]>=0 THEN PRINT @(O[0]),B[0]:M1$,
5070 IF B[0]<0 THEN PRINT @(O[1]),-B[0]:M1$,
5080 IF P4$(3,1)="Y" THEN PRINT @(O[2]),B[1]:M2$,
5090 RETURN 
5100 REM " --- List"
5105 LET Q1=0
5110 GOSUB 5400
5120 READ (GLE11_DEV,KEY=A0$,DOM=5130)
5130 LET K$=KEY(GLE11_DEV,END=5300)
5150 IF POS(A0$=K$)<>1 THEN GOTO 5300
5160 READ (GLE11_DEV)IOL=GLE11A
5190 IF L<=L9 THEN GOTO 5250
5200 REM 
5210 LET V4$="<Enter>=Continue:"
5215 LET V0$="S",V1$="CE",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5230 IF V3=4 THEN GOTO 5300
5240 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+2,K9$=B0$(1,K9)
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5420 PRINT @(0,L1+1),'CE',
5440 LET L=L1+1
5490 RETURN 
5500 REM " --- Display running total"
5510 GOSUB 6000
5520 PRINT @(COLUMN,3),DEBITS:M1$,@(COLUMN,4),-CREDITS:M1$,@(COLUMN,5),TOTAL:M1$
5590 RETURN 
6000 REM " --- Accumulate totals for entry"
6010 LET DEBITS=0,CREDITS=0,UNITS=0,TOTAL=0,X$=B0$
6090 READ (GLE11_DEV,KEY=A0$,DOM=6100)
6100 REM " --- Read next detail"
6110 LET K$=KEY(GLE11_DEV,END=6200)
6120 IF POS(A0$=K$)<>1 THEN GOTO 6200
6130 READ (GLE11_DEV)IOL=GLE11A
6140 IF B[0]>0 THEN LET DEBITS=DEBITS+B[0]
6150 IF B[0]<0 THEN LET CREDITS=CREDITS+B[0]
6160 LET UNITS=UNITS+B[1]
6190 GOTO 6100
6200 REM " --- Done accumulating totals"
6210 LET TOTAL=DEBITS+CREDITS
6220 READ (GLE11_DEV,KEY=X$,DOM=6230)
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8085 DEF FNS$(Q$)=Q$(POS(" "<>Q$))
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

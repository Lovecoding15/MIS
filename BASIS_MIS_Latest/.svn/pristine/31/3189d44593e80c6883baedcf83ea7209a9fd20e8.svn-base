0010 REM "SYX - Display Program Information"
0020 REM "Program SYX.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=1
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0500 REM " --- Initializations"
0510 DIM MESSAGE$[1]
0550 LET MAXLINE=50,M1$="0000",M2$="##,##0",L0=6,L9=22,CHANNEL=0
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,5,0
0750 PRINT 'SB',@(0,3),"Program Name:",@(1,4),"Line Number:",'SF',
1000 REM " --- File to clear"
1005 PRINT 'CF',
1010 LET V0$="S",V1$="CE",V2$="",V3$="",V0=6,V1=14,V2=3
1020 LET V4$="Enter the name of the program to be displayed"
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1000,9900
1050 IF V$="" THEN GOTO 1000
1060 LET PROGRAM$=V$,FILES$[FILES]=PROGRAM$,OPTIONS$[FILES]="F"
1090 CLOSE (CHANNEL,ERR=1100)
1100 REM " --- Open file and retrieve file information"
1110 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
1115 IF STATUS>0 THEN GOTO 1200
1120 LET CHANNEL=CHANNELS[FILES]
1125 IF CHANNEL=0 THEN GOTO 1200
1130 CALL "SYC.JA",CHANNEL,X$,PATHNAME$,FILETYPE$,K,R,SIZE,N,STATUS
1140 IF FILETYPE$="P" THEN GOTO 1400
1150 REM " --- Not a program"
1160 LET V4$="Is Not A Program"
1190 GOTO 1250
1200 REM " --- Program not found"
1210 LET V4$="Can Not Be Found Or Accessed"
1250 REM " --- Display error message"
1260 LET MESSAGE$[0]=PROGRAM$+" "+V4$+" (<Enter>=Continue)"
1270 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
1290 GOTO 1000
1400 REM " --- Retrieve pathname"
1410 CALL "SYC.UA",PROGRAM$,PATHNAME$
1500 REM " --- Display file information"
1510 LET SIZE$=STR(SIZE:M2$)+" Byte(s)",DISK$="Disk "+STR(DISK)
1550 PRINT @(21,3),"(",PATHNAME$,")",@(80-LEN(SIZE$)),SIZE$,@(80-LEN(DISK$),4),DISK$
1600 REM " --- Last line"
1610 LET V0$="N",V1$="C",V2$=STR(MAXLINE),V3$=M1$,V0=4,V1=14,V2=4
1620 LET V4$="Enter the last line number of the program to be displayed"
1630 GOSUB 7000
1640 ON V3 GOTO 1650,1600,1000,1600,9900
1650 LET MAXLINE=V
1660 PRINT @(V1,V2),MAXLINE:M1$,
2000 REM " --- Retrieve and display lines"
2010 LET L=L0
2020 FOR LINE=1 TO MAXLINE
2030 LET LINE$=STR(LINE),INDENT=0,L1=0
2040 CALL "SYC.UB",CHANNEL,PROGRAM$,STATEMENT$,LINE$,STATUS
2050 IF STATUS>0 THEN GOTO 2900
2060 IF FNP$(STATEMENT$)="" THEN GOTO 2900
2070 IF LEN(STATEMENT$)>4 THEN LET LINE=NUM(STATEMENT$(1,5),ERR=2070)+1
2090 IF LEN(STATEMENT$)>80 THEN LET INDENT=POS(" "=STATEMENT$)
2200 REM " --- Page full?"
2210 IF L<L9 THEN GOTO 2500
2220 GOSUB 6000
2230 IF V3=4 THEN EXITTO 1000
2500 REM 
2510 IF LEN(STATEMENT$)<80+INDENT THEN GOTO 2600
2520 IF L1>0 THEN LET STATEMENT$=FILL(INDENT)+FNS$(STATEMENT$)
2530 LET X$=STATEMENT$(1,80),STATEMENT$=STATEMENT$(81)
2540 GOSUB 5000
2590 GOTO 2200
2600 REM 
2610 IF L1>0 THEN LET STATEMENT$=FILL(INDENT)+FNS$(STATEMENT$)
2620 LET X$=STATEMENT$
2630 GOSUB 5000
2900 REM " --- Next line"
2990 NEXT LINE
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 1000
5000 REM " --- Display"
5010 PRINT @(P,L),X$,
5020 LET L=L+1,L1=L1+1
5090 RETURN 
6000 REM " --- Continue?"
6010 LET V4$="Press <Enter> To Continue:"
6020 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
6030 GOSUB 7000
6040 ON V3 GOTO 6050,6000,6050,6000,6050
6050 PRINT @(0,L0),'CE',
6060 LET L=L0
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNS$(Q$)=CVS(Q$,1)
8085 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

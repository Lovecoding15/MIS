0010 REM "WOS - WO Operation & Subcontract Transaction Adjustment Entry"
0020 REM "Program:  WOE.FA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "13Nov96 JJD: Properly initialize adjustment file fields (520,1005,1240,1245)"
0032 REM "13Nov96 JJD: Access appropriate data file (1410)"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=13
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-12",FILES$[2]="WOE-42",FILES$[3]="WOE-01"
0120 LET FILES$[4]="SYS-01",FILES$[5]="WOT-01",FILES$[6]="WOT-31"
0125 LET FILES$[7]="IVM-10",FILES$[8]="IVM-01",FILES$[9]="WOM-10"
0150 CALL "SYC.DA",1,1,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE12_DEV=CHANNELS[1],WOE42_DEV=CHANNELS[2],WOE01_DEV=CHANNELS[3]
0165 LET SYS01_DEV=CHANNELS[4],WOT01_DEV=CHANNELS[5],WOT31_DEV=CHANNELS[6]
0170 LET IVM10_DEV=CHANNELS[7],IVM01_DEV=CHANNELS[8],WOM10_DEV=CHANNELS[9]
0200 REM " --- IOLISTs"
0210 WOADJ: IOLIST A0$,A1$,A[ALL]
0220 IVM01A: IOLIST C0$,C1$(1)
0230 WOE01A2: IOLIST X$,X0$
0240 WOE01A: IOLIST D0$,D1$,D[ALL]
0250 WOTRAN: IOLIST B0$,B1$,B[ALL]
0260 WOS01A: IOLIST X$,P1$,P2$,P9$,X$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 IVS01A: IOLIST X$,X$,P4$,P3$
0290 WOM10A: IOLIST X1$
0300 APM01A: IOLIST O0$,O1$(1)
0310 OPCODE: IOLIST O2$
0320 EMPCODE: IOLIST O0$,O1$
0330 PRS01A: IOLIST I0$,I1$,I2$,*,*,I3$
0340 ARS01A: IOLIST *,*,X$,*,*,P7$
0350 APS01A: IOLIST *,*,X$,*,*,P8$
0400 REM " --- Parameters"
0405 DIM P[1]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N4$=F5$
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0425 LET BM$=P9$(1,1),PR$=P9$(4,1),AR$=P9$(2,1),AP$=P9$(3,1)
0430 IF PR$<>"Y" THEN GOTO 0445
0435 FIND (SYS01_DEV,KEY=N0$+"PR00",DOM=9800)IOL=PRS01A
0440 LET P1$=I1$,P2$=I2$,M0$=I3$
0445 IF AR$<>"Y" THEN GOTO 0460
0450 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=0465)IOL=ARS01A
0455 LET P7=NUM(X$(1,2))
0460 IF AP$<>"Y" THEN GOTO 0475
0465 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=0475)IOL=APS01A
0470 LET P8=NUM(X$(1,2))
0475 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0480 LET P[0]=NUM(P4$(1,2)),P[1]=NUM(P4$(5,1)),P0=NUM(P2$(1,2)),P1=P[1]
0485 LET GL$="N",STATUS=0,SOURCE$=PGM(-2)
0490 CALL "GLC.BA",ERR=9900,SOURCE$,N2$,GLW11$,GL$,STATUS
0495 IF STATUS<>0 THEN GOTO 9900
0500 REM " --- Init Data"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 DIM A0$(18),A[11],D[11],B[11],DESC[3]
0530 LET K0=1,K9=11
0540 LET A0$(1)=N0$
0550 FOR X=1 TO 3
0555 LET DESC[X]=NUM(P4$((X-1)*2+9,2))
0560 NEXT X
0570 CALL "SYC.RM",M1$,M1,10,12
0575 CALL "SYC.RM",M2$,M2,10,12
0580 CALL "SYC.RM",M3$,M3,10,12
0600 REM " --- Additional File Opens "
0610 IF PR$="Y" THEN LET FILES$[10]="PRM-01"
0620 IF PR$<>"Y" THEN LET FILES$[10]="WOM-01"
0630 IF BM$="Y" THEN LET FILES$[11]="BMM-08"
0640 IF BM$<>"Y" THEN LET FILES$[11]="WOM-02"
0650 IF AP$="Y" THEN LET FILES$[12]="APM-01"
0655 CALL "SYC.DA",1,10,12,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0660 IF STATUS THEN GOTO 9000
0670 LET EMPCODE_DEV=CHANNELS[10],OPCODE_DEV=CHANNELS[11],APM01_DEV=CHANNELS[12]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,10,0
0720 PRINT 'SB',@(0,3),"WO Number:",@(20),"Type:",@(54),"Status:",@(0,5),"Bill Number:",@(0,6),"Description:",@(52,4),"Category:",@(51,5),"Warehouse:",@(57,6),"U/M:",@(5,7),"Source: ",@(1,8),"Trans Date:",@(8,9),"Seq:",
0790 PRINT 'SF',
0800 REM " --- Options"
0900 REM " --- Init Files
0910 PRECISION 4
1000 REM " --- Key"
1005 DIM A[11]
1010 PRINT 'CF',
1020 LET V2$=""
1030 LET V0$="Z",V1$="CE",V3$="",V0=7,V1=11,V2=3,V4$="Enter WO #, <F3>=Lookup"
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1060 IF V3=3 THEN CALL "SYC.LW",V$,"O"
1070 IF V$="" OR POS("0"<>V$)=0 THEN GOTO 0900
1080 LET A0$(5)=V$,O9$="0"
1090 FIND (WOE01_DEV,KEY=A0$(1,11),DOM=1000)IOL=WOE01A
1100 GOSUB 5000
1110 IF D0$(15,1)<>"C" THEN GOTO 1200
1120 LET V0$="S",V1$="C",V2$="",V3$="",V4$="This Work Order Is Closed, <Enter> To Continue: ",V0=1,V1=FNV(V4$),V2=22
1130 PRINT @(V1,V2),'RB',
1140 GOSUB 7000
1150 GOTO 1000
1200 REM " --- Transaction Source (Operations or Subcontracts)"
1205 LET V2$=""
1210 LET V0$="S",V1$="RC",V3$="MOS",V4$="Enter Transaction Source ID, O=Operations, S=Subcontracts",V0=1,V1=14,V2=7
1215 GOSUB 7000
1220 IF V3=2 THEN GOTO 1000
1225 IF V3=4 THEN GOTO 1000
1230 IF POS(V$="OS")=0 THEN GOTO 1205
1235 LET A0$(15,1)=V$
1240 IF A0$(15,1)="O" THEN LET WOTRAN_DEV=WOT01_DEV,WOADJ_DEV=WOE12_DEV; DIM A1$(23)
1245 IF A0$(15,1)="S" THEN LET WOTRAN_DEV=WOT31_DEV,WOADJ_DEV=WOE42_DEV; DIM A1$(30)
1250 READ (WOTRAN_DEV,KEY=A0$(1,11),DOM=1255)
1255 LET V2$="",V2$=KEY(WOTRAN_DEV,END=1265)
1260 IF V2$(1,11)=A0$(1,11) THEN GOTO 1300
1265 DIM MESS$[2]
1270 IF A0$(15,1)="O" THEN LET TYPE$="Operation" ELSE LET TYPE$="Subcontract"
1275 LET MESS$[0]="There Are No"+TYPE$+" Transactions"
1280 LET MESS$[1]="For This Work Order. <Enter>=Continue."
1285 CALL "SYC.XA",0,MESS$[ALL],1,22,-1,V$,V3
1290 GOTO 1200
1300 REM " --- Transaction Date "
1310 LET V2$=V2$(12,3)
1320 LET V0$="D",V1$="CRE",V3$="",V4$="Enter Transaction Date To Modify, <F1>=List Transactions",V0=3,V1=14,V2=8,X9$=""
1325 GOSUB 7000
1330 IF V3=4 THEN GOTO 1000
1335 IF V3=2 THEN GOTO 1205
1340 IF V3<>1 THEN GOTO 1380
1345 GOSUB 6200
1350 IF V$="" THEN GOTO 1250
1355 LET V$=A0$(12,3)
1360 GOSUB 6900
1365 IF STATUS>99 THEN GOTO 1250
1370 PRINT @(14,7),A0$(15,1),@(14,8),FNB$(A0$(12)),@(14,9),A0$(16,3)
1375 GOTO 1530
1380 IF V$="" THEN GOTO 1250
1385 LET A0$(12,3)=V$
1390 GOSUB 6900
1395 IF STATUS>99 THEN GOTO 1250
1400 REM " --- Sequence #
1410 LET V2$="",V2$=KEY(WOADJ_DEV,END=1440)
1415 IF V2$(1,15)<>A0$(1,15) THEN LET V2$=""; GOTO 1440
1420 LET V2$=V2$(16,3)
1440 LET V0$="Z",V1$="ERC",V3$="000",V4$="Enter Sequence Number, <F1>=List Transactions",V0=3,V1=14,V2=9
1460 GOSUB 7000
1470 IF V3=2 THEN GOTO 1250
1480 IF V3=4 THEN GOTO 1000
1500 IF V3<>1 THEN GOTO 1510
1502 READ (WOTRAN_DEV,KEY=A0$(1,15),DOM=1504)
1504 LET X9$=A0$(12,3)
1506 GOSUB 6220
1508 IF V$<>"" THEN PRINT @(14,9),A0$(16,3); GOTO 1530
1510 IF V$="" THEN LET V$="000"
1515 PRINT @(14,9),V$,
1520 LET A0$(16,3)=V$
1530 FIND (WOTRAN_DEV,KEY=A0$,DOM=1550)IOL=WOTRAN
1540 GOTO 1600
1550 LET V0$="S",V1$="C",V2$="",V3$="",V4$="No Transaction For This Data, <Enter> To Retry:",V0=1,V1=FNV(V4$),V2=22
1552 PRINT @(V1,V2),'RB',
1555 GOSUB 7000
1560 PRINT @(0,11),'CE'
1570 FOR X=8 TO 9; PRINT @(14,X),'CL'; NEXT X
1575 REMOVE (WOADJ_DEV,KEY=A0$,DOM=1580)
1580 GOTO 1250
1600 REM " --- Additional Reads
1610 GOSUB 5200
1620 ON POS(B0$(15,1)="OS")-1 GOTO 1700,1800
1700 REM " --- Display Operation Info
1710 DIM O1$(30),O2$(30)
1715 LET O1$(1)="Missing ",O1$(17)="Conversion",O2$(7,20)="*** Not On File ***"
1720 FIND (EMPCODE_DEV,KEY=N0$+B1$(7,9),DOM=1730)IOL=EMPCODE
1730 LET O1$=O1$(1,16)+", "+O1$(17,14),D9$=O1$
1740 GOSUB 5700
1750 FIND (OPCODE_DEV,KEY=N0$+B1$(4,3),DOM=1760)IOL=OPCODE
1760 LET O2$=O2$(6,20)
1770 PRINT @(47,8),FNF$(B1$(7,P0),M0$)," ",D9$,@(47,9),B1$(4,3)," ",O2$
1780 PRINT @(61,12),B[3]:M3$,@(61,13),B[1]-B[3]:M3$,@(61,14),B[6]:M2$,
1785 PRINT @(61,17),B[5]:M2$,
1790 GOTO 1900
1800 REM " --- Display Subcontract Info
1810 DIM O1$(30)
1815 LET O1$(1)="*** Not On File ***"
1820 FIND (APM01_DEV,KEY=N0$+B1$(4,6),DOM=1840)IOL=APM01A
1840 PRINT @(47,8),FNF$(B1$(4,P8),P8$)," ",O1$,@(47,9),B1$(10,7)
1850 PRINT @(61,13),B[1]:M3$
1890 GOTO 1900
1900 REM " --- Finish Displaying Existing Here
1920 PRINT @(61,15),B[0]:M2$,@(61,16),B[2]:M1$
1970 FIND (WOADJ_DEV,KEY=A0$,DOM=2000)IOL=WOADJ
1980 GOSUB 5500
1990 GOTO 4000
2000 REM " --- New Entry?
2020 LET V4$="Is This A New Entry: "
2040 CALL "SYC.YN",1,V4$,1,V$,V3
2060 IF V$="N" THEN GOTO 1560
2080 LET A1$(1,7)=A0$(5,7),A1$(8,3)=A0$(12,3),A[0]=B[0]
2090 IF A0$(15,1)="O" THEN LET A[1]=B[3],A[2]=B[1]-B[3],A[3]=B[6],A[4]=B[5] ELSE LET A[1]=B[1]
2100 REM " --- New Rates Here
2110 IF A0$(15,1)<>"O" THEN GOTO 2190
2120 LET V0$="N",V1$="C",V2$=STR(A[1]),V3$=M3$,V4$="Enter New Direct Rate, <F1>=No Change",V0=5,V1=19,V2=12
2140 GOSUB 7000
2150 IF V3=1 THEN LET V=B[3]
2170 LET A[1]=V
2180 PRINT @(V1,V2),A[1]:M3$
2190 GOTO 2900
2200 REM " --- Overhead Rate
2210 IF A0$(15,1)<>"O" THEN GOTO 2290
2220 LET V0$="N",V1$="C",V2$=STR(A[2]),V3$=M3$,V4$="Enter New Overhead Rate, <F1>=No Change",V0=5,V1=19,V2=13
2230 GOSUB 7000
2240 IF V3=1 THEN LET V=B[1]-B[3]
2260 LET A[2]=V
2280 PRINT @(V1,V2),A[2]:M3$
2290 GOTO 2900
2300 REM " --- Unit Cost (Subcontracts)
2320 IF A0$(15,1)<>"S" THEN GOTO 2390
2330 LET V0$="N",V1$="C",V2$=STR(A[1]),V3$=M2$,V4$="Enter New Unit Cost, <F1>=No Change",V0=5,V1=19,V2=13
2340 GOSUB 7000
2350 IF V3=1 THEN LET V=B[1]
2370 LET A[1]=V
2380 PRINT @(V1,V2),A[1]:M3$
2390 GOTO 2900
2400 REM " --- Units Here
2420 LET V0$="N",V1$="C",V2$=STR(A[0]),V3$=M2$,V4$="Enter New Units, <F1>=No Change",V0=5,V1=19,V2=15
2425 IF A0$(15,1)="O" THEN LET V4$="Enter New Run Hours, <F1>=No Change"
2430 GOSUB 7000
2450 IF V3=1 THEN LET V=B[0]
2470 LET A[0]=V
2480 PRINT @(V1,V2),A[0]:M2$,
2490 GOTO 2900
2500 REM " --- New Work Order #     
2520 LET V0$="Z",V1$="C",V2$=A1$(1,7),V3$="",V4$="Enter New Work Order #, <F1>=No Change, <F3>=Lookup",V0=7,V1=19,V2=19
2540 GOSUB 7000
2550 IF V3=1 THEN LET V$=A0$(5,7); PRINT @(V1,V2),V$
2555 IF V3=3 THEN CALL "SYC.LW",V$,"O"
2560 IF V$="" OR NUM(V$,ERR=2500)=0 THEN LET V$=A0$(5,7)
2570 FIND (WOE01_DEV,KEY=N0$+"  "+V$,DOM=2500)IOL=WOE01A2
2572 IF X$(15,1)<>"C" THEN GOTO 2580
2575 LET V0$="S",V1$="C",V2$="",V3$="",V4$="This Work Order Is Closed!! <Enter> To Retry. ",V0=1,V1=FNV(V4$),V2=22; PRINT @(V1,V2),'RB',
2577 GOSUB 7000
2578 GOTO 2500
2580 LET A1$(1,7)=V$
2585 PRINT @(V1,V2),A1$(1,7),@(V1+V0+2,V2),X0$(1,30)
2590 GOTO 3900
2600 REM " --- New Transaction Date
2610 IF A1$(8,3)="   " THEN LET A1$(8,3)=A0$(12,3)
2620 LET V0$="D",V1$="C",V2$=A1$(8,3),V3$="",V4$="Enter A New Transaction Date, <F1>=No Change",V0=3,V1=19,V2=20
2640 GOSUB 7000
2650 IF V3=1 THEN LET V$=A0$(12,3)
2660 IF V$="" THEN LET V$=A0$(12,3)
2670 LET A1$(8,3)=V$
2673 GOSUB 6900
2675 IF STATUS>99 THEN GOTO 2600
2680 PRINT @(V1,V2),FNB$(A1$(8,3))
2690 GOTO 3900
2700 REM " --- Setup Units Here
2710 IF A0$(15,1)<>"O" THEN GOTO 2790
2720 LET V0$="N",V1$="C",V2$=STR(A[3]),V3$=M2$,V4$="Enter New Setup Units, <F1>=No Change",V0=5,V1=19,V2=14
2740 GOSUB 7000
2750 IF V3=1 THEN LET V=B[6]
2770 LET A[3]=V
2780 PRINT @(V1,V2),A[3]:M2$,
2790 GOTO 2900
2900 REM " --- Display New Extension
2920 PRINT @(19,16),(A[0]+A[3])*(A[1]+A[2]):M1$,
2990 GOTO 3900
3000 REM " --- Qty Complete Here
3010 IF A0$(15,1)<>"O" THEN GOTO 3090
3020 LET V0$="N",V1$="C",V2$=STR(A[4]),V3$=M2$,V4$="Enter New Quantity Complete, <F1>=No Change",V0=5,V1=19,V2=17
3040 GOSUB 7000
3050 IF V3=1 THEN LET V=B[5]
3070 LET A[4]=V
3080 PRINT @(V1,V2),A[4]:M2$,
3090 GOTO 2900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 2100,2200,2300,2700,2400,3000,2500,2600,4000
4000 REM " --- Options"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct? (Y/N/Delete): ",V0=7,V1=FNV(V4$),V2=22,I0=0
4025 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4050 ON POS(V$="YN") GOTO 4000,4100,2100
4100 REM " --- Write"
4120 WRITE (WOADJ_DEV,KEY=A0$)IOL=WOADJ
4190 GOTO 4300
4200 REM " --- Delete Adjustments Here
4220 REMOVE (WOADJ_DEV,KEY=A0$,DOM=4300)
4300 REM " --- Clean Up Screen Here
4320 PRINT @(0,11),'CE'
4340 FOR X=7 TO 9; PRINT @(14,X),'CL'; NEXT X
4390 GOTO 1000
5000 REM " --- Display"
5005 DIM C1$(60)
5010 FIND (IVM01_DEV,KEY=N0$+D0$(56),DOM=5020)IOL=IVM01A
5020 DIM X1$(30)
5025 LET X1$(6,20)="** Not On File **",I9$="N"
5040 FIND (WOM10_DEV,KEY=N0$+"A"+D0$(12,2),DOM=5060)IOL=WOM10A
5050 LET I9$=D0$(14,1)
5060 PRINT @(11,3),A0$(5,7),@(27,3),D0$(12,2),"  ",X1$(6,20),@(63),D0$(15,1),@(63,4),D0$(14,1),@(63,5),D0$(54,2),@(63,6),D0$(50,2)
5070 IF I9$="I" THEN PRINT @(65,4),"Inventory",@(14,5),D0$(56) ELSE IF I9$="N" THEN PRINT @(65,4),"Non-Stocking", ELSE IF I9$="R" THEN PRINT @(65,4),"Recurring"
5075 IF D0$(15,1)="C" OR POS(" "<>D0$(31,3))>0 THEN PRINT @(65,8),FNB$(D0$(31,3))
5080 IF I9$<>"I" THEN PRINT @(14,6),D1$(1,30),
5090 IF I9$="I" THEN PRINT @(14,6),C1$(1,MIN(DESC[1],42)),
5100 RETURN 
5200 REM " --- Display Prompts Here
5220 PRINT @(0,11),'CE'
5230 PRINT 'SB',@(16,7),"-"
5240 ON POS(B0$(15,1)="OS")-1 GOTO 5300,5400
5300 PRINT @(18,7),"Operations"
5310 PRINT @(36,8),"Employee:",@(37,9),"Op Code:",
5320 PRINT @(5,12),"Direct Rate:",@(45),"Direct Rate:",
5330 PRINT @(3,13),"Overhead Rate:",@(43),"Overhead Rate:",
5340 PRINT @(5,14),"Setup Units:",@(45),"Setup Units:",
5345 PRINT @(4,17),"Qty Complete:",@(44),"Qty Complete:",
5350 GOTO 5450
5400 PRINT @(18,7),"Subcontracts"
5420 PRINT @(38,8),"Vendor: ",@(35,9),"PO Number:",
5440 PRINT @(7,13),"Unit Cost:",@(47,13),"Unit Cost:"
5450 PRINT @(0,11),"Adjustment:",@(40,11),"Original:",
5460 PRINT @(11,15),"Units:",@(51,15),"Units:",@(11,16),"Total:",@(51,16),"Total:",
5470 PRINT @(7,19),"WO Number:",@(6,20),"Trans Date:",
5480 PRINT 'SF'
5490 RETURN 
5500 REM " --- Display Data Here
5520 IF A0$(15,1)="O" THEN PRINT @(19,12),A[1]:M3$,@(19,13),A[2]:M3$,@(19,14),A[3]:M2$,@(19,17),A[4]:M2$,
5530 IF A0$(15,1)="S" THEN PRINT @(19,13),A[1]:M3$
5540 PRINT @(19,15),A[0]:M2$,@(19,16),(A[0]+A[3])*(A[1]+A[2]):M1$,
5560 PRINT @(19,19),A1$(1,7),@(19,20),FNB$(A1$(8))
5590 RETURN 
5700 REM " --- Compress Desc
5730 LET Z=POS(" "<>D9$); IF Z>1 THEN LET D9$=D9$(Z)
5740 LET X8=POS("  "=D9$); IF X8<>0 THEN LET Y8=POS(" "<>D9$(X8+2)); LET D9$=D9$(1,X8)+D9$(X8+Y8+1); IF Y8<>0 THEN GOTO 5740
5790 RETURN 
6200 REM " --- Display All Transaction "
6205 LET V0$="D",V1$="CE",V2$="",V3$="",V4$="Enter Beginning Date, <Enter>=First: ",V0=3,V1=FNV(V4$),V2=22
6210 GOSUB 7000
6215 READ (WOTRAN_DEV,KEY=A0$(1,11)+V$,DOM=6220)
6220 LET K9$="",L9=22,L=12,X=0
6225 PRINT @(4,11),'SB',"Tran Dt  Src Seq   Description",@(77-M3-M1-M3-6),"Units",@(77-M3-M1-5),"Cost",@(77-M3-6),"Total",@(77-7),"Direct",'SF'
6230 LET K$=KEY(WOTRAN_DEV,END=6500)
6235 IF POS(A0$(1,11)=K$)<>1 THEN GOTO 6500
6240 IF POS(K$(15,1)="OS")=0 THEN READ (WOTRAN_DEV); GOTO 6230
6245 IF A0$(15,1)<>" " THEN IF A0$(15,1)<>K$(15,1) THEN READ (WOTRAN_DEV); GOTO 6230
6250 IF X9$<>"" THEN IF K$(12,3)<>X9$ THEN GOTO 6500
6255 READ (WOTRAN_DEV,KEY=K$)IOL=WOTRAN
6260 IF L<L9 THEN GOTO 6400
6265 IF X<>0 THEN GOTO 6290
6270 LET V0$="S",V1$="C",V2$="",V3$="",V4$="No Selections Found, <Enter> To Return: ",V0=1,V1=FNV(V4$),V2=22
6275 GOSUB 7000
6280 LET V$=""
6285 GOTO 6590
6290 LET V0$="Z",V1$="C",V2$="",V3$="",V4$="Select A Transaction (1 Through "+STR(X)+"), <Enter> To Continue, <F4>=End: ",V0=LEN(STR(X)),V1=FNV(V4$)+V0,V2=22
6295 GOSUB 7000
6300 IF V3=4 THEN GOTO 1560
6301 IF NUM(V$)=0 THEN PRINT @(0,12),'CE'; GOTO 6220
6305 IF NUM(V$)>X THEN GOTO 6290 ELSE LET X=NUM(V$),A0$=K9$((X-1)*18+1,18),V$="GOTIT"; GOTO 6590
6310 PRINT @(0,12),'CE'
6315 LET L=12,K9$="",X=0
6320 IF K8$="END" THEN LET K8$=""; GOTO 6230
6400 REM " --- Display Information "
6410 PRINT @(0,L),X+1:"#0",". ",FNB$(K$(12)),"  ",K$(15,1),"  ",K$(16,3),
6420 ON POS(B0$(15,1)="OS")-1 GOTO 6430,6460
6430 REM " --- Op Codes Here
6440 PRINT @(23,L),"Emp: ",FNF$(B1$(7,P0),M0$),"  Op Code: ",B1$(4,3),
6450 GOTO 6470
6460 PRINT @(23,L),"Vndr: ",FNF$(B1$(4,P8),P8$),"  PO Number: ",B1$(10,7)
6470 PRINT @(77-M1-M3*2-M2,L+1),B[0]:M2$,B[1]:M3$,B[2]:M1$,B[3]:M3$
6480 LET X=X+1,L=L+2,K9$=K9$+K$
6490 GOTO 6230
6500 REM " --- End Of File Here
6510 READ (WOTRAN_DEV,KEY=A0$(1,11)+X9$,DOM=6520)
6520 LET K8$="END"
6540 GOTO 6265
6590 PRINT @(0,11),'CE'
6595 RETURN 
6900 REM " --- Check transaction date"
6910 LET STATUS=0
6920 IF GL$="Y" THEN CALL "GLC.EA",V$,"Y",PERIOD$,YEAR$,STATUS
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

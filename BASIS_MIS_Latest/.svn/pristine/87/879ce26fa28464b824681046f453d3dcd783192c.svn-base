0010 REM BLK.12 - based on dsb_print -Distributor License/ActKey form printing w/unform
0020 BEGIN
0030 LET SYS01T=UNT; OPEN (SYS01T)"SYS-01"
0040 LET TERMINAL$="T"+FID(0); READ (SYS01T,KEY=TERMINAL$)*,F0$
0050 CLOSE (SYS01T)
0060 LET AON$=STBL("AON"),FILE_TO_PRINT$=AON$+"tmp/"+FID(0)+"BLK10.txt"
0070 LET EMAIL_FILE$=AON$+"tmp/"+FID(0)+"BLK10dsb.txt",lf$=$0A$
0080 LET PRINTER$=F0$(13,2),smbclient=0
0090 REM ' Check for GUI or character version of PRO5
0100 IF INFO(6,0)<>"WINDOWS" THEN LET GUI_VERSION=0 ELSE LET GUI_VERSION=1
0110 REM ' Check TSK() function for SYSGUI and SYSPRINT devices
0120 LET TSKNUM=0,SYSGUI$="",PRTLIST$="",PRTCOUNT=0
0130 DIM TSK$:"type:u(1),opens:u(1),name:c(1*=0),dname:c(1*=0),pname:c(1*=0),modes:c(1*=0)"
0140 CHECK_TSK: 
0150 LET TSK$=TSK(TSKNUM,ERR=DONE_WITH_TSK)
0160 IF SYSGUI$="" AND CVS(TSK.PNAME$,7)="SYSGUI" THEN LET SYSGUI$=TSK.NAME$
0170 IF TSK.TYPE=1 THEN LET PRTLIST$=PRTLIST$+TSK.NAME$+" "+TSK.DNAME$+lf$,PRTCOUNT=PRTCOUNT+1
0180 IF TSK.name$=PRINTER$ THEN LET smbclient=POS("smbclient"=TSK.pname$)
0190 LET TSKNUM=TSKNUM+1
0200 GOTO CHECK_TSK
0210 DONE_WITH_TSK: 
0220 GOSUB OPEN_PRINTER
0230 PRINT 'CS','WINDOW'(5,5,70,8,"Key/License Form Printing"),
0260 GOSUB OPEN_FILE
0270 REM ' Templates
0280 LET KEY_TMP$="number:c(10*=44),auth_key:c(13*=44),users:c(3*=44),order_no:c(7*=44),order_date:c(10*=44),part_num:c(22*=44),desc1:c(30*=44),rev_lvl:c(10*=44),system:c(48*=44),po:c(5*=44),cust:c(6)"
0290 LET LIC_TMP$="number:c(10*=44),auth_key:c(10*=44),users:c(3*=44),order_no:c(7*=44),order_date:c(10*=44),part_num:c(22*=44),desc1:c(30*=44),rev_lvl:c(10*=44),system:c(48*=44),flex_feature:c(7*=44),flex_key:c(12*=44),flex_rev_lvl:c(10*=44),flex_host_id:c(10*=44),flex_expiry_date:c(11*=44),flex_checksum:c(2*=44),flex_users:c(10*=44),flex_number:c(10*=44),platform:c(1*=44),po:c(5*=44),cust:c(6)"
0300 LET FIRST_PR_COL$=FILL(37),SECOND_PR_COL$=FILL(69),INSTRUCTION_COL$=FILL(8),eof=0
0310 PRINT_IT: 
0320 READ (INFILE_CHAN,END=END_OF_FILE)READ_REC$
0330 IF CVS(READ_REC$,3)=$$ THEN GOTO PRINT_IT
0340 REM ' Parse activation key or authorization number into k$
0350 LET K$="",C1=POS(","=READ_REC$)
0360 IF C1 THEN LET C2=POS(","=READ_REC$(C1+1)); IF C2 THEN LET K$=READ_REC$(C1+1,C2-1)
0370 LET K_SIZE=LEN(CVS(K$,3)); REM Keys are 13 characters, authorization numbers are 10
0380 IF K_SIZE=10 THEN DIM REC$:LIC_TMP$; LET REC_TYPE$="lic" ELSE DIM REC$:KEY_TMP$; LET REC_TYPE$="key"
0390 LET REC$=READ_REC$
0400 PRINT 'CS','CR',"  Printing ",REC.NUMBER$
0410 IF LAST_K_SIZE=0 THEN LET LAST_K_SIZE=K_SIZE ELSE IF LAST_K_SIZE<>K_SIZE THEN LET LAST_K_SIZE=K_SIZE; GOSUB RESELECT_FORM
0420 IF PAGE>=40 THEN GOSUB RESELECT_FORM; REM close & reopen printer every 40 pages anyway to keep print spooler from choking
0430 IF PAGE THEN GOSUB EJECT_THE_PAGE
0440 LET PAGE=PAGE+1
0450 IF REC_TYPE$="lic" THEN GOSUB FLEXLM ELSE GOSUB ACT_KEY
0460 GOTO PRINT_IT
0470 REM ' Subroutines
0480 ACT_KEY: 
0490 FOR LINE=1 TO 9
0500 PRINT (PRINTER)""
0510 NEXT LINE
0520 LET ONUM$=REC.ORDER_NO$; IF F0$(16,2)="02" THEN LET ONUM$=ONUM$+"-"; REM  '-' triggers unform to print company 2 name
0530 PRINT (PRINTER)FIRST_PR_COL$,REC.NUMBER$,lf$,FIRST_PR_COL$,REC.AUTH_KEY$,lf$,FIRST_PR_COL$,REC.USERS$,lf$,FIRST_PR_COL$,REC.PART_NUM$,lf$,FIRST_PR_COL$,REC.REV_LVL$,lf$,FIRST_PR_COL$,REC.SYSTEM$,lf$,FIRST_PR_COL$,REC.DESC1$
0540 PRINT (PRINTER)SECOND_PR_COL$,ONUM$,lf$,SECOND_PR_COL$,REC.ORDER_DATE$
0550 FOR LINE=1 TO 13; PRINT (PRINTER)""; NEXT LINE
0560 PRINT (PRINTER)INSTRUCTION_COL$,"For installation instructions, please see your Installation",lf$,INSTRUCTION_COL$,"and Configuration Guide"
0570 RETURN
0580 FLEXLM: 
0590 PRINT (PRINTER)""
0600 PRINT (PRINTER)"License Authorization"
0610 PRINT (PRINTER)REC.ORDER_DATE$
0620 PRINT (PRINTER)REC.CUST$
0630 PRINT (PRINTER)REC.ORDER_NO$
0640 PRINT (PRINTER)REC.PART_NUM$
0650 PRINT (PRINTER)REC.DESC1$
0660 PRINT (PRINTER)REC.SYSTEM$
0670 PRINT (PRINTER)REC.FLEX_REV_LVL$
0680 PRINT (PRINTER)REC.USERS$
0690 PRINT (PRINTER)REC.NUMBER$
0700 PRINT (PRINTER)REC.AUTH_KEY$
0710 PRINT (PRINTER)REC.FLEX_FEATURE$
0720 PRINT (PRINTER)REC.FLEX_KEY$
0730 PRINT (PRINTER)""
0740 PRINT (PRINTER)REC.FLEX_EXPIRY_DATE$
0750 PRINT (PRINTER)REC.FLEX_CHECKSUM$
0760 PRINT (PRINTER)REC.FLEX_USERS$
0770 PRINT (PRINTER)""
0780 PRINT (PRINTER)REC.PO$
0790 RETURN
0800 EJECT_THE_PAGE: 
0810 IF smbclient THEN GOSUB open_printfile; RETURN
0820 PRINT (PRINTER)$0C$,
0830 RETURN
0840 RESELECT_FORM: 
0850 IF smbclient THEN RETURN
0860 gosub open_printer
0870 page=0
0880 RETURN
0890 OPEN_FILE: 
0900 LET INFILE_CHAN=UNT
0910 OPEN (INFILE_CHAN)FILE_TO_PRINT$
0920 RETURN
0930 OPEN_PRINTER: 
1100 CLOSE (PRINTER,ERR=*NEXT)
1110 LET PRINTER=UNT
1120 IF smbclient THEN GOSUB open_printfile; RETURN
1125 CALL "SYC.GA",printer,1,"","",STATUS
1127 IF STATUS THEN GOTO end_program
1135 textprt=pos(printer$="PF,PX")
1140 RETURN
1150 open_printfile: 
1160 IF PAGE THEN GOSUB print_printfile
1170 IF eof THEN RETURN
1180 LET printfile$=STBL("TEMP")+PRINTER$+DATE(0:"%m%s")+".txt"
1190 ERASE printfile$,ERR=*NEXT
1200 STRING printfile$
1210 OPEN (PRINTER)printfile$
1220 RETURN
1230 print_printfile: 
1240 CLOSE (PRINTER,ERR=*NEXT)
1250 LET a=SCALL("unform50 -i "+printfile$+" -o "+printfile$+".pcl -f basis.rul")
1260 IF a THEN RETURN
1270 LET f=UNT; OPEN (f,ERR=end_program)printfile$+".pcl"
1280 LET fin$=FIN(f),fsize=DEC(fin$(1,4)),smbclient=0
1290 READ RECORD (f,SIZ=fsize)f$
1300 CLOSE (f)
1310 gosub open_printer
1320 if textprt=0 then PRINT (PRINTER)'BO',f$,'EO' else PRINT (PRINTER)f$
1330 CLOSE (PRINTER)
1340 ERASE printfile$,ERR=*NEXT
1350 ERASE printfile$+".pcl",ERR=*NEXT
1360 RETURN
1370 END_OF_FILE: 
1380 LET eof=1; GOSUB EJECT_THE_PAGE
1390 CLOSE (INFILE_CHAN); CLOSE (PRINTER)
1400 PRINT 'POP'
1410 ERASE FILE_TO_PRINT$,ERR=*NEXT
1420 ERASE EMAIL_FILE$,ERR=*NEXT
1430 END_PROGRAM: 
1440 RUN "SYS.AA"

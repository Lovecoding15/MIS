0010 REM "OPM - Order/Invoice Line Code Maintenance"
0020 REM "Program OPM.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "23Oct96 JWK: Redisplay Code after lookup (5010)
0040 REM 
0060 BEGIN 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ARM-10",FILES$[2]="IVM-10",FILES$[3]="ARM-10",FILES$[4]="SYS-01",FILES$[5]="GLM-01"
0140 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET ARM10_DEV1=CHANNELS[1],IVM10_DEV=CHANNELS[2],ARM10_DEV2=CHANNELS[3],SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLists"
0210 ARM10E: IOLIST A0$(1),A1$(1)
0220 GLM01A: IOLIST B0$(1),B1$(1)
0230 IVM10A: IOLIST C0$(1)
0240 ARM10D: IOLIST D0$(1),D1$(1)
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters "
0405 DIM G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 CALL "SYC.VA","OP",INFO$[ALL]
0495 LET GL$=INFO$[9]
0500 REM " --- Initialize Data "
0510 DIM A0$(32),A1$(G[4]+2),B0$(2+G[4]),B1$(35),C0$(32)
0520 DIM D0$(32),D1$(8*G[4])
0590 LET K0=1,K9=4,A0$(1,3)=N0$+"E"
0600 REM " --- Additional File Opens"
0610 IF GL$<>"Y" THEN GOTO 0700
0615 CALL "SYC.DA",1,5,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS>0 THEN GOTO 9900
0630 LET GLM01_DEV=CHANNELS[5]
0700 REM " --- Background "
0720 PRINT 'SB',@(14,3),"Line Code:",@(12,4),"Description:",@(14,5),"Line Type:",@(11,6),"Message Type:",
0730 IF GL$="Y" THEN PRINT @(6,7),"G/L Sales Account:",
0740 PRINT @(16,8),"Taxable:",@(14,9),"Drop Ship:",@(0,10),"Product Type Processing:",@(3,11),"Default Product Type:",@(6,12),"Distribution Code:",'SF',
0900 REM "INIT FILE"
0920 READ (ARM10_DEV1,KEY=A0$(1,3),DOM=0930)
1000 REM " --- Key "
1010 PRINT 'CF',@(0,13),'CE',
1020 LET V2$="",V2$=KEY(ARM10_DEV1,END=1050)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4)
1050 LET V0$="S",V1$="KEC",V3$="",V0=1,V1=25,V2=3,V4$="Enter A Valid Line Code (<F3>=Lookup)"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN GOSUB 6000
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(4)=V$
1200 REM " --- Find Key "
1210 LET O9=0
1220 FIND (ARM10_DEV1,KEY=A0$(1,K9),DOM=1400)IOL=ARM10E
1300 REM " --- Display "
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record "
1420 LET V4$="Is This A New Record",V0=1,V1=FNV(V4$),V2=22
1430 CALL "SYC.YN",1,V4$,1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-Up "
1510 LET A0$(6)="",A1$(1)=""
2000 REM " --- Description "
2010 LET V0$="S",V1$="",V2$=A0$(5,20),V3$="",V0=20,V1=25,V2=4,I0=0
2020 LET V4$="Enter The Description For This Line Code",O9=1
2040 GOSUB 7000
2060 LET A0$(5,20)=V$
2090 GOTO 3900
2100 REM " --- Type "
2120 LET V0$="S",V1$="RC^",V2$=A0$(25,1),V3$="SOMNP",V4$="S=Regular Sale   N=Non-Stock   O=Other   M=Memo   P=Special Distribution",V0=1,V1=25,V2=5
2130 GOSUB 7000
2135 IF POS(V$="MNOPS")=0 THEN GOTO 2100
2140 LET A0$(25,1)=V$
2150 IF POS(A0$(25,1)="O")=0 THEN LET A1$(1,G[4])=""
2160 IF POS(A0$(25,1)="M")<>0 THEN LET A0$(26,1)=""
2170 IF POS(A0$(25,1)="OM")<>0 THEN LET A0$(27,1)=""
2180 IF POS(A0$(25,1)="NOP")=0 THEN LET A0$(29,4)=""
2182 IF POS(A0$(25,1)="NPS")=0 THEN LET A1$(G[4]+1,2)=""
2190 GOTO 3900
2200 REM " --- G/L Account"
2210 LET V0$="M",V1$="RC",V2$=A1$(1,G[3]),V3$=G5$,V0=G[4],V1=25,V2=7
2215 LET V4$="Enter A Valid G/L Account (<F3>=Lookup)"
2220 IF POS(A0$(25,1)="O")=0 THEN GOTO 2300
2225 IF GL$<>"Y" THEN GOTO 2300
2230 PRINT @(0,13),'CE',
2235 GOSUB 7000
2240 IF V3=4 THEN GOTO 3900
2245 IF V3=3 THEN CALL "SYC.LG",V$
2250 IF V3=2 THEN GOTO 3900
2255 IF V$="" THEN GOTO 2200
2260 GOSUB 6600
2265 IF STATUS<>0 THEN GOTO 2200
2270 LET A1$(1,G[4])=B0$(3)
2290 GOTO 3900
2300 REM " --- No GL or account not required"
2310 PRINT @(V1,V2),'CL',
2390 GOTO 3900
2400 REM " --- Taxable "
2410 IF POS(A0$(25,1)="M")<>0 THEN GOTO 3900
2420 LET V0$="Y",V1$="CR^",V2$=A0$(26,1),V3$="YN",V4$="Y=Yes  N=No",V0=1,V1=25,V2=8
2440 GOSUB 7000
2460 LET A0$(26,1)=V$
2490 GOTO 3900
2500 REM " --- Drop Ship "
2510 IF POS(A0$(25,1)="SNP")=0 THEN GOTO 3900
2520 LET V0$="Y",V1$="CR^",V2$=A0$(27,1),V3$="YN",V4$="Y=Yes  N=No",V0=1,V1=25,V2=9
2540 GOSUB 7000
2560 LET A0$(27,1)=V$
2570 IF A0$(25,1)="S" AND A0$(27,1)="N" THEN LET A1$(11,2)=""; PRINT @(25,12),"  ",@(0,13),'CE',
2590 GOTO 3900
2600 REM  " --- Product Category "
2605 IF POS(A0$(32,1)="D")=0 THEN GOTO 3900
2610 IF POS(A0$(25,1)="NOP")=0 THEN GOTO 3900
2620 LET V0$="S",V1$="KCR",V2$=A0$(29,3),V4$="Enter The Default Product Type (<F3>=Lookup)",V0=3,V1=25,V2=11
2630 GOSUB 7000
2632 IF V3=3 THEN GOSUB 6100
2640 LET C0$(1)=N0$+"A"+V$
2660 FIND (IVM10_DEV,KEY=C0$(1,6),DOM=2600)IOL=IVM10A
2665 LET A0$(29,3)=C0$(4,3)
2670 PRINT @(V1,V2),C0$(4,3)," ",C0$(7,20),
2690 GOTO 3900
2700 REM " --- Distribution Code "
2705 IF A0$(27,1)="Y" THEN GOTO 2720
2710 IF POS(A0$(25,1)="NP")=0 THEN GOTO 2790
2720 LET V0$="S",V1$="KCR",V2$=A1$(G[4]+1,2),V3$="",V0=2,V1=25,V2=12,V4$="Enter A Valid Distribution Code (<F3>=Lookup)"
2730 GOSUB 7000
2732 IF V3=3 THEN GOSUB 6200
2740 LET D0$(1)=N0$+"D"+V$
2750 FIND (ARM10_DEV2,KEY=D0$(1,5),DOM=2700)IOL=ARM10D
2760 LET A1$(G[4]+1,2)=D0$(4,2)
2770 PRINT @(V1,V2),D0$(4,2)," ",D0$(6,20),
2780 GOSUB 5200
2790 GOTO 3900
2800 REM " --- Prod Type Processing "
2810 IF POS(A0$(25,1)="SM")<>0 THEN GOTO 3900
2820 LET V0$="S",V1$="CR^",V2$=A0$(32,1),V4$="E=Enter Type   N=No Processing   D=Use Default Type",V0=1,V1=25,V2=10,V3$="END"
2840 GOSUB 7000
2860 LET A0$(32,1)=V$
2870 IF POS(A0$(32,1)="EN")<>0 THEN LET A0$(29,3)=""; PRINT @(25,11),'CL',
2890 GOTO 3900
2900 REM " --- Message Type"
2910 IF A0$(25,1)<>"M" THEN LET A0$(28,1)=""; GOTO 2990
2920 IF A0$(28,1)=" " THEN LET A0$(28,1)="B"
2930 LET V0$="S",V1$="C^",V2$=A0$(28,1),V3$="IOBN",V4$="Where Should Messages Be Printed (I=Invoice/O=Order/B=Both/N=Neither)?",V0=1,V1=25,V2=6
2940 GOSUB 7000
2950 IF POS(V$="IOBN")=0 THEN GOTO 2900
2960 LET A0$(28,1)=V$
2990 GOTO 3900
3900 REM " --- Next Field "
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2900,2200,2400,2500,2800,2600,2700,3990
4000 REM " --- Options "
4020 LET V0$="S",V1$="KC^",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF O9=0 THEN IF V$="" THEN GOTO 1000
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write "
4120 WRITE (ARM10_DEV1,KEY=A0$(1,K9))IOL=ARM10E
4190 GOTO 1000
4200 REM " --- Delete "
4220 REMOVE (ARM10_DEV1,KEY=A0$(1,K9),DOM=4230)
4290 GOTO 1000
4500 REM " --- Print?"
4510 LET V4$="Print A List Of All Records"
4530 CALL "SYC.YN",1,V4$,1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4600,9900
4600 REM " --- Report Initializations"
4610 LET PROGRAM$="",TOTAL$="Order/Invoice Line Codes",WIDTH=132
4620 LET CHANNEL=ARM10_DEV1,NUMBER=9,HEADINGS=3,RECORD$="E"
4630 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4640 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4650 DIM HEADINGS$[HEADINGS]
4660 LET FIELD$[0]="Code",FIELD[0]=1,POSITION[0]=4
4670 LET LENGTH[0]=1,TYPE$[0]="S",MASK$[0]=""
4680 LET FIELD$[1]="Description",FIELD[1]=1,POSITION[1]=5
4690 LET LENGTH[1]=20,TYPE$[1]="S",MASK$[1]=""
4720 LET FIELD$[2]="Type",FIELD[2]=1,POSITION[2]=25
4730 LET LENGTH[2]=1,TYPE$[2]="S",MASK$[2]=""
4740 LET FIELD$[3]="Tax?",FIELD[3]=1,POSITION[3]=26
4750 LET LENGTH[3]=1,TYPE$[3]="Y",MASK$[3]=""
4760 LET FIELD$[4]="Drop Ship?",FIELD[4]=1,POSITION[4]=27
4770 LET LENGTH[4]=1,TYPE$[4]="Y",MASK$[4]=""
4780 LET FIELD$[5]="Msg Type",FIELD[5]=1,POSITION[5]=28
4790 LET LENGTH[5]=1,TYPE$[5]="S",MASK$[5]=""
4800 LET FIELD$[6]="Product Type",FIELD[6]=1,POSITION[6]=32
4810 LET LENGTH[6]=1,TYPE$[6]="S",MASK$[6]=""
4820 LET FIELD$[7]="Default Prod",FIELD[7]=1,POSITION[7]=29
4830 LET LENGTH[7]=3,TYPE$[7]="S",MASK$[7]=""
4840 LET FIELD$[8]="Distribution Code",FIELD[8]=2,POSITION[8]=G[4]+1
4850 LET LENGTH[8]=2,TYPE$[8]="S",MASK$[8]="",NUMBER=8
4860 IF GL$<>"Y" THEN GOTO 4900
4880 LET FIELD$[9]="GL Sales Acct",FIELD[9]=2,POSITION[9]=1
4890 LET LENGTH[9]=G[3],TYPE$[9]="M",MASK$[9]=G5$,NUMBER=9
4900 REM 
4980 LET HEADINGS$[0]=N1$,HEADINGS$[1]=TOTAL$+" Listing"
4990 RUN "SYR.AA"
5000 REM " --- Display "
5010 PRINT @(25,3),A0$(4,1),@(25,4),A0$(5,20),@(25,5),A0$(25,1),@(25,6),A0$(28,1),
5020 IF POS(A0$(25,1)="NSMP")>0 THEN GOTO 5040
5025 LET V1=25,V2=7,V$=A1$(1,G[4])
5030 GOSUB 6600
5040 PRINT @(25,8),A0$(26,1),@(25,9),A0$(27,1),@(25,10),A0$(32,1),@(25,11),A0$(29,3),
5050 FIND (IVM10_DEV,KEY=N0$+"A"+A0$(29,3),DOM=5070)IOL=IVM10A
5060 PRINT @(25,11),A0$(29,3)," ",C0$(7,20),
5070 PRINT @(25,12),A1$(G[4]+1,2),
5075 IF A0$(27,1)="Y" THEN GOTO 5100
5080 IF POS(A0$(25,1)="NP")<>0 THEN GOTO 5100
5090 RETURN 
5100 REM " --- Retrieve distibution code and display accounts"
5110 LET D0$(1)=N0$+"D"+A1$(G[4]+1,2)
5120 FIND (ARM10_DEV2,KEY=D0$(1,5),DOM=5290)IOL=ARM10D
5130 PRINT @(25,12),D0$(4,2)," ",D0$(6,20),
5200 REM " --- Display Account Numbers "
5205 IF GL$<>"Y" THEN GOTO 5290
5206 IF P3$(11,1)="N" AND POS(A0$(25,1)="N")<>0 THEN GOTO 5210
5208 PRINT 'SB',@(10,13),"Sales Account:",'SF',
5210 PRINT 'SB',@(6,14),"Inventory Account:",@(11,15),"COGS Account:",'SF',
5215 IF P3$(11,1)="N" AND POS(A0$(25,1)="N")<>0 THEN GOTO 5240
5220 LET V1=25,V2=13,V$=D1$(2*G[4]+1,G[4])
5230 GOSUB 6600
5240 LET V2=14,V$=D1$(6*G[4]+1,G[4])
5250 GOSUB 6600
5260 LET V2=15,V$=D1$(7*G[4]+1,G[4])
5270 GOSUB 6600
5290 RETURN 
6000 REM " --- Line Code Lookup"
6010 LET CHANNEL=ARM10_DEV2,KEYLEN=1,RECORD$="E",TITLE$="Order/Invoice Line Codes"
6090 GOTO 6500
6100 REM " --- Product Type Lookup"
6110 LET CHANNEL=IVM10_DEV,KEYLEN=3,RECORD$="A",TITLE$="Product Types"
6190 GOTO 6500
6200 REM " --- Distribution Code Lookup"
6210 LET CHANNEL=ARM10_DEV2,KEYLEN=2,RECORD$="D",TITLE$="AR Distribution Codes"
6290 GOTO 6500
6500 REM " --- Standard Code Lookup"
6510 LET NUMBER=1
6520 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6530 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6540 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=KEYLEN
6550 LET FIELD[1]=1,POSITION[1]=POSITION[0]+KEYLEN,LENGTH[1]=20
6560 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6570 LET V$="SKIP",ROW=V2+2
6580 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6590 RETURN 
6600 REM " --- Retrieve G/L account"
6605 IF GL$<>"Y" THEN GOTO 6690
6610 DIM B0$(2+G[4]),B1$(35)
6620 LET B0$(1)=N0$+V$,B1$(1)="(Account Not Found)",STATUS=1
6630 FIND (GLM01_DEV,KEY=B0$,DOM=6690)IOL=GLM01A
6640 PRINT @(V1,V2),FNF$(B0$(3,G[3]),G5$)," ",B1$,
6650 LET STATUS=0
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions "
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

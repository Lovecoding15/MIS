0010 REM "IVR - Inventory Transfer Register"
0020 REM "Program IVR.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=18
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="IVE-02",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0130 LET FILES$[4]="IVM-10",FILES$[5]="SYS-01",FILES$[6]="IVW-04"
0150 LET OPTIONS$[1]="L"
0160 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET IVE02_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0185 LET IVM10_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5]
0200 REM " --- IOLists
0210 IVE02A: IOLIST A0$,A1$,A[ALL]
0220 IVM01A: IOLIST B0$,B1$(1),B2$,B3$,B4$,B5$,B6$(1),B[ALL]
0230 IVM10C: IOLIST X1$(1)
0240 IVM02A: IOLIST D0$,D1$(1)
0250 ARM10D: IOLIST *,Z9$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0285 ARS01A: IOLIST X$,X$,X$,A9$
0400 REM " --- Parameters"
0405 DIM P[5],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N3$=F5$,SOURCE$=PGM(-2),WHEN$=F0$(7,3)
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 LET G[0]=NUM(G2$(1,2)),G[1]=NUM(G2$(3,2)),G[2]=NUM(G2$(5,2))
0440 LET G[3]=NUM(G2$(7,2)),G[4]=NUM(G2$(9,2))
0445 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(7,2)),P[2]=NUM(P2$(5,1))
0450 LET P[3]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2)),P[5]=NUM(P2$(13,2))
0455 LET LF$="N",LS$="N",AR$="N",A9$="N"
0460 IF POS(P3$(3,1)="LF")>0 THEN LET LF$="Y"
0465 IF POS(P3$(17,1)="LS")>0 THEN LET LS$="Y"
0470 CALL "SYC.VA","AR",INFO$[ALL]
0475 LET AR$=INFO$[20]
0480 IF AR$<>"Y" THEN GOTO 0500
0485 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0490 LET A9$=A9$(11,1)
0495 LET PLACES=P[2]
0500 REM " --- Initializations"
0510 PRECISION P[2]
0520 DIM A[2],B[12],J$(80,"-"),ITEM$[10],HEADINGS$[1],O[4],LF[10]
0550 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),M0=P[0],P8$="Lot/Serial Number"
0555 LET WIDTH=132,PAGE=0,L9=55,L=L9+1,W=34,UNITS=0,T0$="",T1$=""
0560 LET HEADINGS=1,HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,H3$="",T9$=""
0570 LET O[4]=WIDTH-9,O[3]=O[4]-M1-1,O[2]=O[3]-M2
0575 LET O[1]=O[2]-M3,O[0]=O[1]-3,ERRORS=0,GLSTATUS=0
0600 REM " --- Additional File Opens"
0610 LET GL$="N",STATUS=0
0615 IF A9$="Y" THEN CALL "GLC.BA",ERR=0620,SOURCE$,N2$,GLW11$,GL$,STATUS
0620 IF STATUS<>0 THEN GOTO 9900
0625 IF LF$="Y" THEN LET FILES$[6]="IVM-04",FILES$[7]="IVW-04",OPTIONS$[7]="CL"
0630 IF GL$="Y" THEN LET FILES$[8]="GLM-01",FILES$[9]=GLW11$,OPTIONS$[9]="CL"
0635 IF GL$="Y" THEN IF A9$="Y" THEN LET FILES$[10]="ARM-10"
0640 CALL "SYC.DA",1,6,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0645 IF STATUS>0 THEN GOTO 9900
0650 LET IVM04_DEV=CHANNELS[6],IVW04_DEV=CHANNELS[7],GLM01_DEV=CHANNELS[8]
0655 LET GLW11_DEV=CHANNELS[9],ARM10_DEV=CHANNELS[10]
0660 LET LF[0]=IVM04_DEV,LF[1]=IVW04_DEV,LF[2]=IVM02_DEV
0800 REM " --- Run Report?
0810 LET V4$="Do You Want To Print The "+N3$
0820 CALL "SYC.YN",1,V4$,2,V$,V3
0830 IF V3=4 THEN GOTO 9900
0840 IF V$<>"Y" THEN GOTO 9900
0900 REM " --- File Initializations"
0910 CALL "SYC.GA",7,1,"","",STATUS
0920 IF STATUS THEN GOTO 9900
0930 CALL "SYC.NB","Printing",P[0]+3,COLUMN
1000 REM " --- Initial Read"
1020 READ (IVE02_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Heading Read"
1120 READ (IVE02_DEV,END=4000)IOL=IVE02A
1140 LET COST=A[2]
1200 REM " --- Breaks"
1220 IF POS(N0$=A0$)<>1 THEN GOTO 4000
1240 IF A0$(3,2)<>T0$ THEN GOSUB 6000
1260 IF A0$(5,3)<>T1$ THEN GOSUB 6200
1280 PRINT @(COLUMN,11),A0$(3,2)," ",A0$(12,P[0]),
1300 GOSUB 6400
1500 REM " --- LIFO/FIFO?"
1510 IF LF$="N" THEN GOTO 2000
1525 LET ITEM$[0]=N0$,ITEM$[1]=A0$(3,2),ITEM$[2]=A0$(12,20),LF_DATE$=A0$(5,3)
1530 LET ACTION$=P3$(3,1)+"I",QUANTITY=A[0],COST=A[2],RET_COST=0,TYPE$="R"
1560 CALL "IVC.LF",ACTION$,TYPE$,LF_DATE$,LF[ALL],ITEM$[ALL],PLACES,QUANTITY,COST,RET_COST,STATUS
1600 REM " --- Cost change?"
1610 IF A[1]=A[0]*RET_COST THEN GOTO 2000
1620 IF RET_COST<>0 THEN LET A[1]=A[0]*RET_COST,A[2]=RET_COST,COST=RET_COST
1690 WRITE (IVE02_DEV,KEY=A0$)IOL=IVE02A
2000 REM " --- Print"
2020 IF L+2>L9 THEN GOSUB 5000
2025 PRECISION 2
2030 LET TOT_COST=A[1]*1
2035 PRECISION P[2]
2060 PRINT (7)@(3),A0$(10,2)," ",X1$,@(27),A0$(12,P[0])," ",A0$(32,P[1]),@(O[0]),B2$(4,2),@(O[1]),COST:M2$,@(O[2]),A[0]:M2$,@(O[3]),TOT_COST:M1$,@(O[4]),A1$(1,6)
2070 PRINT (7)@(27),B1$
2080 LET L=L+2,T0=T0+TOT_COST,U0=U0+TOT_COST
2085 IF GL$="Y" THEN CALL "GLC.EA",A0$(5,3),"N",PERIOD$,YEAR$,GLSTATUS
2090 IF GLSTATUS>0 THEN CALL "GLC.GA",7,A0$(5,3),GLSTATUS,L,ERRORS,STATUS
3000 REM " --- Generate G/L Recap"
3010 IF GL$<>"Y" THEN GOTO 3900
3020 IF A9$<>"Y" THEN GOTO 3900
3100 REM " --- From Warehouse"
3105 PRECISION 2
3110 DIM Z9$(8*G[4],"0"),D1$(40)
3120 LET GLDATE$=A0$(5,3),REF1$="Xfr "+A1$(1,6),MEMO$=A0$(12,P[0])+" "+B1$
3130 LET REF2$="Fr Whse "+A0$(3,2),REF3$="To Whse "+A0$(10,2)
3140 FIND (IVM02_DEV,KEY=N0$+A0$(3,2)+A0$(12,20),DOM=3200)IOL=IVM02A
3150 FIND (ARM10_DEV,KEY=N0$+"D"+D1$(13,2),DOM=3200)IOL=ARM10D
3160 LET ACCOUNT$=Z9$(6*G[4]+1,G[4]),AMOUNT=(-A[0])*COST,UNITS=0
3170 GOSUB GLRECAP
3200 REM " --- To Warehouse"
3210 DIM Z9$(8*G[4],"0")
3220 LET X$=REF2$,REF2$=REF3$,REF3$=X$
3240 FIND (IVM02_DEV,KEY=N0$+A0$(10,2)+A0$(12,20),DOM=3290)IOL=IVM02A
3250 FIND (ARM10_DEV,KEY=N0$+"D"+D1$(13,2),DOM=3290)IOL=ARM10D
3260 LET ACCOUNT$=Z9$(6*G[4]+1,G[4]),AMOUNT=A[0]*COST,UNITS=0
3270 GOSUB GLRECAP
3290 PRECISION P[2]
3900 REM 
3990 GOTO 1100
4000 REM " --- Totals"
4010 IF L>L9 THEN GOSUB 5000
4020 LET T9$="E",UPDATE$="IVU.DA"
4030 GOSUB 6000
4080 IF GL$="Y" AND A9$="Y" THEN RUN "GLR.XA"
4090 RUN UPDATE$
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 IF H3$<>"" THEN PRINT (7)@(FNC(H3$,WIDTH)),H3$; LET L=L+1
5050 IF T1$<>"" THEN PRINT (7)"Transfer Date ",FNB$(T1$); LET L=L+1
5060 PRINT (7)""
5070 PRINT (7)@(27),"Item",@(28+P[0]),P8$
5080 PRINT (7)"   To Warehouse",@(27),"No  / Description",@(O[0]),"UM",@(O[1]+M3-5),"Cost",@(O[2]+M2-4),"Qty",@(O[3]+M1-6),"Total",@(O[4]),"Reference"
5085 PRINT (7)""
5087 LET L=L+4
5090 RETURN 
5200 REM " --- Date Heading"
5210 IF L+2>L9 THEN GOSUB 5000
5220 PRINT (7)@(0),"Transfer Date: ",FNB$(T1$),'LF'
5240 LET L=L+2
5290 RETURN 
6000 REM " --- Warehouse"
6020 IF T0$="" THEN GOTO 6100
6030 IF T9$="" THEN LET T9$="A"
6040 GOSUB 6200
6050 IF T9$="A" THEN LET T9$=""
6055 IF L+2>L9 THEN GOSUB 5000
6060 PRINT (7)@(O[3]),J$(1,M1)
6070 PRINT (7)@(O[3]-23),"Total For Warehouse ",T0$,@(O[3]),T0:M1$
6080 LET L=L9+1,T0=0
6100 IF T9$<>"" THEN RETURN 
6110 DIM X1$(30)
6120 LET T0$=A0$(3,2),T1$=A0$(5,3),X1$(4)=A0$(3,2)
6125 LET X1$(6)="Unknown Warehouse",U0=0
6130 FIND (IVM10_DEV,KEY=N0$+"C"+A0$(3,2),DOM=6140)IOL=IVM10C
6140 LET H3$="From Warehouse "+FNP$(X1$(4,2)+" "+X1$(6,24))
6190 RETURN 
6200 REM " --- Date"
6220 IF T1$="" THEN GOTO 6300
6230 IF L+2>L9 THEN GOSUB 5000
6240 PRINT (7)@(O[3]),J$(1,M1)
6250 PRINT (7)@(O[3]-18),"Total For ",FNB$(T1$),@(O[3]),U0:M1$
6260 LET L=L+2
6300 IF T9$<>"" THEN RETURN 
6320 LET U0=0,T1$=A0$(5,3),L=L9+1
6390 RETURN 
6400 REM " --- Item"
6420 DIM B1$(60),B2$(20),B6$(G[4]),X1$(30)
6430 LET B1$(1)="Unknown Item",X1$(6)="Unknown Warehouse"
6440 FIND (IVM01_DEV,KEY=N0$+A0$(12,20),DOM=6460)IOL=IVM01A
6460 FIND (IVM10_DEV,KEY=N0$+"C"+A0$(10,2),DOM=6470)IOL=IVM10C
6470 LET X1$=X1$(6,24),B1$=FNITEM$(B1$,P[3],P[4],P[5])
6490 RETURN 
6900 REM " --- Standard G/L Recap Routine"
6910 GLRECAP: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLW11_DEV,GLT05_DEV,ACCOUNT$,GLDATE$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

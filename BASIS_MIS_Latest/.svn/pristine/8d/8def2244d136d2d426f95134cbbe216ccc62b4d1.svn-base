0010 REM "WOS - Work Order Entry - Transaction Display "
0020 REM "Program:  WOE.AT"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "13Jan97 JJD: Don't display Subcont. record sequence#. (5440)"
0040 REM 
0055 REM " L9=Max Lines, L1=First Line, L0=Header Line, L=Current Line
0060 REM " IOLIST WOTRAN is for WOT-01/21/31 (Open WOs) or WOT-03/23/33 (Closed)
0065 REM " IOLIST LOTSER is for WOT-11 (Open WOs) or WOT-12 (Closed)
0085 SETESC 9000
0090 SETERR 9000
0095 PRECISION IVS01_PRECISION
0100 REM " --- Open/Lock Files
0110 LET FILES=1
0120 DIM FILES$[FILES]
0130 LET FILES$[1]="APM-01",OPTIONS$[1]="F"
0140 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET APM01_DEV=CHANNELS[1]
0200 REM " --- IOLISTs
0210 WOTRAN: IOLIST W0$,W1$,W[ALL]
0220 LOTSER: IOLIST H0$,H1$,H2$,H[ALL]
0230 APM01A: IOLIST *,X0$(1)
0240 IVM01A: IOLIST *,X1$(1),X2$
0250 EMPCODE: IOLIST *,X1$(1)
0260 OPCODE: IOLIST X0$
0500 REM " --- Inititialize Data "
0510 IF A0$(15,1)="C" AND A0$(31,3)<=G9$ THEN LET WO_STATUS$="0"
0520 IF A0$(15,1)<>"C" OR A0$(31,3)>G9$ THEN LET WO_STATUS$="1"
0530 IF WO_STATUS$="0" THEN LET LOTSER_DEV=WOT12_DEV
0540 IF WO_STATUS$="1" THEN LET LOTSER_DEV=WOT11_DEV
0545 LET L0=11,L1=12,L9=19,L=L1,IO=0
0550 LET M9=M1+7
0560 IF M9*4>77 THEN LET M9=M9-1; GOTO 0560
0570 LET X9$="",X8$="",X7$="",T0=0,T1=0,T2=0,T3=0
0700 REM " --- Background"
0710 PRINT @(0,L0+1),'CE'
0720 PRINT 'SB',@(2,L0-1),DESCRIPTION$,@(0,L0),"Date",@(9),"Src",@(13),"Item/Op-Employee/Vend-Inv",@(77-M1-M3-6),"Units",@(77-M1-5),"Rate",@(77-5),"Cost"
0730 PRINT @(17,L+1),"Enter Beginning Date: ",@(20,L+2),"Enter Ending Date:",@(15,L+4),"Enter Source Selection: "
0740 PRINT 'SF'
1000 REM " --- Input Options"
1020 LET V0$="D",V1$="C",V2$=X9$,V3$="",V4$="Enter Date To Start Displaying, <F1>=First",V0=3,V1=40,V2=L+1,I0=0
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 4500
1060 IF V$="" OR V3=1 THEN PRINT @(V1,V2),"First",'CL'; GOTO 1090
1080 LET X9$=V$
1090 GOTO 3900
1200 REM " --- Ending Date
1220 LET V0$="D",V1$="C",V2$=X8$,V3$="",V4$="Enter Date To Stop Displaying, <F1>=Last",V0=3,V1=40,V2=L+2
1240 GOSUB 7000
1250 IF V3=4 THEN GOTO 4500
1260 IF V$="" OR V3=1 THEN PRINT @(V1,V2),"Last",'CL'; GOTO 1290
1270 IF X9$<>"" AND V$<X9$ THEN GOTO 1200
1280 LET X8$=V$
1290 GOTO 3900
1400 REM " --- Source Selections
1420 LET V0$="S",V1$="C^",V2$=X7$,V3$="",V4$="O=Operations, M=Materials, S=Subcontracts, Any Combination, <F1>=All",V0=3,V1=40,V2=L+4
1440 GOSUB 7000
1450 IF V3=4 THEN GOTO 4500
1460 IF V$="" OR V3=1 THEN LET X7$=""; PRINT @(V1,V2),"All Sources "; GOTO 1590
1470 FOR X=1 TO LEN(V$)
1480 IF POS(V$(X,1)="MOS")=0 THEN EXITTO 1400
1490 NEXT X
1500 LET X7$=V$
1510 PRINT @(V1,V2),X7$,'CL',
1590 GOTO 3900
2000 REM " --- Go Display Them..
2010 DIM W[11]
2020 GOSUB 5900
2050 READ (WOM07_DEV,KEY=A0$(1,11),DOM=2060)
2060 LET K7$=KEY(WOM07_DEV,END=3020)
2070 READ (WOM07_DEV,KEY=K7$)
2080 IF K7$(1,2)<>N0$ THEN GOTO 3020
2100 IF K7$(1,11)<>A0$(1,11) THEN GOTO 3020
2110 IF X9$<>"" THEN IF K7$(12,3)<X9$ THEN GOTO 2060
2120 IF X8$<>"" THEN IF K7$(12,3)>X8$ THEN GOTO 3020
2125 GOSUB READ_TRANSACTIONS
2130 IF NOT_FOUND THEN GOTO 2060
2150 IF X7$<>"" THEN IF POS(W0$(15,1)=X7$)=0 THEN GOTO 2280
2160 IF L<=L9 THEN GOTO 2240
2180 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="(<Enter> To Continue/<F4>=End)",V0=1,V1=FNV(V4$),V2=22
2190 GOSUB 7000
2200 IF V3=4 THEN GOTO 3050
2220 GOSUB 5900
2240 GOSUB 5000
2260 LET L=L+2,K9$=W0$(1,18)
2280 IF W0$(15,1)="O" THEN LET T0=T0+W[0],T1=T1+W[2]
2300 IF W0$(15,1)="M" THEN LET T2=T2+W[2]; GOTO 2400
2320 IF W0$(15,1)="S" THEN LET T3=T3+W[2]
2390 GOTO 2900
2400 REM " --- Serial # Check
2410 IF (X7$<>"" AND POS("M"=X7$)=0) OR X2$(19,2)<>"YY" THEN GOTO 2900
2415 IF P3$(17,1)="S" THEN LET X9$="Serial: " ELSE IF P3$(17,1)="L" THEN LET X9$="Lot #:" ELSE GOTO 2900
2420 DIM H[2]
2430 READ (LOTSER_DEV,KEY=W0$,DOM=2440)
2440 LET K3$=KEY(LOTSER_DEV,END=2900)
2450 IF POS(W0$=K3$)<>1 THEN GOTO 2900
2460 READ (LOTSER_DEV,KEY=K3$)IOL=LOTSER
2470 IF L<=L9 THEN GOTO 2580
2480 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="(<Enter> To Continue/<F4>=End)",V0=1,V1=FNV(V4$),V2=22
2485 GOSUB 7000
2490 IF V3=4 THEN GOTO 3050
2500 GOSUB 5900
2510 GOSUB 5000
2520 LET L=L+2,K9$=W0$(1,18)
2530 PRINT @(0,L-1),"Cont'd",
2580 GOSUB 5600
2590 GOTO 2440
2900 GOTO 2060
3020 IF L<=L9 THEN GOTO 3100
3030 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="(<Enter> To Continue/<F4>=End)",V0=1,V1=FNV(V4$),V2=22
3035 GOSUB 7000
3040 IF V3=4 THEN GOTO 3900
3050 GOSUB 5900
3100 REM " --- Now Print Totals
3130 PRINT @(77-M9*4,L),"WO Totals: ",@(77-M1-M2-M3-7),"Hours: ",@(77-M1-M2-M3),T0:M2$,@(77-M9),"Total: ",@(77-M1),T1+T2+T3:M1$
3135 IF L+1<20 THEN LET L=L+1
3140 PRINT @(77-M1*3-17,L+1),"Ops: ",T1:M1$,@(77-M1*2-12),"Mats: ",T2:M1$,@(77-M1-6),"Subs: ",T3:M1$
3200 LET V0$="S",V1$="C",V2$="",V3$="",V4$="(<Enter> To End/<F2>=Restart)",V0=1,V1=FNV(V4$),V2=22
3220 GOSUB 7000
3240 IF V3=2 THEN GOTO 0540
3260 GOTO 4500
3500 REM " --- Read Transactions "
3505 READ_TRANSACTIONS: 
3510 LET NOT_FOUND=0
3515 ON POS(WO_STATUS$="01") GOTO 3570,WO_CLOSED,WO_OPEN
3520 WO_CLOSED: 
3530 IF K7$(15,1)="O" THEN LET WOTRAN_DEV=WOT03_DEV
3540 IF K7$(15,1)="M" THEN LET WOTRAN_DEV=WOT23_DEV
3550 IF K7$(15,1)="S" THEN LET WOTRAN_DEV=WOT33_DEV
3560 FIND (WOTRAN_DEV,KEY=K7$,DOM=3660)IOL=WOTRAN
3570 GOTO 3670
3600 WO_OPEN: 
3610 IF K7$(15,1)="O" THEN LET WOTRAN_DEV=WOT01_DEV
3620 IF K7$(15,1)="M" THEN LET WOTRAN_DEV=WOT21_DEV
3630 IF K7$(15,1)="S" THEN LET WOTRAN_DEV=WOT31_DEV
3640 FIND (WOTRAN_DEV,KEY=K7$,DOM=3660)IOL=WOTRAN
3650 GOTO 3670
3660 LET NOT_FOUND=1
3670 RETURN 
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,4000
4000 REM " --- Overlay"
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct? (Y/N): ",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 4500
4050 IF V$="" THEN LET V$="Y"
4060 IF V$="N" THEN GOTO 1000
4080 IF V$<>"Y" THEN GOTO 4000
4100 GOTO 2000
4500 LET O9=2
4510 CLOSE (APM01_DEV,ERR=4540); LET APM01_DEV=0
4540 RUN "WOE.AA"
5000 REM " --- Display"
5010 IF W0$(15,1)="O" THEN LET W[0]=W[0]+W[6]
5020 PRINT @(0,L),FNB$(W0$(12,3)),"  ",W0$(15,1),@(77-M1-M2-M3),W[0]:M2$,W[1]:M3$,W[2]:M1$
5040 ON POS(W0$(15,1)="OMS") GOTO 5500,5060,5200,5400
5060 DIM X0$(20),X1$(30); LET X0$(1)="*** Not On File ***",X1$(1)=X0$
5070 FIND (OPCODE_DEV,KEY=N0$+W1$(4,3),DOM=5080)IOL=OPCODE
5075 LET X0$=X0$(6,20)
5080 FIND (EMPCODE_DEV,KEY=N0$+W1$(7,9),DOM=5100)IOL=EMPCODE
5085 LET X1$(1)=FNEMP$(X1$)
5100 PRINT @(13,L),W1$(1,3)," ",W1$(4,3),"  ",X0$
5120 PRINT @(13,L+1),FNF$(W1$(7,P6),P6$),"  ",X1$,@(54-M2)," Cmp Qty: ",W[5]:M2$," Setup: ",W[6]:"##0.00-",
5160 GOTO 5500
5200 REM " --- Materials
5210 DIM X1$(DESCLEN),X2$(40); LET X1$(1)="***Not On File***"
5220 FIND (IVM01_DEV,KEY=N0$+W1$(24,20),DOM=5260)IOL=IVM01A
5240 LET X1$(1)=FNITEM$(X1$,DESC[1],DESC[2],DESC[3])
5250 IF LEN(X1$)>30 THEN LET X1$=X1$(1,30)
5260 PRINT @(13,L),W1$(1,3),"  ",W1$(24),"  ",W1$(20,2)
5265 IF W1$(10,10)<>"          " THEN LET X1$=X1$+" P.O. #: "+W1$(10,7)+"-"+W1$(17,3)
5270 PRINT @(13,L+1),X1$
5290 GOTO 5500
5400 REM " --- Subcontracts
5410 DIM X0$(59-M1-M2-M3-P7); LET X0$(1)="*** Not On File ***"
5420 FIND (APM01_DEV,KEY=N0$+W1$(4,6),DOM=5440)IOL=APM01A
5440 PRINT @(13,L),FNF$(W1$(4,P7),P5$),"  ",X0$
5450 PRINT @(13,L+1),"P.O. #: ",W1$(10,7)+"-"+W1$(17,3),"  ",W1$(20)
5500 RETURN 
5600 REM " --- Display Serial/Lots Here
5620 PRINT @(10,L),X9$," ",H2$,@(77-M1-M2-M3),H[0]:M2$,H[1]:M3$,H[2]:M1$
5640 LET L=L+1
5690 RETURN 
5900 REM " --- Clear Screen"
5920 PRINT @(0,L1),'CE',
5940 LET L=L1
5960 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8080 DEF FNEMP$(Q$)=Q$(17,POS(" "<>Q$(17,14),-1))+", "+Q$(1,16)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "BMM - OP Code Maintenance Listing
0020 REM "Program  BMM.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0095 PRECISION NUM(P6$(5,1))
0100 REM " --- Open/Lock Files
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLists
0210 BMM08A: IOLIST A0$(1),A[ALL]
0500 REM " --- Init Data
0510 LET H0=132,H=0,H1$=N4$+" Listing"
0520 LET L9=59,L=L9+1,T0=0,HEADINGS=2
0530 DIM HEADINGS$[HEADINGS]
0540 LET WHEN$=F0$(7,3),CLOCK$=""
0550 LET HEADINGS$[0]=N1$,HEADINGS$[1]=H1$
1000 REM " --- Initial Read
1020 READ (BMM08_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Main Read
1120 READ (BMM08_DEV,END=4000)IOL=BMM08A
1200 REM " --- Breaks
1220 IF A0$(1,2)<>N0$ THEN GOTO 4000
2000 REM " --- Print"
2020 IF L+1>L9 THEN GOSUB 5000
2040 PRINT (7)@(0),A0$(3,3),"  ",A0$(6,20),
2050 IF GL$="Y" THEN PRINT (7)@(46),FNF$(A0$(31,G0),G5$),@(61),FNF$(A0$(31+G1,G0),G5$),
2060 IF WO$="Y" THEN PRINT (7)@(79+P1+P2+P3+2*M6-4),A[3]:M6$,@(80+P1+P2+P3+3*M6-4),A[5]:M6$,
2070 PRINT (7)@(73),A[0]:P1$,@(75+P1),A[1]:P2$,@(77+P1+P2),A[2]:P3$,@(78+P1+P2+P3+M6-4),A[4]:M6$
2200 LET L=L+1,T0=T0+1
2290 GOTO 1100
4000 REM " --- Options
4020 PRINT (7)'LF',@(10),"Total Records:",T0
4100 GOTO 9900
5000 REM " --- Heading
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)@(0),"Operation",
5050 IF GL$="Y" THEN PRINT (7)@(44),"Direct Labor",@(58),"Overhead Labor",
5060 IF WO$="Y" THEN PRINT (7)@(79+P1+P2+P3+2*M6-4),"Queue",@(80+P1+P2+P3+3*M6-3),"Move",
5070 PRINT (7)@(76),"Labor",@(75+P1),"Pieces",@(77+P1+P2),"Overhead",@(78+P1+P2+P3+M6-4),"Setup"
5080 PRINT (7)@(0),"Code",@(5),"Description",
5090 IF GL$="Y" THEN PRINT (7)@(47),"Account",@(61),"Account",
5100 IF WO$="Y" THEN PRINT (7)@(79+P1+P2+P3+2*M6-3),"Time",@(80+P1+P2+P3+3*M6-3),"Time",
5110 PRINT (7)@(76),"Rate",@(75+P1),"/Hour",@(77+P1+P2),"Factor",@(78+P1+P2+P3+M6-4)," Time",'LF'
5180 LET L=L+3
5190 RETURN 
8000 REM " --- Functions
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

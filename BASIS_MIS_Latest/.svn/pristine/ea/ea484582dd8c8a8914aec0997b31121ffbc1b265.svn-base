0010 REM "DDC - Dictionary Lookup"
0020 REM "Program DDC.LA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "DDM01_DEV : DDM-01 channel number (input)"
0050 REM "WIN_Y     : Screen row for lookup (input)"
0055 REM "SELECTION$: User selection (returned)"
0060 REM 
0080 SETERR 9000
0085 SETESC 9900
0090 ENTER DDM01_DEV,WIN_Y,SELECTION$
0200 REM " --- IOLists
0210 DDM01A: IOLIST A0$(1),A1$(1),A[ALL]
0400 REM " --- Parameters
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0500 REM " --- Initializations
0510 DIM A0$(12),A1$(105),A[2],TYPES$[9]
0540 LET WIDTH=60,HEIGHT=12,WIN_X=11,MAX_ROW=HEIGHT-5,OTITLE$="Method"
0550 IF WIN_Y+HEIGHT>22 THEN LET WIN_Y=23-HEIGHT
0555 IF WIN_Y<1 THEN LET WIN_Y=1
0560 LET PGM$=PGM(-2),OPTIONS=0,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3
0570 LET TYPES$[1]="Str",TYPES$[2]="Zero",TYPES$[3]="Y/N"
0575 LET TYPES$[4]="Hyph",TYPES$[5]="Mask",TYPES$[6]="Time"
0580 LET TYPES$[7]="Date",TYPES$[8]="Numb",TYPES$[9]="Asc"
0650 REM " --- Window arrays
0670 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0680 LET HEADING$(2)="Data Name",HEADING$(15)="Description"
0685 LET HEADING$(46)="Type",HEADING$(52)="Length"
0690 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0695 LET TITLE$="Data Element Lookup"
0700 REM " --- Background
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Which search option?"
0810 DIM STRINGS$[10],NUMBERS[10]
0820 LET STRINGS$[0]=PGM$,NUMBERS[0]=MAXROW
0830 LET NUMBERS[1]=OWIN_X,NUMBERS[2]=OWIN_Y
0850 CALL "SYC.SM",STRINGS$[ALL],NUMBERS[ALL],FKEY
0860 IF FKEY THEN GOTO 9900
0870 LET PREFIX$=STRINGS$[2],START$=STRINGS$[3],METHOD$=STRINGS$[4]
0880 LET V3$=STRINGS$[5],V0=NUMBERS[4]
0900 REM " --- Detail Window
0915 CALL "SYC.WB",NAME$,0
0925 PRINT @(0,0),'CF','SB','BR',HEADING$,'ER','SF',
1000 REM " --- Position file
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,1]
1020 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$=""
1040 IF PREFIX$="*" THEN GOTO 1300
1200 REM " --- Data Name
1210 READ (DDM01_DEV,KEY=START$,DOM=1400)IOL=DDM01A
1220 LET K$=A0$
1290 GOTO 1900
1300 REM " --- Keyword
1310 LET START$=FNU$(START$)
1320 READ (DDM01_DEV,KEY="",DOM=1400)
1400 REM " --- Get next key
1810 LET K$=KEY(DDM01_DEV,END=3000)
1900 REM " --- Read record
1905 LET LOCKED=0
1910 READ (DDM01_DEV,KEY=K$,ERR=1950)IOL=DDM01A
1920 IF PREFIX$<>"*" THEN GOTO 2000
1923 IF START$="" THEN GOTO 2000
1925 LET X=POS(START$=FNU$(A0$))+POS(START$=FNU$(A1$))
1930 IF X=0 THEN GOTO 2900
1940 GOTO 2000
1950 REM " --- Error reading record
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1980 LET LOCKED=1
2000 REM " --- Display page?
2010 IF L<MAX_ROW THEN GOTO 2400
2020 LET MODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 0800
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2400 REM " --- Lookup window record
2410 DIM G1$(WIDTH-2)
2413 LET G1$(2)=K$
2415 IF LOCKED=1 THEN LET G1$(19)="Record In Use"; GOTO 2470
2430 LET G1$(15)=A1$(1,30),G1$(46)=TYPES$[POS(A1$(37,1)="SZYHMTDNA")]
2440 LET G1$(52)=STR(A[0]:"##,##0")
2490 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$
2690 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record
2910 IF LOCKED=0 THEN GOTO 1400
2920 LET LOCKED=0
2930 IF PREFIX$<>"" AND PREFIX$<>"*" THEN GOTO 1400
2940 LET K$=K$(1,LEN(K$)-1)+CHR(ASC(K$(LEN(K$),1))+1)
2950 READ (DDM01_DEV,KEY=K$,DOM=1400,END=3000)IOL=DDM01A
2990 GOTO 1820
3000 REM " --- End of file
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3035 LET MESSAGE$[0]="No Data Elements Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page
3510 LET MODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
4000 REM " --- All done
4090 GOTO 9900
6200 REM " --- Display page
6210 CALL "SYC.SA",MODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9935 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
9940 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9945 IF SELECTION$="END" THEN LET SELECTION$=""
9950 EXIT 
9999 END

0010 REM "SHX - UPS Generate Sample Manifest Data"
0020 REM "Program SHXAMA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Feb97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Feb97 CAJ: Modified for 1997 SpeedShip Update"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 SHE01A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0220 SHE02A: IOLIST W0$,W1$(1),W[ALL]
0230 SHM22A: IOLIST C0$,C[ALL]
0240 SHM24A: IOLIST X0$,X[ALL]
0250 SHM10A: IOLIST S0$(1),S1$(1),I1
0255 SHM10G: IOLIST G0$(1),G1$(1)
0260 SHM10C: IOLIST R0$(1),R[ALL]
0500 REM " --- Initializations"
0510 DIM C[36],J0$(20,"0"),S0$(10),S1$(128)
0540 LET COD=10000000,COD=NUM(F0$(18,6),ERR=0550)*1000
0550 LET ZONES=85,CODAMOUNT=100.01,VALUE=100.01,N=0,TRACKING=COD
0580 LET DW$="N",AH$="Y",PREFIX$=N0$+P8$+"A",LASTFILE$="",SHM24_DEV=0
0585 LET PICKUP$=FNP$(I8$(37,10)),PICKUP$=PICKUP$+"-"+J0$(11-LEN(PICKUP$))
0590 LET PICKUP$(LEN(PICKUP$),1)="X",OVERSIZE$="N",COD$="Y"
0600 REM " --- Zone Initializations"
0605 DIM Z[ZONES],Y[ZONES]
0610 LET Z[1]=2,Z[2]=3,Z[3]=4,Z[4]=5,Z[5]=6,Z[6]=7,Z[7]=8
0615 IF P9$="B" THEN GOTO 0950
0620 LET Z[8]=102,Z[9]=103,Z[10]=104,Z[11]=105,Z[12]=106,Z[13]=107
0623 LET Z[14]=108,Z[15]=124,Z[16]=125,Z[17]=126,Z[18]=132,Z[19]=133
0625 LET Z[20]=134,Z[21]=135,Z[22]=136,Z[23]=137,Z[24]=138,Z[25]=142
0627 LET Z[26]=143,Z[27]=144,Z[28]=145,Z[29]=146,Z[30]=147,Z[31]=148
0630 LET Z[32]=202,Z[33]=203,Z[34]=204,Z[35]=205,Z[36]=206,Z[37]=207
0633 LET Z[38]=208,Z[39]=224,Z[40]=225,Z[41]=226,Z[42]=242,Z[43]=243
0635 LET Z[44]=244,Z[45]=245,Z[46]=246,Z[47]=247,Z[48]=248,Z[49]=302
0637 LET Z[50]=303,Z[51]=304,Z[52]=305,Z[53]=306,Z[54]=307,Z[55]=308
0640 LET Z[56]=51,Z[57]=52,Z[58]=53,Z[59]=54,Z[60]=55,Z[61]=56
0645 LET Z[62]=81,Z[63]=82,Z[64]=71,Z[65]=72
0650 REM " --- Define which numeric element to use for each zone"
0653 LET Y[1]=0,Y[2]=0,Y[3]=0,Y[4]=0,Y[5]=0,Y[6]=0,Y[7]=0
0655 LET Y[8]=1,Y[9]=1,Y[10]=1,Y[11]=1,Y[12]=1,Y[13]=1
0657 LET Y[14]=1,Y[15]=1,Y[16]=1,Y[17]=1,Y[18]=0,Y[19]=0
0660 LET Y[20]=0,Y[21]=0,Y[22]=0,Y[23]=0,Y[24]=0,Y[25]=0
0663 LET Y[26]=0,Y[27]=0,Y[28]=0,Y[29]=0,Y[30]=0,Y[31]=0
0665 LET Y[32]=2,Y[33]=2,Y[34]=2,Y[35]=2,Y[36]=2,Y[37]=2
0667 LET Y[38]=2,Y[39]=2,Y[40]=2,Y[41]=2,Y[42]=2,Y[43]=1
0670 LET Y[44]=1,Y[45]=1,Y[46]=1,Y[47]=1,Y[48]=1,Y[49]=3
0673 LET Y[50]=3,Y[51]=3,Y[52]=3,Y[53]=3,Y[54]=3,Y[55]=3
0675 LET Y[56]=0,Y[57]=0,Y[58]=0,Y[59]=0,Y[60]=0,Y[61]=0
0680 LET Y[62]=1,Y[63]=1,Y[64]=2,Y[65]=2
0800 REM " --- International zones"
0810 LET Z[66]=80,Z[67]=84,Z[68]=900,Z[79]=901,Z[70]=902
0820 LET Z[71]=903,Z[72]=904,Z[73]=905,Z[74]=906,Z[75]=907
0830 LET Z[76]=908,Z[77]=84,Z[78]=601,Z[79]=602,Z[80]=603
0835 IF STATUS THEN GOTO 0950
0840 LET Z[81]=604,Z[82]=605,Z[83]=606,Z[84]=607,Z[85]=608
0850 REM " --- Numeric elements for international zones"
0860 LET Y[66]=0,Y[67]=0,Y[68]=0,Y[69]=0,Y[70]=0,Y[71]=0,Y[72]=0
0870 LET Y[73]=0,Y[74]=0,Y[75]=0,Y[76]=0,Y[77]=1,Y[78]=1,Y[79]=1
0880 LET Y[80]=1,Y[81]=1,Y[82]=1,Y[83]=1,Y[84]=1,Y[85]=1
1000 REM " --- Ground Zones (2 through 8)"
1005 LET Z$=""
1010 PRINT @(COLUMN,11),FILL(11),
1020 FOR X=2 TO 8
1025 IF P9$="A" THEN LET Z$=Z$+"/UPS GCDUPS GRDUPS CT "
1030 IF P9$="B" THEN LET Z$=Z$+"/UPS GSR"
1035 NEXT X
1040 LET Z$=Z$(2)
1045 IF P9$="B" THEN GOTO 1900
1100 REM " --- Next Day Air Zones (102 through 108, 124, 125 and 126)"
1110 FOR X=1 TO 10
1120 LET Z$=Z$+"/UPS NDA"
1130 IF N=0 THEN LET Z$=Z$+"UPS NDL"
1140 NEXT X
1150 REM " --- Next Day Air Saver Zones (132 through 138)"
1160 FOR X=132 TO 138
1170 LET Z$=Z$+"/UPS NSP"
1180 IF N=0 THEN LET Z$=Z$+"UPS NSL"
1190 NEXT X
1200 REM " --- Next Day Air Early AM Zones (142 through 148)"
1210 FOR X=142 TO 148
1220 LET Z$=Z$+"/UPS NEP"
1230 IF N=0 THEN LET Z$=Z$+"UPS NEL"
1240 NEXT X
1300 REM " --- 2nd Day Air Zones (202 through 208, 224, 225 and 226)"
1310 FOR X=1 TO 10
1320 LET Z$=Z$+"/UPS 2DA"
1330 IF N=0 THEN LET Z$=Z$+"UPS 2DL"
1340 NEXT X
1350 REM " ---2nd Day Air Early AM Zones (242 through 248)"
1360 FOR X=242 TO 248
1370 LET Z$=Z$+"/UPS 2EP"
1380 IF N=0 THEN LET Z$=Z$+"UPS 2EL"
1390 NEXT X
1400 REM " --- 3rd Day Select Zones (302 through 308)"
1410 FOR X=302 TO 308
1420 LET Z$=Z$+"/UPS 3DA"
1430 NEXT X
1500 REM " --- Canadian Ground Zones (51 through 56)"
1510 FOR X=51 TO 56
1520 LET Z$=Z$+"/UPS CST"
1530 NEXT X
1550 REM " --- Canadian Express Zones (81 through 82)"
1560 FOR X=81 TO 82
1570 LET Z$=Z$+"/UPS CEX"
1580 IF N=0 THEN LET Z$=Z$+"UPS CLT"
1590 NEXT X
1600 REM " --- Canadian Expedited Zones (71 through 72)"
1610 FOR X=71 TO 72
1620 LET Z$=Z$+"/UPS CEP"
1630 NEXT X
1700 REM " --- International Express Zones (80, 84 and 900 through 908)"
1710 FOR X=1 TO 11
1720 LET Z$=Z$+"/UPS INT"
1730 IF N=0 THEN LET Z$=Z$+"UPS ILT"
1740 NEXT X
1800 REM " --- International Expedited Zones (74 and 601 through 608)"
1810 FOR X=1 TO 9
1820 LET Z$=Z$+"/UPS IXP"
1830 IF N=0 THEN LET Z$=Z$+"UPS IXL"
1840 NEXT X
1900 REM " --- Add ending /"
1910 LET Z$=Z$+"/"
2000 REM " --- Top of Zone Loop"
2010 FOR J=1 TO ZONES
2020 IF LEN(Z$)<2 THEN GOTO 3800
2030 LET Z1$=Z$(1,POS("/"=Z$)-1),Z$=Z$(POS("/"=Z$)+1),ZONE=Z[J]
2035 IF ZONE=0 THEN GOTO 3800
2040 IF LEN(Z1$)=0 THEN GOTO 3800
2050 PRINT @(COLUMN,11),ZONE:"##0",
2060 LET AK=0,DW$=""
2065 IF ZONE=124 OR ZONE=126 OR ZONE=224 OR ZONE=226 THEN LET AK=1
2075 IF N=0 THEN GOTO 2100
2080 IF POS("UPS NDA"=Z1$,7)>0 THEN LET DW$="Y"
2085 IF POS("UPS 2DA"=Z1$,7)>0 THEN LET DW$="Y"
2087 IF POS("UPS 2EP"=Z1$,7)>0 THEN LET DW$="Y"
2090 IF POS("UPS 3DA"=Z1$,7)>0 THEN LET DW$="Y"
2095 IF POS("UPS NEP"=Z1$,7)>0 THEN LET DW$="Y"
2100 REM " --- Top of Service Loop"
2110 FOR K=1 TO LEN(Z1$) STEP 7
2120 PRINT @(COLUMN+4,11),Z1$(K,7),
2130 FIND (SHM10_DEV,KEY=N0$+"A"+Z1$(K,7),DOM=3500)IOL=SHM10A
2200 REM " --- Initialize SHE-01 Header"
2210 LET MANUAL=MANUAL+1,A0$(1)=N0$+"M"+STR(MANUAL:J0$(1,6))
2220 LET A0$(18,3)=J0$(1,3),A1$(1)=J0$(1,6)+S1$(2,24)
2230 LET A1$(31,96)=C1$(31,96),A2$(1)=F0$(18,6)
2240 FOR X=7 TO 103 STEP 24
2250 IF FNP$(A1$(X,24))<>"" THEN GOTO 2270
2255 LET X9=X-24
2260 IF X9<7 THEN LET X9=7
2265 EXITTO 2400
2270 NEXT X
2280 LET X9=103
2290 IF A1$(144,24)<>"" THEN LET X9=144
2400 REM " --- Initialize SHE-02 Detail"
2410 DIM W1$(128),W[20]
2420 LET W[0]=W+N,W=W[0]
2430 IF W[0]>150 THEN EXITTO 3900
2440 LET W0$(1)=A0$(1,9),W0$(18,3)="001",W1$(59,2)=P8$
2450 LET W1$(1,3)=STR(Z[J]:"000"),W1$(4,7)=Z1$(K,7),W1$(11,1)=S1$(1,1)
2455 IF FNP$(W1$(29,24))="" THEN LET W1$(29,24)=S1$(2,24)
2480 IF W[0]<51 THEN GOTO 2500
2490 IF MOD(W[0],2)<>0 THEN LET W[0]=INT(W[0]/2)*2
2500 REM " --- Set defaults"
2510 DIM G0$(10),G1$(128),R0$(10),R[20]
2520 FIND (SHM10_DEV,KEY=N0$+"C"+W1$(4,7),DOM=2530)IOL=SHM10C
2530 FIND (SHM10_DEV,KEY=N0$+"G"+W1$(4,7),DOM=2600)IOL=SHM10G
2540 LET G1$=FNP$(G1$),L=LEN(G1$)
2550 IF L=0 THEN GOTO 2600
2560 FOR X=1 TO L
2580 IF G1$(X,1)<>" " THEN IF W1$(X,1)=" " THEN LET W1$(X,1)=G1$(X,1)
2590 NEXT X
2600 REM " --- Special fields based on service"
2610 IF W[0]=0 THEN LET W[0]=1
2620 LET W1$(16,1)=OVERSIZE$,W1$(17,1)=COD$
2630 IF W1$(16,1)<>"Y" THEN GOTO 2650
2640 LET COD=COD+1,W1$(20,7)=STR(COD:J0$(1,7)),W[2]=CODAMOUNT
2650 IF OVERSIZE$="Y" THEN LET OVERSIZE$="N",W[1]=VALUE
2655 IF AH$="Y" THEN LET W1$(126,1)="Y",AH$="N"
2660 IF COD$="Y" THEN LET COD$="N",OVERSIZE$="Y"
2670 LET TRACKING=TRACKING+1,W1$(108,18)="1"+STR(TRACKING:J0$(1,17))
2680 IF POS(W1$(61,1)="CMI")=0 THEN GOSUB 6000
2700 REM " --- Dimensional weight"
2710 IF DW$<>"Y" THEN GOTO 2800
2720 IF R[20]=0 THEN GOTO 2800
2730 LET W1$(88,1)="D",DW$="N",X=INT(SQR(W[0]))+1
2740 LET W[17]=X,W[18]=X+1,W[19]=X-1
2750 LET W[20]=W[17]*W[18]*W[19]/R[20]
2760 IF W[20]>W[0] THEN GOTO 2790
2770 LET X=X+2
2780 GOTO 2740
2790 IF INT(W[20])<>W[20] THEN LET W[20]=INT(W[20])+1
2900 REM " --- Generate zip code/destination for this package"
2910 GOSUB 6200
2920 IF FNP$(A1$(127,3))<>"" THEN GOTO 3000
2930 IF AK>0 THEN GOTO 3000
2940 IF W1$(61,1)<>"I" THEN GOTO 3500
2950 IF CITY$="" THEN GOTO 3500
3000 REM " --- Write sample records"
3010 IF W1$(127,1)="L" THEN LET W[0]=0
3020 WRITE (SHE01_DEV,KEY=A0$)IOL=SHE01A
3030 WRITE (SHE02_DEV,KEY=W0$)IOL=SHE02A
3500 REM " --- End of Service Loop"
3590 NEXT K
3800 REM " --- End of Zone Loop"
3890 NEXT J
3900 REM " --- Check for completion"
3910 IF N>0 THEN GOTO 4000
3920 LET N=1
3990 GOTO 1000
4000 REM " --- Run generation overlay"
4010 CLOSE (SHM24_DEV,ERR=4020)
4090 RUN "SHX.MB"
6000 REM " --- Extract destination state code from address"
6010 LET X$=FNP$(A1$(144,24))
6020 IF LEN(X$)>1 THEN GOTO 6080
6030 FOR X=103 TO 31 STEP -24
6040 LET X$=FNP$(A1$(X,24))
6050 IF LEN(X$)>1 THEN EXITTO 6080
6060 NEXT X
6070 LET X$=FILL(2)
6080 LET A1$(142,2)=X$(LEN(X$)-1,2)
6090 RETURN 
6200 REM " --- Generate pseudo zip code for this zone"
6210 DIM X[3]
6220 LET P=Y[J],ZONEFILE$=S1$(42,6)
6225 LET CITY$=A1$(X9,24),STATE$=A1$(142,2),ZIPCODE$=""
6230 IF W1$(61,1)="I" THEN LET CITY$=""
6240 IF AK>0 THEN LET ZONEFILE$="SHM-25"
6250 IF ZONEFILE$=LASTFILE$ THEN GOTO 6290
6260 GOSUB 6900
6270 IF STATUS THEN GOTO 6700
6290 READ (SHM24_DEV,KEY=PREFIX$,DOM=6300)
6300 REM " --- Read next zone chart record"
6310 LET K$=KEY(SHM24_DEV,END=6700)
6320 IF POS(PREFIX$=K$)<>1 THEN GOTO 6700
6325 READ (SHM24_DEV)IOL=SHM24A
6330 IF AK>0 OR W1$(61,1)="I" THEN IF X0$(6,1)="*" THEN GOTO 6300
6340 IF ZONE<>X[P] THEN GOTO 6300
6350 IF AK>0 THEN GOTO 6500
6390 IF W1$(61,1)="I" THEN GOTO 6500
6400 REM " --- Domestic/Canadian by postal code"
6410 IF W1$(61,1)="C" THEN GOTO 6450
6420 LET ZIPCODE$=X0$(6,3)+"00"
6425 IF W1$(61,1)="E" THEN LET ZIPCODE$=X0$(6,5)
6430 IF ZONE=125 OR ZONE=225 THEN LET CITY$="Puerto Rico",STATE$="PR"
6440 GOTO 6700
6450 IF W1$(61,1)="C" THEN LET CITY$="Canada",ZIPCODE$=X0$(6,3)+X0$(11,3)
6470 LET STATE$=""
6490 GOTO 6700
6500 REM " --- Alaskan/International by destination"
6510 LET CITY$=FNP$(X0$(6)),STATE$="",ZIPCODE$=""
6520 IF AK>0 THEN LET STATE$="AK",CITY$=CITY$+" "+STATE$
6590 GOTO 6700
6700 REM " --- Pseudo zip assigned"
6710 LET A1$(X9,24)=CITY$,A1$(142,2)=STATE$,A1$(127,9)=ZIPCODE$
6790 RETURN 
6900 REM " --- Open a zone file"
6910 LET FILES$[FILES]=ZONEFILE$,LASTFILE$="",X=FILES
6920 CLOSE (SHM24_DEV,ERR=6930)
6930 CALL "SYC.DA",1,X,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
6940 IF STATUS=0 THEN LET LASTFILE$=ZONEFILE$
6950 LET SHM24_DEV=CHANNELS[FILES]
6990 RETURN 
8000 REM " --- Function Definitions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

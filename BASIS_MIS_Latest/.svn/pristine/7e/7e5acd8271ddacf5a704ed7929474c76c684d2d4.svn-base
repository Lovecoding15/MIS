0010 REM "OPR - Invoice from current or history
0020 REM "Program BIN.12 - based on OPR.CA"
0060 SETESC 9000
0070 SETERR 9000
0080 ENTER INVOICE_NUM$,PRINTER; LET INVOICE_NUM$=STR(NUM(INVOICE_NUM$):"00000
0080:00")
0100 LET FILES=2
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="ART-03",FILES$[2]="SYS-01"
0130 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STA
0130:TUS
0140 IF STATUS>0 THEN GOTO 9900
0150 LET ART03_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0160 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0170 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0180 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0190 LET N0$=F0$(16,2),N2$="AR",INVHIST=0
0200 READ (ART03_DEV,KEY=N0$+INVOICE_NUM$,KNUM=1,ERR=160)
0210 LET INVHIST=1
0220 FOR I=1 TO FILES; CLOSE (CHANNELS[I]); NEXT I
0300 REM " --- IOLists" 
0310 ARE03A: IOLIST A0$(1),A[ALL]
0320 ARE13A: IOLIST W0$,W1$(1),W[ALL]
0330 ARE33A: IOLIST D0$,D1$
0340 ARM01A1: IOLIST B0$,B1$
0350 ARM01A2: IOLIST A0$,A1$
0360 ARM02A: IOLIST B0$,B1$,B[ALL]
0370 ARM03A: IOLIST C0$,C1$
0380 ARM09A: IOLIST X$,X8$(1)
0390 ARM10A: IOLIST X1$
0400 ARM10E: IOLIST Y0$(1)
0410 ARM10F: IOLIST X3$
0420 ARM10G: IOLIST *,X4$
0430 IVM01A: IOLIST *,X2$,X9$(1),*,*,X5$(1)
0440 CSM03A: IOLIST CS0$,CS1$,CS2$
0450 ARE73A: IOLIST WX0$,WX1$,WX[ALL]
0460 ARE83A: IOLIST WY0$,WY1$,WY2$,WY[ALL]
0500 REM " --- Open/lock files"
0510 LET FILES=13
0520 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0530 LET FILES$[1]="ARE-03",FILES$[2]="ARE-73",FILES$[3]="ARE-13"
0540 LET FILES$[4]="ARE-83",FILES$[5]="ARE-33",FILES$[6]="CSM-03"
0550 LET FILES$[7]="ARM-01",FILES$[8]="ARM-03",FILES$[9]="ARM-09"
0560 LET FILES$[10]="ARM-10",FILES$[11]="IVM-01",FILES$[12]="SYS-01"
0570 LET FILES$[13]="TMM-01"
0580 IF INVHIST THEN LET FILES$[1]="ART-03",FILES$[2]="ART-73",FILES$[3]="ART-
0580:13",FILES$[4]="ART-83",FILES$[5]="ART-33"
0590 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STA
0590:TUS
0600 IF STATUS<>0 THEN GOTO 4100
0610 LET ARE03_DEV=CHANNELS[1],ARE73_DEV=CHANNELS[2],ARE13_DEV=CHANNELS[3]
0620 LET ARE83_DEV=CHANNELS[4],ARE33_DEV=CHANNELS[5],CSM03_DEV=CHANNELS[6]
0630 LET ARM01_DEV=CHANNELS[7],ARM03_DEV=CHANNELS[8],ARM09_DEV=CHANNELS[9]
0640 LET ARM10_DEV=CHANNELS[10],IVM01_DEV=CHANNELS[11],SYS01_DEV=CHANNELS[12]
0650 LET TMM01_DEV=CHANNELS[13]
0800 REM " --- Initialize Data"
0810 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0820 DIM P[2]
0830 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0840 LET M0=LEN(M0$),M2=LEN(M2$)
0850 DIM WX[4],WY[2],X5$(2),A0$(117),A[10]
0860 LET L9=18,HEADINGS=0,WHEN$=F0$(7,3),HZ$=FILL(79,"_")
0870 DIM H9$(50,$0A$)
0880 DIM W1$(64),W[14],HEADINGS$[HEADINGS]
0890 LET EXT_MASK$=M1$,LF$=$0A$
1000 REM " --- Read Invoice 
1010 IF INVHIST THEN LET I=11; READ (ARE03_DEV,KEY=N0$+INVOICE_NUM$,KNUM=1,DOM
1010:=CHECK_INVOICE)IOL=ARE03A; GOTO CHECK_INVOICE
1015 LET I=42; READ (ARE03_DEV,KEY=N0$,KNUM=0,DOM=READ_INVOICE)
1020 READ_INVOICE: READ (ARE03_DEV,END=4000)IOL=ARE03A
1030 CHECK_INVOICE: IF POS(N0$=A0$)<>1 THEN GOTO 4000
1040 IF A0$(I,7)<>INVOICE_NUM$ THEN IF INVHIST THEN GOTO 4000 ELSE GOTO READ_I
1040:NVOICE
1050 REM " --- Background"
1060 LET ROW=11; CALL "SYC.NB","Printing",LEN(M0$)+8,COLUMN
1070 IF A0$(22,1)<>"I" THEN GOTO 4000
1400 REM " --- Heading"
1410 PRINT @(COLUMN,ROW),FNF$(A0$(5,P[0]),M0$)," ",A0$(42,7),
1420 DIM B$(150),B1$(325)
1440 LET B1$(1)="Not On File",CONTACT$=""
1460 READ (ARM01_DEV,KEY=N0$+A0$(5,6),DOM=1490)IOL=ARM01A1
1465 LET CONTACT$=B1$(228,20)
1470 REM LET B$=B1$(31,72)+B1$(179,48)+B1$(103,9)
1480 REM CALL "SYC.AA",B$,24,5,P[2],30
1490 REM LET B$=B1$(1,30)+B$
1495 GOSUB 10000
1500 REM " --- Ship-To"
1510 LET C$=B$
1520 IF FNP$(A0$(102,6))="" THEN GOTO 1600
1530 DIM C1$(155)
1540 IF A0$(102,6)<>"000099" THEN GOTO 1600
1545 LET SHIPTO$=""
1550 FIND (ARE33_DEV,KEY=N0$+A0$(5,13),DOM=1560)IOL=ARE33A
1555 LET C$=D1$(31,81),C1$(1)=D1$(1,30)
1560 CALL "SYC.AA",C$,24,3,P[2],30
1570 LET C$=C1$(1,30)+C$
1590 GOTO 1690
1600 LET SHIPTO$=""
1610 FIND (ARM03_DEV,KEY=N0$+A0$(5,6)+A0$(102,6),DOM=1690)IOL=ARM03A
1620 LET SHIPTO$=A0$(102,6)
1630 LET C$=C1$(31,81)
1640 CALL "SYC.AA",C$,24,3,P[2],30
1650 LET C$=C1$(1,30)+C$
1690 IF LEN(C$)<LEN(B$) THEN LET C$=C$+FILL(40); GOTO 1690
1700 REM " --- Terms"
1710 DIM X1$(15); LET PMTMSG$=""
1720 FIND (ARM10_DEV,KEY=N0$+"A"+A0$(62,2),DOM=1740)IOL=ARM10A
1730 LET X1$=X1$(6,20)
1735 IF POS("NET"=CVS(X1$,4)) THEN LET PMTMSG$="Pay this invoice. No statement
1735: will be sent."
1737 IF POS("CREDIT CARD"=CVS(X1$,4)) OR POS("PREPAID"=CVS(X1$,4)) THEN LET PM
1737:TMSG$="This invoice has already been paid."
1740 REM " --- Salesperson"
1745 LET X3$=""
1750 FIND (ARM10_DEV,KEY=N0$+"F"+A0$(59,3),DOM=1800)IOL=ARM10F
1760 LET X3$=X3$(7,20)
1800 REM " --- Job Name"
1810 DIM X8$(30)
1820 IF ARM09_DEV=0 THEN GOTO 1880
1830 FIND (ARM09_DEV,KEY=N0$+A0$(5,6)+A0$(86,10),DOM=1880)IOL=ARM09A
1840 LET X8$(1)=X8$
1850 GOTO 1900
1880 LET X8$(1)=A0$(86,10)
1900 REM " --- Print Heading"
1910 LET P=0
1920 GOSUB 15000
1940 LET T0=0,X6$=""
1990 READ (ARE13_DEV,KEY=A0$(1,17),DOM=2000)
2000 REM " --- Detail"
2020 LET K$=KEY(ARE13_DEV,END=3000)
2040 IF K$(1,17)<>A0$(1,17) THEN GOTO 3000
2060 READ (ARE13_DEV)IOL=ARE13A
2100 REM " --- Type"
2120 DIM Y0$(30)
2140 FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=2160)IOL=ARM10E
2150 LET X2$=W0$(23)
2160 IF POS(Y0$(25,1)=" SP")=0 THEN GOTO 2200
2170 DIM X9$(62),X2$(60)
2180 FIND (IVM01_DEV,KEY=N0$+W0$(33),DOM=2190)IOL=IVM01A
2185 GOSUB 5900
2190 LET X2$=X2$+FILL(40),X2$=X2$(1,42)
2200 IF Y0$(25,1)="M" AND POS(Y0$(28,1)="BI ")=0 THEN GOTO 2300
2210 GOSUB 12000
2220 GOSUB 5200
2250 REM " --- Print configurator/serial number info
2260 GOSUB 15400
2300 REM " --- Total"
2320 LET T0=T0+W[6]
2400 REM 
2900 GOTO 2000
3000 REM " --- Total"
3020 GOSUB 5600
4000 REM " --- Totals"
4105 CLOSE (PRINTER,ERR=4110)
4110 FOR I=1 TO FILES
4120 CLOSE (CHANNELS[I],ERR=4130)
4130 NEXT I
4140 PRINT @(0,ROW-1),'CE'
4190 GOTO 9900
5200 REM " --- Item Detail"
5210 IF L>L9-1 THEN GOSUB 5800
5220 IF POS(Y0$(25,1)="MO")<>0 THEN GOTO 5240
5230 PRINT (PRINTER)@(0),W[2],@(7),W[4],
5240 IF POS(Y0$(25,1)="MNO")<>0 THEN PRINT (PRINTER)@(19),W1$(1,40),
5250 IF POS(Y0$(25,1)=" SRDP")<>0 THEN PRINT (PRINTER)@(21),W0$(33),
5260 PRINT (PRINTER)@(50),PORT$,
5270 IF POS(Y0$(25,1)=" SRDNP")<>0 THEN PRINT (PRINTER)@(56),W[1]:EXT_MASK$,
5280 IF POS(Y0$(25,1)="M")=0 THEN PRINT (PRINTER)@(68),W[6]:EXT_MASK$,
5290 PRINT (PRINTER)""
5300 LET L=L+1
5310 IF LEN(X2$)>40 THEN LET X2$=X2$(1,40)
5320 IF POS(Y0$(25,1)="SP")<>0 AND L>L9-1 THEN GOSUB 5800
5330 IF POS(Y0$(25,1)="SP")<>0 THEN PRINT (PRINTER)@(19),X2$
5340 IF POS(Y0$(25,1)="SP")<>0 THEN LET L=L+1
5390 RETURN
5600 REM " --- Total"
5610 PRINT (PRINTER)H9$(1,L9-L+3),HZ$,LF$
5620 IF T0<0 THEN LET PMTMSG$=""
5640 PRINT (PRINTER)PMTMSG$,FILL(53-LEN(PMTMSG$)),"NET INVOICE:   ",T0:EXT_MAS
5640:K$
5650 PRINT (PRINTER)FILL(51),"LESS DISCOUNT:   ",-A[2]:EXT_MASK$
5660 PRINT (PRINTER)FILL(57),"FREIGHT:   ",A[1]:EXT_MASK$
5670 PRINT (PRINTER)FILL(55),"SALES TAX:   ",A[0]:EXT_MASK$
5680 PRINT (PRINTER)FILL(51),"INVOICE TOTAL:   ",T0+A[0]-A[2]+A[1]:EXT_MASK$
5690 RETURN
5800 REM " --- Continued"
5820 PRINT (PRINTER)H9$(1,L9-L+1)
5840 PRINT (PRINTER)@(70),"Continued",'FF',
5870 GOSUB 15000
5890 RETURN
5900 REM " --- Compress Description"
5905 LET Z=POS(" "<>X2$); IF Z>1 THEN LET X2$=X2$(Z)
5910 LET X=POS("  "=X2$); IF X<>0 THEN LET Y=POS(" "<>X2$(X+2)); LET X2$=X2$(1
5910:,X)+X2$(X+Y+1); IF Y<>0 THEN GOTO 5910
5920 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,LF$
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,LF$
9350 ESCAPE
9390 RETURN
9900 EXIT
9999 END
10000 REM " --- Get 30 character address from Telemaster"
10010 READ (TMM01_DEV,KNUM=0,KEY=A0$(1,2)+A0$(5,6),DOM=*next)
10020 LET TMM01_K$=KEY(TMM01_DEV,END=10090)
10030 IF POS(A0$(1,2)+A0$(5,6)=TMM01_K$)<>1 THEN GOTO 10090
10040 READ (TMM01_DEV,KEY=TMM01_K$)TMM0$
10050 LET CUST_TYPE$=TMM0$(260,3)
10060 LET B$=TMM0$(142,60)+TMM0$(389,60)+TMM0$(202,25)+" "+TMM0$(227,2)+"  "+TMM0$(275,2)+FILL(28," ")+TMM0$(229,9)
10070 CALL "SYC.AX",B$,30,6,P[2],30
10080 LET B$=B1$(1,30)+B$
10090 RETURN
12000 REM " --- Find Port Id
12010 LET PORT$=""
12030 IF Y0$(25,1)<>"S" THEN GOTO 12090
12040 FIND (CSM03_DEV,KEY=N0$+W0$(39,3),DOM=12090)IOL=CSM03A
12050 LET PORT$=CS2$(1,5); IF W0$(33,2)="P4" THEN LET PORT$(5,1)="4"
12090 RETURN
14500 REM Date with 4 digit year
14505 DEF FNA4$(Q$,Q2$)=STR(((ASC(Q$)-32)+1900)*POS(" "<>Q2$(2,1)):"0000")
14510 DEF FNB4$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA4$(Q1$(1),Q1
14510:$)
15000 REM " --- Heading"
15005 LET L=0,P=P+1,ORDER$=A0$(11,7),INVOICE$=A0$(42,7)
15010 IF INVHIST THEN LET ORDER$=A0$(42,7),INVOICE$=A0$(11,7)
15015 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
15020 PRINT (PRINTER)"      Please remit to:",FILL(32),HZ$(1,13),LF$,LF$,"    
15020:  BASIS International Ltd.",FILL(24),"I N V O I C E",LF$,"      5901 Jef
15020:ferson Street NE",FILL(24),HZ$(1,13),LF$,"      Albuquerque NM 87109-343
15020:2"
15025 LET PH$="      Phone 505.345.5232  Fax 505.345.5082",EM$="      Email in
15025:fo@basis.cloud  www.basis.cloud"
15030 PRINT (PRINTER)"      USA",FILL(36),"INVOICE NUMBER:   ",INVOICE$,LF$,FI
15030:LL(47),"INVOICE DATE:   ",FNB4$(A0$(24,3)),LF$,PH$,FILL(55-LEN(PH$)),"PA
15030:GE:   ",P
15033 PRINT (PRINTER)EM$,FILL(47-LEN(EM$)),"ORDER NUMBER:   ",ORDER$
15035 PRINT (PRINTER)FILL(49),"ORDER DATE:   ",FNB4$(A0$(77,3)),LF$,FILL(48),"
15035:CUSTOMER NO:   ",A0$(5,6),"  ",CUST_TYPE$
15037 PRINT (PRINTER)HZ$,LF$,LF$,"      SOLD TO:",FILL(30),"SHIP TO:",LF$
15040 FOR I=1 TO 6
15045 PRINT (PRINTER)FILL(6),B$(I*30-29,30),FILL(8),C$(I*30-29,30)
15050 NEXT I
15055 IF CVS(CONTACT$,2)<>"" THEN PRINT (PRINTER)" CONFIRM TO: ",CONTACT$ ELSE
15055: PRINT (PRINTER)
15060 PRINT (PRINTER)HZ$,LF$,LF$," CUSTOMER P.O.  SHIP VIA        SALESPERSON 
15060:              TERMS"
15065 PRINT (PRINTER)" ",A0$(49,10),FILL(5),A0$(32,10),FILL(6),X3$,FILL(6),X1$
15065:(1,15)
15070 PRINT (PRINTER)HZ$,LF$,LF$,"ORDER   SHIP       DESCRIPTION              
15070:                 PRICE       TOTAL",LF$,HZ$,LF$
15090 RETURN
15400 REM " --- configuration detail
15402 IF X5$(1,1)<>"Y" THEN GOTO 15900
15405 FIND (ARE73_DEV,KEY=W0$(1,20),DOM=15900)IOL=ARE73A
15410 IF L+1>L9 THEN GOSUB 5800
15415 PRINT (PRINTER)@(21),"Type: ",WX1$(4,3),
15420 IF WX[1]<>0 THEN PRINT (PRINTER)@(31),"Media: ",WX1$(1,3),
15430 IF WX[1]<>0 THEN PRINT (PRINTER)"  Sets: ",WX[1]:"#0",
15435 PRINT (PRINTER)""
15440 LET L=L+1
15445 IF L+1>L9 THEN GOSUB 5800
15455 PRINT (PRINTER)@(21),"Number of users: ",WX[2]:"###0",
15460 PRINT (PRINTER)""
15465 LET L=L+1
15500 REM " --- serial number detail"
15510 LET ACT$="",DEACT$="",MOD$=""
15520 READ (ARE83_DEV,KEY=W0$(1,20),DOM=15521)
15530 LET ARE83_K$=KEY(ARE83_DEV,END=15600)
15540 IF POS(W0$(1,20)=ARE83_K$)<>1 THEN GOTO 15600
15550 READ (ARE83_DEV,KEY=ARE83_K$)IOL=ARE83A
15560 IF WY1$(1,1)="M" THEN LET MOD$=MOD$+WY2$
15570 IF WY1$(1,1)="A" THEN LET ACT$=ACT$+WY2$
15580 IF WY1$(1,1)="D" THEN LET DEACT$=DEACT$+WY2$
15590 GOTO 15530
15600 REM " --- print them
15610 LET STR$=MOD$,TITLE$="Serial Numbers: "
15620 GOSUB 15700
15630 LET STR$=ACT$,TITLE$="New Serial Numbers: "
15640 GOSUB 15700
15650 LET STR$=DEACT$,TITLE$="Deactivated Serial Numbers: "
15660 GOSUB 15700
15665 PRINT (PRINTER)""; LET L=L+1; IF L>L9 THEN GOSUB 5800
15670 GOTO 15900
15700 REM " --- print them
15705 IF STR$="" THEN GOTO 15790
15715 LET SNUMBS=INT(LEN(STR$)/20)
15724 IF L+2>L9 THEN GOSUB 5800
15730 PRINT (PRINTER)@(21),TITLE$; LET L=L+1
15740 PRINT (PRINTER)@(23),
15750 FOR SL=1 TO SNUMBS
15760 PRINT (PRINTER)STR$((SL-1)*20+1,20),"  ",
15770 IF MOD(SL,2)=0 THEN PRINT (PRINTER)LF$,@(23),; LET L=L+1
15772 IF L+1>L9 THEN GOSUB 5800; PRINT (PRINTER)@(23),
15780 NEXT SL
15785 IF MOD(SNUMBS,2)<>0 THEN PRINT (PRINTER)""; LET L=L+1
15787 IF L+1>L9 THEN GOSUB 5800
15790 RETURN
15900 REM " --- Done"
15990 RETURN
46500 END

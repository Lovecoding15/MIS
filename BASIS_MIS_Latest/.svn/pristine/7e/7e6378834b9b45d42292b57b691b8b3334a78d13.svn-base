0010 REM "ARU - CASH RECEIPTS UPDATE"
0020 REM "Program ARU.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 15Dec97 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "23Sep96 Caj: MTD cash buckets (ARS-10C) weren't being updated (3220,3230,3235,3240,3280)
0032 REM "18Nov96 JWK: Ave Days to Pay calc- wrong Days-between-dates (2245)
0033 REM "24Jun97 WGH: On-Account invoices written incorrectly to ART-01 and ART-11 (290,330,530,1820,1870,1920-1930,2010,2080,2110,2140,2180-2190)"
0034 REM "09Sep97 WGH: Write new ARM-02 record if it is missing (3030-3040)"
0035 REM "26Aug98 KJS: Don't update Ave days to Pay if payment isn't positive (2232)"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=19
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARE-01",FILES$[2]="ARE-11",FILES$[3]="ARE-21"
0130 LET FILES$[4]="ARE-31",FILES$[5]="ARM-01",FILES$[6]="ARM-02"
0135 LET FILES$[7]="ARM-06",FILES$[8]="ARM-10",FILES$[9]="ART-01"
0140 LET FILES$[10]="ART-11",FILES$[11]="ART-06",FILES$[12]="ART-16"
0145 LET FILES$[13]="ART-26",FILES$[14]="ARS-10",FILES$[15]="SYS-01"
0150 LET FILES$[16]="GLM-01",FILES$[17]="GLT-04",FILES$[18]="GLT-05"
0155 LET FILES$[19]="APM-12"
0157 LET OPTIONS$[1]="L",OPTIONS$[2]="L",OPTIONS$[3]="L",OPTIONS$[4]="L"
0160 CALL "SYC.DA",1,1,15,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET ARE01_DEV=CHANNELS[1],ARE11_DEV=CHANNELS[2],ARE21_DEV=CHANNELS[3]
0180 LET ARE31_DEV=CHANNELS[4],ARM01_DEV=CHANNELS[5],ARM02_DEV=CHANNELS[6]
0185 LET ARM06_DEV=CHANNELS[7],ARM10_DEV=CHANNELS[8],ART01_DEV=CHANNELS[9]
0190 LET ART11_DEV=CHANNELS[10],ART06_DEV=CHANNELS[11],ART16_DEV=CHANNELS[12]
0195 LET ART26_DEV=CHANNELS[13],ARS10_DEV=CHANNELS[14],SYS01_DEV=CHANNELS[15]
0200 REM " --- IOLists
0210 ARE01A: IOLIST A0$,A[0]
0220 ARE11A: IOLIST W0$,W[ALL]
0230 ARE21A: IOLIST W0$,W[ALL]
0240 ARM01A: IOLIST *,H1$(1)
0250 ARM02A: IOLIST B0$,B1$,B[ALL]
0260 ARM06A: IOLIST D0$(1),D1$(1),D[ALL]
0270 ARM10C: IOLIST *,Z1$
0280 ARM10D: IOLIST *,Z0$
0290 ART01A: IOLIST C0$(1),C[ALL]
0300 ART06A: IOLIST S0$,A[0]
0310 ART16A: IOLIST S0$,W[ALL]
0320 ART26A: IOLIST S0$,W[ALL]
0330 ART11A: IOLIST C0$,C1$(1),C[ALL]
0340 APM12A: IOLIST X0$,X1$,X[ALL]
0350 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0360 ARS01C: IOLIST X0$
0370 ARS10C: IOLIST X0$,X[ALL]
0380 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0390 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0405 DIM P[6],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=3440)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=3440)IOL=ARS01A
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=3440)IOL=GLS01A
0430 FOR X=0 TO 4
0435 LET G[X]=NUM(G2$(X*2+1,2),ERR=0440)
0440 NEXT X
0445 LET G[2]=FNYY_YEAR(G2$(5,2))
0450 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2))
0455 LET P[2]=NUM(P4$(1,2)),P[3]=FNYY_YEAR(P4$(3,2))
0490 CALL "SYC.VA",N2$,INFO$[ALL]
0495 LET GL$=INFO$[9]
0500 REM " --- Initializations"
0511 DIM D0$(24),D1$(24),D[21]
0530 DIM A[1],B[10],V[1],W[1],U[5],X[5]
0540 LET U0$=N0$,M0=LEN(M0$)
0550 CALL "SYC.PA",SYS01_DEV,P[2],P[3],X$,G9$,STATUS
0600 REM " --- Other File Opens"
0610 IF GL$<>"Y" THEN GOTO 0690
0625 CALL "SYC.DA",1,16,18,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS>0 THEN GOTO 9000
0635 LET GLM01_DEV=CHANNELS[16],GLT04_DEV=CHANNELS[17],GLT05_DEV=CHANNELS[18]
0640 IF P3$(14,1)<>"Y" THEN GOTO 0700
0655 CALL "SYC.DA",1,19,19,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0660 IF STATUS>0 THEN GOTO 0690
0670 LET APM12_DEV=CHANNELS[19]
0680 GOTO 0700
0690 LET P3$(14,1)="N"
0700 REM " --- Background
0710 PRINT @(0,3),'CE',
0720 CALL "SYC.NB","Updating",M0+9,COLUMN
0800 REM " --- Options
0810 LET V4$="Are You Ready To Update The "+N3$
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
1000 REM " --- Initial Read
1020 READ (ARE01_DEV,KEY=N0$,DOM=1100)
1100 REM " --- Main Read
1120 LET K0$=KEY(ARE01_DEV,END=4000)
1130 IF K0$(1,2)<>N0$ THEN GOTO 4000
1180 READ (ARE01_DEV)IOL=ARE01A
1190 LET A9$=A0$(15,3)
1200 GOSUB 6000
1210 LET T0=0,T1=0
1220 DIM U[5]
1230 IF P3$(14,1)="Y" THEN GOSUB 6200
1240 PRINT @(COLUMN,11),FNB$(A0$(6,3))," ",FNF$(A0$(9,P[0]),M0$),
1300 REM " --- Cash Receipt History - Heading
1330 DIM S0$(46)
1340 LET S0$(1)=A0$(1,5)+A0$(9,6)+A0$(6,3)+A0$(15)
1350 WRITE (ART06_DEV,KEY=S0$(1,25))IOL=ART06A
1400 REM " --- Retrieve customer"
1410 DIM H1$(30)
1420 LET H1$(1)="Customer Not On File"
1430 FIND (ARM01_DEV,KEY=N0$+A0$(9,6),DOM=2400)IOL=ARM01A
1600 REM " --- Invoice Detail
1610 READ (ARE11_DEV,KEY=K0$(1,24),DOM=1620)
1620 LET K11$=KEY(ARE11_DEV,END=2580)
1660 IF K11$(1,24)<>K0$(1,24) THEN GOTO 2580
1680 READ (ARE11_DEV)IOL=ARE11A
1700 REM " --- Cash Receipt History - Detail
1720 LET X9=42,X8=32
1730 DIM S0$(X9)
1740 LET S0$(1)=W0$(1,5)+W0$(9,6)+W0$(6,3)+W0$(15)
1750 WRITE (ART16_DEV,KEY=S0$(1,X8))IOL=ART16A
1800 REM " --- AR
1820 DIM C0$(50),C[1],C1$(11)
1830 LET C0$(1)=A0$(1,4)+A0$(9,6)+W0$(26,7)+"00",C0$(24,9)=A0$(6,3)+A0$(6,3)+A0$(6,3),C0$(33,1)="S",C[0]=(-W[0])-W[1],C1=0,C9$="  ",D2$="   "
1840 FIND (ART01_DEV,KEY=C0$(1,19),DOM=1880)IOL=ART01A
1850 LET D2$=C0$(24,3)
1860 LET C9$=C0$(20,2)
1870 GOTO 2000
1880 FIND (ARM02_DEV,KEY=C0$(1,2)+C0$(5,6)+C0$(3,2),DOM=1910)IOL=ARM02A
1890 LET C9$=B1$(8,2)
1900 LET C0$(20,2)=C9$
1910 WRITE (ART01_DEV,KEY=C0$(1,19))IOL=ART01A
1920 LET C1=1,C[0]=0,C[1]=0
1930 GOTO 2190
2000 REM " --- ART-11 Detail
2020 READ (ART11_DEV,KEY=C0$(1,17),DOM=2030)
2030 LET K$=KEY(ART11_DEV,END=2110)
2040 IF K$(1,17)<>C0$(1,17) THEN GOTO 2110
2050 READ (ART11_DEV)IOL=ART11A
2060 LET C1=NUM(C0$(18,2))
2080 IF C1$(1,1)=" " THEN GOTO 2180
2100 GOTO 2030
2110 DIM C[1],C1$(11)
2120 LET C1=C1+1
2130 IF C1<=99 THEN GOTO 2180
2140 LET C0$=C0$(1,17)+"99"
2150 EXTRACT (ART11_DEV,KEY=C0$(1,19),DOM=2155)IOL=ART11A
2155 IF POS(C1$(1,1)=" C") THEN LET C1$(1,1)="C" ELSE LET C1$(1,1)="A"
2160 LET C1$(2)=A0$(6,3)+W0$(18,7)
2165 LET C[0]=C[0]-W[0],C[1]=C[1]-W[1]
2170 GOTO 2200
2180 LET C[0]=-W[0],C[1]=-W[1]
2190 LET C0$=C0$(1,17)+STR(C1:"00"),C1$(1,11)="C"+A0$(6,3)+W0$(18,7)
2200 WRITE (ART11_DEV,KEY=C0$(1,19))IOL=ART11A
2205 REM " --- Update Customer Average Days to Pay
2206 REM "     D(6)=Avg Days to Pay
2207 REM "     D(7)=Total Number of Payments
2208 REM "     V0=Actual Days to Pay on this Invoice
2232 IF C0$(11,2)="OA" OR A[0]<=0 THEN GOTO 2300
2235 DIM D0$(8),D1$(16),D[21]
2240 LET D0$=C0$(1,2)+C0$(5,6)
2245 LET V4$=FNYY_YY21$(FNM$(FNC$(D2$))),V5$=FNYY_YY21$(FNM$(FNC$(W0$(6,3)))),V0=0
2250 CALL "SYC.CA",V4$,V5$,V0
2260 EXTRACT (ARM06_DEV,KEY=D0$(1,8),DOM=2270)IOL=ARM06A
2270 LET D1$(4,3)=W0$(6,3)
2275 IF D[7]=0 THEN LET D[6]=V0; GOTO 2285; REM "1st payment ever for cust"
2280 LET D[6]=(D[7]*D[6]+V0)/(D[7]+1)
2285 LET D[7]=D[7]+1
2287 PRECISION 0; LET D[6]=D[6]*1; PRECISION 2
2290 IF A0$(6,3)<=G9$ THEN LET X=10 ELSE LET X=16
2292 LET D[X]=(D[X]*D[X+1]+V0)/(D[X+1]+1),D[X+1]=D[X+1]+1
2294 IF A0$(6,3)>G9$ THEN GOTO 2298
2296 LET X=12,D[X]=(D[X]*D[X+1]+V0)/(D[X+1]+1),D[X+1]=D[X+1]+1
2298 WRITE (ARM06_DEV,KEY=D0$(1,8))IOL=ARM06A
2300 REM " --- Dist Code"
2310 DIM Z0$(10*G[4])
2320 FIND (ARM10_DEV,KEY=N0$+"D"+C9$,DOM=2380)IOL=ARM10D
2330 LET Z0$=Z0$(1,G[4])
2400 IF A0$(6,3)<=G9$ THEN LET U[0]=U[0]+W[0],U[1]=U[1]+W[1]
2420 IF A0$(6,3)>G9$ THEN LET U[3]=U[3]+W[0],U[4]=U[4]+W[1]
2430 LET T0=T0-W[0]-W[1],T1=T1-W[0]
2440 LET ACCOUNT$=Z0$,GLDATE$=A0$(6,3),MEMO$=H1$,AMOUNT=(-W[0])-W[1]
2445 LET REF1$=FNF$(A0$(9,P[0]),M0$),REF2$=W0$(18,7),REF3$=W0$(26,7)
2450 GOSUB GLPOST
2460 LET ACCOUNT$=Z1$,AMOUNT=W[0]
2470 GOSUB GLPOST
2480 LET ACCOUNT$=Z2$,AMOUNT=W[1]
2490 GOSUB GLPOST
2550 REM " --- Remove ARE-11 Detail
2560 REMOVE (ARE11_DEV,KEY=K11$)
2570 GOTO 1620
2580 REM " --- GL
2590 READ (ARE21_DEV,KEY=K0$(1,24),DOM=2600)
2600 LET K21$=KEY(ARE21_DEV,END=2800)
2610 IF K21$(1,24)<>K0$(1,24) THEN GOTO 2800
2620 READ (ARE21_DEV)IOL=ARE21A
2630 IF A0$(6,3)<=G9$ THEN LET U[2]=U[2]+W[0]
2640 IF A0$(6,3)>G9$ THEN LET U[5]=U[5]+W[0]
2660 LET ACCOUNT$=W0$(26,G[4]),GLDATE$=A0$(6,3),MEMO$=H1$,AMOUNT=W[0]
2665 LET REF1$=FNF$(A0$(9,P[0]),M0$),REF2$=W0$(18,7),REF3$=""
2670 GOSUB GLPOST
2680 LET ACCOUNT$=Z1$,AMOUNT=-W[0]
2690 GOSUB GLPOST
2700 REM " --- Cash Receipt History - Detail
2710 LET X9=45,X8=35
2720 DIM S0$(X9)
2730 LET S0$(1)=W0$(1,5)+W0$(9,6)+W0$(6,3)+W0$(15)
2740 WRITE (ART26_DEV,KEY=S0$(1,X8))IOL=ART26A
2750 REM " --- Remove ARE-21 Detail
2760 REMOVE (ARE21_DEV,KEY=K21$)
2770 GOTO 2600
3000 REM " --- Update Customer
3010 IF T0=0 THEN GOTO 3200
3020 LET B0$=N0$+A0$(9,6)+A0$(3,2)
3030 DIM B1$(30),B[10]
3040 EXTRACT (ARM02_DEV,KEY=B0$,DOM=3050)IOL=ARM02A
3050 LET B[1]=B[1]+T0,B1$(17,3)=A0$(6,3)
3060 WRITE (ARM02_DEV,KEY=B0$)IOL=ARM02A
3200 REM " --- Update MTD Cash
3220 DIM X0$(3),X[5]
3230 LET X0$(1)=N0$+"C"
3240 EXTRACT (ARS10_DEV,KEY=X0$,DOM=3250)IOL=ARS10C
3250 FOR X=0 TO 5
3260 LET X[X]=X[X]+U[X]
3270 NEXT X
3280 WRITE (ARS10_DEV,KEY=X0$)IOL=ARS10C
3900 REM " --- Remove Heading
3920 REMOVE (ARE01_DEV,KEY=K0$)
3990 GOTO 1100
4000 REM " --- End
4005 REM " --- Remove ARE-31 Detail
4010 READ (ARE31_DEV,KEY="",DOM=4020)
4020 LET K31$=KEY(ARE31_DEV,END=4090)
4030 REMOVE (ARE31_DEV,KEY=K31$)
4040 GOTO 4020
4090 IF GL$="Y" THEN CALL "GLC.CA",STATUS
4095 CALL "SYC.BB",STATUS
4100 GOTO 9900
6000 REM " --- Trans Code
6040 DIM Z1$(2*G[4],"0")
6050 FIND (ARM10_DEV,KEY=N0$+"C"+A9$,DOM=6060)IOL=ARM10C
6060 LET Z2$=Z1$(G[4]+1,G[4]),Z1$=Z1$(1,G[4])
6090 RETURN 
6200 REM " --- Bank Rec
6210 IF P3$(14,1)<>"Y" THEN GOTO 6290
6215 IF GL$<>"Y" THEN GOTO 6290
6220 DIM X1$(50),X[1]
6230 LET X0$=N0$+Z1$+"C"+FNYY_YY21$(FNM$(FNC$(A0$(6,3)))),X1$(1)="DO"+FNYY_YY21$(FNM$(FNC$(A0$(6,3)))),X1$(9,30)="Cash Receipts Register"
6250 FIND (APM12_DEV,KEY=X0$,DOM=6260)IOL=APM12A
6260 LET X[0]=X[0]+A[0]
6270 WRITE (APM12_DEV,KEY=X0$)IOL=APM12A
6290 RETURN 
6900 REM " --- Standard G/L Posting Routine"
6910 GLPOST: 
6920 IF GL$<>"Y" THEN GOTO 6990
6930 LET UNITS=0
6950 CALL "GLC.AA",GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,GLDATE$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

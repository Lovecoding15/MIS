rem ' ****************************************************************
rem ' send email using email.bbj
rem ' ****************************************************************

seterr errorRoutine

rem ' ****************************************************************
rem ' ENTER
rem ' ****************************************************************

enter from$, to$, cc$, bcc$, subject$, msgtxt$, file$

rem ' from$ a single email address - required
rem ' to$ is a comma separated list of to email addresses
rem ' cc$ is a comma separated list of cc email addresses
rem ' bcc$ is a comman separted list of bcc email addresses
rem ' subject$ is the email subject
rem ' msgtxt$ is the text of the message
rem ' file$ is a | separated list of files to attach to the email

rem ' ****************************************************************
rem ' get properties
rem ' ****************************************************************

use java.io.File
use java.io.FileInputStream
use java.io.BufferedInputStream
use java.util.Properties

props=unt
open(props)"sendEmail.properties"
props$=fid(props)
props$=props$(9)
close(props)
file!= new java.io.File(props$)
fis!=new java.io.FileInputStream(file!)
bis!=new java.io.BufferedInputStream(fis!)
props!=new java.util.Properties()
props!.load(bis!)

rem ' ****************************************************************
rem ' get Email instance
rem ' ****************************************************************

rem ' if the directory location of email.bbj is not in the prefix
rem ' add it
location$ = props!.getProperty("email.bbj.location")
thePrefix$ = pfx
if pos(location$ = thePrefix$) = 0 then
	thePrefix$ = thePrefix$ + " " + location$
	prefix thePrefix$
fi

use ::email.bbj::Email
use java.util.Date

declare Email mail!

host$ = props!.getProperty("mailhost")
port = num(props!.getProperty("mailport"))
rem ' smtp$ = props!.getProperty("smtp")
rem ' password$ = props!.getProperty("password")
rem ' user$ = props!.getProperty("user")

if cvs(user$,3) = "" then
	mail! = new Email(host$, port)
else
	mail! = new Email(host$, port, user$, password$, smtp$)
fi

rem ' ****************************************************************
rem ' from is required, only a single from is supported
rem ' ****************************************************************
mail!.setFrom(from$)
rem ' make the from address the reply to address
mail!.setReplyTo(from$)

rem ' ****************************************************************
rem ' supports multiple to with a comma separated input string
rem ' example: buser@basis.cloud, xuser@basis.cloud, yuser@basis.cloud
rem ' to is required
rem ' ****************************************************************
toAddresses! = BBjAPI().makeVector()
while pos("," = to$)
	p = pos("," = to$)
	xTo$ = cvs(to$(1, p - 1), 3)
	to$ = cvs(to$(p + 1),3)
	rem ' mail!.addTo(xTo$) - this was the used until 1/6/2015, kew, new method added to take all the addresses in a BBjVector
	toAddresses!.add(xTo$)
wend
rem ' add the last one
rem ' mail!.addTo(to$)
toAddresses!.add(to$)
mail!.addTo(toAddresses!)

rem ' ****************************************************************
rem ' supports multiple cc with a comma separated input string
rem ' example: buser@basis.cloud, xuser@basis.cloud, yuser@basis.cloud
rem ' cc is not required
rem ' ****************************************************************
ccAddresses! = BBjAPI().makeVector()
if cvs(cc$,3) <> "" then
	while pos("," = cc$)
		p = pos("," = cc$)
		xCC$ = cvs(cc$(1, p - 1), 3)
		cc$ = cvs(cc$(p + 1),3)
		rem ' mail!.addCc(xCC$) - this was the used until 1/6/2015, kew, new method added to take all the addresses in a BBjVector
		ccAddresses!.add(xCC$)
	wend
	rem ' add the last one
	rem ' mail!.addCc(cc$)
	ccAddresses!.add(cc$)
	mail!.addCc(ccAddresses!)
fi

rem ' ****************************************************************
rem ' supports multiple bcc with a comma separated input string
rem ' example: buser@basis.cloud, xuser@basis.cloud, yuser@basis.cloud
rem ' bcc is not required
rem ' ****************************************************************
bccAddresses! = BBjAPI().makeVector()
if cvs(bcc$,3) <> "" then
	while pos("," = bcc$)
		p = pos("," = bcc$)
		xBcc$ = cvs(bcc$(1, p - 1), 3)
		bcc$ = cvs(bcc$(p + 1),3)
		rem ' mail!.addBcc(xBcc$)
		bccAddresses!.add(xBcc$)
	wend
	rem ' add the last one
	rem ' mail!.addBcc(bcc$)
	bccAddresses!.add(bcc$)
	mail!.addBcc(bccAddresses!)
fi

if cvs(subject$,3) <> "" then mail!.setSubject(subject$)

mail!.setSentDate(new Date())

mail!.addText(msgtxt$)

rem ' ****************************************************************
rem ' supports multiple files to be attached with a | separated input string
rem ' example: "/mnt/data/fileStore/fileA.xls|/mnt/data/acct/fileB.csv
rem ' a file list is not required
rem ' ****************************************************************
if cvs(file$,3) <> "" then
	filelist$ = file$
	while len(filelist$)
		spos = pos("|"=filelist$)
		if spos then
			sfile$ = filelist$(1,spos - 1)
			filelist$ = filelist$(spos + 1)
			mail!.addFile(cvs(sfile$,3))
		else
			sfile$ = filelist$
			mail!.addFile(cvs(sfile$,3))
			filelist$ = ""
		fi
	wend
fi

mail!.setHeader("X-Mailer","BBj")

mail!.send()

exit

errorRoutine:
	errmsg$ = "An error " + str(err) + " occurred on line: " + str(tcb(5)) + " of sendEmail.src. " + errmes(-1)
	throw errmsg$, 252
	exit
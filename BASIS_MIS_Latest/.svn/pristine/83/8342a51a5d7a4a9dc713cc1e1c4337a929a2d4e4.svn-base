0010 REM "WOS - Work Order Entry - Forward/Backward/Manual Scheduling"
0020 REM "Program:  WOE.AK"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "31Oct96 JJD: When scheduling manually, do not skip materials when there are no operation requirements. (4190,4200)"
0032 REM "31Oct98 WGH: Lead Time may be wrong for items stocked by warehouse (1270)
0040 REM 
0055 REM "L9=MAX LINES, L1=FIRST LINES, L0=HEADER LINE, L=CURRENT LINE
0085 SETESC 9000
0090 SETERR 9000
0095 PRECISION IVS01_PRECISION
0100 REM " --- Open/Lock Files
0200 REM " --- IOLISTs"
0210 WOE01A: IOLIST A0$,A1$,A[ALL]
0220 WOE02A: IOLIST W0$,W1$,W[ALL]
0225 WOE22A: IOLIST W0$,W1$,W[ALL]
0230 OPCODEA: IOLIST L0$,L[ALL]
0240 WOM05A: IOLIST X0$,X1$,X2$,X[ALL]
0250 WOM06D: IOLIST SCHEDSORT$
0260 WOE23A: IOLIST W3O$,W31$,W3[ALL]
0500 REM " --- Init Data
0510 DIM W[13],SCHEDSORT$(18)
0520 LET L0=11,L1=12,L9=19,L=L1,STATUS$="00"
0530 IF A0$(15,1)<>"C" THEN GOTO 0700
0540 DIM MESSAGE$[1]
0550 LET MESSAGE$[0]="You cannot schedule a closed Work Order. <Enter> to continue."
0560 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
0570 GOTO 4700
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0720 PRINT @(0,L0),'CE'
0730 PRINT 'SB',@(2,L0-1),"Schedule Work Order",
0740 PRINT @(21,13),"Scheduling Method:",@(18,14),"Estimated Start Date:",@(13,15),"Estimated Completion Date:",
0750 PRINT 'SF'
0900 REM " --- Display Defaults"
0910 GOSUB 5000
1000 REM " --- Scheduling Method
1010 LET V0$="S",V1$="C",V2$=A1$(98,1),V3$="BFM",V4$="B=Backwards Scheduling   F=Forward Scheduling   M=Manual",V0=1,V1=40,V2=13,I0=0
1020 GOSUB 7000
1030 IF V3=4 AND POS(V$="BFM")=0 THEN GOTO 1000
1050 LET A1$(98,1)=V$
1090 GOTO 3900
1200 REM " --- Estimated Start Date
1210 IF A1$(98,1)="B" THEN GOTO 1290
1220 LET V0$="D",V1$="R",V2$=A0$(19,3),V3$="",V4$="",V0=3,V1=40,V2=14
1240 GOSUB 7000
1250 LET A0$(19,3)=V$
1260 IF POS(" "<>A0$(40,7))>0 THEN GOTO 1290
1270 CALL "SYC.CA",A0$(19,3),DATE$,LEADTIME
1280 IF DATE$<>"N" THEN LET A0$(22,3)=DATE$
1290 GOTO 3900
1400 REM " --- Estimated Completion Date
1410 IF A1$(98,1)="F" THEN GOTO 1490
1415 IF A1$(98,1)="M" THEN LET V2$=A0$(19,3) ELSE LET V2$=A0$(22,3)
1420 LET V0$="D",V1$="R",V3$="",V4$="",V0=8,V1=40,V2=15
1440 GOSUB 7000
1450 IF A1$(98,1)="M" AND V$<A0$(19,3) THEN GOTO 1400
1460 LET A0$(22,3)=V$
1490 GOTO 3900
1600 REM " --- Calculate Estimated Start/Completion Date"
1610 LET STATUS$="00"
1620 IF A1$(98,1)="M" THEN GOTO 1710
1630 IF A1$(98,1)="F" THEN LET F_DATE$=A0$(19,3) ELSE LET F_DATE$=A0$(22,3)
1640 CALL "WOC.AA",A0$(5,7),F_DATE$,NEW_DATE$,A1$(98,1),OPCODE_DEV,STATUS$
1650 IF STATUS$(1,1)="1" THEN GOSUB 2100
1660 IF STATUS$(2,1)="0" THEN GOTO 1690
1670 GOSUB 1800
1680 GOTO 1710
1690 IF A1$(98,1)="F" THEN LET A0$(22,3)=NEW_DATE$ ELSE LET A0$(19,3)=NEW_DATE$
1700 PRINT @(40,14),FNB$(A0$(19,3)),@(40,15),FNB$(A0$(22,3)),
1710 GOTO 3900
1800 REM " --- Error In Calculating Date " 
1810 DIM MESSAGE$[4]
1820 ON POS(STATUS$(2,1)="135") GOTO 2160,1830,1930,1880
1830 LET MESSAGE$[0]="An Unscheduled Date For An Operation Code Was Found"
1840 LET MESSAGE$[1]="During The Calculation Process. Correct The Calendar"
1850 LET MESSAGE$[2]="Or Change The Scheduling Method To Manual."
1860 LET FIELDS=2
1870 GOTO 1990
1880 LET MESSAGE$[0]="An Unscheduled Date For A Subcontract Was Found"
1890 LET MESSAGE$[1]="During The Calculation Process. Correct The Calendar"
1900 LET MESSAGE$[2]="Or Change The Scheduling Method To Manual."
1910 LET FIELDS=2
1920 GOTO 1990
1930 LET MESSAGE$[0]="A File Was Found To Be Missing During The Calculation"
1940 LET MESSAGE$[1]="Process. Make Sure That The Calendar Has Been Created"
1950 LET MESSAGE$[2]="And There Are Operations For The Current Work Order."
1960 LET FIELDS=2
1970 GOTO 1990
1980 LET MESSAGE$[0]="An Error Has Occurred During The Calculation Process.",MESSAGE$[1]="Date Will Not Be Calculated.",FIELDS=1
1990 CALL "SYC.XA",1,MESSAGE$[ALL],FIELDS,22,-1,V$,V3
2000 RETURN 
2100 REM " --- Subcontract Date Changed"
2110 DIM MESSAGE$[2]
2120 LET MESSAGE$[0]="A Date Required For A Subcontract Line Has Changed. You"
2130 LET MESSAGE$[1]="Should Check The Associated Purchase Orders And Requisitions"
2140 LET MESSAGE$[2]="And Change Those Dates As Necessary."
2150 CALL "SYC.XA",2,MESSAGE$[ALL],2,22,-1,V$,V3
2160 RETURN 
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,4000
4000 REM " --- Overlay"
4020 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4040 IF V3=4 THEN GOTO 4700
4060 IF V$="N" THEN GOTO 1000
4080 IF V$<>"Y" THEN GOTO 4000
4100 REM " --- Write Records"
4110 IF STATUS$(2,1)<>"0" THEN GOTO 4700
4120 REM " --- Write New Date to Work Order Master "
4130 WRITE (WOE01_DEV,KEY=A0$(1,11))IOL=WOE01A
4140 REM " --- If Not Manual Method Skip This Part "
4150 IF A1$(98,1)<>"M" THEN GOTO 4360
4160 GOSUB CLEAR_SCHEDULE
4170 REM " --- Process Operations Records "
4180 READ (WOE02_DEV,KEY=A0$(1,11)+"A",DOM=4190)
4190 LET K15$=KEY(WOE02_DEV,END=4360)
4200 IF POS(A0$(1,11)+"A"=K15$)<>1 THEN GOTO 4360
4210 EXTRACT (WOE02_DEV,KEY=K15$)IOL=WOE02A
4220 REM " --- New Date Required "
4230 LET W1$(4,3)=A0$(19,3)
4240 WRITE (WOE02_DEV,KEY=K15$)IOL=WOE02A
4250 DIM L[13],X[9],X1$(20),X2$(20)
4260 FIND (OPCODE_DEV,KEY=A0$(1,2)+W1$(1,3),DOM=4280)IOL=OPCODEA
4270 LET X[0]=L[3]
4280 LET X0$=A0$(1,2)+W1$(1,3)+W1$(4,3)+W0$(5,7)+W0$(13,3)
4290 LET SCHEDSORT$=X0$(1,2)+X0$(9,10)+X0$(3,6)
4300 LET X[1]=W[8],X[2]=W[2]-W[8],X[3]=W[9]
4310 REM " --- Write Schedule Record "
4320 WRITE (WOM05_DEV,KEY=X0$)IOL=WOM05A
4330 REM " --- Write Schedule Sort Record "
4340 WRITE (WOM06_DEV,KEY=SCHEDSORT$)IOL=WOM06D
4350 GOTO 4190
4360 REM " --- Rewrite Date Required for Materials
4370 READ (WOE22_DEV,KEY=A0$(1,11)+"B",DOM=4380)
4380 LET K15$=KEY(WOE22_DEV,END=4500)
4390 IF POS(A0$(1,11)+"B"=K15$)<>1 THEN GOTO 4500
4400 EXTRACT (WOE22_DEV,KEY=K15$)IOL=WOE22A
4410 LET W1$(4,3)=A0$(19,3)
4420 WRITE (WOE22_DEV,KEY=K15$)IOL=WOE22A
4430 GOTO 4380
4500 REM " --- Change Material Commitment Date Required "
4510 DIM W30$(14),W31$(40),W3[4]
4520 READ (WOE23_DEV,KEY=A0$(1,11),DOM=4530)
4530 LET K23$=KEY(WOE23_DEV,END=4700)
4540 IF K23$(1,11)<>A0$(1,11) THEN GOTO 4700
4550 READ (WOE23_DEV,KEY=K23$)IOL=WOE23A
4560 LET W31$(4,3)=A0$(19,3)
4570 WRITE (WOE23_DEV,KEY=K23$)IOL=WOE23A
4580 GOTO 4530
4700 IF O0=0 THEN RUN "WOE.AM"
4710 LET O9=2
4720 RUN "WOE.AA"
5000 REM " --- Display"
5010 PRINT @(40,13),A1$(98,1),@(40,14),FNB$(A0$(19,3)),@(40,15),FNB$(A0$(22,3)),
5090 RETURN 
6700 REM " --- Remove Schedule Detail and Sort Records"
6710 CLEAR_SCHEDULE: 
6720 READ (WOM06_DEV,KEY=A0$(1,2)+A0$(5,7),DOM=6730)
6730 LET SCHEDSORT$=KEY(WOM06_DEV,END=6790)
6740 IF POS(A0$(1,2)+A0$(5,7)=SCHEDSORT$)<>1 THEN GOTO 6790
6750 REMOVE (WOM06_DEV,KEY=SCHEDSORT$)
6760 REM " --- Clear Schedule Record "
6770 REMOVE (WOM05_DEV,KEY=SCHEDSORT$(1,2)+SCHEDSORT$(13,6)+SCHEDSORT$(3,10),DOM=6780)
6780 GOTO 6730
6790 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

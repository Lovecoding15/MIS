0010 REM "SYM - Printer Maintenance (Detail Lines)"
0020 REM "Program SYM.GB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 SYM17A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1)
0500 REM " --- Initializations"
0510 DIM B0$(3),B1$(30),B2$(60),B3$(16)
0590 LET B0$(1)=A0$,L9=19,L=L0,K9$=""
0900 REM " --- Display any existing lines"
0910 GOSUB 5100
0920 IF Q1=0 THEN GOTO 1400
0990 GOTO 4000
1000 REM " --- Overflow"
1010 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L0),'LD','LD',
1020 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1110 LET K$=KEY(SYM17_DEV,END=1400)
1120 IF POS(A0$=K$)<>1 THEN GOTO 1400
1130 READ (SYM17_DEV)IOL=SYM17A
1190 GOTO 1490
1400 REM " --- Font code"
1410 LET W0=MAX(ASC(B0$(3))+1,49)
1415 IF W0>57 THEN IF W0<65 THEN LET W0=65
1420 IF W0>122 THEN LET W0=32
1425 LET V4$="Enter A 1 Character Option Code (<F3>=Lookup)"
1430 LET V0$="S",V1$="KERC",V2$=CHR(W0),V3$="",V0=1,V1=1,V2=L,I0=0
1435 GOSUB 7000
1440 IF V3=3 THEN GOSUB 6200
1445 IF V3=4 THEN PRINT @(V1,L)," "; GOTO 4000
1450 IF V$="" THEN GOTO 1400
1460 LET B0$(3)=V$
1480 FIND (SYM17_DEV,KEY=B0$,DOM=1600)IOL=SYM17A
1490 IF B0$<=K9$ THEN GOSUB 5400
1500 GOSUB 5000
1520 LET I0=2
1530 GOTO 3300
1600 REM " --- New Record"
1610 DIM B1$(30),B2$(60),B3$(16)
1620 LET B3$(1,1)="S"
1630 PRINT @(6,L),'CL',@(6,L+1),'CL',
2000 REM " --- Description"
2010 LET V4$="Enter A Description For This Option",I0=1
2020 LET V0$="S",V1$="C",V2$=B1$,V3$="",V0=30,V1=4,V2=L
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3200,2000,3200
2050 LET B1$(1)=V$
2090 GOTO 3200
2200 REM " --- Printer Font Hex Code"
2210 LET V4$="Enter The Hex Value (e.g. 0A) To Be Sent To The Printer For This Option"
2220 LET V0$="S",V1$="C",V2$=B2$,V3$="",V0=60,V1=4,V2=L+1
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3200,2200,3200
2250 CALL "SYC.HX",V$,STATUS
2260 IF STATUS THEN GOTO 2200
2300 REM 
2310 LET B2$(1)=V$
2320 PRINT @(V1,V2),B2$,
2390 GOTO 3200
2400 REM " --- Compressed or Standard"
2410 LET V4$="Does This Option Generate Standard Or Compressed Print (S/C)?"
2415 IF POS(B3$(1,1)="SC")=0 THEN LET B3$(1,1)="S"
2420 LET V0$="S",V1$="C",V2$=B3$(1,1),V0=1,V1=67,V2=L
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3200,2400,3200
2450 IF POS(V$="SC")=0 THEN GOTO 2400
2460 LET B3$(1,1)=V$
2490 GOTO 3200
3200 REM " --- Sequence Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,2000,2200,2400,3300
3300 REM " --- Line Correct?"
3310 LET V4$="<Enter>=Correct     <F2>=Change     <D>=Delete"
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=B0$,I0=4
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3300,3200,3300,3350
3350 IF V$="" THEN GOTO 3400
3360 IF V$="D" THEN GOTO 3700
3390 GOTO 3300
3400 REM " --- Write"
3410 WRITE (SYM17_DEV,KEY=B0$)IOL=SYM17A
3450 LET L=L+2
3460 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3700 REM " --- Remove"
3710 REMOVE (SYM17_DEV,KEY=B0$,DOM=3720)
3750 PRINT @(0,L),'CL',@(0,L+1),'CL',
3760 IF V3=4 THEN GOTO 4000
3790 GOTO 1000
4000 REM " --- Options"
4010 LET V4$="Is The Above Information Correct (Y/N/List/Review/Delete)?"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4100
4050 IF V$="L" OR V$="LIST" THEN GOTO 4400
4070 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4080 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run Header Overlay"
4190 RUN "SYM.GA"
4200 REM " --- Remove"
4210 READ (SYM17_DEV,KEY=A0$,DOM=4220)
4220 LET K$=KEY(SYM17_DEV,END=4260)
4230 IF POS(A0$=K$)<>1 THEN GOTO 4260
4240 REMOVE (SYM17_DEV,KEY=K$,DOM=4250)
4250 GOTO 4220
4260 REMOVE (SYM07_DEV,KEY=A0$,DOM=4270)
4290 RUN "SYM.GA"
4400 REM " --- List"
4410 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4610 READ (SYM17_DEV,KEY=A0$,DOM=4630)
4620 GOSUB 5400
4690 GOTO 1000
5000 REM " --- Display"
5010 PRINT @(1,L),'CL',@(1),B0$(3),@(4),B1$,@(67),B3$(1,1),@(4,L+1),B2$,
5090 RETURN 
5100 REM " --- List"
5105 LET Q1=0
5110 GOSUB 5400
5120 READ (SYM17_DEV,KEY=A0$,DOM=5130)
5130 LET K$=KEY(SYM17_DEV,END=5300)
5150 IF POS(A0$=K$)<>1 THEN GOTO 5300
5160 READ (SYM17_DEV)IOL=SYM17A
5190 IF L<=L9 THEN GOTO 5250
5200 REM 
5210 LET V4$="Press <Enter> To Continue"
5215 LET V0$="S",V1$="CE",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5230 IF V3=4 THEN GOTO 5300
5240 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+2,K9$=B0$,Q1=Q1+1
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5410 PRINT @(0,L0),'CE',
5440 LET L=L0
5490 RETURN 
6200 REM " --- Standard windowed lookup"
6210 LET RECORD$=A0$,CHANNEL=SYM17_DEV,TITLE$=A0$+" "+FNP$(A1$(1,30))+" Options"
6220 LET DESCRIPTION$[0]="ID",FIELD[0]=1,POSITION[0]=3,LENGTH[0]=1
6230 LET DESCRIPTION$[1]="Description",FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6240 LET ROW=L0+1,COLUMN=79-LENGTH[0]-LENGTH[1]-NUMBER*4,V$="NO COMPANY"
6280 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "POE - Purchase Order Receipt Entry (Header/Ribbon)"
0020 REM "Program POE.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Sep96 JAL: PO sequence # record is NOT required (423)"
0032 REM "19Sep96 JAL: Do not allow blank/zero receiver number (1405)"
0033 REM "14Oct96 WGH: Create POE-44 x-ref for 'S' Line Types (6530-6540)"
0034 REM "05Dec96 Caj: Error 26 @ 1405 if <F2> or <F3> from 'Is This the Correct PO' prompt (1405)
0035 REM "05Feb97 JJD: Don't allow Vendor/PO/Receiver combination if previously received and updated (105,147,155,195,620,635,645,647,655,670,675,680,685,690,1505,1600,1765-1768,6100-6170)"
0036 REM "04Mar97 WGH: Don't delete receipt when <F4> at ITAIC (4040)"
0037 REM "27Oct97 WGH: Retain message line and vendor part line in history (1767)"
0038 REM "04Feb98 WGH: Prevent accidentally duplicating a Receiver number (1050,1282,1550,3660,3750,3850,4040,4280)"
0039 REM "15Jun98 WGH: Prevent possible blank Receiver Number (2730,3705)"
0040 REM 
0050 SETERR 9000
0060 SETESC 9000
0070 ON O0 GOTO 0080,1000,4000,4200
0080 BEGIN 
0085 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=40
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0113 LET FILES$[1]="SYS-01",FILES$[2]="POE-02",FILES$[3]="POE-03"
0115 LET FILES$[4]="POE-04",FILES$[5]="POE-05",FILES$[6]="POE-12"
0117 LET FILES$[7]="POE-13",FILES$[8]="POE-14",FILES$[9]=""
0120 LET FILES$[10]="POE-32",FILES$[11]="POE-34",FILES$[12]="POE-42"
0125 LET FILES$[13]="POE-44",FILES$[14]="POE-54",FILES$[15]="POM-02"
0130 LET FILES$[16]="POM-03",FILES$[17]="POM-04",FILES$[18]="POS-10"
0135 LET FILES$[19]="APM-01",FILES$[20]="APM-02",FILES$[21]="APM-05"
0140 LET FILES$[22]="APM-09",FILES$[23]="APM-10",FILES$[24]="IVM-01"
0145 LET FILES$[25]="IVM-02",FILES$[26]="IVM-05",FILES$[27]="IVM-10"
0147 LET FILES$[28]="IVM-14",FILES$[29]="POT-04"
0155 CALL "SYC.DA",1,1,29,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0165 LET SYS01_DEV=CHANNELS[1],POE02_DEV=CHANNELS[2],POE03_DEV=CHANNELS[3]
0170 LET POE04_DEV=CHANNELS[4],POE05_DEV=CHANNELS[5],POE12_DEV=CHANNELS[6]
0173 LET POE13_DEV=CHANNELS[7],POE14_DEV=CHANNELS[8]
0175 LET POE32_DEV=CHANNELS[10],POE34_DEV=CHANNELS[11],POE42_DEV=CHANNELS[12]
0180 LET POE44_DEV=CHANNELS[13],POE54_DEV=CHANNELS[14],POM02_DEV=CHANNELS[15]
0183 LET POM03_DEV=CHANNELS[16],POM04_DEV=CHANNELS[17],POS10_DEV=CHANNELS[18]
0185 LET APM01_DEV=CHANNELS[19],APM02_DEV=CHANNELS[20],APM05_DEV=CHANNELS[21]
0190 LET APM09_DEV=CHANNELS[22],APM10_DEV=CHANNELS[23],IVM01_DEV=CHANNELS[24]
0193 LET IVM02_DEV=CHANNELS[25],IVM05_DEV=CHANNELS[26],IVM10_DEV=CHANNELS[27]
0195 LET IVM14_DEV=CHANNELS[28],POT04_DEV=CHANNELS[29]
0200 REM " --- IOLIST's"
0210 POE04A: IOLIST A0$(1),A1$(1),A2$(1),A3$,A[ALL]; REM "WA:200
0220 POE02A: IOLIST B0$(1),B1$(1),B2$,B3$,B[ALL]
0230 APM02A: IOLIST C0$(1),C1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,I5$,M1$,M2$,M3$,M4$,M5$
0280 POS01A: IOLIST P0$,P1$,P2$,P3$,P4$
0285 POS10N: IOLIST P0$,P[ALL]
0290 APS01A: IOLIST X$,P1$,P2$,X$,X$,M0$
0300 ARS01A: IOLIST X$,P5$,P6$,X$,X$,P7$
0320 POE34A: IOLIST X3$(1)
0350 POE54A: IOLIST X5$(1)
0360 POM02A: IOLIST Y0$(1),Y1$(1)
0380 POE12A: IOLIST R0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0390 POE14A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0400 REM " --- Parameters"
0405 DIM I[6],P[3],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO",AP$="N",AR$="N",SF$="N"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=POS01A
0423 FIND (POS10_DEV,KEY=N0$+"N",DOM=0425)IOL=POS10N
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 LET VENDLEN=NUM(P2$(1,2))
0435 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1)),I[2]=NUM(I2$(7,2))
0445 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0447 LET I[6]=NUM(I2$(3,2))
0450 CALL "SYC.VA","AR",INFO$[ALL]
0455 LET AR$=INFO$[20]
0460 IF AR$="Y" THEN CALL "SYC.VA","OP",INFO$[ALL]
0465 LET OP$=INFO$[20]
0470 CALL "SYC.VA","WO",INFO$[ALL]
0475 LET SF$=INFO$[20]
0480 CALL "GLC.BA",ERR=0490,SOURCE$,N2$,GLW11$,GL$,STATUS
0490 IF STATUS THEN GOTO 9900
0500 REM " --- Initializations"
0505 PRECISION I[1]
0510 DIM A0$(22),A1$(160),A2$(20),A[8],A$[2],O[5],MESSAGE$[2],VENDOR$(8); REM "WA:200
0520 DIM RIBBON[20],RIBBON$[20],RFILES[10],Y0$(4),Y1$(32),B0$(15),B1$(160)
0530 DIM B[8],R0$(18),W0$(25),W1$(48),W2$(32),W3$(22),W4$(40),W[12]
0550 LET A0$(1)=N0$,L0=12,INCREMENT=P[3],ACTION$="B",TYPE$="C",REC=0
0560 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),M5=LEN(M5$),P=10,LS$="N"
0565 IF POS(I3$(17,1)="LS") THEN LET LS$="Y"
0570 LET RIBBON[1]=22,RIBBON[2]=L0,RIBBON$[1]=N0$,RFILES[1]=APM05_DEV
0575 LET RFILES[2]=APM10_DEV,RFILES[3]=IVM10_DEV,RFILES[4]=POM04_DEV
0577 LET RIBBON$[2]=GL$
0580 CALL "SYC.RM",M1$,M1,12,15
0585 CALL "SYC.RM",M2$,M2,9,12
0590 CALL "SYC.RM",M3$,M3,9,12
0595 CALL "SYC.RM",M5$,M5,5,12
0600 REM " --- Additional File Opens"
0610 IF AR$="N" THEN GOTO 0630
0615 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0620 LET CUSTLEN=NUM(P6$(1,2)),FILES$[30]="ARM-01",FILES$[31]="ARM-03"
0630 IF OP$="N" THEN GOTO 0640
0635 LET FILES$[32]="ARE-03",FILES$[33]="ARE-13",FILES$[34]="ARE-33"
0640 IF SF$="N" THEN GOTO 0650
0645 LET FILES$[35]="WOE-01",FILES$[36]="WOE-04"
0647 LET FILES$[37]="WOE-22",FILES$[38]="WOE-32"
0650 IF LS$="N" THEN GOTO 0660
0655 LET FILES$[39]="POE-24",FILES$[40]="IVM-07"
0660 CALL "SYC.DA",1,30,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0665 IF STATUS THEN GOTO 9900
0670 LET ARM01_DEV=CHANNELS[30],ARM03_DEV=CHANNELS[31]
0675 LET ARE03_DEV=CHANNELS[32],ARE13_DEV=CHANNELS[33],ARE33_DEV=CHANNELS[34]
0680 LET WOE01_DEV=CHANNELS[35],WOE04_DEV=CHANNELS[36],WOE22_DEV=CHANNELS[37]
0685 LET WOE32_DEV=CHANNELS[38]
0690 LET POE24_DEV=CHANNELS[39],IVM07_DEV=CHANNELS[40]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,1,0,L0-3,0
0720 PRINT 'SB',@(1,3),"Vendor:",@(19),"Rec #:",@(35),"PO #:",@(50),"Purchase Address:",@(3,4),"Name:",@(0,5),"Address:",'SF',
0730 CALL "POC.RA",TYPE$,ACTION$,RIBBON$[ALL],RIBBON[ALL],RFILES[ALL],A$[ALL],A[ALL],V3
0800 REM " --- Resize masks and set print positions"
0810 IF FNP(M1,M2,M3,M5,P)>0 THEN GOTO 0900
0820 CALL "SYC.RM",M1$,M1,10,M1-1
0830 IF FNP(M1,M2,M3,M5,P)>0 THEN GOTO 0900
0840 CALL "SYC.RM",M2$,M2,6,M2-1
0850 IF FNP(M1,M2,M3,M5,P)>0 THEN GOTO 0900
0860 CALL "SYC.RM",M3$,M3,6,M3-1
0870 IF FNP(M1,M2,M3,M5,P)>0 THEN GOTO 0900
0880 CALL "SYC.RM",M5$,M5,5,M5-1
0890 GOTO 0800
0900 REM " --- Position files"
0910 LET O[5]=79-M1,O[4]=O[5]-M3,O[3]=O[4]-M2
0920 LET O[2]=O[3]-M2,O[1]=O[2]-M5,O[0]=O[1]-3
0970 READ (POE54_DEV,KEY=N0$,DOM=0980)
0980 READ (POE34_DEV,KEY=N0$,DOM=0990)
0990 READ (POE04_DEV,KEY=A0$,DOM=1000)
1000 REM " --- Vendor number"
1005 PRINT 'CF',
1010 DIM B0$(15),B1$(160),B[8]
1020 LET V2$="",V2$=KEY(POE04_DEV,END=1050)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3,6)
1050 LET V0$="M",V1$="KCE",V3$=P1$,V0=6,V1=9,V2=3,REC=0,NEXTREC=0
1055 LET V4$="Enter A Valid Vendor (<F1>=Receiver Number/<F2>=PO Number/<F3>=Lookup)"
1060 GOSUB 7000
1070 ON V3 GOTO 1100,2000,2500,1080,9900
1080 CALL "SYC.LV",V$
1090 IF V$="" THEN GOTO 0900
1100 REM " --- Retrieve and display vendor info"
1110 GOSUB 5200
1120 IF STATUS THEN GOTO 1000
1130 IF POS(VENDOR$=A0$)<>1 THEN LET A0$(1)=VENDOR$
1190 READ (POE04_DEV,KEY=A0$,DOM=1200)
1200 REM " --- Receiver Number"
1210 LET V2$="",V2$=KEY(POE04_DEV,END=1240)
1220 IF POS(VENDOR$=V2$)<>1 THEN LET V2$=""
1230 IF V2$<>"" THEN LET V2$=V2$(9,7)
1240 LET V4$="Enter A Valid Receiver Number (<F1>=Next Available/<F2>=Vendor/<F3>=Lookup)"
1250 LET V0$="Z",V1$="CKER",V3$="",V0=7,V1=26,V2=3
1260 GOSUB 7000
1270 ON V3 GOTO 1350,1280,1000,1300,9900
1280 CALL "POC.NA","A",TYPE$,POS10_DEV,N0$,V$
1282 LET NEXTREC=1
1285 PRINT @(26,3),V$,
1290 GOTO 1350
1300 REM " --- <F3> Lookup"
1310 CALL "POC.LA",TYPE$,A0$(3,6),V$
1320 IF V$="" THEN GOTO 1200
1350 REM " --- Find first entry for this vendor/receiver combination"
1360 LET A0$(1)=VENDOR$+V$
1370 READ (POE04_DEV,KEY=A0$,DOM=1380)
1380 LET K$=KEY(POE04_DEV,END=1400)
1390 IF POS(A0$(1,15)=K$)=1 THEN LET V$=K$(9)
1400 REM " --- Read/display receiver"
1405 IF NUM(V$,ERR=1200)=0 THEN GOTO 1200
1410 GOSUB 5400
1420 IF STATUS THEN GOTO 1500
1430 LET X$="Receiver",EXISTS=1,NOTNEW=1
1440 GOSUB 6000
1450 ON V3 GOTO 1460,1470,1400,1400,1200
1460 ON POS(V$="YN") GOTO 1400,3900,1200
1470 LET K$=KEY(POE04_DEV,END=1200)
1475 IF POS(VENDOR$=K$)<>1 THEN GOTO 1200
1480 LET V$=K$(9)
1490 GOTO 1400
1500 REM " --- New record?"
1505 LET NEW_RECVR=0
1510 CALL "SYC.YN",1,"Is This A New Record",2,V$,V3
1520 ON V3 GOTO 1530,1500,1500,1500,1550
1530 ON POS(V$="YN") GOTO 1500,1600,1550
1550 IF NEXTREC THEN CALL "POC.NA","D",TYPE$,POS10_DEV,N0$,A0$(9,7)
1590 GOTO 1200
1600 LET NEW_RECVR=1
1700 REM " --- Get purchase order being received"
1710 LET V4$="Enter The Purchase Order Being Received (<F3>=Lookup)"
1720 LET V0$="Z",V1$="C",V2$=A0$(16,7),V3$="",V0=7,V1=41,V2=3
1730 GOSUB 7000
1735 ON V3 GOTO 1750,1700,1550,1740,1550
1740 CALL "POC.LA","P",VENDOR$(3),V$
1745 IF V$="" THEN GOTO 1700
1750 LET A0$(16)=V$,B0$(1)=A0$(1,8)+V$
1760 FIND (POE02_DEV,KEY=B0$,DOM=1700)IOL=POE02A
1765 IF NEW_RECVR=0 THEN GOTO 1770
1766 READ (POT04_DEV,KEY=A0$(1,8)+A0$(16,7)+A0$(9,7),DOM=1770)
1767 GOSUB 6100; IF V$="SPECIAL" THEN GOTO 1770
1768 GOTO 1200
1770 LET A1$(1)=B1$,A[ALL]=B[ALL]
1780 LET A2$(1)=B2$; REM "WA:200
1790 GOSUB 5000
1800 REM " --- Create receipt from specified purchase order?"
1810 CALL "SYC.YN",1,"Is This The Correct Purchase Order",2,V$,V3
1820 ON V3 GOTO 1830,1800,1700,1800,1700
1830 ON POS(V$="YN") GOTO 1800,1840,1700
1840 LET EXISTS=1,NOTNEW=0
1850 GOSUB 6500
1900 REM " --- Display receiver"
1920 GOSUB 5000
1990 GOTO 3900
2000 REM " --- Retrieve record using only receiver number"
2005 PRINT 'CF',
2010 LET V2$="",V2$=KEY(POE54_DEV,END=2040)
2020 IF POS(N0$=V2$)<>1 THEN LET V2$=""
2030 IF V2$<>"" THEN LET V2$=V2$(3,7)
2040 LET V4$="Enter A Valid Receiver Number (<F1>=Purchase Order/<F2>=Vendor/<F3>=Lookup)"
2050 LET V0$="Z",V1$="CKER",V3$="",V0=7,V1=26,V2=3
2060 GOSUB 7000
2070 ON V3 GOTO 2100,2500,1000,2080,9900
2080 CALL "POC.LB",TYPE$,V$
2085 IF V$="" THEN GOTO 2000
2090 LET K$=V$
2095 GOTO 2300
2100 REM " --- Position purchase order xref"
2110 LET X$=N0$+V$
2190 READ (POE54_DEV,KEY=X$,DOM=2200)
2200 REM " --- Read next xref"
2210 LET K$=KEY(POE54_DEV,END=2000)
2220 IF POS(X$=K$)<>1 THEN GOTO 2000
2230 READ (POE54_DEV)
2300 REM " --- Read/display purchase order"
2310 LET V$=K$(3,6)
2320 GOSUB 5200
2330 IF STATUS THEN GOTO 2000
2340 LET V$=K$(9)
2350 GOSUB 5400
2360 IF STATUS THEN GOTO 2000
2400 REM " --- Right purchase order?"
2410 LET X$="Receiver"
2420 GOSUB 6000
2430 ON V3 GOTO 2440,2450,2400,2400,2000
2440 ON POS(V$="YN") GOTO 2400,3900,2000
2450 LET X$=N0$
2490 GOTO 2200
2500 REM " --- Retrieve record using only purchase order number"
2505 PRINT 'CF',
2510 LET V2$="",V2$=KEY(POE34_DEV,END=2540)
2520 IF POS(N0$=V2$)<>1 THEN LET V2$=""
2530 IF V2$<>"" THEN LET V2$=V2$(3,7)
2540 LET V4$="Enter A Valid Purchase Order Number (<F1>=Vendor/<F2>=Receiver/<F3>=PO Lookup)"
2550 LET V0$="Z",V1$="CKER",V3$="",V0=7,V1=41,V2=3
2560 GOSUB 7000
2570 ON V3 GOTO 3300,1000,2000,2600,9900
2600 REM " --- PO Lookup"
2610 CALL "POC.LB",TYPE$,V$
2620 IF V$="" THEN GOTO 2500
2630 LET X$=V$,V$=V$(3,6)
2640 GOSUB 5200
2650 IF STATUS THEN GOTO 2500
2660 LET V$=X$(9)
2670 GOSUB 5400
2680 IF STATUS THEN GOTO 2500
2700 REM " --- Right purchase order?"
2710 LET V4$="Purchase Order",X$=""
2720 GOSUB 6000
2730 ON V3 GOTO 2740,2700,2700,2700,2500
2740 ON POS(V$="YN") GOTO 2750,3900,2500
2800 REM " --- Position receiver xref"
2810 LET X$=N0$+V$
2890 READ (POE34_DEV,KEY=X$,DOM=2900)
2900 REM " --- Get next receiver/purchase order xref"
2910 LET K$=KEY(POE34_DEV,END=3300)
2920 IF POS(X$=K$)<>1 THEN GOTO 3300
2930 READ (POE34_DEV)
3000 REM " --- Read/display purchase order info"
3010 LET V$=K$(10)
3020 GOSUB 5200
3030 IF STATUS THEN GOTO 2500
3040 LET V$=K$(3,7)+K$(16,7)
3050 GOSUB 5400
3060 IF STATUS THEN GOTO 2500
3100 REM " --- Right purchase order?"
3110 LET X$="Purchase Order"
3120 GOSUB 6000
3130 ON V3 GOTO 3140,3150,3100,3100,2500
3140 ON POS(V$="YN") GOTO 3600,3900,2500
3150 LET K$=KEY(POE34_DEV,END=2500)
3160 IF POS(N0$=K$)<>1 THEN GOTO 2500
3170 READ (POE34_DEV)
3190 GOTO 3000
3200 REM " --- Purchase order lookup"
3210 CALL "POC.LA","P",VENDOR$,V$
3220 IF V$="" THEN GOTO 2500
3300 REM " --- Read purchase order"
3310 READ (POE32_DEV,KEY=N0$+V$,DOM=3320)
3320 LET K$=KEY(POE32_DEV,END=2500)
3330 IF POS(N0$+V$=K$)<>1 THEN GOTO 2500
3340 LET B0$(1)=N0$+K$(10)+K$(3),X$=N0$+B0$(9,7)+B0$(3,6)
3350 FIND (POE02_DEV,KEY=B0$,DOM=2500)IOL=POE02A
3360 READ (POE34_DEV,KEY=X$,DOM=3370)
3370 LET K$=KEY(POE34_DEV,END=3400)
3375 IF POS(X$=K$)<>1 THEN GOTO 3400
3380 LET K$=K$(1,2)+K$(16,7)+K$(10,6)+K$(3,7)
3390 GOTO 3000
3400 REM " --- Display vendor/purchase order"
3410 LET A0$(1,8)=B0$,A0$(16,7)=B0$(9,7),V$=A0$(3,6)
3420 GOSUB 5200
3430 IF STATUS THEN GOTO 2500
3440 LET A1$(1)=B1$
3450 GOSUB 5000
3500 REM " --- Create receipt from specified purchase order?"
3510 CALL "SYC.YN",1,"Is This The Correct Purchase Order",2,V$,V3
3520 ON V3 GOTO 3530,3500,2500,3500,2500
3530 ON POS(V$="YN") GOTO 3500,3540,2500
3600 REM " --- Receiver number"
3610 LET V4$="Enter A Valid Receiver Number (<F1>=Next Available)"
3620 LET V0$="Z",V1$="C",V2$=A0$(9),V3$="",V0=7,V1=26,V2=3
3630 GOSUB 7000
3640 ON V3 GOTO 3700,3650,4200,3600,2500
3650 CALL "POC.NA","A",TYPE$,POS10_DEV,N0$,V$
3660 LET NEXTREC=1
3700 REM " --- Receiver number being used?"
3705 IF NUM(V$,ERR=3600)=0 THEN GOTO 3600
3710 LET A0$(9,7)=V$
3715 PRINT @(26,3),A0$(9,7),
3720 FIND (POE04_DEV,KEY=A0$,DOM=3800)
3730 LET MESSAGE$[0]="This Receiver Number Already Exists (<Enter>=Continue)"
3740 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
3750 IF NEXTREC THEN CALL "POC.NA","D",TYPE$,POS10_DEV,N0$,A0$(9,7)
3790 GOTO 3600
3800 REM " --- Create receiver"
3810 LET EXISTS=1
3820 FIND (POE02_DEV,KEY=B0$,DOM=3850)IOL=POE02A
3830 GOSUB 6500
3840 GOTO 3900
3850 IF NEXTREC THEN CALL "POC.NA","D",TYPE$,POS10_DEV,N0$,A0$(9,7)
3890 GOTO 2500
3900 REM " --- Process ribbon"
3910 LET ACTION$="I",A$[0]=A0$,A$[1]=A1$,RIBBON[3]=EXISTS
3915 LET A$[2]=A2$; REM "WA:200
3920 CALL "POC.RA",TYPE$,ACTION$,RIBBON$[ALL],RIBBON[ALL],RFILES[ALL],A$[ALL],A[ALL],V3
3930 IF V3=2 THEN GOTO 1550
3940 LET A0$(1)=A$[0],A1$(1)=A$[1]
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$),EXISTS=0
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4000
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,3900
4100 REM " --- Update header"
4110 LET O0=0
4120 WRITE (POE04_DEV,KEY=A0$)IOL=POE04A
4180 IF NOTNEW THEN RUN "POE.DC"
4190 RUN "POE.DB"
4200 REM " --- Delete receipt"
4210 DIM FILES[20],PARAMS[10],PARAMS$[10],B0$(15),B1$(160),B[8]
4220 LET FILES[1]=POS10_DEV,FILES[2]=POM02_DEV,FILES[3]=POE04_DEV
4230 LET FILES[4]=POE14_DEV,FILES[5]=POE24_DEV,FILES[6]=POE34_DEV
4240 LET FILES[7]=POE44_DEV,FILES[8]=POE54_DEV,B0$(1)=A0$(1,8)+A0$(16)
4250 LET PARAMS$[0]=N0$,PARAMS$[1]=SF$,PARAMS$[2]=I2$
4260 LET PARAMS$[3]=I3$,PARAMS$[4]=I4$,PARAMS[0]=I[1]
4270 CALL "POC.EA",TYPE$,FILES[ALL],PARAMS[ALL],PARAMS$[ALL],A0$,A1$,STATUS
4280 IF NEXTREC THEN CALL "POC.NA","D",TYPE$,POS10_DEV,N0$,A0$(9,7)
4290 READ (POE12_DEV,KEY=B0$,DOM=4300)
4300 REM " --- Reset WIP, approved and rejected quantities on PO detail"
4310 LET K$=KEY(POE12_DEV,END=4400)
4320 IF POS(B0$=K$)<>1 THEN GOTO 4400
4330 READ (POE12_DEV)IOL=POE12A
4340 LET W[4]=0,W[5]=0,W[6]=0
4350 WRITE (POE12_DEV,KEY=K$)IOL=POE12A
4390 GOTO 4300
4400 REM " --- Done with delete"
4480 IF REC THEN GOTO 1200
4490 GOTO 1000
5000 REM " --- Display record"
5010 PRINT @(26,3),A0$(9,7),@(41,3),A0$(16,7),
5020 LET ACTION$="D",A$[0]=A0$,A$[1]=A1$
5025 LET A$[2]=A2$; REM "WA:200
5030 CALL "POC.RA",TYPE$,ACTION$,RIBBON$[ALL],RIBBON[ALL],RFILES[ALL],A$[ALL],A[ALL],V3
5090 RETURN 
5200 REM " --- Display vendor and comments"
5210 LET VENDOR$(1)=N0$+V$
5220 CALL "POC.VA","V",VENDOR$,VENDLEN,M0$,APM01_DEV,APM09_DEV,P3$,L0,STATUS
5290 RETURN 
5400 REM " --- Read/display purchase order"
5410 LET A0$(1)=VENDOR$+V$,STATUS=11
5420 FIND (POE04_DEV,KEY=A0$,DOM=5490)IOL=POE04A
5430 LET STATUS=0
5440 GOSUB 5000
5490 RETURN 
6000 REM " --- Right PO/Requisition?"
6010 LET V0$="Y",V1$="C",V4$="Is This The Correct Purchase Order (Y/N"
6020 IF X$<>"" THEN LET V4$=V4$+"/<F1>=Next "+X$
6030 LET V4$=V4$+")?",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22,EXISTS=1,PRINTIT=0
6040 GOSUB 7000
6090 RETURN 
6100 REM " --- Previously used vendor+po+receiver "
6110 LET NUMBER=2
6120 DIM MESSAGES$[NUMBER]
6130 LET MESSAGES$[0]="Receiver # "+A0$(9,7)+" has been previously used"
6140 LET MESSAGES$[1]="for this Vendor and this Purchase Order, please"
6150 LET MESSAGES$[2]="use another receiver #."
6160 CALL "SYC.XA",2,MESSAGES$[ALL],NUMBER,22,-1,V$,V3
6170 RETURN 
6500 REM " --- Create receiver from purchase order"
6510 DIM FILES[10],PARAMS$[10]
6520 LET FILES[0]=POE12_DEV,FILES[1]=POE04_DEV,FILES[2]=POE14_DEV
6530 LET FILES[3]=POE34_DEV,FILES[4]=POE54_DEV,FILES[5]=POM02_DEV
6540 LET FILES[10]=POE44_DEV,PARAMS$[0]=F0$
6550 CALL "POC.MA",TYPE$,FILES[ALL],PARAMS$[ALL],A0$,A1$,A[ALL],B0$,B1$,B[ALL],I[1],STATUS
6590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8070 DEF FNP(Q1,Q2,Q3,Q4,Q5)=(80-(Q1+(Q2*2)+Q3+Q4+20))-Q5
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

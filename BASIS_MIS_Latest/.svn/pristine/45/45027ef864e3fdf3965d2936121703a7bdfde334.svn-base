0010 REM "POC - Requisition/Purchase Order/Receiver Lookup By Vendor"
0020 REM "Program POC.LA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "TYPE$     : Lookup type (input)"
0050 REM "            R=Requisition/P=Purchase Order"
0055 REM "            Q=QA Receiver/C=PO Receiver"
0060 REM "VENDOR$   : Vendor Number (input)"
0065 REM "SELECTION$: Requisition/Purchase Order/Receiver Number (returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER TYPE$,VENDOR$,SELECTION$
0100 REM " --- Open Files"
0105 LET FILES=7
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="SYS-01",FILES$[2]="SYM-06",FILES$[3]="IVM-10"
0120 LET FILES$[4]="POM-02",FILES$[5]="POE-01",FILES$[6]="POE-11"
0125 ON POS(TYPE$="RPQCH") GOTO 9900,0170,0130,0140,0150,0160
0130 LET FILES$[5]="POE-02",FILES$[6]="POE-12",FILES$[7]="POE-52"
0135 GOTO 0170
0140 LET FILES$[5]="POE-03",FILES$[6]="POE-13",FILES$[7]="POE-53"
0145 GOTO 0170
0150 LET FILES$[5]="POE-04",FILES$[6]="POE-14",FILES$[7]="POE-54"
0155 GOTO 0170
0160 LET FILES$[5]="POT-04",FILES$[6]="POT-14"
0170 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0175 IF STATUS THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1],SYM06_DEV=CHANNELS[2],IVM10_DEV=CHANNELS[3]
0185 LET POM02_DEV=CHANNELS[4],HEADER_DEV=CHANNELS[5],DETAIL_DEV=CHANNELS[6]
0190 LET XREF_DEV=CHANNELS[7]
0200 REM " --- IOLIST's"
0210 POE01A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0220 POE11A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0230 POM02A: IOLIST Y0$(1),Y1$(1)
0240 IVM10C: IOLIST X0$(1)
0250 SYM06A: IOLIST *,COMPANY$
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM P[2]
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0425 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),SPEEDSEARCH$="N",M7$="(###)-###-####"
0440 LET X$=STBL("!SPEEDSEARCH",ERR=0450),N0$=X$(1,2),SPEEDSEARCH$="Y"
0450 FIND (SYM06_DEV,KEY=N0$,DOM=9800)IOL=SYM06A
0460 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0470 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(5,1)),KEYLEN=15
0480 IF POS(TYPE$="QCH") THEN LET KEYLEN=22
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM A0$(KEYLEN),A1$(160),A[8],W0$(18),X0$(160),Y0$(4),Y1$(32),O[5]
0520 DIM W0$(KEYLEN+3),W1$(48),W2$(32),W3$(22),W4$(40),W[12]
0540 LET WIDTH=80,HEIGHT=12,WIN_X=0,WIN_Y=5,MAX_ROW=HEIGHT-5
0545 LET SWIDTH=WIDTH,SHEIGHT=4,SWIN_X=WIN_X,SWIN_Y=WIN_Y+HEIGHT,R$="Req #"
0550 LET OPTIONS=0,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3,M1=LEN(M1$),SELECTION$=""
0555 LET LABEL$="Purchase Order",N$="PO #",D$="Ordered",PGM$=PGM(-2)
0565 IF M1<12 THEN CALL "SYC.RM",M1$,M1,12,15
0570 LET O[5]=WIDTH-1-M1,O[4]=O[5]-8
0575 IF TYPE$="R" THEN LET O[4]=O[5]
0580 LET O[3]=O[4]-6,O[0]=2,O[1]=10,O[2]=19
0600 REM " --- Initializations based on lookup type"
0605 ON POS(TYPE$="PRQCH") GOTO 9900,0650,0610,0620,0630,0640
0610 LET LABEL$="Requisition",PGM$="POC.LR"
0615 LET N$="Req #",D$="Req Date",R$=""
0617 GOTO 0650
0620 LET LABEL$="Quality Assurance Receiver",PGM$="POC.LY"
0625 LET N$="Rec #",D$="Rec Date",R$="PO #"
0627 GOTO 0650
0630 LET LABEL$="Purchase Order Receiver",PGM$="POC.LY"
0635 LET N$="Rec #",D$="Rec Date",R$="PO #"
0637 GOTO 0650
0640 LET LABEL$="Purchase Order Receipt",PGM$="POC.LH"
0645 LET N$="PO #",D$="Rec Date",R$="Rec #"
0650 REM " --- Window arrays"
0655 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0660 LET HEADING$(O[0])=N$,HEADING$(O[1])=D$,HEADING$(O[2])="Warehouse"
0665 LET HEADING$(O[3])="Terms",HEADING$(O[4])=R$,DW=MIN(O[3]-O[2]-4,24)
0670 LET HEADING$(WIDTH-13)="Open Amount",TITLE$=LABEL$+" Lookup"
0675 IF TYPE$="H" THEN LET HEADING$(WIDTH-14)="Amount Rec'd"
0690 LET STITLE$=LABEL$+" Detail",FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Which search option?"
0810 DIM STRINGS$[10],NUMBERS[10]
0820 LET STRINGS$[0]=PGM$,STRINGS$[1]="",NUMBERS[0]=MAXROW
0830 LET NUMBERS[1]=OWIN_X,NUMBERS[2]=OWIN_Y,NUMBERS[3]=P[0]
0840 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
0850 CALL "SYC.SM",STRINGS$[ALL],NUMBERS[ALL],FKEY
0860 IF FKEY THEN GOTO 9900
0870 LET PREFIX$=STRINGS$[2],START$=STRINGS$[3],METHOD$=STRINGS$[4]
0880 LET V3$=STRINGS$[5],V0=NUMBERS[4]
0900 REM " --- Detail Window"
0910 CALL "SYC.WA",0,SWIDTH,SHEIGHT,SWIN_X,SWIN_Y,STITLE$,SNAME$
0920 CALL "SYC.WB",NAME$,0
0930 PRINT 'CF',
0940 IF SPEEDSEARCH$<>"Y" THEN GOTO 1000
0950 REM " --- Display SpeedSearch Company"
0955 DIM X$(WIDTH+SWIDTH)
0960 LET X$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
0965 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
0970 CALL "SYC.WA",0,WIDTH+SWIDTH,3,WIN_X,WIN_Y+HEIGHT+SHEIGHT,"",CNAME$
0975 PRINT @(0,0),'BR',X$,'ER',
0980 CALL "SYC.WB",NAME$,0
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,SHEIGHT-2]
1020 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$=""
1030 IF PREFIX$="" THEN GOTO 1200
1040 IF POS(PREFIX$="*IW") THEN GOTO 1300
1050 IF PREFIX$="P" THEN GOTO 1150
1100 REM " --- PO'S by Requisition"
1110 READ (XREF_DEV,KEY=N0$+START$,DOM=1400)
1140 GOTO 1400
1150 REM " --- Receivers by Receiver Number"
1160 READ (XREF_DEV,KEY=N0$+START$,DOM=1400)
1190 GOTO 1400
1200 REM " --- Req/PO Number"
1210 READ (HEADER_DEV,KEY=N0$+VENDOR$+START$,DOM=1400)IOL=POE01A
1220 LET K$=A0$
1290 GOTO 1900
1300 REM " --- Keyword/Item/Warehouse"
1310 IF PREFIX$="*" THEN LET START$=FNU$(START$)
1320 READ (HEADER_DEV,KEY=N0$+VENDOR$,DOM=1400)
1340 GOTO 1400
1400 REM " --- Read next record"
1410 IF PREFIX$="P" THEN GOTO 1600
1420 IF PREFIX$<>"R" THEN GOTO 1800
1500 REM " --- Next POE-52 sort record"
1510 LET K$=KEY(XREF_DEV,END=3000)
1520 IF POS(N0$=K$)<>1 THEN GOTO 3000
1540 READ (XREF_DEV)
1550 IF K$(10,6)<>VENDOR$ THEN GOTO 2900
1580 LET K$=N0$+K$(10)
1590 GOTO 1900
1600 REM " --- Next POE-53/POE-54 sort record"
1610 LET K$=KEY(XREF_DEV,END=3000)
1620 IF POS(N0$=K$)<>1 THEN GOTO 3000
1630 READ (XREF_DEV)
1640 IF K$(10,6)<>VENDOR$ THEN GOTO 2900
1650 LET K$=N0$+K$(10,6)+K$(3,7)+K$(16,7)
1690 GOTO 1900
1800 REM " --- Next header key"
1810 LET K$=KEY(HEADER_DEV,END=3000)
1820 IF POS(N0$+VENDOR$=K$)<>1 THEN GOTO 3000
1900 REM " --- Read header record"
1905 LET LOCKED=0
1910 READ (HEADER_DEV,KEY=K$,ERR=1950)IOL=POE01A
1920 IF POS(PREFIX$="*W")=0 THEN GOTO 2000
1930 IF PREFIX$="*" THEN IF START$<>"" THEN IF POS(START$=FNU$(A1$))=0 THEN GOTO 2900
1935 IF PREFIX$="W" THEN IF START$<>"" THEN IF A1$(1,2)<>START$ THEN GOTO 2900
1940 GOTO 2000
1950 REM " --- Error reading header record"
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1980 LET LOCKED=1
2000 REM " --- Position detail file to accumulate open amount"
2010 LET AMOUNT=0,FOUND=0,Y0$(1)="",Y1$(1)=""
2090 READ (DETAIL_DEV,KEY=A0$,DOM=2100)
2100 REM " --- Read next detail"
2110 LET K$=KEY(DETAIL_DEV,END=2300)
2120 IF POS(A0$=K$)<>1 THEN GOTO 2300
2130 READ (DETAIL_DEV)IOL=POE11A
2140 IF PREFIX$="I" THEN IF FNP$(W3$(3))=START$ THEN LET FOUND=1
2150 IF W0$(1,2)+W1$(1,2)=Y0$ THEN GOTO 2160
2155 FIND (POM02_DEV,KEY=W0$(1,2)+W1$(1,2),DOM=2100)IOL=POM02A
2160 IF POS(Y1$(21,1)="MV") THEN GOTO 2100
2170 LET COST=W[1],QTY=W[3]-W[7]
2175 IF TYPE$="R" THEN LET QTY=W[2]-W[3]
2180 IF TYPE$="H" THEN LET QTY=W[7]
2185 IF POS(TYPE$="QC") THEN LET QTY=W[3]-W[4]-W[7]
2190 IF Y1$(21,1)="O" THEN LET QTY=1
2200 PRECISION 2
2210 LET AMOUNT=AMOUNT+(COST*QTY)
2220 PRECISION P[1]
2290 GOTO 2100
2300 REM " --- Display page?"
2310 IF PREFIX$="I" THEN IF FOUND=0 THEN GOTO 2900
2320 IF L<MAX_ROW THEN GOTO 2400
2330 LET XMODE=5
2340 GOSUB 6200
2350 IF FKEY=1 THEN GOTO 0800
2360 IF FKEY=4 THEN GOTO 9900
2370 IF SELECTION$<>"" THEN GOTO 4000
2400 REM " --- Lookup window record"
2410 DIM G$(WIDTH-2)
2420 LET G$(O[0])=A0$(9,7),G$(O[1])="Record In Use",WHEN$=A1$(5,6)
2425 IF TYPE$="H" THEN LET WHEN$=A1$(29,6)
2430 IF LOCKED THEN GOTO 2490
2440 IF X0$(4,2)=A1$(1,2) THEN GOTO 2460
2450 LET X0$(1)=N0$+"C"+A1$(1,2)+"(Not On File)"
2455 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=2460)IOL=IVM10C
2460 LET G$(O[1])=FNB6$(WHEN$),G$(O[2])=X0$(4,2)+" "+X0$(6,DW)
2465 LET G$(O[3])=A1$(38,2),G$(O[4])=A1$(112,7)
2480 IF POS(TYPE$="HQC") THEN LET G$(O[4])=A0$(16,7)
2485 LET G$(O[5])=STR(AMOUNT:M1$)
2490 LET LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=A0$(9,7)
2600 REM " --- Detail window record"
2610 LET G$(1)="{Required}"
2615 IF FNP$(A1$(23,6))<>"" THEN LET G$(11)=FNB6$(A1$(23,6))
2620 LET G$(19)="{Not Before}"
2625 IF FNP$(A1$(17,6))<>"" THEN LET G$(31)=FNB6$(A1$(17,6))
2630 LET G$(44)="{Freight Terms}"+A1$(44,15),MORE$[PAGE,X1,1]=G$
2635 LET G$(1)="{Promised}"
2640 IF FNP$(A1$(11,6))<>"" THEN LET G$(11)=FNB6$(A1$(11,6))
2645 LET G$(21)="{Ship Via}"+A1$(59,15)
2660 LET G$(44)=" {Acknowledged}"+A1$(74,20),MORE$[PAGE,X1,2]=G$
2680 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),LLIST$[PAGE,X1,5]=SNAME$
2690 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2910 IF LOCKED=0 THEN GOTO 1400
2920 LET LOCKED=0
2950 READ (HEADER_DEV,KEY=K$(1,14)+$FF$,DOM=1400,END=3000)
2990 GOTO 1400
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3035 LET MESSAGE$[0]="No "+LABEL$+"s Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page"
3510 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
8000 REM " --- Functions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNU$(Q$)=CVS(Q$,4)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 FOR X=1 TO FILES
9915 CLOSE (CHANNELS[X],ERR=9916)
9920 NEXT X
9925 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9930 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
9935 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9940 IF SELECTION$="END" THEN LET SELECTION$=""
9945 IF SELECTION$="PRIOR" AND SPEEDSEARCH$="N" THEN LET SELECTION$=""
9950 EXIT 
9999 END

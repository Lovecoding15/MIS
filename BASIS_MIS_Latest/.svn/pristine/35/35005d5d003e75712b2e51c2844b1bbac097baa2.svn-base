0010 REM "APR - Vendor Detail Listing"
0020 REM "Program APR.XA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM "OPEN/LOCK FILES"
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APM-01",FILES$[2]="APM-02",FILES$[3]="APM-10",FILES$[4]="APT-01"
0140 LET FILES$[5]="SYS-01",FILES$[6]="APM-09",FILES$[7]="APM-05",FILES$[8]="APT-11"
0150 LET FILES$[9]="GLM-01"
0160 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET APM01_DEV=CHANNELS[1],APM02_DEV=CHANNELS[2],APM10_DEV=CHANNELS[3],APT01_DEV=CHANNELS[4]
0185 LET SYS01_DEV=CHANNELS[5],APM09_DEV=CHANNELS[6],APM05_DEV=CHANNELS[7],APT11_DEV=CHANNELS[8]
0200 REM "IOLISTS"
0220 APM01A: IOLIST *,X0$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0400 REM "PARAMETERS"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",H1$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[2],G[4],INFO$[20]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0475 LET G[2]=FNYY_YEAR(G2$(5,2))
0480 LET P[0]=NUM(P2$(1,2)),P[2]=NUM(P3$(9,1))
0490 CALL "SYC.VA",N2$,INFO$[ALL]
0495 LET GL$=INFO$[9]
0500 REM "INIT DATA"
0530 DIM J$(80,"-"),W0$(2),Z0$(100),J0$(80,"0"),J1$(80," ")
0540 LET M8$="(###)-###-####",M9$="###-##-####",M7$="00000"
0550 DIM B[15],X[8],W[5]
0560 LET M0=LEN(M0$),M1=LEN(M1$)
0590 IF P[2]=9 THEN LET M7$="00000-0000"
0600 REM " --- Additional File Opens"
0610 IF GL$<>"Y" THEN GOTO 0700
0630 CALL "SYC.DA",1,9,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0640 IF STATUS>0 THEN GOTO 9900
0650 LET GLM01_DEV=CHANNELS[9]
0700 REM "BACKGROUND"
0710 PRINT 'CF',
0720 FOR X=4 TO 10 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0750 PRINT 'SB',@(22,3),"Beginning Vendor:",@(25,5),"Ending Vendor:",@(22,7),"Include Comments:",'SF',
1000 REM "BEGINNING VENDOR
1005 PRINT 'CF',
1010 IF LEN(K0$)=0 THEN DIM K0$(P[0])
1020 LET V0$="M",V1$="KCE",V2$=K0$(1,P[0]),V3$=M0$,V4$="Enter Starting Vendor (<F1>=First/<F3>=Lookup)",V0=6,V1=40,V2=3,I0=0
1030 GOSUB 7000
1040 IF V3=4 THEN GOTO 9900
1045 IF V3=3 THEN CALL "SYC.LV",V$
1050 IF V3=1 THEN PRINT @(V1,V2),"First",'CL',; LET K0$=""; GOTO 1090
1060 FIND (APM01_DEV,KEY=N0$+V$,DOM=1000)IOL=APM01A
1070 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$)," ",X0$(1,30),'CL',
1080 LET K0$=V$
1090 GOTO 3900
1100 REM "ENDING VENDOR"
1110 IF LEN(K1$)=0 THEN DIM K1$(P[0])
1120 LET V0$="M",V1$="KCE",V2$=K1$(1,P[0]),V3$=M0$,V4$="Enter Ending Vendor (<F1>=Last/<F3>=Lookup)",V0=6,V1=40,V2=5
1130 GOSUB 7000
1135 IF V3=2 THEN GOTO 1190
1140 IF V3=4 THEN GOTO 9900
1145 IF V3=3 THEN CALL "SYC.LV",V$
1150 IF V3=1 THEN PRINT @(V1,V2),"Last",'CL',; LET K1$=""; GOTO 1190
1160 FIND (APM01_DEV,KEY=N0$+V$,DOM=1100)IOL=APM01A
1165 IF V$<K0$ THEN GOTO 1100
1170 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$)," ",X0$(1,30),'CL',
1180 LET K1$=V$
1190 GOTO 3900
1200 REM "INCLUDE COMMENTS?
1210 LET V0$="Y",V1$="",V2$="Y",V3$="",V4$="Include Vendor Comments (Y/N)?",V0=1,V1=40,V2=7
1220 GOSUB 7000
1230 LET Y$=V$
1290 GOTO 3900
3900 REM "NEXT FIELD"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 IF ABS(I0)>1000 THEN GOTO 3990
3960 ON I0 GOTO 1000,1100,1200,3990
4000 REM "END"
4010 LET V0$="Y",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V3=4 THEN GOTO 9900
4040 IF V$="N" THEN GOTO 1000
4050 IF V$<>"Y" THEN GOTO 4000
4060 PRINT @(0,22),'CL',
4070 CALL "SYC.GA",7,1,"","",STATUS
4080 IF STATUS THEN GOTO 9900
4180 LET O9$="SYS.AA"
4190 RUN "APM.MG"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

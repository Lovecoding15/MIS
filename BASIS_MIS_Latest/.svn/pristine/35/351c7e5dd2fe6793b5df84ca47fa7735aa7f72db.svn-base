0010 REM "OPX - Remove Unlinked Order Lines"
0020 REM "Program OPX.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ARE-03",FILES$[2]="ARE-13",FILES$[3]="ARE-23"
0135 LET FILES$[4]="ARE-33",FILES$[5]="SYS-01"
0140 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0145 IF STATUS>0 THEN GOTO 9900
0150 LET ARE03_DEV=CHANNELS[1],ARE13_DEV=CHANNELS[2],ARE23_DEV=CHANNELS[3]
0155 LET ARE33_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters "
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)
0800 REM " --- Options
0810 LET V4$="Do You Wish To Remove Unlinked Order Lines"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM " --- Initialize "       
0910 CALL "SYC.NB","Processing",17,COLUMN
1000 REM " --- Read Detail Records "
1005 DIM K1$(17)
1010 LET K$=KEY(ARE13_DEV,END=1100)
1015 IF POS(K$(1,2)=N0$)=0 THEN GOTO 1100
1020 READ (ARE13_DEV,KEY=K$,DOM=1010)
1030 GOSUB CHECK_HEADER
1040 IF HEADER$="YES" THEN READ (ARE13_DEV,KEY=K$(1,17)+$FF$,DOM=1010)
1050 LET K1$=K$(1,17)
1060 REMOVE (ARE13_DEV,KEY=K$)
1070 LET K$=KEY(ARE13_DEV,END=1100)
1080 IF POS(K$(1,17)=K1$)>0 THEN GOTO 1060
1090 GOTO 1010
1100 REM " --- Read Manual Ship-To Records "
1110 DIM K1$(15),K$(20)
1120 LET K1$=KEY(ARE33_DEV,END=1200)
1125 IF POS(K1$(1,2)=N0$)=0 THEN GOTO 1200
1140 READ (ARE33_DEV,KEY=K1$,DOM=1120)
1150 LET K$=K1$(1,2)+"  "+K1$(3,13)
1160 GOSUB CHECK_HEADER
1170 IF HEADER$="YES" THEN GOTO 1120
1180 REMOVE (ARE33_DEV,KEY=K1$)
1190 GOTO 1120
1200 REM " --- Read Lot/Serial Detail Records "
1210 DIM K1$(17)
1220 LET K$=KEY(ARE23_DEV,END=4000)
1225 IF POS(K1$(1,2)=N0$)=0 THEN GOTO 4000
1230 READ (ARE23_DEV,KEY=K$,DOM=1220)
1240 GOSUB CHECK_HEADER
1250 IF HEADER$="YES" THEN READ (ARE23_DEV,KEY=K$(1,17)+$FF$,DOM=1220)
1260 LET K1$=K$(1,17)
1270 REMOVE (ARE23_DEV,KEY=K$)
1280 LET K$=KEY(ARE23_DEV,END=4000)
1290 IF POS(K1$(1,17)=K$)>0 THEN GOTO 1270
1300 GOTO 1220
4000 REM " --- End "
4090 GOTO 9900
6000 REM " --- Check For Valid Header Record " 
6010 CHECK_HEADER: 
6020 LET HEADER$="NO"
6025 PRINT @(COLUMN,11),K$(1,2)+" "+K$(5,6)+" "+K$(11,7)
6030 FIND (ARE03_DEV,KEY=K$(1,17)+"000",DOM=6050)
6040 LET HEADER$="YES"
6050 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "SYC - Executive Summary (Calendar)"
0020 REM "Program SYC.QF"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL],CHANNELS[ALL],TNAME$
0100 REM " --- Open/Lock Files"
0110 LET SYE01_DEV=CHANNELS[1],SYE02_DEV=CHANNELS[2],SYE03_DEV=CHANNELS[3]
0120 LET SYE04_DEV=CHANNELS[4],SYE05_DEV=CHANNELS[5],SYE06_DEV=CHANNELS[6]
0130 LET SYE07_DEV=CHANNELS[7],SYE08_DEV=CHANNELS[8],SYE09_DEV=CHANNELS[9]
0140 LET SYE10_DEV=CHANNELS[10],SYM06_DEV=CHANNELS[11]
0200 REM " --- IOLIST's"
0210 SYE02A: IOLIST A0$(1),A1$(1),A[ALL],U[ALL],L[ALL]
0220 SYE08A: IOLIST S0$(1),S1$(1),S[ALL]
0260 SYE01A: IOLIST X$,P1$
0310 SYM06A: IOLIST C0$(1),C1$(1)
0400 REM " --- Parameters"
0410 DIM A0$(8),SPEED$(128)
0420 LET X$=OPTION$[3],SYSTEM$=X$(1,2),A0$(3)=X$(3)
0430 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=SPEED$(1,2)
0440 LET OWIDTH=NUM(SPEED$(83,2)),OHEIGHT=NUM(SPEED$(85,2))
0450 LET ONAME$=FNP$(SPEED$(27,8)),MAXOPT=NUM(SPEED$(57,2))
0460 FIND (SYE01_DEV,KEY=N0$,DOM=9900)IOL=SYE01A
0470 LET A0$(1,2)=N0$,ASOF$=P1$(1,6)
0500 REM " --- Initializations"
0510 DIM A1$(32),A[31],U[31],L[31],M[12],OPTLIST$[2,MAXOPT,6]
0520 DIM S0$(8),S1$(32),S[31],C0$(2),C1$(40)
0550 LET D_STATUS=0,CR$="(<Enter>=Continue)",YEAR$="",MONTH$=""
0560 LET DAYS$="SunMonTueWedThuFriSat",OWIN_X=79-OWIDTH,OWIN_Y=3
0570 LET M1$="#,###,##0-",M1=LEN(M1$),UM2$="##,###,##0-",AM2$="$"+UM2$
0580 LET M[1]=31,M[2]=28,M[3]=31,M[4]=30,M[5]=31,M[6]=30,M[7]=31,M[8]=31
0585 LET M[9]=30,M[10]=31,M[11]=30,M[12]=31,PGM$=PGM(-2)
0590 LET TNAME$=OPTION$[4],TWIDTH=80,TYPE=NUM(OPTION$[5])
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTIONS,OPTLIST$[ALL],X$,OMAXROW,Q$,ONAME$
0700 REM " --- Create main window"
0710 LET WIDTH=78,HEIGHT=21,COLUMN=1,ROW=2,TITLE$=""
0720 CALL "SYC.WA",0,WIDTH,HEIGHT,COLUMN,ROW,TITLE$,NAME$
0800 REM " --- Fill in calendar block"
0810 FOR X=10 TO 65 STEP 11
0820 CALL "SYC.WC",1,1,0,HEIGHT-2,X,0,0
0830 NEXT X
0840 CALL "SYC.WC",1,0,WIDTH-2,0,0,1,0
0850 FOR X=4 TO 16 STEP 3
0860 CALL "SYC.WC",1,0,WIDTH-2,0,0,X,0
0870 NEXT X
0880 PRINT @(22,HEIGHT-4),'CL',@(22,HEIGHT-3),'CL',
1000 REM " --- Month/Year?"
1010 IF A0$=N0$+YEAR$+MONTH$ THEN GOTO 1200
1020 LET N0$=A0$(1,2),YEAR$=A0$(3,4),MONTH$=A0$(7,2),PERIOD$=YEAR$+MONTH$
1030 LET JULIAN=JUL(NUM(YEAR$),NUM(MONTH$),1)
1100 REM " --- Display top window"
1110 DIM HEADING$(TWIDTH-2)
1120 FIND (SYM06_DEV,KEY=N0$,DOM=1130)IOL=SYM06A
1130 CALL "SYC.WB",TNAME$,0
1140 LET HEADING$(2)="Company: "+FNP$(C1$)
1150 LET HEADING$(TWIDTH-9)=" "+DATE(JULIAN:"%Ms")+" "+YEAR$(3,2)
1160 PRINT @(0,0),'SB','BR',HEADING$,'ER','SF',
1170 CALL "SYC.WB",NAME$,0
1200 REM " --- System?"
1210 IF SYSTEM$="AR" THEN LET CHANNEL=SYE02_DEV,TITLE$="Accounts Receivable"
1220 IF SYSTEM$="AP" THEN LET CHANNEL=SYE03_DEV,TITLE$="Accounts Payable"
1230 IF SYSTEM$="OP" THEN LET CHANNEL=SYE04_DEV,TITLE$="Open Orders"
1240 IF SYSTEM$="PO" THEN LET CHANNEL=SYE06_DEV,TITLE$="Purchase Orders"
1250 IF SYSTEM$="SA" THEN LET CHANNEL=SYE07_DEV,TITLE$="Sales Totals"
1260 IF SYSTEM$="SP" THEN LET CHANNEL=SYE08_DEV,TITLE$="Gross Profit"
1270 IF SYSTEM$="IV" THEN LET CHANNEL=SYE09_DEV,TITLE$="Inventory"
1300 REM " --- Units to be displayed?"
1310 LET TYPE$="Amounts",M2$=AM2$
1320 IF TYPE=1 THEN LET TYPE$="Units",M2$=UM2$
1330 IF TYPE=2 THEN LET TYPE$="Averages"
1390 LET TITLE$=TITLE$+" "+TYPE$+" For "+DATE(JULIAN:"%Ms")+" "+YEAR$(3,2)
1400 REM " --- Days"
1410 LET COLUMN=0,ROW=0
1420 FOR X=1 TO 7
1430 PRINT @(COLUMN,ROW),DAYS$((X-1)*3+1,3),
1440 LET COLUMN=COLUMN+11
1450 NEXT X
1500 REM " --- Determine starting day of the week"
1510 DIM DAYS[42]
1515 LET X$=DATE(JULIAN:"%Ds"),D=0,DATE=0
1520 LET FIRST=((POS(X$="MonTueWedThuFriSatSun",3)-1)/3)+1
1530 LET FIRST=FIRST+1,M[2]=28,YYYY=NUM(YEAR$),MM=NUM(MONTH$)
1540 IF FIRST>7 THEN LET FIRST=1
1550 IF MOD(YY,4)=0 THEN LET M[2]=29
1560 FOR X=FIRST TO 42
1570 LET DATE=DATE+1
1580 IF DATE<=M[MM] THEN LET D=D+1,DAYS[X]=D
1590 NEXT X
1900 REM " --- Display dates on calendar"
1910 LET COLUMN=0,ROW=2
1920 FOR X=1 TO 42
1930 IF DAYS[X]>0 THEN PRINT @(COLUMN,ROW),DAYS[X],
1940 LET COLUMN=COLUMN+11
1950 IF COLUMN<77 THEN GOTO 1990
1960 LET COLUMN=0,ROW=ROW+3
1990 NEXT X
2000 REM " --- Retrieve data record for system/month"
2010 DIM A1$(32),A[31],S1$(32),S[31]
2020 READ (CHANNEL,KEY=A0$,DOM=2030)IOL=SYE02A
2200 REM " --- Calculate gross profit"
2210 IF SYSTEM$<>"SP" THEN GOTO 2500
2220 LET S0$(1)=A0$(1)
2230 FIND (SYE07_DEV,KEY=S0$,DOM=2240)IOL=SYE02A
2240 FOR X=1 TO 31
2250 LET A[X]=S[X]-A[X]
2260 NEXT X
2500 REM " --- Prior month inventory"
2510 IF SYSTEM$<>"IV" THEN GOTO 3000
2520 IF A0$(3)>ASOF$ THEN GOTO 2600
2530 LET K$=KEY(SYE09_DEV,END=3000)
2540 IF POS(N0$=K$)<>1 THEN GOTO 3000
2550 READ (SYE09_DEV,KEY=K$)IOL=SYE02A
2560 FOR X=2 TO 31
2570 LET A[X]=A[1]
2580 NEXT X
2590 GOTO 3000
2600 REM " --- Future month inventory"
2610 LET K$=KEYP(SYE09_DEV,END=3000)
2620 IF POS(N0$=K$)<>1 THEN GOTO 3000
2630 READ (SYE09_DEV,KEY=K$)IOL=SYE02A
2640 FOR X=1 TO 30
2650 LET A[X]=A[31]
2660 NEXT X
3000 REM " --- Display data"
3010 LET COLUMN=0,ROW=3,DATE=1,TOTAL=0,ATOTAL=0,UTOTAL=0
3020 FOR X=1 TO 42
3030 IF DAYS[X]=0 THEN GOTO 3150
3040 LET N=A[DATE]
3050 IF TYPE=1 THEN LET N=U[DATE]
3060 IF TYPE<>2 THEN GOTO 3100
3070 LET N=0
3080 IF U[DATE]<>0 THEN LET N=INT(A[DATE]/U[DATE])
3100 LET TOTAL=TOTAL+N,ATOTAL=ATOTAL+A[DATE],UTOTAL=UTOTAL+U[DATE]
3110 PRINT @(COLUMN,ROW),N:M1$,
3120 LET DATE=DATE+1
3150 LET COLUMN=COLUMN+11
3170 IF COLUMN<77 THEN GOTO 3190
3180 LET COLUMN=0,ROW=ROW+3
3190 NEXT X
3500 REM " --- Calulate MTD Average (if display option is averages)"
3510 LET TOTAL$="Total Is "
3520 IF TYPE<>2 THEN GOTO 3600
3530 LET TOTAL=0,TOTAL$="Monthly Average Is "
3540 IF UTOTAL<>0 THEN LET TOTAL=INT(ATOTAL/UTOTAL)
3900 REM 
3910 LET V0$="S",V1$="al",V2$="",V3$="",V4$=CR$,V0=1
3915 IF SYSTEM$<>"IV" THEN LET V4$=TOTAL$+FNS$(STR(TOTAL:M2$))+" "+CR$
3920 LET V1$="",X0=22+FNC(TITLE$,WIDTH-22),X1=X0+FNC(V4$,LEN(TITLE$))
3950 PRINT @(X0,HEIGHT-4),TITLE$,@(X1,HEIGHT-3),V4$,
3970 LET V1=X1+LEN(V4$)+1,V2=HEIGHT-3,V4$=""
3980 GOSUB 7000
3985 IF V3=5 THEN GOTO 4700
3990 IF V3=4 OR V3=6 THEN GOTO 4800
4000 REM " --- Process option window"
4010 GOSUB 5500
4020 IF OPTION$[1]="EXIT" THEN GOTO 4900
4030 IF OPTION$[1]="PRIOR" THEN GOTO 4900
4040 IF OPTION$[1]="STARTOVER" THEN GOTO 4900
4050 IF ANSWER$="SUMMARY" THEN GOTO 4900
4060 PRINT 'CF',
4070 IF TYPE$="C" THEN LET SYSTEM$=ANSWER$
4090 GOTO 1000
4700 REM " --- Startover"
4710 LET OPTION$[1]="STARTOVER"
4790 GOTO 4900
4800 REM " --- Exit"
4810 LET OPTION$[1]="EXIT"
4900 REM " --- All done"
4990 GOTO 9900
5500 REM " --- Option window processing"
5510 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
5520 LET PAGE=1,UNITS=TYPE
5530 CALL "SYC.WB",ONAME$,0
5540 PRINT 'CS',
5600 REM " --- Call lightbar routine"
5610 CALL "SYC.SA",0,OPTLIST$[ALL],MORE$[ALL],ANSWER$,PAGE,OMAXROW,ONAME$,OHEIGHT,OWIDTH,FKEY
5620 IF FKEY=-16 THEN GOTO 5500
5625 IF FKEY=-1 OR FKEY=-2 OR ANSWER$="PRIOR" THEN LET OPTION$[1]="PRIOR"; GOTO 5900
5630 IF FKEY=4 OR ANSWER$="END" THEN LET OPTION$[1]="EXIT"; GOTO 5900
5640 IF FKEY=5 THEN LET OPTION$[1]="STARTOVER"; GOTO 5900
5645 FOR X=1 TO OPTIONS
5650 IF OPTLIST$[1,X,2]<>ANSWER$ THEN GOTO 5665
5655 LET OPTION$[2]=FNU$(OPTLIST$[1,X,3]),SELECTION=X,TYPE$=OPTLIST$[1,X,4]
5660 LET SECURITY$=OPTLIST$[1,X,5],PASSWORD$=OPTLIST$[1,X,6],PARAM$=OPTION$[2]
5665 NEXT X
5675 IF SECURITY$="" AND PASSWORD$="" THEN GOTO 5690
5680 CALL "SYC.QX","M",SECURITY$,SELECTION,OPTLIST$[ALL],STATUS
5685 IF STATUS>0 THEN GOTO 5600
5690 LET SUBROUTINE$=FNP$(ANSWER$),OPTION$[1]=SUBROUTINE$
5695 LET OPTION$[3]=SYSTEM$+PERIOD$
5700 REM " --- Call Now (Change system)"
5710 IF TYPE$="C" THEN GOTO 5900
5800 REM " --- Search (Change display, month or firm)"
5810 IF TYPE$="D" THEN GOTO 5900
5820 IF PARAM$="MONTH" THEN LET OPTION$[3]=PERIOD$
5830 IF PARAM$="TYPE" THEN LET OPTION$[3]=STR(UNITS)
5840 CALL SUBROUTINE$,ERR=5600,D_STATUS,OPTION$[ALL]
5850 IF OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" THEN GOTO 5900
5860 IF OPTION$[1]="PRIOR" THEN LET OPTION$[1]=""; GOTO 5900
5870 IF PARAM$="MONTH" THEN LET PERIOD$=OPTION$[3]
5880 IF PARAM$="TYPE" THEN LET UNITS=NUM(OPTION$[3])
5885 LET OPTION$[3]=SYSTEM$+PERIOD$
5890 GOTO 5900
5900 REM " --- Option processing completed"
5910 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5920 IF UNITS<>TYPE THEN LET TYPE=UNITS,CONTROL=3
5930 IF PERIOD$=YEAR$+MONTH$ THEN GOTO 5950
5940 LET A0$(1)=N0$+PERIOD$,CONTROL=2
5950 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=5980),FIRM$=SPEED$(1,2)
5960 IF N0$<>FIRM$ THEN LET A0$(1,2)=FIRM$,CONTROL=1
5980 CALL "SYC.WB",NAME$,0
5990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNS$(Q$)=CVS(Q$,1)
8085 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9920 LET OPTION$[3]=A0$
9950 EXIT 
9999 END

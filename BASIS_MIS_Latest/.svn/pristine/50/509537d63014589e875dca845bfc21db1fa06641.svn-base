0010 REM "SYC - Open Printer"
0020 REM "Program SYC.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "PRINTER  : Printer Channel"
0050 REM "MESSAGE  : Message Flag (1=<F4> to Interrupt.../0=No Message)"
0051 REM "         : 2=No Message & no input for non-null PF File Descs)"
0055 REM "PFDESC$  : Override PF Report Description (no user input)"
0060 REM "SELECTPR$: Specified Printer (override terminal default)"
0065 REM "STATUS   : Return status (0=No error)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER PRINTER,MESSAGE,PFDESC$,SELECTPR$,STATUS
0100 REM " --- Open files"
0105 LET FILES=8
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SYM-02",FILES$[3]="SYM-07"
0140 LET FILES$[4]="SYM-17",FILES$[5]="SYM-01",FILES$[6]="SYM-11"
0150 IF PRINTER>0 THEN OPEN (PRINTER)FID(0)
0160 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0165 CLOSE (PRINTER,ERR=0170)
0170 IF STATUS>0 THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1],SYM02_DEV=CHANNELS[2],SYM07_DEV=CHANNELS[3]
0190 LET SYM17_DEV=CHANNELS[4],SYM01_DEV=CHANNELS[5],SYM11_DEV=CHANNELS[6]
0200 REM " --- IOLIST's"
0210 PFXXXA: IOLIST A0$(1),A1$(1)
0220 SYM02A: IOLIST P0$(1),P1$(1),P[ALL]
0230 SYM07A: IOLIST B0$(1),B1$(1)
0240 SYM17A: IOLIST C0$(1),C1$(1),C2$(1),C3$(1)
0250 SYM01A: IOLIST D0$(1),D1$(1)
0260 SYS01T: IOLIST X$,F0$,F1$,F2$,F3$,F4$,F5$
0400 REM " --- Parameters"
0410 DIM P0$(3),P1$(128),P[4]
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 IF SELECTPR$="" THEN GOTO 0450
0435 LET F0$(13,2)=SELECTPR$
0440 IF LEN(SELECTPR$)>2 THEN LET F0$(25,1)=SELECTPR$(3)
0450 FIND (SYM02_DEV,KEY=F0$(4,3),DOM=9800)IOL=SYM02A
0460 IF P[0]<1 THEN LET P[0]=10
0470 IF FNP$(P1$(101,3))="" THEN LET P1$(101,3)=F0$(4,3)
0480 FOR X=101 TO 103
0485 IF POS(P1$(X,1)="0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ")=0 THEN EXITTO 9700
0490 NEXT X
0500 REM " --- Initializations"
0510 DIM MESSAGE$(80),A0$(2),A1$(64),B0$(2),B1$(48)
0520 DIM C0$(3),C1$(30),C2$(60),C3$(16),D0$(6),D1$(35)
0550 LET MESSAGE$(1)="     Press <F4> to interrupt printing ...",FF$="Y"
0560 LET F$="",STATUS=0,PFNAME$="PF"+FNP$(P1$(101,3)),PFINDEX$=PFNAME$+"A"
1000 REM " --- Printer or PF?"
1010 IF F0$(13,2)="PF" THEN GOTO 2000
1020 OPEN (PRINTER,ERR=9500)F0$(13,2)
1025 IF F0$(13,2)=FID(0) THEN GOTO 4000
1030 FIND (SYM07_DEV,KEY=F0$(13,2),DOM=2040)IOL=SYM07A
1040 IF B1$(31,1)="N" THEN LET FF$="N"
1050 LET X$=STBL("!FF",FF$,ERR=1060),C0$(1)=B0$+F0$(25,1)
1100 REM " --- Retrieve font record"
1110 LET C0$(1)=B0$+F0$(25,1)
1120 FIND (SYM17_DEV,KEY=C0$,DOM=1200)IOL=SYM17A
1200 REM " --- For Windows, we must use SP and CP mnemonics instead of font"
1210 LET V$=INFO(6,0,ERR=1300)
1220 IF V$="" THEN GOTO 1300
1230 IF C3$(1,1)="C" THEN PRINT (PRINTER,ERR=4000)'CP',
1240 IF C3$(1,1)="S" THEN PRINT (PRINTER,ERR=4000)'SP',
1290 GOTO 4000
1300 REM " --- Switch printer to requested font"
1310 LET C2$=FNP$(C2$)
1320 IF C2$<>"" THEN PRINT (PRINTER,ERR=4000)'BO',ATH(C2$),'EO',
1390 GOTO 4000
1900 REM " --- Missing printer and/or attribute record"
1920 LET V4$="Missing Printer Or Attribute Record"
1930 GOSUB 6000
1990 GOTO 9800
2000 REM " --- Open PF index file"
2010 LET FILES$[8]=PFINDEX$,OPTIONS$[8]="F"
2020 CALL "SYC.DA",1,8,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
2030 IF STATUS>0 THEN GOTO 9900
2040 LET PFILE_DEV=CHANNELS[8]
2050 IF PFILE_DEV>0 THEN GOTO 2100
2060 CALL "SYC.MA","M",PFINDEX$,"SY","2",0,96,STATUS
2065 IF STATUS=0 THEN GOTO 2000
2070 LET V4$="Unable To Define PF Index File"
2080 GOSUB 6000
2090 GOTO 9800
2100 REM " --- Set report description default"
2110 LET V2$=F5$,TITLE$=""
2120 IF PFDESC$<>"" THEN LET V2$=PFDESC$
2130 IF V2$<>"" THEN GOTO 2170
2140 LET D0$(1)=F2$,D1$(1)=""
2150 FIND (SYM01_DEV,KEY=D0$,DOM=2160)IOL=SYM01A
2160 LET V2$=D1$
2170 IF MESSAGE<>2 THEN GOTO 2200
2180 LET V$=V2$,PFDESC$="",MESSAGE=0
2190 GOTO 3060
2200 REM " --- Get PF report description"
2210 LET V0$="S",V1$="I",V3$="",X$="Report Description:",V0=40,IGNORE$="Y"
2220 LET HEIGHT=3,WIDTH=LEN(X$)+V0+6,WIN_X=INT((80-WIDTH)/2),V4$=""
2230 LET WIN_Y=20,NAME$="",V1=LEN(X$)+2,V2=0
2240 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
2250 PRINT @(1,0),X$,
2260 GOSUB 7000
2270 CALL "SYC.WD",NAME$
2280 LET IGNORE$=""
2285 IF V3=4 THEN GOTO 9800
2300 REM " --- Valid description?"
2310 IF V$="" THEN GOTO 2100
2320 LET REPORT$=V$,OLDEST$="",OLDEST=1
2400 REM " --- Find free PF file"
2410 FOR REPORT=1 TO P[0]
2420 LET A0$(1)=STR(REPORT:"00")
2430 FIND (PFILE_DEV,KEY=A0$,DOM=2480)IOL=PFXXXA
2440 IF OLDEST$="" OR A1$(41,6)<OLDEST$ THEN LET OLDEST$=A1$(41,6),OLDEST=REPORT
2470 GOTO 2490
2480 EXITTO 3000
2490 NEXT REPORT
2500 REM " --- No free PF files"
2510 LET V4$="No More PF Files (<Enter>=Delete Oldest/<F1>=Display/<F4>=Exit)"
2520 GOSUB 6100
2530 IF V3=1 THEN GOTO 2600
2540 IF V3=4 THEN GOTO 9800
2590 GOTO 2700
2600 REM " --- <F1> Display"
2610 DIM OPTION$[5],SPEED$(128)
2620 LET SPEED$(1,2)=N0$,SPEED$(57,2)="15",SPEED$(63,2)="15"
2630 LET X$=STBL("!SYS01",STR(SYS01_DEV)),X$=STBL("!SYM01",STR(SYM01_DEV))
2640 LET X$=STBL("!SYM11",STR(SYM11_DEV)),X$=STBL("!SPEEDSEARCH",SPEED$)
2650 CALL "SYC.QR",D_STATUS,OPTION$[ALL]
2660 LET X$=STBL("!CLEAR","!SPEEDSEARCH",ERR=2400)
2690 GOTO 2400
2700 REM " --- <Enter> Delete oldest PF file"
2710 LET REPORT=OLDEST,A0$(1)=STR(REPORT:"00")
2720 REMOVE (PFILE_DEV,KEY=A0$,DOM=2730)
3000 REM " --- Erase any existing PF file"
3010 LET PFNAME$=PFNAME$+STR(REPORT)
3020 ERASE PFNAME$,ERR=3100
3090 GOTO 3020
3100 REM " --- Create new PF file"
3110 CALL "SYC.MA","C",PFNAME$,"SY","",0,0,STATUS
3120 IF STATUS=0 THEN GOTO 3200
3130 LET V4$="Unable To Create PF Output File"
3140 GOSUB 6000
3190 GOTO 9800
3200 REM " --- Open PF report file"
3210 LET FILES$[7]=PFNAME$,X=1
3220 IF PRINTER>0 THEN LET X=0
3230 CALL "SYC.DA",X,7,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
3240 IF STATUS>0 THEN GOTO 9900
3250 LET PRINTER=CHANNELS[7]
3300 REM " --- Update PF index file"
3310 LET A0$(1)=STR(REPORT:"00"),A1$(1)=REPORT$
3320 LET A1$(41,6)=F0$(18,6),A1$(47,8)=FNE$(Q$)
3330 WRITE (PFILE_DEV,KEY=A0$)IOL=PFXXXA
3340 LOCK (PRINTER)
4000 REM " --- All done"
4090 GOTO 9900
6000 REM " --- Complete error message"
6010 LET V4$=V4$+" (<Enter>=Continue)"
6100 REM " --- Standard error message"
6110 DIM MESSAGE$[1]
6120 LET MESSAGE$[0]=V4$
6130 CALL "SYC.XA",1,MESSAGE$[ALL],0,22,-1,V$,V3
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8050 DEF FNE$(Q$)=DATE(0:"%hz:%mz %p")
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9500 REM " --- Printer busy"
9520 LET V4$="Printer "+F0$(13,2)+" Is Unavailable (<Enter>=Retry/<F4>=Exit):"
9530 GOSUB 6100
9540 IF V3=4 THEN GOTO 9800
9590 GOTO 1000
9700 REM " --- Invalid PF File ID"
9710 LET V4$="A Valid PF File ID Is Required For This User"
9720 GOSUB 6000
9800 REM " --- Return error"
9810 LET STATUS=1
9900 REM " --- Return to calling program"
9910 FOR X=1 TO FILES
9920 IF CHANNELS[X]<>PRINTER THEN CLOSE (CHANNELS[X],ERR=9921)
9930 NEXT X
9935 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9940 IF STATUS=0 THEN IF MESSAGE THEN PRINT @(0,22),'CL','BR',MESSAGE$,'ER',@(0,0),
9950 EXIT 
9999 END

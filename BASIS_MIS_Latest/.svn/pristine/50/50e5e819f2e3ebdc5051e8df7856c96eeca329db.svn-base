0010 REM "ARX - Cross-Reference Rebuild"
0020 REM "Program ARX.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ARM-02",FILES$[3]="ARM-04"
0130 LET FILES$[4]="SYS-01"
0140 LET OPTIONS$[3]="C"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET ARM01_DEV=CHANNELS[1],ARM02_DEV=CHANNELS[2],ARM04_DEV=CHANNELS[3]
0190 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLists
0210 ARM01A: IOLIST A0$,A1$
0220 ARM02A: IOLIST B0$,B1$
0230 ARM04A: IOLIST C0$
0240 ARM04B: IOLIST C0$
0250 ARM04C: IOLIST C0$
0260 ARM04D: IOLIST C0$
0270 ARM04E: IOLIST C0$
0280 ARM04F: IOLIST C0$
0300 ARM04G: IOLIST C0$
0310 ARM04H: IOLIST C0$
0320 ARM04J: IOLIST C0$
0330 ARM04K: IOLIST C0$
0340 ARM04L: IOLIST C0$
0350 ARM04M: IOLIST C0$
0360 ARM04N: IOLIST C0$
0390 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0500 REM " --- Init Data
0520 DIM A[10],B[15]
0800 REM " --- Options
0810 LET V4$="Do You Wish To Rebuild Accounts Receivable Cross-References"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0950 CALL "SYC.NB","Updating",9,COLUMN
1000 REM " --- Customer Master Xrefs"
1010 READ (ARM01_DEV,END=4000)IOL=ARM01A
1020 IF A1$="" THEN GOTO 2000
1030 PRINT @(COLUMN,11),A0$(1,2)," ",A0$(3),
1040 LET C0$=A0$(1,2)+"L"+A1$(112,10)+A0$(3)
1050 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04L; REM "Phone #"
1055 LET C0$=A0$(1,2)+"A"+A1$(146,10)+A0$(3)
1060 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04A; REM "Alternate Sequence"
1065 LET C0$=A0$(1,2)+"E"+A1$(103,9)+A0$(3)
1070 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04E; REM "Zip"
1075 LET C0$=A0$(1,2)+"D"+A1$(228,10)+A0$(3)
1080 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04D; REM "Contact Name"
1082 LET C0$=A0$(1,2)+"M"+A1$(248,9)+A0$(3)
1085 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04M; REM "D&B Number " 
1090 LET C0$=A0$(1,2)+"N"+A1$(257,8)+A0$(3)
1095 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04N; REM "SIC Code"    
2000 REM " --- Customer Detail Xrefs"
2005 FIND (ARM02_DEV,KEY=A0$+"  ",DOM=3900)IOL=ARM02A
2010 IF B1$="" THEN GOTO 3900
2025 LET C0$=B0$(1,2)+"B"+B1$(1,3)+B0$(3,6)
2030 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04B; REM "Salesperson"
2035 LET C0$=B0$(1,2)+"C"+B1$(21,3)+B0$(3,6)
2040 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04C; REM "Territory"
2045 LET C0$=B0$(1,2)+"F"+B1$(4,2)+B0$(3,6)
2050 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04F; REM "Terms Code"
2055 LET C0$=B0$(1,2)+"G"+B1$(6,2)+B0$(3,6)
2060 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04G; REM "Sale Discount"
2065 LET C0$=B0$(1,2)+"H"+B1$(8,2)+B0$(3,6)
2070 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04H; REM "Distribution"
2075 LET C0$=B0$(1,2)+"J"+B1$(24,4)+B0$(3,6)
2080 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04J; REM "Pricing"
2085 LET C0$=B0$(1,2)+"K"+B1$(32,2)+B0$(3,6)
2090 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04K; REM "Tax"
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- End
4090 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

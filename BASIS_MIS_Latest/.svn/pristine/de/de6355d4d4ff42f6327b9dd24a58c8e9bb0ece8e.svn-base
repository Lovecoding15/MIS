0010 REM "IVM - Item Maintenance (Synonyms)"
0020 REM "Program IVM.MS"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 PRECISION P[1]
0200 REM " --- IOLIST's"
0210 IVM14A: IOLIST T0$(1)
0220 IVM24A: IOLIST S0$(1)
0500 REM " --- Initializations"
0510 DIM S0$(42),T0$(42)
0550 LET L9=21,L=L0+1,S0$(1)=A0$
0700 REM " --- Background"
0710 PRINT 'SB',@(1,L0),"Synonym",'SF',
0900 REM " --- Display any existing lines"
0910 GOSUB 5100
0920 IF Q1=0 THEN GOTO 1400
0990 GOTO 4000
1000 REM " --- Overflow"
1010 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L0+1),'LD',
1020 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1110 LET K$=KEY(IVM24_DEV,END=1400)
1120 IF POS(A0$=K$)<>1 THEN GOTO 1400
1130 READ (IVM24_DEV)IOL=IVM24A
1190 GOTO 1500
1400 REM " --- Synonym"
1410 LET V4$="Enter A Synonym For This Item"
1420 LET V0$="S",V1$="KERC",V2$="",V3$="",V0=20,V1=1,V2=L,I0=0
1430 GOSUB 7000
1440 IF V3=4 THEN PRINT @(V1,L),'CL',; GOTO 4000
1450 IF FNP$(V$)="" THEN GOTO 1400
1460 LET S0$(1)=A0$+V$
1480 FIND (IVM24_DEV,KEY=S0$,DOM=1500)IOL=IVM24A
1500 REM " --- Display"
1510 IF S0$<=K9$ THEN GOSUB 5400
1520 GOSUB 5000
1530 LET I0=1
3200 REM " --- Sequence Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,3300
3300 REM " --- Line Correct?"
3310 LET V4$="<Enter>=Correct     <D>=Delete"
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=S0$
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3300,3300,3300,3350
3350 IF V$="" THEN GOTO 3400
3360 IF V$="D" THEN GOTO 3700
3390 GOTO 3300
3400 REM " --- Write"
3410 LET T0$(1)=S0$(1,2)+S0$(23)+S0$(3)
3420 WRITE (IVM14_DEV,KEY=T0$)IOL=IVM14A
3430 WRITE (IVM24_DEV,KEY=S0$)IOL=IVM24A
3450 LET L=L+1
3460 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3700 REM " --- Remove"
3710 LET T0$(1)=S0$(1,2)+S0$(23)+S0$(3)
3720 REMOVE (IVM14_DEV,KEY=T0$,DOM=3730)
3730 REMOVE (IVM24_DEV,KEY=S0$,DOM=3740)
3750 PRINT @(0,L),'CL',
3760 IF V3=4 THEN GOTO 4000
3790 GOTO 1000
4000 REM " --- Options"
4010 LET V4$="Is The Above Information Correct (Y/N/List/Review)?"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4050 IF V$="L" OR V$="LIST" THEN GOTO 4400
4070 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4080 IF LEN(V$)=6 THEN GOTO 4350
4090 ON POS(V$="YN") GOTO 4000,4900,4100
4100 REM " --- Position file"
4110 READ (IVM24_DEV,KEY=A0$+$FF$,DOM=1000)
4190 GOTO 1000
4400 REM " --- List"
4410 LET S0$(1)=A0$
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4610 LET S0$(1)=A0$
4650 REM " --- Restart"
4660 READ (IVM24_DEV,KEY=S0$,DOM=4670)
4670 GOSUB 5400
4690 GOTO 1000
4900 REM " --- Run menu overlay"
4990 RUN "IVM.MA"
5000 REM " --- Display"
5010 LET Q1=1
5020 PRINT @(0,L),'CL',@(1),S0$(23,20),
5090 RETURN 
5100 REM " --- List"
5105 LET Q1=0
5110 GOSUB 5400
5120 READ (IVM24_DEV,KEY=S0$,DOM=5130)
5130 LET K$=KEY(IVM24_DEV,END=5300)
5140 IF POS(A0$=K$)<>1 THEN GOTO 5300
5160 READ (IVM24_DEV)IOL=IVM24A
5190 IF L<=L9 THEN GOTO 5250
5200 REM " --- Continue"
5210 LET V4$="<Enter>=Continue:"
5215 LET V0$="S",V1$="CE",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5230 IF V3=4 THEN GOTO 5300
5240 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+1,K9$=S0$
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5410 PRINT @(0,L0+1),'CE',
5420 LET L=L0+1
5490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

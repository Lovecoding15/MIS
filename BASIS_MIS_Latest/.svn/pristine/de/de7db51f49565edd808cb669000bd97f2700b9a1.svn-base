0010 REM "DDX - Data Dictionary Resequence Detail Lines
0020 REM "Program DDX.BB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 DDM01A: IOLIST B0$(1),B1$(1),B[ALL]
0220 DDM04A: IOLIST A0$(1),A1$(1),A[ALL]
0230 DDM14A: IOLIST C0$(1),C1$(1)
0240 DDW04A: IOLIST A0$(1),A1$(1),A[ALL]
0250 DDW14A: IOLIST C0$(1),C1$(1)
0270 DDM06A: IOLIST DDM06_K$
0500 REM " --- Restart Logic
0510 DIM A0$(10),A1$(122),A[2],B0$(12),B1$(105),B[2],C0$(13),C1$(60)
1000 REM " --- Phase 1: Verify Enough Lines Available To Resequence"
1010 CALL "SYC.NB","Phase 1:",3,COLUMN
1020 LET DDM04_K$=R0$+R1$,SEQ=R3
1040 READ (DDM04_DEV,KEY=DDM04_K$,DOM=1100)
1090 GOTO 1140
1100 REM " --- Phase 1: Count Lines Required For Resequencing"
1120 LET DDM04_K$=KEY(DDM04_DEV,END=2000)
1140 IF POS(R0$+R1$=DDM04_K$)<>1 THEN GOTO 2000
1160 READ (DDM04_DEV,KEY=DDM04_K$)IOL=DDM04A
1180 PRINT @(COLUMN,11),A0$(8,3),
1200 LET SEQ=SEQ+R3
1220 IF SEQ<=999 THEN GOTO 1100
1500 REM " --- Phase 1: Insufficient Lines Available For Resequencing"
1510 LET NUMBER=1
1520 DIM MESSAGE$[NUMBER]
1530 LET MESSAGE$[0]="There are NOT enough line numbers available to resequence"
1540 LET MESSAGE$[1]="file "+R0$+" by "+STR(R3)+".   (<Enter>=Continue)"
1550 CALL "SYC.XA",2,MESSAGE$[ALL],NUMBER,-1,-1,V$,V3
1590 GOTO 9900
2000 REM " --- Phase 2: Build Work Files With New Sequence Numbers"
2010 CALL "SYC.NB","Phase 2:",3,COLUMN
2020 LET DDM04_K$=R0$+R1$,SEQ=R3
2040 READ (DDM04_DEV,KEY=DDM04_K$,DOM=2100)
2090 GOTO 2140
2100 REM " --- Phase 2: DDM-04 to DDW-04 Loop"
2120 LET DDM04_K$=KEY(DDM04_DEV,END=2800)
2140 IF POS(R0$+R1$=DDM04_K$)<>1 THEN GOTO 2800
2160 READ (DDM04_DEV,KEY=DDM04_K$)IOL=DDM04A
2180 PRINT @(COLUMN,11),A0$(8,3),
2200 REM " --- Phase 2: Write DDW-04 With New Sequence Number"
2220 LET A0$(8,3)=STR(SEQ:"000"),SEQ=SEQ+R3
2240 WRITE (DDW04_DEV,KEY=A0$)IOL=DDW04A
2300 REM " --- Phase 2: DDM-14 to DDW-14 Loop"
2320 READ (DDM14_DEV,KEY=DDM04_K$,DOM=2340)
2340 LET DDM14_K$=KEY(DDM14_DEV,END=2500)
2360 IF POS(DDM04_K$=DDM14_K$)<>1 THEN GOTO 2500
2380 READ (DDM14_DEV,KEY=DDM14_K$)IOL=DDM14A
2400 REM " --- Phase 2: Write DDW-14 With New Sequence Number"
2420 LET C0$(8,3)=A0$(8,3)
2440 WRITE (DDW14_DEV,KEY=C0$)IOL=DDW14A
2490 GOTO 2340
2500 REM " --- Phase 2: Get Next DDM-04 Record"
2590 GOTO 2100
3000 REM " --- Phase 3: Delete Old Sequence Numbers"
3010 CALL "SYC.NB","Phase 3:",3,COLUMN
3020 LET DDM04_K$=R0$+R1$
3040 READ (DDM04_DEV,KEY=DDM04_K$,DOM=3100)
3090 GOTO 3140
3100 REM " --- Phase 3: DDM-04 Delete Loop"
3120 LET DDM04_K$=KEY(DDM04_DEV,END=4000)
3140 IF POS(R0$+R1$=DDM04_K$)<>1 THEN GOTO 4000
3160 READ (DDM04_DEV,KEY=DDM04_K$)IOL=DDM04A
3180 PRINT @(COLUMN,11),DDM04_K$(8,3),
3200 REM " --- Phase 3: Delete DDM-04 Old Record & DDM-06 Cross Reference"
3220 REMOVE (DDM04_DEV,KEY=DDM04_K$)
3240 GOSUB 6000
3260 IF FNP$(DDM06_K$)<>"" THEN REMOVE (DDM06_DEV,KEY=DDM06_K$,DOM=3270)
3300 REM " --- Phase 3: DDM-14 Delete Loop"
3320 READ (DDM14_DEV,KEY=DDM04_K$,DOM=3340)
3340 LET DDM14_K$=KEY(DDM14_DEV,END=3500)
3360 IF POS(DDM04_K$=DDM14_K$)<>1 THEN GOTO 3500
3400 REM " --- Phase 3: Delete DDW-14 Old Record"
3420 REMOVE (DDM14_DEV,KEY=DDM14_K$)
3490 GOTO 3340
3500 REM " --- Phase 3: Get Next DDM-04 Record"
3590 GOTO 3100
4000 REM " --- Phase 4: Copy New Sequence Numbers"
4010 CALL "SYC.NB","Phase 4:",3,COLUMN
4020 READ (DDW04_DEV,KEY="",DOM=4100)
4100 REM " --- Phase 4: DDW-04 Copy Loop"
4120 LET DDW04_K$=KEY(DDW04_DEV,END=4900)
4140 READ (DDW04_DEV,KEY=DDW04_K$)IOL=DDW04A
4160 WRITE (DDM04_DEV,KEY=A0$)IOL=DDM04A
4180 PRINT @(COLUMN,11),A0$(8,3),
4200 REM " --- Phase 4: Write DDM-06 Cross Reference"
4220 GOSUB 6000
4240 IF FNP$(DDM06_K$)<>"" THEN WRITE (DDM06_DEV,KEY=DDM06_K$)IOL=DDM06A
4300 REM " --- Phase 4: DDW-14 Copy Loop"
4320 READ (DDW14_DEV,KEY=DDW04_K$,DOM=4340)
4340 LET DDW14_K$=KEY(DDW14_DEV,END=4500)
4360 IF POS(DDW04_K$=DDW14_K$)<>1 THEN GOTO 4500
4380 READ (DDW14_DEV,KEY=DDW14_K$)IOL=DDW14A
4400 REM " --- Phase 4: Write DDM-14 New Record"
4420 WRITE (DDM14_DEV,KEY=C0$)IOL=DDM14A
4490 GOTO 4340
4500 REM " --- Phase 4: Get Next DDW-04 Record"
4590 GOTO 4100
4900 REM " --- Done"
4920 GOTO 9900
6000 REM " --- Build key for DDM-06"
6010 DIM DDM06_K$(57)
6020 READ (DDM01_DEV,KEY=A1$(1,12),DOM=6090)IOL=DDM01A
6030 IF FNP$(A1$(17,30))<>"" THEN LET DESC$=A1$(17,30) ELSE LET DESC$=B1$(1,30)
6040 IF A[1]>0 THEN LET LENGTH=B[0]*A[1] ELSE LET LENGTH=B[0]
6050 LET DDM06_K$(1)=A1$(1,12)+DESC$+B1$(37,1)+STR(LENGTH:"0000")+A0$(1,10)
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8060 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "WOR - Production Exception Report"
0020 REM "Program: WOR.SB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "02Jan97 JJD: Include only current firm work orders (1130)"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 APM01A: IOLIST B0$,B1$
0220 ARM01A: IOLIST C0$,C1$
0230 BMM08A: IOLIST X0$,X[ALL]
0240 WOE01A: IOLIST A0$(1),A1$(1),A[ALL]
0250 WOE02A: IOLIST D0$,D1$,D[ALL]
0260 WOM02A: IOLIST X0$,X[ALL]
0270 WOE32A: IOLIST D0$,D1$,D[ALL]
0290 WOT01A: IOLIST W0$,W1$,W[ALL]
0500 REM " --- Init Data
0510 LET HEADINGS=3
0520 DIM X[13],T[2],HEADINGS$[HEADINGS],A0$(75),A1$(141)
0530 LET H4$="For Run Date Of "+FNB$(R0$)
0535 IF R2$="Y" THEN LET H4$=H4$+" Including All Incomplete Steps" ELSE LET H4$=H4$+" Including Only Past Due Steps"
0540 LET H5$="For "
0550 IF POS("L"=R1$)>0 THEN LET H5$=H5$+"Past Due Labor And "
0560 IF POS("P"=R1$)>0 THEN LET H5$=H5$+"Past Due P.O.'s And "
0565 LET H5$=H5$(1,LEN(H5$)-5)
0570 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$
0580 LET HEADINGS$[2]=H4$,HEADINGS$[3]=H5$,H0=80,H=0,WHEN$=F0$(7,3),CLOCK$=""
0600 REM " --- Open Printer
0610 CALL "SYC.GA",7,1,"","",STATUS
0620 IF STATUS<>0 THEN GOTO 9900
0700 REM " --- Background
0720 CALL "SYC.NB","Calculating:",25,COLUMN
0730 PRINT 'SB',@(COLUMN+8,11),"Printing:",'SF',
0740 LET COLUMN1=COLUMN+18
1000 REM " --- Init Read
1020 READ (WOE01_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Main Read
1120 READ (WOE01_DEV,END=4000)IOL=WOE01A
1130 IF POS(N0$=A0$)<>1 THEN GOTO 4000
1200 REM " --- Breaks
1210 PRINT @(COLUMN,11),A0$(5,7),
1220 IF A0$(15,1)<>"O" THEN GOTO 1100
1410 LET LABOR$=""
1500 REM " --- Operation Overdue?
1510 IF POS("L"=R1$)=0 THEN GOTO 1600
1520 GOSUB 6100
1530 IF OPIND$="" THEN GOTO 1600
1540 IF R0$<=DATE_REQ$ THEN GOTO 1600
1550 LET LABOR$="Y"
1600 REM " --- Purchase Overdue?
1610 IF POS("P"=R1$)=0 THEN GOTO 1700
1620 GOSUB 6600
1700 REM " --- Check For Print
1710 IF LABOR$="" AND LEN(SUBS$)=0 THEN GOTO 1100
1990 PRINT @(COLUMN1,11),A0$(5,7),
2000 REM " --- Print Data
2010 IF L+3>L9 THEN GOSUB 5000
2020 DIM C1$(30),DESC$(20)
2030 IF A0$(14,1)="I" THEN LET DESC$(1)=A0$(56) ELSE LET DESC$(1)=FNP$(A1$(1,30))+" "+FNP$(A1$(31,30))
2040 IF AR$="N" THEN GOTO 2060
2050 FIND (ARM01_DEV,KEY=N0$+A0$(34,6),DOM=2051)IOL=ARM01A
2060 PRINT (7)@(1),A1$(97,1),@(4),A0$(5,7),@(12),DESC$,@(33),FNB$(A0$(19,3)),@(42),FNB$(A0$(25,3)),@(51),FNB$(A0$(22,3)),@(61),A0$(12,2),@(66),A1$(98,1); LET L=L+1
2070 IF FNP$(C1$(1,30))<>"" THEN PRINT (7)@(12),C1$(1,30),@(70); LET L=L+1
2100 REM " --- Operation Loop
2110 IF LABOR$="" THEN GOTO 2200
2120 FOR X=NOW TO NO_OPS
2130 IF FNP$(OPS$[X,0])="" THEN EXITTO 2200
2140 IF R2$="N" THEN IF OPS$[X,1]>R0$ THEN GOTO 2190
2150 IF P3$(1,1)="Y" THEN FIND (BMM08_DEV,KEY=N0$+OPS$[X,0],DOM=2151)IOL=BMM08A
2155 IF P3$(1,1)<>"Y" THEN FIND (WOM02_DEV,KEY=N0$+OPS$[X,0],DOM=2151)IOL=WOM02A
2160 IF L+1>L9 THEN GOSUB 5000
2180 PRINT (7)@(6),"Op:",@(10),OPS$[X,0],@(14),X0$(6,20),@(35),"Setup Left: ",OPS[X,2]-OPS[X,3]:"###.00",@(54),"Run Left: ",OPS[X,0]-OPS[X,1]:"###.00",@(72),FNB$(OPS$[X,1]); LET L=L+1
2190 NEXT X
2200 REM " --- Subcontract Loop
2210 IF LEN(SUBS$)=0 THEN GOTO 2900
2220 FOR X=1 TO LEN(SUBS$) STEP 3
2230 IF L+1>L9 THEN GOSUB 5000
2240 FIND (WOE32_DEV,KEY=A0$(1,11)+"C"+SUBS$(X,3))IOL=WOE32A
2245 IF R2$="N" AND D1$(4,3)>R0$ THEN GOTO 2310
2250 DIM B1$(30)
2255 LET B1$(1)="Invalid Vendor",STATUS$=""
2260 FIND (APM01_DEV,KEY=N0$+D1$(7,6),DOM=2261)IOL=APM01A
2265 IF D1$(56,1)=" " THEN LET STATUS$="None"
2270 IF D1$(56,1)="R" THEN LET STATUS$="Req."
2280 IF D1$(56,1)="P" THEN LET STATUS$="P.O."
2290 PRINT (7)@(6),"Stat:",@(12),STATUS$,@(17),"Vndr:",@(23),B1$(1,30),@(53),"Days Left:",D[5]:"##0",@(72),FNB$(D1$(4,3))
2300 LET L=L+1
2310 NEXT X
2910 IF L+2>L9 THEN GOSUB 5000; GOTO 2990
2920 PRINT (7)'LF',""
2930 LET L=L+2
2990 GOTO 1100
4000 REM " --- End Of File Here
4100 GOTO 9900
5000 REM " --- Heading
5010 LET L=HEADINGS+1
5040 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5050 IF STATUS>0 THEN EXITTO 9900
5080 PRINT (7)""
5100 PRINT (7)@(0),"Pri",@(6),"W.O.",@(12),"Description",@(33),"Estimate",@(43),"Actual",@(51),"Estimate",@(60),"Type",@(65),"Sch",@(74),"Date"
5110 PRINT (7)@(0),"Cod",@(5),"Number",@(12),"Customer",@(35),"Start",@(44),"Start",@(51),"Complete",@(65),"Mth",@(72),"Required"
5120 PRINT (7)""
5140 LET L=L+4
5190 RETURN 
6100 REM " --- Find Current Location of W.O.
6110 LET OPIND$="",X=0,NOW_OP$="",LAB_REM=0
6120 DIM OPS$[NO_OPS,1],OPS[NO_OPS,4]
6130 READ (WOE02_DEV,KEY=A0$(1,11)+"A",DOM=6131)
6200 REM " --- Read Loop for Standards
6210 LET K2$=KEY(WOE02_DEV,END=6290)
6220 IF POS(A0$(1,11)=K2$)<>1 THEN GOTO 6290
6230 READ (WOE02_DEV,KEY=K2$)IOL=WOE02A
6240 IF D[2]=0 THEN GOTO 6200; REM "Must have hours to work!
6250 LET OPIND$=OPIND$+D0$(13,3),OPS$[X,0]=D1$(1,3),OPS$[X,1]=D1$(4,3),OPS[X,0]=D[2],OPS[X,2]=D[8]
6260 LET X=X+1
6270 GOTO 6200
6280 IF OPIND$="" THEN GOTO 6590; REM "No labor required!
6290 READ (WOT01_DEV,KEY=A0$(1,11),DOM=6300)
6300 REM " --- Read Loop for Actuals
6310 LET K3$=KEY(WOT01_DEV,END=6400)
6320 IF POS(A0$(1,11)=K3$)<>1 THEN GOTO 6400
6330 READ (WOT01_DEV,KEY=K3$)IOL=WOT01A
6350 LET X=POS(W1$(1,3)=OPIND$,3)
6360 IF X=0 THEN GOTO 6300
6370 LET X=INT(X/3)
6380 LET OPS[X,1]=OPS[X,1]+W[0],OPS[X,3]=OPS[X,3]+W[6],OPS[X,4]=OPS[X,4]+W[5]
6390 GOTO 6300
6400 REM " --- Setup Current Op Code
6410 LET NOW=0
6420 FOR X=0 TO LEN(OPIND$)/3-1
6430 IF OPS[X,1]>0 THEN LET NOW=X
6435 IF OPS[X,4]>=A[0] THEN LET NOW=X+1
6440 NEXT X
6450 IF OPS[NOW,1]>=OPS[NOW,0] THEN LET NOW=NOW+1
6460 IF NOW>LEN(OPIND$)/3-1 THEN LET OPIND$=""; GOTO 6590
6470 LET NOW_SEQ$=OPIND$(NOW*3+1,3)
6500 REM " --- Now setup NOW_OP$
6510 LET NOW_OP$=OPS$[NOW,0],DATE_REQ$=OPS$[NOW,1]
6590 RETURN 
6600 REM " --- Check for Subcontracts Overdue
6610 LET SUBS$="",SUB_REM=0
6620 READ (WOE32_DEV,KEY=A0$(1,11)+"C",DOM=6621)
6700 REM " --- Read Loop for Subs
6710 LET K2$=KEY(WOE32_DEV,END=6790)
6720 IF POS(A0$(1,11)+"C"=K2$)<>1 THEN GOTO 6790
6730 READ (WOE32_DEV,KEY=K2$)IOL=WOE32A
6735 IF POS(D1$(91,1)="MI")>0 THEN GOTO 6710
6740 IF D1$(56,1)="C" THEN GOTO 6700
6750 IF D1$(4,3)>=R0$ THEN GOTO 6700
6760 LET SUBS$=SUBS$+D0$(13,3)
6770 LET SUB_REM=SUB_REM+D[5]
6780 GOTO 6700
6790 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

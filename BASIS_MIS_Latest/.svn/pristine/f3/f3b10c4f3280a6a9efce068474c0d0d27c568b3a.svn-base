0010 REM "Replenishment Report Program (Input Overlay)
0020 REM "Program POR.FA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "21Oct96 CAJ: Program rewritten for 6.0.2 replenishment
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=24
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="SYS-01",FILES$[2]="APM-01",FILES$[3]="APM-02"
0130 LET FILES$[4]="APM-05",FILES$[5]="APM-06",FILES$[6]="IVM-01"
0140 LET FILES$[7]="IVM-10",FILES$[8]="POE-01",FILES$[9]="POE-02"
0145 LET FILES$[10]="POE-06",FILES$[11]="POE-07",FILES$[12]="POE-11"
0150 LET FILES$[13]="POE-16",FILES$[14]="POE-17",FILES$[15]="POE-21"
0153 LET FILES$[16]="POE-26",FILES$[17]="POE-31",FILES$[18]="POE-36"
0155 LET FILES$[19]="POE-41",FILES$[20]="POE-52",FILES$[21]="POM-02"
0157 LET FILES$[22]="POS-10"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET SYS01_DEV=CHANNELS[1],APM01_DEV=CHANNELS[2],APM02_DEV=CHANNELS[3]
0180 LET APM05_DEV=CHANNELS[4],APM06_DEV=CHANNELS[5],IVM01_DEV=CHANNELS[6]
0185 LET IVM10_DEV=CHANNELS[7],POE01_DEV=CHANNELS[8],POE02_DEV=CHANNELS[9]
0187 LET POE06_DEV=CHANNELS[10],POE07_DEV=CHANNELS[11],POE11_DEV=CHANNELS[12]
0190 LET POE16_DEV=CHANNELS[13],POE17_DEV=CHANNELS[14],POE21_DEV=CHANNELS[15]
0193 LET POE26_DEV=CHANNELS[16],POE31_DEV=CHANNELS[17],POE36_DEV=CHANNELS[18]
0195 LET POE41_DEV=CHANNELS[19],POE52_DEV=CHANNELS[20],POM02_DEV=CHANNELS[21]
0197 LET POS10_DEV=CHANNELS[22]
0200 REM " --- IOLists
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 APS01A: IOLIST P0$,VENDIMASK$,P2$,P3$,P4$,VENDOMASK$
0320 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,X$,M1$,M2$,M3$
0350 IVS01C: IOLIST *,ABCP[ALL],FACTORS[ALL]
0360 GLS01A: IOLIST G0$,G1$,G2$,G3$
0400 REM " --- Parameters
0405 DIM INFO$[20],DESC[3],DESCLEN[3],ABCP[25],FACTORS[25]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0435 LET VENDLEN=NUM(P2$(1,2))
0440 FIND (SYS01_DEV,KEY=N0$+"IV02",DOM=9800)IOL=IVS01C
0445 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0450 LET IVPRECISION=NUM(I2$(5,1)),ITEMLEN=NUM(I2$(1,2)),DEFAULTWHSE$=I3$(6,2)
0455 LET CURPER$=I4$(1,2),CURYR$=I4$(3,2),CURPER=NUM(CURPER$),CURYR=FNYY_YEAR(CURYR$)
0460 CALL "SYC.PA",SYS01_DEV,CURPER,CURYR,BEGPER$,ENDPER$,STATUS
0465 FOR X=1 TO 3
0470 LET DESC[X]=NUM(I2$((X-1)*2+9,2)),DESCLEN=DESCLEN+DESC[X]
0475 NEXT X
0480 LET DAYOFMONTH=NUM(F0$(22,2))
0485 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0490 LET TOT_PERS=NUM(G2$(1,2))
0495 PRECISION IVPRECISION
0500 REM " --- Init Data
0510 DIM DASHES$(132,"-"),O[14],H[14],HSEL[9]
0520 LET SUGGONLY$="Y"
0530 CALL "SYC.RM",M2$,M2,8,10
0550 LET M6$="###0.00-",M9$="##0-",PHNMSK$="(###) ###-####"
0560 LET M1=LEN(M1$),M3=LEN(M3$),M6=LEN(M6$),M9=LEN(M9$)
0570 LET TOTMLEN=M1+(10*M2)+M3+M6+M9+7
0575 IF TOTMLEN<=130 THEN GOTO 0600
0580 IF M1>12 THEN CALL "SYC.RM",M1$,M1,12,M1-1; GOTO 0560
0585 IF M3>9 THEN CALL "SYC.RM",M3$,M3,9,M3-1; GOTO 0560
0590 IF M2>8 THEN CALL "SYC.RM",M2$,M2,8,M2-1; GOTO 0560
0600 LET O[0]=132-(TOTMLEN),O[1]=O[0]+4,O[2]=O[1]+M2,O[3]=O[2]+M2
0610 LET O[4]=O[3]+M2,O[5]=O[4]+M9,O[6]=O[5]+M2,O[7]=O[6]+M2,O[8]=O[7]+M2
0620 LET O[9]=O[8]+M2,O[10]=O[9]+M2,O[11]=O[10]+M2,O[12]=O[11]+M3+1
0630 LET O[13]=O[12]+M2+1,O[14]=O[13]+M6+1
0635 REM " --- Column headers
0640 LET H[0]=O[0]-1,H[1]=O[1]+M2-8,H[2]=O[2]+M2-8,H[3]=O[3]+M2-7
0645 LET H[4]=O[4]+M9-4,H[5]=O[5]+M2-6,H[6]=O[6]+M2-6,H[7]=O[7]+M2-7
0650 LET H[8]=O[8]+M2-6,H[9]=O[9]+M2-5,H[10]=O[10]+M2-6,H[11]=O[11]+M3-6
0655 LET H[12]=O[12]+M2-10,H[13]=O[13]+M6-7,H[14]=O[14]+M1-9
0660 REM " --- Selections column headers
0670 LET HSEL[0]=24,HSEL[1]=30,HSEL[2]=38,HSEL[3]=46,HSEL[4]=55,HSEL[5]=64
0680 LET HSEL[6]=75,HSEL[7]=86,HSEL[8]=93,HSEL[9]=100
0700 REM " --- Background
0720 FOR X=4 TO 6 STEP 2
0725 CALL "SYC.WC",1,0,80,0,0,X,0
0727 NEXT X
0730 PRINT 'SB',
0750 PRINT @(23,3),"Print Warehouse Detail:",@(15,5),"Print Only Suggested Buy Items:",'SF'
1000 REM " --- Options
1010 IF INCLUDEWHSE$="" THEN LET INCLUDEWHSE$="Y"
1020 LET V0$="Y",V1$="C",V2$=INCLUDEWHSE$,V3$="YN",V4$="Include Warehouse Usage Detail for Multiple Warehouse Buys? ",V0=1,V1=47,V2=3,I0=0
1040 GOSUB 7000
1060 LET INCLUDEWHSE$=V$
1090 GOTO 3900
1100 REM " --- All or Suggested Buy only
1120 LET V0$="Y",V1$="C",V2$=SUGGONLY$,V3$="YN",V4$="N=Print all Items for Vendor, Y=Only Items with Replenishment Amounts",V0=1,V1=47,V2=5
1140 GOSUB 7000
1160 LET SUGGONLY$=V$
1180 GOTO 3900
3900 REM " --- Next Field
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,4000
4000 REM " --- Overlay
4020 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is the Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4060 IF V$="N" THEN GOTO 1000
4080 IF V$<>"Y" THEN GOTO 4000
4100 RUN "POR.FB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

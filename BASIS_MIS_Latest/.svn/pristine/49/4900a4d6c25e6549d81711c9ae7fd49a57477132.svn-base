0010 REM <license_history.bbx>
0020 SETESC EXIT
0030 SETERR EXIT
0040 ENTER SN$
0050 LET SN$=PAD(SN$,20)
0060 GOSUB LICENSE_HISTORY
0070 LET TERM$=STBL("!TERMS"),A$=$1E031F04$
0080 IF POS(A$=TERM$)=0 THEN LET TRASH$=STBL("!TERMS",A$+TERM$)
0090 LET LINES=POS($0A$=LBOX$,1,0); IF LINES=0 THEN LET LBOX$=" "+$0A$
0100 LET MAXLINES=15
0110 PRINT 'WINDOW'(2,6,77,MAXLINES+4,""),
0120 LET TOPLINE=0,DIRECTION=0
0130 SCREEN: GOSUB P1_P2
0140 PRINT 'CS',@(0,0),LBOX$(P1,P2),
0150 PRINT @(14,MAXLINES+1),'BR',"Use up/down arrows to scroll (E=End) ",'ER',
0160 INPUT X$,
0170 ON CTL GOTO C0,SCREEN,SCREEN,C3,C4,C0
0180 C3: LET TOPLINE=TOPLINE-1,DIRECTION=1; GOTO SCREEN
0190 C4: LET TOPLINE=TOPLINE+1,DIRECTION=0; GOTO SCREEN
0200 C0: IF CVS(X$,7)="E" THEN GOTO EXIT
0210 IF DIRECTION=0 AND TOPLINE<LINES THEN GOTO C4 ELSE LET DIRECTION=1
0220 IF TOPLINE>0 THEN GOTO C3 ELSE LET DIRECTION=0; GOTO C4
0230 EXIT: 
0240 IF TERM$<>$$ THEN LET TRASH$=STBL("!TERMS",TERM$)
0250 SETERR EXIT2; PRINT 'POP'
0260 EXIT2: 
0270 EXIT
0280 P1_P2: 
0290 IF TOPLINE<=0 THEN LET TOPLINE=0,P1=1; GOTO P2
0300 IF TOPLINE>=LINES THEN LET TOPLINE=LINES; GOTO P2
0310 LET P1=POS($0A$=LBOX$,1,TOPLINE)+1
0320 P2: 
0330 LET L=MAXLINES
0340 P2A: LET P2=POS($0A$=LBOX$(P1),1,L)
0350 IF P2>0 THEN RETURN
0360 LET L=L-1; IF L<1 THEN RETURN
0370 GOTO P2A
0380 LICENSE_HISTORY: 
0390 DIM ARE83$:"FIRM_ID:C(2),AR_TYPE:C(2),CUSTOMER_NBR:C(6),ORDER_NUMBER:C(7),LINE_NUMBER:C(3),LICENSE_SEQ:C(3),SEQUENCE_NBR:C(3*=10),ACTION:C(1),SNHIST_FLAG:C(1),AUTH_CODE:C(15),ACT_KEY:C(15),CONTRACT:C(6),SAM_ACTIVE:C(1),DEALER_STR:C(1*=10),SERIAL_NBR:C(20*=10),LICENSE_CNT:N(7*=10),DEALER_NUM_1:N(1*=10),DEALER_NUM_2:N(1*=10)"
0400 DIM SNM01$:"SERIAL_NBR:C(20*=10),SERIAL_DESC:C(60*=10),PRODUCT:C(3),PLATFORM:C(3),OS_LEVEL:C(3),PRODUCT_REV:C(3),LICENSE_TYPE:C(3),MEDIA_TYPE:C(3*=10),ACTIVE_FLAG:C(1),ORIG_SALE_DATE:C(3),CUSTOMER_NBR:C(6),PORT_ID:C(5),FIRM_ID:C(2),CONTRACT:C(6),SAM_ACTIVE:C(1),DEALER_STR:C(11*=10),COMMENT:C(60*=10),USERS:N(5*=10),DIST_RESET:N(1*=10),DEALER_NUM_2:N(1*=10)"
0410 DIM SNT01$:"SERIAL_NBR:C(20),TRANS_DATE:C(3),SN_DET_SEQ:C(5*=10),CUSTOMER_NBR:C(6),AR_INV_NBR:C(7),SEQUENCE_NBR:C(3),TYPE_OF_SALE:C(3),PRODUCT_REV:C(3),AUTH_CODE:C(15),SERVER_NBR:C(20*=10),FIRM_ID:C(2),ACTION:C(1),FIXED_FLOAT:C(1),CONTRACT:C(6),SAM_ACTIVE:C(1),DEALER_STR:C(29*=10),USERS:N(5*=10),EXT_PRICE:N(7*=10),LICENSE_CNT:N(7*=10),DEALER_NUM_1:N(1*=10),DEALER_NUM_2:N(1*=10),DEALER_NUM_3:N(1*=10)"
0420 DIM LICENSE_RESET$:"SERIAL_NBR:C(20),RESET_DATE:C(8),RESET_TIME:C(6),RESET_BY:C(8),RESET_TO:C(1),AVAILABLE:C(16*=10)"
0430 DIM LOGEXTRACT$:"SERIAL_NUM:C(12),SEQ_NUM:C(1),GEN_COUNT:C(1),DATE:C(8),TIME:C(5),FROM:C(40),HOSTID:C(40),COMPANY:C(40),LASTNAME:C(20),FIRSTNAME:C(20),PHONE:C(20),FAX:C(20),EMAIL:C(40),AUTHNUM:C(10),HOSTNAME:C(20)"
0440 DIM SN_LICENSE$:"SERIAL_NUM:C(12),DATE:C(8),TIME:C(6),PROD_TYPE:C(14),PROD_REV:C(5),HOSTID:C(60),AUTHNUM:C(10),EXPIRE:C(11),OPEN_STRING:C(24),USERS:N(4),LIC_COUNT:N(4),TEMP_COUNT:N(4),OPEN_NUM:N(4)"
0450 DIM OEM$:"serial_num:c(9),tran:c(4),date:c(8),item_num:c(8),rev_level:c(5),prod_type:c(1),sale_type:c(1),active_flag:c(1),temp_lic_flag:c(1),lic_auth:c(13),memo:c(20),float_flag:c(1),tran_type:c(3),available:c(26*),cur_user:n(4*),new_user:n(4*),lic_count:n(1*),num1:n(1*),num2:n(1*)"
0460 LET ADATA$=STBL("AON")+"ADATA/"
0470 LET FILES=7,C=1; DIM F[FILES]
0480 LET ARE83=UNT,F[C]=ARE83,C=C+1; OPEN (ARE83)ADATA$+"ARE-83"
0490 LET SNM01=UNT,F[C]=SNM01,C=C+1; OPEN (SNM01)ADATA$+"SNM-01"
0500 LET SNT01=UNT,F[C]=SNT01,C=C+1; OPEN (SNT01)ADATA$+"SNT-01"
0510 LET OEM=UNT,F[C]=OEM,C=C+1; OPEN (OEM)ADATA$+"oem_sn_trans.dat"
0520 LET LICENSE_RESET=UNT,F[C]=LICENSE_RESET,C=C+1; OPEN (LICENSE_RESET)ADATA$+"license_reset.dat"
0530 LET LOGEXTRACT=UNT,F[C]=LOGEXTRACT,C=C+1; OPEN (LOGEXTRACT)ADATA$+"logextract.dat"
0540 LET SN_LICENSE=UNT,F[C]=SN_LICENSE,C=C+1; OPEN (SN_LICENSE)ADATA$+"sn_license.dat"
0550 LET ACTIVE$="",OEM_SN=0; READ RECORD (SNM01,KEY=SN$,DOM=SNM01_DONE)SNM01$
0560 LET ACTIVE$=SNM01.ACTIVE_FLAG$
0570 SNM01_DONE: IF ACTIVE$="" THEN GOSUB OEM_CHECK
0580 LET ICHAN=LOGEXTRACT,TEMP_ITEM$="",LBOX$=""
0590 DIM TMPL$:FATTR(LOGEXTRACT$); GOSUB BUILD_TEMP_ITEM
0600 LET ICHAN=SN_LICENSE
0610 DIM TMPL$:FATTR(SN_LICENSE$); GOSUB BUILD_TEMP_ITEM
0620 LET ICHAN=LICENSE_RESET,LRX$=CVS(FATTR(LICENSE_RESET$),4)
0630 LET P=POS("RESET_DATE"=LRX$)
0640 IF P THEN LET LRX$=LRX$(1,P-1)+LRX$(P+6)
0650 LET P=POS("RESET_TIME"=LRX$)
0660 IF P THEN LET LRX$=LRX$(1,P-1)+LRX$(P+6)
0670 DIM TMPL$:LRX$; GOSUB BUILD_TEMP_ITEM
0680 IF LEN(TEMP_ITEM$)<50 THEN GOTO CURRENT_STATUS
0690 LET TEMP_ITEM$=SSORT(TEMP_ITEM$,50)
0700 LET HOLDDATE$="",FILL$=FILL(75,"_")
0710 LICENSE_HISTORY_LOOP: 
0720 LET DATE$=TEMP_ITEM$(1,8),TIME$=TEMP_ITEM$(9,6),ICHAN=NUM(TEMP_ITEM$(15,2)),K$=CVS(TEMP_ITEM$(17,34),2),TEMP_ITEM$=TEMP_ITEM$(51)
0730 IF HOLDDATE$=DATE$ THEN LET LBOX$=LBOX$+$0A$ ELSE LET LBOX$=LBOX$+FILL$+$0A$+DATE$(5,2)+"/"+DATE$(7,2)+"/"+DATE$(1,4)+$0A$
0740 LET HOLDDATE$=DATE$,TIME$=TIME$(1,2)+":"+TIME$(3,2)
0750 SWITCH ICHAN
0760 CASE LOGEXTRACT; DIM TMPL$:FATTR(LOGEXTRACT$); BREAK
0770 CASE SN_LICENSE; DIM TMPL$:FATTR(SN_LICENSE$); BREAK
0780 CASE LICENSE_RESET; DIM TMPL$:LRX$; BREAK
0790 SWEND
0800 READ RECORD (ICHAN,KEY=K$)TMPL$
0810 LET LBOX$=LBOX$+"  "+TIME$+" "
0820 SWITCH ICHAN
0830 CASE LOGEXTRACT; LET LBOX$=LBOX$+"License Request Received"+$0A$
0840 LET X$=CVS(TMPL.FIRSTNAME$+TMPL.LASTNAME$,35),Y$=CVS(TMPL.COMPANY$,3)
0850 IF X$<>"" AND Y$<>"" THEN LET X$=X$+"/"
0860 IF X$+Y$<>"" THEN LET LBOX$=LBOX$+"        From: "+X$+Y$+$0A$
0870 IF CVS(TMPL.EMAIL$,2)<>"" THEN LET LBOX$=LBOX$+"       Email: "+TMPL.EMAIL$+$0A$
0880 IF CVS(TMPL.FAX$,2)<>"" THEN LET LBOX$=LBOX$+"         Fax: "+TMPL.FAX$+$0A$
0890 IF CVS(TMPL.HOSTNAME$,2)<>"" THEN LET LBOX$=LBOX$+"    HostName: "+TMPL.HOSTNAME$+$0A$
0900 LET LBOX$=LBOX$+"      HostId: "+TMPL.HOSTID$+$0A$
0910 LET LBOX$=LBOX$+"      Auth #: "+TMPL.AUTHNUM$+$0A$
0920 BREAK
0930 CASE SN_LICENSE; LET LBOX$=LBOX$+"License Generated"+$0A$
0940 LET LBOX$=LBOX$+"      HostId: "+TMPL.HOSTID$+$0A$
0950 LET LBOX$=LBOX$+"      Auth #: "+TMPL.AUTHNUM$+FILL(12)+"Revision: "+TMPL.PROD_REV$+$0A$
0960 LET LBOX$=LBOX$+"       Users: "+STR(TMPL.USERS:"###0")+FILL(15)+"LicenseType: "+TMPL.PROD_TYPE$+$0A$
0970 IF CVS(TMPL.EXPIRE$,2)<>"" THEN LET LBOX$=LBOX$+"     Expires: "+TMPL.EXPIRE$+$0A$
0980 BREAK
0990 CASE LICENSE_RESET
1000 LET LBOX$=LBOX$+"License Status Reset by "+TMPL.RESET_BY$+$0A$
1010 BREAK
1020 SWEND
1030 IF TEMP_ITEM$<>"" THEN GOTO LICENSE_HISTORY_LOOP
1040 CURRENT_STATUS: 
1050 LET S$=""
1060 IF ACTIVE$<>"Y" THEN GOTO CURRENT_STATUS_END
1070 IF OEM_SN=0 THEN GOSUB LICENSE_STATUS
1080 IF LICENSE_STATUS<0 THEN GOTO CURRENT_STATUS_END
1090 LET S$="Current License Status: "+STR(LICENSE_STATUS)
1100 IF LICENSE_STATUS=0 THEN LET S$=S$+" (Ready for licensing)" ELSE LET S$=S$+" (Licensed)"
1110 CURRENT_STATUS_END: 
1120 LET LBOX$=S$+$0A$+LBOX$
1130 FOR I=1 TO FILES
1140 CLOSE (F[I],ERR=NEXTI)
1150 NEXTI: NEXT I
1160 RETURN
1170 BUILD_TEMP_ITEM: 
1180 READ (ICHAN,KEY=CVS(SN$,2),DOM=BUILD_TEMP_ITEM_ICHAN)
1190 BUILD_TEMP_ITEM_ICHAN: 
1200 LET K$=KEY(ICHAN,END=BUILD_TEMP_ITEM_END)
1210 IF POS(CVS(SN$,2)=K$)<>1 THEN GOTO BUILD_TEMP_ITEM_END
1220 READ RECORD (ICHAN,KEY=K$)TMPL$
1230 IF ICHAN=LICENSE_RESET THEN IF TMPL.RESET_TO$<>"0" THEN GOTO BUILD_TEMP_ITEM_ICHAN
1240 LET TIME$=PAD(TMPL.TIME$,6),P=POS(":"=TIME$)
1250 IF P THEN LET TIME$=TIME$(1,P-1)+TIME$(P+1)+" "
1260 IF ICHAN=LOGEXTRACT THEN LET TIME$="00000"+TMPL.SEQ_NUM$
1270 LET X$=PAD(K$,34),TEMP_ITEM$=TEMP_ITEM$+TMPL.DATE$+TIME$+STR(ICHAN:"00")+X$
1280 GOTO BUILD_TEMP_ITEM_ICHAN
1290 BUILD_TEMP_ITEM_END: 
1300 RETURN
1310 LICENSE_STATUS: 
1320 LET LICENSE_STATUS=-1
1330 READ (ARE83,KEY="",DOM=LICENSE_STATUS_ARE83)
1340 LICENSE_STATUS_ARE83: 
1350 LET K$=KEY(ARE83,END=END_LICENSE_STATUS_ARE83)
1360 READ RECORD (ARE83,KEY=K$)ARE83$
1370 IF CVS(SN$,2)<>CVS(ARE83.SERIAL_NBR$,2) OR ARE83.SNHIST_FLAG$="Y" OR LEN(CVS(ARE83.AUTH_CODE$,3))<>10 THEN GOTO LICENSE_STATUS_ARE83
1380 IF ARE83.ACTION$="D" THEN LET LICENSE_STATUS=-1; GOTO LICENSE_STATUS_ARE83
1390 LET LICENSE_STATUS=ARE83.LICENSE_CNT
1400 GOTO LICENSE_STATUS_ARE83
1410 END_LICENSE_STATUS_ARE83: 
1420 IF LICENSE_STATUS>=0 THEN GOTO LICENSE_STATUS_END
1430 READ (SNT01,KEY=CVS(SN$,2),ERR=LICENSE_STATUS_SNT01)
1440 LICENSE_STATUS_SNT01: 
1450 LET K$=KEY(SNT01,END=LICENSE_STATUS_END)
1460 READ RECORD (SNT01,KEY=K$)SNT01$
1470 IF POS(CVS(SN$,2)=SNT01.SERIAL_NBR$)<>1 THEN GOTO LICENSE_STATUS_END
1480 IF SNT01.ACTION$="D" THEN LET LICENSE_STATUS=-1; GOTO LICENSE_STATUS_SNT01
1490 IF LEN(CVS(SNT01.AUTH_CODE$,3))<>10 THEN GOTO LICENSE_STATUS_SNT01
1500 LET LICENSE_STATUS=SNT01.LICENSE_CNT
1510 GOTO LICENSE_STATUS_SNT01
1520 LICENSE_STATUS_END: 
1530 RETURN
1540 OEM_CHECK: 
1550 READ (OEM,KEY=CVS(SN$,2),DOM=READ_OEM)
1560 READ_OEM: READ RECORD (OEM,END=END_OEM)OEM$
1570 IF OEM.SERIAL_NUM$<>CVS(SN$,2) THEN GOTO END_OEM
1580 LET OEM_SN=1,LICENSE_STATUS=OEM.LIC_COUNT,ACTIVE$=OEM.ACTIVE_FLAG$
1590 END_OEM: 
1600 RETURN
0010 REM "GLM - G/L Account Maintenance (Graphics Display)"
0020 REM "Program GLM.AF"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0210 GLM02A: IOLIST X0$(1),A[ALL],U[ALL]
0500 REM " --- Initializations"
0510 DIM PROMPTS$[6],X0$(3+P[4]),A[MP],U[MP],X[MP,NUMBER]
0540 LET L=9,R0$="A",R1$="B",R2$="a",R3$="b"
0560 LET MASK$=M1$,CHAR$="$"
0565 IF DISPLAY$="U" THEN LET MASK$=M2$,CHAR$="#"
0570 IF DISPLAY$="P" THEN LET CHAR$="*"
0580 LET MASK=LEN(MASK$)
0590 CALL "SYC.RM",MASK$,MASK,9,15
0600 REM 
0610 LET LETTERS$="PpCcNn",PROMPT$="",FOOTER$=""
0620 LET PROMPTS$[1]="Pri Actual",PROMPTS$[2]="Pri Budget"
0630 LET PROMPTS$[3]="Cur Actual",PROMPTS$[4]="Cur Budget"
0640 LET PROMPTS$[5]="Nxt Actual",PROMPTS$[6]="Nxt Budget"
0650 FOR X=1 TO LEN(RECORDS$)
0655 IF RECORDS$(X,1)<>"Y" THEN GOTO 0670
0660 LET PROMPT$=PROMPT$+LETTERS$(X,1)+"="+PROMPTS$[X]+"/"
0665 LET FOOTER$=FOOTER$+LETTERS$(X,1)
0670 NEXT X
0680 LET PROMPT$=PROMPT$+"<Enter>=Continue:",W=MAX(INT((79-(MASK+2))/P[0]),4)
0690 IF W>6 THEN LET W=6
1000 REM " --- Load appropriate records"
1010 LET LETTERS$="230145",X0=(-2)*SGN(POS(A1$(36,1)="LCI"))+1,R=0
1100 REM " --- For each requested record"
1110 FOR X=1 TO LEN(RECORDS$)
1120 IF RECORDS$(X,1)<>"Y" THEN GOTO 1300
1130 DIM A[MP],U[MP]
1140 LET X0$(1)=A0$,X0$(3+P[4],1)=LETTERS$(X,1),R=R+1
1150 FIND (GLM02_DEV,KEY=X0$,DOM=1160)IOL=GLM02A
1160 LET X1=0,X2=0
1170 IF POS(A1$(36,1)="ALC")<>0 THEN LET X1=A[0],X2=U[0]
1200 REM " --- Move amounts"
1210 FOR P=1 TO P[0]
1220 IF POS(A1$(36,1)="ALC")<>0 THEN LET A[P]=A[P]+X1,U[P]=U[P]+X2,X1=A[P],X2=U[P]
1230 IF DISPLAY$="A" THEN LET X[P,R]=A[P]*X0
1240 IF DISPLAY$="U" THEN LET X[P,R]=U[P]*X0
1250 IF DISPLAY$="P" THEN IF U[P]<>0 THEN LET X[P,R]=A[P]/U[P]
1290 NEXT P
1300 REM " --- Next record"
1390 NEXT X
2000 REM " --- Calculate scale limits"
2010 LET MINVALUE=2^15,MAXVALUE=-MINVALUE
2020 FOR P=1 TO P[0]
2030 FOR X=1 TO NUMBER
2040 IF X[P,X]<MINVALUE THEN LET MINVALUE=X[P,X]
2050 IF X[P,X]>MAXVALUE THEN LET MAXVALUE=X[P,X]
2060 NEXT X
2070 NEXT P
2200 REM " --- Determine scale increment"
2210 LET X1=ABS(MAXVALUE-MINVALUE)/14,X2=EPT(X1)
2220 IF X2>0 THEN GOTO 2300
2230 IF X2<1 THEN LET X=X1*10^(ABS(X2)+1)
2240 IF FPT(X)<>0 THEN LET X=INT(X+.99)
2250 LET STEPVALUE=X/10^ABS(X2+1)
2290 GOTO 2400
2300 IF X2>0 THEN LET X=X1/10^(X2-1)
2310 IF FPT(X)<>0 THEN LET X=INT(X+.99)
2320 LET STEPVALUE=X*10^(X2-1)
2400 REM " --- Set starting scale value"
2410 IF STEPVALUE=0 THEN LET STEPVALUE=1
2420 LET SCALE=MINVALUE/STEPVALUE
2440 IF FPT(SCALE)<>0 THEN LET SCALE=INT(SCALE-SGN(1-SGN(SCALE))*.99)
2450 LET SCALE=SCALE*STEPVALUE
2500 REM " --- Display headings/footings"
2510 LET P=MASK+2
2520 PRINT @(0,L0),'CE',@(MASK-LEN(TITLE$)-1),TITLE$
2530 FOR X=1 TO P[0]
2540 PRINT @(P,L0),P3$((X-1)*3+235,3),@(P,21),FOOTER$
2550 LET P=P+W
2560 NEXT X
3000 REM " --- Display Graph"
3010 FOR X=0 TO 14
3020 LET ROW=20-X
3030 IF SCALE=0 THEN LET SCALE=SCALE+STEPVALUE
3040 PRINT @(0,ROW),SCALE:MASK$,
3100 REM 
3110 FOR P=1 TO P[0]
3200 REM 
3210 FOR Y=1 TO NUMBER
3220 LET X$=""
3230 IF SCALE>=0 THEN IF X[P,Y]>0 THEN IF X[P,Y]>=SCALE THEN LET X$=CHAR$
3240 IF SCALE<0 THEN IF X[P,Y]<0 THEN IF X[P,Y]<=SCALE THEN LET X$=CHAR$
3250 IF X$<>"" THEN PRINT @(MASK+2+(P-1)*W+(Y-1),ROW),'BR',X$,'ER',
3290 NEXT Y
3300 REM 
3390 NEXT P
3400 REM 
3410 LET SCALE=SCALE+STEPVALUE
3490 NEXT X
4000 REM " --- All done"
4010 LET V4$=PROMPT$
4020 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4090 RUN "GLM.AE"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
7490 RETURN 
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

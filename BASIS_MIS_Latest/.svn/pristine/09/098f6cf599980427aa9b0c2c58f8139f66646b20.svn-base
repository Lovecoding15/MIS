0010 REM "TMR - Label Printing - II
0020 REM "Program TMR.AB"
0030 REM "12/09/91 by Jerry Koch
0040 REM "COPYRIGHT(C)1991 MicroAccounting Systems, Inc. ALL RIGHTS RESERVED"
0090 SETERR 9000
0100 REM "OPEN/LOCK FILES"
0200 REM "IOLISTS"
0210 IOLIST A0$,A1$,A2$,A3$,A4$
0500 REM "INIT DATA"
0510 LET X9=6
0520 IF W2<=X9 THEN LET X9=W2-1
0530 DIM B0$(48),B1$(48)
0600 CALL "SYC.GA",7,1,"","",STATUS
0605 IF STATUS THEN GOTO 9900
0610 SETERR 9600
0700 REM "BACKGROUND"
0800 REM "OPTIONS"
0810 PRINT @(10,15),"Do You Wish To Print A Test Pattern? <Y/N>: ",
0815 LET V0$="Y",V1$="",V2$="",V3$="",V4$="",V0=1,V1=54,V2=15
0820 GOSUB 7000
0825 PRINT @(0,15),'CL',
0830 IF CTL=4 OR V$="END" THEN GOTO 9900
0840 ON POS(V$="YN") GOTO 0800,0850,0900
0850 DIM W0$(W0*W1*6,"X")
0860 LET X0=1
0870 GOSUB 5500
0880 GOTO 0800
0900 REM "DETERMINE START"
0920 PRINT @(0,15),'CL','SB',@(30,15),"Printing:",'SF',
1000 REM "NUMERIC SEQUENCE"
1020 READ (1,KEY=N0$+P6$,KNUM=NUM(SRT$),DOM=1030)IOL=0210
1025 GOTO 1130
1100 REM "MAIN READ"
1110 LET K$=KEY(1,KNUM=NUM(SRT$),END=4000)
1120 READ (1,KEY=K$,KNUM=NUM(SRT$))IOL=0210
1130 IF A0$(1,2)<>N0$ THEN GOTO 4000
1140 IF P8$<>"" THEN IF K$(3,LEN(P8$))>P8$ THEN GOTO 4000
1150 IF P7$<>"" THEN IF A0$(358,2)<>P7$ THEN GOTO 1100
1160 IF P9$="Y" THEN IF A0$(360,1)<>"Y" THEN GOTO 1100
1200 REM "SETUP"
1280 GOSUB 5000
1290 GOTO 1100
4000 REM "TOTALS"
4020 GOSUB 5500
4030 IF P9$="N" THEN GOTO 9900
4040 PRINT @(0,15),'CL',@(5,15),"Do You Want To Update The Literature Flag/Dates? <Y/N>: ",
4050 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="",V0=1,V1=61,V2=15
4060 GOSUB 7000
4080 IF V$="N" THEN GOTO 9900
4090 READ (1,KEY=N0$,KNUM=NUM(SRT$),DOM=4091)
4100 REM "--- Update Flags
4110 LET K$=KEY(1,KNUM=NUM(SRT$),END=4500)
4120 READ (1,KEY=K$,KNUM=NUM(SRT$))IOL=0210
4150 IF P7$<>"" THEN IF A0$(358,2)<>P7$ THEN GOTO 4100
4160 LET A0$(360,1)="N",A0$(361,8)=FNP$(PRM$[FID,TLDATE]); REM "WA: #221
4170 WRITE (1,KEY=A0$(1,23))IOL=0210
4180 READ (1,KEY=K$,KNUM=NUM(SRT$))
4190 GOTO 4100
4500 REM "--- Finished
4590 GOTO 9900
5000 REM "PREPARE ADDRESS"
5100 REM "ASSEMBLE"
5120 LET X3=0
5130 LET X3=X3+1
5140 IF X3>W3 THEN RETURN 
5150 IF X0>=W0 THEN GOSUB 5500
5160 LET X0=X0+1
5180 PRINT @(40,15),A0$(3,20),
5200 LET W0$(W1*(X0-1)+1,W1)=A0$(112,30),LINE=1
5210 LET W0$(W1*(LINE*W0+X0-1)+1,W1)=A0$(52,30); GOSUB 5300
5220 LET W0$(W1*(LINE*W0+X0-1)+1,W1)=A0$(142,30); GOSUB 5300
5230 LET W0$(W1*(LINE*W0+X0-1)+1,W1)=A0$(172,30); GOSUB 5300
5240 LET W0$(W1*(LINE*W0+X0-1)+1,W1)=CVS(A0$(202,25),2)+", "+A0$(227,2); GOSUB 5300
5250 LET W0$(W1*(LINE*W0+X0-1)+1,W1)=""; GOSUB 5300
5255 LET OFFSET=2
5260 LET X8=POS("  "=W0$(W1*((X9-OFFSET)*W0+X0-1)+1,W1))
5270 IF X8=21 OR X8>=W1 THEN IF OFFSET=2 THEN LET OFFSET=1; GOTO 5260 ELSE GOTO 5290
5275 IF X8=1 THEN LET X8=0
5285 LET W0$(W1*((X9-OFFSET)*W0+X0-1)+X8+1,W1-X8)=FNF$(A0$(229,P[2]),M7$)
5290 GOTO 5130
5300 REM "--- Increment line
5310 IF POS(" "<>W0$(W1*(LINE*W0+X0-1)+1,W1))<>0 THEN LET LINE=LINE+1
5320 RETURN 
5500 REM "PRINT"
5505 GOSUB 7400
5520 IF X0=0 THEN RETURN 
5540 FOR X=0 TO X9-1
5560 PRINT (7)@(0+W9),W0$(X*W0*W1+1,W0*W1)
5580 NEXT X
5600 IF W2>X9 THEN PRINT (7)W9$(1,W2-X9-1)
5620 LET X0=0,W0$(1)=""
5690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
7400 REM " --- Printing interrupt"
7410 INPUT (0,SIZ=0,TIM=0,ERR=7490)Q$
7420 IF CTL<>4 THEN GOTO 7490
7430 CALL "SYC.RI",Q1
7440 IF Q1=0 THEN GOTO 7490
7450 PRINT (7)""
7460 PRINT (7)"----> Report Terminated by Operator"
7470 GOTO 9900
7490 RETURN 
8000 REM "FUNCTIONS"
8005 REM "-- Special Date functions for YYYYMMDD Dates: FNU=Unpack (trims Century), FNP=Pack (add Century), FNB=Display YYYYMMDD as MM/DD/YY
8010 DEF FNA$(Q$,Q2$)=STR((ASC(Q$)-32)*POS(" "<>Q2$(2,1)):"00")
8020 DEF FNB$(Q1$)=Q1$(5,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(7,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(3,2)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(NUM(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8080 DEF FNH(Q$)=ASC(Q$)-32
8090 DEF FNV$(Q)=CHR(Q+32)
8094 DEF FNP$(Q1$)="19"+Q1$
8096 DEF FNU$(Q1$)=Q1$(5,4)+Q1$(3,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "POR - Open Purchase Requisitions Report
0020 REM "Program POR.GA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "20Nov96 Caj: Allow for entry of single character whses (ie '4 ')(2705,2710,2720,2730,5160)
0040 REM 
0070 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=14
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="APM-01",FILES$[2]="APM-04",FILES$[3]="APM-05"
0130 LET FILES$[4]="APM-10",FILES$[5]="IVM-01",FILES$[6]="IVM-05"
0135 LET FILES$[7]="IVM-10",FILES$[8]="POE-01",FILES$[9]="POE-11"
0140 LET FILES$[10]="POE-31",FILES$[11]="POM-02",FILES$[12]="SYS-01"
0145 LET FILES$[13]="ARM-01",FILES$[14]="ARM-03"
0160 CALL "SYC.DA",1,1,12,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET APM01_DEV=CHANNELS[1],APM04_DEV=CHANNELS[2],APM05_DEV=CHANNELS[3]
0180 LET APM10_DEV=CHANNELS[4],IVM01_DEV=CHANNELS[5],IVM05_DEV=CHANNELS[6]
0185 LET IVM10_DEV=CHANNELS[7],POE01_DEV=CHANNELS[8],POE11_DEV=CHANNELS[9]
0190 LET POE31_DEV=CHANNELS[10],POM02_DEV=CHANNELS[11],SYS01_DEV=CHANNELS[12]
0200 REM " --- IOLists
0240 APM01A: IOLIST X0$,X1$(1)
0280 IVM10C: IOLIST X1$(1)
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 APS01A: IOLIST U0$,VENDIMASK$,U2$,U3$,U4$,VENDOMASK$
0320 ARS01A: IOLIST U0$,CUSTIMASK$,U2$,U3$,U4$,CUSTOMASK$
0330 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0340 POS01A: IOLIST P0$,P1$,P2$,P3$,P4$
0400 REM " --- Parameters
0405 DIM INFO$[20],DESC[3],DESCLEN[3]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO",AP$="N",AR$="N",SF$="N",WHEN$=F0$(7,3)
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=POS01A
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 LET VENDLEN=NUM(U2$(1,2))
0435 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET IVPRECISION=NUM(I2$(5,1)),ITEMLEN=NUM(I2$(1,2))
0445 FOR X=1 TO 3
0450 LET DESCLEN[X]=NUM(I2$((X-1)*2+9,2))
0455 NEXT X
0460 CALL "SYC.VA","AR",INFO$[ALL]
0465 LET AR$=INFO$[20]
0500 REM " --- Init Data
0510 PRECISION IVPRECISION
0520 LET PRINTSEQ$="V",SEQDESC$="Vendor Sequence"
0530 LET WHICHDATE$="O",DATETYPE$="Order",WHSE$=""
0540 LET TYPE$="R"; REM "Requisition -- POC.LB
0550 CALL "SYC.RM",M1$,M1,9,15
0560 CALL "SYC.RM",M2$,M2,8,12
0570 CALL "SYC.RM",M3$,M3,4,12
0580 CALL "SYC.RM",M5$,M5,6,9
0590 DIM VENDFNAME$(30),VENDTNAME$(30),WHSENAME$(24)
0600 REM " --- Additional File Opens
0610 IF AR$<>"Y" THEN GOTO 0700
0620 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0630 LET CUSTLEN=NUM(U2$(1,2))
0640 CALL "SYC.DA",1,13,14,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0650 IF STATUS>0 THEN GOTO 9900
0660 LET ARM01_DEV=CHANNELS[13],ARM03_DEV=CHANNELS[14]
0700 REM " --- Background
0710 FOR X=4 TO 20 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(17,3),"Beginning Requisition:",@(20,5),"Ending Requisition:",@(24,7),"Print Sequence:",@(22,9),"Beginning Vendor:",@(25,11),"Ending Vendor:",@(28,13),"Which Date:",@(24,15),"Beginning Date:",@(27,17),"Ending Date:",@(29,19),"Warehouse:",
0760 GOSUB REFRESH
1000 REM " --- Beginning Requisition
1020 LET V0$="Z",V1$="KCE",V2$=REQFROM$,V3$="",V4$="Enter A Valid Requisition Number (<F1>=First/<F3>=Lookup)",V0=7,V1=40,V2=3,I0=0
1030 GOSUB 7000
1040 ON V3 GOTO 1100,1050,1130,1080,4000
1050 LET REQFROM$=""
1060 PRINT @(V1,V2),'CL',@(V1,V2),"First",
1070 GOTO 1130
1080 CALL "POC.LB",TYPE$,VENDREQ$
1090 IF LEN(VENDREQ$)<15 THEN GOTO 1050 ELSE LET V$=VENDREQ$(9,7)
1100 IF V$="" THEN GOTO 1050
1110 LET REQFROM$=V$
1120 PRINT @(40,3),REQFROM$,'CL',
1130 GOTO 3900
1200 REM " --- Ending Requisition
1210 LET V0$="Z",V1$="KCE",V2$=REQTHRU$,V3$="",V4$="Enter A Valid Requisition Number (<F1>=Last/<F3>=Lookup)",V0=7,V1=40,V2=5
1220 GOSUB 7000
1230 ON V3 GOTO 1290,1240,1330,1270,4000
1240 LET REQTHRU$=""
1250 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
1260 GOTO 1330
1270 CALL "POC.LB",TYPE$,VENDREQ$
1280 IF LEN(VENDREQ$)<15 THEN GOTO 1240 ELSE LET V$=VENDREQ$(9,7)
1290 IF V$="" THEN GOTO 1240
1300 IF REQFROM$<>"" THEN IF V$<REQFROM$ THEN GOTO 1200
1310 LET REQTHRU$=V$
1320 PRINT @(40,5),REQTHRU$,'CL',
1330 GOTO 3900
1400 REM " --- Print Sequence
1410 LET V0$="S",V1$="",V2$=PRINTSEQ$,V3$="AVR",V4$="A=Vendor Alternate Sequence  V=Vendor Sequence  R=Requisition Sequence",V0=1,V1=40,V2=7
1420 GOSUB 7000
1430 ON V3 GOTO 1440,1440,1460,1440,4000
1440 IF V$="" THEN GOTO 1400
1450 LET PRINTSEQ$=V$
1460 IF PRINTSEQ$="A" THEN LET SEQDESC$="Alternate Sequence"
1470 IF PRINTSEQ$="V" THEN LET SEQDESC$="Vendor Sequence"
1480 IF PRINTSEQ$="R" THEN LET SEQDESC$="Requisition Sequence"
1485 PRINT @(V1+2,V2),'CL',SEQDESC$
1490 GOTO 3900
1600 REM " --- Beginning Vendor
1610 LET V0$="M",V1$="KCE",V2$=VENDFROM$,V3$=VENDIMASK$,V4$="Enter Beginning Vendor Number (<F1>=First/<F3>=Lookup)",V0=6,V1=40,V2=9
1620 IF PRINTSEQ$="A" THEN LET V0$="S",V2$=VENDFROM$,V3$="",V4$="Enter Beginning Vendor Number (<F1>=First)",V0=10
1625 GOSUB 7000
1630 ON V3 GOTO 1690,1640,1790,1670,4000
1640 LET VENDFROM$=""
1650 PRINT @(V1,V2),'CL',@(V1,V2),"First",
1660 GOTO 1790
1670 IF PRINTSEQ$="A" THEN GOTO 1690
1675 CALL "SYC.LV",V$
1680 IF V$="" THEN GOTO 1600
1690 IF V$="" THEN GOTO 1640
1700 DIM X1$(195)
1705 IF PRINTSEQ$="A" THEN GOTO 1720
1710 FIND (APM01_DEV,KEY=N0$+V$,DOM=1600)IOL=APM01A
1720 LET VENDFROM$=V$,VENDFNAME$(1)=X1$(1,30)
1730 IF PRINTSEQ$="A" THEN PRINT @(V1,V2),VENDFROM$,'CL',
1740 IF PRINTSEQ$<>"A" THEN PRINT @(V1,V2),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
1790 GOTO 3900
1800 REM " --- Ending Vendor
1810 LET V0$="M",V1$="KCE",V2$=VENDTHRU$,V3$=VENDIMASK$,V4$="Enter Ending Vendor Number (<F1>=Last/<F3>=Lookup)",V0=6,V1=40,V2=11
1820 IF PRINTSEQ$="A" THEN LET V0$="S",V2$=VENDTHRU$,V3$="",V4$="Enter Ending Alternate Sequence (<F1>=Last)",V0=10
1825 GOSUB 7000
1830 ON V3 GOTO 1890,1840,1990,1870,4000
1840 LET VENDTHRU$=""
1850 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
1860 GOTO 1990
1870 IF PRINTSEQ$="A" THEN GOTO 1890
1875 CALL "SYC.LV",V$
1880 IF V$="" THEN GOTO 1800
1890 IF V$="" THEN GOTO 1840
1900 IF VENDFROM$<>"" THEN IF V$<VENDFROM$ THEN GOTO 1800
1910 DIM X1$(195)
1915 IF PRINTSEQ$="A" THEN GOTO 1930
1920 FIND (APM01_DEV,KEY=N0$+V$,DOM=1800)IOL=APM01A
1930 LET VENDTHRU$=V$,VENDTNAME$(1)=X1$(1,30)
1940 IF PRINTSEQ$="A" THEN PRINT @(V1,V2),VENDTHRU$,'CL',
1950 IF PRINTSEQ$<>"A" THEN PRINT @(V1,V2),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
1990 GOTO 3900
2000 REM " --- Which Date?
2010 LET V0$="S",V1$="C",V2$=WHICHDATE$,V3$="OPRD",V4$="O=Date Ordered  P=Promised By Date  R=Date Required  D=Don't Deliver Before",V0=1,V1=40,V2=13
2020 GOSUB 7000
2030 ON V3 GOTO 2040,2040,2180,2040,4000
2040 IF V$="" THEN GOTO 2000
2050 LET WHICHDATE$=V$
2060 ON POS(WHICHDATE$="OPRD") GOTO 2000,2080,2100,2120,2140
2080 LET DATETYPE$="Date Ordered"
2090 GOTO 2180
2100 LET DATETYPE$="Date Promised"
2110 GOTO 2180
2120 LET DATETYPE$="Date Required"
2130 GOTO 2180
2140 LET DATETYPE$="Not Before Date"
2150 GOTO 2180
2180 PRINT @(V1+2,V2),'CL',DATETYPE$
2190 GOTO 3900
2200 REM " --- Beginning Date
2210 LET V0$="D",V1$="6N",V2$=DATEFROM$,V3$="",V4$="",V0=6,V1=40,V2=15
2220 GOSUB 7000
2230 ON V3 GOTO 2250,2240,2270,2200,4000
2240 LET V$=""
2250 IF V$="" THEN PRINT @(V1,V2),"First   ",
2260 LET DATEFROM$=V$
2270 GOTO 3900
2400 REM " --- Ending Date
2410 LET V0$="D",V1$="6N",V2$=DATETHRU$,V3$="",V4$="",V0=6,V1=40,V2=17
2420 GOSUB 7000
2430 ON V3 GOTO 2450,2440,2480,2450,4000
2440 LET V$=""
2450 IF V$="" THEN PRINT @(V1,V2),"Last    ",
2460 IF V$<>"" THEN IF V$<DATEFROM$ THEN GOTO 2400
2470 LET DATETHRU$=V$
2480 GOTO 3900
2600 REM " --- Warehouse
2610 LET V0$="S",V1$="",V2$=WHSE$,V3$="",V4$="Enter A Valid Warehouse ID  (<F1>=All/<F3>=Lookup)",V0=2,V1=40,V2=19
2620 GOSUB 7000
2630 ON V3 GOTO 2690,2640,2790,2670,4000
2640 LET WHSE$=""
2650 PRINT @(V1,V2),'CL',@(V1,V2),"All Warehouses",
2660 GOTO 2790
2670 CALL "IVC.LK",3,V1,V2,V$
2680 IF V$="" THEN GOTO 2600
2690 IF V$="" THEN GOTO 2640
2700 DIM X1$(160)
2705 LET X1$(1)=N0$+"C"+V$
2710 FIND (IVM10_DEV,KEY=X1$(1,5),DOM=2600)IOL=IVM10C
2720 LET WHSE$=X1$(4,2),WHSENAME$(1)=X1$(6,24)
2730 PRINT @(V1,V2),WHSE$," ",WHSENAME$(1,24),'CL',
2790 GOTO 3900
3900 REM " --- Next Field
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,2200,2400,2600,4000
4000 REM " --- Correct
4010 GOSUB REFRESH
4020 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run print overlay
4130 IF VENDFROM$="" THEN LET VENDFNAME$=FILL(30)
4140 IF VENDTHRU$="" THEN LET VENDTNAME$=FILL(30)
4150 IF WHSE$="" THEN LET WHSE$="ALL"
4190 RUN "POR.GB"
5000 REM " --- Refresh screen data
5005 REFRESH: 
5010 PRINT 'CF',
5020 IF REQFROM$="" THEN PRINT @(40,3),"First" ELSE PRINT @(40,3),REQFROM$,'CL',
5030 IF REQTHRU$="" THEN PRINT @(40,5),"Last" ELSE PRINT @(40,5),REQTHRU$,'CL',
5040 PRINT @(40,7),PRINTSEQ$," ",SEQDESC$,'CL',
5050 IF PRINTSEQ$="A" THEN GOTO 5090
5060 IF VENDFROM$="" THEN PRINT @(40,9),"First",'CL', ELSE PRINT @(40,9),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5070 IF VENDTHRU$="" THEN PRINT @(40,11),"Last",'CL', ELSE PRINT @(40,11),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5080 GOTO 5110
5090 IF VENDFROM$="" THEN PRINT @(40,9),"First",'CL', ELSE PRINT @(40,9),VENDFROM$,'CL',
5100 IF VENDTHRU$="" THEN PRINT @(40,11),"Last",'CL', ELSE PRINT @(40,11),VENDTHRU$,'CL',
5110 PRINT @(40,13),WHICHDATE$," ",DATETYPE$,'CL',
5130 IF DATEFROM$="" THEN PRINT @(40,15),"First   ", ELSE PRINT @(40,15),FNB6$(DATEFROM$)
5140 IF DATETHRU$="" THEN PRINT @(40,17),"Last    ", ELSE PRINT @(40,17),FNB6$(DATETHRU$)
5150 IF WHSE$="" THEN PRINT @(40,19),"All Warehouses",'CL'
5160 IF WHSE$<>"" THEN PRINT @(40,19),WHSE$," ",WHSENAME$(1,24),'CL',
5190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "SYC - Komments Maintenance"
0020 REM "Program SYC.KA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "02Dec96 JWK: lost detail line entry if <F4> used at end of line (4030,4185)
0032 REM "14Jun00 KJS: Can't display more than one page of help text (d1220)
0040 REM 
0042 REM "OPTION   = Process Control (0=Maintenance/1=Inquiry)"
0043 REM "            0: Comment Maintenance"
0044 REM "            1: Comment Inquiry"
0045 REM "           10: Help Maintenance"
0046 REM "           11: Help Inquiry"
0054 REM "INFO$[0] = Comments File Name
0056 REM "     [1] = Primary Key Segment"
0058 REM "     [2] = Footer (ACTION=0)/Title (ACTION=1)"
0060 REM "INFO(0)  = Window Column (X)"
0062 REM "    (1)  = Window Row (Y)"
0064 REM "    (2)  = Number of Comment Lines"
0066 REM "    (3)  = Sequence Number Length"
0068 REM "    (4)  = Comment Length"
0070 REM "    (5)  = Sequence Number Increment"
0075 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER OPTION,INFO$[ALL],INFO[ALL]
0100 REM " --- Open Files"
0110 LET FILES=1
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]=INFO$[0]
0140 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET CHANNEL=CHANNELS[1]
0200 REM " --- IOLIST's"
0210 TEXT: IOLIST A0$(1),A1$(1)
0400 REM " --- Parameters"
0403 LET WIN_X=INFO[0],WIN_Y=INFO[1],LINES=INFO[2],ACTION=OPTION,HELP=0
0405 IF OPTION=10 THEN LET ACTION=0,HELP=1
0407 IF OPTION=11 THEN LET ACTION=1,HELP=1
0410 LET SEQLEN=INFO[3],COMLEN=INFO[4],NUMBER=INFO[5]
0415 IF SEQLEN=0 THEN LET SEQLEN=2
0420 IF COMLEN=0 THEN LET COMLEN=48
0425 IF NUMBER=0 THEN LET NUMBER=1
0430 IF WIN_Y=0 OR LINES<>0 THEN GOTO 0445
0435 LET LINES=24-WIN_Y-5
0440 IF ACTION>0 AND OPTION<>11 THEN LET LINES=24-WIN_Y-1
0445 IF LINES=0 THEN LET LINES=10
0450 IF ACTION=0 THEN LET HEIGHT=LINES+5,WIDTH=MAX(SEQLEN+COMLEN+7,70)
0455 IF ACTION>0 THEN LET HEIGHT=LINES+2,WIDTH=SEQLEN+COMLEN+6
0457 IF OPTION=11 THEN LET HEIGHT=LINES+5,WIDTH=COMLEN+4
0460 IF WIDTH>80 THEN LET COMLEN=COMLEN-(WIDTH-80),WIDTH=80
0463 IF WIN_Y=0 THEN LET WIN_Y=24-HEIGHT
0465 IF WIN_X=0 THEN LET WIN_X=INT((80-WIDTH)/2)
0470 IF WIN_Y+HEIGHT>23 THEN LET WIN_Y=24-HEIGHT
0475 IF WIN_X+WIDTH>80 THEN LET WIN_X=80-WIDTH
0480 LET KEYVALUE$=INFO$[1],KEYLEN=LEN(KEYVALUE$),P=3+SEQLEN
0485 IF SEQLEN<3 THEN LET P=4+SEQLEN
0490 LET PAGES=INT(10^SEQLEN/LINES-1)+2,MAXROW=HEIGHT-5
0495 IF ACTION>0 THEN IF HELP=0 THEN LET MAXROW=HEIGHT
0500 REM " --- Initializations"
0510 DIM A0$(KEYLEN+SEQLEN),A1$(COMLEN)
0520 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,1]
0530 DIM HEADING$(WIDTH-2),FOOTING1$(WIDTH-2),FOOTING2$(WIDTH-2)
0540 LET A0$(1)=KEYVALUE$,TITLE$="Comment Maintenance"
0560 LET L0=2,L9=MAXROW,MAXROW=MAXROW-1,O=L0-1,CR$="<Enter>=Continue"
0570 IF HELP=0 THEN GOTO 0600
0580 LET TITLE$="ADD+ON Software On-Line Help"
0585 LET FOOTING1$(WIDTH-24)="|PgUp PgDn F4=End"
0590 IF ACTION=0 AND OPTION<>11 THEN LET TITLE$=TITLE$+" Maintenance"
0600 REM 
0605 IF HELP THEN GOTO 0650
0610 IF ACTION=0 THEN GOTO 0650
0620 LET TITLE$=INFO$[2],L0=0,L9=HEIGHT-4
0630 IF FNP$(TITLE$)="" THEN LET TITLE$="Comments"
0640 GOTO 0800
0650 REM " --- Header/Footers"
0660 LET HEADING$(2)="Seq",HEADING$(P)="Comment"
0665 IF HELP THEN LET HEADING$(P)="Help Text"
0670 IF OPTION=11 THEN LET HEADING$(2)=" "+INFO$[2]
0675 IF OPTION<>11 THEN LET FOOTING1$(2)=INFO$[2],FOOTING2$(2)=INFO$[2]
0680 LET FOOTING1$(WIDTH-31)="|PgUp PgDn F1=New F4=End"
0685 IF OPTION=11 THEN LET FOOTING1$(WIDTH-31)="        PgUp PgDn F4=End"
0690 LET FOOTING2$(WIDTH-28)="|D=Delete F2=Change F4=End"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING1$,'ER','SF',
0900 REM " --- Position file"
0910 LET PAGE=1,L=L0,X1=1,X0=0,SELECTION$=""
0990 READ (CHANNEL,KEY=A0$,DOM=1000)
1000 REM " --- Get next key"
1010 LET K$=KEY(CHANNEL,END=2000)
1020 IF POS(KEYVALUE$=K$)<>1 THEN GOTO 2000
1100 REM " --- Read record"
1110 LET LOCKED=0
1120 READ (CHANNEL,KEY=K$,ERR=1150)IOL=TEXT
1140 GOTO 1200
1150 REM " --- Error reading record"
1160 IF ERR=11 THEN GOTO 1900
1170 IF ERR<>0 THEN GOTO 9000
1180 LET LOCKED=1
1200 REM " --- Page full?"
1210 IF L<=L9 THEN GOTO 1300
1230 LET PAGE=PAGE+1,X1=1,L=L0
1300 REM " --- Store record"
1310 DIM G1$(WIDTH-2)
1320 IF LOCKED=1 THEN LET A1$(1)=" Record In Use"
1330 LET SEQUENCE$=A0$(KEYLEN+1),G1$(2)=SEQUENCE$,G1$(P)=A1$
1340 IF OPTION=11 THEN LET G1$(2)=A1$
1380 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=STR(L:"00")+SEQUENCE$
1390 LET X0=X0+1,X1=X1+1,L=L+1
1900 REM " --- Loop back for next record"
1910 IF LOCKED=0 THEN GOTO 1000
1920 LET LOCKED=0
1940 LET K$=K$(1,LEN(K$)-1)+CHR(ASC(K$(LEN(K$),1))+1)
1950 READ (CHANNEL,KEY=K$,DOM=1000,END=2000)
1990 GOTO 1100
2000 REM " --- End of file"
2010 LET LASTPAGE=PAGE,PAGE=1,L=L0
2020 IF ACTION>0 THEN IF HELP=0 THEN GOTO 2100
2030 IF X0=0 THEN LET L=L-1; GOTO 3000
2090 GOTO 2500
2100 REM " --- Display only"
2110 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
2120 IF X0=0 THEN GOTO 2200
2130 LET SELECTION$=""
2140 GOSUB 5100
2150 LET V0$="S",V1$="",V2$="",V3$="",V4$=""
2160 LET V0=1,V1=FNC(CR$,WIDTH)+LEN(CR$)+2,V2=L9+1
2170 PRINT @(FNC(CR$,WIDTH),V2),CR$,
2180 GOSUB 7000
2190 GOTO 9900
2200 REM " --- Nothing Found"
2220 DIM MESSAGE$[1]
2240 LET MESSAGE$[0]="No Comments Found ("+CR$+")"
2250 IF HELP THEN LET MESSAGE$="No Help Available ("+CR$+")"
2260 LET V2=WIN_Y+INT(HEIGHT/2)-1,V1=-1
2280 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
2290 GOTO 9900
2500 REM " --- Display pages"
2510 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING1$,'ER','SF',
2520 GOSUB 6800
2530 IF STATUS>0 THEN GOTO 9900
2560 GOSUB 5000
2570 IF FKEY=4 THEN GOTO 9900
2580 IF SELECTION$="" THEN GOTO 9900
2590 IF ACTION>0 THEN GOTO 2500
2595 IF FKEY=1 THEN GOTO 2700
2600 REM 
2610 LET A0$(KEYLEN+1)=SELECTION$(3),A1$(1)=""
2620 FIND (CHANNEL,KEY=A0$,DOM=2500)IOL=TEXT
2650 LET L=NUM(SELECTION$(1,2))
2660 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING2$,'ER','SF',
2690 GOTO 3700
2700 REM " --- Advance to last page and switch modes"
2710 LET PAGE=LASTPAGE,SELECTION$=""
2720 GOSUB 5100
2730 LET SELECTION$=LAST$,L=NUM(SELECTION$(1,2))+1
2740 LET A0$(KEYLEN+1)=SELECTION$(3)
2760 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING2$,'ER','SF',
2780 IF L>L9 THEN GOTO 3030
2790 GOTO 3300
3000 REM " --- Overflow"
3010 LET L=L+1
3020 IF L<=L9 THEN GOTO 3100
3030 LET PAGE=PAGE+1,SELECTION$=""
3040 IF PAGE>LASTPAGE THEN LET LASTPAGE=PAGE
3080 GOSUB 5100
3090 LET L=NUM(SELECTION$(1,2))
3100 REM " --- Next key"
3120 LET K$=KEY(CHANNEL,END=3300)
3140 IF POS(KEYVALUE$=K$)<>1 THEN GOTO 3300
3150 READ (CHANNEL)IOL=TEXT
3190 GOTO 3490
3300 REM " --- Next sequence number"
3310 LET X=NUM(A0$(KEYLEN+1))+NUMBER
3400 REM " --- Sequence Number"
3420 LET V0$="Z",V1$="KR",V2$=STR(X),V3$="",V4$=""
3425 LET V0=SEQLEN,V1=1,V2=L
3430 GOSUB 7000
3440 IF V3=4 THEN GOTO 2500
3450 IF V$="000" OR V$="" THEN GOTO 3400
3460 LET A0$(KEYLEN+1)=V$
3480 READ (CHANNEL,KEY=A0$,DOM=3600)IOL=TEXT
3490 IF A0$<=K9$ THEN GOSUB 5500
3500 REM 
3510 PRINT @(1,L),'CL',A0$(KEYLEN+1),@(P-1),A1$,
3520 LET SELECTION$=STR(L:"00")+A0$(KEYLEN+1)
3590 GOTO 4000
3600 REM " --- Initializations for new record"
3610 LET A1$(1)="",SELECTION$=STR(L:"00")+A0$(KEYLEN+1)
3700 REM " --- Comment maintenance"
3710 LET V0$="S",V1$="",V2$=A1$,V3$="",V4$=""
3720 LET V0=COMLEN,V1=P-1,V2=L
3730 GOSUB 7000
3740 ON V3 GOTO 3750,3700,3400,3700,4000
3750 LET A1$(1)=V$
4000 REM " --- Correct?"
4010 LET V0$="S",V1$="^",V2$="",V3$="",V4$=""
4015 LET V0=1,V1=WIDTH-4,V2=L,K9$=A0$
4020 GOSUB 7000
4030 ON V3 GOTO 4040,9200,3700,4000,4100
4040 IF V$="D" THEN GOTO 4200
4050 IF V$="" THEN GOTO 4100
4090 GOTO 4000
4100 REM " --- Write"
4110 DIM G1$(WIDTH-2)
4120 LET SEQUENCE$=A0$(KEYLEN+1),G1$(2)=SEQUENCE$,G1$(P)=A1$
4130 FIND (CHANNEL,KEY=A0$,DOM=4140)
4135 GOTO 4150
4140 GOSUB 6000
4150 LET N=L-O,LLIST$[PAGE,N,1]=G1$,SELECTION$=""
4155 LET LLIST$[PAGE,N,2]=STR(N+O:"00")+A0$(KEYLEN+1)
4160 IF INSERTED THEN GOSUB 5100
4170 WRITE (CHANNEL,KEY=A0$)IOL=TEXT
4180 LET SELECTION$=LLIST$[PAGE,N,2],L=NUM(SELECTION$(1,2)),INSERTED=0
4185 IF V3=4 THEN GOTO 2500
4190 GOTO 3000
4200 REM " --- Delete"
4210 REMOVE (CHANNEL,KEY=A0$,DOM=4230)
4220 GOSUB 6500
4230 GOSUB 5100
4240 LET L=NUM(SELECTION$(1,2)),X1=L-O,A0$(KEYLEN+1)=SELECTION$(3)
4290 GOTO 3100
5000 REM " --- Perform lightbar processing"
5010 LET XMODE=5
5020 IF OPTION=11 THEN LET XMODE=25
5030 IF PAGE=LASTPAGE THEN LET SELECTION$=SELECTION$+"|EOF"
5040 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,L9,NAME$,HEIGHT,WIDTH,FKEY
5060 IF FKEY<>-16 THEN GOTO 5090
5070 IF PAGE<LASTPAGE THEN LET PAGE=PAGE+1
5080 GOTO 5000
5090 RETURN
5100 REM " --- Display page"
5110 LET X1=1,LAST$=""
5120 FOR X=L0 TO L9
5130 PRINT @(0,X),'CL',LLIST$[PAGE,X1,1],
5135 IF LLIST$[PAGE,X1,1]<>"" THEN LET LAST$=LLIST$[PAGE,X1,2]
5140 LET X1=X1+1
5150 NEXT X
5160 IF SELECTION$<>"" THEN GOTO 5190
5170 LET L=L0,SELECTION$=STR(L:"00")
5180 IF LEN(LLIST$[PAGE,1,2])>2 THEN LET SELECTION$=SELECTION$+LLIST$[PAGE,1,2](3)
5190 RETURN
5500 REM " --- Determine which page to display"
5505 LET IP=0,IL=0
5510 FOR X=1 TO LASTPAGE
5520 FOR Y=1 TO MAXROW
5530 IF FNP$(LLIST$[X,Y,2])="" THEN GOTO 5560
5535 IF A0$(KEYLEN+1)<>LLIST$[X,Y,2](3) THEN GOTO 5560
5540 LET IP=X,SELECTION$=LLIST$[X,Y,2]
5545 EXITTO 5565
5560 NEXT Y
5565 IF IP>0 THEN EXITTO 5580
5570 NEXT X
5580 LET PAGE=IP
5585 GOSUB 5100
5590 RETURN
6000 REM " --- Does the new record require insertion?"
6010 LET INSERTED=0,IP=0,IL=0
6020 FOR X=1 TO LASTPAGE
6030 FOR Y=1 TO MAXROW
6040 IF FNP$(LLIST$[X,Y,2])="" THEN GOTO 6070
6050 IF LLIST$[X,Y,2](3)<SEQUENCE$ THEN GOTO 6070
6060 LET IP=X,IL=Y
6065 EXITTO 6075
6070 NEXT Y
6075 IF IP>0 THEN EXITTO 6100
6080 NEXT X
6090 GOTO 6290
6100 REM " --- Insert new line into LLIST$"
6110 IF FNP$(LLIST$[LASTPAGE,MAXROW,2])<>"" THEN LET LASTPAGE=LASTPAGE+1
6120 FOR X=LASTPAGE TO IP STEP -1
6130 LET FIRST=1
6140 IF X=IP THEN LET FIRST=IL
6150 FOR Y=MAXROW TO FIRST STEP -1
6160 LET M=X,N=Y+1
6170 IF Y=MAXROW THEN LET M=X+1,N=1
6180 FOR Z=0 TO 5
6190 IF FNP$(LLIST$[X,Y,Z])<>"" THEN LET LLIST$[M,N,Z]=LLIST$[X,Y,Z]
6200 NEXT Z
6210 IF FNP$(LLIST$[M,N,1])<>"" THEN LET LLIST$[M,N,2]=STR(N+O:"00")+LLIST$[M,N,2](3)
6220 NEXT Y
6230 NEXT X
6240 LET PAGE=IP,L=IL+O,INSERTED=1
6290 RETURN
6500 REM " --- Roll back LLIST$ after delete"
6510 FOR X=PAGE TO LASTPAGE
6520 LET FIRST=1
6530 IF X=PAGE THEN LET FIRST=NUM(SELECTION$(1,2))-O,SELECTION$=""
6540 FOR Y=FIRST TO MAXROW
6550 LET M=X,N=Y+1
6560 IF Y=MAXROW THEN LET M=X+1,N=1
6570 FOR Z=0 TO 5
6580 IF FNP$(LLIST$[M,N,Z])<>"" THEN LET LLIST$[X,Y,Z]=LLIST$[M,N,Z]
6590 NEXT Z
6600 IF FNP$(LLIST$[X,Y,1])<>"" THEN LET LLIST$[X,Y,2]=STR(Y+O:"00")+LLIST$[X,Y,2](3)
6610 IF SELECTION$="" THEN LET SELECTION$=LLIST$[X,Y,2]
6620 NEXT Y
6630 NEXT X
6640 LET X1=1
6650 FOR X=1 TO MAXROW
6660 IF FNP$(LLIST$[LASTPAGE,X,1])<>"" THEN LET X1=X
6670 NEXT X
6680 FOR Z=0 TO 5
6690 LET LLIST$[LASTPAGE,X1,Z]=""
6700 NEXT Z
6710 IF X1=1 THEN LET LASTPAGE=MAX(LASTPAGE-1,1)
6720 IF PAGE>LASTPAGE THEN LET PAGE=LASTPAGE
6790 RETURN
6800 REM " --- Anything in LLIST$?"
6810 LET STATUS=0
6820 FOR X=1 TO MAXROW
6830 IF FNP$(LLIST$[PAGE,X,1])<>"" THEN EXITTO 6890
6840 NEXT X
6850 LET STATUS=1
6860 IF PAGE<2 THEN GOTO 6890
6870 LET PAGE=PAGE-1
6880 GOTO 6800
6890 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 REM " --- Return to caller"
9910 CLOSE (CHANNEL,ERR=9911)
9940 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9945 PRINT 'CURSOR'("ON"),
9950 EXIT
9999 END
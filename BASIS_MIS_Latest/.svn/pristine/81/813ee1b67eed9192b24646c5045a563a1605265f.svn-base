0010 REM "IVR - Inventory Requirements Report (Input Overlay)"
0020 REM "Program IVR.MA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=11
0110 DIM FILES$[FILES],FILEOPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0130 LET FILES$[4]="IVM-03",FILES$[5]="IVM-10",FILES$[6]="IVM-12"
0140 LET FILES$[7]="IVM-14",FILES$[8]="IVM-03",FILES$[9]="IVM-15"
0145 LET FILES$[10]="IVM-05",FILES$[11]="APM-01"
0150 CALL "SYC.DA",1,1,10,FILES$[ALL],FILEOPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0175 LET IVM03_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],IVM12_DEV=CHANNELS[6]
0180 LET IVM14_DEV=CHANNELS[7],IVM03_DEV2=CHANNELS[8],IVM15_DEV=CHANNELS[9]
0190 LET IVM05_DEV=CHANNELS[10]
0200 REM " --- IOLIST's"
0210 IVM10F: IOLIST X0$(1)
0220 APM01A: IOLIST D0$(1),D1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0275 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$
0280 GLS01A: IOLIST G0$,G1$,G2$,G3$(1),G4$
0400 REM " --- Parameters"
0405 DIM P[5],G[1],I[1],G3$(325),INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="IV"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1)),P[2]=NUM(I2$(3,2))
0460 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0470 CALL "SYC.VA","AP",INFO$[ALL]
0475 LET AP$=INFO$[20],G[0]=NUM(G2$(1,2))
0480 IF AP$="N" THEN GOTO 0500
0485 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0490 LET I[0]=NUM(P2$(1,2))
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM FILES[15],PARAMS$[3],OPTIONS$[7],X0$(32),D0$(8),D1$(30)
0550 LET FILES[1]=IVM01_DEV,FILES[10]=IVM10_DEV,FILES[14]=IVM14_DEV
0555 LET PARAMS$[0]=N0$,PARAMS$[1]=I1$,PARAMS$[2]=I2$,PARAMS$[3]=I3$
0570 LET SEQUENCE$="B",BEGBUYER$="",ENDBUYER$="",BEGVENDOR$=""
0580 LET ENDVENDOR$="",FILES[15]=IVM15_DEV
0600 REM " --- Additional file opens"
0610 IF AP$="N" THEN GOTO 0700
0620 CALL "SYC.DA",1,11,11,FILES$[ALL],FILEOPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS>0 THEN GOTO 9900
0640 LET APM01_DEV=CHANNELS[11]
0700 REM " --- Background"
0710 PRINT 'SB',@(30,3),"Sequence:",@(23,4),"Beginning Buyer:",@(26,5),"Ending Buyer:",@(22,6),"Beginning Vendor:",@(25,7),"Ending Vendor:",'SF',
0750 CALL "IVC.IB","B","D",8,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0900 REM 
0990 GOSUB 5000
1000 REM " --- Sequence"
1010 LET V0$="S",V1$="C",V2$=SEQUENCE$,V3$="BV",V0=1,V1=40,V2=3,I0=0
1020 LET V4$="List Items In Buyer Or Vendor Sequence (B=Buyer/V=Vendor)"
1025 IF AP$="N" THEN LET V$="B",V3=0; GOTO 1040
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,3900,1000,3900
1050 IF POS(V$="BV")=0 THEN GOTO 1000
1060 LET SEQUENCE$=V$,X$="(By Buyer Code)"
1070 IF V$="V" THEN LET X$="(By Vendor)"
1080 PRINT @(V1,V2),SEQUENCE$," ",X$,'CL',
1090 GOTO 3900
1200 REM " --- Beginning Buyer"
1210 LET V0$="S",V1$="C",V2$=BEGBUYER$,V3$="",V0=3,V1=40,V2=4,X$="First"
1220 LET V4$="Enter The First Buyer Code To Include On This Report (<F1>=First/<F3>=Lookup)"
1230 GOSUB 7000
1240 ON V3 GOTO 1300,1300,3900,1250,3900
1250 CALL "IVC.LK",4,V1,V2,V$
1300 REM 
1310 GOSUB 5200
1320 IF STATUS>0 THEN GOTO 1200
1330 PRINT @(V1,V2),X$,'CL',
1340 IF V$="" THEN LET BEGBUYER$="" ELSE LET BEGBUYER$=X0$(3,3)
1390 GOTO 3900
1400 REM " --- Ending Buyer"
1410 LET V0$="S",V1$="C",V2$=ENDBUYER$,V3$="",V0=3,V1=40,V2=5,X$="Last"
1420 LET V4$="Enter The Last Buyer Code To Include On This Report (<F1>=Last/<F3>=Lookup)"
1430 GOSUB 7000
1440 ON V3 GOTO 1500,1500,3900,1450,3900
1450 CALL "IVC.LK",4,V1,V2,V$
1500 REM 
1510 GOSUB 5200
1520 IF STATUS>0 THEN GOTO 1400
1530 PRINT @(V1,V2),X$,'CL',
1540 IF V$="" THEN LET ENDBUYER$="" ELSE LET ENDBUYER$=X0$(3,3)
1590 GOTO 3900
1600 REM " --- Beginning Vendor"
1610 LET V0$="M",V1$="CK",V2$=BEGVENDOR$,V3$=P1$,V0=6,V1=40,V2=6,X$="First",V$=""
1620 LET V4$="Enter The First Vendor To Include On This Report (<F1>=First/<F3>=Lookup)"
1625 IF AP$="N" THEN GOTO 1700
1630 GOSUB 7000
1640 ON V3 GOTO 1700,1700,3900,1650,3900
1650 CALL "SYC.LV",V$
1700 REM 
1710 GOSUB 5400
1720 IF STATUS>0 THEN GOTO 1600
1730 PRINT @(V1,V2),X$,'CL',
1740 IF V$="" THEN LET BEGVENDOR$="" ELSE LET BEGVENDOR$=D0$(3)
1790 GOTO 3900
1800 REM " --- Ending Vendor"
1810 LET V0$="M",V1$="C",V2$=ENDVENDOR$,V3$=P1$,V0=6,V1=40,V2=7,X$="Last",V$=""
1820 LET V4$="Enter The Last Vendor To Include On This Report (<F1>=Last/<F3>=Lookup)"
1825 IF AP$="N" THEN GOTO 1900
1830 GOSUB 7000
1840 ON V3 GOTO 1900,1900,3900,1850,3900
1850 CALL "SYC.LV",V$
1900 REM 
1910 GOSUB 5400
1920 IF STATUS>0 THEN GOTO 1800
1930 PRINT @(V1,V2),X$,'CL',
1940 IF V$="" THEN LET ENDVENDOR$="" ELSE LET ENDVENDOR$=D0$(3)
1990 GOTO 3900
2000 REM " --- Process standard input"
2010 CALL "IVC.IB","I","D",8,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
2020 ON V3 GOTO 2030,2000,3900,2000,3900
2090 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4030 ON POS(V$="YN") GOTO 4000,4050,1000
4090 RUN "IVR.MB"
5000 REM " --- Display"
5010 LET V0$="(By Buyer Code)"
5015 IF SEQUENCE$="V" THEN LET V0$="(By Vendor)"
5020 LET X$="First",V$=BEGBUYER$
5025 GOSUB 5200
5030 LET V1$=X$,X$="Last",V$=ENDBUYER$
5035 GOSUB 5200
5040 LET V2$=X$,X$="First",V$=BEGVENDOR$
5045 GOSUB 5400
5050 LET V3$=X$,X$="Last",V$=ENDVENDOR$
5055 GOSUB 5400
5060 PRINT @(40,3),SEQUENCE$," ",V0$,'CL',@(40,4),V1$,'CL',@(40,5),V2$,'CL',@(40,6),V3$,'CL',@(40,7),X$,'CL',
5080 CALL "IVC.IB","D","D",8,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
5090 RETURN 
5200 REM " --- Retrieve buyer"
5210 LET X0$(1)=N0$+"F"+V$,STATUS=0
5220 IF V$="" THEN GOTO 5290
5230 FIND (IVM10_DEV,KEY=X0$(1,6),DOM=5280)IOL=IVM10F
5240 LET X$=X0$(4,3)+" "+X0$(7,20)
5250 GOTO 5290
5280 LET STATUS=1
5290 RETURN 
5400 REM " --- Retrieve vendor"
5410 LET D0$(1)=N0$+V$,D1$(1)="",STATUS=0
5420 IF V$="" THEN GOTO 5490
5430 FIND (APM01_DEV,KEY=D0$,DOM=5480)IOL=APM01A
5440 LET X$=FNF$(D0$(3,I[0]),M0$)+" "+D1$
5450 GOTO 5490
5480 LET STATUS=1
5490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

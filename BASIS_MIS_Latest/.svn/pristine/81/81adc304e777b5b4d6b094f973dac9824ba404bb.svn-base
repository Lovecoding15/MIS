0010 REM "OPE - Order Entry/Credit Check"
0020 REM "Program OPE.DE"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0210 ARE03A: IOLIST A0$(1),A[ALL]
0220 ARM02A: IOLIST C0$,C1$,C[ALL]
0230 ARM10A: IOLIST X0$(1)
0240 ARM14C: IOLIST ARM14_K$
0250 ARM14D: IOLIST ARM14_K$
0500 REM " --- Initialize Data"
0510 DIM C[10]
0520 LET C$="",O3$="",STATUS$=""
0700 REM " --- Background"
0720 PRINT @(0,9),'CE'
1000 REM " --- Display Left Side"
1050 CALL "OPC.CA",ARE03_DEV,ARM02_DEV,N0$,A0$(5,6),C9$,S$,M1$,0
1100 IF A0$(68,1)<>"Y" THEN GOTO 1200
1120 LET STATUS$="This Order Has Been Printed!"
1150 GOTO 4000
1200 REM " --- Check For Quotes"
1220 IF A0$(21,1)<>"P" THEN GOTO 1300
1240 LET STATUS$="This is a Quote."
1250 LET A0$(101,1)=""
1290 GOTO 4000
1300 REM " --- Check Terms"
1310 DIM X0$(30)
1320 FIND (ARM10_DEV,KEY=N0$+"A"+A0$(62,2),DOM=1330)IOL=ARM10A
1330 IF X0$(27,1)="Y" THEN LET S$(1,1)="0"
1400 IF S$(1,1)="1" THEN LET A0$(101,1)="",STATUS$="Credit is OK!"; GOTO 4000
1410 IF A0$(101,1)="R" THEN LET STATUS$="Order is Released. "; GOTO 4000
1420 IF A[6]<=0 THEN GOTO 4000
1430 PRINT @(63,0),"***Credit Hold***"
2000 REM " --- Credit Hold?"
2010 CALL "SYC.WC",0,0,29,8,48,11,1
2020 PRINT @(56,11),"Credit Action",'SB',@(50,12),"1. Hold This Order",@(50,13),"2. Hold All Future Orders",@(50,14),"3. Release This Order",@(50,15),"4. Delete This Order",@(50,17),"Enter Your Option:",'SF',
2040 LET V0$="S",V1$="KE",V2$="",V3$="",V0=1,V1=69,V2=17
2045 GOSUB 7000
2060 IF V$="" THEN LET V$="1"
2080 IF V$<"1" OR V$>"4" THEN GOTO 2040
2100 ON NUM(V$)-1 GOTO 2200,2500,2800,2600,2800
2200 REM " --- Hold This Order Only"
2210 LET A0$(101,1)="C"
2220 LET STATUS$="Order is Held"
2290 GOTO 4000
2500 REM " --- Hold The Customer"
2510 GOSUB 5200; IF BAD_PASSWORD THEN GOTO 2000
2520 EXTRACT (ARM02_DEV,KEY=N0$+A0$(5,6)+"  ")IOL=ARM02A
2540 LET C1$(39,1)="Y"
2560 WRITE (ARM02_DEV,KEY=C0$)IOL=ARM02A
2580 LET A0$(101,1)="C",STATUS$="Customer's Orders are Held"
2590 GOTO 4000
2600 REM " --- Delete This Order"
2620 LET V4$="Confirmation Required - Delete This Order"
2622 CALL "SYC.YN",0,V4$,0,V$,V3
2625 IF V$<>"YES" THEN GOTO 2000
2660 GOTO 4700
2800 REM " --- Release This Order"
2810 GOSUB 5200; IF BAD_PASSWORD THEN GOTO 2000
2820 DIM X0$(30); LET X0$(6)="***Not On File***"
2830 FIND (ARM10_DEV,KEY=N0$+"A"+A0$(62,2),DOM=2840)IOL=ARM10A
2840 PRINT @(0,23),"Current Terms Are: ",A0$(62,2)," ",X0$(6,20),
2850 LET V0$="S",V1$="KC",V2$=A0$(62,2),V3$="",V4$="Enter New Terms (<Enter>=No Change/<F3>=Lookup)",V0=2,V1=FNV(V4$),V2=22
2860 GOSUB 7000
2870 IF V3=3 THEN GOSUB 5500
2900 LET V$=V$+"  ",V$=V$(1,2)
2910 FIND (ARM10_DEV,KEY=N0$+"A"+V$,DOM=2820)IOL=ARM10A
2940 LET A0$(101,1)="R",STATUS$="Order Released"
2950 PRINT @(0,23),'CL',
2960 GOTO 4000
4000 REM " --- Process it here"
4020 PRINT @(48,20),STATUS$,
4030 LET V4$="(<Enter>=Continue/<F2>=Line Detail"
4035 IF C9$(18,1)<>"N" OR A0$(101,1)<>"C" THEN LET V4$=V4$+"/<F1>=Print/<F3>=Select Printer"
4037 LET V4$=V4$+")"
4040 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
4050 GOSUB 7000
4060 LET O3$=""
4070 IF V3=3 THEN GOSUB 4600; GOTO 4020
4080 IF V3=1 THEN IF C9$(18,1)<>"N" OR A0$(101,1)<>"C" THEN LET O3$="P"
4090 IF V3=2 THEN LET O3$="D"
4100 REM " --- Update Tickler File"
4120 IF A0$(101,1)<>"C" THEN LET C$="DELETE" ELSE LET C$=""
4190 GOSUB 5600
4200 REM " --- Write It Here"
4210 IF POS(O3$="PD")=0 THEN IF WARN$="YES" THEN GOSUB 6000; IF WARN$="YES" THEN GOTO 4000
4220 LET A[6]=U[0]
4230 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
4240 PRINT @(0,9),'CE'
4250 IF O3$="D" THEN RUN "OPE.DD"
4290 IF O3$="P" THEN GOTO 4500
4300 LET A0$(69,1)="N"
4320 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
4340 RUN "OPE.DB"
4500 REM " --- Print"
4510 LET PRTR_DEV=UNT
4520 CALL "SYC.GA",PRTR_DEV,0,"Print Order "+A0$(11,7),R2$(15,2)+FORMAT$,STATUS
4530 IF STATUS THEN GOTO 4000
4550 IF A0$(68,1)="Y" THEN LET A0$(100,1)="Y"
4560 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
4580 LET WARN$="",O9$="OPE.DB",O9=2
4590 RUN "OPR.PA"
4600 REM " --- Printer Select"
4610 DIM OPTION$[2]
4620 LET OPTION$[1]="ORDER"
4630 CALL "SYC.QP",STAT,OPTION$[ALL]
4635 IF OPTION$[1]="EXIT" THEN GOTO 4660
4640 LET R2$(15,2)=OPTION$[1]
4650 IF LEN(OPTION$[1])>2 THEN LET FORMAT$=OPTION$[1](3) ELSE LET FORMAT$=""
4660 PRINT 'SB',@(40,1),R2$(15,2),
4680 PRINT 'SF',
4690 GOTO 4000
4700 REM " --- Delete The Order"
4720 LET O0=4,V$="DELETE",A0$(101,1)=""
4790 GOTO 4100
5200 REM " --- Password"
5210 LET BAD_PASSWORD=0
5220 IF FNP$(C9$(19,6))="" THEN GOTO 5390
5230 LET KEYBOARD=UNT,I=0
5240 OPEN (KEYBOARD)FID(0)
5250 PRINT 'RB','SB',@(50,21),"Enter Password: [",'SF',"XXXXXX",'SB',"]",'SF',@(67,21),
5260 INPUT (KEYBOARD,TIM=10,ERR=5280)V$,@(67,21),'CL',
5270 IF FNU$(FNP$(V$))=FNU$(FNP$(C9$(19,6))) THEN GOTO 5350
5280 LET I=I+1
5290 IF I<3 THEN GOTO 5250
5300 LET BAD_PASSWORD=1
5350 CLOSE (KEYBOARD,ERR=5360)
5360 PRINT @(50,21),'CL','CURSOR'("OFF"),
5390 RETURN 
5500 REM " --- Terms Code Lookup"
5505 LET NMBR=1
5510 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
5520 LET DESCRIPT$[0]="Terms",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
5525 LET DESCRIPT$[1]="Description",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
5530 CALL "SYC.LK",ARM10_DEV,"A",NMBR,"Terms Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
5540 RETURN 
5600 REM " --- Update Tickler File"
5620 READ (ARM14_DEV,KEY=N0$+"C"+A0$(5,6),DOM=5621); REM "check for existing order...
5630 LET K$=KEY(ARM14_DEV,END=5690)
5640 IF K$(1,9)<>N0$+"C"+A0$(5,6) THEN GOTO 5690
5650 IF K$(16,7)<>A0$(11,7) THEN READ (ARM14_DEV); GOTO 5630
5660 IF FNYY_YY21$(FNM$(FNC$(A0$(77,3))))=K$(10,6) AND C$<>"DELETE" THEN GOTO 5790; REM "already there
5670 REMOVE (ARM14_DEV,KEY=N0$+"D"+K$(10,6)+K$(4,6)+K$(16,7),DOM=5680)
5680 REMOVE (ARM14_DEV,KEY=K$)
5690 IF C$="DELETE" THEN RETURN 
5710 LET ARM14_K$=N0$+"C"+A0$(5,6)+FNYY_YY21$(FNM$(FNC$(A0$(77,3))))+A0$(11,7)
5720 WRITE (ARM14_DEV,KEY=ARM14_K$)IOL=ARM14C
5730 LET ARM14_K$=N0$+"D"+FNYY_YY21$(FNM$(FNC$(A0$(77,3))))+A0$(5,13)
5740 WRITE (ARM14_DEV,KEY=ARM14_K$)IOL=ARM14D
5790 RETURN 
6000 REM " --- Warn About Printing Pick Ticket"
6010 IF A0$(68,1)="N" THEN GOTO 6090
6020 IF A0$(101,1)="C" AND C9$(18,1)="N" THEN GOTO 6090
6030 CALL "SYC.YN",1,"New Items Have Been Committed.  Print Pick Ticket Now",2,V$,V3
6040 IF V$="N" THEN LET WARN$=""
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8070 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNU$(Q$)=CVS(Q$,4)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

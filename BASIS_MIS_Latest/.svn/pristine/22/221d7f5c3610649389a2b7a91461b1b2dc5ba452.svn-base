0010 REM "Invoice Emailing "
0020 REM "Program BIN.10 - based on OPR.GA"
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES=19
0115 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="ARE-03",FILES$[2]="ARE-04",FILES$[3]="ARE-13"
0125 LET FILES$[4]="ARE-23",FILES$[5]="ARE-33",FILES$[6]="ARM-01"
0130 LET FILES$[7]="ARM-03",FILES$[8]="ARM-10",FILES$[9]="IVM-01"
0135 LET FILES$[10]="SYS-01",FILES$[11]="ARW-43",OPTIONS$[11]="CL"
0140 LET FILES$[12]="TMM-01",FILES$[13]="ARE-73",FILES$[14]="ARE-83"
0145 LET FILES$[15]="CSM-03",FILES$[16]="ARM-02",FILES$[17]="TMM-05"
0147 LET FILES$[18]="SMC-01",FILES$[19]="TMM-03"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS>0 THEN GOTO 9900
0160 LET ARE03_DEV=CHANNELS[1],ARE04_DEV=CHANNELS[2],ARE13_DEV=CHANNELS[3]
0165 LET ARE23_DEV=CHANNELS[4],ARE33_DEV=CHANNELS[5]
0170 LET ARM01_DEV=CHANNELS[6],ARM03_DEV=CHANNELS[7]
0175 LET ARM10_DEV=CHANNELS[8],IVM01_DEV=CHANNELS[9],SYS01_DEV=CHANNELS[10]
0180 LET ARW43_DEV=CHANNELS[11],TMM01_DEV=CHANNELS[12]
0185 LET ARE73_DEV=CHANNELS[13],ARE83_DEV=CHANNELS[14]
0190 LET CSM03_DEV=CHANNELS[15],ARM02_DEV=CHANNELS[16]
0195 LET TMM05_DEV=CHANNELS[17],SMC01_DEV=CHANNELS[18],TMM03_DEV=CHANNELS[19]
0200 REM " --- IOLists"
0210 call "templates.pgm::ARM02"
0220 call "templates.pgm::ARE03"
0230 ARM01A: IOLIST *,X0$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 IVS01A: IOLIST X$,X$,I2$,I3$,X$,X$,M1$,M2$,M3$,M4$
0290 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0300 ARW43A: IOLIST ARW43_K$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0460 DIM P[2],I[6]
0465 FOR X=0 TO 6
0470 LET I[X]=NUM(I2$(X*2+1,2))
0475 NEXT X
0485 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0490 LET M0=LEN(M0$),M2=LEN(M2$),M3=LEN(M3$),M4=LEN(M4$)
0495 CALL "SYC.RM",M1$,M1,8,12
0500 REM " --- Initialize Data"
0510 LET P5$="",P6$=""

0600 if n0$<>"01" then 
0602    xresp = msgbox("This Function is only available for firm 01, BAS.",0,"Not Firm BAS")
0604	goto done
0606 endif

0700 REM " --- Background"
0710 LET V4$="Email invoices to all distributors"
0715 if n0$="02" then v4$="Email invoices of all BSG customers"
0720 CALL "SYC.YN",1,V4$,4,V$,V3
0730 IF V3=4 THEN GOTO 9900
0740 LET DIST$=V$; ON POS(V$="YN") GOTO 0700,4130,0900
0900 PRINT 'SB',@(19,5),"Beginning Customer:",@(22,7),"Ending Customer:",'SF',
1000 REM " --- Beginning Cust
1010 PRINT 'CF',
1020 LET V2$=P5$,V1=39,V2=5
1040 GOSUB 10200
1050 IF V3=4 THEN GOTO 9900
1060 LET P5$=V$; IF P6$="" THEN LET P6$=P5$
1200 REM " --- Ending Cust
1220 LET V2$=P6$,V1=39,V2=7
1240 GOSUB 10200
1260 LET P6$=V$; IF P6$<P5$ THEN GOTO 1200
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct"
4020 CALL "SYC.YN",1,V4$,2,V$,V3
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4130,1000
4130 REM " --- Go Sort By Cust #
4140 GOSUB 10000
4150 REM " --- Run Overlay"
4190 RUN "BIN.11"
4290 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 done:
9910 RUN "SYS.AA"
9999 END
10000 REM " --- Sort By Cust # , type (Sam renew or std invoice)
10005 CALL "SYC.NB","Sorting",LEN(M0$)+8,COLUMN; LET ROW=11
10010 READ (ARE03_DEV,KEY=N0$,DOM=READ_ARE03)
10020 READ_ARE03: READ record(ARE03_DEV,END=END_SORT_RTN)ARE03$
10025 IF are03.firm_id$<>N0$ THEN GOTO END_SORT_RTN
10030 IF Are03.ord_inv_flag$<>"I" or are03.invoice_type$="V" THEN GOTO READ_ARE03
10035 IF Are03.ord_prt_flag$<>"Y" THEN GOTO read_are03;REM 'No unprinted invoices
10040 IF Are03.ord_prt_flag$<>"B" AND Are03.ord_lock$="Y" THEN GOTO read_are03
10050 if pos("SAM RENEW"=cvs(are03.ar_po_number$,7)) then 
10051    rnw$="SM" 
10052 else
10053    if pos("RNT RENEW"=cvs(are03.ar_po_number$,7)) then
10054       rnw$ = "RN"
10055    else
10056       if pos("DVK RENEW"=cvs(are03.ar_po_number$,7)) then
10057          rnw$="DV" 
10058       else
10059          rnw$="  "
10060       fi
10061    fi
10062 fi
10064 IF DIST$<>"Y" THEN GOTO NOT_DIST
10066 READ record(ARM02_DEV,KEY=N0$+are03.customer_nbr$+"  ",DOM=READ_ARE03)arm02$
10070 IF arm02.invoice_copy$="N" THEN GOTO WRITE_IT ELSE GOTO READ_ARE03
10080 NOT_DIST: IF P5$<>"" AND Are03.customer_nbr$<P5$ THEN GOTO READ_ARE03
10090 IF P6$<>"" AND Are03.customer_nbr$>P6$ THEN GOTO READ_ARE03
10100 WRITE_IT: LET ARW43_K$=n0$+are03.customer_nbr$+rnw$+are03.order_number$
10110 WRITE (ARW43_DEV,KEY=ARW43_K$)IOL=ARW43A
10120 PRINT @(COLUMN,ROW),are03.customer_nbr$," ",Are03.ar_inv_nbr$
10130 GOTO READ_ARE03
10140 END_SORT_RTN: RETURN 
10200 REM " --- Customer"
10220 LET V0$="Z",V1$="K",V3$="",V0=6,V4$=""
10230 GOSUB 7000
10280 PRINT @(V1,V2),V$
10290 RETURN 
46500 END

0005 IF STBL("+LEGACY",ERR=*NEXT)="Y" THEN RELEASE
0010 REM "SYS - ADD+ON Menu Processing"
0020 REM "Program SYS.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "10Sep96 WGH: Use user Default Firm for login initialization when multi-firm (835-840)"
0040 REM 
0050 ENDTRACE
0060 BEGIN
0070 LET X$=STBL("!CLEAR","!PROCESS",ERR=80)
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=10
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYM-01",FILES$[2]="SYM-02",FILES$[3]="SYM-04"
0135 LET FILES$[4]="SYM-06",FILES$[5]="SYM-09",FILES$[6]="SYM-11"
0140 LET FILES$[7]="SYM-12",FILES$[8]="SYM-19",FILES$[9]="SYM-29"
0145 LET FILES$[10]="SYS-01"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0170 LET SYM01_DEV=CHANNELS[1],SYM02_DEV=CHANNELS[2],SYM04_DEV=CHANNELS[3]
0180 LET SYM06_DEV=CHANNELS[4],SYM09_DEV=CHANNELS[5],SYM11_DEV=CHANNELS[6]
0190 LET SYM12_DEV=CHANNELS[7],SYM19_DEV=CHANNELS[8],SYM29_DEV=CHANNELS[9]
0195 LET SYS01_DEV=CHANNELS[10]
0200 REM " --- IOLIST's"
0210 SYM01A: IOLIST A0$(1),A1$(1),A2$(1)
0220 SYM02A: IOLIST B0$(1),B1$(1)
0230 SYM04A: IOLIST C0$(1),C1$(1),C2$(1),C3$(1),C[ALL]
0240 SYM06A: IOLIST D0$(1),D1$(1),D2$(1)
0250 SYM09A: IOLIST *,*,Y2$
0260 SYM19A: IOLIST *,*,X2$
0300 SYS01T: IOLIST TERMINAL$,F0$(1),F1$,F2$,F3$,F4$,F5$
0310 SYS01S: IOLIST S0$,S1$,S2$,S3$,S4$,S5$,S6$
0400 REM " --- Parameters"
0410 LET P9=18,P7=30,FG$="",BG$="",VERSION$="6.0.0",TERMINAL$="T"+FID(0)
0420 LET VERSION$=STBL("!VERSION",ERR=430)
0430 REM " --- Reset function keys"
0440 CALL "SYC.FK",ERR=450,STATUS
0450 REM " --- Clear global variables"
0455 LET X$=STBL("!GLCONTROL",ERR=480)
0460 CALL "GLC.DA",ERR=470,STATUS
0470 LET X$=STBL("!CLEAR","!GLCONTROL",ERR=480)
0480 LET X$=STBL("!CLEAR","!HELP",ERR=485)
0485 LET X$=STBL("!CLEAR","!SPEEDSEARCH",ERR=490)
0490 LET X$=STBL("!CLEAR","!FF",ERR=495)
0500 REM " --- Initializations"
0510 DIM OPTLIST$[2,17,6],SELECTION$(6),DESCRIPTION$(35),FOOTER$(80)
0520 DIM A0$(6),A1$(35),A2$(16),B0$(3),B1$(128),B[4],C0$(2),C1$(64)
0530 DIM C2$(50),C3$(50),C[2],D0$(2),D1$(40),D2$(16),F0$(25)
0550 LET HELP$="Y",BOXWIDTH=55,BOXCOL=FNC(80,BOXWIDTH),M0$="#0.-"
0560 LET LOGIN$="",MENU=0
0800 REM " --- Retrieve terminal and user records"
0810 FIND (SYS01_DEV,KEY=TERMINAL$,DOM=820)IOL=SYS01T
0820 IF LEN(FNP$(F1$))<7 THEN LET F5$=""
0825 IF FNP$(F0$(7,3))="" THEN LET F0$(7,3)=FND$(STR(DATE(0:"%Mz%Dz%Yz")))
0830 WRITE (SYS01_DEV,KEY=TERMINAL$)IOL=SYS01T
0835 FIND (SYM02_DEV,KEY=F0$(4,3),DOM=9600)IOL=SYM02A
0840 IF FNP$(F0$(16,2))="" THEN LET F0$(16,2)=B1$(111,2)
0845 IF FNP$(F0$(16,2))<>"" THEN GOTO 1000
0850 REM " --- Use first valid firm for user"
0860 READ (SYM12_DEV,KEY=B0$,DOM=870)
0870 LET K$=KEY(SYM12_DEV,END=900)
0880 IF POS(B0$=K$)<>1 THEN GOTO 0900
0885 LET F0$(16,2)=K$(4)
0890 GOTO 1000
0900 REM " --- Use first valid firm"
0910 READ (SYM06_DEV,KEY="",DOM=920)
0920 LET K$=KEY(SYM06_DEV,END=940)
0930 LET F0$(16,2)=K$
0935 GOTO 0950
0940 LET F0$(16,2)="99"
0950 WRITE (SYS01_DEV,KEY=TERMINAL$)IOL=SYS01T
1000 REM " --- Drop any programs ADDR'd for previous task"
1010 LET ACTION$="D",PGM$=FNP$(F1$)
1020 IF LEN(PGM$)>6 THEN LET PGM$=PGM$(1,6)
1030 IF PGM$<>"" THEN CALL "SYC.PP",ACTION$,PGM$,STATUS
1100 REM " --- Multiple programs for this task?"
1110 LET PGM$=FNP$(F1$)
1120 IF LEN(PGM$)<7 THEN GOTO 1200
1130 LET F1$=PGM$(7),PGM$=FNP$(F1$)
1140 IF LEN(PGM$)>6 THEN LET PGM$=PGM$(1,6)
1150 PRINT @(0,3),'CE',
1190 GOTO 3950
1200 REM " --- Background"
1210 CALL "SYC.WK",ERR=1220,""
1220 CALL "SYC.WC",0,0,47,3,0,0,0
1230 CALL "SYC.WC",0,0,33,3,47,0,0
1240 CALL "SYC.WC",0,0,BOXWIDTH,19,BOXCOL,3,0
1250 PRINT 'SB',@(49,0)," ADD+ON Software v",VERSION$," ",@(68,1),FID(0),'SF',
1300 REM " --- Retrieve firm, update header and display footer"
1310 LET PRINTER$=F0$(13,2)+"  "
1320 IF F0$(25,1)<>" " THEN LET PRINTER$(3,2)="-"+F0$(25,1)
1330 GOSUB 5200
1340 PRINT 'SB',@(49,1),FNB$(F0$(7,3)),@(58),FNE$(Q$),@(74),PRINTER$,'SF',
1500 REM " --- Retrieve menu"
1510 LET OPTIONS=0,TITLE$="",NAME$="",SECURITY$=F0$(15,1)
1520 IF FNP$(F2$)="" THEN LET F2$="SYS"
1530 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,F2$,SECURITY$,OPTIONS,OPTLIST$[ALL],TITLE$,MAXROW,PREVMENU$,NAME$
1540 IF OPTIONS>0 THEN GOTO 1600
1550 IF F2$="SYS" THEN GOTO 1600
1560 LET F2$=PREVMENU$
1590 GOTO 1500
1600 REM " --- Calculate column position"
1610 LET SELECTION$(1)=F2$,WIDTH=0,CURRENT=1,PRIOR=0
1620 FOR X=1 TO OPTIONS
1630 IF LEN(OPTLIST$[1,X,1])>WIDTH THEN LET WIDTH=LEN(OPTLIST$[1,X,1])
1640 IF LEN(F1$)>5 THEN IF POS(F1$(1,6)=OPTLIST$[1,X,2])=1 THEN LET CURRENT=X
1650 NEXT X
1690 LET WIDTH=WIDTH+3+(MAXROW>9),COLUMN=FNC(80,WIDTH)
1700 REM " --- Display menu options"
1710 DIM BAR$(WIDTH)
1720 LET DESCRIPTION$(1)=TITLE$
1730 CALL "SYC.WC",1,0,BOXWIDTH-2,1,BOXCOL+1,3,0
1740 PRINT 'CF','SB',@(3,1),SELECTION$," ",DESCRIPTION$,'SF',@(FNC(80,LEN(TITLE$))-1,3)," ",TITLE$," ",
1750 FOR X=1 TO OPTIONS
1760 PRINT @(COLUMN,X+3),X:M0$,OPTLIST$[1,X,1],
1770 NEXT X
1800 REM " --- If MENU option is enabled, display option ID's"
1810 IF MENU=0 THEN GOTO 1900
1820 DIM X$(6)
1830 FOR X=1 TO OPTIONS
1840 LET X$(1)=OPTLIST$[1,X,2]
1850 PRINT @(7,X+3),X$," ---> ",
1890 NEXT X
2000 REM " --- Display prompt"
2010 LET V1=49,V4$="Enter An Option (On/Off/Date"
2020 IF B1$(110,1)="Y" THEN LET V4$=V4$+"/Menu",V1=54
2030 PRINT 'SB',@(19,21),V4$,")",'SF',
2100 REM " --- Accept and process user input"
2110 PRINT 'SB',@(58,1),FNE$(Q$),'SF',
2120 GOSUB 5000
2130 GOSUB 6000
2135 FIND (SYS01_DEV,KEY=TERMINAL$)IOL=SYS01T
2140 ON V3 GOTO 2150,2200,2100,2300,2400,2100
2150 PRINT @(COLUMN,CURRENT+3),BAR$,@(COLUMN,CURRENT+3),CURRENT:M0$,OPTLIST$[1,CURRENT,1],
2160 LET COMMAND$=FNS$(V$)
2190 GOTO 2500
2200 REM " --- <F1> Reports Display"
2210 LET PGM$="SYC.QR"
2220 GOSUB 6600
2290 GOTO 2100
2300 REM " --- <F3> Change Firm"
2310 CALL "SYC.LX",V$
2320 IF V$="" THEN GOTO 2390
2330 IF V$=F0$(16,2) THEN GOTO 2390
2340 FIND (SYM06_DEV,KEY=V$,DOM=2300)
2350 LET F0$(16,2)=V$
2360 GOSUB 5200
2390 GOTO 2100
2400 REM " --- <F4> Previous Menu"
2410 IF SELECTION$=B1$(104,6) THEN LET PREVMENU$=B1$(104,6)
2420 LET F2$=PREVMENU$
2430 IF FNP$(F2$)="" THEN LET F2$="SYS"
2440 WRITE (SYS01_DEV,KEY=TERMINAL$)IOL=SYS01T
2490 GOTO 1500
2500 REM " --- Entered a menu name?"
2510 IF LEN(V$)<>3 THEN GOTO 3000
2520 IF B1$(110,1)<>"Y" THEN GOTO 3000
2550 LET A0$(1)=V$
2560 FIND (SYM01_DEV,KEY=A0$,DOM=2580)IOL=SYM01A
2570 GOTO 2600
2580 LET A0$(1)=COMMAND$
2590 FIND (SYM01_DEV,KEY=A0$,DOM=3000)IOL=SYM01A
2600 REM " --- Is system installed?"
2610 IF A2$(9,1)<>"A" THEN GOTO 3000
2620 LET SYSTEM$=A2$(1,2)
2630 GOSUB 5500
2640 IF STATUS>0 THEN GOTO 2100
2650 IF FNP$(A2$(10,1))<>"" THEN IF F0$(15,1)<A2$(10,1) THEN GOTO 2700
2670 LET F2$=FNP$(A0$)
2680 WRITE (SYS01_DEV,KEY=TERMINAL$)IOL=SYS01T
2690 GOTO 1500
2700 REM " --- Security Violation"
2710 DIM MESSAGE$[1]
2720 LET STATUS=1,TITLE$="System Security"
2730 LET MESSAGE$[0]="Security Violation! Access Denied (<Enter>=Continue)"
2740 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
2790 GOTO 2100
3000 REM " --- Entered a number?"
3010 LET SELECTION=NUM(V$,ERR=4000),ACTION$="M",SECURITY$=F0$(15,1)
3020 IF SELECTION<1 THEN GOTO 2100
3030 IF SELECTION>OPTIONS THEN GOTO 2100
3040 CALL "SYC.QX",ACTION$,SECURITY$,SELECTION,OPTLIST$[ALL],STATUS
3050 IF STATUS>0 THEN GOTO 2100
3100 REM " --- Selection is a menu"
3110 IF OPTLIST$[1,SELECTION,4]<>"M" THEN GOTO 3200
3120 LET V$=FNP$(OPTLIST$[1,SELECTION,2])
3190 GOTO 2550
3200 REM " --- Selection is a program"
3210 IF POS(OPTLIST$[1,SELECTION,4]="RT")>0 THEN GOTO 3300
3220 LET PGM$=FNP$(OPTLIST$[1,SELECTION,2])
3225 IF LEN(PGM$)>6 THEN LET PGM$=PGM$(1,6)
3230 GOSUB 6500
3240 IF STATUS=0 THEN GOTO 3400
3250 GOSUB 6800
3290 GOTO 2100
3300 REM " --- GENERAL Report Writer or TAOS"
3310 IF OPTLIST$[1,SELECTION,4]="R" THEN LET PGM$="SYS.RR"
3320 IF OPTLIST$[1,SELECTION,4]="T" THEN LET PGM$="SYS.RT"
3390 GOTO 3900
3400 REM " --- System installed/multi-firm?"
3410 LET A0$(1)=F2$
3420 FIND (SYM01_DEV,KEY=A0$,DOM=3480)IOL=SYM01A
3430 LET SYSTEM$=A1$(1,2)
3440 GOSUB 5500
3450 IF STATUS>0 THEN GOTO 2100
3460 GOTO 3900
3480 GOSUB 6700
3490 GOTO 2100
3900 REM " --- Run selected program"
3910 FIND (SYS01_DEV,KEY=TERMINAL$)IOL=SYS01T
3920 LET F1$=FNP$(OPTLIST$[1,SELECTION,2]),F5$=OPTLIST$[1,SELECTION,1]
3930 LET SELECTION$(1)=F1$,DESCRIPTION$(1)=F5$
3940 PRINT 'SB',@(3,1),SELECTION$," ",DESCRIPTION$,@(58),FNE$(Q$),'SF',@(0,3),'CE',
3950 WRITE (SYS01_DEV,KEY=TERMINAL$)IOL=SYS01T
3970 CALL "SYC.PP","A",PGM$,STATUS
3980 GOSUB 5400
3990 RUN PGM$,ERR=1500
4000 REM " --- MENU - Display menu information"
4010 IF COMMAND$<>"MENU" THEN GOTO 4100
4020 IF MENU THEN GOTO 4050
4030 LET MENU=1
4040 GOTO 1500
4050 LET MENU=0
4060 CALL "SYC.WC",0,0,BOXWIDTH,19,BOXCOL,3,0
4090 GOTO 1500
4100 REM " --- ON/OFF - Login/logoff"
4110 IF COMMAND$="ON" THEN GOTO 9600
4120 IF COMMAND$="OFF" THEN GOTO 9900
4130 IF CVS(COMMAND$,4)="OUT" THEN GOTO 9900
4200 REM " --- DATE - Change terminal date"
4210 IF COMMAND$<>"DATE" THEN GOTO 4300
4220 CALL "SYC.WA",0,48,3,16,10,"",NAME$
4230 LET V0$="D",V1$="R",V2$=F0$(7,3),V3$="",V4$="",V0=3,V1=36,V2=0
4240 PRINT @(2,0),"Please Enter The Processing Date:",
4250 GOSUB 7000
4260 CALL "SYC.WD",NAME$
4270 LET F0$(7,3)=V$,F0$(18,6)=FNYY_YY21$(FNM$(FNC$(F0$(7,3))))
4275 WRITE (SYS01_DEV,KEY=TERMINAL$)IOL=SYS01T
4280 PRINT @(49,1),'SB',FNB$(F0$(7,3)),'SF',
4290 GOTO 2000
4300 REM " --- SYSTEM/SHELL/S - Shell out to operating system"
4310 IF COMMAND$="SYSTEM" THEN GOTO 9700
4320 IF COMMAND$="SHELL" THEN GOTO 9700
4330 IF COMMAND$="S" THEN GOTO 9700
4400 REM " --- PASSWORD - Change user password"
4410 IF COMMAND$<>"PASSWORD" THEN GOTO 4500
4420 LET PGM$="SYC.QD"
4430 GOSUB 6600
4490 GOTO 2100
4500 REM " --- BASIC/B - Exit to BASIC prompt"
4510 IF COMMAND$<>"BASIC" AND COMMAND$<>"B" THEN GOTO 4700
4520 CALL "SYC.ES","",0,X$,V2,V3
4530 IF V3<>127 THEN GOTO 2100
4590 GOTO 9800
4700 REM " --- Direct entry program?"
4710 IF F0$(15,1)<"9" THEN GOTO 2100
4720 LET PGM$=V$
4730 GOSUB 6500
4740 IF STATUS=0 THEN GOTO 4800
4750 LET PGM$=COMMAND$
4760 GOSUB 6500
4770 IF STATUS=0 THEN GOTO 4800
4780 GOSUB 6900
4790 GOTO 2100
4800 REM " --- Run direct entry program"
4810 FIND (SYS01_DEV,KEY=TERMINAL$)IOL=SYS01T
4820 LET F1$=FNP$(PGM$),SELECTION$(1)=F1$,DESCRIPTION$(1)="",F5$="",X$=F1$
4830 IF LEN(X$)>6 THEN LET X$=X$(1,6)
4840 WRITE (SYS01_DEV,KEY=TERMINAL$)IOL=SYS01T
4850 PRINT 'SB',@(3,1),SELECTION$," ",DESCRIPTION$,@(58),FNE$(Q$),'SF',@(0,3),'CE','CURSOR'("ON"),
4860 CALL "SYC.PP","A",X$,STATUS
4880 GOSUB 5400
4890 RUN PGM$,ERR=1500
5000 REM " --- Update lightbar"
5010 IF PRIOR>0 THEN PRINT @(COLUMN,PRIOR+3),BAR$,@(COLUMN,PRIOR+3),PRIOR:M0$,OPTLIST$[1,PRIOR,1],
5020 PRINT @(COLUMN,CURRENT+3),'BR',BAR$,@(COLUMN,CURRENT+3),CURRENT:M0$,OPTLIST$[1,CURRENT,1],'ER',
5090 RETURN
5200 REM " --- Display firm and footer"
5210 LET D1$(1)="",D2$(1)="",HELP$="Y",px$=pfx,p0=pos("BACK"=px$)
5215 FIND (SYM06_DEV,KEY=F0$(16,2),DOM=5220)IOL=SYM06A
5220 IF D2$(1,1)="N" THEN LET HELP$="N"
5230 LET FOOTER$(1)=" F1=Reports  F3=Company  F5=SpeedSearch"
5240 IF HELP$="Y" THEN LET FOOTER$(42,7)="F6=Help"
5250 LET FOOTER$(56)="ADD+ON Software (c) 1996"
5260 LET F4$=FNP$(D1$),X$=STBL("!SCREEN"),X$(9,1)=HELP$,X$=STBL("!SCREEN",X$)
5270 CALL "SYC.WC",1,0,42,1,2,0,0
5275 if p0 then f4$=f4$+" "+px$(p0,5)
5280 PRINT 'SB',@(2,0)," ",F4$," ",'BR',@(0,22),FOOTER$,'ER','SF','CURSOR'("OFF"),
5285 WRITE (SYS01_DEV,KEY=TERMINAL$)IOL=SYS01T
5290 RETURN
5400 REM " --- Set !PROCESS Global Variable"
5410 READ (SYM29_DEV,KEY=PGM$,DOM=5415)
5415 LET SYM29_K$=KEY(SYM29_DEV,END=5490)
5420 IF FNP$(SYM29_K$(1,8))<>PGM$ THEN GOTO 5490
5430 FIND (SYM09_DEV,KEY=SYM29_K$(9,10),DOM=5490)IOL=SYM09A
5435 IF Y2$(3,1)<>"Y" THEN GOTO 5490
5440 FIND (SYM04_DEV,KEY=Y2$(1,2),DOM=5490)IOL=SYM04A
5445 IF C1$(47,1)<>"Y" THEN GOTO 5490
5450 FIND (SYM06_DEV,KEY=F0$(16,2),DOM=5490)IOL=SYM06A
5455 IF D2$(2,1)<>"Y" THEN GOTO 5490
5470 FIND (SYM19_DEV,KEY=SYM29_K$(9,10)+SYM29_K$(1,8),DOM=5490)IOL=SYM19A
5480 LET X$=STBL("!PROCESS",SYM29_K$(9,10)+F0$(16,2)+FILL(3,"0")+X2$(1,1)+F0$(4,3)+F0$(18,6),ERR=5490)
5490 RETURN
5500 REM " --- System installed?"
5510 LET C0$(1)=A2$(1,2),STATUS=0
5520 FIND (SYM04_DEV,KEY=C0$,DOM=5700)IOL=SYM04A
5530 IF C1$(39,1)<>"Y" THEN GOTO 5700
5600 REM " --- Multi-firm?"
5610 IF C1$(36,1)="Y" THEN GOTO 5890
5620 READ (SYM12_DEV,KEY=F0$(4,3)+C1$(37,2),DOM=5650)
5640 GOTO 5800
5650 READ (SYM12_DEV,KEY=F0$(4,3),DOM=5660)
5660 LET K$=KEY(SYM12_DEV,END=5800)
5670 IF POS(F0$(4,3)=K$)<>1 THEN GOTO 5800
5700 REM " --- Not installed or not a valid firm"
5710 GOSUB 6700
5720 LET STATUS=1
5790 GOTO 5890
5801 REM " --- Switch to new firm"
5810 LET F0$(16,2)=C1$(37,2)
5820 GOSUB 5200
5890 RETURN
6000 REM " --- Lightbar Input"
6010 LET V0$="S",V1$="M",V2$="",V3$="",V4$="",V0=8,V2=21
6020 GOSUB 7000
6030 IF V3<>-2 THEN GOTO 6200
6100 REM " --- User entered a character - Switch to normal input"
6110 LET V2$=V$,V1$="E"
6120 IF B1$(26,1)="R" THEN PRINT @(V1,V2),'BR',V2$,'ER',
6130 GOSUB 7000
6200 REM " --- User entered an arrow or fkey"
6210 IF V3<>0 THEN LET V$=""
6220 IF V3=-3 OR V3=2 THEN GOTO 6300
6290 GOTO 6400
6300 REM " --- Up/Down Arrow - Move lightbar"
6310 LET PRIOR=CURRENT
6320 IF V3=-3 THEN LET CURRENT=CURRENT+1
6330 IF V3=2 THEN LET CURRENT=CURRENT-1
6340 IF CURRENT>OPTIONS THEN LET CURRENT=1
6350 IF CURRENT=0 THEN LET CURRENT=OPTIONS
6360 GOSUB 5000
6390 GOTO 6000
6400 REM " --- Lightbar input completed"
6410 IF V3=0 AND FNS$(V$)="END" THEN LET V3=4
6420 IF V$="" THEN LET V$=STR(CURRENT)
6490 RETURN
6500 REM " --- Does program exist?"
6510 LET CHANNEL=UNT,STATUS=0
6520 OPEN (CHANNEL,ERR=6550)PGM$
6525 LET X$=FID(CHANNEL,ERR=6550)
6530 IF AND(X$(1,1),$0F$)<>$04$ THEN GOTO 6550; REM BBj Mod: Program file returns $84$ as file type instead of $04$
6540 GOTO 6580
6550 LET X$=$22$+PGM$+$22$+" Is Not A Valid Option",STATUS=1
6580 CLOSE (CHANNEL,ERR=6590)
6590 RETURN
6600 REM " --- SpeedSearch call for PF or password"
6610 DIM OPTION$[5],SPEED$(128)
6620 LET SPEED$(1,2)=F0$(16,2),SPEED$(57,2)="15",SPEED$(63,2)="15"
6630 LET SPEED$(65,1)=F0$(15,1),SPEED$(47,3)=F0$(4,3)
6640 LET X$=STBL("!SPEEDSEARCH",SPEED$),X$=STBL("!SYS01",STR(SYS01_DEV))
6650 LET X$=STBL("!SYM01",STR(SYM01_DEV)),X$=STBL("!SYM11",STR(SYM11_DEV))
6660 CALL PGM$,ERR=6670,STATUS,OPTION$[ALL]
6670 LET X$=STBL("!CLEAR","!SPEEDSEARCH",ERR=6670)
6675 LET X$=STBL("!CLEAR","!SYS01",ERR=6680)
6680 LET X$=STBL("!CLEAR","!SYM01",ERR=6685)
6685 LET X$=STBL("!CLEAR","!SYM11",ERR=6690)
6690 RETURN
6700 REM " --- Access Violation"
6710 LET X$="This Is Not A Valid Option"
6790 GOTO 6900
6800 REM " --- Option Not Available"
6810 LET X$="This Option Is Not Available"
6900 REM " --- Display window with error message"
6910 DIM MESSAGE$[1]
6920 LET MESSAGE$[0]=X$+" (<Enter>=Continue)"
6930 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
6990 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=DATE(0:"%hz:%mz %p")
8060 DEF FNC(Q1,Q2)=INT(((Q1-Q2))/2+0.5)
8070 DEF FNS$(Q$)=CVS(Q$,6)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9800
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9600 REM " --- Return to login"
9610 LET LOGIN$="SYS.ZA"
9690 GOTO 9900
9700 REM " --- Shell to operating system"
9710 CALL "SYC.WK",ERR=9720,"CLEAR"
9720 PRINT @(0,0),"Operating System Shell ...",@(0,2),"Type ",$22$,"exit",$22$," To Return To ADD+ON",@(0,4),'CURSOR'("ON"),
9730 FIND (SYS01_DEV,KEY="S",DOM=9740)IOL=SYS01S
9740 CALL "SYC.OS",S5$
9790 GOTO 0001
9800 REM " --- BASIC Console Mode"
9810 CALL "SYC.WK",ERR=9820,"CLEAR"
9820 WRITE (SYS01_DEV,KEY=TERMINAL$)IOL=SYS01T
9830 PRINT @(0,0),"Business BASIC Console Mode...",'CURSOR'("ON")
9840 BEGIN
9850 DELETE 1,9999
9900 REM " --- Exit"
9910 CALL "SYC.WK",ERR=9920,"CLEAR"
9920 LET F0$(4,3)="",F0$(16,2)="",F2$="",F4$="",F5$=""
9930 WRITE (SYS01_DEV,KEY=TERMINAL$,ERR=9940)IOL=SYS01T
9940 rem 'RUN LOGIN$,ERR=9950

10000 rem ' exit message
10010 if info(6,0) = "" then
10020      print "You have Logged out of the BASIS CUI Production/Test app, refresh the browser page to login again"
10030 else
10040      print @(0,0), "Exiting AddonSoftware, powered by BBj (CR) to continue "
10050      input (0, tim=10, err=*next) *
10060 fi
10070 release
100080 end





0010 REM "SYC - Batch Entry Control"
0020 REM "Program SYC.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "SYM02_DEV: SYM-02 channel"
0052 REM "SYM09_DEV: SYM-09 channel"
0054 REM "SYM49_DEV: SYM-49 channel"
0056 REM "SYM59_DEV: SYM-59 channel"
0058 REM "STATUS   : Return status (  0=No Error)"
0060 REM "                         (256=No Batch selected)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER SYM02_DEV,SYM09_DEV,SYM49_DEV,SYM59_DEV,STATUS
0200 REM " --- IOLIST's"
0210 SYM02A: IOLIST *,X1$(1)
0220 SYM09A: IOLIST *,Y1$(1)
0230 SYM49A: IOLIST D0$(1),D1$(1),D2$(1),D3$,D[ALL]
0240 SYM59A: IOLIST Z0$
0400 REM " --- Parameters"
0410 LET STATUS=0,GLOBAL$=STBL("!PROCESS",ERR=9900)
0420 LET PROCESS$=GLOBAL$(1,10),N0$=GLOBAL$(11,2),NEW$=GLOBAL$(16,1)
0430 LET USR$=GLOBAL$(17,3),DATE$=GLOBAL$(20,6)
0500 REM " --- Initializations"
0510 DIM Y1$(30),D0$(3),D1$(50),D2$(30),D[3]
0550 LET WIDTH=52,HEIGHT=11,X_WIN=15,Y_WIN=5
0700 REM " --- Background"
0710 LET NAME$=""
0720 CALL "SYC.WA",0,WIDTH,HEIGHT,X_WIN,Y_WIN,"Batch Entry",NAME$
0740 PRINT 'SB',@(5,1),"Batch Number:",@(6,2),"Description:",@(10,3),"Firm ID:",@(7,4),"Created By:",@(5,5),"Batch Opened:",@(2,6),"Batch Last Used:",'SF',
0800 REM " --- Exit If No Batches And Can't Create One"
0805 IF NEW$="Y" THEN GOTO 0900
0810 READ (SYM59_DEV,KEY=PROCESS$+N0$,DOM=0820)
0820 LET SYM59_K$="",SYM59_K$=KEY(SYM59_DEV,END=0830)
0830 IF POS(PROCESS$+N0$=SYM59_K$)=1 THEN GOTO 0900
0840 LET NUMBER=1
0850 DIM MESSAGE$[NUMBER]
0860 LET MESSAGE$[0]="No Batches Currently Exist."
0870 LET MESSAGE$[1]=" <Enter>=Continue"
0880 CALL "SYC.XA",2,MESSAGE$[ALL],NUMBER,-1,-1,V$,V3
0890 GOTO 4500
0900 REM " --- Position File"
0990 READ (SYM59_DEV,KEY=PROCESS$+N0$,DOM=1000)
1000 REM " --- Batch Number"
1010 PRINT 'CF',
1020 LET NEW_BATCH=0,D0$(1)="",SYM59_K$=KEY(SYM59_DEV,END=1050)
1030 READ (SYM59_DEV)
1040 IF POS(PROCESS$+N0$=SYM59_K$)=1 THEN LET D0$(1)=SYM59_K$(13)
1050 LET V0$="Z",V1$="KE",V2$=D0$,V3$="",V4$="<F3>=Lookup",V0=3,V1=19,V2=1
1055 IF NEW$="Y" THEN LET V4$="<F1>=Next Batch Number     "+V4$
1060 GOSUB 7000
1070 ON V3 GOTO 1100,1080,0900,1090,4500
1080 IF NEW$<>"Y" THEN GOTO 1050 ELSE GOTO 1500
1090 CALL "SYC.LF",SYM02_DEV,SYM49_DEV,SYM59_DEV,PROCESS$,FIRM$,V$
1100 IF V$="" THEN GOTO 0900
1200 REM " --- Retrieve record"
1210 FIND (SYM49_DEV,KEY=V$,DOM=1050)IOL=SYM49A
1220 IF POS(PROCESS$+N0$=D1$)<>1 THEN GOTO 1050
1230 IF FNP$(D1$(26,10))="" THEN GOTO 1050; REM "Batch creation not completed
1240 READ (SYM59_DEV,KEY=PROCESS$+N0$+V$,DOM=1250)
1250 LET D0$(1)=V$
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1500 REM " --- New Batch Number"
1510 LET D0$(1)="",D0$(1)=KEYL(SYM49_DEV,END=1520)
1520 IF D0$="999" THEN LET D0$(1)=""
1530 LET D0$(1)=STR(NUM(D0$)+1:"000")
1540 IF D0$>="999" THEN GOTO 1600
1550 READ (SYM49_DEV,KEY=D0$,DOM=1700)
1590 GOTO 1530
1600 REM " --- No New Batches Available"
1610 LET NUMBER=1
1620 DIM MESSAGE$[NUMBER]
1630 LET MESSAGE$[0]="No New Batch Numbers Are Available."
1640 LET MESSAGE$[1]="    <Enter>=Continue"
1650 CALL "SYC.XA",2,MESSAGE$[ALL],NUMBER,-1,-1,V$,V3
1690 GOTO 4500
1700 REM " --- Initialize New Batch"
1710 DIM D[3]
1720 LET NEW_BATCH=1,D1$(1)=PROCESS$+N0$+USR$+DATE$+DATE(0:"%Hz%mz")
1730 LET Y1$(1)="",D1$(26,10)=D1$(16,10)
1740 FIND (SYM09_DEV,KEY=PROCESS$,DOM=1750)IOL=SYM09A
1750 LET D2$(1)=Y1$
1760 WRITE (SYM49_DEV,KEY=D0$,DOM=1590)IOL=SYM49A
1770 LET Z0$=D1$(1,10)+D1$(11,2)+D0$
1780 WRITE (SYM59_DEV,KEY=Z0$)IOL=SYM59A
1790 GOSUB 5000
2000 REM " --- Description"
2020 LET V0$="S",V1$="",V2$=D2$,V3$="",V4$="Enter Description For This Batch",V0=30,V1=19,V2=2,I0=0
2040 GOSUB 7000
2050 ON V3 GOTO 2060,2000,3900,2000,3900
2060 LET D2$(1)=V$
2090 GOTO 3900
3900 REM " --- Next Field"
3902 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,4000
4000 REM " --- Correct?"
4005 LET V0$="Y",V4$="Is This The Correct Batch (Y/N)?",V0=1
4010 IF NEW_BATCH THEN LET V0$="S",V4$="Is Batch Data Correct (Y/N/Delete)?",V0=6
4020 LET V1$="^CE",V2$="",V3$="",V1=FNV(V4$,WIDTH-2),V2=9
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4050 IF NEW_BATCH AND V$="DELETE" THEN GOTO 4300
4080 ON POS(V$="YN") GOTO 4000,4100,4090
4090 IF NEW_BATCH THEN GOTO 2000 ELSE GOTO 1000
4100 REM " --- Write Batch"
4110 LET D1$(26,10)=DATE$+DATE(0:"%Hz%mz")
4120 WRITE (SYM49_DEV,KEY=D0$)IOL=SYM49A
4130 LET Z0$=D1$(1,10)+D1$(11,2)+D0$
4140 WRITE (SYM59_DEV,KEY=Z0$)IOL=SYM59A
4180 LET X$=STBL("!PROCESS",GLOBAL$(1,12)+D0$+GLOBAL$(16))
4190 GOTO 9900
4300 REM " --- Delete New Batch Number"
4320 REMOVE (SYM49_DEV,KEY=D0$,DOM=4330)
4340 REMOVE (SYM59_DEV,KEY=D1$(1,10)+D1$(11,2)+D0$,DOM=4350)
4390 GOTO 1000
4500 REM " --- Exit Without Batch Number"
4510 IF NEW_BATCH THEN GOTO 4300
4580 LET STATUS=256
4590 GOTO 9900
5000 REM " --- Display"
5010 GOSUB 6000
5020 IF D1$(22,2)>="12" THEN LET PM$=" PM" ELSE LET PM$=" AM"
5030 IF D1$(22,2)>"12" THEN LET HOUR$=STR(NUM(D1$(22,2))-12:"00") ELSE LET HOUR$=D1$(22,2)
5040 PRINT @(19,1),D0$,@(19,2),D2$,@(19,3),D1$(11,2),@(19,4),X1$,@(19,5),FNB6$(D1$(16,6))," ",HOUR$,":",D1$(24,2),PM$,
5050 IF FNP$(D1$(32,2))="" THEN LET PM$="" ELSE IF D1$(32,2)>="12" THEN LET PM$=" PM" ELSE LET PM$=" AM"
5060 IF D1$(32,2)>"12" THEN LET HOUR$=STR(NUM(D1$(32,2))-12:"00") ELSE LET HOUR$=D1$(32,2)
5070 PRINT @(19,6),FNB6$(D1$(26,6))," ",HOUR$,":",D1$(34,2),PM$,
5090 RETURN 
6000 REM " --- Retrieve Operator Description"
6010 DIM X1$(24)
6020 LET X1$(1)="Not On File"
6030 READ (SYM02_DEV,KEY=D1$(13,3),DOM=6090)IOL=SYM02A
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$,Q)=(INT(Q/2)+2)+(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9800
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Return STATUS = ERR"
9810 LET STATUS=ERR
9820 IF STATUS=0 THEN LET STATUS=999
9900 REM " --- Return to calling program"
9910 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9950 EXIT 
9999 END

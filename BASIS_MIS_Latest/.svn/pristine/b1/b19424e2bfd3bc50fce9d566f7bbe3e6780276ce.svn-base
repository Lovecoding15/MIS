0010 REM "POS - Replenishment Selection Update"
0020 REM "Program POU.BA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "21Oct96 CAJ: Program rewritten for 6.0.2 replenishment
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 APM06A: IOLIST W0$,W1$,W[ALL]
0220 IVM01A: IOLIST R0$,R1$,R2$(1),R3$,R4$,R5$,R6$,R[ALL]
0230 IVM02A: IOLIST S0$,S1$(1),S2$(1),S[ALL]
0240 IVM05A: IOLIST T0$,T1$,T2$,T[ALL],T0
0250 IVM10F: IOLIST *,BUYERCYCLEDAYS
0270 POE06A: IOLIST A0$(1),A1$(1)
0280 POE16A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0290 POE26A: IOLIST C0$(1),C1$(1),C2$,C[ALL]
0500 REM " --- Initialization
0510 DIM A0$(14),A1$(40)
0530 LET CREATE$="N"; REM "Create Requisition?"
0540 LET DATEREQ$=F0$(18,6),DATEORD$=F0$(18,6)
0800 REM " --- Update Review Cycles?
0820 CALL "SYC.YN",0,"Update Review Cycle Dates for All Selected Buyers and Vendors",0,V$,V3
0860 IF V$="NO" OR V3=4 THEN GOTO 4000
0880 IF V$<>"YES" THEN GOTO 0800
0900 REM " --- Create Purchase Req?
0910 CALL "SYC.YN",0,"Create Purchase Requisitions",0,V$,V3
0920 IF V$="NO" THEN GOTO 0960
0930 IF V3=4 THEN GOTO 4000
0940 IF V$<>"YES" THEN GOTO 0900
0950 LET CREATE$="Y"
0960 CALL "SYC.NB","Updating: ",VENDLEN+4,COLUMN
0970 REM " --- Disallow 'M'enu option in Error Routine
0980 LET EXIT_CTRL=1
1000 REM " --- Init Read
1010 READ (POE06_DEV,KEY=N0$,DOM=1100)
1100 REM " --- Read Suggested Order File
1110 LET K$=KEY(POE06_DEV,END=4000)
1120 IF POS(N0$=K$)<>1 THEN GOTO 4000
1130 READ (POE06_DEV)IOL=POE06A
1140 LET BUYER$=A0$(3,3),VENDOR$=A0$(6,6),REVIEWDATE$=A1$(3,3)
1150 PRINT @(COLUMN,11),BUYER$,"  ",FNF$(VENDOR$(1,VENDLEN),M0$)
1160 FIND (IVM10_DEV,KEY=N0$+"F"+BUYER$,DOM=1250)IOL=IVM10F
1170 LET W0$=N0$+VENDOR$+BUYER$
1180 DIM W1$(24),W[11]
1190 EXTRACT (APM06_DEV,KEY=W0$,DOM=1250)IOL=APM06A
1200 LET DAYS=BUYERCYCLEDAYS
1210 IF W[0]<>0 THEN LET DAYS=W[0]
1220 CALL "SYC.CA",REVIEWDATE$,NEXTREVIEWDATE$,DAYS
1230 LET W1$(1,3)=REVIEWDATE$,W1$(4,3)=NEXTREVIEWDATE$
1240 WRITE (APM06_DEV,KEY=W0$)IOL=APM06A
1250 IF CREATE$="Y" THEN GOSUB CREATE_REQ
1260 REMOVE (POE06_DEV,KEY=K$)
1270 GOTO 1100
4000 REM " --- Done
4010 GOTO 9900
6000 REM " --- Create Requisitions
6005 CREATE_REQ: 
6020 DIM B0$(36),B1$(20),B[16],PREVITEM$(20)
6030 READ (POE16_DEV,KEY=A0$(1,11),DOM=READPOE16)
6035 READPOE16: 
6040 LET K16$=KEY(POE16_DEV,END=INIT_POE26)
6050 IF K16$(1,11)<>A0$(1,11) THEN GOTO INIT_POE26
6060 READ (POE16_DEV)IOL=POE16A
6070 LET ITEMNUM$=B0$(15,20),STOCKLEVEL$=B1$(1,1)
6080 IF PREVITEM$<>ITEMNUM$ THEN GOSUB REMOVEPOE36
6090 IF STOCKLEVEL$="I" THEN GOTO NEXTPOE16
6100 DIM R2$(62),R[12],UNITMEAS$(2)
6110 LET CONVFACT=0
6120 READ (IVM01_DEV,KEY=N0$+ITEMNUM$,DOM=6140)IOL=IVM01A
6130 LET UNITMEAS$=R2$(6,2),CONVFACT=R[0]
6140 LET COST=B[10],WHSE$=B0$(35,2),SUGG=B[9]
6150 GOSUB CALLPOCBA
6160 NEXTPOE16: 
6170 REMOVE (POE16_DEV,KEY=K16$)
6190 GOTO READPOE16
6200 REM " --- Use POE-26 (Totals) to process items stocked by 'I'
6205 INIT_POE26: 
6210 DIM C0$(36),C1$(20),C[16]
6220 READ (POE26_DEV,KEY=A0$(1,11),DOM=READPOE26)
6230 READPOE26: 
6240 LET K26$=KEY(POE26_DEV,END=DONE_CREATE)
6250 IF POS(A0$(1,11)=K26$)<>1 THEN GOTO DONE_CREATE
6260 READ (POE26_DEV)IOL=POE26A
6270 LET ITEMNUM$=C0$(15,20),STOCKLEVEL$=C1$(1,1)
6280 IF STOCKLEVEL$="W" THEN GOTO NEXTPOE26
6290 DIM R2$(62),R[12],UNITMEAS$(2)
6300 LET CONVFACT=0
6310 READ (IVM01_DEV,KEY=N0$+ITEMNUM$,DOM=6330)IOL=IVM01A
6320 LET UNITMEAS$=R2$(6,2),CONVFACT=R[0]
6330 LET COST=C[10],WHSE$=DEFAULTWHSE$,SUGG=C[9]
6340 GOSUB CALLPOCBA
6360 NEXTPOE26: 
6370 REMOVE (POE26_DEV,KEY=K26$)
6390 GOTO READPOE26
6400 DONE_CREATE: 
6410 RETURN 
6600 REM " --- Assign and call poc.ba to create requisition
6610 CALLPOCBA: 
6620 IF SUGG<=0 THEN GOTO DONECALL
6630 DIM CHANS[11],INFO$[15],INFO[2]
6640 LET CHANS[1]=APM02_DEV,CHANS[2]=POE01_DEV,CHANS[3]=POE02_DEV
6650 LET CHANS[4]=POE11_DEV,CHANS[5]=POE52_DEV,CHANS[6]=POM02_DEV
6660 LET CHANS[7]=POS10_DEV,CHANS[8]=POE21_DEV,CHANS[9]=POE31_DEV
6670 LET CHANS[10]=POE41_DEV,CHANS[11]=SYS01_DEV
6680 LET INFO$[0]=N0$,INFO$[1]=VENDOR$,INFO$[2]="S",INFO$[3]=WHSE$
6690 LET INFO$[4]=DATEORD$,INFO$[5]=DATEREQ$,INFO$[6]=UNITMEAS$,INFO$[7]="R"
6700 LET INFO$[8]=FILL(3),INFO$[9]=FILL(10),INFO$[10]=FILL(6)
6710 LET INFO$[11]=FILL(10),INFO$[12]=FILL(10),INFO$[13]=ITEMNUM$
6720 LET INFO$[14]=FILL(40)
6730 LET INFO[0]=CONVFACT,INFO[1]=COST,INFO[2]=SUGG
6740 CALL "POC.BA",CHANS[ALL],INFO$[ALL],INFO[ALL],DETAILKEY$,STATUS
6750 DONECALL: 
6790 RETURN 
6800 REM " --- Remove 'surplus' records
6805 REMOVEPOE36: 
6820 READ (POE36_DEV,KEY=N0$+ITEMNUM$,DOM=READPOE36)
6830 READPOE36: 
6840 LET K36$=KEY(POE36_DEV,END=DONEPOE36)
6850 IF POS(N0$+ITEMNUM$=K36$)<>1 THEN GOTO DONEPOE36
6860 REMOVE (POE36_DEV,KEY=K36$)
6870 GOTO READPOE36
6880 DONEPOE36: 
6885 LET PREVITEM$=ITEMNUM$
6890 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

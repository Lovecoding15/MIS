0010 REM "TMS - Telemarketing (Invoice History)"
0020 REM "Program TMC.IA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "15Aug96 WGH: Reinitialize when Invoice History Print program not found (4040-4050,4330-4340)"
0032 REM "27Nov96 WGH: Reinitialize Invoice History Print variables for next customer (1050,1210,6240,9220)"
0033 REM "20Feb97 WGH: Use IV Price Mask for OP price fields (575-590,4050)"
0034 REM "25Mar97 WGH: Include Discount, Tax and Freight in Summary and Detail invoice total (2560,3920,3940-3945,3960)"
0035 REM "01May97 WGH: When printing an Invoice, the default Invoice number may be an Order number when displaying by Order number (1060,2140,4220)"
0040 REM 
0050 ENTER CUST$,N0$
0055 LET A0$=N0$+CUST$
0085 SETESC 9000
0090 SETERR 9000
0095 LET HISTORY$="Y"
0100 REM " --- Open/Lock Files
0110 LET ART03_DEV=UNT; OPEN (ART03_DEV)"ART-03"
0120 LET ART13_DEV=UNT; OPEN (ART13_DEV)"ART-13"
0130 LET ART23_DEV=UNT; OPEN (ART23_DEV)"ART-23"
0140 LET ART33_DEV=UNT; OPEN (ART33_DEV)"ART-33"
0150 LET HEADER_CHANNEL=ART03_DEV,DETAIL_CHANNEL=ART13_DEV,LOT_CHANNEL=ART23_DEV,SHIPTO_CHANNEL=ART33_DEV
0160 LET SYS01I=UNT; OPEN (SYS01I)"SYS-01"
0170 LET ARM10_DEV=UNT; OPEN (ARM10_DEV)"ARM-10"
0180 LET IVM01_DEV=UNT; OPEN (IVM01_DEV)"IVM-01"
0190 LET ARM06_DEV=UNT; OPEN (ARM06_DEV)"ARM-06"
0195 LET ART83_DEV=UNT; OPEN (ART83_DEV)"ART-83"
0200 REM " --- IOLists
0210 ARE03A: IOLIST X0$(1),X[ALL]
0220 ARE13A: IOLIST Z0$,Z1$(1),Z[ALL]
0230 ARE23A: IOLIST R0$,R1$,R2$,R0,R1,R2
0234 ARE33A: IOLIST Y0$,Y1$
0250 ARM10E: IOLIST Y$(1)
0260 IVM01A: IOLIST X$,U0$
0290 ART83A: IOLIST W0$,W1$,W2$
0320 IVS01A: IOLIST X$,X$,I2$,I3$,X$,X$,I6$,I7$,I8$,I9$
0410 DIM I[3],I3$(20)
0485 FIND (SYS01I,KEY=N0$+"IV00",DOM=0500)IOL=IVS01A
0490 LET I[0]=NUM(I2$(1,2)),I[2]=NUM(I2$(5,1)),I[3]=NUM(I2$(7,2)),S0$=I3$(17,1)
0500 REM " --- Init Data
0510 DIM X0$(117),X[10],Z0$(52),Z1$(64),Z[14],O[4],J0$(80)
0520 LET L9=20,L1=6,L=L1,T9$=""
0530 LET V9$="Invoice",V8$="Order"
0540 IF AR_A0$="" THEN LET AR_A0$=A0$
0545 IF AR_A1$="" THEN LET AR_A1$=A1$
0550 IF AR_M1$="" THEN LET AR_M1$=M1$
0560 IF AR_M2$="" THEN LET AR_M2$=M2$
0570 IF AR_M3$="" THEN LET AR_M3$=M3$
0575 IF AR_M4$="" THEN LET AR_M4$=M4$
0580 LET M1$=I6$,M2$=I7$,M3$=I8$,M4$=I9$
0590 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),M4=LEN(M4$)
0595 LET SER_N0$=""
0600 REM " --- Detail print positions
0610 LET MAX1=M1,MAX2=M2,MAX3=M3,X=MAX(MAX(I[0],10),I[3]),XW=7+X
0620 IF S0$<>"" THEN LET XW=XW+5
0630 WHILE M2*2+M1+M3+XW+8>80
0635 LET MAX1=MAX1-1,MAX2=MAX2-1,MAX3=MAX3-1
0640 CALL "SYC.RM",M1$,M1,8,MAX1
0645 CALL "SYC.RM",M2$,M2,8,MAX2
0650 CALL "SYC.RM",M3$,M3,8,MAX3
0655 WEND
0660 LET O[4]=80-M1,O[3]=O[4]-M3,O[2]=O[3]-M2,O[1]=O[2]-5,O[0]=O[1]-M2-1
0700 REM " --- Background
0710 PRINT @(0,6),'CE',
0720 IF HISTORY$<>"Y" THEN GOTO NO_HIST1
0730 PRINT 'SB',@(15,5),"Invoice History"
0740 LET W3$="Inv.",W4$="Order",W5$="Inv-Date"
0750 GOTO 0790
0760 NO_HIST1: 
0770 PRINT 'SB',@(15,5),"Open Orders"
0780 LET W3$="Order",W4$="Invoice",W5$="Ord-Date"
0790 PRINT 'SB',@(0,6),W3$,@(6),"Typ",@(10),W5$,@(19),"Shp-Date",@(28),"PO Number",@(39),"Ship-Via",@(50),"Slp",@(54),W4$,@(62+M1-6),"Total",@(21,17),"Enter Your Option:",'SF',
1000 REM " --- Summary or Detail
1010 REM PRINT @(0,17),'CF',
1015 GOSUB 5400; REM "Sales Buckets
1020 LET V0$="S",V1$="CERK^",V2$=P9$,V3$="SD",V4$="Summary or Detail (S/D)?",V0=1,V1=40,V2=17
1030 IF V2$="" THEN LET V2$="S"
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 4040
1060 LET P9$=V$,P8$="",HISTINV1$=""
1100 REM " --- Order/Invoice Number for Detail Listing
1110 PRINT @(0,18),'CL','SB'
1120 LET V0$="Z",V1$="KC",V2$=P8$,V0=7,V1=40,V2=18
1130 IF HISTORY$<>"Y" THEN GOTO NO_HIST2
1140 IF V9$="Invoice" THEN PRINT @(14,18),"Beginning Invoice Number:", ELSE PRINT @(17,18),"Selected Order Number: ",
1150 LET V4$="Enter Valid "+V9$(POS(" "<V9$))+" Number, <Enter>=All "+V9$(POS(" "<V9$))+"s"
1160 GOTO 1200
1170 NO_HIST2: 
1180 PRINT @(16,18),"Beginning Order Number:"
1190 LET V4$="Enter A Valid Order Number, <Enter>=All Orders"
1200 GOSUB 7000
1210 IF V3=4 THEN GOTO 4040
1220 IF V3<>1 OR HISTORY$<>"Y" THEN GOTO 1250
1230 IF V9$="Invoice" THEN LET V9$="  Order",V8$="Invoice" ELSE LET V9$="Invoice",V8$="Order"
1240 GOTO 1110
1250 IF V3=2 THEN PRINT @(0,18),'CL',; GOTO 1000
1260 IF V3=3 THEN LET V$=""
1270 IF V$<>J0$(1,V0) THEN GOTO 1280
1273 LET V$=""
1275 IF HISTORY$<>"Y" OR V9$="  Order" THEN PRINT @(40,10),"All Orders",'CL', ELSE PRINT @(40,10),"All Invoices",'CL',
1280 LET P8$=V$
2000 REM " --- Init Header File
2010 LET X0$(1)=N0$+"  "+A0$(3,6)
2020 IF V9$="  Order" THEN READ (HEADER_CHANNEL,KEY=X0$(1,10),DOM=2040)
2030 READ (HEADER_CHANNEL,KEY=X0$(1,10)+P8$,DOM=2040)
2090 GOSUB 6000
2100 REM " --- Header Read
2110 READ (HEADER_CHANNEL,END=4000)IOL=ARE03A
2120 IF POS(N0$+"  "+A0$(3,6)=X0$)<>1 THEN GOTO 4000
2130 IF V9$="  Order" AND P8$<>FILL(LEN(P8$)) AND X0$(42,7)<>P8$ THEN GOTO 2100
2140 IF HISTORY$="Y" AND P8$<>"" AND HISTINV1$="" THEN LET HISTINV1$=X0$(11,7)
2300 REM " --- Type
2310 LET W0$=X0$(22,1)
2320 IF W0$<>"O" THEN GOTO 2390
2330 IF X0$(21,1)="P" THEN LET W0$="Q"
2340 IF X0$(101,1)="C" THEN LET W0$="C"
2350 IF X0$(101,1)="R" THEN LET W0$="R"
2360 IF X0$(23,1)="B" THEN LET W0$="B"
2390 IF X0$(21,1)="V" THEN LET W0$="V"
2500 REM " --- Print Header
2520 GOSUB 5200
2540 IF P9$="D" THEN GOTO 2900
2560 PRINT @(62,L),X[6]+X[0]+X[1]-X[2]:M1$,
2580 LET L=L+1
2700 GOTO 2100
2900 REM " --- Init Detail File
2910 LET L2=0,L3=0,L4=0
2920 READ (DETAIL_CHANNEL,KEY=X0$(1,17),DOM=3000)
3000 REM " --- Get Detail
3040 LET K$=KEY(DETAIL_CHANNEL,END=3900)
3060 IF POS(X0$(1,17)=K$)<>1 THEN GOTO 3900
3080 READ (DETAIL_CHANNEL)IOL=ARE13A
3100 REM " --- Item Type
3110 DIM Y$(30)
3120 FIND (ARM10_DEV,KEY=N0$+"E"+Z0$(21,1),DOM=3800)IOL=ARM10E
3140 LET U0$=Z1$(1,40)
3200 REM " --- Item Description
3210 IF POS(Y$(25,1)="SP")=0 THEN GOTO 3500
3220 LET U0$=""
3240 FIND (IVM01_DEV,KEY=N0$+Z0$(33),DOM=3500)IOL=IVM01A
3500 REM " --- Print
3520 IF L+3>L9 THEN GOSUB 6200
3530 IF L2=0 THEN GOSUB 5000
3540 LET L=L+1
3550 PRINT @(2,L),Z0$(18,3),@(XW),Z0$(31,2),
3570 IF POS(Y$(25,1)="O")<>0 THEN PRINT @(O[4]),Z[6]:M1$,
3580 IF POS(Y$(25,1)=" SPN")=0 THEN GOTO 3630
3610 IF Z1$(44,1)<>"Y" THEN LET Z1$(44,1)="N"
3620 PRINT @(6,L),Z0$(33,15),@(O[0]),Z[2]:M2$,@(O[1]),Z1$(44,1),@(O[2]),Z[4]:M2$,@(O[3]),Z[1]:M3$,@(O[4]),Z[6]:M1$,
3630 LET L=L+1
3640 PRINT @(8,L),U0$,
3650 IF POS(Y$(25,1)="MO")>0 THEN GOTO 3690
3660 IF Z1$(41,3)<>"   " AND Z1$(41,3)<>X0$(27,3) AND HISTORY$<>"Y" THEN PRINT 'SB',@(62,L),"Ship: ",'SF',FNB$(Z1$(41))
3690 LET L3=L3+1
3700 REM " --- Print Serial/Lotted History
3710 REM IF SER_N0$<>"Y" THEN GOTO 3800
3720 READ (ART83_DEV,KEY=K$(1,20),DOM=3730)
3730 LET R0$=KEY(ART83_DEV,END=3800)
3740 IF R0$(1,20)<>K$(1,20) THEN GOTO 3800
3750 READ (ART83_DEV,KEY=R0$)IOL=ART83A
3755 IF L+1>L9 THEN GOSUB 6200
3757 IF L4=0 THEN GOSUB 5100
3760 LET L=L+1
3770 PRINT @(12,L),W2$,@(39),W1$(1,1)
3780 GOTO 3730
3800 REM " --- Get Next Detail Record
3890 GOTO 3000
3900 REM " --- Print Totals
3910 LET L=L+1,X$="Invoice"
3915 IF HISTORY$<>"Y" THEN LET X$="  Order"
3920 IF L+ABS(SGN(X[0]))+ABS(SGN(X[1]))+ABS(SGN(X[2]))>L9 THEN GOSUB 6200
3940 IF X[2] THEN PRINT @(O[4]-10,L),'SB',"Discount: ",'SF',-X[2]:M1$; LET L=L+1
3945 IF X[0] THEN PRINT @(O[4]-5,L),'SB',"Tax: ",'SF',X[0]:M1$; LET L=L+1
3950 IF X[1] THEN PRINT @(O[4]-9,L),'SB',"Freight: ",'SF',X[1]:M1$; LET L=L+1
3960 PRINT @(O[4]-15,L),'SB',X$," Total: ",'SF',X[6]+X[0]+X[1]-X[2]:M1$
3980 LET L=L+1
3990 GOTO 2100
4000 REM " --- End
4010 LET T9$="E"
4020 GOSUB 6200
4040 LET A0$=AR_A0$,A1$=AR_A1$,AR_A0$="",AR_A1$=""
4050 LET M1$=AR_M1$,M2$=AR_M2$,M3$=AR_M3$,M4$=AR_M4$,AR_M1$="",AR_M2$="",AR_M3$="",AR_M4$=""
4090 GOTO 9900
5000 REM " --- Print Heading For Detail
5002 IF L3>0 THEN GOSUB 5200
5010 LET L=L+1
5015 IF S0$="S" THEN LET S$="/Ser#" ELSE IF S0$="L" THEN LET S$="/Lot#" ELSE LET S$=""
5020 PRINT 'SB',@(2,L),"Seq",@(6),"Item/Desc",S$,@(XW),"Wh",@(O[0]+M2-6),"Order",@(O[1]-1),"Com?",@(O[2]+M2-5),"Ship",@(O[3]+M3-6),"Price",@(O[4]+M1-10),"Extension",'SF',
5040 LET L2=L2+1
5090 RETURN 
5100 REM " --- Print Heading For Detail
5110 LET L=L+1
5120 PRINT 'SB',@(12,L),"Serial Number",@(39),"Action",'SF',
5140 LET L4=L4+1
5190 RETURN 
5200 REM " --- Print Header
5210 IF P9$="D" THEN IF L+3>L9 THEN GOSUB 6200
5220 IF L>L9 THEN GOSUB 6200
5230 LET PDATE$=FNB$(X0$(24,3))
5235 IF HISTORY$<>"Y" THEN LET PDATE$=FNB$(X0$(77,3))
5240 PRINT @(0,L),X0$(11,7),@(8),W0$,@(10),PDATE$,@(19),FNB$(X0$(27,3)),@(28),X0$(49,10),@(39),X0$(32,10),@(50),X0$(59,3),@(54),X0$(42,7),
5290 RETURN 
5400 REM "Sales Buckets
5405 DIM D[21]
5410 FIND (ARM06_DEV,KEY=N0$+CUST$,DOM=5490)*,*,D[ALL]
5420 PRINT @(19,10),'SB',"Sales Month-To-Date:",@(26,11),"Year-To-Date:",@(25,12),"Previous Year:",@(28,13),"Next Month:",'SF',
5430 PRINT @(40,10),D[0]:M1$,@(40,11),D[1]:M1$,@(40,12),D[2]:M1$,@(40,13),D[8]:M1$
5490 RETURN 
6000 REM " --- Clear Screen
6020 PRINT @(0,L1),'CE',
6040 LET L=L1,L2=0,L4=0
6090 RETURN 
6200 REM " --- Overflow"
6205 LET V0$="S",V1$="CE^",V2$="",V3$="",V4$="Enter R To Restart (<Enter>=Continue)",V0=1,V2=22
6210 IF HISTORY$="Y" THEN LET V4$="Enter R To Restart, <S>=Show Serial Numbers, (<Enter>=Continue)"
6215 LET V1=FNV(V4$)
6220 GOSUB 7000
6230 IF V$="R" THEN EXITTO 0200
6240 IF V3=4 THEN EXITTO 4040
6250 IF V$="S" THEN LET SER_N0$="Y",P9$="D"; EXITTO 2000
6260 IF T9$="" THEN GOSUB 6000
6270 LET L=L1
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 4040
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 CLOSE (ART03_DEV,ERR=9901)
9910 CLOSE (ART13_DEV,ERR=9911)
9920 CLOSE (ART23_DEV,ERR=9921)
9930 CLOSE (ART33_DEV,ERR=9921)
9940 CLOSE (SYS01I,ERR=9941)
9950 CLOSE (ARM10_DEV,ERR=9951)
9960 CLOSE (IVM01_DEV,ERR=9961)
9970 CLOSE (ARM06_DEV,ERR=9971)
9999 EXIT 

0010 REM "APX - Convert AP to 6.0"
0020 REM "Program APX60A"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0140 LET FILES$[1]="SYS-01",FILES$[2]="APE-03",FILES$[3]="APE-13"
0150 LET FILES$[4]="APM-01",FILES$[5]="APS-10",FILES$[6]="APT-01"
0155 LET FILES$[7]="APT-11"
0170 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0175 IF STATUS>0 THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1],APE03_DEV=CHANNELS[2],APE13_DEV=CHANNELS[3]
0185 LET APM01_DEV=CHANNELS[4],APS10_DEV=CHANNELS[5],APT01_DEV=CHANNELS[6]
0190 LET APT11_DEV=CHANNELS[7]
0200 REM " --- IOLISTs
0210 APT01AO: IOLIST A0$,A1$,A[0],A[1],A[2],A[3],A[4],A[5]
0220 APT01AN: IOLIST B0$,B[0],B[1],B[2]
0230 APT01B: IOLIST A0$,A1$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8]
0250 APT11A: IOLIST W0$,W1$,W[0],W[1],W[2]
0260 APS10N: IOLIST B0$,B
0310 APM01A: IOLIST A0$,A1$
0370 SYS01N: IOLIST X$,X[ALL]
0380 SYS01T: IOLIST X$,F0$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N3$=F4$
0500 REM " --- Init Data
0505 PRECISION 14
0510 DIM A0$(71),B1$(11),A[8],B[2],W[2],BLANKS$(25)
0530 LET M1$="##,##0"
0800 REM " --- Display Warning"
0810 CALL "SYC60A",N2$,SYS01_DEV,STATUS
0820 IF STATUS THEN GOTO 9900
0890 GOSUB 6800
0950 REM " --- Initialize for APE-01 Conversion
0980 CALL "SYC.NB","Processing",25,COLUMN
1400 REM " --- Next Automatic Vendor Number"
1405 PRINT @(40,3),"APS-10 Next Vendor Number",'CL',@(COLUMN,11),BLANKS$,
1410 FOR I=1 TO 99
1420 DIM X[2]
1430 LET N0$=STR(I:"00"),B0$=N0$+"N"
1440 FIND (SYS01_DEV,KEY=N0$+"SY99",DOM=1490)IOL=SYS01N
1445 PRINT @(COLUMN,11),N0$,
1450 FIND (APS10_DEV,KEY=B0$,DOM=1460)IOL=APS10N
1460 LET B=X[0],X[0]=0
1470 WRITE (SYS01_DEV,KEY=N0$+"SY99")IOL=SYS01N
1480 WRITE (APS10_DEV,KEY=B0$)IOL=APS10N
1490 NEXT I
1500 REM " --- Initialize for APE-03 Conversion
1510 LET LAST$=""
1520 PRINT @(40,3),"APE-03 Recurring Invoices",@(COLUMN,11),BLANKS$,
1600 REM " --- Convert APE-03
1610 LET K$=KEY(APE03_DEV,END=1800)
1615 IF POS("Z"=K$)=1 THEN REMOVE (APE03_DEV,KEY=K$); GOTO 1800
1620 READ RECORD(APE03_DEV)A0$
1625 IF LEN(K$)<22 OR NUM(K$(21,2),ERR=1680)=0 THEN GOTO 1680
1630 IF K$(1,10)=LAST$ THEN GOTO 1655
1640 LET LAST$=K$(1,10)
1645 PRINT @(COLUMN,11),K$(1,2)," ",K$(3,2)," ",K$(5),
1655 LET A0$=A0$(1,POS($00$<>A0$,-1))
1660 WRITE RECORD(APE13_DEV,KEY=K$)A0$
1680 REMOVE (APE03_DEV,KEY=K$)
1690 GOTO 1600
1800 REM " --- Rename APR-01 to APE-04
1810 PRINT @(40,3),"APR-01 Check Printing",'CL',@(COLUMN,11),BLANKS$,
1820 LET NUMBER=1
1830 DIM SOURCE$[NUMBER],TARGET$[NUMBER],FLAG$[NUMBER]
1840 LET SOURCE$[1]="APR-01",TARGET$[1]="APE-04"
1850 CALL "SYC.RN",SOURCE$[ALL],TARGET$[ALL],FLAG$[ALL],NUMBER,STATUS
1860 IF STATUS<>0 THEN GOTO 9900
1900 REM " --- Rename APR-02 to APE-05
1910 PRINT @(40,3),"APR-02 Open Checks",'CL',@(COLUMN,11),BLANKS$,
1920 LET NUMBER=1
1930 DIM SOURCE$[NUMBER],TARGET$[NUMBER],FLAG$[NUMBER]
1940 LET SOURCE$[1]="APR-02",TARGET$[1]="APE-05"
1950 CALL "SYC.RN",SOURCE$[ALL],TARGET$[ALL],FLAG$[ALL],NUMBER,STATUS
1960 IF STATUS<>0 THEN GOTO 9900
2000 REM " --- Initialize for APT-01 Conversion
2010 LET LAST$=""
2020 PRINT @(40,3),"APT-01 Open Invoices",'CL',@(COLUMN,11),BLANKS$,
2100 REM " --- Convert APT-01"
2110 LET K$=KEY(APT01_DEV,END=2900)
2120 IF POS("Z"=K$)=1 THEN REMOVE (APT01_DEV,KEY=K$); GOTO 2900
2125 IF LEN(K$)<22 THEN GOTO 2480
2130 IF NUM(K$(21,2))>0 THEN GOTO 2300
2140 READ (APT01_DEV)IOL=APT01AO
2145 IF K$(1,10)=LAST$ THEN GOTO 2160
2150 LET LAST$=K$(1,10)
2155 PRINT @(COLUMN,11),K$(1,2)," ",K$(3,2)," ",K$(5),
2160 LET B0$=A0$,B[0]=A[0],B[1]=A[1],B[2]=A[2]
2165 WRITE (APT01_DEV,KEY=K$)IOL=APT01AN
2200 IF A[3]=0 AND A[4]=0 AND A[5]=0 THEN GOTO 2100
2205 LET W0$=A0$(1,20),W1$=A1$,W[0]=A[3],W[1]=A[4],W[2]=A[5]
2210 READ (APT11_DEV,KEY=W0$+$FF$,DOM=2211)
2220 LET K0$=KEYP(APT11_DEV,ERR=2221)
2230 IF POS(W0$(1,20)=K0$)<>1 THEN LET W0$=W0$+"01"; GOTO 2280
2240 LET SEQ_NO=NUM(K0$(21,2))+1
2250 IF SEQ_NO<100 THEN LET W0$=W0$(1,20)+STR(SEQ_NO:"00"); GOTO 2280
2260 READ (APT11_DEV,KEY=W0$+"99")IOL=APT11A
2270 LET W[0]=W[0]+A[3],W[1]=W[1]+A[4],W[2]=W[2]+A[5]
2280 WRITE (APT11_DEV,KEY=W0$)IOL=APT11A
2290 GOTO 2100
2300 REM " --- Put Detail Records into APT-11"
2310 READ (APT01_DEV)IOL=APT01B
2320 LET COUNT=COUNT+1
2330 PRINT @(COLUMN+7,11),COUNT:M1$,
2340 LET W0$=A0$(1,20)
2350 FOR X=0 TO 2
2360 IF A[X*3]=0 AND A[X*3+1]=0 AND A[X*3+2]=0 THEN GOTO 2470
2370 LET W1$=A1$(X*11+1,11)
2380 LET W[0]=A[X*3],W[1]=A[X*3+1],W[2]=A[X*3+2]
2390 READ (APT11_DEV,KEY=W0$+$FF$,DOM=2391)
2400 LET K0$=KEYP(APT11_DEV,ERR=2410)
2410 IF POS(W0$(1,20)=K0$)<>1 THEN LET W0$=W0$+"01"; GOTO 2460
2420 LET SEQ_NO=NUM(K0$(21,2))+1
2430 IF SEQ_NO<100 THEN LET W0$=W0$(1,20)+STR(SEQ_NO:"00"); GOTO 2460
2440 READ (APT11_DEV,KEY=W0$+"99")IOL=APT11A
2450 LET W[0]=W[0]+A[3],W[1]=W[1]+A[4],W[2]=W[2]+A[5]
2460 WRITE (APT11_DEV,KEY=W0$)IOL=APT11A
2470 NEXT X
2480 REMOVE (APT01_DEV,KEY=K$)
2490 GOTO 2100
3200 REM " --- Rename Bank Rec files to AP file names
3210 LET FILES=3
3220 DIM SOURCE$[FILES],TARGET$[FILES],ERASE$[FILES]
3230 LET SOURCE$[1]="BRM-01",TARGET$[1]="APM-11",ERASE$[1]="Y"
3240 LET SOURCE$[2]="BRM-02",TARGET$[2]="APM-12",ERASE$[1]="Y"
3250 LET SOURCE$[3]="BRM-03",TARGET$[3]="APM-13",ERASE$[1]="Y"
3260 CALL "SYC.RN",SOURCE$[ALL],TARGET$[ALL],ERASE$[ALL],FILES,STATUS
4000 REM " --- End Of Conversion
4090 GOTO 9900
6800 REM " --- Erase old xref files"
6820 ERASE "APW-03",ERR=6890
6825 GOTO 6820
6890 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

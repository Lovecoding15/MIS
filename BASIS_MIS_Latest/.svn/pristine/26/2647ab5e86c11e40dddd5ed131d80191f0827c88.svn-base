0010 REM "SYX - Generate Executive Summary (Consolidation)"
0020 REM "Program SYX.EI"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 SYE02A: IOLIST A0$(1),A1$(1),A[ALL],U[ALL]
0220 SYE02B: IOLIST B0$(1),B1$(1),B[ALL],V[ALL]
0230 SYE04A: IOLIST A0$(1),A1$(1),A[ALL],U[ALL],L[ALL]
0240 SYE04B: IOLIST B0$(1),B1$(1),B[ALL],V[ALL],M[ALL]
0250 SYE09A: IOLIST A0$(1),A1$(1),A[ALL]
0260 SYE09B: IOLIST B0$(1),B1$(1),B[ALL]
0270 SYE10A: IOLIST A0$(1),A1$(1),A[0]
0280 SYE10B: IOLIST B0$(1),B1$(1),B[0]
0290 SYM06A: IOLIST N0$,N1$(1)
0400 REM " --- Parameters"
0410 LET TITLE$="Consolidation",PGM$="SYS.AA",N9$="99",N0$=N9$
0420 FIND (SYM06_DEV,KEY=N0$,DOM=4000)IOL=SYM06A
0500 REM " --- Initialize"
0510 DIM A0$(8),A1$(32),A[31],U[31],L[31],CHANNELS[10]
0520 DIM B0$(8),B1$(32),B[31],V[31],M[31],C0$(4),C1$(32)
0550 LET CHANNELS[2]=SYE02_DEV,CHANNELS[3]=SYE03_DEV,CHANNELS[4]=SYE04_DEV
0560 LET CHANNELS[5]=SYE05_DEV,CHANNELS[6]=SYE06_DEV,CHANNELS[7]=SYE07_DEV
0570 LET CHANNELS[8]=SYE08_DEV,CHANNELS[9]=SYE09_DEV,CHANNELS[10]=SYE10_DEV
0700 REM " --- Background"
0710 PRINT @(40,5),TITLE$,'CL',@(40,7),N0$," ",N1$,@(COLUMN,11),FILL(24),
1000 REM " --- For each SYE-xx file"
1010 FOR FILE=2 TO 10
1020 LET CHANNEL=CHANNELS[FILE]
1900 REM " --- Position file"
1990 READ (CHANNEL,KEY="",DOM=2000)
2000 REM " --- Read next record"
2010 LET K$=KEY(CHANNEL,END=3000)
2020 IF POS(N9$=K$)=1 THEN GOTO 3000
2030 PRINT @(COLUMN,11),K$(1,2)," ",K$(7,2),"/",K$(3,4),
2100 REM " --- Record initializations"
2110 DIM A0$(8),A1$(32),A[31],U[31],L[31]
2120 DIM B0$(8),B1$(32),B[31],V[31],M[31]
2130 LET B0$(1)=N9$+K$(3)
2190 ON FILE GOTO 3000,3000,2200,2200,2300,2300,2200,2200,2200,2400,2500
2200 REM " --- SYE-02/03/06/07/08 (AR, AP, PO, Sales, Costs)"
2210 READ (CHANNEL)IOL=SYE02A
2220 FIND (CHANNEL,KEY=B0$,DOM=2230)IOL=SYE02B
2230 GOSUB 6000
2240 WRITE (CHANNEL,KEY=B0$)IOL=SYE02B
2290 GOTO 2900
2300 REM " --- SYE-04/05 (Open Orders, Order Costs)"
2310 READ (CHANNEL)IOL=SYE04A
2320 FIND (CHANNEL,KEY=B0$,DOM=2330)IOL=SYE04A
2330 GOSUB 6000
2340 WRITE (CHANNEL,KEY=B0$)IOL=SYE04B
2390 GOTO 2900
2400 REM " --- SYE-09 (IV)
2410 READ (CHANNEL)IOL=SYE09A
2420 FIND (CHANNEL,KEY=B0$,DOM=2430)IOL=SYE09A
2430 GOSUB 6000
2440 WRITE (CHANNEL,KEY=B0$)IOL=SYE09B
2490 GOTO 2900
2500 REM " --- SYE-10 (WIP)
2510 READ (CHANNEL)IOL=SYE10A
2520 FIND (CHANNEL,KEY=B0$,DOM=2530)IOL=SYE10A
2530 GOSUB 6000
2540 WRITE (CHANNEL,KEY=B0$)IOL=SYE10B
2900 REM " --- Reposition file and loop back for next record"
2910 READ (CHANNEL,KEY=K$)
2990 GOTO 2000
3900 REM " --- Next file"
3990 NEXT FILE
4000 REM " --- Run next overlay"
4090 RUN PGM$,ERR=9900
6000 REM " --- Update SYE-xx Totals"
6010 LET B0$(1)=N9$+A0$(3)
6020 FOR X=0 TO 31
6030 LET B[X]=B[X]+A[X],V[X]=V[X]+U[X],M[X]=M[X]+L[X]
6040 NEXT X
6090 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

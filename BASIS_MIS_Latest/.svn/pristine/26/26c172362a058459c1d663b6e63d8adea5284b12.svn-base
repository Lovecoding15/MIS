0010 REM "POS - Update Purchase Requisition File"
0020 REM "Program POC.BA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0041 REM "CHANS[1]    : APM-02 channel number
0042 REM "CHANS[2]    : POE-01 channel number
0043 REM "CHANS[3]    : POE-02 channel number
0044 REM "CHANS[4]    : POE-11 channel number
0045 REM "CHANS[5]    : POE-52 channel number
0046 REM "CHANS[6]    : POM-02 channel number
0047 REM "CHANS[7]    : POS-10 channel number
0048 REM "CHANS[8]    : POE-21 channel number
0049 REM "CHANS[9]    : POE-31 channel number
0050 REM "CHANS[10]   : POE-41 channel number
0051 REM "CHANS[11]   : SYS-01 channel number
0053 REM "
0054 REM "INFO$[0]   : Firm ID
0055 REM "INFO$[1]   : Vendor #
0056 REM "INFO$[2]   : Line Type (S=Stock/N=Non-stock/M=Message)
0057 REM "INFO$[3]   : Warehouse ID
0058 REM "INFO$[4]   : Date Ordered (YYMMDD)
0059 REM "INFO$[5]   : Date Required (YYMMDD)
0061 REM "INFO$[6]   : Unit of Measure
0062 REM "INFO$[7]   : Source (W=Work Orders/M=MRP/R=Replenishment)
0063 REM "INFO$[8]   : MRP Forecast Type
0064 REM "INFO$[9]   : Work Order Number (and optional WO seq number)
0065 REM "INFO$[10]  : Customer Number
0066 REM "INFO$[11]  : Sales Order Number (and optional SO seq number)
0067 REM "INFO$[12]  : Customer Ship-To Number
0068 REM "INFO$[13]  : Item Number (Type S)
0069 REM "INFO$[14]  : Memo/Non-stock Description
0070 REM "
0071 REM "INFO[0]    : Conversion factor (if 0, defaults to 1)
0072 REM "INFO[1]    : Unit Cost
0073 REM "INFO[2]    : Requisition Qty
0074 REM "
0075 REM "DETAILKEY$ : Returned, First string of detail record
0076 REM "STATUS     : Returned, error if > 0"
0080 SETESC 9000
0085 SETERR 9000
0090 ENTER CHANS[ALL],INFO$[ALL],INFO[ALL],DETAILKEY$,STATUS
0200 REM " --- IOLists
0210 APM02A: IOLIST C0$,C1$,C2$,C[ALL]
0220 POE01A: IOLIST A0$,A1$,A2$,A3$,A[ALL]
0230 POE11A: IOLIST B0$,B1$,B2$,B3$,B4$,B5$,B6$,B[ALL]
0240 POE21A: IOLIST X2$(1)
0250 POE31A: IOLIST XREF$
0260 POE41A: IOLIST XREF$
0270 POM02A: IOLIST *,X1$(1)
0280 POS10N: IOLIST D0$,D[ALL]
0300 POS01A: IOLIST *,*,*,DEFAULTS$(1),*
0310 IVS01A: IOLIST *,*,I2$
0500 REM " --- Initializations
0510 DIM A[8],B[12],D[3],B0$(18),X2$(15)
0520 DIM REQNUM$(7),DETAILKEY$(18)
0530 DIM PURCHADDRCODE$(2),DATEPROM$(6),NOTB4DATE$(6),ACK$(20),WHSELOC$(10)
0540 DIM FIRM$(2),LINETYPE$(1),VENDOR$(6),WHSE$(2),DATEORD$(6)
0550 DIM DATEREQ$(6),TERMSCODE$(2),UNITMEAS$(2)
0560 DIM SOURCE$(1),FORCASTTYPE$(3),WONUM$(7)
0570 DIM WONUM_SEQ$(10),CUSTNUM$(6),SONUM_SEQ$(10)
0580 DIM SHIPTONUM$(6),ITEMNUM$(20),MEMODESC$(60)
0600 REM " --- Assign input params
0610 LET APM02_DEV=CHANS[1],POE01_DEV=CHANS[2],POE02_DEV=CHANS[3]
0620 LET POE11_DEV=CHANS[4],POE52_DEV=CHANS[5],POM02_DEV=CHANS[6]
0630 LET POS10_DEV=CHANS[7],POE21_DEV=CHANS[8],POE31_DEV=CHANS[9]
0635 LET POE41_DEV=CHANS[10],SYS01_DEV=CHANS[11]
0640 LET FIRM$(1)=INFO$[0],VENDOR$(1)=INFO$[1],LINETYPE$(1)=INFO$[2]
0650 LET WHSE$(1)=INFO$[3],DATEORD$(1)=INFO$[4],DATEREQ$(1)=INFO$[5]
0660 LET UNITMEAS$(1)=INFO$[6],SOURCE$(1)=INFO$[7]
0670 LET FORCASTTYPE$(1)=INFO$[8],WONUM_SEQ$(1)=INFO$[9],CUSTNUM$(1)=INFO$[10]
0680 LET SONUM_SEQ$(1)=INFO$[11],SHIPTONUM$(1)=INFO$[12],ITEMNUM$(1)=INFO$[13]
0690 LET MEMODESC$(1)=INFO$[14]
0800 REM " --- Defaults
0810 DIM DEFAULTS$(68),D[3]
0820 FIND (SYS01_DEV,KEY=FIRM$+"PO00",DOM=4100)IOL=POS01A
0830 FIND (POS10_DEV,KEY=FIRM$+"N",DOM=0840)IOL=POS10N
0840 IF D[3]=0 THEN LET D[3]=10
0850 LET VSHIPVIA$=DEFAULTS$(3,15),FREIGHTTERMS$=DEFAULTS$(18,15)
0860 LET FOB$=DEFAULTS$(33,15),HOLD$=DEFAULTS$(48,1)
0870 LET REQMSG$=DEFAULTS$(49,3)
0880 GOSUB LINECODES
0900 REM " --- Establish correct precision"
0910 FIND (SYS01_DEV,KEY=FIRM$+"IV00",DOM=4100)IOL=IVS01A
0920 LET PLACES=NUM(I2$(5,1))
1000 REM " --- Check POE-01 for existing requisition for this vendor
1010 PRECISION PLACES
1020 READ (POE01_DEV,KEY=FIRM$+VENDOR$,DOM=1030)
1030 LET K01$=KEY(POE01_DEV,END=1050)
1040 IF K01$(1,8)=FIRM$+VENDOR$ THEN GOSUB FINDSPOT; GOTO 1100
1050 GOSUB GETTERMS
1060 GOSUB BUILDHEADER
1100 REM " --- Build and write detail record
1110 DIM B1$(48),B2$(32),B3$(22),B4$(40),B5$(1),B6$(1),B[12]
1140 LET B1$(1)=LINECODE$+DATEREQ$+DATEPROM$+NOTB4DATE$+LINE4LEAD$+UNITMEAS$+WHSELOC$+SOURCE$+FORCASTTYPE$
1150 LET B2$(1)=WONUM_SEQ$+CUSTNUM$+SONUM_SEQ$+SHIPTONUM$
1160 LET B3$(1)=WHSE$
1170 IF LINETYPE$="S" THEN LET B3$(1)=B3$(1,2)+ITEMNUM$ ELSE LET B4$(1)=MEMODESC$
1180 IF LINETYPE$="M" THEN GOTO 1230
1190 FOR CNT=0 TO 2
1200 LET B[CNT]=INFO[CNT]
1210 NEXT CNT
1220 IF LINETYPE$="N" THEN LET B[0]=0; REM "No conversion factor for non stock
1230 WRITE (POE11_DEV,KEY=B0$)IOL=POE11A
1240 LET XREF$=B0$(1,2)+B3$(3,20)+B0$(9,10)+B0$(3,6)
1250 WRITE (POE41_DEV,KEY=XREF$)IOL=POE41A
1260 IF LINETYPE$<>"M" OR LEN(CVS(MEMODESC$,2))<=40 THEN GOTO 4000
1270 LET B4$(1)=MEMODESC$(41),MEMODESC$=""
1280 GOSUB FINDSPOT
1290 GOTO 1230
4000 REM " --- Done - Successful
4010 REM " --- Update print file
4020 LET X2$(1)=B0$(1,15)
4030 WRITE (POE21_DEV,KEY=X2$)IOL=POE21A
4090 GOTO 9900
4100 REM " --- Done - Unsuccessful
4110 IF STATUS=0 THEN LET STATUS=1
4190 GOTO 9900
6000 REM " --- At least 1 req exists for vendor, so find spot for new req line
6001 REM "     (Add to 1st req if possible, next if not, ... etc)
6005 FINDSPOT: 
6010 READ (POE11_DEV,KEY=K01$+$FF$,DOM=6020)
6020 LET K11$=KEYP(POE11_DEV,END=WONTFIT)
6030 IF LOOPING THEN GOTO 6050
6040 IF K11$(1,15)<>K01$ THEN LET B0$=K01$+STR(D[3]:"000"); GOTO 6190; REM "Orphan hdr
6045 GOTO 6060
6050 IF K11$(1,15)<>K01$ THEN GOTO GETNEXTHEAD
6060 LET SEQ=NUM(K11$(16,3))+D[3]
6070 IF SEQ>99 THEN GOTO GETNEXTHEAD
6080 LET B0$=K11$(1,15)+STR(SEQ:"000")
6090 GOTO 6190
6100 REM " --- Get next req from header file
6110 GETNEXTHEAD: 
6120 READ (POE01_DEV,END=WONTFIT)
6130 LET K01$=KEY(POE01_DEV)
6140 IF K01$(1,8)=FIRM$+VENDOR$ THEN LET LOOPING=1; GOTO FINDSPOT ELSE GOTO WONTFIT
6150 REM " --- couldn't find room in existing reqs
6160 WONTFIT: 
6170 GOSUB BUILDHEADER
6180 GOTO 6190
6190 LET LOOPING=0
6195 RETURN 
6200 REM " --- Get Terms code from APM-02
6205 GETTERMS: 
6210 DIM C1$(24),C[13]
6220 READ (APM02_DEV,KEY=FIRM$+VENDOR$,DOM=6230)
6230 LET APM02_KEY$=KEY(APM02_DEV,END=6260)
6240 IF POS(FIRM$+VENDOR$=APM02_KEY$)<>1 THEN GOTO 6260
6250 READ (APM02_DEV,KEY=APM02_KEY$)IOL=APM02A
6270 LET TERMSCODE$=C1$(5,2)
6290 RETURN 
6300 REM " --- Build header record and write it to POE-01
6305 BUILDHEADER: 
6310 DIM A0$(15),A1$(160),A2$(1),A3$(1),A[8],D[3]
6315 REM " --- get 'next req #'; increment and write back
6320 EXTRACT (POS10_DEV,KEY=FIRM$+"N",DOM=6330)IOL=POS10N
6330 IF D[1]=0 THEN LET D[1]=1
6340 LET REQNUM=D[1],D[1]=D[1]+1
6350 READ (POE52_DEV,KEY=FIRM$+$FF$,DOM=6360)
6360 LET K52$=KEYP(POE52_DEV,END=6380)
6370 IF K52$(3,7)>STR(REQNUM:"0000000") THEN LET REQNUM=NUM(K52$(3,7)),D[1]=REQNUM+1
6380 IF D[1]>9999999 THEN LET D[1]=1
6390 READ (POE52_DEV,KEY=FIRM$+STR(REQNUM:"0000000"),DOM=6400)
6400 LET K52$=KEY(POE52_DEV,END=6460)
6410 IF K52$(3,7)<>STR(REQNUM:"0000000") THEN GOTO 6440
6420 FIND (POE01_DEV,KEY=FIRM$+K52$(10,6)+K52$(3,7),DOM=6440)
6430 GOTO 6340
6440 FIND (POE02_DEV,KEY=FIRM$+K52$(10),DOM=6460)
6450 GOTO 6340
6460 WRITE (POS10_DEV,KEY=D0$)IOL=POS10N
6470 REM " --- make header
6480 LET REQNUM$=STR(D[1]:"0000000")
6490 LET A0$=FIRM$+VENDOR$+REQNUM$
6500 LET A1$(1)=WHSE$+PURCHADDRCODE$+DATEORD$+DATEPROM$+NOTB4DATE$+DATEREQ$+FILL(6)+HOLD$+FILL(2)+TERMSCODE$+FILL(4)+FREIGHTTERMS$+VSHIPVIA$+ACK$+FOB$+REQMSG$+FILL(49)
6520 WRITE (POE01_DEV,KEY=A0$)IOL=POE01A
6530 LET XREF$=A0$(1,2)+A0$(9,7)+A0$(3,6)
6540 WRITE (POE31_DEV,KEY=XREF$)IOL=POE31A
6580 LET B0$(1)=A0$+"010"
6590 RETURN 
6600 REM " --- Assign line code info based on line type defaults
6605 LINECODES: 
6610 DIM LINE4LEAD$(1),LINECODE$(2),X1$(32)
6630 ON POS(LINETYPE$="SNM") GOTO STYPE,STYPE,NTYPE,MTYPE
6635 STYPE: 
6640 LET LINECODE$=DEFAULTS$(63,2); REM "Standard item type
6650 GOTO GETLINE4LEAD
6655 NTYPE: 
6660 LET LINECODE$=DEFAULTS$(65,2); REM "Non-stock item type
6670 GOTO GETLINE4LEAD
6675 MTYPE: 
6680 LET LINECODE$=DEFAULTS$(67,2); REM "Message line type
6690 GOTO GETLINE4LEAD
6700 GETLINE4LEAD: 
6710 FIND (POM02_DEV,KEY=FIRM$+LINECODE$,DOM=6720)IOL=POM02A
6720 LET LINE4LEAD$(1)=X1$(23,1)
6790 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 LET STATUS=E0
9230 IF STATUS=0 THEN LET STATUS=256
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller
9910 LET DETAILKEY$(1)=B0$
9950 EXIT 
9999 END

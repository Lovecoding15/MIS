0010 REM "DDM - Record Layout Maintenance (Ribbon)"
0020 REM "Program DDM.BB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0060 ON O0 GOTO 0090,1000
0200 REM " --- IOList
0210 DDM01A: IOLIST X0$(1),X1$(1),X[ALL]
0230 DDM03A: IOLIST B0$(1),B1$(1),B[ALL]
0240 DDM04A: IOLIST C0$(1),C1$(1),C[ALL]
0250 DDM07A: IOLIST Y0$
0280 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0500 REM " --- Initializations
0510 DIM DDFILES[8]
0550 LET DDFILES[1]=SYS01_DEV,DDFILES[3]=DDM01_DEV,DDFILES[4]=DDM06_DEV
0560 LET DDFILES[5]=DDM10_DEV,DDFILES[6]=DDM20_DEV
0900 REM " --- Position file
0990 READ (DDM03_DEV,KEY=A0$,DOM=1000)
1000 REM " --- Record ID
1005 PRINT @(5,L0-3),FILL(32),@(43),FILL(12),@(70),'CL',@(0,L0-1),'CE',
1010 LET V2$="",V2$=KEY(DDM03_DEV,END=1020)
1015 IF POS(A0$=V2$)<>1 THEN LET V2$="" ELSE LET V2$=V2$(7,1)
1020 LET V0$="S",V1$="CK^",V3$="",V0=1,V1=3,V2=L0-3,I0=0
1030 LET V4$="Enter Record ID (<F3>=Lookup)"
1040 GOSUB 7000
1050 ON V3 GOTO 1070,1000,1000,1060,4500
1060 GOSUB 6000
1070 IF V$="" THEN GOTO 1100
1080 LET B0$(1)=A0$+V$
1090 GOTO 1200
1100 REM " --- Get next record
1110 LET K$=KEY(DDM03_DEV,END=0900)
1120 IF POS(A0$=K$)<>1 THEN GOTO 0900
1130 READ (DDM03_DEV)
1140 LET B0$=K$
1200 REM " --- Retrieve record
1210 READ (DDM03_DEV,KEY=B0$,DOM=1400)IOL=DDM03A
1220 FIND RECORD(DDM03_DEV,KEY=B0$)OLDRECORD$
1230 LET OLDALIAS$=B1$(37,12)
1240 IF FNP$(B1$(37,12))="" THEN LET B1$(37,12)=A1$(40,12)
1300 REM " --- Display record
1320 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record
1410 LET V4$="Is This A New Record"
1420 CALL "SYC.YN",1,V4$,1,V$,V3
1440 ON V3 GOTO 1490,1400,1400,1400,1000
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record
1510 DIM B1$(64),B[2]
1520 LET OLDRECORD$="",OLDALIAS$=""
1530 LET B1$(1)=A1$(1),B1$(31)=REVISION$,B1$(37)=A1$(40,12)
1540 GOSUB 5000
1600 REM " --- Description
1610 LET V0$="S",V1$="C",V2$=B1$(1,30),V3$="",V0=30,V1=5,V2=L0-3,I0=1
1620 LET V4$="Enter The Description Of This Record ID (<F1>=File Description)"
1630 GOSUB 7000
1640 ON V3 GOTO 1660,1650,3900,1600,3900
1650 LET V$=A1$(1,30)
1660 LET B1$(1,30)=V$
1670 PRINT @(V1,V2),B1$(1,30),
1690 GOTO 3900
1700 REM " --- Alias"
1710 LET V0$="S",V1$="C",V2$=B1$(37,12),V3$="",V0=12,V1=43,V2=L0-3
1720 LET V4$="Enter The Alias For This Record ID (<F1>=File Alias)"
1730 GOSUB 7000
1740 ON V3 GOTO 1760,1750,3900,1700,3900
1750 LET V$=A1$(40,12)
1760 LET B1$(37,12)=V$
1770 PRINT @(V1,V2),B1$(37,12),
1790 GOTO 3900
3900 REM " --- Next field
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1600,1700,3990
3990 LET I0=0
4000 REM " --- Correct?
4010 LET V4$="Is The Above Information Correct (Y/N/Delete/Comment)?"
4020 LET V0$="S",V1$="^CR",V2$="Y",V3$="",V0=7,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4050 IF V$="D" OR V$="DELETE" THEN GOTO 4200
4060 IF V$="C" OR V$="COMMENT" THEN GOTO 4100
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record
4110 WRITE (DDM03_DEV,KEY=B0$)IOL=DDM03A
4120 FIND RECORD(DDM03_DEV,KEY=B0$)NEWRECORD$
4130 IF NEWRECORD$=OLDRECORD$ THEN GOTO 4180
4135 LET B1$(31,6)=REVISION$
4140 WRITE (DDM03_DEV,KEY=B0$)IOL=DDM03A
4145 GOSUB 6800
4150 IF B1$(37,12)=OLDALIAS$ THEN GOTO 4180
4155 REMOVE (DDM07_DEV,KEY=OLDALIAS$+B0$(1,6),DOM=4160)
4160 LET Y0$=B1$(37,12)+B0$(1,6)
4165 WRITE (DDM07_DEV,KEY=Y0$)IOL=DDM07A
4180 IF V$="C" OR V$="COMMENT" THEN GOTO 4800
4190 GOTO 4900
4200 REM " --- Remove record
4210 LET V4$="OK to delete all fields for this record"
4220 CALL "SYC.YN",0,V4$,0,V$,V3
4230 IF V$<>"YES" THEN GOTO 4000
4300 REM " --- Delete remarks
4305 READ (DDM13_DEV,KEY=B0$,DOM=4310)
4310 LET K$=KEY(DDM13_DEV,END=4335)
4315 IF POS(B0$=K$)<>1 THEN GOTO 4335
4320 REMOVE (DDM13_DEV,KEY=K$)
4325 GOTO 4310
4335 READ (DDM14_DEV,KEY=B0$,DOM=4340)
4340 LET K$=KEY(DDM14_DEV,END=4365)
4345 IF POS(B0$=K$)<>1 THEN GOTO 4365
4350 REMOVE (DDM14_DEV,KEY=K$)
4355 GOTO 4340
4400 REM " --- Delete fields
4405 READ (DDM04_DEV,KEY=B0$,DOM=4410)
4410 LET K$=KEY(DDM04_DEV,END=4490)
4420 IF POS(B0$=K$)<>1 THEN GOTO 4490
4430 READ (DDM04_DEV,KEY=K$)IOL=DDM04A
4440 LET X0$(1)=C1$(1),X1$(1,30)=C1$(17,30)
4450 FIND (DDM01_DEV,KEY=X0$,DOM=4480)IOL=DDM01A
4455 LET LENGTH=X[0]
4460 IF C[1]>0 THEN LET LENGTH=LENGTH*C[1]
4465 LET XREF$(1)=X0$(1,12)+X1$(1,30)+X1$(37,1)+STR(LENGTH:"0000")+C0$
4470 REMOVE (DDM06_DEV,KEY=XREF$,DOM=4475)
4475 REMOVE (DDM04_DEV,KEY=K$)
4480 GOTO 4410
4490 REMOVE (DDM03_DEV,KEY=B0$,DOM=1000)
4495 GOTO 1000
4500 REM " --- Run header overlay
4510 LET O0=1
4590 RUN "SYM.FA"
4800 REM " --- Run comments overlay
4810 DIM COMINFO$[2],COMINFO[5]
4820 LET COMINFO$[0]="DDM-13",COMINFO$[1]=B0$
4830 LET COMINFO$[2]="Record "+B0$(7,1)+" "+B1$(1,30)
4840 LET COMINFO[1]=L0-3,COMINFO[3]=3,COMINFO[4]=60,COMINFO[5]=10
4850 CALL "SYC.KA",0,COMINFO$[ALL],COMINFO[ALL]
4890 GOTO 4000
4900 REM " --- Run detail overlay
4990 RUN "DDM.BC"
5000 REM " --- Display record
5010 PRINT @(3,L0-3),B0$(7,1),@(5),B1$(1,30),@(43),B1$(37,12),@(70),FNB6$(B1$(31,6))
5090 RETURN 
6000 REM " --- Record ID Lookup
6010 LET CHANNEL=DDM03_DEV,RECORD$=A0$,NUMBER=2,TITLE$=A0$+" Record ID's"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="ID",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=7,LENGTH[0]=1
6050 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6055 LET FIELD[2]=2,POSITION[2]=37,LENGTH[2]=12
6060 LET DESCRIPTION$[2]="Alias",COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6070 LET V$="NO COMPANY & SKIP",ROW=L0
6080 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
6800 REM " --- Update Last Revision Date on Header Record
6810 LET A0$(1)=B0$
6820 FIND (SYM03_DEV,KEY=A0$)IOL=SYM03A
6830 LET A1$(31,6)=REVISION$
6840 WRITE (SYM03_DEV,KEY=A0$)IOL=SYM03A
6890 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

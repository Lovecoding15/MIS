0010 REM "SYM - System Description Maintenance"
0020 REM "Program SYM.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=3
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="SYM-04",FILES$[3]="SYM-06"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SYM04_DEV=CHANNELS[2],SYM06_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 SYM04A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A[ALL]
0220 SYM06A: IOLIST C0$(1),C1$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0420 LET N0$=F0$(16,2),VERSION$=""
0430 LET VERSION$=STBL("!VERSION",ERR=0440)
0500 REM " --- Initializations"
0510 DIM A0$(2),A1$(64),A2$(50),A3$(50),A[2],C0$(2),C1$(40)
0700 REM " --- Background"
0710 PRINT 'SB',@(4,3),"System ID:",@(2,4),"Description:",@(0,5),"Multiple Firm:",@(1,6),"Default Firm:",@(4,7),"Data Path:",@(1,8),"Program Path:",@(3,9),"Post To GL:",@(2,10),"Batch Entry:",@(4,11),"Installed:",@(6,12),"Version:",'SF',
0800 REM " --- G/L Installed?"
0810 DIM INFO$[20]
0820 CALL "SYC.VA","GL",INFO$[ALL]
0830 LET GL$=INFO$[20]
0900 REM " --- Position File"
0990 READ (SYM04_DEV,KEY="",DOM=1000)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(SYM04_DEV,END=1030)
1030 LET V0$="S",V1$="CEK",V3$="",V0=2,V1=15,V2=3,I0=0
1040 LET V4$="Enter System ID (<F3>=Lookup)"
1050 GOSUB 7000
1060 ON V3 GOTO 1080,1000,1000,1070,4500
1070 CALL "SYC.LM",V$
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(1)=V$
1200 REM " --- Find key"
1210 FIND (SYM04_DEV,KEY=A0$,DOM=1400)IOL=SYM04A
1220 IF FNP$(A1$(40,5))="" THEN LET A1$(40,5)=VERSION$
1230 IF POS(A1$(46,1)="YN")=0 THEN LET A1$(46,1)="Y"
1240 IF POS(A1$(47,1)="YN")=0 THEN LET A1$(47,1)="N"
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- New record initializations"
1510 DIM A1$(64),A2$(50),A3$(50),A[2]
1520 LET A1$(36,1)="Y",A1$(40,5)=VERSION$,A1$(46,2)="NN"
1530 GOSUB 5000
2000 REM " --- System Descriptiion"
2010 LET V4$="Enter The Description For This System"
2020 LET V0$="S",V1$="C",V2$=A1$(1,35),V3$="",V0=35,V1=15,V2=4,I0=0
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 LET A1$(1,35)=V$
2090 GOTO 3900
2100 REM " --- Multiple Firms?"
2110 LET V4$="Will This System Be Used By Multiple Firms (Y/N)?"
2120 LET V0$="Y",V1$="C",V2$=A1$(36,1),V3$="",V0=1,V1=15,V2=5
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2150 LET A1$(36,1)=V$
2190 GOTO 3900
2200 REM " --- Default Firm"
2210 LET V4$="Enter The Default Firm ID For This System (<F3>=Lookup)"
2220 LET V0$="Z",V1$="C",V2$=A1$(37,2),V3$="",V0=2,V1=15,V2=6
2230 GOSUB 7000
2240 ON V3 GOTO 2260,2200,3900,2250,3900
2250 CALL "SYC.LX",V$
2255 IF V$="" THEN GOTO 2200
2260 LET C0$(1)=V$
2270 FIND (SYM06_DEV,KEY=C0$,DOM=2200)IOL=SYM06A
2275 PRINT @(V1,V2),'CL',C0$," ",C1$
2280 LET A1$(37,2)=V$
2290 GOTO 3900
2400 REM " --- Data Pathname"
2410 LET V4$="Enter The Directory Pathname Where This System's Data Files Reside"
2420 LET V0=50,V0$="S",V1$="C",V2$=A2$,V3$="",V1=15,V2=7
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 GOSUB 6100
2460 IF STATUS>0 THEN GOTO 2400
2470 LET A2$(1)=V$
2480 PRINT @(V1,V2),A2$,
2490 GOTO 3900
2600 REM " --- Program Pathname"
2610 LET V4$="Enter The Directory Pathname Where This System's Programs Reside"
2620 LET V0=50,V0$="S",V1$="C",V2$=A3$,V3$="",V1=15,V2=8
2630 GOSUB 7000
2640 ON V3 GOTO 2650,2600,3900,2600,3900
2650 GOSUB 6100
2660 IF STATUS>0 THEN GOTO 2600
2670 LET A3$(1)=V$
2680 PRINT @(V1,V2),A3$,
2690 GOTO 3900
2700 REM " --- G/L Posting Control Flag"
2710 LET V4$="Should This System Post To The General Ledger (Y/N)?"
2720 LET V0$="Y",V1$="C",V2$=A1$(46,1),V3$="",V0=1,V1=15,V2=9
2725 IF GL$<>"Y" THEN LET V$="N"; GOTO 2750
2730 GOSUB 7000
2740 ON V3 GOTO 2750,2700,3900,2700,3900
2750 LET A1$(46,1)=V$
2760 PRINT @(V1,V2),A1$(46,1),
2790 GOTO 3900
2800 REM " --- Batch Entry?"
2810 LET V4$="Should This System Use Batched Entry (Y/N)?"
2820 LET V0$="Y",V1$="C",V2$=A1$(47,1),V3$="",V0=1,V1=15,V2=10
2830 GOSUB 7000
2840 ON V3 GOTO 2850,2800,3900,2800,3900
2850 LET A1$(47,1)=V$
2890 GOTO 3900
2900 REM " --- Installed?"
2910 LET V4$="Is This System Installed (Y/N)?"
2920 LET V0$="Y",V1$="C",V2$=A1$(39,1),V3$="",V0=1,V1=15,V2=11
2930 GOSUB 7000
2940 ON V3 GOTO 2950,2900,3900,2900,3900
2950 LET A1$(39,1)=V$
2960 IF A0$="GL" THEN IF A1$(39,1)="Y" THEN LET GL$="Y"
2990 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,2400,2600,2700,2800,2900,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4005 LET V4$="Is The Above Information Correct (Y/N",V0=1
4010 IF A1$(39,1)<>"Y" THEN LET V4$=V4$+"/Delete",V0=6
4015 LET V4$=V4$+")?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record"
4110 WRITE (SYM04_DEV,KEY=A0$)IOL=SYM04A
4190 GOTO 1000
4200 REM " --- Remove record"
4210 REMOVE (SYM04_DEV,KEY=A0$,DOM=4220)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4550 ON POS(V$="YN") GOTO 4500,4590,9900
4590 RUN "SYM.DB"
5000 REM " --- Display record"
5010 LET C0$(1)=A1$(37,2),C1$(1)="(Not On File)"
5020 FIND (SYM06_DEV,KEY=C0$,DOM=5030)IOL=SYM06A
5030 PRINT @(15,3),A0$,@(15,4),A1$(1,35),@(15,5),A1$(36,1),@(15,6),A1$(37,2)," ",C1$,@(15,7),A2$,@(15,8),A3$,@(15,9),A1$(46,1),@(15,10),A1$(47,1),@(15,11),A1$(39,1),@(15,12),A1$(40,5),
5090 RETURN 
6100 REM " --- Test Pathname"
6110 LET CHANNEL=UNT,STATUS=1
6120 IF FNP$(V$)<>"" THEN OPEN (CHANNEL,ERR=6150)V$
6130 LET STATUS=0
6180 CLOSE (CHANNEL,ERR=6190)
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

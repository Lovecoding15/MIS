0010 REM "APM - Vendor Maintenance (Detail Information)"
0020 REM "Program APM.MC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "29Jan97 JAL: Default ITAIC prompt to Y (4010)"
0032 REM "21Apr98 JWK: N at ITAIC prompt performed a Restore (4050)
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0060 ON O9 GOTO 0100,4000
0200 REM " --- IOLists
0220 APM02A: IOLIST B0$(1),B1$(1),B2$(1),B[ALL]
0230 APM10A: IOLIST X0$
0240 GLM01A: IOLIST X$,X4$
0500 REM " --- Init Data
0550 LET B0$(1)=A0$,O5=0
0560 DIM X4$(35); REM "WA:200
0700 REM " --- Background
0710 GOSUB 6000
0720 PRINT 'SB',@(15,10),"Detail",
0740 IF P3$(1,1)="Y" THEN PRINT @(6,11),"AP Type:",
0750 IF P3$(2,1)="Y" THEN PRINT @(4,12),"Dist Code:",
0760 PRINT @(0,13),"Payment Group:",@(3,14),"Terms Code:",
0770 IF GL$="Y" THEN PRINT @(3,15),"Account No:",
0790 PRINT 'SF',
0900 REM " --- Init File
0990 READ (APM02_DEV,KEY=A0$,DOM=1000)
1000 REM " --- Key
1010 IF P3$(1,1)="Y" THEN GOTO 1100
1020 LET B0$=A0$+"  "
1030 GOSUB 6200
1040 IF X0$<>"" THEN LET B1$(1,2)=X0$(26,2)
1060 READ (APM02_DEV,KEY=B0$,DOM=2000)IOL=APM02A
1070 GOSUB 5000
1090 GOTO 4000
1100 REM " --- Init File
1120 READ (APM02_DEV,KEY=A0$,DOM=1130)
1200 REM " --- AP Type
1210 PRINT @(15,11),'CL'; GOSUB 6300
1220 LET V2$="",V2$=KEY(APM02_DEV,END=1250)
1230 IF V2$(1,LEN(A0$))<>A0$ THEN LET V2$=""
1240 IF V2$<>"" THEN LET V2$=V2$(9)
1250 LET V0$="S",V1$="KCE",V0=2,V1=15,V2=11,V4$="Enter A Valid AP Type (<F3>=Lookup)"
1260 GOSUB 7000
1262 IF V3<>3 THEN GOTO 1270
1264 LET V4=1,V5=APM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="A"
1266 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
1268 IF V$="" THEN GOTO 1250
1270 IF V3=4 THEN GOSUB 6600; RUN "APM.MA"
1280 LET V$=V$+"  ",V$=V$(1,2)
1285 FIND (APM10_DEV,KEY=N0$+"A"+V$,DOM=1100)
1290 LET B0$(9,2)=V$,O9=0
1300 FIND (APM02_DEV,KEY=B0$,DOM=1400)IOL=APM02A
1320 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New Record
1420 GOSUB 6200
1440 IF X0$="" THEN GOTO 1200
1450 PRINT @(18,11),X0$(6,20),
1460 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1480 ON POS(V$="YN") GOTO 1470,1500,1200
1500 REM " --- Init Record
1510 DIM B[13]
1520 LET B1$(1)="",B2$(1)=""
2000 REM " --- Overlay
2010 EXTRACT (APM02_DEV,KEY=B0$,DOM=2011)IOL=APM02A
2015 GOSUB 5000
2020 LET O9=1,V3=0
2040 RUN "APM.MD"
4000 REM " --- Options
4002 IF O9=1 THEN GOSUB 5000
4005 IF F9$<>"111" THEN GOTO 6400
4010 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/##/Restore/Delete)?",V0=7,V1=FNV(V4$),V2=22,O7=0
4020 GOSUB 7000
4024 LET I=INT(NUM(V$,ERR=4030))
4026 IF I>0 THEN LET I0=I-2,O7=2; GOTO 2000
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF O9=0 THEN IF V$="" THEN GOTO 4140
4045 IF V$="R" OR V$="RESTORE" THEN GOTO 4700
4050 ON POS(V$="YN") GOTO 4000,4100,2020
4100 REM " --- Write
4110 LET O8=1
4120 WRITE (APM02_DEV,KEY=B0$)IOL=APM02A
4140 IF P3$(1,1)="Y" THEN GOTO 1200
4190 RUN "APM.MA"
4200 REM " --- Delete
4220 FOR X=0 TO 13
4230 IF B[X]<>0 THEN EXITTO 4270
4240 NEXT X
4245 GOTO 4500
4250 REMOVE (APM02_DEV,KEY=B0$,DOM=4260)
4260 GOTO 4280
4270 LET V0$="S",V1$="C",V2$="",V3$="",V4$="**Transactions Exist** - Delete Request Ignored, <Enter>: ",V0=1,V1=FNV(V4$),V2=22
4272 PRINT @(V1,V2),'RB',
4275 GOSUB 7000
4280 IF O5=1 THEN RUN "APM.MB"
4285 IF P3$(1,1)<>"Y" THEN RUN "APM.MA"
4290 GOTO 1000
4500 REM " --- Check APT-01
4520 READ (APT01_DEV,KEY=N0$+B0$(9,2)+B0$(3,6),ERR=4540)
4540 LET K$=KEY(APT01_DEV,END=4250)
4560 IF K$(1,10)<>N0$+B0$(9,2)+B0$(3,6) THEN GOTO 4250
4580 GOTO 4270
4700 REM " --- Restore
4720 FIND (APM02_DEV,KEY=B0$,DOM=4010)IOL=APM02A
4730 GOSUB 6300
4740 GOSUB 5000
4760 GOTO 4000
5000 REM " --- Display
5005 GOSUB 6500
5010 IF P3$(1,1)<>"Y" THEN GOTO 5050
5020 GOSUB 6200
5030 IF X0$<>"" THEN PRINT @(18,11),X0$(6,20),
5050 IF P3$(2,1)="Y" THEN PRINT @(15,12),B1$(1,2),X1$(6,20),
5060 PRINT @(15,13),B1$(3,2),@(15,14),B1$(5,2),
5065 PRINT @(18,13),X2$(6,20),@(18,14),X3$(6,20),
5070 IF GL$<>"Y" THEN GOTO 5100
5080 FIND (GLM01_DEV,KEY=N0$+B2$,DOM=5090)IOL=GLM01A
5090 PRINT @(15,15),FNF$(B2$(1,G[3]),G1$),@(15+G5+1,15),X4$(1,35),
5190 RETURN 
6000 REM " --- Clear Screen
6020 PRINT @(0,L0),'CE',
6090 RETURN 
6200 REM " --- AP Type
6210 LET X0$=""
6220 FIND (APM10_DEV,KEY=N0$+"A"+B0$(9,2),DOM=6230)IOL=APM10A
6290 RETURN 
6300 REM " --- Clear Data
6320 FOR X=12 TO 15
6330 PRINT @(15,X),'CL',
6340 NEXT X
6390 RETURN 
6400 REM " --- Messages
6410 IF F9$(1,1)=" " THEN LET V4$="Distribution Code "; GOTO 6460
6420 IF F9$(2,1)=" " THEN LET V4$="Payment Group Code "; GOTO 6460
6430 IF F9$(3,1)=" " THEN LET V4$="Terms Code "
6460 LET V0$="S",V1$="C",V2$="",V3$="",V4$=V4$+"Is Required, <Enter>=Re-Enter, <F4>=Exit: ",V0=1,V1=FNV(V4$),V2=22,O7=0
6465 PRINT @(V1,V2),'RB',
6470 GOSUB 7000
6475 IF V3=4 THEN GOTO 4010
6480 LET O7=1
6490 GOTO 2000
6500 REM " --- Verify
6502 DIM F9$(3),X1$(25),X2$(25),X3$(25)
6515 IF P3$(2,1)<>"Y" THEN GOTO 6530
6520 FIND (APM10_DEV,KEY=N0$+"B"+B1$(1,2),DOM=6540)X1$(1); REM "WA:200
6530 LET F9$(1,1)="1"
6540 FIND (APM10_DEV,KEY=N0$+"D"+B1$(3,2),DOM=6560)X2$(1); REM "WA:200
6550 LET F9$(2,1)="1"
6560 FIND (APM10_DEV,KEY=N0$+"C"+B1$(5,2),DOM=6580)X3$(1); REM "WA:200
6570 LET F9$(3,1)="1"
6590 RETURN 
6600 REM " --- Verify and AP test exists
6610 IF P3$(1,1)<>"Y" THEN EXITTO 6700
6620 READ (APM02_DEV,KEY=B0$(1,8),DOM=6630)
6630 LET X$=KEY(APM02_DEV,END=6650)
6640 IF X$(1,8)=B0$(1,8) THEN GOTO 6690
6650 LET V0$="S",V1$="C",V2$="",V3$="",V4$="A Valid AP Type Is Required, <Enter>=Re-Enter, <F4>=Exit: ",V0=1,V1=FNV(V4$),V2=22
6660 GOSUB 7000
6665 PRINT @(V1,V2),'RB',
6670 IF V3=4 THEN EXITTO 6700
6680 EXITTO 1250
6690 RETURN 
6700 REM " --- Delete
6720 LET V0$="S",V1$="C",V2$="",V3$="",V4$="This Vendor Code Will Be Deleted, <Enter>: ",V0=1,V1=FNV(V4$),V2=22
6730 PRINT @(V1,V2),'RB',
6740 GOSUB 7000
6750 LET O5=1
6790 GOTO 4200
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 LET O8=1; RUN "APM.MA"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "APU - PERIOD-END UPDATE"
0020 REM "Program APU.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=8
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET OPTIONS$[1]="L",OPTIONS$[2]="L",OPTIONS$[5]="L"; LET OPTIONS$[6]="L"
0130 LET FILES$[1]="APT-01",FILES$[2]="APT-11",FILES$[3]="APT-02"
0140 LET FILES$[4]="APE-05",FILES$[5]="APM-02",FILES$[6]="APM-03"
0150 LET FILES$[7]="APT-03",FILES$[8]="SYS-01"
0160 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET APT01_DEV=CHANNELS[1],APT11_DEV=CHANNELS[2],APT02_DEV=CHANNELS[3]
0185 LET APE05_DEV=CHANNELS[4],APM02_DEV=CHANNELS[5],APM03_DEV=CHANNELS[6]
0190 LET APT03_DEV=CHANNELS[7],SYS01_DEV=CHANNELS[8]
0200 REM " --- IOLists"
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[2],G[4]
0460 FOR X=0 TO 4
0470 LET G[X]=NUM(G2$(X*2+1,2),ERR=0480)
0480 NEXT X
0485 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P4$(1,2)),P[2]=FNYY_YEAR(P4$(3,2))
0500 REM " --- Init Data"
0540 DIM A[2],B[2],D[13],T[2]
0700 REM " --- Background"
0710 FOR X=4 TO 8 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(16,3),"Period <1 Thru "+STR(P[1]:"##")+">:",'SF',@(0,22),'CL',
1000 REM " --- Options"
1005 LET V4$="<F4> to End"
1010 LET V0$="S",V1$="E",V2$="",V3$="",V0=LEN(STR(P[1])),V1=40,V2=3
1020 GOSUB 7000
1040 IF V3=4 THEN GOTO 9900
1060 LET I=NUM(V$,ERR=1000)
1080 IF I<1 OR I>P[1] THEN GOTO 1000
1100 REM " --- Determinve Period Limits"
1120 LET V=I
1140 CALL "SYC.PA",SYS01_DEV,V,P[2],V0$,V9$,STATUS
1160 LET P8$="",P9$=V9$,P9=I
1170 LET H3$="Period "+STR(I)+"  From "+FNB$(V0$)+" To "+FNB$(V9$)
1180 PRINT @(40,3),H3$(8),
1200 REM " --- Correct?"
1205 PRINT @(22,5),"Is This The Correct Period (Yes/No)?",
1210 LET V0$="S",V1$="KCE",V2$="NO",V3$="",V4$="",V0=3,V1=59,V2=5
1220 GOSUB 7000
1240 IF V3=4 THEN GOTO 9900
1250 IF V$="NO" THEN GOTO 1000
1260 IF V$<>"YES" THEN GOTO 1200
1280 IF FNH(P9$(2,1))=12 AND FNH(P9$(1,1))=G[2] THEN GOSUB 2000
1290 GOTO 4000
2000 REM " --- Year-End"
2005 IF P9=G[0] THEN GOTO 2020
2010 CALL "SYC.PA",SYS01_DEV,P9+1,P[2],X0$,X1$,STATUS
2015 IF FNH(X1$(2,1))=12 AND FNH(X1$(1,1))=G[2] THEN GOTO 2090; REM "There is another December period
2020 PRINT 'RB',@(10,8),"Attention: Updating This Period Will Cause Vendor Calendar",@(10,10),"Year-To-Date Summaries To Roll-Over From Current To Prior Year.",@(10,12),"1099 Forms Must Be Printed Prior To Closing This Period.",
2025 LET V4$="Do You Wish To Continue With The Update (Yes/No)?"
2030 LET V0$="S",V1$="KC",V2$="NO",V3$="",V0=3,V1=FNV(V4$),V2=22
2040 GOSUB 7000
2050 PRINT @(0,8),'CE'
2060 IF V$="NO" THEN EXITTO 9900
2080 IF V$<>"YES" THEN GOTO 2000
2085 LET P8$="YREND"
2090 RETURN 
4000 REM " --- Overlay"
4100 RUN "APU.DB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNH(Q$)=ASC(Q$)-32
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

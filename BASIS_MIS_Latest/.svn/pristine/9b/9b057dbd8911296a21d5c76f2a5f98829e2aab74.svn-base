0010 REM "GLP - G/L Fiscal Calendar Maintenance"
0020 REM "Program GLP.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0210 GLS01A: IOLIST A0$,A1$,A2$(1),A3$(1),A4$,A5$,A6$,A7$,A8$
0500 REM " --- Initializations"
0550 LET I0=0,OFFSET=3,L0=6,A0$=N0$+N2$+"00"
0560 LET PRIOR$=STR(1900+FNYY_YEAR(A2$(5,2))-1)
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0720 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0750 PRINT 'SB',@(0,3),"Current Period:",@(52),"Fiscal Year ",PRIOR$," Closed?",@(2,4),"Current Year:",@(62),"Total Periods:",@(11,L0),"Period",@(19),"Ends",@(26),"Period Name",@(44),"Abbr",@(50),"Locked?",@(61),"Date",'SF',
1200 REM " --- Retrieve record"
1210 FIND (SYS01_DEV,KEY=A0$,DOM=1500)IOL=GLS01A
1220 LET NUMBER=0,NUMBER=NUM(A2$(1,2),ERR=1500)
1230 IF NUMBER=0 THEN GOTO 1500
1240 IF POS("  "=A3$(1,2*NUMBER))<>0 THEN GOTO 1500
1250 LET LOCKED$=A3$(FNP4(1,MP),MP)
1300 REM " --- Display"
1310 GOSUB 5000
1390 GOTO 4000
1500 REM " --- New Record"
1510 LET A3$(1)=FNI$(1)+FNI$(31)+FNI$(2)+FNI$(28)+FNI$(3)+FNI$(31)+FNI$(4)+FNI$(30)+FNI$(5)+FNI$(31)+FNI$(6)+FNI$(30)+FNI$(7)+FNI$(31)+FNI$(8)+FNI$(31)+FNI$(9)+FNI$(30)+FNI$(10)+FNI$(31)+FNI$(11)+FNI$(30)+FNI$(12)+FNI$(31)+FNI$(12)+FNI$(31)
1520 LET A3$(FNP2(1,MP))="January         February        March          "
1530 LET A3$(FNP2(4,MP))="April           May             June           "
1540 LET A3$(FNP2(7,MP))="July            August          September      "
1550 LET A3$(FNP2(10,MP))="October         November        December       "
1560 LET A3$(FNP3(1,MP))="JanFebMarAprMayJunJulAugSepOctNovDec"
1570 LET A3$(FNP4(1,MP))="NNNNNNNNNNNN",LOCKED$=A3$(FNP4(1,MP),MP)
1580 LET NUMBER=12,A2$(1,2)="12",A2$(3,2)=F0$(20,2),A2$(5,2)=FNYY_YY21$(F0$(18,2))
1590 GOSUB 5000
2000 REM " --- Current Period"
2010 LET V4$="Enter The Current Period (1 Through "+STR(NUM(A2$(1,2)))+")"
2020 LET V0$="Z",V1$="C",V2$=A2$(3,2),V3$="",V0=2,V1=16,V2=3,I0=0
2030 GOSUB 7000
2035 ON V3 GOTO 2040,2000,2000,2000,3900
2040 LET V=NUM(V$,ERR=2000)
2050 IF V<1 OR V>NUMBER THEN GOTO 2000
2060 LET A2$(3,2)=V$
2090 GOTO 3900
2100 REM " --- Current Year"
2110 LET V4$="Enter The Calender Year In Which The First Period Begins"
2120 LET V0$="D",V1$="CY",V2$=A2$(5,2),V3$="",V0=2,V1=16,V2=4
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2150 LET A2$(5,2)=V$
2190 GOTO 3900
2200 REM " --- Period Locked?"
2205 LET X=I0-1,P4=FNP4(X,MP)
2210 IF X>NUMBER THEN GOTO 3900
2215 LET V4$="Is This Period Locked (Y/N)?"
2220 LET V0$="Y",V1$="C",V2$=A3$(P4,1),V3$="",V0=3,V1=52,V2=L0+X
2225 IF POS(V2$="YN")=0 THEN LET V2$="N"
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,2270,2200,2270
2250 IF POS(V$="YN")=0 THEN GOTO 2200
2260 LET A3$(P4,1)=V$
2270 IF A3$(P4,1)="Y" THEN PRINT @(V1,V2),"Yes"
2280 IF A3$(P4,1)="N" THEN PRINT @(V1,V2),"No "
2290 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,2200,2200,2200,2200,2200,2200,2200,2200,2200,2200,2200,2200,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4400
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Check for Duplicate Period Ending Dates"
4110 FOR X=1 TO NUMBER
4115 LET P1=(X-1)*2+1
4120 IF POS(A3$(P1,2)=A3$(1,NUMBER*2))<>P1 THEN EXITTO 4150
4130 NEXT X
4140 GOTO 4200
4150 LET V4$="Duplicate Period Ending Dates Not Allowed (<Enter>=Continue):"
4160 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
4170 GOSUB 7000
4190 GOTO 2000
4200 REM " --- Any period locks changed?"
4210 FOR X=1 TO MP
4220 IF LOCKED$(X,1)="N" THEN IF A3$(FNP4(X,MP),1)="Y" THEN LET A3$(FNP5(X,MP),3)=F0$(7,3)
4230 NEXT X
4300 REM " --- Write"
4310 WRITE (SYS01_DEV,KEY=A0$)IOL=GLS01A
4350 IF O0=2 THEN GOTO 4900
4400 REM " --- Return to menu"
4410 LET O0=1
4490 RUN "GLP.AA"
4900 REM " --- Run next overlay"
4990 RUN "GLP.AC"
5000 REM " --- Display"
5005 IF POS(A4$(6,1)="YN")=0 THEN LET A4$(6,1)="Y"
5010 PRINT @(0,L0+1),'CE',@(16,3),A2$(3,2),@(77),A4$(6,1),@(16,4),FNYY21_YY$(A2$(5,2)),@(77),A2$(1,2),
5020 FOR X=1 TO NUMBER
5030 LET X$="No",Y$="",LX=FNP4(X,MP)
5035 IF POS(A3$(LX,1)="YN")=0 THEN LET A3$(LX,1)="N"
5040 IF A3$(LX,1)="Y" THEN LET X$="Yes"
5050 IF FNP$(A3$(FNP5(X,MP),3))<>"" THEN LET Y$=FNB$(A3$(FNP5(X,MP),3))
5070 PRINT @(13,X+L0),X:"##",@(19),FNM$(A3$(FNP1(X,MP),2)),@(26),A3$(FNP2(X,MP),16),@(44),A3$(FNP3(X,MP),3),@(52),X$,@(59),Y$,
5080 NEXT X
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8050 DEF FNI$(Q)=CHR(Q+32)
8055 DEF FNM$(Q1$)=FNA$(Q1$(1),"XXX")+"/"+FNA$(Q1$(2),"XXX")
8060 DEF FNP$(Q$)=CVS(Q$,2)
8065 DEF FNP1(Q1,Q2)=(Q1-1)*2+1
8070 DEF FNP2(Q1,Q2)=Q2*2+((Q1-1)*16+1)
8075 DEF FNP3(Q1,Q2)=Q2*2+Q2*16+((Q1-1)*3+1)
8080 DEF FNP4(Q1,Q2)=Q2*2+Q2*16+Q2*3+Q1
8085 DEF FNP5(Q1,Q2)=Q2*2+Q2*16+Q2*3+Q2*1+((Q1-1)*3+1)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "APU - MONTH-END UPDATE 2"
0020 REM "Program APU.DB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0210 APT01A: IOLIST A0$,A[ALL]
0220 APE05A: IOLIST B0$,B1$,B[ALL]
0240 APM02A: IOLIST D0$,D1$,D2$,D[ALL]
0270 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0310 APT11A: IOLIST X0$,A1$,A[ALL]
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
1000 REM " --- Init Invoice Read"
1010 IF P3$(5,1)="Y" THEN GOTO 2000
1020 READ (APT01_DEV,KEY=N0$,DOM=1030)
1040 CALL "SYC.NB","Updating Invoice: ",P[0]+11,COLUMN
1100 REM " --- Invoice Read"
1120 LET K0$=KEY(APT01_DEV,END=2000)
1140 IF K0$(1,2)<>N0$ THEN GOTO 2000
1240 IF K0$(21,2)<>"00" THEN GOTO 1800
1260 READ (APT01_DEV)IOL=APT01A
1270 IF A0$(29,3)>P9$ OR A0$(32,3)>P9$ THEN GOTO 1700
1280 PRINT @(COLUMN,11),A0$(5,P[0])," ",A0$(11,10),
1300 REM " --- Init Totals"
1310 DIM T[2]
1320 LET O9=0
1400 REM " --- Find Totals"
1430 LET T[0]=T[0]+A[0],T[1]=T[1]+A[1],T[2]=T[2]+A[2]
1500 REM " --- Invoice Detail"
1505 READ (APT11_DEV,KEY=K0$(1,20),DOM=1510)
1510 LET K$=KEY(APT11_DEV,END=1600)
1520 IF K$(1,20)<>K0$(1,20) THEN GOTO 1600
1530 READ (APT11_DEV)IOL=APT11A
1550 IF A1$(1,1)=" " THEN GOTO 1580
1560 LET T[0]=T[0]+A[0],T[1]=T[1]+A[1],T[2]=T[2]+A[2]
1570 IF A1$(2,3)>P9$ THEN GOTO 1700
1590 GOTO 1510
1600 REM " --- Keep or Remove"
1620 IF T[0]<>0 OR T[2]<>0 THEN GOTO 1700
1640 IF O9=0 THEN GOTO 1800
1700 REM " --- Next Invoice"
1720 READ (APT01_DEV,KEY=K0$(1,20)+"Z",DOM=1790)
1790 GOTO 1100
1800 REM " --- Remove Invoice"
1815 READ (APT11_DEV,KEY=K0$(1,20),DOM=1820)
1830 LET K$=KEY(APT11_DEV,END=1870)
1840 IF K$(1,20)<>K0$(1,20) THEN GOTO 1870
1850 REMOVE (APT11_DEV,KEY=K$)
1860 GOTO 1830
1870 REMOVE (APT01_DEV,KEY=K0$,DOM=1875)
1875 REMOVE (APT03_DEV,KEY=A0$(1,2)+"A"+A0$(3,2)+A0$(5,6)+A0$(29,3)+A0$(11,10),DOM=1880)
1880 REMOVE (APT03_DEV,KEY=A0$(1,2)+"B"+A0$(3,2)+A0$(5,6)+A0$(41,10)+A0$(11,10),DOM=1885)
1885 REMOVE (APT03_DEV,KEY=A0$(1,2)+"C"+A0$(3,2)+A0$(11,10)+A0$(5,6),DOM=1890)
1890 READ (APT02_DEV,KEY=K0$(1,10)+"I"+K0$(11,10),DOM=1900)
1900 LET K$=KEY(APT02_DEV,END=1980)
1910 IF K$(1,21)<>K0$(1,10)+"I"+K0$(11,10) THEN GOTO 1980
1920 REMOVE (APT02_DEV,KEY=K$)
1930 GOTO 1900
1980 IF K0$(5,4)="TEMP" THEN REMOVE (APM03_DEV,KEY=K0$(1,4)+K0$(11,10),DOM=1990)
1990 GOTO 1100
2000 REM " --- Init Check Read"
2020 READ (APE05_DEV,KEY=N0$,DOM=2030)
2040 PRINT @(0,5),'CL','SB',@(23,5),"Examining Check:",'SF',
2100 REM " --- Main Check Read"
2120 LET K0$=KEY(APE05_DEV,END=3000)
2140 IF K0$(1,2)<>N0$ THEN GOTO 3000
2160 READ (APE05_DEV)IOL=APE05A
2180 PRINT @(40,5),B0$(5,7),
2200 REM " --- Keep or Remove"
2220 IF B0$(28,3)>P9$ THEN GOTO 2100
2800 REM " --- Remove Check"
2820 REMOVE (APE05_DEV,KEY=K0$)
2825 IF P3$(5,1)="Y" THEN GOTO 2100
2830 IF B0$(31,1)<>"M" THEN GOTO 2100
2840 LET C$=B0$(1,4)+B0$(12,6)+"C"+B0$(5,7)
2890 READ (APT02_DEV,KEY=C$,DOM=2900)
2900 LET K$=KEY(APT02_DEV,END=2990)
2910 IF K$(1,18)<>C$ THEN GOTO 2990
2920 REMOVE (APT02_DEV,KEY=K$)
2950 GOTO 2900
2990 GOTO 2100
3000 REM " --- Roll Over Totals at Year-end"
3020 IF P9<>G[0] THEN IF P8$<>"YREND" THEN GOTO 3500
3040 READ (APM02_DEV,KEY=N0$,DOM=3050)IOL=APM02A
3060 PRINT @(0,5),'CL','SB',@(23,5),"Updating Vendor:",'SF',
3100 REM " --- Main Vendor Read"
3120 LET K0$=KEY(APM02_DEV,END=3500)
3140 IF K0$(1,2)<>N0$ THEN GOTO 3500
3160 READ (APM02_DEV)IOL=APM02A
3170 PRINT @(COLUMN,11),D0$(3,P[0]),
3175 IF P9<>G[0] THEN GOTO 3220
3180 FOR X=0 TO 2
3190 LET D[X*3+3]=D[X*3+2],D[X*3+2]=D[X*3+4],D[X*3+4]=0
3200 NEXT X
3220 IF P8$="YREND" THEN LET D[12]=D[11],D[11]=D[13],D[13]=0
3280 WRITE (APM02_DEV,KEY=K0$)IOL=APM02A
3290 GOTO 3100
3500 REM " --- Clr Temp Vndrs"
3520 READ (APM03_DEV,KEY=N0$,DOM=3540)
3540 LET K$=KEY(APM03_DEV,END=3900)
3550 IF K$(1,2)<>N0$ THEN GOTO 4000
3560 READ (APM03_DEV)
3570 FIND (APT01_DEV,KEY=K$(1,4)+"TEMP  "+K$(5,10)+"00",DOM=3600)
3590 GOTO 3540
3600 REMOVE (APM03_DEV,KEY=K$)
3690 GOTO 3540
4000 REM " --- Increment Period"
4010 IF P9<P[1] THEN GOTO 4100
4020 LET P[1]=P[1]+1
4030 IF P[1]>G[0] THEN LET P[1]=1,P[2]=P[2]+1
4050 EXTRACT (SYS01_DEV,KEY=N0$+N2$+"00",DOM=4060)IOL=APS01A
4060 LET P4$(1,2)=STR(P[1]:"00"),P4$(3,2)=FNYEAR_YY21$(P[2])
4070 WRITE (SYS01_DEV,KEY=N0$+N2$+"00")IOL=APS01A
4100 GOTO 9900
8000 REM " --- Functions"
8080 DEF FNH(Q$)=ASC(Q$)-32
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

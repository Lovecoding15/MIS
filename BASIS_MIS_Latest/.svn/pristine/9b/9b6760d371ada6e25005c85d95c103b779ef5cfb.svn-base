0010 REM "WOS - Work Order Header Report - II"
0020 REM "Program:  WOR.GB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "17Sep96 JJD: Include closed WO transactions in prior period if appropriate when printing "open" work order totals.(1705)"
0032 REM "17Sep96 JJD: Correct variable spelling.(535)"
0040 REM 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files"
0200 REM " --- IOLISTs"
0210 WOE01A: IOLIST A0$(1),A1$(1),A[ALL]
0220 IVM01A: IOLIST D0$,D1$(1)
0320 WOTRAN: IOLIST W0$,W1$,W[ALL]
0335 WOM10A: IOLIST H5$
0340 ARM01A: IOLIST *,H5$(1)
0500 REM " --- Init Data"
0520 DIM U[1],T[1],W[11]
0530 LET R0=121-C1-ITEM_LEN-(46+M2+M1)-2
0535 LET ENDDATE$=P4$
0540 IF R0>30 THEN LET R0=30
0550 LET HEADINGS$[2]=H3$
0560 LET HEADINGS$[3]=H4$
0570 LET HEADINGS$[4]=H6$
0600 CALL "SYC.GA",7,1,"","",STATUS
0610 IF STATUS THEN GOTO 9900
0620 LET V=5
0700 REM " --- Background"
0720 PRINT 'SB',@(29,13),"Printing:",'SF',
0800 REM " --- Options"
1000 REM " --- Init Read"
1010 LET K$=N0$+X9$+P8$
1020 READ (SOURCE_DEV,KEY=N0$+X9$+P8$,DOM=1100)
1030 GOTO 1300
1100 REM " --- Main Read"
1120 LET K$=KEY(SOURCE_DEV,END=4000)
1140 IF POS(N0$=K$)<>1 THEN GOTO 4000
1160 IF K$(3,LEN(X9$))<>X9$ THEN GOTO 4000
1180 IF P7$<>"" THEN IF K$(1,LEN(N0$+X9$+P7$))>N0$+X9$+P7$ THEN GOTO 4000
1190 IF P9$="W" THEN GOTO 1300
1200 REM " --- Breaks"
1220 IF K$(4,OFFSET)<>T0$ THEN GOSUB 6000
1240 LET K$=K$(1,2)+K$(OFFSET+4)
1260 IF P9$="C" THEN LET K$=K$(1,2)+K$(13,9)
1280 READ (SOURCE_DEV)
1300 PRINT @(40,13),K$(5),
1320 READ (WOE01_DEV,KEY=K$(1,11),DOM=1120)IOL=WOE01A
1322 IF ENDDATE$<>"" AND A0$(16,3)>ENDDATE$ THEN GOTO 1100
1325 IF POS("P"=STATUS$)>0 AND A0$(15,1)="P" THEN GOTO 1400
1330 IF POS("Q"=STATUS$)>0 AND A0$(15,1)="Q" THEN GOTO 1400
1335 IF POS("O"=STATUS$)>0 AND A0$(15,1)="O" THEN GOTO 1400
1350 IF POS("P"=STATUS$)=0 AND A0$(15,1)="P" THEN GOTO 1100
1355 IF POS("Q"=STATUS$)=0 AND A0$(15,1)="Q" THEN GOTO 1100
1360 IF POS("O"=STATUS$)=0 AND A0$(15,1)="O" THEN GOTO 1100
1365 IF POS("C"=STATUS$)>0 AND A0$(15,1)<>"C" THEN GOTO 1100
1370 IF POS("C"=STATUS$)=0 AND A0$(15,1)="C" AND ENDDATE$="" THEN GOTO 1100
1375 IF POS("O"=STATUS$)>0 AND POS("C"=STATUS$)=0 AND A0$(15,1)="C" AND A0$(31,3)<=ENDDATE$ THEN GOTO 1100
1380 IF POS("P"=STATUS$)>0 AND POS("C"=STATUS$)=0 AND A0$(15,1)="C" AND A0$(31,3)<=ENDDATE$ THEN GOTO 1100
1385 IF POS("Q"=STATUS$)>0 AND POS("C"=STATUS$)=0 AND A0$(15,1)="C" AND A0$(31,3)<=ENDDATE$ THEN GOTO 1100
1390 IF POS("C"=STATUS$)>0 AND LEN(ENDDATE$)>0 AND A0$(31,3)>ENDDATE$ THEN GOTO 1100
1400 IF A0$(14,1)<>"I" THEN GOTO 1500
1410 DIM D1$(60)
1420 LET D1$(1)="**Invalid Item**"
1430 FIND (IVM01_DEV,KEY=N0$+A0$(56,20),DOM=1440)IOL=IVM01A
1440 LET D1$(1)=FNITEM$(D1$,DESC[1],DESC[2],DESC[3])
1500 REM " --- Calculate Actuals "   
1520 IF A0$(15,1)="C" AND A0$(31,3)<=G9$ THEN GOSUB CLOSED ELSE GOSUB NOT_CLOSED
1700 REM " --- Print Closed Values "
1706 IF POS("C"=STATUS$)>0 OR A0$(15,1)="C" THEN GOTO 2000
1710 REM " --- Reduce By Closed Amount "
1715 IF ENDDATE$="" THEN LET A[1]=A[1]-A[3]
1716 IF A0$(31,3)<=ENDDATE$ THEN LET A[1]=A[1]-A[3]
2000 REM " --- Print"
2020 IF L+1>L9 THEN GOSUB 5200
2030 PRINT (7)A0$(5,7),@(18),A0$(12,2),@(22),A0$(14,1),@(25),A0$(15,1),@(45),A[0]:M2$,A[1]:M1$,
2035 IF A0$(15,1)="C" THEN LET STAT$="*Closed*" ELSE IF A0$(15,1)="P" THEN LET STAT$="*Plannd*" ELSE IF A0$(15,1)="Q" THEN LET STAT$="*Quoted*" ELSE GOTO 2060
2050 IF A0$(15,1)="C" THEN PRINT (7)@(8),STAT$,@(36),FNB$(A0$(31,3)),; GOTO 2110
2060 PRINT (7)@(8),FNB$(A0$(16,3)),@(36),FNB$(A0$(22,3)),
2110 PRINT (7)@(27),FNB$(A0$(19,3)),
2120 IF A0$(14,1)="I" THEN PRINT (7)@(46+M2+M1),D1$(1,DESCLEN),@(121-C1-ITEM_LEN),A0$(56,ITEM_LEN),J1$(1,C1),
2130 IF A0$(14,1)="N" THEN PRINT (7)@(121-C1-ITEM_LEN),"Special",J1$(1,10),
2140 IF POS(A0$(14,1)="NR")>0 THEN PRINT (7)@(46+M2+M1),A1$(1,R0),
2200 IF P3$(2,1)<>"Y" THEN GOTO 2300
2220 IF POS(" "<>A0$(34,6))>0 THEN PRINT (7)@(132-9-C1),FNF$(A0$(34,C0),C1$),
2230 IF POS(" "<>A0$(40,7))>0 THEN PRINT (7)@(132-7),A0$(40,7),
2300 PRINT (7)""
2320 LET T[0]=T[0]+A[0],T[1]=T[1]+A[1],U[0]=U[0]+A[0],U[1]=U[1]+A[1],L=L+1,L0=L0+1
2900 GOTO 1100
4000 REM " --- End"
4005 LET T9$="U"
4010 IF P9$<>"W" THEN GOSUB 6000
4020 IF H=0 THEN GOSUB 5000
4030 IF L+1>L9 THEN GOSUB 5000
4040 PRINT (7)@(45+M2),J$(1,M1-1),'LF',
4050 PRINT (7)@(20),"Grand Total: ",@(45+M2),U[1]:M1$
4100 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)"W.O.",@(10),"Date",@(18),"WO",@(29),"Start",@(38),"Comp",
5110 IF P3$(2,1)="Y" THEN PRINT (7)@(132-9-C1),"Customer",@(132-5),"Order",
5115 IF POS("C"=STATUS$)=0 THEN PRINT (7)@(45+M2-5),"Prod",@(45+M2+M1-8),"Current", ELSE PRINT (7)@(45+M2-6),"Close",@(45+M2+M1-6),"Close",
5120 PRINT (7)'LF',"Number",@(9),"Opened",@(17),"Type Ct St",@(30),"Date",@(38),"Date",@(45+M2-5),"Quan",@(45+M2+M1-6),"Value",@(46+M2+M1),"Description",@(121-C1-ITEM_LEN),"Bill No.",
5130 IF P3$(2,1)="Y" THEN PRINT (7)@(132-9-C1),"Number",@(132-6),"Number",
5140 PRINT (7)'LF'
5150 LET L=L+HEADINGS+2
5190 RETURN 
5200 REM " --- Sub Heading
5210 IF L>L9 THEN GOSUB 5000
5220 ON POS(P9$="TC") GOTO 5290,5240,5270
5240 PRINT (7)'LF',FNH$(T0$+"   "),H5$,'LF'
5250 LET L=L+3
5260 GOTO 5290
5270 PRINT (7)FNF$(T0$(1,C0),C1$)," ",H5$
5280 LET L=L+1
5290 RETURN 
6000 REM " --- First Sub Total
6010 IF T0$="" THEN GOTO 6100
6020 IF L0<=1 THEN PRINT (7)""; LET L=L+1; GOTO 6100
6030 IF L+2>L9 THEN GOSUB 5200
6040 IF P9$="B" THEN PRINT (7)@(45),J$(1,M2-1),
6050 PRINT (7)@(45+M2),J$(1,M1-1),'LF',
6080 IF P9$="B" THEN PRINT (7)@(45),T[0]:M2$,
6090 PRINT (7)@(10),H9$," ",FNH$(T0$+"   "),"Total: ",@(45+M2),T[1]:M1$,'LF'
6095 LET L=L+3
6100 IF T9$<>"" THEN RETURN 
6120 DIM T[1]
6140 LET T0$=K$(4,OFFSET),L0=0
6150 ON POS(P9$="TC") GOTO 6290,6160,6200
6160 DIM H5$(39); LET H5$(1)="*** Not On File ***"
6170 FIND (WOM10_DEV,KEY=N0$+"A"+T0$,DOM=6190)IOL=WOM10A
6180 LET H5$=H5$(6,20)
6185 GOSUB 5200
6190 GOTO 6290
6200 REM " --- Customer
6220 DIM H5$(30); LET H5$(1)="*** Not On File ***"
6240 FIND (ARM01_DEV,KEY=N0$+T0$,DOM=6120)IOL=ARM01A
6260 GOSUB 5200
6290 RETURN 
6400 REM " --- Process Closed Work Order Transactions "
6410 CLOSED: 
6420 LET WOTRAN_DEV=WOT03_DEV
6430 READ (WOTRAN_DEV,KEY=A0$(1,11),DOM=6440)
6440 LET K5$=KEY(WOTRAN_DEV,END=6500)
6450 IF K5$(1,11)<>A0$(1,11) THEN GOTO 6500
6460 IF ENDDATE$<>"" THEN IF K5$(12,3)>ENDDATE$ THEN GOTO 6500
6470 READ (WOTRAN_DEV,KEY=K5$)IOL=WOTRAN
6480 LET A[1]=A[1]+W[2]
6490 GOTO 6440
6500 IF WOTRAN_DEV=WOT03_DEV THEN LET WOTRAN_DEV=WOT23_DEV; GOTO 6430
6510 IF WOTRAN_DEV=WOT23_DEV THEN LET WOTRAN_DEV=WOT33_DEV; GOTO 6430
6520 RETURN 
6600 REM " --- Process Work Order Transactions "
6610 NOT_CLOSED: 
6620 LET WOTRAN_DEV=WOT01_DEV
6630 READ (WOTRAN_DEV,KEY=A0$(1,11),DOM=6640)
6640 LET K5$=KEY(WOTRAN_DEV,END=6700)
6650 IF K5$(1,11)<>A0$(1,11) THEN GOTO 6700
6660 IF ENDDATE$<>"" THEN IF K5$(12,3)>ENDDATE$ THEN GOTO 6700
6670 READ (WOTRAN_DEV,KEY=K5$)IOL=WOTRAN
6680 LET A[1]=A[1]+W[2]
6690 GOTO 6640
6700 IF WOTRAN_DEV=WOT01_DEV THEN LET WOTRAN_DEV=WOT21_DEV; GOTO 6630
6710 IF WOTRAN_DEV=WOT21_DEV THEN LET WOTRAN_DEV=WOT31_DEV; GOTO 6630
6720 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8080 DEF FNH$(Q$)=Q$(1,2)+CHR(32*POS("C"=X9$))+Q$(3)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

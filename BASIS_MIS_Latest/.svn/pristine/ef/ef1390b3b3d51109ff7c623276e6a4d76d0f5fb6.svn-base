0010 REM "IVM - Item Maintenance (Vendor Information)"
0020 REM "Program IVM.MF"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "25Sep96 JJD: When deleting vendor, delete from Inventory Vendor Detail file (4210)"
0032 REM "11Oct96 JWK: Update IVM-03 F record (240,340,510,4160-4180,4260-4280,6000-6290)
0033 REM "08Jan97 JWK: Error 11 when deleting new vendor (6230)
0034 REM "13Jan99 JAL: Vendor masks less than 6 characters not handled (1040)"
0040 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 PRECISION P[1]
0100 REM " --- Skip If AP Not Installed"
0110 IF AP$="N" THEN GOTO 4900
0200 REM " --- IOLIST's"
0220 IVM05A: IOLIST B0$(1),B1$(1),B2$(1),B[ALL]
0230 APM01A: IOLIST C0$(1),C1$(1)
0240 IVM02A: IOLIST S0$(1),S1$(1)
0320 IVM03D: IOLIST X1$(1)
0330 IVM03H: IOLIST X0$(1)
0340 IVM03F: IOLIST X2$(1)
0500 REM " --- Initializations"
0510 DIM B0$(28),B1$(32),B2$(20),B[12],C0$(8),C1$(30),X0$(29),X1$(29),X2$(37)
0550 LET M9$="##0-",M9=LEN(M9$)
0560 LET LP$=" For The Last Purchase Order For This Vendor/Item"
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,10,0
0720 CALL "SYC.WC",1,0,80,0,0,14,0
0750 PRINT 'SB',@(14,6),"Vendor:",@(3,7),"Primary/Secondary:",@(0,8),"Vendor's Item Number:",@(11,9),"Lead Time:",@(8,10)," Quantity Discounts ",@(4,11),"Level 1 Quantity:",@(39),"Level 1 Cost:",@(4,12),"Level 2 Quantity:",@(39),"Level 2 Cost:",@(4,13),"Level 3 Quantity:",@(39),"Level 3 Cost:",@(8,14)," Last Purchase ",@(2,15),"Last Purchase Date:",@(2,16),"Last Purchase Cost:",@(3,17),"Last PO Lead Time:",'SF',
0900 REM " --- Position file"
0990 READ (IVM03_DEV,KEY=A0$(1,2)+"H"+A0$(3),DOM=1000)
1000 REM " --- Vendor"
1005 PRINT 'CF',
1010 LET V2$="",V2$=KEY(IVM03_DEV,END=1040)
1020 IF POS(A0$(1,2)+"H"+A0$(3)=V2$)<>1 THEN LET V2$=""
1030 IF V2$<>"" THEN LET V2$=V2$(24)
1040 LET V0$="M",V1$="KCE",V3$=VENDMASK$,V0=6,V1=22,V2=6
1045 LET V4$="Enter A Valid Vendor (<F1>=List Vendors For This Item/<F3>=Lookup)"
1050 GOSUB 7000
1055 ON V3 GOTO 1200,1060,1000,1080,4900
1060 CALL "IVC.LV",IVM03_DEV,APM01_DEV,V1,V2,N0$,A0$(3),VENDLEN,VENDMASK$,V$
1070 GOTO 1200
1080 CALL "SYC.LV",V$
1090 IF LEN(V$)=0 THEN GOTO 1000
1200 REM " --- Retrieve record"
1205 IF LEN(V$)=0 THEN GOTO 0900
1210 LET B0$(1)=N0$+V$+A0$(3),C0$(1)=N0$+V$,C1$(1)=""
1220 LET X0$(1)=N0$+"H"+A0$(3)+V$,X1$=N0$+"D"+B0$(3)
1230 FIND (APM01_DEV,KEY=C0$,DOM=1000)IOL=APM01A
1240 FIND (IVM05_DEV,KEY=B0$,DOM=1400)IOL=IVM05A
1250 READ (IVM03_DEV,KEY=X0$,DOM=1300)
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 PRINT @(22,6),FNF$(B0$(3,VENDLEN),VENDMASK$)," ",C1$,
1420 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,1000
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Initializations for new record"
1510 DIM B1$(5),B2$(20),B[12]
1520 LET B1$(1,1)="S"
1590 GOSUB 5000
2000 REM " --- Primary/Secondary"
2010 LET V4$="Is This The Primary Or A Secondary Vendor"+FI$+" (P/S)?"
2020 LET V0$="S",V1$="C",V2$=B1$(1,1),V3$="PS",V0=1,V1=22,V2=7,I0=0
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 LET B1$(1,1)=V$
2090 GOTO 3900
2100 REM " --- Vendor's Item Number"
2110 LET V4$="Enter The Vendor's Number For This Item"
2120 LET V0$="S",V1$="C",V2$=B2$(1,P[8]),V3$="",V0=P[8],V1=22,V2=8
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2150 LET B2$(1)=V$
2190 GOTO 3900
2200 REM " --- Lead Time"
2210 LET V4$="Enter The Lead Time For This Vendor/Item"
2220 LET V0$="N",V1$="C",V2$=STR(B[8]),V3$=M9$,V0=M9,V1=22,V2=9
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 IF V<0 OR V>999 THEN GOTO 2200
2260 LET B[8]=V
2290 GOTO 3900
2300 REM " --- Quantity Break"
2310 LET X=(I0-1)/2,V1$="C",V2$=STR(B[X-1]),V3$=M2$,V0=M2,V1=22,V2=10+X
2320 LET V0$="N",V4$="Enter The Level "+STR(X)+" Quantity For This Vendor"
2330 GOSUB 7000
2340 ON V3 GOTO 2350,2300,3900,2300,3900
2350 IF V<0 THEN GOTO 2300
2360 LET B[X-1]=V
2390 GOTO 3900
2400 REM " --- Cost Break"
2410 LET X=(I0-2)/2,V1$="C",V2$=STR(B[X+2]),V3$=M3$,V0=M3,V1=53,V2=10+X
2420 LET V0$="N",V4$="Enter The Level "+STR(X)+" Cost For This Vendor"
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 IF V<0 THEN GOTO 2400
2460 LET B[X+2]=V
2490 GOTO 3900
2500 REM " --- Last Purchase Date"
2510 LET V4$="Enter The Last Date That This Item Was Purchased From This Vendor"
2520 LET V0$="D",V1$="C",V2$=B1$(2,3),V3$="",V0=3,V1=22,V2=15
2530 GOSUB 7000
2540 ON V3 GOTO 2550,2500,3900,2500,3900
2550 LET B1$(2,3)=V$
2590 GOTO 3900
2600 REM " --- Last PO Cost"
2610 LET V4$="Enter The Unit Cost"+LP$
2620 LET V0$="N",V1$="C",V2$=STR(B[6]),V3$=M3$,V0=M3,V1=22,V2=16
2630 GOSUB 7000
2640 ON V3 GOTO 2650,2600,3900,2600,3900
2650 IF V<0 THEN GOTO 2600
2660 LET B[6]=V
2690 GOTO 3900
2700 REM " --- Last PO Lead Time"
2710 LET V4$="Enter The Lead Time"+LP$
2720 LET V0$="N",V1$="C",V2$=STR(B[7]),V3$=M9$,V0=M9,V1=22,V2=17
2730 GOSUB 7000
2740 ON V3 GOTO 2750,2700,3900,2700,3900
2750 IF V<0 OR V>999 THEN GOTO 2700
2760 LET B[7]=V
2790 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,2300,2400,2300,2400,2300,2400,2500,2600,2700,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="KC",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write"
4110 LET X0$(1)=N0$+"H"+A0$(3)+B0$(3),X1$=N0$+"D"+B0$(3)
4120 WRITE (IVM05_DEV,KEY=B0$)IOL=IVM05A
4130 WRITE (IVM03_DEV,KEY=X1$)IOL=IVM03D
4140 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03H
4160 REM " --- Other xrefs
4170 LET OPER$="W"
4180 GOSUB 6000
4190 GOTO 1000
4200 REM " --- Delete"
4210 REMOVE (IVM05_DEV,KEY=B0$,DOM=4220)
4220 REMOVE (IVM03_DEV,KEY=X0$,DOM=4230)
4230 REMOVE (IVM03_DEV,KEY=X1$,DOM=4240)
4260 REM " --- Other xrefs
4270 LET OPER$="R"
4280 GOSUB 6000
4290 GOTO 1000
4900 REM " --- Run menu overlay"
4990 RUN "IVM.MA"
5000 REM " --- Display record"
5010 LET WHEN$="",COST$="",LEADTIME$=""
5020 IF FNP$(B1$(2,3))="" THEN GOTO 5050
5030 LET WHEN$=FNB$(B1$(2,3)),COST$=STR(B[6]:M3$),LEADTIME$=STR(B[7]:M9$)
5050 PRINT @(22,6),FNF$(B0$(3,VENDLEN),VENDMASK$)," ",C1$,@(22,7),B1$(1,1),@(22,8),B2$,@(22,9),B[8]:M9$,@(22,11),B[0]:M2$,@(53),B[3]:M3$,@(22,12),B[1]:M2$,@(53),B[4]:M3$,@(22,13),B[2]:M2$,@(53),B[5]:M3$,@(22,15),WHEN$,@(22,16),COST$,@(22,17),LEADTIME$,
5090 RETURN 
6000 REM " --- Update Warehouse xrefs
6010 READ (IVM03_DEV,KEY=N0$+"G"+A0$(3),DOM=6011)
6050 LET K_G$=KEY(IVM03_DEV,END=6290)
6060 IF POS(N0$+"G"+A0$(3)=K_G$)<>1 THEN GOTO 6290
6065 LET WHS$=K_G$(24,2)
6110 IF A2$(24,1)="I" THEN LET BUYER$=A2$(9,3),WHS$=FILL(2); GOTO 6150
6120 DIM S0$(24),S1$(64)
6130 READ (IVM02_DEV,KEY=N0$+WHS$+A0$(3))IOL=IVM02A
6140 LET BUYER$=S1$(19,3)
6150 READ (IVM03_DEV,KEY=N0$+"H"+A0$(3),DOM=6151)
6160 LET K_H$=KEY(IVM03_DEV,END=6270)
6170 IF POS(N0$+"H"+A0$(3)=K_H$)<>1 THEN GOTO 6270
6200 REM " --- IVM03F: Buyer/Vend/Prod/Item/Whse
6210 LET X2$(1)=A0$(1,2)+"F"+BUYER$+K_H$(24,6)+A2$(1,3)+A0$(3)+WHS$
6220 IF OPER$="W" THEN WRITE (IVM03_DEV,KEY=X2$)IOL=IVM03F
6230 IF OPER$="R" THEN REMOVE (IVM03_DEV,KEY=X2$,DOM=6231)
6250 READ (IVM03_DEV,KEY=K_H$)
6260 GOTO 6160
6270 READ (IVM03_DEV,KEY=K_G$)
6280 GOTO 6050
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

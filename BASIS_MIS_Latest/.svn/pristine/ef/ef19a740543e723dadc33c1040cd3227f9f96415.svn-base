0010 REM "POR - Quality Assurance Rejection History Report (Print Overlay)
0020 REM "Program POR.SB
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "21May96 Caj: Report's totals were wrong & some rej codes didn't show in totals (6840,2220)
0032 REM "20Nov96 Caj: Report was printing for whses other than one selected (1610)
0040 REM 
0080 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 APM01A: IOLIST D0$(1),D1$(1)
0230 IVM01A: IOLIST *,X1$(1)
0250 POM03A: IOLIST *,REJDESC$(1)
0260 POT03A: IOLIST A0$(1),A1$(1),A2$,A[ALL]
0270 POT04A: IOLIST B0$(1),B1$(1),B2$,B3$,B[ALL]
0280 POT14A: IOLIST C0$(1),C1$(1),C2$(1),C3$(1),C4$(1),C5$,C6$,C[ALL]
0290 POW16A: IOLIST S0$(1),REJDESC$(1),S[ALL]
0500 REM " --- Init Data
0510 PRECISION IVPRECISION
0520 DIM A0$(28),A1$(41),A[6]
0530 LET FIRSTTIME=1
0550 LET MPCNT$="-##0.00%",MPERCENT=LEN(MPCNT$)
0600 REM " --- Headings and print positions
0610 LET HEADINGS=4
0620 DIM HEADINGS$[HEADINGS],O[16]
0630 LET WIDTH=132,PAGE=0,L9=59,L=L9+1
0650 GOSUB ASSIGNHEADS
0760 POSITIONS: 
0770 LET O[1]=O[0]+8,O[2]=O[1]+4,O[3]=O[2]+9,O[4]=O[3]+3
0780 LET O[5]=O[4]+4,O[6]=O[5]+4,O[7]=O[3]+ITEMLEN+1,O[8]=O[6]+31
0790 LET O[9]=O[8]+4,O[10]=O[7]+48-ITEMLEN,O[11]=O[10]+2,O[12]=O[11]+M2
0800 LET O[13]=O[12]+M2,O[14]=O[13]+M2,O[15]=O[14]+M3,O[16]=O[15]+M1
0810 IF O[16]+8<WIDTH THEN GOTO 0950
0820 LET O[16]=WIDTH-MPERCENT,O[15]=O[16]-M1,O[14]=O[15]-M3
0830 LET O[13]=O[14]-M2,O[12]=O[13]-M2,O[11]=O[12]-M2
0950 REM " --- Position files
0960 CALL "SYC.GA",7,1,"","",STATUS
0970 IF STATUS THEN GOTO 9900
0980 CALL "SYC.NB","Printing",22,COLUMN
1000 REM " --- Init read POT-04
1020 READ (POT04_DEV,KEY=N0$+VENDFROM$,DOM=READRCPTHIST)
1100 REM " --- Read through receipt headers
1110 READRCPTHIST: 
1120 LET K$=KEY(POT04_DEV,END=TOTALS)
1130 IF POS(N0$=K$)<>1 THEN GOTO TOTALS
1140 DIM B0$(22),B1$(160),B[8]
1150 READ (POT04_DEV,KEY=K$)IOL=POT04A
1255 REM " --- Test Ranges
1260 IF VENDTHRU$<>FILL(6) AND B0$(3,6)>VENDTHRU$ THEN GOTO TOTALS
1265 REM " --- Test Ranges
1270 IF DATEFROM$<>"" AND B1$(29,6)<DATEFROM$ THEN GOTO NEXTRCPTHIST
1280 IF DATETHRU$<>"" AND B1$(29,6)>DATETHRU$ THEN GOTO NEXTRCPTHIST
1400 REM " --- Assign POT-04 variables
1410 DIM RCPTDATE$(8)
1420 IF B1$(29,6)<>FILL(6) THEN LET RCPTDATE$=FNB6$(B1$(29,6))
1440 LET PONUM$=B0$(9,7),VENDOR$=B0$(3,6)
1450 READ (POT14_DEV,KEY=B0$,DOM=READDETAIL)
1500 REM " --- Get detail data
1510 READDETAIL: 
1520 DIM C0$(25),C1$(48),C2$(32),C3$(22),C4$(40),C[12]
1530 LET K14$=KEY(POT14_DEV,END=NEXTRCPTHIST)
1540 IF POS(B0$=K14$)<>1 THEN GOTO NEXTRCPTHIST
1560 READ (POT14_DEV)IOL=POT14A
1600 REM " --- Correct Warehouse?
1610 IF WHSEFOR$<>"" AND C3$(1,2)<>WHSEFOR$ THEN GOTO NEXTDETAIL
1700 REM " --- Assign POT-14 data 
1705 ASSIGNDETAIL: 
1720 LET REC$=C0$(16,7),POSEQ$=C0$(23,3),ITEM$=C3$(3,20),UNITMEAS$=C1$(22,2)
1730 LET UNITCOST=C[1],QTYORD=C[3],QTYRECD=C[7]
1740 LET STARTREJ$=K14$(1,2)+VENDOR$+REC$+PONUM$+POSEQ$
1750 GOSUB SUMREJQTYS
1760 IF REJRECS=0 THEN GOTO NEXTDETAIL
1800 REM " --- Get Item Desc 
1820 DIM ITEMDESC$(60),X1$(60)
1830 LET ITEMDESC$(1)="Not On File"
1840 FIND (IVM01_DEV,KEY=N0$+ITEM$,DOM=1900)IOL=IVM01A
1850 LET ITEMDESC$(1)=FNITEM$(X1$,DESCLEN[1],DESCLEN[2],DESCLEN[3])
1900 REM " --- Test breaks
1920 IF VENDOR$=PREVVENDOR$ THEN GOTO TESTED
1940 GOSUB NEWVENDOR
1950 TESTED: 
1980 PRINT @(COLUMN,11),FNF$(VENDOR$(1,VENDLEN),VENDOMASK$)," ",PONUM$," ",B0$(16,7)
1990 GOSUB PRINTLINE
2000 REM " --- Go print rejection data
2010 GOSUB PRINTREJLINES
2200 REM " --- Loop up for next receipt hist detail
2210 NEXTDETAIL: 
2230 GOTO READDETAIL
2400 REM " --- Loop up for receipt hist record
2410 NEXTRCPTHIST: 
2460 GOTO READRCPTHIST
3000 REM " --- Print totals from work file
3005 TOTALS: 
3010 LET M1$=EXTMASK$,M1=LEN(M1$),M2$=QTYMASK$,M2=LEN(M2$)
3015 LET DOINGTOTALS=1,COLSUSED=WIDTH-35-M2-M1-MPERCENT
3020 LET STARTCOL=INT(COLSUSED/2)
3030 DIM OTOT[4]
3040 LET OTOT[0]=STARTCOL,OTOT[1]=OTOT[0]+4,OTOT[2]=OTOT[1]+31,OTOT[3]=OTOT[2]+M2,OTOT[4]=OTOT[3]+M1
3050 GOSUB 5000
3060 DIM S0$(5),REJDESC$(30),S[1]
3070 READ (POW16_DEV,KEY="",DOM=3080)
3080 READ (POW16_DEV,END=4000)IOL=POW16A
3090 LET REJCODE$=S0$(3,3),TOTQTYREJ=S[0],TOTAMTREJ=S[1]
3100 PRECISION 4
3110 LET TOTPERCENT=0
3120 IF TOTALRPTVALUE<>0 THEN LET TOTPERCENT=TOTAMTREJ/TOTALRPTVALUE*100
3150 PRINT (7)@(OTOT[0]),REJCODE$,@(OTOT[1]),REJDESC$,@(OTOT[2]),TOTQTYREJ:M2$,@(OTOT[3]),TOTAMTREJ:M1$,@(OTOT[4]),TOTPERCENT:MPCNT$
3190 GOTO 3080
4000 REM " --- All Done
4090 GOTO 9900
5000 REM " --- Heading
5010 LET L=HEADING+1
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS>0 THEN EXITTO 9900
5050 IF DOINGTOTALS THEN GOTO 5140
5060 PRINT (7)@(O[8]),"Rej",@(O[12]-9),"Quantity",@(O[13]-9),"Quantity",@(O[14]-9),"Quantity",@(O[15]-5),"Unit"
5070 PRINT (7)@(O[0]),"PO #",@(O[1]),"Seq",@(O[2]),"Received",@(O[3]),"Item",@(O[5]),"Reason",@(O[8]),"By",@(O[10]),"UM",@(O[12]-9),"Ordered",@(O[13]-9),"Received",@(O[14]-9),"Rejected",@(O[15]-5),"Cost",@(O[16]-10),"Extension",@(O[16]),"Percent"
5080 PRINT (7)""
5090 LET L=L+3
5100 GOTO 5190
5140 REM " --- Totals page headers
5150 PRINT (7)@(OTOT[2]+M2-9),"Quantity",@(OTOT[3]+M1-7),"Total"
5160 PRINT (7)@(OTOT[0]),"Reason",@(OTOT[2]+M2-9),"Rejected",@(OTOT[3]+M1-7),"Amount",@(OTOT[4]),"Percent"
5170 PRINT (7)""
5180 LET L=L+3
5190 RETURN 
5300 REM " --- Assign headings variables
5305 ASSIGNHEADS: 
5310 DIM TMPH3$(WIDTH),TMPH4$(WIDTH)
5320 IF VENDFROM$=FILL(6) THEN LET VENDF$="First" ELSE LET VENDF$=FNF$(VENDFROM$(1,VENDLEN),VENDOMASK$)
5330 IF VENDTHRU$=FILL(6) THEN LET VENDT$="Last" ELSE LET VENDT$=FNF$(VENDTHRU$(1,VENDLEN),VENDOMASK$)
5340 IF DATEFROM$="" THEN LET DATEF$="First" ELSE LET DATEF$=FNB6$(DATEFROM$)
5350 IF DATETHRU$="" THEN LET DATET$="Last" ELSE LET DATET$=FNB6$(DATETHRU$)
5360 IF WHSEFOR$="" THEN LET WHSEHEAD$="All Warehouses" ELSE LET WHSEHEAD$="Warehouse "+WHSEFOR$+" "+WHSEFNAME$
5370 LET TMPH3$(1)="From Vendor: "+VENDF$+" "+VENDFNAME$(1,20)
5380 LET TMPH3$(WIDTH-LEN(DATEF$)-18)="From Receipt Date: "+DATEF$
5390 LET TMPH4$(1)="  To Vendor: "+VENDT$+" "+VENDTNAME$(1,20)
5400 LET TMPH4$(WIDTH-LEN(DATEF$)-18)="  To Receipt Date: "+DATET$
5410 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$+" For "+WHSEHEAD$
5420 LET HEADINGS$[2]=TMPH3$,HEADINGS$[3]=TMPH4$
5490 RETURN 
6100 REM " --- New Vendor
6105 NEWVENDOR: 
6110 DIM D0$(8),D1$(195)
6130 LET VENDNAME$="Not On File",PREVVENDOR$=VENDOR$
6150 FIND (APM01_DEV,KEY=N0$+PREVVENDOR$,DOM=6170)IOL=APM01A
6160 LET VENDNAME$=D1$(1,30)
6170 IF FIRSTTIME THEN GOTO 6210
6180 IF L+3>L9 THEN GOSUB 5000
6190 PRINT (7)""
6200 LET L=L+1
6210 IF L+2>L9 THEN GOSUB 5000
6220 PRINT (7)@(0),FNF$(PREVVENDOR$(1,VENDLEN),VENDOMASK$)," ",VENDNAME$
6230 LET L=L+1,FIRSTTIME=0
6290 RETURN 
6300 REM " --- Accumulate rejection qtys by rej code in workfile
6305 WORKFILE: 
6310 DIM S0$(5),S[1]
6320 LET S0$=A0$(1,2)+A1$(1,3)
6330 FIND (POW16_DEV,KEY=S0$,DOM=6340)IOL=POW16A
6340 LET S[0]=S[0]+QTYREJ
6350 LET S[1]=S[1]+EXTENSION
6470 WRITE (POW16_DEV,KEY=S0$)IOL=POW16A
6490 RETURN 
6500 REM " --- Print POT-14 data line
6505 PRINTLINE: 
6510 PRECISION 2
6515 LET TOTALEXT=UNITCOST*TOTREJ
6520 LET TOTALRPTVALUE=TOTALRPTVALUE+TOTALEXT
6525 PRECISION 4
6530 LET TOTALPCNT=0
6535 IF QTYORD<>0 THEN LET TOTALPCNT=(TOTREJ/QTYORD)*100
6540 PRECISION IVPRECISION
6560 IF L+1>L9 THEN GOSUB 5000
6570 PRINT (7)@(O[0]),PONUM$,@(O[1]),POSEQ$,@(O[2]),RCPTDATE$,@(O[3]),ITEM$(1,ITEMLEN),@(O[7]),ITEMDESC$(1,47-ITEMLEN),@(O[10]),UNITMEAS$,@(O[11]),QTYORD:M2$,@(O[12]),QTYRECD:M2$,@(O[13]),TOTREJ:M2$,@(O[14]),UNITCOST:M3$,@(O[15]),TOTALEXT:M1$,@(O[16]),TOTALPCNT:MPCNT$
6580 LET L=L+1
6590 RETURN 
6600 REM " --- Print POT-03 rejection data lines
6605 PRINTREJLINES: 
6620 READ (POT03_DEV,KEY=STARTREJ$,DOM=READPOT03)
6630 REM " --- Read through req headers
6640 READPOT03: 
6650 LET K03$=KEY(POT03_DEV,END=DONEPOT03)
6660 IF POS(STARTREJ$=K03$)<>1 THEN GOTO DONEPOT03
6670 READ (POT03_DEV,KEY=K03$)IOL=POT03A
6680 REM " --- Assign POT-03 variables
6690 LET REJSEQ$=A0$(26,3),REJCODE$=A1$(1,3),OPCODE$=A1$(4,3),RANUM$=A1$(7,15)
6700 LET QTYREJ=A[0]
6710 PRECISION 2
6720 LET EXTENSION=UNITCOST*QTYREJ
6730 LET PERCENT=0
6740 IF QTYORD<>0 THEN LET PERCENT=QTYREJ/QTYORD*100
6750 PRECISION IVPRECISION
6760 REM " --- Get rejection desc 
6765 DIM REJDESC$(30)
6770 LET REJDESC$(1)="Not On File"
6780 FIND (POM03_DEV,KEY=A0$(1,2)+A1$(1,3),DOM=6790)IOL=POM03A
6790 IF L+1>L9 THEN GOSUB 5000
6800 PRINT (7)@(O[4]),REJSEQ$,@(O[5]),REJCODE$,@(O[6]),REJDESC$,@(O[8]),OPCODE$,
6810 IF RANUM$<>FILL(15) THEN PRINT (7)@(O[9]),"(RA # ",RANUM$,")",
6820 PRINT (7)@(O[13]),QTYREJ:M2$,@(O[15]),EXTENSION:M1$,@(O[16]),PERCENT:MPCNT$
6830 LET L=L+1
6840 GOSUB WORKFILE
6860 GOTO READPOT03
6880 DONEPOT03: 
6890 RETURN 
6900 REM " --- Add up quantities rejected for this PO/Rec/PO Line Num
6905 SUMREJQTYS: 
6910 DIM A0$(28),A1$(41),A[6]
6920 LET REJRECS=0,TOTREJ=0
6930 READ (POT03_DEV,KEY=STARTREJ$,DOM=READREJHIST)
6935 READREJHIST: 
6940 LET K03$=KEY(POT03_DEV,END=DONESUM)
6950 IF POS(STARTREJ$=K03$)<>1 THEN GOTO DONESUM
6960 READ (POT03_DEV,KEY=K03$)IOL=POT03A
6970 LET REJSEQ$=A0$(26,3),REJCODE$=A1$(1,3),OPCODE$=A1$(4,3),RANUM$=A1$(7,15)
6980 LET TOTREJ=TOTREJ+A[0],REJRECS=REJRECS+1
6985 GOTO READREJHIST
6990 DONESUM: 
6995 RETURN 
8000 REM " --- Functions
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

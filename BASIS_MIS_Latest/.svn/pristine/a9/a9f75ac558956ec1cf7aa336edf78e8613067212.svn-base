0010 REM "ARM - Distribution Code Hardcopy"
0020 REM "Program ARM.DB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0090 SETERR 9000
0095 SETESC 9000
0100 REM "--- Open/Lock Files "
0110 CALL "SYC.GA",7,1,"","",STATUS
0115 IF STATUS THEN GOTO 9900
0200 REM " ---IOLISTS "
0210 ARM10D: IOLIST A0$(1),A1$(1)
0220 GLM01A: IOLIST B0$(1),B1$(1)
0500 REM " --- Initialize Data "
0510 DIM HEADINGS$[2],BUF$(36),TITLES$[10]
0540 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$+" Listing",NUMBER=0
0550 LET L9=59,L=L9+1,WIDTH=80,PAGE=0,CLOCK$="",WHEN$=F0$(7,3)
0560 LET TITLES$[1]="AR",TITLES$[2]="Cash",TITLES$[3]="Sales",A0$(1)=N0$+"D"
0570 LET TITLES$[4]="Discount",TITLES$[5]="Freight",TITLES$[6]="Inventory"
0580 LET TITLES$[7]="COGS",TITLES$[8]="COGS Adj",TITLES$[9]="PPV"
0590 LET TITLES$[10]="Purchases"
0700 REM " --- Background "
0710 CALL "SYC.NB","Printing",2,COL
0900 REM " --- Initial Read "
0990 READ (ARM10_DEV,KEY=A0$(1,3),DOM=1000)
1000 REM " --- Main Read "
1010 READ (ARM10_DEV,END=4000)IOL=ARM10D
1200 REM " --- Breaks "
1210 IF POS(N0$+"D"=A0$)<>1 THEN GOTO 4000
2000 REM " --- Print "
2020 IF L+10>L9 THEN GOSUB 5000
2030 PRINT @(COL,11),A0$(4,2),
2040 PRINT (7)""
2050 PRINT (7)A0$(4,2),@(6),A0$(6,20),
2060 IF GL$="Y" THEN GOTO 2100
2070 PRINT (7)""
2080 LET L=L+2
2090 GOTO 3900
2100 REM " --- Print GL accounts"
2110 LET XLIMIT=7
2120 IF PO$="Y" THEN LET XLIMIT=10
2130 FOR X=1 TO XLIMIT
2140 LET V=(X-1)*10+1
2150 IF X>5 THEN LET V=X*10+1
2160 LET B0$(1)=N0$+A1$(V,10),B1$(1)="",BUF$(1)="",V$=TITLES$[X]+" Account"
2170 FIND (GLM01_DEV,KEY=B0$,DOM=2180)IOL=GLM01A
2180 LET BUF$(1)=FNF$(B0$(3,G[3]),G5$)+" "+B1$
2190 PRINT (7)@(43-LEN(V$)),V$," ",BUF$
2200 NEXT X
2210 LET L=L+10
3900 REM " --- Loop back for next record"
3910 LET NUMBER=NUMBER+1
3990 GOTO 1000
4000 REM " --- All Done"
4010 IF L+2>L9 THEN GOSUB 5000
4020 PRINT (7)""
4030 PRINT (7)NUMBER," Distribution Codes"
4090 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+2
5020 CALL "SYC.HA",7,HEADINGS$[ALL],2,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS THEN EXITTO 9900
5040 PRINT (7)"Code  Description",@(26),FILL(20,"-")," GL Accounts ",FILL(21,"-")
5090 RETURN 
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

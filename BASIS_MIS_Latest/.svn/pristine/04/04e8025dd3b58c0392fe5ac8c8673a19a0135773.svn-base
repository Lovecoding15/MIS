0010 REM "SYM - User ID Maintenance"
0020 REM "Program SYM.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "02Aug96 WGH: Clear screen color selectin via <F1> (3695)"
0032 REM "13Nov96 WGH: Add Default Firm for login initialization when multi-firm (710,730,750,3000-3080,3960,5040,5350-5380,5510)"
0033 REM "23Oct96 JWK: Use correct Row/Col info and prevent ERR42 (3605)
0034 REM "24Apr97 JWK: Check for duplicate printer names by 2's (2730)
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=8
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="SYM-01",FILES$[3]="SYM-02"
0130 LET FILES$[4]="SYM-06",FILES$[5]="SYM-07",FILES$[6]="SYM-12"
0140 LET FILES$[7]="SYM-22"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],SYM01_DEV=CHANNELS[2],SYM02_DEV=CHANNELS[3]
0180 LET SYM06_DEV=CHANNELS[4],SYM07_DEV=CHANNELS[5],SYM12_DEV=CHANNELS[6]
0190 LET SYM22_DEV=CHANNELS[7]
0200 REM " --- IOLIST's"
0210 SYM02A: IOLIST A0$(1),A1$(1),A[ALL]
0220 SYM01A: IOLIST B0$(1),B1$(1),B2$(1)
0240 SYM12A: IOLIST X0$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0500 REM " --- Initializations"
0510 DIM A0$(3),A1$(128),A[4],B0$(6),B1$(35),B2$(16),R[6],C[6],X[6]
0520 DIM X0$(5),FIRMS$(36),OLDFIRMS$(36),PRINTERS$(36),OLDPRINTERS$(36)
0530 DIM DESCRIPTION$[1],FIELD[1],POSITION[1],LENGTH[1],COLORS$[8],MESSAGE$[1]
0540 LET C[1]=23,C[2]=23,C[3]=41,C[4]=41,C[5]=59,C[6]=59
0550 LET R[1]=20,R[2]=21,R[3]=20,R[4]=21,R[5]=20,R[6]=21
0560 LET X[1]=41,X[2]=51,X[3]=61,X[4]=71,X[5]=81,X[6]=91
0570 LET COLORS$[1]="Black",COLORS$[2]="Blue",COLORS$[3]="Cyan"
0575 LET COLORS$[4]="Green",COLORS$[5]="Magenta",COLORS$[6]="Red"
0580 LET COLORS$[7]="White",COLORS$[8]="Yellow"
0590 LET M2$="#0",M2=LEN(M2$),NUMBER=1
0700 REM " --- Background"
0720 CALL "SYC.WC",1,0,80,0,0,12,0
0740 CALL "SYC.WC",1,0,80,0,0,18,0
0750 PRINT 'SB',@(14,3),"User ID:",@(17,4),"Name:",@(7,5),"Security Level:",@(10,6),"Valid Firms:",@(13,7),"Printers:",@(13,8),"Password:",@(9,9),"Default Menu:",@(9,10),"Default Firm:",@(6,12),"User Preferences",@(5,13),"Input Field Type:",@(29),"Maximum Number Of PF Files:",@(2,14),"Case Conversion Key:",@(45),"PF File ID:",@(7,15),"Upper Case Key:",@(37),"Allow Menu Jumping:",@(7,18),"Color Selection",@(29,19),"Base",@(45),"Window 1",@(61),"Window 2",@(11,20),"Background:",@(11,21),"Foreground:",'SF',
0900 REM " --- Position File"
0990 READ (SYM02_DEV,KEY="",DOM=1000)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(SYM02_DEV,END=1030)
1030 LET V0$="S",V1$="EK",V3$="",V4$="",V0=3,V1=23,V2=3
1040 LET V4$="Enter A Valid User ID (<F3>=Lookup)"
1050 GOSUB 7000
1060 ON V3 GOTO 1080,1000,1000,1070,4500
1070 GOSUB 6000
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(1)=V$
1200 REM " --- Find key"
1210 FIND (SYM02_DEV,KEY=A0$,DOM=1400)IOL=SYM02A
1220 IF FNP$(A1$(101,3))="" THEN LET A1$(101,3)=A0$
1300 REM " --- Display record"
1310 LET NOREADS=0
1320 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- New record initializations"
1510 DIM A1$(128),A[4]
1520 LET A[0]=9,A1$(25,1)="0",A1$(26,1)="B",A1$(27,1)="N",A1$(37,2)="12"
1530 LET A1$(39,2)="09",A1$(41,10)="BLACK",A1$(51,10)="WHITE"
1540 LET A1$(61,10)="BLACK",A1$(71,10)="WHITE",A1$(81,10)="BLACK"
1550 LET A1$(91,10)="WHITE",A1$(101,3)=A0$,A1$(104,6)="SYS",A1$(110,1)="N"
1580 LET NOREADS=0
1590 GOSUB 5000
2000 REM " --- User name"
2010 LET V3$="Enter The Name Of This User"
2020 LET V0$="S",V1$="C",V2$=A1$(1,24),V3$="",V0=24,V1=23,V2=4,I0=0
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 LET A1$(1,24)=V$
2090 GOTO 3900
2100 REM " --- Security level"
2110 LET V4$="Enter The Security Level For This User (0 Through 9)"
2120 LET V0$="S",V1$="C",V2$=A1$(25,1),V3$="0123456789",V0=1,V1=23,V2=5
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2150 IF POS(V$="0123456789")=0 THEN GOTO 2100
2160 LET A1$(25,1)=V$
2190 GOTO 3900
2200 REM " --- Valid firms"
2210 FOR X=1 TO 18
2220 LET P=(X-1)*2+1,V4$="Enter A Valid Firm ID (<F1>=All/<F3>=Lookup/<F4>=End)"
2230 LET V0$="S",V1$="C",V2$=FNP$(FIRMS$(P,2)),V3$="",V0=2,V1=(X-1)*3+23,V2=6
2240 GOSUB 7000
2250 ON V3 GOTO 2350,2260,2300,2330,2290
2260 LET FIRMS$(1)=""
2270 PRINT @(23,V2),"All",'CL',
2280 EXITTO 3900
2290 LET V3=0
2295 EXITTO 2400
2300 LET X=X-1
2310 IF X<1 THEN EXITTO 2400
2320 GOTO 2220
2330 CALL "SYC.LX",V$
2350 IF V$="  " THEN GOTO 2380
2355 IF V$="" THEN EXITTO 2400
2360 LET V=NUM(V$,ERR=2220),V$=STR(V:"00")
2370 FIND (SYM06_DEV,KEY=V$,DOM=2220)
2380 LET FIRMS$(P,2)=V$
2385 PRINT @(V1,V2),FIRMS$(P,2),
2390 NEXT X
2400 REM " --- Redisplay firms"
2405 DIM X$(36)
2410 LET X$(1)=FNS$(FIRMS$),FIRMS$(1)="",W=1
2420 FOR X=1 TO 18
2425 LET P=(X-1)*2+1,V$=X$(P,2)
2430 IF POS(V$=FIRMS$)=0 THEN LET FIRMS$(W)=V$,W=W+2
2435 NEXT X
2450 LET X$=FNP$(FIRMS$),FIRMS$(1)=SSORT(X$,2)
2460 FOR X=1 TO 18
2470 LET P=(X-1)*2+1,V1=(X-1)*3+23
2475 PRINT @(V1,V2),FIRMS$(P,2),
2480 NEXT X
2485 IF FNP$(FIRMS$)="" THEN PRINT @(23,V2),"All",'CL',
2490 GOTO 3900
2500 REM " --- Valid printers"
2510 FOR X=1 TO 18
2520 LET P=(X-1)*2+1,V0=2,V4$="Enter A Valid Printer ID (<F3>=Lookup/<F4>=End)"
2530 LET V0$="S",V1$="C",V2$=FNP$(PRINTERS$(P,2)),V3$="",V1=(X-1)*3+23,V2=7
2540 GOSUB 7000
2550 ON V3 GOTO 2650,2500,2600,2630,2560
2560 LET V3=0
2590 EXITTO 2700
2600 LET X=X-1
2610 IF X<1 THEN EXITTO 2700
2620 GOTO 2520
2630 GOSUB 6100
2650 IF V$="  " THEN GOTO 2680
2655 IF V$="" THEN EXITTO 2700
2660 LET V$=V$+"  ",V$=V$(1,2)
2670 IF V$<>"PF" THEN FIND (SYM07_DEV,KEY=V$,DOM=2520)
2680 LET PRINTERS$(P,2)=V$
2685 PRINT @(V1,V2),PRINTERS$(P,2),
2690 NEXT X
2700 REM " --- Redisplay printers"
2705 DIM X$(36)
2710 LET X$(1)=FNS$(PRINTERS$),PRINTERS$(1)="",W=1
2720 FOR X=1 TO 18
2725 LET P=(X-1)*2+1,V$=X$(P,2)
2730 IF POS(V$=PRINTERS$,2)=0 THEN LET PRINTERS$(W)=V$,W=W+2
2735 NEXT X
2760 FOR X=1 TO 18
2770 LET P=(X-1)*2+1,V1=(X-1)*3+23
2775 PRINT @(V1,V2),PRINTERS$(P,2),
2780 NEXT X
2790 GOTO 3900
2800 REM " --- Password Set?"
2810 LET V0$="Y",V1$="C",V2$=A1$(27,1),V3$="",V0=1,V1=23,V2=8,V$=V2$
2815 IF V2$<>"Y" THEN GOTO 2850
2820 LET V4$="Has This User Defined A Password For Login (Y/N)?"
2830 GOSUB 7000
2840 ON V3 GOTO 2850,2800,3900,2800,3900
2850 LET A1$(27,1)=V$
2860 PRINT @(V1,V2),A1$(27,1),
2890 GOTO 3900
2900 REM " --- Default menu"
2905 IF FNP$(A1$(104,6))="" THEN LET A1$(104,6)="SYS"
2910 LET V0$="S",V1$="C",V2$=A1$(104,6),V3$="",V0=6,V1=23,V2=9
2915 LET V4$="Enter The Starting (Default) Menu For This User (<F3>=Lookup)"
2920 GOSUB 7000
2925 ON V3 GOTO 2935,2900,3900,2930,3900
2930 GOSUB 6200
2935 IF V$="" THEN LET V$="SYS"
2940 LET B0$(1)=V$
2945 FIND (SYM01_DEV,KEY=B0$,DOM=2900)IOL=SYM01A
2950 IF B2$(9,1)<>"A" THEN GOTO 2900
2955 IF FNP$(B2$(10,1))="" THEN GOTO 2980
2960 IF A1$(25,1)>=B2$(10,1) THEN GOTO 2980
2965 LET MESSAGE$[0]="This Menu Requires A Security Level Of "+$22$+B2$(10,1)+$22$
2967 LET MESSAGE$[1]=" Or Greater (<Enter>=Continue)"
2970 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
2975 IF V$<>"SPECIAL" THEN GOTO 2900
2980 PRINT @(V1,V2),'CL',B0$,@(V1+7,V2),B1$,
2985 LET A1$(104,6)=B0$
2990 GOTO 3900
3000 REM " --- Default Firm ID"
3005 LET V4$="Enter initial Firm ID to use after login when multi-firm"
3010 LET V0$="Z",V1$="C",V2$=A1$(111,2),V3$="",V0=2,V1=23,V2=10
3015 GOSUB 7000
3020 ON V3 GOTO 3025,3000,3900,3000,3900
3025 IF POS(" "<>FIRMS$,2)<>0 AND POS(V$=FIRMS$,2)=0 THEN GOTO 3000
3030 IF POS(" "<>FIRMS$,2)=0 THEN FIND (SYM06_DEV,KEY=V$,DOM=3000)
3035 LET A1$(111,2)=V$
3040 GOTO 3900
3050 REM " --- Input field type"
3055 LET V4$="Enter Input Field Type For This User (B=Brackets And Underline/R=Reverse Video)"
3060 LET V0$="S",V1$="KR^",V2$=A1$(26,1),V3$="BR",V0=1,V1=23,V2=13
3065 GOSUB 7000
3070 ON V3 GOTO 3080,3050,3900,3050,3900
3080 LET A1$(26,1)=V$
3090 GOTO 3900
3100 REM " --- Case Conversion Key"
3110 LET V0$="Z",V1$="C",V2$=A1$(37,2),V3$="",V0=2,V1=23,V2=14
3120 LET V4$="Enter The Function Key To Be Used For Initial Caps Format (<F1>=None)"
3130 GOSUB 7000
3140 ON V3 GOTO 3150,3170,3900,3100,3900
3150 LET V=NUM(V$,ERR=3100)
3153 IF V=0 THEN GOTO 3170
3155 IF V<9 THEN GOTO 3100
3160 LET A1$(37,2)=V$
3165 GOTO 3180
3170 LET A1$(37,2)=""
3180 PRINT @(V1,V2),A1$(37,2),
3190 GOTO 3900
3200 REM " --- Caps Conversion Key"
3210 LET V0$="Z",V1$="C",V2$=A1$(39,2),V3$="",V0=2,V1=23,V2=15
3220 LET V4$="Enter The Function Key To Be Used For All Caps Format (<F1>=None)"
3230 GOSUB 7000
3240 ON V3 GOTO 3250,3270,3900,3200,3900
3250 LET V=NUM(V$,ERR=3200)
3253 IF V=0 THEN GOTO 3270
3255 IF V<9 THEN GOTO 3200
3260 LET A1$(39,2)=V$
3265 GOTO 3280
3270 LET A1$(39,2)=""
3280 PRINT @(V1,V2),A1$(39,2),
3290 GOTO 3900
3300 REM " --- Maximum number of PF Files"
3310 LET V0$="N",V1$="C",V3$=M2$,V2$=STR(A[0]:M2$),V0=M2,V1=57,V2=13
3320 LET V4$="Enter The Maximum Number Of Print To File (PF) Files Permitted This User"
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3300,3900,3300,3900
3350 IF V<1 OR V>99 THEN GOTO 3300
3360 LET A[0]=V
3370 PRINT @(V1,V2),V:M2$,
3390 GOTO 3900
3400 REM " --- PF File ID"
3410 LET V0$="S",V1$="C^",V2$=A1$(101,3),V3$="",V0=3,V1=57,V2=14,STATUS=0
3420 LET V4$="Enter The 3 Character PF File ID For This User (Letters And Digits Only)"
3430 GOSUB 7000
3435 ON V3 GOTO 3440,3400,3900,3400,3900
3440 IF LEN(FNP$(V$))<>V0 THEN GOTO 3400
3450 FOR X=1 TO 3
3460 IF POS(V$(X,1)="0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ")=0 THEN LET STATUS=1
3470 NEXT X
3480 IF STATUS>0 THEN GOTO 3400
3500 REM " --- PF ID is OK"
3510 LET A1$(101,3)=V$
3520 PRINT @(V1,V2),A1$(101,3)
3590 GOTO 3900
3600 REM " --- Colors"
3605 LET X=I0-12,V0$="S",V1$="C",V2$="",V3$="",V0=16,V1=C[X],V2=R[X]
3610 LET V4$="Enter A Valid Screen Color (<F1>=Clear/<F3>=Lookup)"
3615 FOR W=1 TO 8
3620 IF FNP$(A1$(X[X],10))=FNU$(COLORS$[W]) THEN LET V2$=COLORS$[W]
3625 NEXT W
3640 GOSUB 7000
3645 ON V3 GOTO 3660,3690,3900,3650,3900
3650 GOSUB 6500
3660 LET V$=FNP$(FNU$(V$))
3665 IF V$="" THEN GOTO 3690
3670 FOR W=1 TO 8
3675 IF V$=FNU$(COLORS$[W]) THEN LET C1$(1)=COLORS$[W]; EXITTO 3700
3680 NEXT W
3685 GOTO 3600
3690 LET V$="",C1$(1)=""
3695 GOTO 3770
3700 REM " --- Make sure foreground and background are different"
3710 DIM X$(10)
3720 LET X$(1)=V$
3730 IF MOD(X,2)>0 THEN GOTO 3770
3740 IF X$<>A1$(X[X-1],10) THEN GOTO 3770
3750 IF X$<>FILL(10) THEN GOTO 3600
3770 LET A1$(X[X],10)=V$
3780 PRINT @(V1,V2),C1$,
3790 GOTO 3900
3800 REM " --- Allow menu jumping?"
3810 LET V4$="Should This User Be Permitted To "+$22$+"Jump"+$22$+" Directly To Menus (Y/N)?"
3820 LET V0$="Y",V1$="C",V2$=A1$(110,1),V3$="",V0=1,V1=57,V2=15
3830 GOSUB 7000
3840 ON V3 GOTO 3850,3800,3900,3800,3900
3850 LET A1$(110,1)=V$
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 3980
3915 IF V3<>2 THEN GOTO 3950
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,2500,2800,2900,3000,3050,3100,3200,3300,3400,3800,3600,3600,3600,3600,3600,3600,3980
3980 LET NOREADS=1
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="KC",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write record"
4110 GOSUB 6700
4120 LET CHANNEL=SYM12_DEV,V$=FNP$(FIRMS$)
4130 IF V$<>"" THEN GOSUB 6900
4140 LET CHANNEL=SYM22_DEV,V$=FNP$(PRINTERS$)
4150 IF V$<>"" THEN GOSUB 6900
4180 WRITE (SYM02_DEV,KEY=A0$)IOL=SYM02A
4190 GOTO 1000
4200 REM " --- Remove record"
4210 GOSUB 6700
4220 REMOVE (SYM02_DEV,KEY=A0$,DOM=4230)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4550 ON POS(V$="YN") GOTO 4500,4590,9900
4590 RUN "SYM.BB"
5000 REM " --- Display record"
5010 LET B0$(1)=A1$(104,6),B1$(1)=""
5020 FIND (SYM01_DEV,KEY=B0$,DOM=5030)IOL=SYM01A
5030 PRINT @(23,3),A0$,@(23,4),A1$(1,24),@(23,5),A1$(25,1),
5040 IF NOREADS THEN GOTO 5350
5050 LET FIRMS$(1)="",PRINTERS$(1)="",F=1,P=1
5090 READ (SYM12_DEV,KEY=A0$,DOM=5100)
5100 REM " --- Display firms"
5110 LET K$=KEY(SYM12_DEV,END=5190)
5120 IF POS(A0$=K$)<>1 THEN GOTO 5190
5130 READ (SYM12_DEV)
5140 LET FIRMS$(F)=K$(4),F=F+2
5160 GOTO 5100
5190 READ (SYM22_DEV,KEY=A0$,DOM=5200)
5200 REM " --- Display printers"
5210 LET K$=KEY(SYM22_DEV,END=5300)
5220 IF POS(A0$=K$)<>1 THEN GOTO 5300
5230 READ (SYM22_DEV)
5240 LET PRINTERS$(P)=K$(4),P=P+2
5260 GOTO 5200
5300 REM " --- Store original firms and printers"
5310 LET OLDFIRMS$(1)=FIRMS$,OLDPRINTERS$(1)=PRINTERS$
5350 REM " --- Initialize Default Firm ID"
5360 IF POS(" "<>FIRMS$,2)=0 THEN LET FIRSTFIRM$="01" ELSE LET FIRSTFIRM$=FIRMS$(POS(" "<>FIRMS$,2),2)
5370 IF FNP$(A1$(111,2))="" THEN LET A1$(111,2)=FIRSTFIRM$
5380 IF POS(" "<>FIRMS$,2)<>0 AND POS(A1$(111,2)=FIRMS$,2)=0 THEN LET A1$(111,2)=FIRSTFIRM$
5400 REM " --- Display firms and printers
5410 LET V1=23,V2=6,V$=FNP$(FIRMS$)
5420 IF V$<>"" THEN GOSUB 5600
5430 IF V$="" THEN PRINT @(V1,V2),"All",'CL',
5440 LET V1=23,V2=7,V$=FNP$(PRINTERS$)
5450 IF V$<>"" THEN GOSUB 5600
5500 REM " --- Display remainder of record"
5510 PRINT @(23,8),A1$(27,1),@(23,9),B0$," ",B1$,@(23,10),A1$(111,2),@(23,13),A1$(26,1),@(57),A[0]:M2$,@(23,14),A1$(37,2),@(57),A1$(101,3),@(23,15),A1$(39,2),@(57),A1$(110,1),
5520 FOR X=1 TO 6
5530 DIM C1$(16)
5540 FOR W=1 TO 8
5550 IF FNP$(A1$(X[X],10))=FNU$(COLORS$[W]) THEN LET C1$(1)=COLORS$[W]
5560 NEXT W
5570 PRINT @(C[X],R[X]),C1$,
5580 NEXT X
5590 RETURN 
5600 REM " --- Display firms/printers"
5610 PRINT @(V1,V2),'CL',
5620 FOR X=1 TO LEN(V$) STEP 2
5630 PRINT @(V1,V2),V$(X,2),
5640 LET V1=V1+3
5650 NEXT X
5690 RETURN 
6000 REM " --- User Lookup"
6010 LET CHANNEL=SYM02_DEV,TITLE$="ADD+ON User ID Lookup"
6030 LET DESCRIPTION$[0]="ID",LENGTH[0]=3
6040 LET DESCRIPTION$[1]="Name",LENGTH[1]=24
6090 GOTO 6400
6100 REM " --- Printer lookup"
6110 LET CHANNEL=SYM07_DEV,TITLE$="Valid Printers"
6120 LET DESCRIPTION$[0]="ID",LENGTH[0]=2
6130 LET DESCRIPTION$[1]="Description",LENGTH[1]=30
6190 GOTO 6400
6200 REM " --- Menu lookup"
6210 LET CHANNEL=SYM01_DEV,TITLE$="ADD+ON System Menus"
6220 LET DESCRIPTION$[0]="Menu",LENGTH[0]=6
6230 LET DESCRIPTION$[1]="Description",LENGTH[1]=35
6400 REM " --- Standard windowed lookup"
6410 LET FIELD[0]=1,POSITION[0]=1,FIELD[1]=2,POSITION[1]=1
6420 LET RECORD$="",V$="NO COMPANY"
6470 LET NUMBER=1,ROW=3,COLUMN=79-LENGTH[0]-LENGTH[1]-NUMBER*4
6480 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6490 RETURN 
6500 REM " --- Color lookup"
6510 LET WIDTH=28,HEIGHT=14,WIN_X=79-WIDTH,WIN_Y=3,MAXROW=HEIGHT-5,W=WIDTH-2
6520 DIM LLIST$[2,MAXROW,5],MORE$[2,MAXROW,1],G1$(W),HEADING$(W),FOOTING$(W)
6530 LET HEADING$(2)="Available Colors",FOOTING$(2)="F4=End"
6540 LET XMODE=5,V$=V2$+"|EOF",PAGE=1
6550 FOR W=1 TO 8
6560 LET G1$(2)=COLORS$[W],LLIST$[1,W,1]=G1$,LLIST$[1,W,2]=FNU$(COLORS$[W])
6570 NEXT W
6600 REM " --- Display color window"
6610 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,"",NAME$
6620 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
6640 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],V$,PAGE,MAXROW,NAME$,HEIGHT,WIDTH,V3
6650 CALL "SYC.WD",NAME$
6690 RETURN 
6700 REM " --- Remove old firms/printers"
6710 LET CHANNEL=SYM12_DEV,V$=FNP$(OLDFIRMS$)
6720 IF V$<>"" THEN GOSUB 6800
6730 LET CHANNEL=SYM22_DEV,V$=FNP$(OLDPRINTERS$)
6740 IF V$<>"" THEN GOSUB 6800
6790 RETURN 
6800 REM " --- Remove firm/printer records"
6810 FOR X=1 TO LEN(V$) STEP 2
6820 LET K$=A0$+V$(X,2)
6830 REMOVE (CHANNEL,KEY=K$,DOM=6840)
6840 NEXT X
6890 RETURN 
6900 REM " --- Write firm/printer records"
6910 FOR X=1 TO LEN(V$) STEP 2
6920 LET X0$(1)=A0$+V$(X,2)
6930 WRITE (CHANNEL,KEY=X0$)IOL=SYM12A
6940 NEXT X
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNS$(Q$)=CVS(Q$,1)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8095 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

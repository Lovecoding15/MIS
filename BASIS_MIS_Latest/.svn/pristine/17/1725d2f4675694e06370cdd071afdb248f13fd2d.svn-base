0010 REM "ARE - Cash Receipts Header"
0020 REM "Program ARE.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "31Mar97 WGH: Initialize !GLCONTROL based on current program (660)"
0032 REM "30Nov98 Caj: Delete extraneous opening of ARM-02 (135,193)
0040 REM 
0050 SETERR 9000
0055 SETESC 9000
0060 ON O0 GOTO 0070,1000
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=12
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARE-01",FILES$[2]="ARE-11",FILES$[3]="ARE-21"
0130 LET FILES$[4]="ARE-31",FILES$[5]="ART-01",FILES$[6]="ART-11"
0135 LET FILES$[7]="ARM-01",FILES$[8]="",FILES$[9]="ARM-10"
0140 LET FILES$[10]="SYS-01"
0160 CALL "SYC.DA",1,1,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0185 LET ARE01_DEV=CHANNELS[1],ARE11_DEV=CHANNELS[2],ARE21_DEV=CHANNELS[3]
0190 LET ARE31_DEV=CHANNELS[4],ART01_DEV=CHANNELS[5],ART11_DEV=CHANNELS[6]
0193 LET ARM01_DEV=CHANNELS[7],UNUSEDDEV=CHANNELS[8],ARM10_DEV=CHANNELS[9]
0195 LET SYS01_DEV=CHANNELS[10]
0200 REM " --- IOLists"
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0405 DIM P[2],G[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 FOR X=0 TO 4
0455 LET G[X]=NUM(G2$(X*2+1,2),ERR=0460)
0460 NEXT X
0465 LET G[2]=FNYY_YEAR(G2$(5,2))
0470 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0500 REM " --- Init Data"
0510 DIM A0$(8),B[10],C0$(40),W0$(50),C[1],W[1],Z[1],X[1],O[7]
0550 LET K0=1,K9=8,L0=10,L1=11,L9=20,M0=LEN(M0$),G5=LEN(G5$),G6=LEN(G6$)
0560 LET A0$(1,2)=N0$,C0$(1,5)=N0$+FILL(3)
0600 REM " --- Resize masks"
0610 CALL "SYC.RM",M1$,M1,10,10
0620 LET O[0]=1,O[1]=9,O[2]=16,O[3]=23,O[4]=34,O[5]=45,O[6]=56,O[7]=67
0650 REM " --- Additional File Opens"
0655 LET GL$="N",STATUS=0
0660 CALL "GLC.BA",ERR=0665,PGM(-2),N2$,GLW11$,GL$,STATUS
0665 IF STATUS THEN GOTO 9900
0670 IF GL$<>"Y" THEN GOTO 0700
0675 LET FILES$[11]="GLM-01",FILES$[12]=GLW11$,OPTIONS$[12]="C"
0680 CALL "SYC.DA",1,11,12,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0685 IF STATUS THEN GOTO 9900
0690 LET GLM01_DEV=CHANNELS[11],GLW11_DEV=CHANNELS[12]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,9,0
0740 PRINT 'SB',@(1,3),"Receipt Date:",@(5,5),"Customer:",@(0,6),"Cash Rec Code:",@(1,7),"Check Number:",@(31),"Cash Or Check:",@(55),"ABA Number:",@(7,8),"Amount:",@(58),"Balance:",'SF',
1000 REM " --- Date"
1010 PRINT 'CF',
1020 LET V0$="D",V1$="ER",V2$=F0$(7,3),V0=8,V1=15,V2=3
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1060 LET C0$(6,3)=V$,STATUS=0
1070 IF GL$="Y" THEN CALL "GLC.EA",C0$(6,3),"Y",PERIOD$,YEAR$,STATUS
1075 IF STATUS>99 THEN GOTO 1000
1080 PRINT 'SB',@(V1,V2),FNB$(C0$(6,3)),'SF',
4000 REM " --- Overlay"
4020 LET O0=1
4100 RUN "ARE.AB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

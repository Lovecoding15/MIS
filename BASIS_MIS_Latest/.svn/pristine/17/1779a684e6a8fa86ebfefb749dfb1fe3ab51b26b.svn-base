0010 REM "APR - Vendor Name & Address Listing"
0020 REM "Program APR.PA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APM-01",FILES$[2]="SYS-01"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET APM01_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLISTS"
0210 APM01A: IOLIST A0$,A1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0405 DIM P[2]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",H1$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0440 LET P[0]=NUM(P2$(1,2))
0500 REM " --- Initializations"
0510 DIM A1$(196),HEADINGS$[2],O[3]
0550 LET M0=LEN(M0$),M8$="(###)-###-####",WHEN$=F0$(7,3),CLOCK$=""
0560 LET L9=59,L=L9+1,HEADINGS=2,HEADINGS$[0]=N1$,HEADINGS$[1]=H1$
0570 LET BEGVEND$="",ENDVEND$="",WIDTH=80,PAGE=0,NUMBER=0
0580 LET O[0]=0,O[1]=MAX(M0+1,7),O[2]=M0+32,O[3]=WIDTH-LEN(M8$)
0590 LET CW=MIN(20,O[3]-O[2]-1)
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,6,0
0750 PRINT 'SB',@(22,3),"Beginning Vendor:",@(25,5),"Ending Vendor:",'SF',
0800 REM " --- Beginning Vendor"
0805 LET V4$="Enter Beginning Vendor Number (<F1>=First/<F3>=Lookup)"
0810 LET V0$="M",V1$="KCE",V2$=BEGVEND$,V3$=M0$,V0=6,V1=40,V2=3
0815 GOSUB 7000
0820 ON V3 GOTO 0840,0825,0800,0835,0900
0825 LET V$=""
0830 GOTO 0840
0835 CALL "SYC.LV",V$
0840 GOSUB 6000
0843 IF STATUS<>0 THEN GOTO 0800
0845 LET BEGVEND$=V$
0850 REM " --- Ending Vendor"
0855 LET V4$="Enter Ending Vendor Number (<F1>=Last/<F3>=Lookup)"
0860 LET V0$="M",V1$="KCE",V2$=ENDVEND$,V3$=M0$,V0=6,V1=40,V2=5
0865 GOSUB 7000
0870 ON V3 GOTO 0890,0875,0800,0885,0900
0875 LET V$=""
0880 GOTO 0890
0885 CALL "SYC.LV",V$
0890 IF V$<>"" THEN IF V$<BEGVEND$ THEN GOTO 0850
0893 GOSUB 6000
0895 IF STATUS<>0 THEN GOTO 0850
0897 LET ENDVEND$=V$
0900 REM " --- Correct?"
0910 LET V4$="Is The Above Information Correct (Y/N)?"
0915 LET V0$="Y",V1$="C",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
0920 GOSUB 7000
0925 ON V3 GOTO 0930,0900,0900,0900,9900
0930 ON POS(V$="YN") GOTO 0900,0950,0800
0950 REM " --- Position file"
0960 CALL "SYC.GA",7,1,"","",STATUS
0970 IF STATUS THEN GOTO 9900
0975 CALL "SYC.NB","Printing",M0,COLUMN
0980 LET K$=N0$+BEGVEND$
0985 READ (APM01_DEV,KEY=K$,DOM=1000)
0990 GOTO 1100
1000 REM " --- Get next key"
1010 LET K$=KEY(APM01_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 IF ENDVEND$<>"" THEN IF K$(3)>ENDVEND$ THEN GOTO 4000
1100 REM " --- Read next vendor"
1110 PRINT @(COLUMN,11),FNF$(K$(3,P[0]),M0$),
1120 READ (APM01_DEV,KEY=K$)IOL=APM01A
1130 LET NUMBER=NUMBER+1
1500 REM " --- Format output fields"
1510 LET PHONE$="",FAX$=""
1520 IF FNP$(A1$(112,10))<>"" THEN LET PHONE$=FNF$(A1$(112,10),M8$)
1530 IF FNP$(A1$(186,10))<>"" THEN LET FAX$=FNF$(A1$(186,10),M8$)
1540 LET ADDRESS$=A1$(31,72)+A1$(103,9)
1550 CALL "SYC.AA",ADDRESS$,24,3,9,30
2000 REM " --- Print vendor"
2010 LET X=2+LEN(ADDRESS$)/30,ADDRESS=LEN(FNP$(ADDRESS$))
2020 IF ADDRESS=0 THEN IF FAX$<>"" THEN LET X=X+1
2030 IF L+X>L9 THEN GOSUB 5000
2040 PRINT (7)""
2050 PRINT (7)@(O[0]),FNF$(A0$(3,P[0]),M0$),@(O[1]),A1$(1,30),@(O[2]),A1$(126,CW),@(O[3]),PHONE$
2060 LET L=L+2
2100 REM " --- Print address"
2110 IF ADDRESS>0 THEN GOTO 2150
2120 IF FAX$="" THEN GOTO 3900
2130 PRINT (7)@(O[3]),FAX$
2135 LET L=L+1
2140 GOTO 3900
2150 FOR X=1 TO ADDRESS STEP 30
2160 IF X=1 THEN LET X$=FAX$
2170 PRINT (7)@(O[1]),ADDRESS$(X,30),@(O[3]),X$
2180 LET L=L+1,X$=""
2190 NEXT X
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 LET X$="No"
4020 IF NUMBER>0 THEN LET X$=STR(NUMBER)
4030 IF L+2>L9 THEN GOSUB 5000
4040 PRINT (7)""
4050 PRINT (7)X$," Vendor(s) Listed"
4090 GOTO 9900
5000 REM " --- Report heading"
5010 LET L=HEADINGS+2
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)@(O[0]),"Number",@(O[1]),"Name/Address",@(O[2]),"Contact",@(O[3]),"Telephone/Fax"
5090 RETURN 
6000 REM " --- Retrieve and display vendor"
6010 LET STATUS=0
6020 IF V$="" THEN GOTO 6100
6030 LET A0$=N0$+V$,STATUS=1
6040 FIND (APM01_DEV,KEY=A0$,DOM=6190)IOL=APM01A
6050 LET STATUS=0,V$=A0$(3)
6060 PRINT @(V1,V2),FNF$(A0$(3,P[0]),M0$)," ",A1$(1,30),'CL',
6090 GOTO 6190
6100 REM " --- First/Last"
6110 LET X$="First"
6120 IF V2>3 THEN LET X$="Last"
6130 PRINT @(V1,V2),X$,'CL',
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

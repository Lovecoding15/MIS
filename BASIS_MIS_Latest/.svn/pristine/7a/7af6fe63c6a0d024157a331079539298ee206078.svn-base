0010 REM "GLX - Convert 5.2  G/L Files to 6.0 Format"
0020 REM "Program GLX60A"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Nov98 KJS: Don't pad masks (1580)
0032 REM "30Nov98 Caj: 10th segment of 5.2's GLM-01 2nd and 3rd strings weren't being written to 6.0's GLM-11 and GLM-21 (1140)
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=18
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="GLM-01",FILES$[2]="GLM-10",FILES$[3]="GLM-11"
0130 LET FILES$[4]="GLM-12",FILES$[5]="GLM-21",FILES$[6]="GLM-22"
0135 LET FILES$[7]="GLM-32",FILES$[8]="GLR-01",FILES$[9]="SYS-01"
0140 LET FILES$[10]="GLM-22",FILES$[11]="GLM-13",FILES$[12]="GLR-05"
0145 LET FILES$[13]="SYM-09",FILES$[14]="SYM-19",FILES$[15]="SYM-29",FILES$[16]="GLM-03"
0150 LET FILES$[17]="GLM-06",FILES$[18]="GLM06.TMP"
0155 LET OPTIONS$[8]="F",OPTIONS$[12]="F"
0160 CALL "SYC.DA",1,1,16,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET GLM01_DEV=CHANNELS[1],GLM10_DEV=CHANNELS[2],GLM11_DEV=CHANNELS[3]
0180 LET GLM12_DEV=CHANNELS[4],GLM21_DEV=CHANNELS[5],GLM22_DEV=CHANNELS[6]
0185 LET GLM32_DEV=CHANNELS[7],GLR01_DEV=CHANNELS[8],SYS01_DEV=CHANNELS[9]
0190 LET GLM22_DEV=CHANNELS[10],GLM13_DEV=CHANNELS[11],GLR05_DEV=CHANNELS[12]
0195 LET SYM09_DEV=CHANNELS[13],SYM19_DEV=CHANNELS[14],SYM29_DEV=CHANNELS[15],GLM03_DEV=CHANNELS[16]
0200 REM " --- IOLIST's
0210 GLM01A52: IOLIST A0$(1),A1$(1),A2$(1),A3$(1)
0220 GLM01A60: IOLIST A0$(1),A1$(1)
0230 GLM11A: IOLIST B0$(1),B1$(1)
0240 GLM21A: IOLIST C0$(1),C1$(1)
0245 GLR01A: IOLIST D0$(1),D1$(1),D2$(1),D3$(1),D4$(1),D5$(1)
0250 GLR01B: IOLIST E0$(1),E1$(1),E2$(1),E3$(1),E[ALL]
0255 GLM12A: IOLIST F0$(1),F1$(1),F2$(1),F3$(1),F4$(1)
0260 GLM22A: IOLIST G0$(1),G1$(1),G2$(1),G3$(1)
0270 GLM32A: IOLIST H0$(1),H1$(1)
0280 GLR05A: IOLIST R0$(1),R1$(1),R2$(1)
0285 GLM13A: IOLIST S0$(1),S1$(1)
0290 GLM06A: IOLIST I0$(1),I1$,I2$,I3$,I[ALL]
0300 GLM03A: IOLIST J0$,J1$,J[ALL]
0330 GLS01A: IOLIST P0$,P1$,P2$(1),P3$(1),P4$(1),M0$,M1$,M2$,M3$
0340 GLS01B52: IOLIST V0$(1),V1$(1),V2$(1),V3$(1)
0350 GLS01B60: IOLIST V0$(1),V3$(1)
0360 GLM10A: IOLIST W0$(1),W1$(1),W2$(1)
0370 SYM09A: IOLIST X0$,X1$,X2$,X3$,X[ALL]
0380 SYM19A: IOLIST Y0$,Y1$,Y2$,Y3$,Y[ALL]
0385 SYM29A: IOLIST SYM29_K$
0390 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N4$=F5$
0500 REM " --- Initializtions"
0505 PRECISION 14
0510 DIM A0$(12),A1$(40),A2$(60),A3$(60)
0550 LET WIDTH=20
0800 REM " --- Display Warning"
0810 CALL "SYC60A",N2$,SYS01_DEV,STATUS
0820 IF STATUS THEN GOTO 9900
0900 REM " --- Additional file Opens
0905 PRINT @(40,3),'CL',@(0,5),'CE',
0910 CALL "SYC.NB"," Making Temporary Files",0,COLUMN
0920 LET NUMBER=1
0930 DIM FROM$[NUMBER],TMPFILE$[NUMBER],FLAG$[NUMBER]
0935 LET FROM$[1]="GLM-06",TMPFILE$[1]="GLM06.TMP"
0950 CALL "SYC.RN",FROM$[ALL],TMPFILE$[ALL],FLAG$[ALL],NUMBER,STATUS
0955 IF STATUS<>0 THEN GOTO 9900
0970 CALL "SYC.DA",1,17,18,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0975 IF STATUS<>0 THEN GOTO 9900
0990 LET GLM06_DEV=CHANNELS[17],GLM06TMP_DEV=CHANNELS[18]
0995 CALL "SYC.NB","Processing",WIDTH,COLUMN
1000 REM " --- Convert GLM-01
1010 LET N0$="",SOURCE=GLM01_DEV,CURFILE$="GLM-01",CNT_READ=0,CNT_WRIT=0
1020 PRINT @(40,3),"GLM-01 Account Master",'CL',
1050 REM " --- Read through File
1055 READ_GLM01: 
1060 LET K$=KEY(SOURCE,END=DONE_GLM01)
1065 IF POS("Z"=K$)=1 THEN REMOVE (SOURCE,KEY=K$); GOTO NEXT_GLM01
1070 LET CNT_READ=CNT_READ+1
1080 READ (SOURCE)IOL=GLM01A52
1090 PRINT @(COLUMN,11),A0$(1,2)," ",A0$(3),
1100 REM " --- Move Financial Report Assignments (A2$) to GLM-11
1105 REM " --- And Consolidated Financial Report Assignments (A3$) to GLM-21
1130 DIM B0$(14),B1$(6),C0$(14),C1$(6)
1135 LET ASSIGNNUM=1
1140 FOR CNT=1 TO 55 STEP 6
1150 LET B0$(1)=A0$(1,12)+STR(ASSIGNNUM:"00"),C0$(1)=A0$(1,12)+STR(ASSIGNNUM:"00")
1160 LET B1$=A2$(CNT,6),C1$=A3$(CNT,6)
1170 IF B1$<>FILL(6) THEN WRITE (GLM11_DEV,KEY=B0$)IOL=GLM11A
1175 IF C1$<>FILL(6) THEN WRITE (GLM21_DEV,KEY=C0$)IOL=GLM21A
1180 LET ASSIGNNUM=ASSIGNNUM+1
1190 NEXT CNT
1200 REM " --- Remove old GLM-01 record & write 6.0 version
1210 REMOVE (SOURCE,KEY=A0$)
1220 WRITE (SOURCE,KEY=A0$)IOL=GLM01A60
1230 LET CNT_WRIT=CNT_WRIT+1
1250 REM " --- Next Record - GLM-01"
1260 NEXT_GLM01: 
1270 GOTO READ_GLM01
1300 REM " --- Done with GLM-01
1310 DONE_GLM01: 
1400 REM " --- Process GLR-01 
1405 IF GLR01_DEV=0 THEN GOTO DONE_GLR01
1415 LET N0$="",SOURCE=GLR01_DEV,CURFILE$="GLR-01"
1420 LET CNT_READ=0,CNT_WRIT=0
1430 PRINT @(40,3),"GLR-01 Financial Reports",'CL',@(COLUMN,11),FILL(WIDTH),
1450 REM 
1455 DIM D0$(30),D1$(40),D2$(15),D3$(12),D4$(10),D5$(10)
1460 DIM E0$(8),E1$(50),E2$(10),E3$(8),E[35]
1470 REM " --- Read through File
1475 READ_GLR01: 
1490 LET K$=KEY(SOURCE,END=DONE_GLR01)
1495 IF LEN(K$)<>8 OR POS("Z"=K$)=1 THEN READ (SOURCE,KEY=K$); GOTO NEXT_GLR01
1505 IF K$(5,4)<>"0000" THEN GOTO DETAIL_GLR01
1510 DIM F0$(30),F1$(15),F2$(12),F3$(10),F4$(10),H0$(7),H1$(7)
1520 READ (SOURCE)IOL=GLR01A
1525 LET CNT_READ=CNT_READ+1
1550 PRINT @(COLUMN,11),D0$(1,2)," ",D0$(3,6),
1560 REM " --- Move Header Info from 'A' Record to  GLM-12
1575 REM " --- And Column Info from 'A' Record to GLM-32
1580 LET F0$=D0$,F1$=D2$,F2$=D3$,F3$=D4$,F4$=D5$
1590 WRITE (GLM12_DEV,KEY=F0$(1,8))IOL=GLM12A
1595 LET SEQNUM=1
1600 FOR CNT=1 TO 36 STEP 5
1610 LET H0$(1)=D0$(1,4)+STR(SEQNUM:"000")
1620 LET H1$(1,4)=D1$(CNT,4)
1630 LET H1$(5,3)=FNG$(D1$(CNT+4,1))
1640 WRITE (GLM32_DEV,KEY=H0$)IOL=GLM32A
1650 LET SEQNUM=SEQNUM+1
1660 NEXT CNT
1670 GOTO NEXT_GLR01
1700 REM " --- Move Detail String Info from 'B' Record to GLM-22
1705 REM " --- (Numerics will be created in GLE-06 when Financial Rpts run)
1710 DETAIL_GLR01: 
1715 DIM G0$(8),G1$(56),G2$(20),G3$(24)
1720 READ (SOURCE)IOL=GLR01B
1750 PRINT @(COLUMN,11),D0$(1,2)," ",D0$(3,6),
1760 REM " --- Move String Info from 'B' Record to  GLM-22
1770 LET G0$(1)=E0$
1780 LET G1$(1)=E1$(1,36),G1$(37,3)=FNG$(E1$(37,1)),G1$(40,5)=E1$(38,5)
1790 LET G1$(45,3)=FNG$(E1$(43,1)),G1$(48,3)=FNG$(E1$(44,1)),G1$(51,6)=E1$(45,6)
1800 LET POSNUM=1
1810 FOR CNT=1 TO 9 STEP 2
1820 LET G2$(POSNUM,3)=FNG$(E2$(CNT,1))
1830 LET POSNUM=POSNUM+3
1840 NEXT CNT
1850 LET POSNUM=16
1860 FOR CNT=2 TO 10 STEP 2
1870 LET G2$(POSNUM,1)=E2$(CNT,1)
1880 LET POSNUM=POSNUM+1
1890 NEXT CNT
1900 LET POSNUM=1
1910 FOR CNT=1 TO 8
1920 LET G3$(POSNUM,3)=FNG$(E3$(CNT,1))
1930 LET POSNUM=POSNUM+3
1940 NEXT CNT
1960 WRITE (GLM22_DEV,KEY=G0$)IOL=GLM22A
1970 REM " --- Next Record -  GLR-01"
1975 NEXT_GLR01: 
1980 GOTO READ_GLR01
1990 DONE_GLR01: 
2000 REM " --- Process GLR-05
2010 IF GLR05_DEV=0 THEN GOTO DONE_GLR05
2030 PRINT @(40,3),"GLR-05 Financial Report Headings",'CL',@(COLUMN,11),FILL(WIDTH),
2100 REM " --- Move GLR-05 data to GLM-13 & normalize
2105 REM " --- (And lop off unused last 28 chars in ea header line)
2110 LET SOURCE=GLR05_DEV,CURFILE$="GLR-05"
2115 LET CNT_READ=0,CNT_WRIT=0
2120 DIM R0$(4),R1$(80),R2$(800)
2140 REM " --- Read through File
2145 READ_GLR05: 
2150 LET K$=KEY(SOURCE,END=DONE_GLR05)
2160 IF LEN(K$)<>4 OR POS("Z"=K$)=1 THEN READ (SOURCE,KEY=K$); GOTO NEXT_GLR05
2170 DIM S0$(7),S1$(132)
2180 READ (SOURCE)IOL=GLR05A
2190 LET CNT_READ=CNT_READ+1
2220 PRINT @(COLUMN,11),R0$(1,4),
2230 LET SEQNUM=0
2240 FOR CNT=1 TO 641 STEP 160
2245 LET S0$(1)=R0$(1,4)+STR(SEQNUM:"000")
2250 LET S1$(1)=R2$(CNT,132)
2253 REM " --- Write non-blank header lines"
2255 IF S1$<>FILL(132) THEN WRITE (GLM13_DEV,KEY=S0$(1,7))IOL=GLM13A
2260 LET SEQNUM=SEQNUM+1
2265 NEXT CNT
2275 REM " --- Next Record - GLR-05"
2280 NEXT_GLR05: 
2290 GOTO READ_GLR05
2295 DONE_GLR05: 
2360 REM " --- Convert GLS-01 'B'
2370 LET N0$="",SOURCE=SYS01_DEV,CURFILE$="GLS-01"
2390 PRINT @(40,3),"GLS-01 'B' Chart of Accounts Breaks",'CL',@(COLUMN,11),FILL(WIDTH),
2400 REM " --- Initialization
2410 LET CNT_READ=0,CNT_WRIT=0
2415 DIM V0$(6),V1$(320),V2$(640),V3$(10)
2420 READ (SOURCE,KEY="",DOM=2425)
2430 REM " --- Read through File
2435 READ_GLS01: 
2440 DIM W0$(5),W1$(10),W2$(20)
2445 LET K$=KEY(SOURCE,END=DONE_GLS01)
2460 READ (SOURCE)IOL=GLS01B52
2465 LET CNT_READ=CNT_READ+1
2475 IF V0$(3,4)<>"GL01" THEN GOTO NEXT_GLS01
2490 PRINT @(COLUMN,11),V0$(1,2),
2500 REM " --- Move 'B' record into GLM-10
2510 LET SEQNUM=0
2520 FOR CNT=1 TO 311 STEP 10
2530 LET W0$(1)=V0$(1,2)+STR(SEQNUM:"000")
2535 LET W1$(1)=V1$(CNT,10)
2540 LET W2$(1)=V2$((CNT-1)*2+1,20)
2550 WRITE (GLM10_DEV,KEY=W0$)IOL=GLM10A
2560 LET SEQNUM=SEQNUM+1
2565 NEXT CNT
2567 REM " --- Remove old GLS-01 record & write 6.0 version
2570 REMOVE (SOURCE,KEY=V0$)
2575 WRITE (SOURCE,KEY=V0$)IOL=GLS01B60
2580 REM " --- Next Record - GLS-01"
2585 NEXT_GLS01: 
2590 GOTO READ_GLS01
2595 DONE_GLS01: 
2600 REM " --- Erase old files"
2610 ERASE_GLR: 
2650 CLOSE (GLR01_DEV,ERR=2680)
2680 CLOSE (GLR05_DEV,ERR=2710)
2710 ERASE "GLR-01",ERR=2720; GOTO 2710
2720 ERASE "GLR-02",ERR=2730; GOTO 2720
2730 ERASE "GLR-03",ERR=2740; GOTO 2730
2740 ERASE "GLR-05",ERR=2750; GOTO 2740
2750 ERASE "GLR-06",ERR=2760; GOTO 2750
2800 REM " --- Convert GLM-06"
2810 PRINT @(40,3),"GLM-06 Audit Control Detail",'CL',@(COLUMN,11),FILL(WIDTH),
2820 DIM I0$(20),I[6]; LET FIRM$=""
2830 READ (GLM06TMP_DEV,KEY="",DOM=2840)
2840 LET GLM06TMP_K$=KEY(GLM06TMP_DEV,END=3060)
2845 READ (GLM06TMP_DEV,KEY=GLM06TMP_K$)IOL=GLM06A
2850 IF LEN(GLM06TMP_K$)=20 THEN GOTO 2840; REM "Record already converted to 6.0
2855 LET I0$(1)=I0$(1,2)+FILL(10)+I0$(3,8)
2860 PRINT @(COLUMN,11),I0$(1,2)," ",I0$(13,8),
2865 IF FIRM$<>I0$(1,2) THEN GOSUB NEXT_GLM06_FIRM; GOTO 2845
2870 IF FNP$(I0$(13,8))="GLX.CA" THEN GOTO 3050; REM "Program replaced
2875 IF FNP$(I0$(13,8))="IVE.DA" THEN GOTO 3050; REM "Program replaced
2880 IF FNP$(I0$(13,8))="IVR.XA" THEN LET I0$(13,8)="IVR.AA"; REM "Program replaced
2885 IF FNP$(I0$(13,8))="OPE.CC" THEN LET I0$(13,8)="OPE.CA"; REM "Source changed
2890 IF FNP$(I0$(13,8))="POR.EA" THEN LET I0$(13,8)="POR.DA"; REM "Program renamed
2895 IF FNP$(I0$(13,8))="PRR.4A" THEN LET I0$(13,8)="PRR.TA"; REM "Program renamed
2900 REM " --- Is Program Part Of A Process?"
2910 READ (SYM29_DEV,KEY=I0$(13,8),DOM=2920)
2920 LET SYM29_K$=KEY(SYM29_DEV,END=2950)
2930 IF SYM29_K$(1,8)=I0$(13,8) THEN GOTO 3000
2950 REM " --- Add Missing Program To Blank Process"
2955 DIM X0$(10),X1$(30),X2$(23),X[3],Y0$(18),Y1$(30),Y2$(11),Y[3],SYM29_K$(18)
2960 LET X0$(1)=I0$(3),X1$(1)="Undefined (5.2 Conversion)",X3$="",Y0$(1)=I0$(3),Y1$(1)="Undefined (5.2 Conversion)",Y3$="",SYM29_K$=I0$(13,8)+I0$(3,10)
2970 WRITE (SYM09_DEV,KEY=X0$,DOM=2980)IOL=SYM09A
2980 WRITE (SYM19_DEV,KEY=Y0$,DOM=2990)IOL=SYM19A
2990 WRITE (SYM29_DEV,KEY=SYM29_K$,DOM=3000)IOL=SYM29A
3000 REM " --- Update GLM-06"
3010 IF FNP$(I0$(13,8))="IVR.AA" THEN IF IVRAA_AUDIT<I[0] THEN LET IVRAA_AUDIT=I[0] ELSE GOTO 3050
3020 LET I0$(3,10)=SYM29_K$(9,10)
3030 WRITE (GLM06_DEV,KEY=I0$)IOL=GLM06A
3040 IF FNP$(I0$(13,8))="OPE.CA" THEN LET I0$(13,8)="OPE.DA"; GOTO 2900; REM "OPE.CC changed to OPE.CA and OPE.DA
3050 GOTO 2840
3080 CLOSE (GLM06TMP_DEV,ERR=3090)
3090 ERASE "GLM06.TMP",ERR=3100
3100 REM " --- Erase Old GLM-05 (Replaced By SYM-09)"
3110 ERASE "GLM-05",ERR=3200
3120 GOTO 3110
3200 REM " --- Add New Journal ID Codes To GLM-03"
3210 PRINT @(40,3),"GLM-03 Journal ID Codes",'CL',@(COLUMN,11),FILL(WIDTH),
3220 DIM J[2]
3230 READ (GLM03_DEV,KEY="99PO",DOM=3300)IOL=GLM03A; REM "New Purchase Journal record
3240 READ (GLM03_DEV,KEY="",DOM=3250)
3250 LET K$=KEY(GLM03_DEV,END=3300)
3260 LET J0$(1,2)=K$(1,2)
3270 WRITE (GLM03_DEV,KEY=J0$(1,4),DOM=3280)IOL=GLM03A
3275 PRINT @(COLUMN,11),J0$(1,2),
3280 READ (GLM03_DEV,KEY=J0$(1,2)+$FFFF$,DOM=3290)
3290 GOTO 3250
4000 REM " --- All done"
4010 REBUILD: 
4090 GOTO 9900
6000 REM " --- Next GLM-06 Firm - Add New 6.0 Records"
6005 NEXT_GLM06_FIRM: 
6010 LET FIRM$=I0$(1,2),IVRAA_AUDIT=0
6020 READ (GLM06TMP_DEV,KEY="99",DOM=6030)
6030 LET TMPK$=KEY(GLM06TMP_DEV,END=6090)
6035 IF TMPK$(1,2)<>"99" THEN GOTO 6090
6040 READ (GLM06TMP_DEV)IOL=GLM06A
6045 IF LEN(TMPK$)<20 THEN GOTO 6080; REM "Old 5.2 firm 99 record
6060 LET I0$(1,2)=FIRM$
6070 WRITE (GLM06_DEV,KEY=I0$,DOM=6080)IOL=GLM06A; REM "Do NOT overwrite existing records
6075 PRINT @(COLUMN,11),I0$(1,2)," ",I0$(13,8),
6080 GOTO 6030
6090 RETURN 
8000 REM " --- Functions"
8055 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9880 LET LINE_ERR$=LST(PGM(TCB(5)))
9890 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

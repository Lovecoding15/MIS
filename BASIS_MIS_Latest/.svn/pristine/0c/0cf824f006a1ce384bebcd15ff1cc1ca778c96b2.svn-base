0010 REM "SYX - v6.0 Administrator Upgrade (ZFILES Overlay)"
0020 REM "Program SYX60B"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9900
0100 REM " --- Open/Lock Files"
0110 LET SYS01_DEV=UNT
0120 OPEN (SYS01_DEV,ERR=9750)"SYS-01"; REM "At this point SYS-01 is still v5.2
0200 REM " --- IOLIST's"
0210 SYS01A: IOLIST X0$(1),X1$(1),X2$,X3$,X[0],X[1],X[2]
0230 ZFILES: IOLIST A0$(1),A1$(1)
0300 FOOBAR: IOLIST X$
0400 REM " --- Parameters"
0410 LET MODULES=1
0500 REM " --- Initialization"
0510 DIM MODULES$[2,4],A0$(6),A1$(32),X0$(6),X1$(64),X[2]
0540 LET UPGRADE$=""
0550 REM " --- Software Modules"
0560 LET MODULES$[1,1]="SY",MODULES$[1,2]="ADD+ON Administrator"
0600 REM " --- Build UPGRADE$ string"
0610 FOR X=1 TO MODULES
0620 LET UPGRADE$=UPGRADE$+MODULES$[X,1]
0630 NEXT X
0700 REM " --- Background"
0710 IF ERRORS=0 THEN GOTO 0800
0715 PRINT @(0,3),'CE',@(0,4),'BR',FILL(80),@(33,4),"W A R N I N G",'ER',
0720 PRINT @(7,6),"The system has determined that there are registers and updates which"
0725 PRINT @(7,7),"have not been run."
0730 PRINT @(7,9),"If you choose to continue with this upgrade process the information"
0735 PRINT @(7,10),"contained in those files will be lost."
0740 PRINT @(7,12),"If you choose to abort the upgrade process at this time you will"
0745 PRINT @(7,13),"need to restore your Version 5.2 programs and SCN's in order to run"
0750 PRINT @(7,14),"your system. You do NOT need to restore your data files."
0755 GOSUB 6900
0760 PRINT @(0,6),'CE',@(7,6),"You have chosen to continue with the upgrade. Any information for"
0765 PRINT @(7,7),"which a register and update has not been run will be lost."
0770 GOSUB 6800
0775 IF V$<>"YES" THEN GOTO 9900
0780 LET X$="FOOBAR"
0790 WRITE (SYS01_DEV,KEY=X$)IOL=FOOBAR
0800 REM " --- Tell the user what we're going to do"
0810 PRINT @(0,3),'CE',
0820 CALL "SYC.WC",ERR=9800,0,0,65,4,8,4,0
0830 PRINT @(10,5),"  This phase of the upgrade will replace your current menu"
0840 PRINT @(10,6),"system and install new Version 6.0 files."
0890 GOSUB 6900
0900 REM " --- Check for distribution files"
0910 LET ZFILES_DEV=UNT
0920 OPEN (ZFILES_DEV,ERR=9700)"ZFILES"
1000 REM " --- Format screen"
1010 LET ROW=10,COLUMN=0
1090 PRINT @(0,9),'CE',"The following ADD+ON Software Modules will be installed or upgraded:"
1100 REM " --- Which modules?"
1110 FOR X=1 TO MODULES
1120 LET PROGRAM$=MODULES$[X,1]+"C.VN"
1130 CALL PROGRAM$,ERR=1170,VERSION$
1140 LET ROW=ROW+1,MODULES$[X,3]="Y",MODULES$[X,4]=VERSION$
1150 PRINT @(COLUMN,ROW),MODULES$[X,1],"  ",MODULES$[X,2],@(COLUMN+26,ROW),MODULES$[X,4],
1160 IF ROW>16 THEN LET ROW=10,COLUMN=40
1170 NEXT X
1190 GOSUB 6900
1300 REM " --- Look for files which might be overwritten"
1310 PRINT @(0,9),'CE',"Verifying Distribution File",
1320 LET FOUND=0,ROW=12,COLUMN=0
1400 REM " --- Read next ZFILES record"
1410 LET K$=KEY(ZFILES_DEV,END=1800)
1420 READ (ZFILES_DEV)IOL=ZFILES
1430 IF POS(A1$(25,2)=UPGRADE$,2)=0 THEN GOTO 1400
1440 LET FILENAME$=FNP$(K$)
1450 PRINT @(28,9),FILENAME$,'CL',
1500 REM " --- Does this file exist?"
1510 LET SOURCE_DEV=UNT,TARGET_DEV=0
1520 OPEN (SOURCE_DEV,ERR=1700)"Z"+FILENAME$
1530 LET TARGET_DEV=UNT
1540 OPEN (TARGET_DEV,ERR=1700)FILENAME$
1600 REM " --- It does!"
1610 IF FOUND=0 THEN PRINT @(0,11),'CE',"The following files will be overwritten if the installation continues:"
1620 LET ROW=ROW+1,FOUND=FOUND+1
1630 PRINT @(COLUMN,ROW),FILENAME$,@(COLUMN+8),A1$(1,24),
1640 IF ROW<20 THEN GOTO 1690
1650 IF COLUMN<40 THEN LET ROW=12,COLUMN=40; GOTO 1690
1660 GOSUB 6900
1670 LET ROW=12,COLUMN=0
1680 PRINT @(COLUMN,ROW+1),'CE',
1700 REM " --- Loop back for next ZFILES record"
1710 CLOSE (SOURCE_DEV,ERR=1720)
1720 CLOSE (TARGET_DEV,ERR=1730)
1790 GOTO 1400
1800 REM " --- Done"
1810 IF FOUND=0 THEN GOTO 1900
1820 GOSUB 6800
1830 IF V$<>"YES" THEN GOTO 9900
1900 REM " --- Get rid of old 5.1 SYM-04 (System Sizing File)
1920 ERASE "SYM-04",ERR=2000
1930 GOTO 1920
2000 REM " --- Install Distribution Files"
2010 PRINT @(0,9),'CE',"Installing Distribution Files",
2050 LET ROW=11,COLUMN=0
2090 READ (ZFILES_DEV,KEY="",DOM=2100)
2100 REM " --- Read next ZFILES record"
2105 LET K$=KEY(ZFILES_DEV,END=2300)
2110 READ (ZFILES_DEV)IOL=ZFILES
2115 IF POS(A1$(25,2)=UPGRADE$,2)=0 THEN GOTO 2100
2120 LET FILENAME$=FNP$(K$),SOURCE_DEV=UNT,TARGET_DEV=UNT
2130 OPEN (TARGET_DEV,ERR=2150)FILENAME$
2135 CLOSE (TARGET_DEV,ERR=2140)
2140 IF A1$(27,1)<>"A" THEN GOTO 2100
2150 OPEN (SOURCE_DEV,ERR=2100)"Z"+FILENAME$
2155 CLOSE (SOURCE_DEV,ERR=2160)
2160 PRINT @(COLUMN,ROW),FILENAME$,@(COLUMN+8),A1$(1,24),
2165 LET ROW=ROW+1
2170 IF ROW<21 THEN GOTO 2200
2175 IF COLUMN<40 THEN LET ROW=11,COLUMN=40; GOTO 2200
2180 GOSUB 6900
2190 LET ROW=11,COLUMN=0
2195 PRINT @(COLUMN,ROW+1),'CE',
2200 REM " --- Rename Zxxx-xx to xxx-xx"
2210 LET NUMBER=1
2220 DIM SOURCE$[NUMBER],TARGET$[NUMBER],ERASE$[NUMBER]
2240 LET SOURCE$[1]="Z"+FILENAME$,TARGET$[1]=FILENAME$
2260 CALL "SYC.RN",SOURCE$[ALL],TARGET$[ALL],ERASE$[ALL],NUMBER,STATUS
2280 IF STATUS>0 THEN GOTO 9500
2290 GOTO 2100
3000 REM " --- Update system record for installed modules"
3030 PRINT @(0,9),'CE',"Updating System Records"
3050 LET ROW=11,COLUMN=0
3100 REM " --- Update each installed module"
3110 FOR X=1 TO MODULES
3120 IF MODULES$[X,3]<>"Y" THEN GOTO 3290
3130 PRINT @(COLUMN,ROW),MODULES$[X,1],"  ",MODULES$[X,2]
3140 LET ROW=ROW+1
3150 IF ROW>20 THEN LET ROW=11,COLUMN=40
3200 REM " --- Update v5.2 SYS-01 System Description"
3210 DIM X0$(6),X1$(64),X3$(100),X[2]
3220 LET X0$(1)="00"+MODULES$[X,1]+"00",X1$(1)=MODULES$[X,2]
3230 LET X1$(36,1)="Y",X1$(37,2)="99",X2$=""
3240 FIND (SYS01_DEV,KEY=X0$,DOM=3250)IOL=SYS01A
3250 LET X1$(39,1)="Y",X1$(46,1)="N",X3$(1,50)=PATHNAME$
3270 WRITE (SYS01_DEV,KEY=X0$)IOL=SYS01A
3290 NEXT X
3300 REM " --- Remove distribution files"
3310 GOSUB 6900
3320 PRINT @(0,9),'CE',"Removing Distribution Files",
3330 LET ROW=11,COLUMN=0
3390 READ (ZFILES_DEV,KEY="",DOM=3400)
3400 REM " --- Read next ZFILES record"
3410 LET K$=KEY(ZFILES_DEV,END=3500)
3420 READ (ZFILES_DEV)IOL=ZFILES
3430 IF POS(A1$(25,2)=UPGRADE$)=0 THEN GOTO 3400
3440 LET X$="Z",FILENAME$=FNP$(K$)
3450 GOSUB 6000
3490 GOTO 3400
4000 REM " --- Installation completed"
4010 GOSUB 6900
4020 PRINT @(0,9),'CE',@(7),"The Version 6.0 Administrator data files have been installed."
4040 GOSUB 6900
4090 RUN "SYX60C"
6000 REM " --- Erase ZFILE"
6010 LET SOURCE_DEV=UNT
6020 OPEN (SOURCE_DEV,ERR=6050)X$+FILENAME$
6030 CLOSE (SOURCE_DEV,ERR=6040)
6040 GOTO 6070
6050 OPEN (SOURCE_DEV,ERR=6190)FILENAME$
6060 CLOSE (SOURCE_DEV,ERR=6070)
6070 PRINT @(COLUMN,ROW),FILENAME$,@(COLUMN+8),A1$(1,24)
6080 ERASE X$+FILENAME$,ERR=6100
6090 GOTO 6080
6100 LET ROW=ROW+1
6110 IF ROW<21 THEN GOTO 6190
6120 IF COLUMN<40 THEN LET ROW=11,COLUMN=40; GOTO 6190
6130 GOSUB 6900
6140 LET ROW=11,COLUMN=0
6150 PRINT @(COLUMN,ROW),'CE',
6190 RETURN 
6800 REM " --- Verify operator's intent to overwrite existing files"
6810 PRINT @(0,22),'CL',"Are you SURE that you wish to proceed (Enter YES to continue/<F4> to exit)?",
6820 LET V0$="S",V1$="CEF",V2$="NO",V3$="YESNO ",V4$="",V0=3,V1=76,V2=22
6830 GOSUB 7000
6840 IF V3<>0 THEN LET V$="NO"
6890 RETURN 
6900 REM " --- Proceed or quit?"
6910 PRINT @(0,22),'CL',@(14,22),"Press <Enter> to continue or <F4> to exit:",'CI',
6920 LET V0$="S",V1$="CF",V2$="",V3$="",V4$="",V0=1,V1=57,V2=22
6930 GOSUB 7000
6940 ON V3 GOTO 6950,6900,6900,6900,9800
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9800
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9500 REM " --- Rename problem"
9510 PRINT @(0,3),'CE',@(0,4),"ERROR: Unable to rename ","Z"+FILENAME$," to ",FILENAME$
9590 GOTO 9800
9700 REM " --- Missing distribution file(s)"
9710 PRINT @(0,3),'CE',@(1,4),"ERROR: Unable to locate or open installation data file ZFILES"
9740 GOTO 9800
9750 REM " --- Missing SYS-01"
9760 PRINT @(0,3),'CE',@(1,4),"ERROR: Unable to locate or open ADD+ON System Control File (SYS-01)"
9800 REM " --- Distribution installation and/or rename problem"
9810 PRINT @(0,10),'CE',
9820 CALL "SYC.WC",ERR=9830,0,0,80,9,0,10,0
9830 PRINT @(2,11),"If you are upgrading to ADD+ON Software Version 6.0:"
9840 PRINT @(2,13),"Restore your original version 5.2 Administrator data files, reload the"
9845 PRINT @(2,14),"version 6.0 Administrator media and try the installation procedure again."
9850 PRINT @(2,16),"If problems persist:"
9860 PRINT @(2,17),"Call ADD+ON Software Support at (800) 275-6350"
9870 LET V4$="Press <Enter> to continue:"
9880 LET V0$="S",V1$="CF",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
9890 GOSUB 7000
9900 REM " --- Abort installation"
9910 PRINT @(1,20),'CE','SF',"ADD+ON Software Installation Terminated."
9950 RELEASE 
9999 END

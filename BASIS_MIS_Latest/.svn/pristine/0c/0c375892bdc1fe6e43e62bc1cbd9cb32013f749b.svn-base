0010 REM "WOS - Work Order Entry - Customer Order Information "
0020 REM "Program WOE.AC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Jul96 JJD: Use correct Customer/WO cross reference variable (4360)"
0032 REM "31Oct98 WGH: Delete option does not work (4045,4690)
0033 REM "31Oct98 WGH: Lead Time may be wrong for items stocked by warehouse (535-590)
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0095 PRECISION IVS01_PRECISION
0100 REM " --- Open/Lock Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ARM-01",FILES$[2]="ARM-10",FILES$[3]="ARE-03"
0140 LET FILES$[4]="ARE-13",FILES$[5]="ARE-23"
0200 REM " --- IOLISTs"
0205 BMM09A: IOLIST X0$,X1$
0210 WOE01A: IOLIST A0$(1),A1$(1),A[ALL]
0215 WOE07A: IOLIST X0$,X1$
0220 ARM01A: IOLIST *,X0$(1)
0225 WOE01A2: IOLIST X0$,X1$,X0,*,X1
0230 ARE03A: IOLIST X$
0235 WOE07A2: IOLIST W0$,W1$
0240 ARE13A: IOLIST W0$,W1$
0250 ARM10E: IOLIST I8$
0260 IVM02A: IOLIST B0$,B1$,B2$,B[ALL]
0270 IVM10C: IOLIST X0$
0275 WOE03A: IOLIST TYPESORT$
0280 WOE03B: IOLIST CUSTSORT$
0290 WOE03C: IOLIST ITEMSORT$
0292 WOE03D: IOLIST OPNSSORT$
0294 WOE03E: IOLIST MATRSORT$
0296 WOE03F: IOLIST SUBCSORT$
0310 WOE02A: IOLIST W0$,W1$,W[ALL]
0312 WOREQA: IOLIST W0$,W1$,W[ALL]
0315 WOS10A: IOLIST WN0$,WONUM
0500 REM " --- Init Data"
0510 DIM W[15],PARAMS[0],PARAMS$[4],ITEMS$[3],REFS$[11],REFS[5],IVFILES[44]
0515 DIM TYPESORT$(14),CUSTSORT$(28),ITEMSORT$(34),OPNSSORT$(21),MATRSORT$(37),SUBCSORT$(21)
0520 LET PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3),PARAMS$[2]=P2$,PARAMS$[3]=P3$,PARAMS$[4]=P4$,ITEMS$[0]=N0$
0525 LET NUMBER=1,IVFILES[0]=SYS01_DEV,IVFILES[1]=IVM01_DEV,IVFILES[2]=IVM02_DEV
0530 LET IVFILES[7]=IVM07_DEV,IVFILES[17]=IVM17_DEV
0535 IF O0<>0 THEN GOTO 0580
0540 IF A1$(98,1)<>" " THEN GOTO 0580
0545 LET A1$(98,1)="M",A0$(19,3)=A0$(16,3),DATE$="",LEADTIME=D[7]
0550 FIND (IVM02_DEV,KEY=N0$+A0$(54,2)+A0$(56),DOM=0560)IOL=IVM02A
0555 IF D2$(24,1)="W" THEN LET LEADTIME=B[7]
0560 CALL "SYC.CA",A0$(19,3),DATE$,LEADTIME
0565 IF DATE$<>"N" THEN LET A0$(22,3)=DATE$
0570 LET ORG_COMP_DATE$=A0$(22,3)
0580 LET FIELD_NUMB=0,MRPTYPE$="MRP Forecast Type:"
0590 IF MP$="N" THEN LET MRPTYPE$=""
0600 REM " --- Additional File Opens "
0610 IF AR$<>"Y" THEN GOTO 0700
0620 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS THEN GOTO 9900
0640 LET ARM01_DEV=CHANNELS[1],ARM10_DEV=CHANNELS[2],ARE03_DEV=CHANNELS[3]
0650 LET ARE13_DEV=CHANNELS[4],ARE23_DEV=CHANNELS[5]
0700 REM " --- Background"
0710 IF AR$="Y" THEN PRINT 'SB',@(3,11),"Customer:",@(0,12),"Sales Order:",@(21),"Line:",@(1,13),"Order Item:",'SF',
0720 PRINT 'SB',@(2,15),"Warehouse:",@(1,16),"Scheduling",@(3,17),"Priority:",@(48,11),"Scheduling Method:",@(45,12),"Estimated Start Date:",@(40,13),"Estimated Completion Date:",@(48,15),"Actual Start Date:",@(47,16),"Last Activity Date:",@(48,17),MRPTYPE$,'SF',
0900 REM " --- Initialize File '
0910 GOSUB 5000
0920 IF O9>0 THEN GOTO 4000
1000 REM " --- Customer Number
1010 IF AR$="Y" THEN GOTO 1050
1015 IF FIELD_NUMB THEN GOTO 4000
1020 LET I0=I0+2
1030 LET V3=1
1040 GOTO 3900
1050 LET V0$="M",V1$="C",V2$=A0$(34,P8),V3$=P8$,V4$="Enter Valid Customer Number (<F1>=None/<F3>=Lookup)",V0=6,V1=13,V2=11,I0=0
1070 GOSUB 7000
1080 IF V3=1 THEN LET A0$(34,16)=""; PRINT @(V1,V2),FILL(34),; GOTO 1180
1090 IF V3=2 THEN GOTO 1180
1095 IF V3=4 THEN GOTO 1050
1100 IF V3<>3 THEN GOTO 1130
1110 LET V0$=P8$,L0=18,L9=21,V4=7,V6=8,V$=""
1120 CALL "SYC.LC",V$
1130 LET V$=V$+"000000",V$=V$(1,6)
1135 DIM X0$(MIN(30,48-(14+LEN(P8$))))
1140 FIND (ARM01_DEV,KEY=N0$+V$,DOM=1000)IOL=ARM01A
1150 IF V$<>A0$(34,6) THEN LET A0$(40,10)=""; PRINT @(13,13),A0$(40,7),@(27,13),A0$(47,3),
1160 LET A0$(34,6)=V$
1170 PRINT @(V1,V2),FNF$(A0$(34,P8),P8$)," ",X0$,
1180 GOTO 3900
1190 REM " --- Sales Order Number
1200 IF AR$<>"Y" OR POS(" "<>A0$(34,6))=0 THEN GOTO 3900
1210 LET V0$="Z",V1$="C",V2$=A0$(40,7),V3$="",V4$="Enter A Valid Sales Order Number (<F1>=None/<F3>=Lookup)",V0=7,V1=13,V2=12
1220 GOSUB 7000
1230 IF V3=1 OR (NUM(V$,ERR=1190)=0 AND V3<>3) THEN LET A0$(40,10)=""; PRINT @(13,12),A0$(40,7),@(27,13),A0$(47,3),; GOTO 3900
1240 IF V3<>3 THEN GOTO 1270
1250 LET V$=A0$(34,6),V3$="ONNN"
1260 CALL "SYC.LO",V$,V3$
1270 IF NUM(V$,ERR=1190)=0 OR V$="" OR V3=2 THEN GOTO 3900
1280 FIND (ARE03_DEV,KEY=A0$(1,4)+A0$(34,6)+V$+"000",DOM=1190)IOL=ARE03A
1290 LET A0$(40,7)=V$
1300 IF O0=0 THEN IF X$(27,3)<ORG_COMP_DATE$ THEN LET S_DATE$=X$(27,3); GOSUB 6900; GOTO 1310 ELSE LET A0$(22,3)=X$(27,3); PRINT @(67,13),FNB$(A0$(22)),
1310 PRINT @(V1,V2),A0$(40,7)
1320 GOTO 3900
1500 REM " --- Line Number
1510 IF AR$<>"Y" OR POS(" "<>A0$(34,6))=0 OR NUM(A0$(40,7))=0 THEN GOTO 3900
1520 LET V0$="Z",V1$="CR",V2$=A0$(47,3),V3$="",V4$="Enter Line Number From Order (<F1>=None)",V0=3,V1=27,V2=12
1530 GOSUB 7000
1540 IF V$="" OR NUM(V$)=0 OR V3=1 THEN LET A0$(47,3)=""; PRINT @(V1,V2),A0$(47,3); GOTO 3900
1550 LET A0$(47,3)=V$
1560 FIND (ARE13_DEV,KEY=A0$(1,4)+A0$(34,16),DOM=1500)IOL=ARE13A
1570 IF O0=0 THEN IF W1$(41,3)<ORG_COMP_DATE$ THEN LET S_DATE$=W1$(41,3); GOSUB 6900; GOTO 1580 ELSE LET A0$(22,3)=W1$(41,3); PRINT @(67,13),FNB$(A0$(22)),
1580 DIM I8$(30); LET I8$(25,1)="?"
1590 FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=1600)IOL=ARM10E
1600 IF FNP$(W0$(33))<>"" THEN PRINT @(13,13),W0$(33) ELSE PRINT @(13,13),W1$(1,40)
1610 IF (POS(I8$(25,1)="PS")>0 AND I9$="I") OR (I8$(25,1)="N" AND I9$="N") THEN GOTO 1660
1630 LET V4$="Work Order Type And Sales Order Do Not Match"
1640 GOSUB 5900
1650 GOTO 1700
1660 IF POS(I8$(25,1)="PS")>0 AND I9$="I" AND W0$(31)=A0$(54) THEN GOTO 1700
1670 IF I8$(25,1)="N" AND I9$="N" THEN GOTO 1700
1680 LET V4$="Item Number And/Or Warehouse ID Does Not Match"
1690 GOSUB 5900
1700 REM 
1790 GOTO 3900
1800 REM " --- Warehouse Code
1805 IF FIELD_NUMB AND FIELD_NUMB<>4 THEN GOTO 4000
1810 LET V0$="S",V1$="C",V2$=A0$(54,2),V3$="",V4$="Enter A Valid Warehouse ID (<F3>=Lookup)",V0=2,V1=13,V2=15
1820 GOSUB 7000
1830 IF V3=4 THEN GOTO 4000
1840 IF V3<>3 THEN GOTO 1870
1850 LET V4=1,V5=8,V6=4,V7=2,V8=6,V9=20,V5$="C"
1860 CALL "IVC.LK",3,V1,V2,V$
1870 LET V$=V$+"  ",V$=V$(1,2)
1880 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=1800)IOL=IVM10C
1890 IF A0$(14,1)="I" THEN FIND (IVM02_DEV,KEY=N0$+V$+A0$(56,20),DOM=1910)
1900 GOTO 1960
1910 LET V4$="Warehouse Not Valid For This Item"
1920 GOSUB 5900
1950 GOTO 1800
1960 LET A0$(54,2)=V$
1970 PRINT @(V1,V2),A0$(54,2)," ",X0$(6,24)
1980 GOTO 3900
2100 REM " --- Scheduling Priority
2105 IF FIELD_NUMB AND FIELD_NUMB<>5 THEN GOTO 4000
2110 LET V0$="S",V1$="C",V2$=A1$(97,1),V3$=" 123456789",V4$="Enter a Priority Code for Operations Scheduling (1=High to 9=Low)",V0=1,V1=13,V2=17
2120 GOSUB 7000
2125 IF NUM(V$)=0 THEN GOTO 2100
2130 LET A1$(97,1)=V$
2140 GOTO 3900
2600 REM " --- MRP Forecast Type"
2610 IF MP$="N" THEN GOTO 2790
2615 IF FIELD_NUMB AND FIELD_NUMB<>6 THEN GOTO 4000
2620 LET V0$="S",V1$="C",V2$=A1$(99,3),V3$="",V4$="Enter Valid MRP Forecast Type (<F1>=None/<F3>=Lookup)",V0=3,V1=67,V2=17
2630 GOSUB 7000
2640 IF V3=2 THEN GOTO 2790
2650 IF V3=3 THEN GOSUB 5800
2660 LET V$=V$+"   ",V$=V$(1,3)
2670 IF V$="   " OR V3=1 THEN LET V$=""; GOTO 2790
2680 FIND (MPM01_DEV,KEY=N0$+V$,DOM=2600)
2690 LET A1$(99,3)=V$
2700 PRINT @(V1,V2),A1$(99,3),
2790 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 IF ABS(I0)>1000 THEN GOTO 4000
3960 ON I0 GOTO 1000,1190,1500,1800,2100,2600,4000
4000 REM " --- Options"
4005 IF A0$(15,1)="C" THEN GOTO 4600
4010 LET V0$="S",V1$="C",V2$="Y",V3$="",V0=7,V2=22,V4$="Is The Above Information Correct (Y/N/#/Delete"
4015 IF I9$="N" THEN LET V4$=V4$+"/Copy"
4020 LET V4$=V4$+")?",FIELD_NUMB=0,V1=FNV(V4$)
4030 GOSUB 7000
4035 LET I=INT(NUM(V$,ERR=4045))
4040 IF I>0 THEN LET I0=I-1,FIELD_NUMB=I; GOTO 3950
4045 IF V$="DELETE" THEN LET A0$(12,3)=S9$(1,3),A0$(34,16)=S9$(4,16),A0$(54)=S9$(20),A[0]=S9; GOTO 4650
4050 IF V$="COPY" AND I9$="N" THEN GOTO 4100
4055 IF V$="" THEN LET V$="Y"
4060 LET I0=0
4065 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Write"
4120 WRITE (WOE01_DEV,KEY=A0$(1,K9))IOL=WOE01A
4130 IF O0<>0 OR BM$<>"Y" THEN GOTO 4190
4145 READ (BMM09_DEV,KEY=A0$(1,2)+A0$(56,20),DOM=4150)
4150 LET K9$=KEY(BMM09_DEV,END=4190)
4155 IF K9$(1,22)<>A0$(1,2)+A0$(56,20) THEN GOTO 4190
4160 FIND (BMM09_DEV,KEY=K9$)IOL=BMM09A
4165 LET X0$=A0$(1,11)+STR(NUM(K9$(23)):"000")
4170 WRITE (WOE07_DEV,KEY=X0$)IOL=WOE07A
4175 GOTO 4150
4190 IF S9$=A0$(12,3)+A0$(34,16)+A0$(54) AND S9=A[0] THEN GOTO 4500
4200 REM " --- Write W/O "Type" Sort Record "
4210 IF S9$(1,2)=A0$(12,2) THEN GOTO 4320
4220 REMOVE (WOE03_DEV,KEY=N0$+"A"+S9$(1,2)+A0$(3,9),DOM=4225)
4225 LET TYPESORT$=N0$+"A"+A0$(12,2)+A0$(3,9)
4230 WRITE (WOE03_DEV,KEY=TYPESORT$)IOL=WOE03A
4240 LET S9$(1,2)=A0$(12,2)
4300 REM " --- Write "Customer/Order#/Line#" Sort Record "
4310 IF AR$<>"Y" THEN GOTO 4400
4320 IF S9$(4,16)=A0$(34,16) THEN GOTO 4400
4330 IF POS(" "<>S9$(4,6))=0 THEN GOTO 4350
4340 REMOVE (WOE03_DEV,KEY=N0$+"B"+S9$(4,16)+A0$(3,9),DOM=4350)
4350 IF POS(" "<>A0$(34,6))=0 THEN GOTO 4370
4355 LET CUSTSORT$=N0$+"B"+A0$(34,16)+A0$(3,9)
4360 WRITE (WOE03_DEV,KEY=CUSTSORT$)IOL=WOE03B
4370 LET S9$(4,16)=A0$(34,16)
4400 REM " --- Write "Warehouse/Item Number (On Order)" Sort Record "
4402 IF POS(A0$(15,1)="PQ")>0 THEN GOTO 4490
4405 LET ACTION$="OO"
4410 IF S9$(3,1)=I9$ AND S9$(20)=A0$(54) AND S9=A[0] THEN GOTO 4500
4420 IF S9$(3,1)<>"I" THEN GOTO 4460
4430 IF POS(" "<>S9$(20))=0 OR S9=0 THEN GOTO 4480
4440 LET O9$=S9$(20),O8=S9,O9=-1,REFS[0]=-S9
4445 LET ITEMS$[0]=S9$(1,2),ITEMS$[1]=S9$(20,2),ITEMS$[2]=S9$(22,20)
4450 GOSUB UPDATE_INVENTORY
4455 REMOVE (WOE03_DEV,KEY=N0$+"C"+S9$(20)+A0$(3,9),DOM=4460)
4460 IF I9$<>"I" THEN GOTO 4500
4470 IF POS(" "<>A0$(54))=0 OR A[0]=0 THEN GOTO 4500
4480 LET O9$=A0$(54),O8=A[0],O9=1,REFS[0]=A[0]
4482 LET ITEMS$[0]=S9$(1,2),ITEMS$[1]=A0$(54,2),ITEMS$[2]=A0$(56,20)
4485 GOSUB UPDATE_INVENTORY
4490 LET ITEMSORT$=N0$+"C"+A0$(54)+A0$(3,9)
4495 WRITE (WOE03_DEV,KEY=ITEMSORT$)IOL=WOE03C
4500 LET S9$(3,1)=I9$,S9$(20)=A0$(54),S9=A[0]
4520 IF O0=0 THEN GOTO 4900
4560 LET O9=2
4590 GOTO 4900
4600 REM " --- No Changes To Closed Work Orders
4610 LET V4$="<Enter> To Continue"
4620 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
4630 GOSUB 7000
4640 GOTO 4900
4650 REM " --- Delete"
4660 LET O9=3
4690 GOTO 4900
4900 REM " --- Run next overlay"
4905 PRINT @(0,11),'CE'
4910 IF V$="COPY" AND I9$="N" THEN GOSUB 6200
4915 CLOSE (ARM01_DEV,ERR=4920); LET ARM01_DEV=0
4920 CLOSE (ARE03_DEV,ERR=4925); LET ARE03_DEV=0
4925 CLOSE (ARE13_DEV,ERR=4930); LET ARE13_DEV=0
4930 CLOSE (ARE23_DEV,ERR=4935); LET ARE23_DEV=0
4935 IF O0=0 AND V$<>"DELETE" THEN GOTO 4950
4940 IF V$="DELETE" THEN RUN "WOE.AB"
4945 RUN "WOE.AA"
4950 IF A0$(14,1)="N" THEN RUN "WOE.AD"
4955 RUN "WOE.AE"
5000 REM " --- Display
5010 IF AR$<>"Y" OR POS(" "<>A0$(34,6))=0 THEN GOTO 5100
5020 DIM X0$(MIN(30,48-(14+LEN(P8$)))); LET X0$(1)="(Not On File)",W1$=""
5030 FIND (ARM01_DEV,KEY=N0$+A0$(34,6),DOM=5040)IOL=ARM01A
5035 PRINT @(13,11),FNF$(A0$(34,P8),P8$)," ",X0$,
5040 FIND (ARE13_DEV,KEY=A0$(1,4)+A0$(34,16),DOM=5100)IOL=ARE13A
5050 PRINT @(13,12),A0$(40,7),@(27,12),A0$(47,3),
5060 IF FNP$(W0$(33))<>"" THEN PRINT @(13,13),W0$(33) ELSE PRINT @(13,13),W1$(1,40)
5100 DIM X0$(30); LET X0$(6,20)="(Not On File)"
5110 FIND (IVM10_DEV,KEY=N0$+"C"+A0$(54,2),DOM=5120)IOL=IVM10C
5120 PRINT @(13,15),A0$(54,2)," ",X0$(6,24),
5130 PRINT @(13,17),A1$(97,1),@(67,11),A1$(98,1),
5200 FOR X=0 TO 3
5210 IF X<2 THEN LET X1=X ELSE LET X1=X+1
5220 PRINT @(67,X1+12),FNB$(A0$(X*3+19)),
5230 NEXT X
5240 PRINT @(67,17),A1$(99,3),
5290 RETURN 
5640 IF POS(X0$(1,11)=K6$)<>1 THEN GOTO 6600
5800 REM " --- Forecast Lookup"
5810 LET RECORD$="",TITLE$="MRP Forecast Types",NUMBER=1,CHANNEL=MPM01_DEV
5820 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
5830 LET DESCRIPTION$[0]="Type",FIELD[0]=1,POSITION[0]=3,LENGTH[0]=3
5840 LET DESCRIPTION$[1]="Description",FIELD[1]=2,POSITION[1]=1
5850 LET LENGTH[1]=30,ROW=V2+2,V$="SKIP"
5860 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
5880 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
5890 RETURN 
5900 REM " --- Display warning message"
5910 DIM MESSAGE$[1]
5920 LET MESSAGE$[0]=V4$+" (<Enter>=Continue)"
5930 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
5990 RETURN 
6000 REM " --- Subroutines"
6010 REM " --- Next W/O #
6020 EXTRACT (WOS10_DEV,KEY=N0$+"N",DOM=6040)IOL=WOS10A
6030 GOTO 6050
6040 LET WONUM=1000
6050 LET WONUM=WONUM+1
6060 IF WONUM>9999999 THEN LET WONUM=1000
6070 LET V$=STR(WONUM:J0$(1,7))
6080 WRITE (WOS10_DEV,KEY=N0$+"N")IOL=WOS10A
6085 PRINT @(V1,V2),V$
6090 RETURN 
6100 REM " --- Update Inventory Information "
6105 UPDATE_INVENTORY: 
6110 CALL "IVC.UA",ACTION$,IVFILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEMS$[ALL],REFS$[ALL],REFS[ALL],STATUS
6190 RETURN 
6200 REM " --- Copy Work Order Requirements From Existing Work Order"
6210 READ (WOE02_DEV,KEY=A0$(1,11),DOM=6215)
6215 LET K2$=KEY(WOE02_DEV,END=6225)
6220 IF POS(A0$(1,11)=K2$)=1 THEN GOTO 6265
6225 READ (WOE22_DEV,KEY=A0$(1,11),DOM=6230)
6230 LET K2$=KEY(WOE22_DEV,END=6240)
6235 IF POS(A0$(1,11)=K2$)=1 THEN GOTO 6265
6240 READ (WOE32_DEV,KEY=A0$(1,11),DOM=6245)
6245 LET K2$=KEY(WOE32_DEV,END=6280)
6250 IF POS(A0$(1,11)=K2$)=0 THEN GOTO 6280
6265 LET V4$="Requirements Already Exist For This Work Order"
6270 GOSUB 5900
6275 GOTO 6890
6280 PRINT @(0,20),'LD','LD','LD','SB',
6285 PRINT @(2,20),"WO Number: ",@(0,21),"Description:",@(43,20),"Prod Qty:",@(43,21),"Opened:",@(62,21),"Closed: ",'SF'
6290 LET V0$="Z",V1$="C",V2$="",V3$="",V4$="Enter WO # To Copy From (<F3>=Lookup)",V0=7,V1=13,V2=20
6295 GOSUB 7000
6300 IF V3=4 THEN GOTO 4000
6310 IF V3<>3 THEN GOTO 6340
6315 LET V3$=P3$(6,2),V$=AR$,V7$=P7$,V6$="?",V8=P[0]
6320 LET V4=1,V5=2,V6=12,V7=21
6330 CALL "SYC.LW",V$,V6$
6335 IF V$="" THEN GOTO 6280
6340 REM " --- Verify Here
6350 FIND (WOE01_DEV,KEY=N0$+"  "+V$,DOM=6200)IOL=WOE01A2
6360 IF A0$(15,1)="C" THEN LET X0=X1
6370 PRINT @(13,21),X1$(1,30),
6380 PRINT @(52,20),X0:M2$,@(51,21),FNB$(A0$(16)),@(70,21),FNB$(A0$(31))
6400 IF X0$(14,1)=A0$(14,1) THEN GOTO 6450
6405 DIM MESSAGE$[1]
6410 LET MESSAGE$[0]="Different Work Order Category (<Enter>=Re-Enter/<F3>=OK)"
6420 CALL "SYC.XA",0,MESSAGE$[ALL],0,22,-1,V$,V3
6425 IF V3<>3 THEN GOTO 6200
6450 IF X0=A[0] THEN GOTO 6500
6460 IF X0=0 THEN LET X0=A[0]; GOTO 6500
6465 IF A[0]=0 THEN GOTO 6890
6470 CALL "SYC.YN",1,"Adjust Requirement Quantities For This Work Order",2,V$,V3
6500 REM " --- Adjust Work Order Requirements "
6505 LET WOREQ_DEV=WOE02_DEV
6510 READ (WOREQ_DEV,KEY=X0$(1,11),DOM=6520)
6520 LET K6$=KEY(WOREQ_DEV,END=6595)
6522 IF POS(X0$(1,11)=K6$)<>1 THEN GOTO 6595
6525 DIM W[13]
6530 FIND (WOREQ_DEV,KEY=K6$)IOL=WOREQA
6540 IF X0=A[0] THEN GOTO 6560
6550 IF V$="Y" THEN LET W[2]=W[2]*A[0]/X0,W[3]=A[0]*W[1] ELSE LET W[0]=W[2]/A[0],W[1]=W[3]/A[0]
6560 LET W0$(1,11)=A0$(1,11)
6565 IF W0$(12,1)="C" THEN LET W1$(43,10)=""
6570 WRITE (WOREQ_DEV,KEY=W0$(1,15))IOL=WOREQA
6575 ON POS(W0$(12,1)="ABC")-1 GOTO 6580,6585,6587
6580 LET OPNSSORT$=N0$+"D"+W0$(3,2)+W1$(1,6)+W0$(5,7)+W0$(13,3)
6582 WRITE (WOE03_DEV,KEY=OPNSSORT$)IOL=WOE03D; GOTO 6590
6585 LET MATRSORT$=N0$+"E"+W0$(3,2)+A0$(54,2)+W1$(27,20)+W0$(5,7)+W0$(13,3)
6586 WRITE (WOE03_DEV,KEY=MATRSORT$)IOL=WOE03E; GOTO 6590
6587 LET SUBCSORT$=N0$+"F"+W0$(3,2)+W1$(7,6)+W0$(5,7)+W0$(13,3)
6588 WRITE (WOE03_DEV,KEY=SUBCSORT$)IOL=WOE03F
6590 READ (WOREQ_DEV,KEY=K6$)
6592 GOTO 6520
6595 IF WOREQ_DEV=WOE02_DEV THEN LET WOREQ_DEV=WOE22_DEV; GOTO 6510
6597 IF WOREQ_DEV=WOE22_DEV THEN LET WOREQ_DEV=WOE32_DEV; GOTO 6510
6600 REM " --- Copy Comments
6630 READ (WOE07_DEV,KEY=X0$(1,11),DOM=6640)
6640 LET K8$=KEY(WOE07_DEV,END=6700)
6650 IF POS(X0$(1,11)=K8$)<>1 THEN GOTO 6700
6660 FIND (WOE07_DEV,KEY=K8$)IOL=WOE07A2
6670 LET W0$(1,11)=A0$(1,11)
6680 WRITE (WOE07_DEV,KEY=W0$(1,14))IOL=WOE07A2
6690 READ (WOE07_DEV,KEY=K8$)
6695 GOTO 6640
6700 REM " --- Copying Complete
6800 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Copying Complete (<Enter>=Continue)",V0=1,V1=FNV(V4$),V2=22; GOSUB 7000
6820 LET O0=1,O9=2
6890 RETURN 
6900 REM " --- Warning Message for Calculated Completion Date"
6910 DIM MESSAGE$[1]
6920 LET MESSAGE$[0]="Sales Order Ship Date Of "+FNB$(S_DATE$)+" Occurs Before",MESSAGE$[1]="Calculated Work Order Completion Date."
6930 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "SYC - Company Lookup"
0020 REM "Program SYC.LX"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "SELECTION$: User selection (returned)"
0060 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER SELECTION$
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SYM-06",FILES$[3]="SYM-12"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],SYM06_DEV=CHANNELS[2],SYM12_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 SYM06A: IOLIST A0$(1),A1$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM SPEED$(128),A0$(2),A1$(40)
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0420 IF PAGES<3 THEN GOTO 9900
0430 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0440 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0450 LET N0$=F0$(16,2),SPEEDSEARCH$="N",COMPANY$="",USER$=F0$(4,3)
0460 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=0500)
0470 LET N0$=SPEED$(1,2),SPEEDSEARCH$="Y"
0480 FIND (SYM06_DEV,KEY=N0$,DOM=0500)IOL=SYM06A
0490 LET COMPANY$=FNP$(A1$)
0500 REM " --- Initializations"
0510 DIM OPTLIST$[1,MAXROW,5],PARAM$[1,MAXROW,3]
0540 LET WIDTH=48,HEIGHT=12,WIN_X=1,WIN_Y=3,MAX_ROW=HEIGHT-5
0560 LET PGM$=PGM(-2),OPTIONS=0,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3
0570 LET OTITLE$="Method",M0$="00",FIRMS$="",TITLE$="Company Lookup"
0650 REM " --- Window arrays"
0670 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0680 LET HEADING$(2)="ID",HEADING$(5)="Name"
0690 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Which search option?"
0810 DIM STRINGS$[10],NUMBERS[10]
0820 LET STRINGS$[0]=PGM$,STRINGS$[1]=M0$,NUMBERS[0]=MAXROW
0830 LET NUMBERS[1]=OWIN_X,NUMBERS[2]=OWIN_Y,NUMBERS[3]=2
0850 CALL "SYC.SM",STRINGS$[ALL],NUMBERS[ALL],FKEY
0860 IF FKEY THEN GOTO 9900
0870 LET PREFIX$=STRINGS$[2],START$=STRINGS$[3],METHOD$=STRINGS$[4]
0880 LET V3$=STRINGS$[5],V0=NUMBERS[4]
0890 READ (SYM12_DEV,KEY=USER$,DOM=0900)
0900 REM " --- Build list of valid firms"
0910 LET K$=KEY(SYM12_DEV,END=0940)
0915 IF POS(USER$=K$)<>1 THEN GOTO 0940
0920 READ (SYM12_DEV)
0925 LET FIRMS$=FIRMS$+K$(4)
0930 GOTO 0900
0940 CALL "SYC.WB",NAME$,0
0945 IF SPEEDSEARCH$<>"Y" THEN GOTO 1000
0950 REM " --- Display SpeedSearch Company"
0955 DIM X$(WIDTH)
0960 LET X$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
0965 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
0970 CALL "SYC.WA",0,WIDTH,3,WIN_X,WIN_Y+HEIGHT,"",CNAME$
0975 PRINT @(0,0),'BR',X$,'ER',
0980 CALL "SYC.WB",NAME$,0
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,5]
1020 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$=""
1040 IF PREFIX$="*" THEN GOTO 1300
1050 LET X=1,X=NUM(START$,ERR=1060)
1060 IF X<1 THEN LET X=1
1070 LET START$=STR(X:M0$)
1200 REM " --- Company Number"
1210 READ (SYM06_DEV,KEY=START$,DOM=1400)IOL=SYM06A
1220 LET K$=A0$
1290 GOTO 1900
1300 REM " --- Keyword"
1310 LET START$=FNU$(START$)
1390 READ (SYM06_DEV,KEY="",DOM=1400)
1400 REM " --- Get next key"
1410 LET K$=KEY(SYM06_DEV,END=3000)
1900 REM " --- Read record"
1910 READ (SYM06_DEV,KEY=K$,ERR=1950)IOL=SYM06A
1915 IF FIRMS$<>"" THEN IF POS(A0$=FIRMS$,2)=0 THEN GOTO 2900
1920 IF PREFIX$<>"*" THEN GOTO 2000
1930 IF START$<>"" THEN IF POS(START$=FNU$(A1$))=0 THEN GOTO 2900
1940 GOTO 2000
1950 REM " --- Error reading record"
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1990 READ (SYM06_DEV,KEY=K$(1,LEN(K$)-1)+$FF$,DOM=2900)
2000 REM " --- Display page?"
2010 IF L<MAX_ROW THEN GOTO 2400
2020 LET MODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 0800
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2400 REM " --- Lookup window record"
2410 DIM G$(WIDTH-2)
2420 LET G$(2)=A0$,G$(5)=A1$,LLIST$[PAGE,X1,1]=G$
2490 LET LLIST$[PAGE,X1,2]=A0$,X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2990 GOTO 1400
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET MESSAGE$[0]="No Company Found (<Enter>=Continue)"
3040 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=INT((WIDTH-(LEN(MESSAGE$[0])+9))/2)
3050 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page"
3510 LET MODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",MODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,5
6280 LET L=1,X1=1
6290 RETURN 
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9921)
9925 NEXT X
9930 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9935 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
9940 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9945 IF SELECTION$="END" THEN LET SELECTION$=""
9950 EXIT 
9999 END

0010 REM "SAP - Sales Analysis Parameter Maintenance"
0020 REM "Program SAP.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "10Sep96 WGH: Parameterize the number of Sales Analysis options for easy modification (505-510,560,720,1310,1510,5010)"
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=1
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1]
0200 REM " --- IOLIST's"
0210 SAS01A: IOLIST A0$(1),A1$(1),A2$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="SA",N4$=F5$
0500 REM " --- Initializations"
0505 LET SAOPTS=11
0510 DIM A0$(6),A1$(SAOPTS),A2$(SAOPTS),OPROMPTS$[SAOPTS],DPROMPTS$[SAOPTS],DTYPES$[SAOPTS]
0550 LET C$="(C=Customer",PI$="/P=Product/I=Item)"
0560 LET D1$=FILL(SAOPTS,"Y"),D2$="IPPPIIIIIPN"
0590 LET A0$(1)=N0$+N2$+"00",PI$="/P=Product/I=Item)",I0=0
0600 REM " --- Prompts"
0605 LET OPROMPTS$[1]="By Customer",OPROMPTS$[2]="By Territory"
0610 LET OPROMPTS$[3]="By Salesperson",OPROMPTS$[4]="By Customer Type"
0615 LET OPROMPTS$[5]="By Customer Ship-To",OPROMPTS$[6]="By Customer SIC"
0620 LET OPROMPTS$[7]="By Product Type",OPROMPTS$[8]="By Warehouse"
0625 LET OPROMPTS$[9]="By Stocking Vendor",OPROMPTS$[10]="By Distribution Code"
0630 LET OPROMPTS$[11]="By Non-Stock Item",DPROMPTS$[1]=C$+PI$
0635 LET DPROMPTS$[2]="(T=Territory"+PI$,DPROMPTS$[3]="(S=Salesperson"+PI$
0640 LET DPROMPTS$[4]=C$+" Type"+PI$,DPROMPTS$[5]=C$+"/S=Customer Ship-To/Item)"
0645 LET DPROMPTS$[6]=C$+" SIC"+PI$,DPROMPTS$[7]="(P=Product/I=Item)"
0650 LET DPROMPTS$[8]="(W=Warehouse"+PI$,DPROMPTS$[9]="(V=Vendor"+PI$
0655 LET DPROMPTS$[10]="(D=Distribution Code"+PI$,DPROMPTS$[11]="(P=Product/N=Non Stock Item)"
0660 LET DTYPES$[1]="CPI",DTYPES$[2]="TPI",DTYPES$[3]="SPI",DTYPES$[4]="CPI"
0665 LET DTYPES$[5]="CSI",DTYPES$[6]="CPI",DTYPES$[7]="PI",DTYPES$[8]="WPI"
0670 LET DTYPES$[9]="VPI",DTYPES$[10]="DPI",DTYPES$[11]="PN"
0700 REM " --- Background"
0710 PRINT 'SB',@(16,4),"Sales Analysis Options",@(44),"Detail Level",'SF',
0720 FOR X=1 TO SAOPTS
0730 PRINT 'SB',@(35-LEN(OPROMPTS$[X]),X+5),OPROMPTS$[X],":",'SF',
0740 NEXT X
1200 REM " --- Retrieve record"
1210 FIND (SYS01_DEV,KEY=A0$,DOM=1500)IOL=SAS01A
1300 REM " --- Display record"
1310 FOR X=1 TO SAOPTS
1320 IF POS(A1$(X,1)="YN")=0 THEN LET A1$(X,1)=D1$(X,1)
1330 IF POS(A2$(X,1)=DTYPES$[X])=0 THEN LET A2$(X,1)=D2$(X,1)
1340 NEXT X
1380 GOSUB 5000
1390 GOTO 4000
1500 REM " --- Initialize new record"
1510 DIM A1$(SAOPTS),A2$(SAOPTS)
1520 LET A1$(1)=D1$,A2$(1)=D2$,I0=0
1590 GOSUB 5000
2000 REM " --- Options"
2010 LET X=(I0/2)+1,V0$="Y",V1$="",V2$=A1$(X,1),V3$="",V0=1,V1=37,V2=X+5
2020 LET V4$="Generate Sales Analysis "+OPROMPTS$[X]+" (Y/N)?"
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 LET A1$(X,1)=V$
2090 GOTO 3900
2200 REM " --- Detail Level"
2210 LET X=((I0+1)/2),V0$="S",V1$="",V2$=A2$(X,1),V3$=DTYPES$[X],V0=1
2220 LET V4$="Select The Desired Level Of Detail "+DPROMPTS$[X],V1=44,V2=X+5
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2000,3900,2000,3900
2250 LET A2$(X,1)=V$
2290 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?",I0=0
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write record"
4110 WRITE (SYS01_DEV,KEY=A0$)IOL=SAS01A
4190 GOTO 9900
4200 REM " --- Remove record"
4210 REMOVE (SYS01_DEV,KEY=A0$,DOM=4220)
4290 GOTO 9900
5000 REM " --- Display"
5010 FOR X=1 TO SAOPTS
5020 LET X$=""
5050 PRINT @(37,X+5),A1$(X,1),@(44),A2$(X,1)," ",X$,'CL',
5080 NEXT X
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

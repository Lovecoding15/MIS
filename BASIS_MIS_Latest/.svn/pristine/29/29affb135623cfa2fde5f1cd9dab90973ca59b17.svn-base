0010 REM "GLM - G/L Financial Heading Maintenance"
0020 REM "Program GLM.KA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0060 ON O0 GOTO 0070,4000
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-13",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM13_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 GLM13A: IOLIST A0$(1),A1$(1)
0260 GLS01A: IOLIST X$,P1$,P2$,P3$(1),P4$(1),M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0405 DIM P[4],P3$(325),P4$(32)
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0450 LET LINES=5,WIDTH=132,DISPLAY=INT(WIDTH/2)
0500 REM " --- Initializations"
0510 DIM A0$(7),A1$(132),HEADLINES$[LINES](132)
0590 LET A0$(1)=N0$,OFFSET=1
0600 REM " --- Build RULER$"
0610 DIM RULER$[1](WIDTH),X$(WIDTH,".")
0620 LET RULER$[1](1)=X$
0630 FOR X=5 TO 130 STEP 5
0640 LET X$="+",Y$=""
0650 IF MOD(X,10)=0 THEN LET X$="|",Y$=STR(X),V=LEN(Y$)
0660 LET RULER$[1](X,1)=X$
0670 IF Y$<>"" THEN LET RULER$[0](X-V+1,V)=Y$
0690 NEXT X
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0750 PRINT 'SB',@(0,3),"Heading Number:",'SF',
0760 FOR X=1 TO 5
0770 PRINT 'SB',@(0,X+6),"Line",X,'SF',
0780 NEXT X
0900 REM " --- Position GLM-13"
0990 READ (GLM13_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Heading Number"
1005 PRINT 'CF',
1010 LET V2$="",V2$=KEY(GLM13_DEV,END=1040)
1020 IF V2$(1,2)<>N0$ THEN LET V2$=""
1030 IF V2$<>"" THEN LET V2$=V2$(3)
1040 LET V0$="Z",V1$="C",V3$="",V0=2,V1=16,V2=3,I0=0
1045 LET V4$="Enter A Valid Heading Number"
1050 GOSUB 7000
1060 ON V3 GOTO 1070,1000,1000,1000,9900
1070 IF V$="" THEN GOTO 1000
1080 LET A0$(3)=V$
1200 REM " --- Search for existance of any Heading Lines for Report"
1210 READ (GLM13_DEV,KEY=A0$,DOM=1220)
1220 LET K$=KEY(GLM13_DEV,END=NOTFOUND)
1230 IF K$(1,4)<>A0$(1,4) THEN GOTO NOTFOUND
1235 READ (GLM13_DEV,DOM=READNEXT)
1240 GOSUB GET_HEADLINES
1250 GOTO DISPLAY
1270 READNEXT: GOTO 1220
1280 NOTFOUND: GOTO NEW_REC
1300 REM " --- Display record"
1310 DISPLAY: 
1320 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record"
1405 NEW_REC: 
1410 LET V4$="Is This A New Record (Y/N)?"
1420 LET V0$="Y",V1$="C",V2$="N",V3$="",V0=1,V1=FNV(V4$),V2=22
1430 GOSUB 7000
1440 ON V3 GOTO 1490,1400,1400,1400,1000
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record"
1510 DIM HEADLINES$[LINES](132)
1520 GOSUB 5000
1590 GOTO 4000
2000 REM " --- Heading Line"
2010 LET L=I0,DIRECT$="Right"
2020 IF OFFSET>1 THEN LET DIRECT$="Left"
2030 LET V0$="S",V1$="C",V2$=HEADLINES$[L](OFFSET,DISPLAY),V3$=""
2040 LET V0=DISPLAY,V1=7,V2=6+L
2050 LET V4$="Enter Optional Heading Information (<F1>="+DIRECT$+"/<F3>=Center Text)"
2060 GOSUB 7000
2070 ON V3 GOTO 2080,2100,3900,2200,3900
2080 LET HEADLINES$[L](OFFSET,DISPLAY)=V$
2090 GOTO 3900
2100 REM " --- Shift display right or left"
2110 GOSUB 6100
2190 GOTO 2000
2200 REM " --- Center existing string"
2210 DIM H$(WIDTH)
2220 LET LINENUM=L,X$=FNP$(HEADLINES$[LINENUM](1,WIDTH)),L=LEN(X$)
2225 IF X$<>"" THEN LET X$=FNS$(X$)
2230 LET L=LEN(X$),P=INT((WIDTH-L)/2),H$(P)=X$,HEADLINES$[LINENUM](1,WIDTH)=H$
2240 GOSUB 5000
2290 GOTO 2000
3900 REM " --- Next field"
3910 IF V3=4 THEN GOTO 3990
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,2000,2000,2000,2000,2000,3990
3990 LET I0=0
4000 REM " --- Correct?"
4005 LET DIRECT$="Right",I0=1
4010 IF OFFSET>1 THEN LET DIRECT$="Left"
4015 LET V4$="Is The Above Information Correct (Y/N/Delete/Print/<F1>="+DIRECT$+")?"
4020 LET V0$="S",V1$="CR",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4300,4000,4000,9900
4050 IF V$="PRINT" THEN GOTO 4500
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write records, but no blank lines"
4110 FOR X=1 TO LINES
4120 LET A0$(1)=A0$(1,4)+STR(X-1:"000"),A1$(1)=HEADLINES$[X](1,132)
4130 IF A1$<>FILL(132) THEN GOTO 4150
4135 REMOVE (GLM13_DEV,KEY=A0$,DOM=4136)
4140 GOTO 4160
4150 WRITE (GLM13_DEV,KEY=A0$)IOL=GLM13A
4160 NEXT X
4190 GOTO 1000
4200 REM " --- Remove record"
4210 FOR X=1 TO LINES
4220 REMOVE (GLM13_DEV,KEY=A0$(1,4)+STR(X-1:"000"),DOM=4230)
4260 NEXT X
4290 GOTO 1000
4300 REM " --- Right or Left"
4310 GOSUB 6100
4390 GOTO 4000
4500 REM " --- Print?"
4510 LET O0=1
4590 RUN "GLM.KB"
5000 REM " --- Display record"
5010 PRINT @(16,3),A0$(3,2)
5020 FOR X=0 TO 1
5030 PRINT 'SB',@(6,5+X),"[",RULER$[X](OFFSET,DISPLAY),"]",'SF'
5040 NEXT X
5050 FOR X=1 TO LINES
5060 PRINT @(7,6+X),HEADLINES$[X](OFFSET,DISPLAY)
5080 NEXT X
5090 RETURN 
6100 REM " --- Shift display right or left"
6110 IF OFFSET>1 THEN GOTO 6150
6120 LET OFFSET=DISPLAY+1
6130 GOTO 6180
6150 LET OFFSET=1
6180 GOSUB 5000
6190 RETURN 
6200 REM " --- Read records into screen vars"
6205 GET_HEADLINES: 
6210 DIM HEADLINES$[LINES](132),A1$(132)
6220 READ (GLM13_DEV,KEY=K$)IOL=GLM13A
6230 LET HEADLINES$[NUM(A0$(5,3))+1](1)=A1$
6240 LET K$=KEY(GLM13_DEV,END=DONE_HEADLINES)
6250 IF K$(1,4)<>A0$(1,4) THEN GOTO DONE_HEADLINES
6270 GOTO 6220
6280 DONE_HEADLINES: 
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNS$(Q$)=Q$(POS(" "<>Q$))
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

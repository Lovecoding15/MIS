0010 REM "GLM - Copy Financial Statements"
0020 REM "Program GLM.JA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-12",FILES$[2]="GLM-22",FILES$[3]="GLM-32"
0140 LET FILES$[4]="GLE-06",FILES$[5]="GLM-12",FILES$[6]="GLM-22"
0150 LET FILES$[7]="GLM-32",FILES$[8]="GLE-06",FILES$[9]="SYS-01"
0160 CALL "SYC.DA",1,1,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM12_DEV=CHANNELS[1],GLM22_DEV=CHANNELS[2],GLM32_DEV=CHANNELS[3]
0185 LET GLE06_DEV=CHANNELS[4],GLM12_DEV2=CHANNELS[5],GLM22_DEV2=CHANNELS[6]
0190 LET GLM32_DEV2=CHANNELS[7],GLE06_DEV2=CHANNELS[8],SYS01_DEV=CHANNELS[9]
0200 REM " --- IOLIST's"
0210 GLM12A: IOLIST A0$(1),A1$,A2$,A3$,A4$
0220 GLM22A: IOLIST B0$(1),B1$,B2$,B3$
0230 GLM32A: IOLIST D0$(1),D1$
0240 GLE06A: IOLIST S0$(1),S[ALL]
0260 GLS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 SYS01C: IOLIST C0$(1),C1$(1)
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0440 DIM P[4]
0450 FOR X=0 TO 4
0460 LET P[X]=NUM(P2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET P[2]=FNYY_YEAR(P2$(5,2))
0500 REM " --- Initializations"
0510 DIM A0$(30),B0$(8),C0$(2),C1$(35)
0550 LET K9=8
0560 LET FROMFIRM$=N0$,FROMREPORT$="",TOFIRM$=N0$,TOREPORT$=""
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,5,0
0720 CALL "SYC.WC",1,0,80,0,0,8,0
0730 CALL "SYC.WC",1,0,80,0,0,10,0
0740 PRINT 'SB',@(26,3),"From Company:",@(20,4),"From Report Number:",@(28,6),"To Company:",@(22,7),"To Report Number:",'SF',
0900 REM " --- Position file"
0990 READ (GLM12_DEV,KEY=FROMFIRM$,DOM=1000)
1000 REM " --- From Company"
1010 PRINT 'CF',
1020 LET V0$="Z",V1$="EK",V2$=N0$,V3$="",V0=2,V1=40,V2=3,I0=0
1030 LET V4$="Enter a Valid Company Number (<F3>=Lookup)"
1040 GOSUB 7000
1050 ON V3 GOTO 1070,1000,1000,1060,9900
1060 CALL "SYC.LX",V$
1070 LET C0$(1)=V$
1075 FIND (SYS01_DEV,KEY=C0$,DOM=1000)IOL=SYS01C
1080 PRINT @(V1,V2),C0$," ",C1$,
1085 LET FROMFIRM$=C0$
1090 GOTO 3900
1200 REM " --- From Report"
1210 LET V0$="Z",V1$="C",V2$=FROMREPORT$,V3$="",V0=2,V1=40,V2=4
1220 LET V4$="Enter a Valid Report Number (<F3>=Lookup)"
1230 GOSUB 7000
1240 ON V3 GOTO 1260,1200,3900,1245,3900
1245 CALL "GLC.LF",N0$,V$
1250 IF V$="" THEN GOTO 1200
1260 LET A0$(1)=FROMFIRM$,A0$(3)=V$,A0$(5)="0000"
1265 FIND (GLM12_DEV,KEY=A0$(1,K9),DOM=1200)IOL=GLM12A
1270 PRINT @(V1,V2),A0$(3,2)," ",A0$(9,20)
1280 LET FROMREPORT$=A0$(3,2)
1290 GOTO 3900
1400 REM " --- To Company"
1410 LET V0$="Z",V1$="C",V2$=TOFIRM$,V3$="",V0=2,V1=40,V2=6
1420 LET V4$="Enter a Valid Company Number (<F3>=Lookup)"
1430 GOSUB 7000
1440 ON V3 GOTO 1460,1400,3900,1450,3900
1450 CALL "SYC.LX",V$
1460 LET C0$(1)=V$
1470 FIND (SYS01_DEV,KEY=C0$,DOM=1400)IOL=SYS01C
1475 PRINT @(V1,V2),C0$," ",C1$
1480 LET TOFIRM$=V$
1490 GOTO 3900
1600 REM " --- To Report"
1610 LET V0$="Z",V1$="C",V2$=TOREPORT$,V3$="",V0=2,V1=40,V2=7
1620 LET V4$="Enter a Valid Report Number (<F3>=Lookup)"
1630 GOSUB 7000
1640 ON V3 GOTO 1660,1600,3900,1645,3900
1645 CALL "GLC.LF",N0$,V$
1650 IF V$="" THEN GOTO 1600
1660 LET A0$(1)=TOFIRM$,A0$(3)=V$,A0$(5)="0000"
1665 FIND (GLM12_DEV,KEY=A0$(1,K9),DOM=1670)IOL=GLM12A
1670 PRINT @(V1,V2),A0$(3,2)," ",A0$(9,20)
1680 LET TOREPORT$=V$
1690 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,4000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V0=1,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4090,4000,4000,4000,9900
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Edit arguments"
4110 LET C0$(1)=FROMFIRM$
4120 FIND (SYS01_DEV,KEY=FROMFIRM$,DOM=1000)
4130 LET C0$(1)=TOFIRM$
4140 FIND (SYS01_DEV,KEY=TOFIRM$,DOM=1000)
4150 LET A0$(1)=FROMFIRM$,A0$(3)=FROMREPORT$,A0$(5)="0000"
4160 FIND (GLM12_DEV,KEY=A0$(1,K9),DOM=1000)
4190 IF FROMFIRM$=TOFIRM$ THEN IF FROMREPORT$=TOREPORT$ THEN GOTO 1000
4200 REM " --- Copy Header"
4210 CALL "SYC.NB","Copying",4,COLUMN
4220 LET A0$(1)=FROMFIRM$,A0$(3)=FROMREPORT$,A0$(5)="0000"
4230 READ (GLM12_DEV,KEY=A0$(1,K9),DOM=4800)IOL=GLM12A
4240 LET A0$(1,2)=TOFIRM$,A0$(3,2)=TOREPORT$
4250 WRITE (GLM12_DEV2,KEY=A0$(1,K9))IOL=GLM12A
4300 REM " --- Read next detail record"
4310 LET K$=KEY(GLM22_DEV,END=4400)
4320 IF POS(FROMFIRM$+FROMREPORT$=K$)<>1 THEN GOTO 4400
4330 READ (GLM22_DEV)IOL=GLM22A
4350 PRINT @(COLUMN,11),B0$(5,4),
4360 LET B0$(1,2)=TOFIRM$,B0$(3,2)=TOREPORT$
4370 WRITE (GLM22_DEV2,KEY=B0$(1,8))IOL=GLM22A
4390 GOTO 4300
4400 REM " --- Copy Column Info
4410 READ (GLM32_DEV,KEY=A0$(1,4),DOM=4420)
4420 LET K32$=KEY(GLM32_DEV,END=4800)
4430 IF POS(FROMFIRM$+FROMREPORT$=K32$)<>1 THEN GOTO 4500
4440 READ (GLM32_DEV,KEY=K32$)IOL=GLM32A
4450 PRINT @(COLUMN,11),D0$(5,3),
4460 LET D0$(1,2)=TOFIRM$,D0$(3,2)=TOREPORT$
4470 WRITE (GLM32_DEV2,KEY=D0$(1,8))IOL=GLM32A
4490 GOTO 4420
4500 REM " --- Copy Numerics
4510 READ (GLE06_DEV,KEY=A0$(1,4),DOM=4520)
4520 LET K06$=KEY(GLE06_DEV,END=4800)
4530 IF POS(FROMFIRM$+FROMREPORT$=K06$)<>1 THEN GOTO 4800
4540 READ (GLE06_DEV,KEY=K06$)IOL=GLE06A
4550 PRINT @(COLUMN,11),S0$(5,4),
4560 LET S0$(1,2)=TOFIRM$,S0$(3,2)=TOREPORT$
4570 WRITE (GLE06_DEV2,KEY=S0$)IOL=GLE06A
4590 GOTO 4520
4800 REM " --- Copy completed"
4820 PRINT @(0,11),'CE',
4830 CALL "SYC.WC",1,0,80,0,0,10,0
4840 GOTO 1000
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

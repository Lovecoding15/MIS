0010 REM "SYC - Kode Lookup"
0020 REM "Program SYC.LK"
0040 REM 
0042 REM "CHANNEL        : Channel number for lookup"
0046 REM "NUMBER         : Number of fields to display"
0048 REM "TITLE$         : Optional lookup title"
0050 REM "DESCRIPTION$[*]: Description of each field"
0052 REM "FIELD(*)       : String position of each field (1=1st, 2=2nd)"
0054 REM "POSITION(*)    : Starting position of field within FIELD(*)"
0056 REM "LENGTH(*)      : Length of field"
0058 REM "COLUMN         : Upper left column of window"
0060 REM "ROW            : Upper left row of window"
0062 REM "SELECTION$     : User selection (returned)"
0063 REM "               : Lookup option SKIP == don't request start key"
0064 REM "               : Lookup option NO COMPANY == no FIRM ID in key"
0070 REM "
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER CHANNEL,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,SELECTION$
0100 REM " --- Open Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SYM-06"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SYM06_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 LIST02: IOLIST A$[1]
0220 SYM06A: IOLIST X$,COMPANY$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 LIST04: IOLIST A$[1],A$[2],A$[3],A$[4]
0400 REM " --- Parameters"
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0425 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0430 LET N0$=F0$(16,2),SPEEDSEARCH$="N",X$=STBL("!SPEEDSEARCH",ERR=0500)
0440 LET N0$=X$(1,2),SPEEDSEARCH$="Y"
0450 FIND (SYM06_DEV,KEY=N0$,DOM=9900)IOL=SYM06A
0500 REM " --- Initializations"
0510 DIM A$[9],P[NUMBER+1]
0520 LET WIDTH=0,HEIGHT=12,WIN_X=COLUMN,WIN_Y=ROW,MAX_ROW=HEIGHT-5,IO=0
0525 LET P[0]=2,KEYPOS=POSITION[0],KEYLEN=LENGTH[0],OLDVAL$=SELECTION$
0530 IF POS("NO COMPANY"=OLDVAL$)>0 THEN LET N0$="",SPEEDSEARCH$="N"
0535 FOR X=0 TO NUMBER
0540 LET W=LENGTH[X],D=LEN(DESCRIPTION$[X])
0545 IF D>W THEN LET W=D
0550 LET WIDTH=P[X]+W+2,P[X+1]=WIDTH
0555 NEXT X
0560 IF pgm(-1)="BSC.01" or pgm(-1)="BTM.01" THEN LET IO=1
0565 IF WIDTH<42 THEN LET WIDTH=42
0570 IF COLUMN>40 THEN LET WIN_X=79-(WIDTH+2)
0575 IF WIN_X+WIDTH>79 THEN LET WIN_X=79-WIDTH
0580 IF WIN_Y+HEIGHT>22 THEN LET WIN_Y=22-HEIGHT
0585 LET OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3,OTITLE$=DESCRIPTION$[0]
0590 LET OWIDTH=LEN(OTITLE$)+6,OHEIGHT=3,START$=""
0595 IF KEYLEN>LEN(DESCRIPTION$[0]) THEN LET OWIDTH=KEYLEN+6
0650 REM " --- Window arrays"
0660 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0670 FOR X=0 TO NUMBER
0675 LET HEADING$(P[X])=DESCRIPTION$[X]
0680 NEXT X
0690 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Options"
0805 IF POS("SKIP"=OLDVAL$)>0 THEN GOTO 0950
0807 PRINT @(0,0),'CF',
0810 CALL "SYC.WA",0,OWIDTH,OHEIGHT,OWIN_X,OWIN_Y,OTITLE$,ONAME$
0820 LET V0$="S",V1$="KCE^",V2$="",V4$="",V0=KEYLEN,V1=1,V2=0
0830 IF V0>OWIDTH-2 THEN LET V0=OWIDTH-2
0840 GOSUB 7000
0850 IF V3=6 THEN GOTO 9900
0860 IF V3=5 THEN GOTO 9900
0870 IF V3=4 THEN GOTO 9900
0880 LET START$=V$
0890 CALL "SYC.WD",ONAME$
0950 REM " --- Display SpeedSearch Company"
0953 IF SPEEDSEARCH$<>"Y" THEN GOTO 1000
0955 DIM X$(WIDTH-2)
0960 LET X$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
0965 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
0970 CALL "SYC.WA",0,WIDTH,3,WIN_X,WIN_Y+HEIGHT,"",CNAME$
0975 PRINT @(0,0),'BR',X$,'ER',
0980 CALL "SYC.WB",NAME$,0
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,5]
1020 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$=""
1070 IF IO=0 THEN READ (CHANNEL,KEY=START$,knum=1,DOM=1400)IOL=LIST02
1080 IF IO=1 THEN READ (CHANNEL,KEY=START$,DOM=1400)IOL=LIST04
1090 GOTO 2000
1400 REM " --- Read next record"
1405 LET LOCKED=0
1410 LET K$=KEY(CHANNEL,END=3000)
1430 IF IO=0 THEN READ (CHANNEL,KEY=K$,knum=1,ERR=1950)IOL=LIST02
1440 IF IO=1 THEN READ (CHANNEL,KEY=K$,ERR=1950)IOL=LIST04
1450 GOTO 2000
1950 REM " --- Error reading code record"
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1980 LET LOCKED=1
2000 REM " --- Display page?"
2010 IF L<MAX_ROW THEN GOTO 2400
2020 LET XMODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 0800
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2200 REM " --- Lookup window record"
2410 DIM G1$(WIDTH-2)
2412 IF LOCKED=1 THEN LET G1$(P[0])=K$(KEYPOS,KEYLEN),G1$(P[1])="Record In Use",A$[1]=FILL(10); GOTO 2480
2415 LET G1$(P[0])=A$[1](KEYPOS,KEYLEN)
2417 IF NUMBER<1 THEN GOTO 2480
2420 FOR X=1 TO NUMBER
2425 IF LENGTH[X]>0 THEN GOTO 2440
2430 LET G1$(P[X])=A$[FIELD[X]]
2435 GOTO 2470
2440 LET Z=LENGTH[X]
2460 IF POSITION[X]+LENGTH[X]-1>LEN(A$[FIELD[X]]) THEN LET Z=LEN(A$[FIELD[X]])-POSITION[X]+1
2465 LET G1$(P[X])=A$[FIELD[X]](POSITION[X],Z)
2470 NEXT X
2480 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A$[1](KEYPOS,KEYLEN)
2490 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2910 IF LOCKED=0 THEN GOTO 1400
2920 LET LOCKED=0
2930 LET K$=K$(1,LEN(K$)-1)+CHR(ASC(K$(LEN(K$),1))+1)
2940 READ (CHANNEL,KEY=K$,DOM=1400,END=3000)IOL=LIST02
2990 GOTO 1420
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=WIN_X-10
3035 LET MESSAGE$[0]="No Records Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3085 IF POS("SKIP"=OLDVAL$)=0 THEN GOTO 0800
3090 GOTO 4000
3500 REM " --- Display last page"
3510 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
4000 REM " --- All done"
4010 IF SELECTION$="" THEN LET SELECTION$=OLDVAL$
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,5
6280 LET L=1,X1=1
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 CLOSE (SYS01_DEV,ERR=9911)
9915 CLOSE (SYM06_DEV,ERR=9916)
9920 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9930 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
9940 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9945 IF SELECTION$="END" THEN LET SELECTION$=OLDVAL$
9947 IF POS("NO COMPANY"=SELECTION$)>0 THEN LET SELECTION$=""
9948 IF POS("SKIP"=SELECTION$)>0 THEN LET SELECTION$=""
9949 IF POS("PRIOR"=SELECTION$)>0 THEN LET SELECTION$=""
9950 EXIT 
9999 END

0010 REM "SYM - Process Maintenance (Hard Copy Listing)"
0020 REM "Program SYM.IF"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOList's"
0210 SYM02A: IOLIST *,Z0$(1)
0220 SYM03A: IOLIST *,Z1$(1)
0230 SYM09A: IOLIST A0$(1),A1$(1),A2$(1),A3$,A[ALL]
0240 SYM19A: IOLIST B0$(1),B1$(1),B2$(1),B3$,B[ALL]
0250 SYM39A: IOLIST C0$(1),C1$,C2$,C[ALL]
0260 SYM49A: IOLIST D0$(1),D1$(1),D2$(1),D3$,D[ALL]
0270 GLM06A: IOLIST X0$(1),X1$(1),X2$(1),X3$,X[ALL]
0500 REM " --- Initializations"
0510 LET L9=59,L=L9+1,WIDTH=132,PAGE=0,HEADINGS=2,WHEN$=F0$(7,3),CLOCK$=""
0520 DIM HEADINGS$[HEADINGS]
0530 LET ORIGINAL$=A0$,START$=A0$,FINISH$=A0$
0550 DIM B0$(18),B1$(30),B2$(11),B[3],C0$(16),C[3],D0$(3),D1$(50),D2$(30),D[3]
0560 DIM X0$(20),X1$(64),X2$(30),X[6],Z0$(24),Z1$(30)
0700 REM " --- Background"
0710 PRINT @(0,L0),'CE','SB',
0720 PRINT @(11,L0+2),"Starting Process:",@(13,L0+3),"Ending Process:",
0790 PRINT 'SF',
1000 REM " --- Starting Process"
1010 LET V0$="S",V1$="C",V2$=START$,V3$="",V0=10,V1=29,V2=L0+2
1020 LET V4$="Enter Process ID (<F1>=First  <F3>=Lookup)"
1030 GOSUB 7000
1040 ON V3 GOTO 1070,1050,1000,1060,4000
1050 LET START$="",A0$(1)="First",A1$(1)=""; GOTO 1095
1060 GOSUB 6000
1065 IF V$="" THEN GOTO 1000
1070 IF FNP$(V$)="" THEN GOTO 1050
1075 LET V$=V$+FILL(V0),V$=V$(1,V0)
1080 FIND (SYM09_DEV,KEY=V$,DOM=1000)IOL=SYM09A
1090 LET START$=V$
1095 PRINT @(V1,V2),A0$,"  ",A1$,
1200 REM " --- Ending Process"
1210 LET V0$="S",V1$="C",V2$=FINISH$,V3$="",V0=10,V1=29,V2=L0+3
1220 LET V4$="Enter Process ID (<F1>=Last  <F3>=Lookup)"
1230 GOSUB 7000
1240 ON V3 GOTO 1270,1250,1000,1260,4000
1250 LET FINISH$="",A0$(1)="Last",A1$(1)=""; GOTO 1295
1260 GOSUB 6000
1265 IF V$="" THEN GOTO 1200
1270 IF FNP$(V$)="" THEN GOTO 1250
1275 LET V$=V$+FILL(V0),V$=V$(1,V0)
1280 IF V$<START$ THEN GOTO 1100
1285 FIND (SYM09_DEV,KEY=V$,DOM=1200)IOL=SYM09A
1290 LET FINISH$=V$
1295 PRINT @(V1,V2),A0$,"  ",A1$,
1500 REM " --- Correct?"
1510 LET V4$="Is The Above Information Correct (Y/N)?"
1520 LET V0$="S",V1$="KC",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
1530 GOSUB 7000
1540 ON V3 GOTO 1550,1500,1500,1500,4000
1590 ON POS(V$="YN") GOTO 1500,1600,1000
1600 REM " --- Open Printer"
1610 CALL "SYC.GA",7,1,"","",STATUS
1615 IF STATUS THEN GOTO 4000
1700 REM " --- Open Notification Box"
1710 CALL "SYC.NB"," Printing",19,COLUMN
1800 REM " --- Initialize Report Headings"
1810 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$+" Listing"
1820 IF START$="" THEN LET FROM$="First" ELSE LET FROM$=START$
1830 IF FINISH$="" THEN LET THRU$="Last" ELSE LET THRU$=FINISH$
1840 LET HEADINGS$[2]="From "+FROM$+" Thru "+THRU$
1900 REM " --- Postion SYM-09 Process Master File"
1910 READ (SYM09_DEV,KEY=START$,DOM=2000)
1920 LET SYM09_K$=START$
1930 GOTO 2020
2000 REM " --- SYM-09 Process Master File"
2010 LET SYM09_K$=KEY(SYM09_DEV,END=4000)
2020 IF FINISH$<>"" AND SYM09_K$>FINISH$ THEN GOTO 4000
2030 READ (SYM09_DEV,KEY=SYM09_K$)IOL=SYM09A
2050 PRINT @(COLUMN,11),A0$," ",FILL(8),
2060 GOSUB 5100
2070 PRINT (7)A0$,@(12),A1$,@(44),A2$(1,2),@(52),A2$(3,1)
2090 LET L=L+1
2200 REM " --- SYM-19 Process Definition File"
2205 GOSUB 5200
2210 READ (SYM19_DEV,KEY=A0$,DOM=2220)
2220 LET SYM19_K$=KEY(SYM19_DEV,END=2600)
2230 IF POS(A0$=SYM19_K$)<>1 THEN GOTO 2600
2240 READ (SYM19_DEV,KEY=SYM19_K$)IOL=SYM19A
2250 PRINT @(COLUMN,11),B0$(1,10)," ",B0$(11,8),
2260 IF L+1>L9 THEN GOSUB 5200
2270 PRINT (7)@(12),B0$(11,8),@(22),B1$,@(56),B2$(1,1),
2280 LET COUNT=0
2290 IF GL$<>"Y" THEN GOTO 2450
2300 REM " --- GLM-06 Audit Control Detail File"
2305 LET FIRM$=""
2310 READ (GLM06_DEV,KEY="",DOM=2320)
2320 LET GLM06_K$=KEY(GLM06_DEV,END=2450),FIRM$=GLM06_K$(1,2)
2330 READ (GLM06_DEV,KEY=FIRM$+B0$,DOM=2400)IOL=GLM06A
2335 LET AUDIT$="None",SUMMARY$="None",DETAIL$="Summary",USEDDATE$="",USEDTIME$="",UPDTDATE$="",UPDTTIME$=""
2340 IF X1$(37,1)="Y" THEN LET AUDIT$=STR(X[0]:"000000")
2345 IF X1$(38,1)="Y" THEN LET SUMMARY$=X1$(31,6)
2350 IF X1$(39,1)="Y" THEN LET DETAIL$="Detail"
2355 IF X1$(39,1)="A" THEN LET DETAIL$="By Acct"
2360 IF FNP$(X1$(11,6))<>"" THEN LET USEDDATE$=FNB6$(X1$(11,6)),USEDTIME$=FNT$(X1$(17,4))
2365 IF FNP$(X1$(21,6))<>"" THEN LET UPDTDATE$=FNB6$(X1$(21,6)),UPDTTIME$=FNT$(X1$(27,4))
2370 IF L+1>L9 THEN GOSUB 5200
2380 PRINT (7)@(65),X0$(1,2),@(70),X1$(1,2),@(74),DETAIL$,@(83),AUDIT$,@(92),USEDDATE$,@(101),USEDTIME$,@(108),UPDTDATE$,@(117),UPDTTIME$,@(124),SUMMARY$
2390 LET L=L+1,COUNT=COUNT+1
2400 REM " --- Get Next GLM-06 Record"
2410 READ (GLM06_DEV,KEY=FIRM$+$FF$,DOM=2420)
2420 GOTO 2320
2450 REM " --- Get Next SYM-19 Record"
2460 IF COUNT=0 THEN PRINT (7)""
2490 GOTO 2220
2600 REM " --- SYM-39 Process Batch Files File"
2605 GOSUB 5300
2610 READ (SYM39_DEV,KEY=A0$,DOM=2620)
2620 LET SYM39_K$=KEY(SYM39_DEV,END=2800)
2630 IF POS(A0$=SYM39_K$)<>1 THEN GOTO 2800
2640 READ (SYM39_DEV,KEY=SYM39_K$)IOL=SYM39A
2650 PRINT @(COLUMN,11),C0$(1,10)," ",C0$(11,6),FILL(2),
2655 LET Z1$(1)="Not On File"
2660 READ (SYM03_DEV,KEY=C0$(11,6),DOM=2670)IOL=SYM03A
2670 IF L+1>L9 THEN GOSUB 5300
2680 PRINT (7)@(12),C0$(11,6),@(20),Z1$
2685 LET L=L+1
2690 GOTO 2620
2800 REM " --- SYM-49 Process Batch Control File"
2805 GOSUB 5400
2810 READ (SYM59_DEV,KEY=A0$,DOM=2820)
2820 LET SYM59_K$=KEY(SYM59_DEV,END=3000)
2830 IF POS(A0$=SYM59_K$)<>1 THEN GOTO 3000
2840 READ (SYM59_DEV,KEY=SYM59_K$)
2850 PRINT @(COLUMN,11),A0$," ",SYM59_K$(13,3),FILL(5),
2860 READ (SYM49_DEV,KEY=SYM59_K$(13,3),DOM=2990)IOL=SYM49A
2870 LET OPENDATE$="",OPENTIME$="",LASTDATE$="",LASTTIME$=""
2880 IF FNP$(D1$(16,6))<>"" THEN LET OPENDATE$=FNB6$(D1$(16,6)),OPENTIME$=FNT$(D1$(22,4))
2890 IF FNP$(D1$(26,6))<>"" THEN LET LASTDATE$=FNB6$(D1$(26,6)),LASTTIME$=FNT$(D1$(32,4))
2900 LET Z0$(1)="Not On File"
2910 READ (SYM02_DEV,KEY=D1$(13,3),DOM=2920)IOL=SYM02A
2950 IF L+1>L9 THEN GOSUB 5400
2960 PRINT (7)@(13),D1$(11,2),@(19),D0$,@(25),D2$,@(57),Z0$,@(83),OPENDATE$,@(92),OPENTIME$,@(99),LASTDATE$,@(108),LASTTIME$
2970 LET L=L+1
2990 GOTO 2820
3000 REM " --- Get Next SYM-19 Record"
3020 GOTO 2000
4000 REM " --- Close Printer"
4010 CLOSE (7,ERR=4020)
4050 REM " --- Reset SYM-09 File Pointer"
4060 READ (SYM09_DEV,KEY=ORIGINAL$,DOM=4070)
4100 REM " --- Run Header Overlay"
4110 RUN "SYM.IA"
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5090 RETURN 
5100 REM " --- Process ID Heading"
5110 IF L+5>L9 THEN GOSUB 5000
5120 PRINT (7)""
5130 PRINT (7)@(41),"Required"
5140 PRINT (7)"Process ID",@(12),"Description",@(42),"System",@(50),"Batch"
5150 PRINT (7)FILL(55,"-")
5180 LET L=L+4
5190 RETURN 
5200 REM " --- Process Definition Heading"
5210 IF L+4>L9 THEN GOSUB 5000
5220 PRINT (7)""
5230 PRINT (7)@(12),FILL(15,"-"),"Process Definition",FILL(15,"-"),@(83),"Last",@(94),"Last Used",@(109),"Last Updated"
5240 PRINT (7)@(12),"Program",@(22),"Description",@(54),"Create",@(64),"Firm",@(69),"Jrnl",@(74),"Detail",@(83),"Audit #",@(92),"Date",@(101),"Time",@(108),"Date",@(117),"Time",@(124),"File"
5280 LET L=L+3
5290 RETURN 
5300 REM " --- Batched Files Heading"
5310 IF L+4>L9 THEN GOSUB 5000
5320 PRINT (7)""
5330 PRINT (7)@(12),FILL(12,"-"),"Batched Files",FILL(13,"-")
5340 PRINT (7)@(12),"File",@(20),"Description"
5380 LET L=L+3
5390 RETURN 
5400 REM " --- Batch Control Heading"
5410 IF L+4>L9 THEN GOSUB 5000
5420 PRINT (7)""
5430 PRINT (7)@(12),FILL(13,"-"),"Existing Batches",FILL(14,"-"),@(84),"When Opened",@(101),"Last Used"
5440 PRINT (7)@(12),"Firm",@(18),"Batch",@(25),"Description",@(57),"Created By",@(83),"Date",@(92),"Time",@(99),"Date",@(108),"Time"
5480 LET L=L+3
5490 RETURN 
6000 REM " --- Process ID Lookup"
6010 LET NMBR=1
6015 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6020 LET DESCRIPT$[0]="Process",FIELD[0]=1,POSITION[0]=1,LENGTH[0]=10
6025 LET DESCRIPT$[1]="Description",FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6030 LET V$="NO COMPANY"
6035 CALL "SYC.LK",SYM09_DEV,"",NMBR,"Process ID Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6040 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8070 DEF FNT$(Q$)=Q$(1,2)+":"+Q$(3,2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "APM - Vendor Maintenance (Detail Information)"
0020 REM "Program APM.MD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 APM10B: IOLIST X0$
0220 APM10C: IOLIST X0$
0230 APM10D: IOLIST X0$
0240 GLM01A: IOLIST X$,X0$
0800 REM " --- Options
0810 IF P3$(2,1)<>"Y" AND O7=2 THEN LET I0=I0+1
0820 ON O7 GOTO 0900,0830,3900
0830 IF F9$(1,1)<>"1" THEN LET I0=0; GOTO 0900
0840 IF F9$(2,1)<>"1" THEN LET I0=1; GOTO 2100
0850 IF F9$(3,1)<>"1" THEN LET I0=2; GOTO 2200
2000 REM " --- Dist Code
2010 IF O7<>2 THEN LET I0=0
2020 IF P3$(2,1)<>"Y" THEN GOTO 3900
2030 LET V0$="S",V1$="KCR",V2$=B1$(1,2),V0=2,V1=15,V2=12,V4$="Enter A Valid Distribution Code, <F3>=Code Lookup"
2040 GOSUB 7000
2041 IF V3=4 THEN GOTO 3900
2042 IF V3<>3 THEN GOTO 2050
2044 LET V4=1,V5=APM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="B"
2046 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
2048 IF V$="" THEN GOTO 2000
2050 LET B1$(1,2)=V$
2060 FIND (APM10_DEV,KEY=N0$+"B"+B1$(1,2),DOM=2000)IOL=APM10B
2070 PRINT @(V1+3,V2),X0$(6,20),
2090 GOTO 3900
2100 REM " --- Payment Group
2120 LET V0$="S",V1$="KC",V2$=B1$(3,2),V0=2,V1=15,V2=13,V4$="Enter A Valid Payment Group, <F3>=Code Lookup"
2130 GOSUB 7000
2131 IF V3=2 THEN IF P3$(2,1)="Y" THEN GOTO 2190 ELSE GOTO 2100
2132 IF V3<>3 THEN GOTO 2140
2134 LET V4=1,V5=APM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="D"
2136 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
2138 IF V$="" THEN GOTO 2100
2140 LET B1$(3,2)=V$
2150 FIND (APM10_DEV,KEY=N0$+"D"+B1$(3,2),DOM=2100)IOL=APM10D
2160 PRINT @(V1+3,V2),X0$(6,20),
2190 GOTO 3900
2200 REM " --- Terms Code
2220 LET V0$="S",V1$="KC",V2$=B1$(5,2),V0=2,V1=15,V2=14,V4$="Enter A Valid Terms Code, <F3>=Code Lookup"
2230 GOSUB 7000
2231 IF V3=2 THEN GOTO 2290 ELSE IF V3=4 THEN GOTO 3900
2232 IF V3<>3 THEN GOTO 2240
2234 LET V4=1,V5=APM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="C"
2236 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
2238 IF V$="" THEN GOTO 2200
2240 LET B1$(5,2)=V$
2250 FIND (APM10_DEV,KEY=N0$+"C"+B1$(5,2),DOM=2200)IOL=APM10C
2260 PRINT @(V1+3,V2),X0$(6,20),
2290 GOTO 3900
2300 REM " --- GL Dist
2310 IF GL$<>"Y" THEN GOTO 2390
2320 LET V0$="M",V1$="C",V2$=B2$(1,G[3]),V3$=G1$,V0=G[4],V1=15,V2=15
2325 LET V4$="Enter A Valid G/L Account, <F3>=Lookup"
2330 GOSUB 7000
2332 IF V3=4 THEN GOTO 3900
2335 IF V3=2 THEN GOTO 3900
2337 IF V3=3 THEN GOTO 5000
2340 LET B2$(1)=V$
2345 IF POS("0"<>V$)=0 THEN PRINT @(V1,V2),'CL',; GOTO 2390
2350 FIND (GLM01_DEV,KEY=N0$+B2$,DOM=2300)IOL=GLM01A
2360 PRINT @(V1,V2),FNF$(B2$(1,G[3]),G1$),@(V1+G5+1,V2),X0$(1,35),
2390 GOTO 3900
3900 REM " --- Next Field
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,2300,3990
4000 REM " --- Overlay
4005 LET V3=0
4010 IF GL$="Y" THEN IF FNP$(B2$(1,G[4]))="" THEN LET O9=0
4020 RUN "APM.MC"
5000 REM " --- GL Lookup
5040 CALL "SYC.LG",V$
5060 IF V$="" THEN GOTO 2300
5080 GOTO 2340
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9220 RUN "APM.MA"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

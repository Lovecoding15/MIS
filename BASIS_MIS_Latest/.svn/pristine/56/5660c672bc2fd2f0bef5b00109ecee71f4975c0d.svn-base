0010 REM "WOE - WO Materials Add, Commit, Uncommit, Pick List Reprint Flag"
0020 REM "Program WOE.CB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "09Jul96 JJD: Err 47 Deleting entire Commit when no detail lines left (6710)"
0032 REM "02Jan97 JWK: Not removing WOE13A record on Delete (4920)
0040 REM 
0055 REM "L9=MAX LINES, L1=FIRST LINES, L0=HEADER LINE, L=CURRENT LINE
0085 SETESC 9000
0090 SETERR 9000
0095 PRECISION IVS01_PRECISION
0200 REM " --- IOLISTs"
0210 WOE04C: IOLIST C0$
0220 WOE23A: IOLIST W0$(1),W1$(1),W[ALL]
0250 IVM01A: IOLIST D0$,D1$(1),D2$
0280 IVM02A: IOLIST B0$,B1$,B2$,B[ALL]
0500 REM " --- Init Data
0510 DIM W0$(14),W1$(40),W[7],V[3],B[16],D1$(DESCLEN)
0550 LET L0=8,L1=9,L9=19,K9$="",O9$="",H1=77-M2*5
0560 LET W0$(1)=A0$(1,11),L=L1
0700 REM " --- Background
0710 PRINT 'SB',@(0,L0),"Seq Wh Item Code",@(9+P[0]),@(H1+M2-4),"Ord",@(H1+M2*2-6),"Issue",@(77-M2*2-4),"Bal",@(77-M2-6),"Avail",@(77-7),"On Ord",'SF',@(0,L0+1),'CE',
0900 REM " --- List"
0910 GOSUB 5100
0990 GOTO 4000
1000 REM 
1020 IF L>L9 THEN PRINT @(0,21),'CL',@(0,22),'CL',@(0,L1),'LD','LD'; LET L=L-2; GOTO 1020
1100 REM " --- Get Next Key"
1120 LET K$=KEY(WOE23_DEV,END=1400)
1130 IF K$="" THEN GOTO 1400
1135 IF K$(1,2)<>N0$ THEN GOTO 1400
1140 IF K$(1,11)<>A0$(1,11) THEN GOTO 1400
1150 READ (WOE23_DEV)IOL=WOE23A
1160 GOTO 1481
1400 REM " --- Seq"
1405 PRINT @(0,21),'CL',@(0,22),'CL',
1407 IF L>L9 THEN PRINT @(0,L1),'LD','LD'; LET L=L-2; GOTO 1407
1410 LET W0=NUM(W0$(12,3))+1
1420 LET V0$="Z",V1$="ER",V2$=STR(W0),V3$="",V0=3,V1=0,V2=L,I0=0
1430 GOSUB 7000
1440 IF V3=4 THEN GOTO 4000
1450 IF V$="000" THEN GOTO 1400
1460 LET W0$(12,3)=V$
1480 FIND (WOE23_DEV,KEY=W0$(1,14),DOM=1600)IOL=WOE23A
1481 DIM D1$(DESCLEN),D2$(10),B[16]; LET D1$(1)="*** Not On File ***"
1482 FIND (IVM01_DEV,KEY=N0$+W1$(21),DOM=1484)IOL=IVM01A
1484 LET D1$=FNITEM$(D1$,DESC[1],DESC[2],DESC[3])
1486 FIND (IVM02_DEV,KEY=N0$+W1$(19),DOM=1490)IOL=IVM02A
1495 IF W0$(1,14)<=K9$ THEN GOSUB 5400
1500 GOSUB 5000
1520 LET O1$=W1$(19),O1=W[0]-W[1]
1530 GOTO 3300
1600 REM " --- Setup"
1620 DIM W[4]
1630 REM " --- Set Up Defaults
1640 LET W1$(1)="",W1$(4,3)=F0$(7,3),O1$="",O1=0,W1$(19)=A1$(49)
1660 PRINT @(4,L),W1$(19,2)
1690 GOTO 3200
2000 REM " --- Item Code
2020 LET V0$="S",V1$="KCR",V2$="",V3$="",V4$="Enter A Valid Item Number, <F3>=Lookup",V0=P[0],V1=7,V2=L,I0=1
2040 GOSUB 7000
2050 IF V3=2 THEN GOTO 3200
2090 IF V3=3 THEN CALL "SYC.LI",V$
2100 IF V$="" THEN GOTO 2000
2110 LET W1$(21)=V$
2115 DIM D1$(DESCLEN)
2120 FIND (IVM01_DEV,KEY=N0$+W1$(21),DOM=2000)IOL=IVM01A
2130 LET D1$=FNITEM$(D1$,DESC[1],DESC[2],DESC[3])
2140 LET W1$(21)=V$,W1$(1,2)=D2$(4,2)
2160 FIND (IVM02_DEV,KEY=N0$+W1$(19),DOM=2000)IOL=IVM02A
2165 LET W[2]=B[11]
2180 GOSUB 5000
2190 GOTO 3200
2200 REM " --- Unit Of Measure
2210 PRINT 'SB',@(50,L+1),"Date Req'd: ",'SF'
2220 LET V0$="S",V1$="",V2$=W1$(1,2),V3$="",V4$="",V0=2,V1=44,V2=L+1
2240 GOSUB 7000
2260 LET W1$(1,2)=V$
2290 GOTO 3200
2300 REM " --- Date Required
2310 PRINT 'SB',@(40,L+1),'SB',"UM: ",'SF'
2320 IF POS(" "<>W1$(4,3))=0 THEN LET W1$(4,3)=F0$(7,3)
2340 LET V0$="D",V1$="C",V2$=W1$(4,3),V3$="",V4$="Enter The Date That This Item Will Be Required. ",V0=3,V1=62,V2=L+1
2360 GOSUB 7000
2380 LET W1$(4,3)=V$
2390 GOTO 3200
2500 REM " --- Quantity Required
2520 LET V0$="N",V1$="C",V2$=STR(W[0]),V3$=M2$,V4$="Enter Order Quantity Required.",V0=0,V1=H1,V2=L
2540 GOSUB 7000
2560 LET W[0]=V
2580 GOSUB 5060
2590 GOTO 3200
3200 REM " --- Seq Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,2000,2500,2200,2300,3300
3300 REM " --- Correct"
3310 PRINT @(78,L),'CL',@(0,22),'CL'
3320 LET V0$="S",V1$="C",V2$="",V0=1,V1=78,V2=L,K9$=W0$(1,14),V4$="<Enter>=Continue     <F2>=Change     <D>=Delete",I0=5
3330 GOSUB 7000
3340 IF V3=2 THEN GOTO 3200
3350 IF V$="" THEN GOTO 3380
3360 IF V$="D" THEN GOTO 3700
3370 GOTO 3300
3390 PRINT @(0,21),'LD','LD',
3400 REM " --- Write"
3410 IF O1$=W1$(19) AND O1=W[0] THEN GOTO 3430
3420 IF O9$="0" THEN LET O9$="1"
3425 IF O1$="" THEN GOTO 3440
3430 LET S9$=O1$,S9=O1,S8=-1; GOSUB 6000
3440 LET S9$=W1$(19),S9=W[0]-W[1],S8=1; GOSUB 6000
3460 WRITE (WOE23_DEV,KEY=W0$(1,14))IOL=WOE23A
3470 IF V3=4 THEN GOTO 4000
3480 LET L=L+2,O1$=W1$(19),O1=W[0]-W[1]
3490 GOTO 1000
3700 REM " --- Delete"
3710 IF O1$="" THEN GOTO 3760
3720 LET S9$=O1$,S9=O1,S8=-1
3730 GOSUB 6000
3760 REMOVE (WOE23_DEV,KEY=W0$(1,14),DOM=3780)
3770 IF O9$="0" THEN LET O9$="1"
3780 PRINT @(0,L),'LD','LD'
3790 GOTO 1000
4000 REM " --- Options"
4005 IF O9$="1" THEN GOSUB 6500
4010 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Detail Information Correct (Y/N/Review/List/Delete)?",V0=7,V1=FNV(V4$),V2=22
4015 GOSUB 7000
4020 IF V$="" THEN GOTO 4100
4030 IF V$="DELETE" THEN GOTO 4700
4040 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4050 IF V$="L" OR V$="LIST" THEN GOTO 4400
4070 ON POS(V$="YN") GOTO 4000,4100,1400
4100 REM " --- Close & Get Ready
4130 CALL "SYC.WC",1,0,80,0,0,L0-1,0
4140 PRINT @(0,L0),'CE'
4200 LET O9=1
4250 RUN "WOE.CA"
4400 REM " --- List"
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4620 READ (WOE23_DEV,KEY=A0$(1,11)+"000",DOM=4630)
4640 GOSUB 5400
4650 LET V3=0
4690 GOTO 1000
4700 REM " --- Delete Here
4720 READ (WOE23_DEV,KEY=A0$,DOM=4800)
4800 LET K$=KEY(WOE23_DEV,END=4900)
4820 IF K$(1,11)<>A0$(1,11) THEN GOTO 4900
4830 READ (WOE23_DEV,KEY=K$)IOL=WOE23A
4840 LET S9$=W1$(19),S9=W[0]-W[1],S8=-1
4850 GOSUB 6000
4860 REMOVE (WOE23_DEV,KEY=K$,DOM=4890)
4890 GOTO 4800
4900 REMOVE (WOE04_DEV,KEY=A0$(1,4)+"C"+A0$(5,7),DOM=4920)
4920 REMOVE (WOE13_DEV,KEY=A0$,DOM=4930)
4930 GOSUB 6700
4940 GOTO 4100
5000 REM " --- Display"
5010 PRINT @(0,L),W0$(12,3),@(4),W1$(19,2),@(7),W1$(21),@(9+P[0]),
5070 PRINT @(H1,L),W[0]:M2$,W[1]:M2$,W[0]-W[1]:M2$,B[0]-B[2]:M2$,B[1]:M2$
5080 PRINT @(7,L+1),D1$,@(40,L+1),'CL','SB',@(40,L+1),"UM: ",'SF',@(44),W1$(1,2),'SB',@(50,L+1),"Date Req'd: ",'SF',@(62),FNB$(W1$(4,3)),
5090 RETURN 
5100 REM " --- List"
5110 GOSUB 5400
5120 READ (WOE23_DEV,KEY=A0$,DOM=5130)
5130 LET K$=KEY(WOE23_DEV,END=5300)
5135 IF K$(1,2)<>N0$ THEN GOTO 5300
5140 IF K$(1,11)<>W0$(1,11) THEN GOTO 5300
5145 IF O9$="" THEN LET O9$="0"
5150 FIND (WOE23_DEV,KEY=K$)IOL=WOE23A
5155 DIM D1$(DESCLEN),B[16]; LET D1$(1)="*** Not On File ***"
5160 FIND (IVM01_DEV,KEY=W0$(1,2)+W1$(21),DOM=5170)IOL=IVM01A
5180 LET D1$=FNITEM$(D1$,DESC[1],DESC[2],DESC[3])
5190 FIND (IVM02_DEV,KEY=N0$+W1$(19),DOM=5200)IOL=IVM02A
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Press <Enter> To Continue:",V0=1,V1=FNV(V4$),V2=22
5215 GOSUB 7000
5220 IF V3=4 THEN GOTO 5300
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+2,K9$=W0$(1,14)
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5420 PRINT @(0,L1),'CE',
5440 LET L=L1
5490 RETURN 
6000 REM " --- Commit/Uncommit Here
6010 IF S8>0 THEN LET ACTION$="CO"
6015 IF S8<0 THEN LET ACTION$="UC"
6020 LET ITEMS$[0]=N0$,ITEMS$[1]=S9$(1,2),ITEMS$[2]=S9$(3,20)
6040 LET REFS[0]=S9
6060 CALL "IVC.UA",ACTION$,IVFILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEMS$[ALL],REFS$[ALL],REFS[ALL],STATUS
6090 RETURN 
6500 REM " --- Reprint???
6520 READ (WOE04_DEV,KEY=A0$(1,4)+"C"+A0$(5,7),DOM=6600)
6540 GOTO 6790
6600 LET V4$="Changes Have Been Made. Reprint Pick List"
6605 PRINT @(V1,V2),'RB',
6610 CALL "SYC.YN",1,V4$,2,V$,V3
6620 IF V$="N" THEN GOTO 6790
6640 IF V$<>"Y" THEN GOTO 6600
6650 LET V0$="S",V1$="C",V2$="",V3$="",V4$="This Pick List Will Print In The Next Batch (<Enter>=Continue)",V0=1,V1=FNV(V4$),V2=22
6660 GOSUB 7000
6670 LET C0$=A0$(1,4)+"C"+A0$(5,7)
6680 WRITE (WOE04_DEV,KEY=C0$)IOL=WOE04C
6700 READ (WOE15_DEV,KEY=A0$(1,11),DOM=6710)
6710 LET K15$=KEY(WOE15_DEV,END=6730)
6715 IF K15$(1,11)=A0$(1,11) THEN REMOVE (WOE15_DEV,KEY=K15$)
6730 READ (WOE25_DEV,KEY=A0$(1,11),DOM=6740)
6740 LET K2$=KEY(WOE25_DEV,END=6780)
6750 IF K2$(1,11)<>A0$(1,11) THEN GOTO 6790
6760 REMOVE (WOE25_DEV,KEY=K2$)
6770 GOTO 6740
6790 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "IVU - Closed Lot/Serial Number Purge"
0020 REM "Program IVU.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="IVM-07",FILES$[2]="SYS-01",FILES$[3]="IVM-08"
0130 LET OPTIONS$[1]="FL"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 IF CHANNELS[1]=0 THEN GOTO 9900
0180 LET IVM07_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 IVM07A: IOLIST A0$,A1$,A[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0445 IF POS(P3$(17,1)="SL")=0 THEN GOTO 4900
0450 DIM P[7]
0490 LET P[0]=NUM(P2$(1,2)),P[6]=NUM(P4$(1,2)),P[7]=FNYY_YEAR(P4$(3,2)),P[2]=NUM(P2$(7,2))
0500 REM " --- Init Data"
0540 DIM A[12]
0600 REM " --- Additional Opens"
0610 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS>0 THEN GOTO 9900
0630 LET IVM08_DEV=CHANNELS[3]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 PRINT 'SB',@(5,3),"Purge Closed Lot/Serial Numbers Through: ",'SF',
0800 REM " --- Determine Current Period Ending Date"
0810 LET V=P[6]; CALL "SYC.PA",SYS01_DEV,V,P[7],V0$,V9$,STATUS
0820 LET I7$=V9$
1000 REM " --- Options"
1020 LET V0$="D",V1$="REC",V2$=P9$,V3$="",V4$="Enter Purge Through Date for Activity",V0=8,V1=46,V2=3
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1055 LET P9$=V$
1060 IF P9$<=I7$ THEN GOTO 1100
1070 CALL "SYC.YN",0,"Current Period Ends: "+FNB$(I7$)+". Continue",0,V$,V3
1080 IF V3=4 THEN GOTO 9900
1090 IF V$="NO" THEN GOTO 1000
1095 IF V$<>"YES" THEN GOTO 1070
1100 IF P9$<=F0$(7,3) THEN GOTO 1200
1120 CALL "SYC.YN",0,"Current Menu Date Is: "+FNB$(F0$(7))+". Continue",0,V$,V3
1130 IF V3=4 THEN GOTO 9900
1160 IF V$="NO" THEN GOTO 1000
1170 IF V$<>"YES" THEN GOTO 1120
4000 REM " --- Options"
4020 LET V0$="S",V1$="CE",V2$="NO",V3$="",V4$="Is The Above Information Correct (Yes/No)?",V0=3,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 IF V$="NO" THEN GOTO 1000
4060 IF V$<>"YES" THEN GOTO 4000
4080 REM " --- Disallow 'M'enu option in Error Routine
4090 LET EXIT_CTRL=1
4200 REM " --- Purge IVM-07 Here"
4210 READ (IVM07_DEV,KEY=N0$,DOM=4220)
4220 CALL "SYC.NB","Purging:",P[0]+P[2]+4,COLUMN
4300 REM " --- Main Read"
4310 READ (IVM07_DEV,END=9900)IOL=IVM07A
4320 IF A0$(1,2)<>N0$ THEN GOTO 9900
4330 PRINT @(COLUMN,11),A0$(3,2)," ",A0$(5,P[0])," ",A0$(25,P[2]),
4400 REM " --- Check The Dates"
4410 IF A1$(54,1)<>"C" THEN GOTO 4300
4420 IF POS(P9$<A1$(61,27),3)>0 THEN GOTO 4300
4500 REM " --- Remove Records Here"
4510 LET K1$=N0$+A0$(25,20)+A0$(3,22)
4520 REMOVE (IVM08_DEV,KEY=K1$,DOM=4521)
4530 REMOVE (IVM07_DEV,KEY=A0$,DOM=4531)
4590 GOTO 4300
4900 REM " --- Lotted/Serialized Inventory not enabled"
4910 CALL "IVC.SB","L",STATUS
4990 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "ARM - Terms Code Maintenance"
0020 REM "Program ARM.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="ARM-10"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],ARM10_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 ARM10A: IOLIST A0$(1),A[ALL]
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0265 ARS01C: IOLIST C9$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM C9$(36)
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$,C9$(9,1)="N"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"AR01",DOM=0500)IOL=ARS01C
0500 REM " --- Initializations"
0510 DIM A0$(32),A[2]
0550 LET CHANNEL=ARM10_DEV,TITLE$="AR Terms Codes",RECORD$="A"
0560 LET ENTER$="Enter The ",TC$=" For This Terms Code"
0590 LET K0=1,K9=5,M8$="##0.00-",M9$="###0-",A0$(1,3)=N0$+RECORD$
0700 REM " --- Background"
0720 PRINT 'SB',@(3,3),"Terms Code:",@(2,4),"Description:",@(9,5),"Type:",@(3,6),"Discount %:",@(3,7),"Net Day(s):",@(0,8),"Discount Days:",'SF',
0730 IF C9$(7,1)="Y" THEN PRINT 'SB',@(2,9),"Credit Hold:",'SF',
0900 REM " --- Position file"
0990 READ (ARM10_DEV,KEY=A0$(1,3),DOM=1000)
1000 REM " --- Terms Code"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(ARM10_DEV,END=1050)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4)
1050 LET V0$="S",V1$="EKC",V3$="",V0=2,V1=15,V2=3,V4$="Enter A Valid Terms Code (<F3>=Lookup)"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN GOSUB 6000
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(4)=V$
1200 REM " --- Get record"
1210 READ (ARM10_DEV,KEY=A0$(1,K9),DOM=1400)IOL=ARM10A
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Initializations for new record"
1510 DIM A[2]
1550 LET A0$(7)=""
2000 REM " --- Description"
2020 LET V0$="S",V1$="",V2$=A0$(6,20),V3$="",V0=20,V1=15,V2=4,I0=0
2030 LET V4$=ENTER$+"Description"+TC$
2040 GOSUB 7000
2050 ON V3 GOTO 2060,2000,3900,2000,3900
2060 LET A0$(6,20)=V$
2090 GOTO 3900
2100 REM " --- Type"
2120 LET V0$="S",V1$="RC",V2$=A0$(26,1),V3$="PD",V4$="P=Prox     D=Days",V0=1,V1=15,V2=5
2130 IF V2$="A" THEN LET V2$="P"
2140 GOSUB 7000
2145 ON V3 GOTO 2150,2100,3900,2100,3900
2150 IF V$="P" THEN LET V$="A"
2160 LET A0$(26,1)=V$
2190 GOTO 3900
2200 REM " --- Discount %"
2210 LET V4$=ENTER$+"Discount Percentage"+TC$
2220 LET V0$="N",V2$=STR(A[0]),V3$=M8$,V0=0,V1=15,V2=6
2240 GOSUB 7000
2250 ON V3 GOTO 2260,2200,3900,2200,3900
2260 LET A[0]=V
2290 GOTO 3900
2300 REM " --- Net Days"
2310 LET V4$=ENTER$+"Net Days Due"+TC$
2320 LET V0$="N",V2$=STR(A[1]),V3$=M9$,V0=0,V1=15,V2=7
2340 GOSUB 7000
2350 ON V3 GOTO 2360,2300,3900,2300,3900
2360 LET A[1]=V
2390 GOTO 3900
2400 REM " --- Discount Days"
2410 LET V4$=ENTER$+"Discount Days"+TC$
2420 LET V0$="N",V2$=STR(A[2]),V3$=M9$,V0=0,V1=15,V2=8
2440 GOSUB 7000
2450 ON V3 GOTO 2460,2400,3900,2400,3900
2460 LET A[2]=V
2490 GOTO 3900
2500 REM " --- Credit Hold This Terms Code?"
2510 IF C9$(7,1)<>"Y" THEN GOTO 3900
2515 LET V4$="Automatically Credit Hold Customers Or Orders With These Terms (Y/N)?"
2520 LET V0$="Y",V1$="C",V2$=A0$(27,1),V3$="",V0=1,V1=15,V2=9
2530 IF V2$=" " THEN LET V2$="N"
2540 GOSUB 7000
2545 ON V3 GOTO 2550,2500,3900,2500,3900
2550 LET A0$(27,1)=V$
2590 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,2300,2400,2500,3990
3990 LET I0=K0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="KC",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4110 WRITE (ARM10_DEV,KEY=A0$(1,K9))IOL=ARM10A
4190 GOTO 1000
4200 REM " --- Delete"
4210 REMOVE (ARM10_DEV,KEY=A0$(1,K9),DOM=4220)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4900,9900
4900 REM " --- Report Initializations"
4905 LET PROGRAM$="",TOTAL$=TITLE$,HEADINGS=3,NUMBER=6,WIDTH=80
4915 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4920 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4925 DIM HEADINGS$[HEADINGS]
4930 LET FIELD$[0]="Code",FIELD[0]=1,POSITION[0]=4
4935 LET LENGTH[0]=2,TYPE$[0]="S",MASK$[0]=""
4940 LET FIELD$[1]="Description",FIELD[1]=1,POSITION[1]=6
4945 LET LENGTH[1]=20,TYPE$[1]="S",MASK$[1]=""
4950 LET FIELD$[2]="Prox/Days",FIELD[2]=1,POSITION[2]=26
4955 LET LENGTH[2]=1,TYPE$[2]="S",MASK$[2]=""
4960 LET FIELD$[3]="Disc Pct",FIELD[3]=2,POSITION[3]=1
4965 LET LENGTH[3]=0,TYPE$[3]="N",MASK$[3]="-##0.00%"
4970 LET FIELD$[4]="Due Days",FIELD[4]=3,POSITION[4]=1
4972 LET LENGTH[4]=0,TYPE$[4]="N",MASK$[4]="-######0"
4974 LET FIELD$[5]="Disc Days",FIELD[5]=4,POSITION[5]=1
4976 LET LENGTH[5]=0,TYPE$[5]="N",MASK$[5]="-#######0",NUMBER=5
4978 IF C9$(7,1)<>"Y" THEN GOTO 4985
4980 LET NUMBER=6,FIELD$[6]="Cred Hold?",FIELD[6]=1,POSITION[6]=27
4983 LET LENGTH[6]=1,TYPE$[6]="Y",MASK$[6]=""
4985 LET HEADINGS$[0]=N1$,HEADINGS$[1]=TOTAL$+" Listing"
4990 RUN "SYR.AA"
5000 REM " --- Display record"
5010 IF A0$(26,1)="A" THEN LET V$="P" ELSE LET V$=A0$(26,1)
5020 PRINT @(15,3),A0$(4,2),@(15,4),A0$(6,20),@(15,5),V$,@(15,6),A[0]:M8$,@(15,7),A[1]:M9$,@(15,8),A[2]:M9$,
5050 IF C9$(7,1)="Y" THEN PRINT @(15,9),A0$(27,1)
5090 RETURN 
6000 REM " --- Code Lookup"
6010 LET NUMBER=1
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6050 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6060 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6070 LET V$="SKIP",ROW=V2+2
6080 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

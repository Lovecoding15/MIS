0010 REM "SYC - Clear Records In A File For A Partial Key"
0020 REM "Program SYC.FB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "CHANNEL : Channel number of file to remove records from (input)"
0050 REM "PARTIAL$: Partial key to remove (input)"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER CHANNEL,PARTIAL$
0700 REM " --- Background"
0710 CALL "SYC.WA",0,80,3,0,20,TITLE$,NAME$
1000 REM " --- Exclusive control (can we lock it?)"
1010 LOCK (CHANNEL,ERR=9800)
1020 LET FID$=FID(CHANNEL)
1030 PRINT @(1,0),"Clearing ",FID$(9),
1900 REM " --- Position file"
1980 READ (CHANNEL,KEY=PARTIAL$,DOM=2000)
1990 REMOVE (CHANNEL,KEY=PARTIAL$)
2000 REM " --- Remove loop"
2010 LET K$=KEY(CHANNEL,END=4000)
2020 IF POS(PARTIAL$=K$)<>1 THEN GOTO 4000
2030 REMOVE (CHANNEL,KEY=K$)
2090 GOTO 2000
4000 REM " --- All done"
4090 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Unable to lock or erase target file"
9810 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9820 EXIT ERR
9900 REM " --- Return to calling program"
9910 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9920 UNLOCK (CHANNEL,ERR=9921)
9950 EXIT 
9999 END

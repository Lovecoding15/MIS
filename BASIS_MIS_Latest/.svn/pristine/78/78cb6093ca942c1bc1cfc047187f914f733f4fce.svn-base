0010 REM "GLU - Daily Detail Update"
0020 REM "Program GLU.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=1
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLT-06"
0160 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLT06_DEV=CHANNELS[1]
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$(1),A1$(1)
0220 GLM02A: IOLIST B0$(1),A[ALL],U[ALL]
0230 GLT04A: IOLIST C0$(1),C1$(1),C2$,C[ALL]
0240 GLT06A: IOLIST D0$(1),D1$(1),D2$,D[ALL]
0500 REM " --- Initializations"
0510 DIM A0$(2+P[4]),A1$(40),B0$(3+P[4]),A[MP],U[MP]
0520 DIM C0$(26+P[4]),C1$(64),C[4],D0$(26+P[4]),D1$(64),D[4],J1$(80)
0700 REM " --- Background"
0710 CALL "SYC.NB","Updating",M0+9,COLUMN
0800 REM " --- Options"
0810 LET V4$="Are You Ready to Update the Daily Detail Register"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
0980 REM " --- Position File"
0990 READ (GLT04_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Get next GLT-04 record"
1010 LET K$=KEY(GLT04_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 READ (GLT04_DEV,END=4000)IOL=GLT04A
1100 REM " --- Display account being processed"
1110 LET X$=C0$(5,7),PERIOD=NUM(P2$(3,2)),PERIOD=NUM(C0$(14,2))
1120 IF SEQUENCE$="D" THEN LET X$=FNB6$(C0$(16,6))
1130 LET X$=X$+" "+FNF$(C0$(22,P[3]),M0$)
1150 PRINT @(COLUMN,11),X$
1500 REM " --- Retrieve G/L Account Master"
1510 DIM A0$(2+P[4]),A1$(40)
1520 LET A0$(1)=N0$,A0$(3)=C0$(22,P[4])
1530 FIND (GLM01_DEV,KEY=A0$,DOM=1600)IOL=GLM01A
1590 GOTO 2000
1600 REM " --- Missing account master. Create one."
1610 LET A1$(1,35)="(Missing Account Master)",A1$(36,2)="AN"
1620 WRITE (GLM01_DEV,KEY=A0$)IOL=GLM01A
2000 REM " --- Update GLM-02 record"
2010 DIM B0$(3+P[4]),A[MP],U[MP]
2020 GOSUB 6000
2030 LET B0$(1)=A0$,B0$(3+P[4],1)=ID$
2040 FIND (GLM02_DEV,KEY=B0$,DOM=2050)IOL=GLM02A
2050 LET A[PERIOD]=A[PERIOD]+C[0]
2060 IF P4$(3,1)="Y" THEN LET U[PERIOD]=U[PERIOD]+C[1]
2090 WRITE (GLM02_DEV,KEY=B0$)IOL=GLM02A
2500 REM " --- Generate GLT-06 Record"
2510 IF P4$(4,1)<>"Y" THEN GOTO 3000
2520 DIM D0$(26+P[4]),D1$(64),D[4],J1$(80)
2530 LET D0$(1)=C0$(1,2),D0$(3,P[4])=C0$(22,P[4]),D0$(3+P[4],2)=C0$(12,2)
2540 LET D0$(5+P[4],2)=C0$(14,2),D0$(7+P[4],6)=C0$(16,6)
2550 LET D0$(13+P[4],2)=C0$(3,2),D0$(15+P[4],7)=C0$(5,7)
2560 LET D0$(22+P[4])=$FF$,D1$(1)=C1$,D2$=C2$
2570 FOR X=0 TO 4
2580 LET D[X]=C[X]
2590 NEXT X
2600 REM " --- Does record exist?"
2610 READ (GLT06_DEV,KEY=D0$,DOM=2620)
2620 LET D0$(22+P[4],5)="00000"
2630 LET L$=KEYP(GLT06_DEV,END=2650)
2640 IF L$(1,21+P[4])=D0$(1,21+P[4]) THEN LET D0$(22+P[4],5)=L$(22+P[4],5)
2650 LET D0$(22+P[4],5)=STR(NUM(D0$(22+P[4],5))+1:"00000")
2660 WRITE (GLT06_DEV,KEY=D0$)IOL=GLT06A
3000 REM " --- Remove GLT-04 Record"
3010 DIM X0$(26+P[4])
3020 LET X0$(1)=C0$(1,2)+C0$(16,6)+C0$(3,2)+C0$(22,P[4])+C0$(5,7)+C0$(12,4)+C0$(22+P[4])
3030 REMOVE (GLT04_DEV,KEY=C0$,DOM=3040)
3040 REMOVE (GLT05_DEV,KEY=X0$,DOM=3050)
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
6000 REM " --- Which bucket/record?"
6010 LET ID$="0",FY=FNYY_YEAR(P2$(5,2),ERR=6300)
6020 LET PY=FY,PY=FNYY_YEAR(C0$(12,2),ERR=6030)
6100 REM " --- G/L Year Closed"
6110 IF P4$(6,1)<>"Y" THEN GOTO 6200
6120 IF PY<FY THEN LET ID$="2"
6130 IF PY>FY THEN LET ID$="4"
6190 GOTO 6300
6200 REM " --- G/L Year Open"
6210 IF PY=FY-2 THEN LET ID$="2"
6220 IF PY=FY-1 THEN LET ID$="0"
6230 IF PY=FY THEN LET ID$="4"
6300 REM " --- All done"
6390 RETURN 
8000 REM " --- Functions"
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

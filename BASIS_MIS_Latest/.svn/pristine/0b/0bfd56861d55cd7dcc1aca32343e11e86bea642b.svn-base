0010 REM "POE - Purchase Order Entry (Header/Ribbon)"
0020 REM "Program POE.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Sep96 JAL: PO sequence # record is NOT required (423)"
0032 REM "19Sep96 JAL: Do not allow blank/zero purchase order number (1405)"
0033 REM "14Oct96 WGH: Create POE-42 x-ref for 'S' Line Types (6550)"
0034 REM "23Oct96 Caj: Was deleting PO when <F4> was pressed at ITAIC (4040)
0035 REM "04Dec96 Caj: Error 26 @ 1405 if <F2> or <F3> from 'Is This the Correct PO' prompt (1405)
0036 REM "12Feb97 JWK: Allow selection from PO lookup at Req# field (2625,2645)
0037 REM "03Feb98 WGH: Prevent accidentally duplicating a PO number (1050,1282,3660,4040,4710)"
0038 REM "25Mar98 WGH: <F1>=Next not allowed from PO Lookup (2710)"
0039 REM "14Oct99 WGH: When PO is deleted, and a Req from a WO is retained, reset the WO's PO status to the requisition (4215,4330,4600-4690)
0040 REM 
0050 SETERR 9000
0060 SETESC 9000
0070 ON O0 GOTO 0080,1000,4000,4200
0080 BEGIN 
0085 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=40
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0113 LET FILES$[1]="SYS-01",FILES$[2]="POE-01",FILES$[3]="POE-02"
0115 LET FILES$[4]="POE-03",FILES$[5]="POE-04",FILES$[6]="POE-05"
0117 LET FILES$[7]="POE-11",FILES$[8]="POE-12",FILES$[9]="POE-21"
0120 LET FILES$[10]="POE-22",FILES$[11]="POE-31",FILES$[12]="POE-32"
0123 LET FILES$[13]="POE-33",FILES$[14]="POE-34",FILES$[15]="POE-35"
0125 LET FILES$[16]="POE-41",FILES$[17]="POE-42",FILES$[18]="POE-52"
0130 LET FILES$[19]="POM-02",FILES$[20]="POM-04",FILES$[21]="POM-14"
0135 LET FILES$[22]="POS-10",FILES$[23]="APM-01",FILES$[24]="APM-02"
0140 LET FILES$[25]="APM-05",FILES$[26]="APM-09",FILES$[27]="APM-10"
0145 LET FILES$[28]="IVM-01",FILES$[29]="IVM-02",FILES$[30]="IVM-05"
0150 LET FILES$[31]="IVM-10",FILES$[32]="IVM-14"
0155 CALL "SYC.DA",1,1,32,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0165 LET SYS01_DEV=CHANNELS[1],POE01_DEV=CHANNELS[2],POE02_DEV=CHANNELS[3]
0170 LET POE03_DEV=CHANNELS[4],POE04_DEV=CHANNELS[5],POE05_DEV=CHANNELS[6]
0173 LET POE11_DEV=CHANNELS[7],POE12_DEV=CHANNELS[8],POE21_DEV=CHANNELS[9]
0175 LET POE22_DEV=CHANNELS[10],POE31_DEV=CHANNELS[11],POE32_DEV=CHANNELS[12]
0177 LET POE33_DEV=CHANNELS[13],POE34_DEV=CHANNELS[14],POE35_DEV=CHANNELS[15]
0180 LET POE41_DEV=CHANNELS[16],POE42_DEV=CHANNELS[17],POE52_DEV=CHANNELS[18]
0183 LET POM02_DEV=CHANNELS[19],POM04_DEV=CHANNELS[20],POM14_DEV=CHANNELS[21]
0185 LET POS10_DEV=CHANNELS[22],APM01_DEV=CHANNELS[23],APM02_DEV=CHANNELS[24]
0190 LET APM05_DEV=CHANNELS[25],APM09_DEV=CHANNELS[26],APM10_DEV=CHANNELS[27]
0193 LET IVM01_DEV=CHANNELS[28],IVM02_DEV=CHANNELS[29],IVM05_DEV=CHANNELS[30]
0195 LET IVM10_DEV=CHANNELS[31],IVM14_DEV=CHANNELS[32]
0200 REM " --- IOLIST's"
0210 POE02A: IOLIST A0$(1),A1$(1),A2$(1),A3$,A[ALL]; REM "WA:200
0220 POE01A: IOLIST B0$(1),B1$(1),B2$,B3$,B[ALL]
0230 APM02A: IOLIST C0$(1),C1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,I5$,I6$,I7$,I8$,I9$,I10$
0280 POS01A: IOLIST P0$,P1$,P2$,P3$,P4$
0285 POS10N: IOLIST P0$,P[ALL]
0290 APS01A: IOLIST X$,P1$,P2$,X$,X$,M0$
0300 ARS01A: IOLIST X$,P5$,P6$,X$,X$,P7$
0310 POE22A: IOLIST X2$(1)
0315 POE31A: IOLIST X3$(1)
0320 POE32A: IOLIST X3$(1)
0325 POE41A: IOLIST X4$(1)
0330 POE42A: IOLIST X4$(1)
0350 POE52A: IOLIST X5$(1)
0360 POM02A: IOLIST Y0$(1),Y1$(1)
0380 POE11A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0390 POE12A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0400 REM " --- Parameters"
0405 DIM I[6],P[3],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO",AP$="N",AR$="N",SF$="N"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=POS01A
0423 FIND (POS10_DEV,KEY=N0$+"N",DOM=0425)IOL=POS10N
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 LET VENDLEN=NUM(P2$(1,2))
0435 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1)),I[2]=NUM(I2$(7,2))
0445 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0447 LET I[6]=NUM(I2$(3,2)),M1$=I6$,M2$=I7$,M3$=I8$,M4$=I9$,M5$=I10$
0448 LET M3$="##,###.0000-"; REM "WA:200
0450 CALL "SYC.VA","AR",INFO$[ALL]
0455 LET AR$=INFO$[20]
0460 IF AR$="Y" THEN CALL "SYC.VA","OP",INFO$[ALL]
0465 LET OP$=INFO$[20]
0470 CALL "SYC.VA","WO",INFO$[ALL]
0475 LET SF$=INFO$[20]
0500 REM " --- Initializations"
0505 PRECISION I[1]
0510 DIM A0$(15),A1$(160),A[8],A$[2],O[5],MESSAGE$[2],VENDOR$(8); REM "WA:200
0515 DIM A2$(20); REM "WA:200
0520 DIM RIBBON[20],RIBBON$[20],RFILES[10],Y0$(4),Y1$(32),X4$(38)
0530 DIM W0$(18),W1$(48),W2$(32),W3$(22),W4$(40),W[12]
0550 LET A0$(1)=N0$,L0=12,INCREMENT=P[3],ACTION$="B",TYPE$="P",PO=0
0560 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),M5=LEN(M5$),P=13+MAX(I[0],5)
0570 LET RIBBON[1]=15,RIBBON[2]=L0,RIBBON$[1]=N0$,RFILES[1]=APM05_DEV
0580 LET RFILES[2]=APM10_DEV,RFILES[3]=IVM10_DEV,RFILES[4]=POM04_DEV
0600 REM " --- Additional File Opens"
0610 IF AR$="N" THEN GOTO 0630
0615 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0620 LET CUSTLEN=NUM(P6$(1,2)),FILES$[33]="ARM-01",FILES$[34]="ARM-03"
0630 IF OP$="N" THEN GOTO 0640
0635 LET FILES$[35]="ARE-03",FILES$[36]="ARE-13",FILES$[37]="ARE-33"
0640 IF SF$="N" THEN GOTO 0650
0645 LET FILES$[38]="WOE-01",FILES$[39]="WOE-22",FILES$[40]="WOE-32"
0650 CALL "SYC.DA",1,33,40,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0655 IF STATUS THEN GOTO 9900
0660 LET ARM01_DEV=CHANNELS[33],ARM03_DEV=CHANNELS[34]
0665 LET ARE03_DEV=CHANNELS[35],ARE13_DEV=CHANNELS[36],ARE33_DEV=CHANNELS[37]
0670 LET WOE01_DEV=CHANNELS[38],WOE22_DEV=CHANNELS[39],WOE32_DEV=CHANNELS[40]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,1,0,L0-3,0
0720 PRINT 'SB',@(1,3),"Vendor:",@(19),"PO #:",@(34),"Req #:",@(50),"Purchase Address:",@(3,4),"Name:",@(0,5),"Address:",'SF',
0730 CALL "POC.RA",TYPE$,ACTION$,RIBBON$[ALL],RIBBON[ALL],RFILES[ALL],A$[ALL],A[ALL],V3
0800 REM " --- Resize masks and set print positions"
0810 IF FNP(M1,M2,M3,M5,P)>0 THEN GOTO 0900
0820 CALL "SYC.RM",M5$,M5,5,M5-1
0830 IF FNP(M1,M2,M3,M5,P)>0 THEN GOTO 0900
0840 CALL "SYC.RM",M1$,M1,10,M1-1
0850 IF FNP(M1,M2,M3,M5,P)>0 THEN GOTO 0900
0860 CALL "SYC.RM",M2$,M2,6,M2-1
0870 IF FNP(M1,M2,M3,M5,P)>0 THEN GOTO 0900
0880 CALL "SYC.RM",M3$,M3,6,M3-1
0890 GOTO 0800
0900 REM " --- Position files"
0910 LET O[5]=79-M1,O[4]=O[5]-M3-1,O[3]=O[4]-M2-1
0920 REM LET O[2]=O[3]-M2-1,O[1]=O[2]-M5-1,O[0]=O[1]-3
0921 LET O[2]=O[3]-M2-1,O[1]=O[2]-M5-1,O[0]=O[1]-4; REM "WA:200
0970 READ (POE52_DEV,KEY=N0$,DOM=0980)
0980 READ (POE32_DEV,KEY=N0$,DOM=0990)
0990 READ (POE02_DEV,KEY=A0$,DOM=1000)
1000 REM " --- Vendor number"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(POE02_DEV,END=1050)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3,6)
1050 LET PO=0,DISPLAY=0,UPDATED=0,UPDATING=0,NEXTPO=0
1055 LET V0$="M",V1$="KCE",V3$=P1$,V0=6,V1=9,V2=3
1060 LET V4$="Enter A Valid Vendor (<F1>=PO Number/<F2>=Requisition Number/<F3>=Lookup)"
1065 GOSUB 7000
1070 ON V3 GOTO 1100,2000,2500,1080,9900
1080 rem ' CALL "SYC.LV",V$
1083 rem ' replace with a barista sprinkle
1086 gosub runBaristaQuery
1090 IF V$="" THEN GOTO 0900
1100 REM " --- Retrieve and display vendor info"
1110 GOSUB 5200
1120 IF STATUS THEN GOTO 1000
1130 IF POS(VENDOR$=A0$)<>1 THEN LET A0$(1)=VENDOR$
1190 READ (POE02_DEV,KEY=A0$,DOM=1200)
1200 REM " --- Purchase Order Number"
1210 LET V2$="",V2$=KEY(POE02_DEV,END=1240)
1220 IF POS(VENDOR$=V2$)<>1 THEN LET V2$=""
1230 IF V2$<>"" THEN LET V2$=V2$(9)
1240 LET V4$="Enter A Valid PO Number (<F1>=Next Available/<F2>=Vendor Number/<F3>=Lookup)"
1250 LET V0$="Z",V1$="CKER",V3$="",V0=7,V1=25,V2=3,PO=1
1260 GOSUB 7000
1270 ON V3 GOTO 1400,1280,1000,1300,9900
1280 CALL "POC.NA","A",TYPE$,POS10_DEV,N0$,V$
1282 LET NEXTPO=1
1285 PRINT @(25,3),V$,
1290 GOTO 1400
1300 REM " --- <F3> Lookup"
1310 CALL "POC.LA",TYPE$,A0$(3,6),V$
1320 IF V$="" THEN GOTO 1200
1400 REM " --- Read/display purchase order"
1405 IF NUM(V$,ERR=1200)=0 THEN GOTO 1200
1410 GOSUB 5400
1420 IF UPDATING=0 THEN IF STATUS THEN GOTO 1500
1430 LET X$="Purchase Order"
1440 GOSUB 6000
1450 ON V3 GOTO 1460,1470,1400,1400,1200
1460 ON POS(V$="YN") GOTO 1400,3900,1200
1470 LET K$=KEY(POE02_DEV,END=1200)
1475 IF POS(VENDOR$=K$)<>1 THEN GOTO 1200
1480 LET V$=K$(9)
1490 GOTO 1400
1500 REM " --- New record?"
1510 CALL "SYC.YN",1,"Is This A New Record",2,V$,V3
1520 ON V3 GOTO 1530,1500,1500,1500,4700
1530 ON POS(V$="YN") GOTO 1500,1600,4700
1600 REM " --- Initialize new record"
1610 DIM A1$(160),A[8],B0$(15),B1$(160),B[8],C0$(10),C1$(24)
1620 LET A1$(1)=I3$(6,2),A1$(5)=F0$(18,6),A1$(23)=F0$(18,6)
1630 LET A1$(44)=P3$(18,15),A1$(59)=P3$(3,15),A1$(94)=P3$(33,15)
1635 LET A1$(109,3)=P3$(52,3); REM "WA:200
1640 LET EXISTS=0,DISPLAY=0,PRINTIT=1,UPDATING=0
1650 REM " --- Get default terms code from first vendor detail record"
1655 READ (APM02_DEV,KEY=VENDOR$,DOM=1660)
1660 LET K$=KEY(APM02_DEV,END=1700)
1670 IF POS(VENDOR$=K$)<>1 THEN GOTO 1700
1680 READ (APM02_DEV)IOL=APM02A
1690 LET A1$(38,2)=C1$(5,2)
1700 REM " --- Source from requisition?"
1710 LET V0$="Z",V1$="C",V2$=A1$(112,7),V3$="",V0=7,V1=41,V2=3,UPDATING=0
1720 LET V4$="Enter A Requisition As The Source For This PO (<F1>=None/<F3>=Lookup)"
1730 GOSUB 7000
1735 ON V3 GOTO 1750,1900,4700,1740,1900; REM "WA:200
1740 CALL "POC.LA","R",VENDOR$(3),V$
1745 IF V$="" THEN GOTO 1700
1750 IF FNP$(V$)="" THEN GOTO 1900
1755 LET B0$(1)=A0$,B0$(9)=V$
1760 FIND (POE01_DEV,KEY=B0$,DOM=1700)IOL=POE01A
1770 LET X$=A1$,A1$(1)=B1$,A1$(112,7)=B0$(9)
1775 GOSUB 5000
1780 LET A1$(1)=X$
1800 REM " --- Create purchase order from specified requisition?"
1810 CALL "SYC.YN",1,"Is This The Correct Requisition",2,V$,V3
1820 ON V3 GOTO 1830,1800,1700,1800,1700
1830 ON POS(V$="YN") GOTO 1800,1840,1700
1840 LET EXISTS=1,DISPLAY=1
1850 GOSUB 6500
1890 GOTO 3900
1900 REM " --- No requisition"
1910 LET A1$(112,7)=""
1920 GOSUB 5000
1990 GOTO 3900
2000 REM " --- Retrieve record using only purchase order number"
2005 PRINT 'CF',
2010 LET V2$="",V2$=KEY(POE32_DEV,END=2040)
2020 IF POS(N0$=V2$)<>1 THEN LET V2$=""
2030 IF V2$<>"" THEN LET V2$=V2$(3,7)
2040 LET V4$="Enter A Valid Purchase Order Number (<F1>=Requisition/<F2>=Vendor/<F3>=Lookup)"
2050 LET V0$="Z",V1$="CKER",V3$="",V0=7,V1=25,V2=3,UPDATING=0
2060 GOSUB 7000
2070 ON V3 GOTO 2100,2500,1000,2080,9900
2080 CALL "POC.LB",TYPE$,V$
2085 IF V$="" THEN GOTO 2000
2090 LET K$=V$(1,2)+V$(9)+V$(3,6)
2095 GOTO 2300
2100 REM " --- Position purchase order xref"
2110 LET X$=N0$+V$
2190 READ (POE32_DEV,KEY=X$,DOM=2200)
2200 REM " --- Read next xref"
2210 LET K$=KEY(POE32_DEV,END=2000)
2220 IF POS(X$=K$)<>1 THEN GOTO 2000
2230 READ (POE32_DEV)
2300 REM " --- Read/display purchase order"
2310 LET V$=K$(10,6)
2320 GOSUB 5200
2330 IF STATUS THEN GOTO 2000
2340 LET V$=K$(3)
2350 GOSUB 5400
2360 IF UPDATING=0 THEN IF STATUS THEN GOTO 2000
2400 REM " --- Right purchase order?"
2410 LET X$="Purchase Order"
2420 GOSUB 6000
2430 ON V3 GOTO 2440,2450,2400,2400,2000
2440 ON POS(V$="YN") GOTO 2400,3900,2000
2450 LET X$=N0$
2490 GOTO 2200
2500 REM " --- Retrieve record using only requisition number"
2505 PRINT 'CF',
2510 LET V2$="",V2$=KEY(POE52_DEV,END=2540)
2520 IF POS(N0$=V2$)<>1 THEN LET V2$=""
2530 IF V2$<>"" THEN LET V2$=V2$(3,7)
2540 LET V4$="Enter A Valid Requisition Number (<F1>=PO Lookup/<F2>=PO Number/<F3>=Req Lookup)"
2550 LET V0$="Z",V1$="CKER",V3$="",V0=7,V1=41,V2=3,PO=0,UPDATING=0
2560 GOSUB 7000
2570 ON V3 GOTO 3300,2600,2000,3200,9900
2600 REM " --- PO Lookup"
2610 CALL "POC.LB","X",V$
2620 IF V$="" THEN GOTO 2500
2625 LET V$=V$(3)
2630 GOSUB 5200
2640 IF STATUS THEN GOTO 2500
2645 LET V$=V$(7)
2650 GOSUB 5400
2660 IF UPDATING=0 THEN IF STATUS THEN GOTO 2500
2700 REM " --- Right purchase order?"
2710 LET X$=""
2720 GOSUB 6000
2730 ON V3 GOTO 2740,2700,2700,3700,2500
2740 ON POS(V$="YN") GOTO 2750,3900,2500
2800 REM " --- Position requisition xref"
2810 LET X$=N0$+V$
2890 READ (POE52_DEV,KEY=X$,DOM=2900)
2900 REM " --- Get next requisition/purchase order xref"
2910 LET K$=KEY(POE52_DEV,END=3300)
2920 IF POS(X$=K$)<>1 THEN GOTO 3300
2930 READ (POE52_DEV)
3000 REM " --- Read/display purchase order info"
3010 LET V$=K$(10)
3020 GOSUB 5200
3030 IF STATUS THEN GOTO 2500
3040 LET V$=K$(3)
3050 GOSUB 5400
3060 IF UPDATING=0 THEN IF STATUS THEN GOTO 2500
3100 REM " --- Right requisition?"
3110 LET X$="Purchase Order"
3120 GOSUB 6000
3130 ON V3 GOTO 3140,3150,3100,3100,2500
3140 ON POS(V$="YN") GOTO 3600,3900,2500
3150 LET K$=KEY(POE52_DEV,END=2500)
3160 IF POS(N0$=K$)<>1 THEN GOTO 2500
3170 READ (POE52_DEV)
3190 GOTO 3000
3200 REM " --- Requisition lookup"
3210 CALL "POC.LB","R",V$
3220 IF V$="" THEN GOTO 2500
3230 LET V$=V$(9,7)+V$(3,6)
3300 REM " --- Read requisition"
3310 READ (POE31_DEV,KEY=N0$+V$,DOM=3350)
3320 LET K$=N0$+V$
3330 GOTO 3370
3350 LET K$=KEY(POE31_DEV,END=2500)
3360 IF POS(N0$+V$=K$)<>1 THEN GOTO 2500
3370 LET A0$(1)=N0$+K$(10)+K$(3)
3380 FIND (POE01_DEV,KEY=A0$,DOM=2500)IOL=POE02A
3400 REM " --- Display vendor/requisition"
3410 LET A1$(112,7)=V$,A0$(9)="",V$=A0$(3,6)
3420 GOSUB 5200
3430 IF STATUS THEN GOTO 2500
3440 GOSUB 5000
3500 REM " --- Create purchase order from specified requisition?"
3510 CALL "SYC.YN",1,"Is This The Correct Requisition",2,V$,V3
3520 ON V3 GOTO 3530,3500,2500,3500,2500
3530 ON POS(V$="YN") GOTO 3500,3540,2500
3600 REM " --- Purchase order number"
3610 LET V4$="Enter A Valid Purchase Order Number (<F1>=Next Available)"
3620 LET V0$="Z",V1$="C",V2$=A0$(9),V3$="",V0=7,V1=25,V2=3
3630 GOSUB 7000
3640 ON V3 GOTO 3700,3650,1000,3600,2500
3650 CALL "POC.NA","A",TYPE$,POS10_DEV,N0$,V$
3660 LET NEXTPO=1
3700 REM " --- PO number being used?"
3710 LET A0$(9)=V$
3720 PRINT @(V1,V2),A0$(9,7),
3730 FIND (POE02_DEV,KEY=A0$,DOM=3800)
3740 LET MESSAGE$[0]="This Purchase Order Number Already Exists (<Enter>=Continue)"
3750 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
3790 GOTO 3600
3800 REM " --- Create purchase order"
3810 DIM B0$(15),B1$(160),B[8]
3820 LET UPDATING=0,EXISTS=1,DISPLAY=1,B0$(1)=A0$,B0$(9)=A1$(112,7)
3830 FIND (POE01_DEV,KEY=B0$,DOM=2500)IOL=POE01A
3840 GOSUB 6500
3900 REM " --- Process ribbon"
3910 IF UPDATING THEN GOTO 4000
3920 LET ACTION$="I",A$[0]=A0$,A$[1]=A1$,RIBBON[3]=DISPLAY
3925 LET A$[2]=A2$; REM "WA:200
3930 CALL "POC.RA",TYPE$,ACTION$,RIBBON$[ALL],RIBBON[ALL],RFILES[ALL],A$[ALL],A[ALL],V3
3940 IF EXISTS=0 THEN IF V3=2 THEN GOTO 1700
3950 IF EXISTS=1 THEN IF V3=2 THEN GOTO 1200
3960 LET A0$(1)=A$[0],A1$(1)=A$[1]
3965 LET A2$(1)=A$[2]; REM "WA:200
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Reprint/Delete)?",PO=1
4015 IF EXISTS=0 THEN LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=7,V2=22,V1=FNV(V4$),DISPLAY=0
4025 IF UPDATING THEN LET V4$="Press <Enter> To Continue",V0=1,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4000
4050 IF UPDATING THEN GOTO 4100
4060 IF V$="DELETE" THEN GOTO 4200
4070 IF V$="R" OR V$="REPRINT" THEN GOTO 4900
4090 ON POS(V$="YN") GOTO 4000,4100,3900
4100 REM " --- Write record"
4110 DIM X3$(15)
4120 WRITE (POE02_DEV,KEY=A0$)IOL=POE02A
4130 LET X3$(1)=A0$(1,2)+A0$(9)+A0$(3,6)
4140 WRITE (POE32_DEV,KEY=X3$)IOL=POE32A
4180 LET O0=0,LINE=L0-1,UPDATED=1
4190 RUN "POE.BB"
4200 REM " --- Retain requisition?"
4210 IF UPDATED=0 THEN GOTO 4700
4215 LET RETAIN_REQ$="N"
4220 IF FNP$(A1$(112,7))="" THEN GOTO 4500
4230 CALL "SYC.YN",0,"Do You Wish To Retain This Requisition",4,V$,V3
4240 ON V3 GOTO 4250,4200,4000,4200,4000
4250 ON POS(V$="YN") GOTO 4200,4300,4500
4300 REM " --- Process header"
4310 DIM X3$(15),B0$(15),B1$(160),B[8]
4320 LET B0$(1)=A0$,B0$(9)=A1$(112,7),B1$(1)=A1$,B1$(112,7)="",B[ALL]=A[ALL]
4330 LET X3$(1)=N0$+B0$(9)+B0$(3),RETAIN_REQ$="Y"
4350 WRITE (POE01_DEV,KEY=B0$)IOL=POE01A
4360 WRITE (POE31_DEV,KEY=X3$)IOL=POE31A
4390 READ (POE12_DEV,KEY=A0$,DOM=4400)
4400 REM " --- Process line items"
4410 LET K$=KEY(POE12_DEV,END=4500)
4415 IF POS(A0$=K$)<>1 THEN GOTO 4500
4420 READ (POE12_DEV)IOL=POE12A
4430 LET W0$(1,15)=B0$,W[3]=0,W[4]=0,W[5]=0,W[6]=0,W[7]=0,W[8]=0
4440 WRITE (POE11_DEV,KEY=W0$)IOL=POE11A
4450 LET Y0$(1)=N0$+W1$(1,2)
4460 FIND (POM02_DEV,KEY=Y0$,DOM=4400)IOL=POM02A
4470 IF Y1$(21,1)<>"S" THEN GOTO 4400
4480 LET X4$(1)=N0$+W3$(3)+W0$(9)+W0$(3,6)
4485 WRITE (POE41_DEV,KEY=X4$)IOL=POE41A
4490 GOTO 4400
4500 REM " --- Delete purchase order"
4510 DIM FILES[20],PARAMS[10],PARAMS$[10]
4520 LET FILES[1]=POS10_DEV,FILES[2]=POM02_DEV,FILES[3]=POE02_DEV
4530 LET FILES[4]=POE12_DEV,FILES[5]=POE22_DEV,FILES[6]=POE32_DEV
4540 LET FILES[7]=POE42_DEV,FILES[8]=POE52_DEV,FILES[9]=IVM01_DEV
4550 LET FILES[10]=IVM02_DEV,FILES[11]=WOE22_DEV,FILES[12]=WOE32_DEV
4560 LET PARAMS$[0]=N0$,PARAMS$[1]=SF$,PARAMS$[2]=I2$
4570 LET PARAMS$[3]=I3$,PARAMS$[4]=I4$,PARAMS[0]=I[1]
4590 CALL "POC.EA",TYPE$,FILES[ALL],PARAMS[ALL],PARAMS$[ALL],A0$,A1$,STATUS
4600 REM " --- Reset Work Order's PO Status For Retained Requisitions
4610 IF SF$="N" OR RETAIN_REQ$="N" THEN GOTO 4700
4620 READ (POE11_DEV,KEY=B0$,DOM=4630)
4630 READ (POE11_DEV,END=4700)IOL=POE11A
4640 IF W0$(1,15)<>B0$ THEN GOTO 4700
4650 IF FNP$(W2$(1,7))="" THEN GOTO 4690
4660 FIND (POM02_DEV,KEY=N0$+W1$(1,2),DOM=4690)IOL=POM02A
4670 IF POS(Y1$(21,1)="NS")=0 THEN GOTO 4690
4680 CALL "POC.WA",WOE22_DEV,WOE32_DEV,N0$,W0$(9),"R",Y1$,W2$(1,10),W2$(1,10),I[1],STATUS
4690 GOTO 4630
4700 REM " --- Reset number"
4710 IF NEXTPO THEN CALL "POC.NA","D",TYPE$,POS10_DEV,N0$,A0$(9)
4800 REM " --- Clear screen"
4810 IF PO=0 THEN GOTO 1000
4820 LET V$=A0$(3,6)
4830 PRINT 'CF',
4840 GOSUB 5200
4890 GOTO 1200
4900 REM " --- Reprint"
4910 LET X2$=A0$
4920 FIND (POE02_DEV,KEY=A0$,DOM=4990)
4930 WRITE (POE22_DEV,KEY=X2$)IOL=POE22A
4940 LET MESSAGE$[0]="This Purchase Order Has Been Queued For Printing (<Enter>=Continue)"
4950 CALL "SYC.XA",3,MESSAGE$[ALL],0,22,-1,V$,V3
4990 GOTO 4000
5000 REM " --- Display record"
5010 PRINT @(25,3),A0$(9),@(41,3),A1$(112,7),
5020 LET ACTION$="D",A$[0]=A0$,A$[1]=A1$
5025 LET A$[2]=A2$; REM "WA:200
5030 CALL "POC.RA",TYPE$,ACTION$,RIBBON$[ALL],RIBBON[ALL],RFILES[ALL],A$[ALL],A[ALL],V3
5090 RETURN 
5200 REM " --- Display vendor and comments"
5210 LET VENDOR$(1)=N0$+V$
5220 CALL "POC.VA","VC",VENDOR$,VENDLEN,M0$,APM01_DEV,APM09_DEV,P3$,L0,STATUS
5290 RETURN 
5400 REM " --- Read/display purchase order"
5410 LET A0$(1)=VENDOR$+V$,STATUS=11,UPDATING=0
5420 READ (POE02_DEV,KEY=A0$,DOM=5590)IOL=POE02A
5430 LET STATUS=0,UPDATED=1
5440 GOSUB 5000
5500 REM " --- Being received or invoiced?"
5510 LET CHANNEL=POE33_DEV,X=1
5515 GOSUB 5700
5520 IF UPDATING THEN GOTO 5600
5530 LET CHANNEL=POE34_DEV,X=2
5535 GOSUB 5700
5540 IF UPDATING THEN GOTO 5600
5550 LET CHANNEL=POE35_DEV,X=3
5555 GOSUB 5700
5560 IF UPDATING THEN GOTO 5600
5590 GOTO 5690
5600 REM " --- Purchase order being received or invoiced"
5610 IF X=1 THEN LET X$="A Quality Assurance Receipt Entry Exists"
5620 IF X=2 THEN LET X$="A Purchase Order Receipt Entry Exists"
5630 IF X=3 THEN LET X$="An Invoice Entry Exists"
5640 LET MESSAGE$[0]=X$+" For This Purchase Order"
5650 LET MESSAGE$[1]="It May Not Be Changed (<Enter>=Continue)"
5660 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
5670 IF V$="SPECIAL" THEN LET UPDATING=0
5690 RETURN 
5700 REM " --- Check for purchase order in other files"
5710 LET UPDATING=0,X$=N0$+A0$(9,7)+A0$(3,6)
5720 READ (CHANNEL,KEY=X$,DOM=5730)
5730 LET K$=KEY(CHANNEL,END=5790)
5740 IF POS(X$=K$)=1 THEN LET UPDATING=11
5790 RETURN 
6000 REM " --- Right PO/Requisition?"
6010 LET V0$="Y",V1$="C",V4$="Is This The Correct Purchase Order (Y/N"
6020 IF X$<>"" THEN LET V4$=V4$+"/<F1>=Next "+X$
6030 LET V4$=V4$+")?",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
6040 GOSUB 7000
6050 IF V$="Y" THEN LET EXISTS=1,PRINTIT=0,DISPLAY=1
6090 RETURN 
6500 REM " --- Create purchase order from requisition"
6510 DIM FILES[10],PARAMS$[10],X2$(15)
6520 LET FILES[0]=POE11_DEV,FILES[1]=POE02_DEV,FILES[2]=POE12_DEV
6530 LET FILES[3]=POE32_DEV,FILES[4]=POE52_DEV,FILES[5]=POM02_DEV
6540 LET FILES[6]=IVM01_DEV,FILES[7]=IVM02_DEV,FILES[8]=WOE22_DEV
6550 LET FILES[9]=WOE32_DEV,FILES[10]=POE42_DEV,PARAMS$[0]=F0$,PARAMS$[2]=I2$
6570 LET PARAMS$[3]=I3$,PARAMS$[4]=I4$,X2$(1)=A0$
6580 CALL "POC.MA",TYPE$,FILES[ALL],PARAMS$[ALL],A0$,A1$,A[ALL],B0$,B1$,B[ALL],I[1],STATUS
6590 WRITE (POE22_DEV,KEY=X2$)IOL=POE22A
6600 REM " --- Delete requisition source"
6610 DIM FILES[20],PARAMS[10],PARAMS$[10]
6620 LET FILES[1]=POS10_DEV,FILES[2]=POM02_DEV,FILES[3]=POE01_DEV
6630 LET FILES[4]=POE11_DEV,FILES[5]=POE21_DEV,FILES[6]=POE31_DEV
6640 LET FILES[7]=POE41_DEV,PARAMS$[0]=N0$,PARAMS$[1]="N",PARAMS[0]=I[1]
6650 LET UPDATED=1
6660 CALL "POC.EA","R",FILES[ALL],PARAMS[ALL],PARAMS$[ALL],B0$,B1$,STATUS
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8070 REM DEF FNP(Q1,Q2,Q3,Q4,Q5)=(75-(Q1+Q2+Q2+Q3+Q4))-Q5
8071 DEF FNP(Q1,Q2,Q3,Q4,Q5)=(76-(Q1+Q2+Q2+Q3+Q4))-Q5
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

10000 runBaristaQuery:
10010 rem ' run barista query
10020 barista$ = stbl("BARISTA")
10030 bbjhome$ = System.getProperty("basis.BBjHome") + "/"
10040 pid$ = hta(info(3,0)); ns_name$="query_result" + pid$
10050 xuserName$ = stbl("SYSTEMUID")
10060 xpassword$ = stbl("SYSTEMPWD")
10070 cmdline$ = bbjhome$ + "bbj -tT0 -q -WD""" + barista$ + """ -c""" + barista$ + "sys/config/enu/barista.cfg"" sys/prog/bax_launch_task.bbj " + "- -yQ -u" + xuserName$ + " -p" + xpassword$ + " -qFUT_APM01 -n" + ns_name$ + " -w"
10080 a = scall(cmdline$)
10090 selectedKey$ = cast(BBjString, BBjAPI().getGroupNamespace().getValue(ns_name$, err=*next))
10100 if pos("^"=selectedKey$,  -1) = len(selectedKey$) then selectedKey$ = selectedKey$(1,len(selectedKey$) -1)
10110 if cvs(selectedKey$,3) <> "" then v$ = selectedKey$(3) else v$ = selectedKey$
10120 return
0010 REM "SHE - RPS Zone Determination"
0020 REM "Program SHCDZA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "05Feb97 JAL: Error 47/2150 when Alaska zip/RPS DEF air (2145)"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER A0$,A1$,W0$,W1$,W[ALL],ZIPLENGTH,ZIPCODE$,ZONE$,SERVICE$,RESERVED$,ZONEFILE$,SPEEDSHIP$
0100 REM " --- Open/Lock Files"
0110 LET FILES=1
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]=ZONEFILE$
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET SHM43_DEV=CHANNELS[1]
0200 REM " --- IOLIST's"
0210 SHM43A: IOLIST Z0$,Z[ALL]
0220 SHM43A2: IOLIST Z0$,Z0
0500 REM " --- Initializations"
0510 DIM Z[3]
0550 LET FIRM$=A0$(1,2),CARRIER$=W1$(11,1),COUNTRY$=W1$(61,1)
0560 LET P=1,L=3,M=L,ZONE=0,WAREHOUSE$=SPEEDSHIP$(70,2)
0570 IF POS(COUNTRY$="AM")>0 THEN LET P=3,M=5,L=5
0575 IF COUNTRY$<>"C" THEN IF ZIPCODE$(1,3)>"994" THEN LET P=3,M=5,L=5
0580 IF SERVICE$(79,1)="A" THEN LET ZONE=1
0590 IF SERVICE$(79,1)="D" THEN LET ZONE=2
1000 REM " --- Verify Zip Code"
1010 IF COUNTRY$="I" THEN GOTO 2500
1020 IF FNP$(ZIPCODE$)="" THEN GOTO 1100
1030 IF POS(COUNTRY$="CM")>0 THEN GOTO 2000
1090 GOTO 2000
1100 REM " --- Request user entry of zip code if none is found"
1110 LET V0$="S",Z0=5
1115 IF COUNTRY$="C" THEN LET Z0=6
1120 CALL "SHC.ZA",V0$,Z0,ZIPCODE$,V3
1130 IF V3=4 THEN GOTO 9900
2000 REM " --- Position zone chart file"
2010 LET N=6+M
2090 READ (SHM43_DEV,KEY=FIRM$+WAREHOUSE$+CARRIER$+ZIPCODE$(1,1),DOM=2100)
2100 REM " --- Lookup zip code in zone chart"
2110 LET K$=KEY(SHM43_DEV,END=3000)
2115 IF POS(FIRM$=K$)<>1 THEN GOTO 3000
2120 READ (SHM43_DEV)IOL=SHM43A
2130 IF K$(3,2)<>WAREHOUSE$ THEN GOTO 3000
2140 IF K$(6,L)>ZIPCODE$(1,L) THEN GOTO 2100
2145 IF (N+L)-1>LEN(K$) THEN GOTO 2100
2150 IF K$(N,L)<ZIPCODE$(1,L) THEN GOTO 2100
2160 IF Z[ZONE]=0 THEN GOTO 3000
2170 LET ZONE$=STR(Z[ZONE]:"00")
2190 GOTO 9900
2500 REM " --- Destination Lookup"
2510 LET V5$=WAREHOUSE$+CARRIER$
2520 CALL "SHC.LI",SHM43_DEV,"I",V5$,V$
2550 IF LEN(V$)<>0 THEN GOTO 2600
2560 LET ZONE$="EXIT"
2570 GOTO 9900
2600 REM " --- Retrieve Zone using lookup selection"
2610 IF LEN(V$)<40 THEN LET V$=V$+FILL(40),V$=V$(1,40)
2620 FIND (SHM43_DEV,KEY=FIRM$+V5$+V$,DOM=3000)IOL=SHM43A2
2630 LET ZONE$=STR(Z0:"00")
2690 GOTO 9900
3000 REM " --- Service not available"
3010 CALL "SHC.MS",ZONE$
3090 GOTO 9900
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9910 CLOSE (SHM43_DEV,ERR=9950)
9950 EXIT 
9999 END

0010 REM "IVM - Lot/Serial Transaction History By Item Inquiry"
0020 REM "Program IVM.FA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=10
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0130 LET FILES$[4]="IVM-03",FILES$[5]="IVM-10",FILES$[6]="IVM-14"
0140 LET FILES$[7]="IVT-01"
0150 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0180 LET IVM03_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],IVM14_DEV=CHANNELS[6]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1)
0230 IVM10C: IOLIST X0$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0275 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$
0280 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$
0285 GLS01A: IOLIST G0$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0405 DIM P[5],G[1],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="IV",WIDTH=80
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0425 IF POS(I3$(17,1)="LS")=0 THEN GOTO 4900
0430 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1)),P[2]=NUM(I2$(7,2))
0435 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0440 CALL "SYC.VA","AP",INFO$[ALL]
0442 LET AP$=INFO$[20]
0444 CALL "SYC.VA","AR",INFO$[ALL]
0446 LET AR$=INFO$[20]
0448 CALL "SYC.VA","GL",INFO$[ALL]
0450 LET GL$=INFO$[20]
0455 IF GL$="N" THEN GOTO 0470
0460 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0465 LET G[1]=NUM(G2$(7,2)),FILES$[8]="GLM-01"
0470 IF AR$="N" THEN GOTO 0485
0475 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0480 LET CUSTLEN=NUM(P2$(1,2)),CUSTMASK$=M0$,FILES$[9]="ARM-01"
0485 IF AP$="N" THEN GOTO 0500
0490 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0495 LET VENDLEN=NUM(P2$(1,2)),VENDMASK$=M0$,FILES$[10]="APM-01"
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM FILES[5],PARAMS[13],PARAMS$[11],X0$(32),I[3],A0$(22),A1$(60),D0$(44)
0515 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),IFILES[14],IPARAMS[3],D1$(96),D[12]
0520 DIM SAVED0$(44)
0535 LET WIN_X=0,WIN_Y=5,HEIGHT=24-WIN_Y,TITLE$="",NAME$=""
0540 LET H0=WIDTH-1,I[3]=H0-8,I[2]=I[3]-13,I[1]=I[2]-P[2]-7,X=P[2]+20
0545 LET X0=MAX(MIN(24,I[1]-X),0),IFILES[1]=IVM01_DEV,IFILES[14]=IVM14_DEV
0550 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),FILES[0]=IVM01_DEV
0560 LET FILES[1]=IVM10_DEV,PARAMS$[0]=N0$,PARAMS$[9]=M1$
0565 LET PARAMS$[10]=M2$,PARAMS$[11]=M3$,PARAMS[0]=P[0],PARAMS[4]=P[3]
0570 LET PARAMS[5]=P[4],PARAMS[6]=P[5],PARAMS[7]=5,PARAMS[8]=HEIGHT-5
0575 LET PARAMS[9]=WIDTH,PARAMS[10]=HEIGHT,PARAMS[11]=52,PARAMS[12]=P[1]
0590 LET IPARAMS[0]=P[0],IPARAMS[1]=P[3],IPARAMS[2]=P[4],IPARAMS[3]=P[5]
0600 REM " --- Additional File Opens"
0610 CALL "SYC.DA",1,7,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS>0 THEN GOTO 9900
0630 IF AR$="Y" THEN LET PARAMS[1]=CUSTLEN,PARAMS$[1]=CUSTMASK$,FILES[2]=CHANNELS[9]
0635 IF AP$="Y" THEN LET PARAMS[2]=VENDLEN,PARAMS$[2]=VENDMASK$,FILES[3]=CHANNELS[10]
0640 IF GL$="Y" THEN LET PARAMS[3]=G[1],PARAMS$[3]=G5$,FILES[4]=CHANNELS[8]
0650 LET HEADING$(2)="  Date   Type Reference",HEADING$(H0-M1-M3-9)="Quantity"
0655 LET HEADING$(H0-M1-5)="Cost",HEADING$(H0-10)="Extension"
0660 LET FOOTING$(2)="<F1>=Next Lot/Serial Number",IVT01_DEV=CHANNELS[7]
0665 LET FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End",FILES[5]=IVT01_DEV
0700 REM " --- Background"
0710 PRINT 'SB',@(5,3),"Item:",@(0,4),"Warehouse:",@(I[1]-14),"Lot/Serial #:",@(I[2]-6),"From:",@(I[3]-4),"To:",'SF',
0720 CALL "SYC.WC",1,0,80,0,0,WIN_Y,0
0900 REM " --- Position file"
0990 READ (IVT01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Item Number"
1010 PRINT 'CF',
1020 DIM ITEM$[6],ITEM[12],X$(78-(15+P[0]))
1030 LET ACTION$="",V1=11,V2=3,I0=0
1040 CALL "IVC.IA",ACTION$,V1,V2,IFILES[ALL],N0$,A0$(3),V4$,IPARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1050 ON V3 GOTO 1060,1000,3900,1000,9900
1060 LET A0$(1)=N0$+ITEM$[0]
1070 FIND (IVM01_DEV,KEY=A0$,DOM=1000)IOL=IVM01A
1080 LET X$(1)=FNITEM$(A1$,P[3],P[4],P[5])
1090 PRINT @(V1,V2),A0$(3,P[0])," ",X$,
1100 REM " --- Position item/warehouse xref"
1110 READ (IVM03_DEV,KEY=A0$(1,2)+"G"+A0$(3),DOM=1120)
1190 GOTO 3900
1200 REM " --- Warehouse ID"
1210 LET V2$="",V2$=KEY(IVM03_DEV,END=1240)
1220 IF POS(A0$(1,2)+"G"+A0$(3)=V2$)<>1 THEN LET V2$=""
1230 IF V2$<>"" THEN LET V2$=V2$(24)
1240 LET V0$="S",V1$="CKE",V3$="",V0=2,V1=11,V2=4
1245 LET V4$="Enter A Valid Warehouse (<F3>=Lookup)"
1250 GOSUB 7000
1260 ON V3 GOTO 1280,1200,3900,1270,9900
1270 CALL "IVC.LK",3,V1,V2,V$
1280 IF V$="" THEN GOTO 1200
1290 LET V$=V$+"  ",V$=V$(1,2)
1300 REM " --- Retrieve warehouse record"
1310 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=1200)IOL=IVM10C
1320 LET K$=A0$(1,2)+"G"+A0$(3)+V$,D0$(1)=A0$(1,2)+V$+A0$(3)
1330 READ (IVM03_DEV,KEY=K$,DOM=1340)
1340 READ (IVT01_DEV,KEY=D0$,DOM=1500)
1500 REM " --- Display record"
1510 LET V1$="First   ",V2$="Last    "
1520 IF BEGDATE$<>"" THEN LET V1$=FNB$(BEGDATE$)
1530 IF ENDDATE$<>"" THEN LET V2$=FNB$(ENDDATE$)
1540 PRINT @(11,4),X0$(4,2)," ",X0$(6,X0),@(I[1]),D0$(25,P[2]),@(I[2]),V1$,@(I[3]),V2$,
1590 GOTO 3900
1900 REM " --- Position file to next lot/serial number"
1990 READ (IVT01_DEV,KEY=D0$+$FF$,DOM=2000)
2000 REM " --- Lot/Serial Number"
2010 LET V2$="",V2$=KEY(IVT01_DEV,END=2040)
2020 IF POS(D0$(1,24)=V2$)<>1 THEN LET V2$=""
2030 IF V2$<>"" THEN LET V2$=V2$(25)
2040 LET V0$="S",V1$="CKE",V3$="",V0=P[2],V1=I[1],V2=4,I0=2
2045 LET V4$="Enter A Valid Lot/Serial Number (<F3>=Lookup)"
2050 GOSUB 7000
2060 ON V3 GOTO 2100,2000,3900,2070,9900
2070 LET ACTION$=""
2075 LET SAVED0$(1)=D0$,SAVED0$(25)=V$
2080 CALL "SYC.LL",ACTION$,D0$,D1$,D[ALL]
2085 IF D0$=FILL(44) THEN LET D0$(1)=SAVED0$
2090 LET V$=D0$(25)
2095 PRINT @(V1,V2),V$,
2100 REM " --- Retrieve lot/serial master"
2110 LET X$=D0$(1,24)+V$
2120 READ (IVT01_DEV,KEY=X$,DOM=2130)
2130 LET K$=KEY(IVT01_DEV,END=2000)
2140 IF POS(X$=K$)<>1 THEN GOTO 2000
2150 LET D0$(1)=K$(1,44)
2190 GOTO 3900
2200 REM " --- Beginning Date"
2210 LET V0$="D",V1$="CE",V2$=BEGDATE$,V3$="",V0=3,V1=I[2],V2=4
2215 LET V4$="Enter The Date Of The First Transaction To Display (<F1>=First)"
2220 GOSUB 7000
2230 ON V3 GOTO 2250,2240,3900,2200,3900
2240 LET V$=""
2250 IF V$="" THEN PRINT @(V1,V2),"First   ",
2260 LET BEGDATE$=V$
2290 GOTO 3900
2300 REM " --- Ending Date"
2310 LET V0$="D",V1$="CE",V2$=ENDDATE$,V3$="",V0=3,V1=I[3],V2=4
2315 LET V4$="Enter The Date Of The Last Transaction To Display (<F1>=Last)"
2320 GOSUB 7000
2330 ON V3 GOTO 2350,2340,3900,2300,3900
2340 LET V$=""
2350 IF V$="" THEN PRINT @(V1,V2),"Last    ",
2360 IF V$<>"" THEN IF V$<BEGDATE$ THEN GOTO 2300
2370 LET ENDDATE$=V$
2390 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,2000,2200,2300,3990
3990 LET I0=0
4000 REM " --- Call display routine"
4010 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
4020 PRINT @(0,0),'SB','BR',HEADING$,'ER','SF',
4025 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
4030 LET ACTION$="",PARAMS$[4]=D0$,PARAMS$[5]=BEGDATE$
4040 LET PARAMS$[6]=ENDDATE$,PARAMS$[7]=NAME$
4050 CALL "IVC.TA",ACTION$,FILES[ALL],PARAMS[ALL],PARAMS$[ALL],SELECTION$,FKEY
4060 CALL "SYC.WD",NAME$
4070 IF FKEY=1 THEN GOTO 1900
4080 IF SELECTION$<>"" THEN GOTO 1900
4090 GOTO 1000
4900 REM " --- Lotted/Serialized Inventory not enabled"
4910 CALL "IVC.SB","L",STATUS
4990 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9910 CALL "SYC.WD",NAME$
9950 RUN "SYS.AA"
9999 END

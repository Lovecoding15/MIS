0010 REM "SHX - Sample Manifest Selection Program"
0020 REM "Program SHX.MA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "20Oct97 KJS: Use F0$(18,6) instead of DAY function (560,8120-8140)"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SHE-01",FILES$[3]="SHE-02"
0140 LET FILES$[4]="SHE-04",FILES$[5]="SHM-10"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1],SHE01_DEV=CHANNELS[2],SHE02_DEV=CHANNELS[3]
0190 LET SHE04_DEV=CHANNELS[4],SHM10_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0290 SHS01A: IOLIST I0$,I1$
0360 SHM10D: IOLIST *,I8$(1)
0365 SHM10E: IOLIST *,I9$
0370 SHM10F: IOLIST *,C1$
0400 REM " --- Parameters"
0405 DIM P[3]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="SH",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=SHS01A
0490 LET P1$="######",P[0]=6,P[2]=5,P[3]=7
0500 REM " --- Initializations"
0510 DIM I8$(128),A0$(20),A1$(168),A2$(64),A[8]
0520 DIM W0$(20),W1$(128),W[20],MESSAGE$[5]
0550 LET M5$="(###)-###-####",M6$="00000-0000",K9=0,N=0
0560 LET TODAY$=F0$(18,6),LABELS$="N"
0700 REM " --- Background"
0750 PRINT 'SB',@(3,4),"Carrier:",@(1,5),"Warehouse:",@(4,10),"Labels:",@(5,12),"Phase:",'SF',
0800 REM " --- Warn operator"
0810 LET MESSAGE$[0]="This task will erase package processing files and"
0820 LET MESSAGE$[1]="create sample manifest data. Any existing packages"
0830 LET MESSAGE$[2]="will be deleted. Make sure manifests and updates"
0840 LET MESSAGE$[3]="have been run for all installed carriers."
0850 LET MESSAGE$[4]=""
0860 LET MESSAGE$[5]="Press <Enter> to continue or <F4> to exit"
0870 CALL "SYC.XA",2,MESSAGE$[ALL],5,22,-1,V$,V3
0890 IF V3>0 THEN GOTO 9900
1000 REM " --- Carrier ID"
1010 LET V0$="S",V1$="CK",V2$=P9$,V3$="",V0=1,V1=12,V2=4,I0=0
1020 LET V4$="<F3>=Lookup"
1030 GOSUB 7000
1040 ON V3 GOTO 1100,1000,1000,1050,9900
1050 CALL "SHC.LK",V1,V2,"D",0,V$
1080 IF V$="" THEN GOTO 1000
1110 FIND (SHM10_DEV,KEY=N0$+"D"+V$,DOM=1000)IOL=SHM10D
1120 LET P9$=V$,C$=V$,V4$=FNP$(I8$(1,30))
1130 IF I8$(97,1)="Y" THEN GOTO 1150
1135 LET V4$=V4$+" Not Installed"
1140 LET MESSAGE$[0]=V4$+" (<Enter>=Continue)"
1143 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
1145 GOTO 1000
1150 FIND (SHM10_DEV,KEY=N0$+"E"+P9$+FID(0)+FILL(4-LEN(FID(0))),DOM=1170)IOL=SHM10E
1155 IF P8$="" THEN LET P8$=I9$(40,2)
1160 GOTO 1180
1170 LET V4$="Terminal not configured for this carrier"
1175 GOTO 1140
1180 PRINT @(V1,V2),P9$,"  ",V4$,
1190 GOTO 3900
1200 REM " --- Warehouse ID"
1210 LET V0$="S",V1$="CK",V2$=P8$,V3$="",V0=2,V1=12,V2=5
1220 LET V4$="<F3>=Lookup"
1230 GOSUB 7000
1240 ON V3 GOTO 1300,1000,1000,1250,9900
1250 CALL "SHC.LK",V1,V2,"F",0,V$
1280 IF V$="" THEN GOTO 1000
1300 REM " --- Retrieve warehouse record"
1310 IF LEN(V$)<2 THEN LET V$=V$+"  ",V$=V$(1,2)
1320 FIND (SHM10_DEV,KEY=N0$+"F"+V$,DOM=1200)IOL=SHM10F
1330 LET P8$=V$
1340 PRINT @(12,5),P8$," ",C1$(1,30),@(15,6),C1$(31,24),@(15,7),C1$(55,24),@(15,8),C1$(79,24),@(15,9),C1$(103,24)," ",FNZ$(C1$(137,9))
1390 GOTO 3900
1400 REM " --- Labels?"
1410 LET V0$="Y",V1$="C",V2$=LABELS$,V3$="",V0=1,V1=12,V2=10
1420 LET V4$="Print Shipping And/Or COD Labels During Sample Manifest Generation (Y/N)?"
1430 GOSUB 7000
1440 ON V3 GOTO 1450,1400,3900,1400,3900
1450 LET LABELS$=V$
1460 PRINT @(V1,V2),LABELS$,
1490 GOTO 3900
3900 REM " --- Next field"
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4030 ON POS(V$="YN") GOTO 4000,4050,1000
4500 REM " --- Initializations"
4510 LET MANUAL=(ASC(P9$)-64)*1000,LOW=MANUAL,HIGH=LOW+999
4700 REM " --- Background"
4710 CALL "SYC.NB","Processing",11,COLUMN
4720 PRINT @(12,12),"Carrier Pass",
4800 REM " --- Clear files"
4810 CALL "SYC.FA",SHE01_DEV
4820 CALL "SYC.FA",SHE02_DEV
4830 CALL "SYC.FA",SHE04_DEV
4900 REM " --- Run Carrier Overlay or Sample Manifest Overlay"
4910 LET MANIFEST$=FNN$(I8$(91,6))
4950 RUN MANIFEST$,ERR=4990
4990 RUN "SHX.MB"
5000 REM " --- Display"
5020 PRINT @(12,5),A1$(1,30),@(12,6),A1$(31,24),@(12,7),A1$(55,24),@(12,8),A1$(79,24),@(12,9),A1$(103,24),@(12,10),FNF$(A1$(127,10),M5$),
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8065 DEF FNN$(Q$)=FNP$(Q$(POS("NONE"=Q$)*4+1))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

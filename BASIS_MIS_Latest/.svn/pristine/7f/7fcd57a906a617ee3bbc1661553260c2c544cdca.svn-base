0010 REM "APU - RECURRING INVOICE UPDATE"
0020 REM "Program APU.FA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET OPTIONS$[3]="L",OPTIONS$[4]="L"
0130 LET FILES$[1]="APE-01",FILES$[2]="APE-11",FILES$[3]="APE-03"
0140 LET FILES$[4]="APE-13",FILES$[5]="APT-01",FILES$[6]="SYS-01"
0150 LET FILES$[7]="APM-10"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET APE01_DEV=CHANNELS[1],APE11_DEV=CHANNELS[2],APE03_DEV=CHANNELS[3]
0185 LET APE13_DEV=CHANNELS[4],APT01_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0190 LET APM10_DEV=CHANNELS[7]
0200 REM " --- IOLists"
0210 APE03A: IOLIST A0$,A[ALL]
0230 APE01A: IOLIST A0$,A[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0310 APE11A: IOLIST W0$,W1$,W2$,W[0],W[1]
0320 APE13A: IOLIST W0$,W1$,W2$,W[0],W[1]
0360 APM10A: IOLIST X0$,X[0],X[1],X[2]
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[6],G[4]
0460 FOR X=0 TO 4
0470 LET G[X]=NUM(G2$(X*2+1,2),ERR=0480)
0480 NEXT X
0485 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[5]=NUM(P4$(1,2)),P[6]=FNYY_YEAR(P4$(3,2))
0500 REM " --- Init Data"
0530 DIM A[3],B[13],W[2],X[2]
0700 REM " --- Background"
0705 PRINT @(0,3),'CE',
0710 FOR X=4 TO 8 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'CF','SB',@(28,5),"Month/Year:",'SF',@(0,22),'CL',
0750 CALL "SYC.NB","Updating:",6,COLUMN
0800 REM " --- Options"
0810 LET V4$="Are You Ready To Update The "+N3$
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0835 LET V0$="S",V1$="",V2$="",V3$="",V4$="Enter the Month and Year to be assigned to each invoice",V0=4,V1=40,V2=5
0840 GOSUB 7000
0845 IF V3=4 THEN GOTO 9900
0850 IF LEN(V$)<>4 THEN GOTO 0840
0860 LET X=NUM(V$,ERR=0840),X=NUM(V$(1,2)),Y=1900+FNYY_YEAR(V$(3,2))
0870 IF X<1 OR X>12 THEN GOTO 0840
0880 LET P9$=FND$(V$(3,2)+"01"+V$(1,2)),P9$=P9$(1,2)
0890 PRINT @(40,5),V$(1,2),"/",V$(3,2),
0900 REM " --- Correct?"
0905 LET V0$="Y",V1$="CE",V2$="N",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
0910 GOSUB 7000
0917 IF V3=4 THEN GOTO 9900
0920 IF V$="N" THEN GOTO 0835
0930 IF V$<>"Y" THEN GOTO 0900
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
1000 REM " --- Initial Read"
1020 READ (APE03_DEV,KEY=N0$,DOM=1040)
1100 REM " --- Main Read"
1120 LET K0$=KEY(APE03_DEV,END=9900)
1140 IF K0$(1,2)<>N0$ THEN GOTO 9900
1150 IF K0$(21,2)<>"00" THEN READ (APE03_DEV); GOTO 1100
1160 READ (APE03_DEV)IOL=APE03A
1190 PRINT @(COLUMN,11),A0$(5,6),
1195 IF LEN(K0$)>20 THEN GOSUB 6600
1196 LET X9$=A0$(1,20)
1197 LET A0$(11,10)=K0$(11,10)
1200 REM " --- Change Date"
1210 LET A0$(29,2)=P9$(2,1)+P9$(1,1)
1220 LET V1$=FNC$(A0$(29,3)),V0=FNYY_YEAR(V1$(5,2)),V1=NUM(V1$(1,2)),V2=NUM(V1$(3,2))
1230 LET V$=FND$(DATE(JUL(1900+V0,V1,V2,ERR=1250):"%Yz%Mz%Dz"))
1240 GOTO 1290
1250 LET V2=V2-1
1260 GOTO 1230
1290 LET A0$(29,3)=V$,A0$(32,3)=A0$(29,3)
1300 REM " --- Create Dates"
1320 FIND (APM10_DEV,KEY=N0$+"C"+A0$(27,2),DOM=1100)IOL=APM10A
1390 GOSUB 6200
1400 REM " --- Write Heading"
1410 LET A0$=A0$+FILL(78-LEN(A0$))
1420 WRITE (APE01_DEV,KEY=K0$,DOM=1100)IOL=APE01A
2400 REM " --- Detail"
2410 READ (APE13_DEV,KEY=X9$(1,20),DOM=2420)
2420 LET K$=KEY(APE13_DEV,END=3000)
2440 IF K$(1,20)<>X9$ THEN GOTO 3000
2460 READ (APE13_DEV)IOL=APE13A
2470 LET K$(11,10)=K0$(11,10),W0$(11,10)=K$(11,10)
2480 WRITE (APE11_DEV,KEY=K$)IOL=APE11A
2900 GOTO 2420
3000 GOTO 1100
6200 REM " --- Create Due and Disc"
6220 LET V=X[1]
6230 CALL "SYC.CD",X0$(26,1),A0$(29,3),V,V$,STATUS
6240 LET A0$(35,3)=V$,V=X[2]
6260 CALL "SYC.CD",X0$(26,1),A0$(29,3),V,V$,STATUS
6270 LET A0$(38,3)=V$
6290 RETURN 
6600 REM " --- Get Unique Invoice Number"
6610 LET JUL_DATE=JUL(Y,X,NUM(F0$(22,2)))
6620 LET X$=STR(JUL_DATE:"0000000")
6630 LET K0$(17,4)=X$(4)
6640 READ (APT01_DEV,KEY=K0$,DOM=6690)
6650 LET JUL_DAT=JUL_DATE+1
6660 GOTO 6620
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

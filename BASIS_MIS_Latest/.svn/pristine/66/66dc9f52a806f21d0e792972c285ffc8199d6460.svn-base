0010 REM "SYX - Merge Data From One File Into Another"
0020 REM "Program SYX.MA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "22Jul96 JAL: New program for 6.0.1"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0500 REM " --- Initializations"
0510 DIM MESSAGE$[1],FILES$[1],OPTIONS$[1],CHANNELS[1]
0550 LET SOURCE$="",TARGET$="",OVERWRITE$="N"
0700 REM " --- Background"
0710 FOR X=4 TO 8 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(23,3),"Input File Name:",@(22,5),"Output File Name:",@(12,7),"Overwrite Existing Records:",'SF',
1000 REM " --- Input file"
1010 LET V0$="S",V1$="CE",V2$=SOURCE$,V3$="",V0=12,V1=40,V2=3
1020 LET V4$="Enter The Name Of The File Containing The Data To Be Merged"
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,3900,1000,3900
1050 LET SOURCE$=FNP$(V$)
1060 IF SOURCE$="" THEN GOTO 1000
1070 GOSUB 6000
1080 IF STATUS THEN GOTO 1000
1090 GOTO 3900
1200 REM " --- Output file"
1210 LET V0$="S",V1$="CE",V2$=TARGET$,V3$="",V0=12,V1=40,V2=5
1220 LET V4$="Enter The Name Of The File The Data Should Be Merged Into"
1230 GOSUB 7000
1240 ON V3 GOTO 1250,1200,3900,1200,3900
1250 LET TARGET$=FNP$(V$)
1260 IF TARGET$="" THEN GOTO 1200
1265 IF TARGET$=SOURCE$ THEN GOTO 1200
1270 GOSUB 6200
1280 IF STATUS THEN GOTO 1200
1290 GOTO 3900
1400 REM " --- Overwrite existing records?"
1410 LET V0$="Y",V1$="CE",V2$=OVERWRITE$,V3$="",V0=1,V1=40,V2=7
1420 LET V4$="Overwrite Existing Records With Data Being Merged (Y/N)?"
1430 GOSUB 7000
1440 ON V3 GOTO 1450,1400,3900,1400,3900
1450 LET OVERWRITE$=V$
1490 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,3990
3990 LET I0=0
4000 REM " --- OK to continue?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",1,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Open file(s)"
4110 GOSUB 6000
4120 IF STATUS THEN GOTO 1000
4130 GOSUB 6200
4140 IF STATUS THEN GOTO 1000
4150 CALL "SYC.NB","Merging",16,COLUMN
4160 LET KL=0,LAST$=""
4190 READ (SOURCE_DEV,KEY="",DOM=4200)
4200 REM " --- Get next key"
4210 LET K$=KEY(SOURCE_DEV,END=4900)
4220 IF KL=0 THEN LET KL=MIN(16,LEN(K$))
4230 LET X$=K$(1,KL)
4240 IF X$=LAST$ THEN GOTO 4300
4250 PRINT @(COLUMN,11),X$,
4260 LET LAST$=K$
4300 REM " --- Copy from source to target"
4310 READ RECORD(SOURCE_DEV)R$
4320 IF OVERWRITE$="Y" THEN WRITE RECORD(TARGET_DEV,KEY=K$)R$
4330 IF OVERWRITE$="N" THEN WRITE RECORD(TARGET_DEV,KEY=K$,DOM=4200)R$
4390 GOTO 4200
4900 REM " --- All done"
4910 PRINT 'CF',
4990 GOTO 1000
6000 REM " --- Open source file"
6010 CLOSE (SOURCE_DEV,ERR=6020)
6020 LET FILES$[1]=SOURCE$
6030 GOSUB 6400
6040 LET SOURCE_DEV=CHANNEL
6090 RETURN 
6200 REM " --- Open target file"
6210 CLOSE (TARGET_DEV,ERR=6220)
6220 LET FILES$[1]=TARGET$
6230 GOSUB 6400
6240 LET TARGET_DEV=CHANNEL
6290 RETURN 
6400 REM " --- Open FILENAME$"
6410 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
6420 LET CHANNEL=CHANNELS[1]
6490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=Q$(1,POS(" "<>Q$,-1))
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

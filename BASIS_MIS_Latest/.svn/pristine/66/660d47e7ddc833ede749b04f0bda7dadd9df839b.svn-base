0010 REM "SYC - On-Line Help Display"
0020 REM "Program SYC.HP"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "PGM$  : Calling program name (input)"
0050 REM "COLUMN: Current screen column (input)"
0055 REM "ROW   : Current screen row (input)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER PGM$,COLUMN,ROW
0100 REM " --- Help already invoked?"
0110 DIM HELP$(8)
0120 LET X$=STBL("!HELP",ERR=0400)
0190 GOTO 9950
0200 REM " --- IOLIST's"
0210 SYS11A: IOLIST A0$(1),A1$(1),A2$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0410 LET X$=STBL("!SCREEN",ERR=9700),HELP$(1)=PGM$
0420 IF X$(9,1)="N" THEN GOTO 9700
0430 LET X$=STBL("!HELP",HELP$,ERR=9900)
0500 REM " --- Initializations"
0550 LET NAME$="",HELPMENU$=PGM(-2)
0600 REM " --- Additional File Opens"
0610 LET FILES=5
0620 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0630 LET FILES$[1]="SYS-01",FILES$[2]="SYS-11",FILES$[3]="SYS-21"
0640 LET FILES$[4]="SYM-01",FILES$[5]="SYM-02"
0660 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0670 IF STATUS>0 THEN GOTO 9900
0680 LET SYS01_DEV=CHANNELS[1],SYS11_DEV=CHANNELS[2],SYS21_DEV=CHANNELS[3]
0690 LET SYM01_DEV=CHANNELS[4],SYM02_DEV=CHANNELS[5]
0800 REM " --- Parameters"
0810 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0820 LET N0$=F0$(16,2),N2$="SY"
1000 REM " --- Help Topic Lookup"
1010 CALL "SYC.HL",1,CHANNELS[ALL],NAME$,HELPMENU$,SELECTION$,FKEY
1020 IF FKEY=3 THEN GOTO 3000
1030 IF FKEY=4 THEN GOTO 4000
1040 IF SELECTION$="" THEN GOTO 4000
1100 REM " --- Retrieve header"
1110 DIM A0$(14),A1$(30),A2$(16)
1120 LET A0$(1)=N0$+SELECTION$
1130 FIND (SYS11_DEV,KEY=A0$,DOM=1200)IOL=SYS11A
1200 REM " --- Display help"
1210 DIM INFO$[5],INFO[5]
1220 LET INFO$[0]="SYS-21",INFO$[1]=A0$,INFO$[2]=A1$
1230 LET ACTION=11,INFO[1]=7,INFO[3]=3,INFO[4]=70
1240 CALL "SYC.KA",ACTION,INFO$[ALL],INFO[ALL]
1900 REM " --- Loop back for topic selection"
1990 GOTO 1000
3000 REM " --- Help Maintenance"
3010 CALL "SYC.WD",NAME$
3020 CALL "SYC.HM",0,CHANNELS[ALL]
3090 GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9700 REM " --- Help not installed"
9710 DIM MESSAGE$[1]
9720 LET MESSAGE$[0]="ADD+ON On-Line Help Has Not Been Installed (<Enter>=Continue)"
9730 CALL "SYC.XA",2,MESSAGE$[ALL],0,-1,-1,V$,V3
9790 GOTO 9900
9900 REM " --- Return to caller"
9910 CLOSE (SYS01_DEV,ERR=9911)
9915 CLOSE (SYS11_DEV,ERR=9916)
9920 CLOSE (SYS21_DEV,ERR=9921)
9925 CLOSE (SYM02_DEV,ERR=9926)
9930 CLOSE (SYM11_DEV,ERR=9931)
9935 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9945 LET X$=STBL("!CLEAR","!HELP",ERR=9950)
9950 EXIT 
9999 END

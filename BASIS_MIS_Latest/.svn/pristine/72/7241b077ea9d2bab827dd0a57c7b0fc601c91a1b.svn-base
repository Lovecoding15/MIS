0010 REM "WOU - Closed Work Order Update"
0020 REM "Program WOU.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "20Jan99 WGH: Delete extraneous obsolete code (3060,3190-3320)
0032 REM "01May99 WGH: Remove references to obsolete file WOT-02 (130,170)"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open Files"
0105 LET FILES=37
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-04",FILES$[2]="WOE-01",FILES$[3]="WOE-02"
0120 LET FILES$[4]="WOE-22",FILES$[5]="WOE-32",FILES$[6]="IVM-01"
0125 LET FILES$[7]="IVM-02",FILES$[8]="SYS-01",FILES$[9]="WOT-01"
0130 LET FILES$[10]="WOT-21",FILES$[11]="WOT-31",FILES$[12]=""
0135 LET FILES$[13]="IVT-04",FILES$[14]="WOM-10",FILES$[15]="WOE-13"
0140 LET FILES$[16]="WOE-23",FILES$[17]="WOE-15",FILES$[18]="WOE-25"
0145 LET FILES$[19]="ARM-10",FILES$[20]="WOE-06",FILES$[21]="WOT-03"
0146 LET FILES$[22]="WOT-23",FILES$[23]="WOT-33",FILES$[24]="WOM-05"
0147 LET FILES$[25]="WOM-06",FILES$[26]="IVM-12"
0148 LET OPTIONS$[1]="L",OPTIONS$[19]="F"
0150 CALL "SYC.DA",1,1,26,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0151 IF STATUS THEN GOTO 9900
0155 LET WOE04_DEV=CHANNELS[1],WOE01_DEV=CHANNELS[2],WOE02_DEV=CHANNELS[3]
0160 LET WOE22_DEV=CHANNELS[4],WOE32_DEV=CHANNELS[5],IVM01_DEV=CHANNELS[6]
0165 LET IVM02_DEV=CHANNELS[7],SYS01_DEV=CHANNELS[8],WOT01_DEV=CHANNELS[9]
0170 LET WOT21_DEV=CHANNELS[10],WOT31_DEV=CHANNELS[11]
0175 LET IVT04_DEV=CHANNELS[13],WOM10_DEV=CHANNELS[14],WOE13_DEV=CHANNELS[15]
0180 LET WOE23_DEV=CHANNELS[16],WOE15_DEV=CHANNELS[17],WOE25_DEV=CHANNELS[18]
0185 LET ARM10_DEV=CHANNELS[19],WOE06_DEV=CHANNELS[20],WOT03_DEV=CHANNELS[21]
0187 LET WOT23_DEV=CHANNELS[22],WOT33_DEV=CHANNELS[23],WOM05_DEV=CHANNELS[24]
0190 LET WOM06_DEV=CHANNELS[25],IVM12_DEV=CHANNELS[26]
0200 REM " --- IOLISTs"
0210 WOE01A: IOLIST A0$,A1$,A[ALL]
0220 IVM01A: IOLIST B0$,B1$,B2$,B3$,B4$,B5$,B6$,B[ALL]
0230 IVM02A: IOLIST C0$,C1$,C2$,C[ALL]
0240 IVM07A: IOLIST H0$,H1$,H[ALL]
0250 IVT01G: IOLIST X0$,X1$,X[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 WOS01A: IOLIST P0$,P1$,P2$,P9$,P4$,M0$,M1$,M2$,M3$
0275 ARS01A: IOLIST *,*,*,R3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0290 IVS01A: IOLIST X$,X$,P2$,P3$,P4$
0310 WOREQA: IOLIST W0$,W1$,W[ALL]
0320 WOTRAN: IOLIST W0$,W1$,W[ALL]
0325 WOE23A: IOLIST W0$,W1$,W[ALL]
0330 WOE06A: IOLIST W0$,W1$,W[ALL]
0340 ARE13A: IOLIST W0$,W1$,W[ALL]
0350 WOHIST: IOLIST W0$,W1$,W[ALL]
0360 WOM10A: IOLIST Y0$,Y1$
0380 IVT04H: IOLIST L0$(1),L1$(1),L2$(1),L[ALL]
0400 REM " --- Parameters"
0405 DIM G[4],INFO$[20],DESC[3]
0407 DIM PARAMS[0],PARAMS$[4],ITEMS$[3],REFS$[11],REFS[5],IVFILES[44]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$,R9$="N",GL$="N"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0435 LET P0=NUM(P2$(1,2)),IVS01_PRECISION=NUM(P2$(5,1)),P3=NUM(P4$(1,2))
0440 LET P4=FNYY_YEAR(P4$(3,2)),P9=NUM(P2$(7,2)),G[4]=NUM(G2$(9,2))
0460 CALL "SYC.VA","AR",INFO$[ALL]
0465 IF INFO$[20]<>"Y" THEN GOTO 0480
0470 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=0480)IOL=ARS01A
0475 LET R9$=R3$(11,1)
0480 CALL "SYC.VA",N2$,INFO$[ALL]
0485 LET GL$=INFO$[9]
0490 FOR X=1 TO 3; LET DESC[X]=NUM(P2$((X-1)*2+9,2)); NEXT X
0500 REM " --- Init Data"
0510 LET PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3),PARAMS$[2]=P2$,PARAMS$[3]=P3$
0512 LET PARAMS$[4]=P4$
0520 DIM J$(80,"-")
0600 REM " --- Open Additional Files"
0605 IF POS(P3$(17,1)="SL")=0 THEN GOTO 0620
0610 LET FILES$[27]="IVM-07",FILES$[28]="IVM-08",FILES$[29]="IVT-01"
0615 LET FILES$[30]="IVM-17",FILES$[31]="IVT-02"
0620 IF P9$(2,1)="Y" THEN LET FILES$[32]="ARE-03",FILES$[33]="ARE-13"
0625 IF GL$<>"Y" THEN GOTO 0635
0630 LET FILES$[34]="GLM-01",FILES$[35]="GLT-05",FILES$[36]="GLT-04"
0635 IF POS(P3$(3,1)="LF")=0 THEN GOTO 0645
0640 LET FILES$[37]="IVM-04"
0645 CALL "SYC.DA",1,27,37,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0650 IF STATUS THEN GOTO 9900
0655 LET IVM07_DEV=CHANNELS[27],IVM08_DEV=CHANNELS[28],IVT01_DEV=CHANNELS[29]
0660 LET IVM17_DEV=CHANNELS[30],IVT02_DEV=CHANNELS[31]
0665 LET ARE03_DEV=CHANNELS[32],ARE13_DEV=CHANNELS[33],GLM01_DEV=CHANNELS[34]
0670 LET GLT05_DEV=CHANNELS[35],GLT04_DEV=CHANNELS[36],IVM04_DEV=CHANNELS[37]
0675 LET IVFILES[1]=IVM01_DEV,IVFILES[2]=IVM02_DEV,IVFILES[4]=IVM04_DEV
0680 LET IVFILES[7]=IVM07_DEV,IVFILES[12]=IVM12_DEV,IVFILES[17]=IVM17_DEV
0685 LET IVFILES[41]=IVT01_DEV,IVFILES[42]=IVT02_DEV,IVFILES[44]=IVT04_DEV
0690 LET IVFILES[0]=SYS01_DEV,IVFILES[8]=IVM08_DEV
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0800 REM " --- Options"
0810 LET V4$="Are You Ready to Update?"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM 
0910 DIM A[11],B[10],C[16],W[11]
0920 CALL "SYC.NB","Updating",7,COLUMN
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
1000 REM " --- Initial Read"
1020 READ (WOE04_DEV,KEY=N0$+"  "+"B",DOM=1100)
1100 LET K0$=KEY(WOE04_DEV,END=4000)
1120 IF K0$(1,5)<>N0$+"  "+"B" THEN GOTO 4000
1140 EXTRACT (WOE01_DEV,KEY=K0$(1,4)+K0$(6,7),DOM=3800)IOL=WOE01A
1150 IF A0$(14,1)="R" THEN READ (WOE04_DEV); GOTO 1100
1160 PRINT @(COLUMN,11),A0$(5,7),
1170 LET REFS$[1]=A0$(34,6),REFS$[2]=A0$(40,7),REFS$[3]=A0$(5,7)
1180 LET REFS$[4]=A0$(12,2)
1200 REM " --- Calc Actuals From Transactions "
1210 DIM D[4],T[5]
1215 LET WOTRAN_DEV=WOT01_DEV
1220 READ (WOTRAN_DEV,KEY=A0$(1,11),DOM=1230)
1230 LET K$=KEY(WOTRAN_DEV,END=1295)
1240 IF K$(1,11)<>A0$(1,11) THEN GOTO 1295
1250 READ (WOTRAN_DEV,KEY=K$)IOL=WOTRAN
1260 LET X=POS(W0$(15,1)="MS")+2
1265 IF X>2 THEN LET D[X]=D[X]+W[2]; GOTO 1280
1270 LET D[1]=D[1]+W[0]*W[3],D[2]=D[2]+W[2]-W[0]*W[3]; REM "Split Direct & OH
1280 LET D[0]=D[0]+W[2]
1290 GOTO 1230
1295 IF WOTRAN_DEV=WOT01_DEV THEN LET WOTRAN_DEV=WOT21_DEV; GOTO 1220
1297 IF WOTRAN_DEV=WOT21_DEV THEN LET WOTRAN_DEV=WOT31_DEV; GOTO 1220
1300 LET T[0]=D[0]-A[3],T[0]=-T[0],T[1]=A[4]*A[5]; REM "CALC TOTALS
1370 DIM Y0$(40),Y1$(70,"0")
1380 FIND (WOM10_DEV,KEY=N0$+"A"+A0$(12,2),DOM=1390)IOL=WOM10A
1390 IF A0$(14,1)<>"I" THEN GOTO 2000
1400 REM " --- Determine GL Posting Accounts "                  
1405 READ (IVM01_DEV,KEY=N0$+A0$(56,20),DOM=1500)IOL=IVM01A
1410 IF R9$<>"Y" THEN LET Y2$=B6$(1,10)
1420 READ (IVM02_DEV,KEY=N0$+A0$(54,22),DOM=1500)IOL=IVM02A
1430 IF R9$="Y" THEN GOSUB 6600
1440 LET Y1$(11,10)=Y2$
1500 REM " --- Update Item ON ORDER Quantity "
1502 LET ACTION$="OO",ITEMS$[0]=N0$,ITEMS$[1]=A0$(54,2),ITEMS$[2]=A0$(56,20)
1503 LET REFS$[0]=A1$(91,3),REFS$[3]=A0$(5,7),ITEMS$[3]=""
1520 LET CLOSE_QTY=A[4],SCHEDULED_WO_TOTAL=A[0],CLOSED_PRIOR=A[2]
1530 LET COMPLETE_QTY=CLOSE_QTY+(SCHEDULED_WO_TOTAL-(CLOSED_PRIOR+CLOSE_QTY))
1540 IF A1$(94,1)="Y" THEN LET REFS[0]=-(COMPLETE_QTY)
1545 IF A1$(94,1)="N" THEN LET REFS[0]=-(CLOSE_QTY)
1550 GOSUB UPDATE_INVENTORY
1560 REM " --- Update Item ISSUED Quantity "
1570 LET ACTION$="WO"
1580 LET REFS$[0]=A1$(91,3),ITEMS$[3]=""
1590 IF A1$(96,1)="Y" THEN GOTO 2000
1600 LET REFS[0]=A[4],REFS[1]=A[5]
1610 GOSUB UPDATE_INVENTORY
2000 REM " --- Serial/Lotted "
2010 IF A1$(96,1)<>"Y" THEN GOTO 2400
2020 DIM X[2],W[11]
2030 READ (WOE06_DEV,KEY=A0$(1,11),DOM=2040)
2040 LET K1$=KEY(WOE06_DEV,END=2400)
2050 IF POS(A0$(1,11)=K1$)<>1 THEN GOTO 2400
2060 READ (WOE06_DEV,KEY=K1$)IOL=WOE06A
2070 IF W[4]<>0 THEN GOTO 2120
2080 IF A1$(94,1)<>"Y" THEN GOTO 2040
2090 IF W1$(1,1)="Y" THEN GOTO 2040
2100 REMOVE (WOE06_DEV,KEY=K1$); REM "Remove unclosed lots if complete
2110 GOTO 2040
2115 REM " --- Update Lot ISSUED Quantity "
2120 LET REFS[0]=W[4],REFS[1]=W[5],ITEMS$[3]=W1$(37,20)
2130 GOSUB UPDATE_INVENTORY
2300 REM " --- History Here"
2380 IF W1$(25,1)="Y" THEN LET W1$(1,1)="Y",W1$(2,3)=A1$(91,3)
2385 LET W[2]=W[2]+W[4],W[3]=W[3]+W[4]*W[5],W[4]=0,W[5]=0
2390 WRITE (WOE06_DEV,KEY=W0$(1,14))IOL=WOE06A
2395 GOTO 2040
2400 REM " --- Do Variances Here
2420 IF A1$(94,1)<>"Y" THEN LET T[0]=-T[1]; GOTO 2900
2430 IF Y0$(27,1)="A" THEN LET T[1]=D[0]-A[3]; GOTO 2900
2500 REM " --- Calc Standards"
2510 DIM E[4]
2515 LET WOREQ_DEV=WOE02_DEV
2520 READ (WOREQ_DEV,KEY=A0$(1,11),DOM=2530)
2530 LET K$=KEY(WOREQ_DEV,END=2595)
2540 IF K$(1,11)<>A0$(1,11) THEN GOTO 2595
2550 READ (WOREQ_DEV,KEY=K$)IOL=WOREQA
2560 LET X=POS(W0$(12,1)="BC")+2; IF X>2 THEN LET E[X]=E[X]+W[3]; GOTO 2580
2570 LET E[1]=E[1]+W[2]*W[6],E[2]=E[2]+W[3]-W[2]*W[6]; REM "Split Direct & OH
2580 LET E[0]=E[0]+W[3]
2590 GOTO 2530
2595 IF WOREQ_DEV=WOE02_DEV THEN LET WOREQ_DEV=WOE22_DEV; GOTO 2520
2596 IF WOREQ_DEV=WOE22_DEV THEN LET WOREQ_DEV=WOE32_DEV; GOTO 2520
2600 REM " --- Calc Variances
2620 PRECISION 2
2625 IF E[0]=0 THEN LET Y0=0; GOTO 2800
2630 IF A0$(14,1)<>"I" AND (A[0]=A[2]+A[4] OR E[0]=0 OR A1$(95,1)="N") THEN LET Y0=A[4]*A[5]+A[3]; GOTO 2800
2700 REM " --- Pro-Rate Standards"
2710 IF A0$(14,1)<>"I" THEN IF A[0]<>0 THEN LET Y0=E[0]*(A[2]+A[4])/A[0] ELSE LET Y0=0 ELSE LET Y0=A[4]*A[5]+A[3]
2720 IF Y0=E[0] THEN GOTO 2800
2725 IF E[0]=0 THEN LET E[1]=0,E[3]=0,E[4]=0; GOTO 2760
2730 LET E[1]=E[1]*Y0/E[0]
2740 LET E[3]=E[3]*Y0/E[0]
2750 LET E[4]=E[4]*Y0/E[0]
2760 LET E[2]=Y0-(E[1]+E[4]+E[3])
2800 REM " --- Calc Variance"
2820 LET T[2]=D[1]-E[1],T[4]=D[3]-E[3],T[5]=D[4]-E[4]
2830 LET T[3]=D[0]-(Y0+T[2]+T[4]+T[5]); REM "Any left overs goto OH VNCE
2900 REM " --- Post G/L Here"
2910 PRECISION 2
2915 LET Y1$=Y1$(1,20)+Y1$(51,40)
2920 FOR X=0 TO 5
2930 IF T[X]=0 THEN GOTO 2990
2940 LET ACCOUNT$=Y1$(X*10+1,10),WHEN$=A1$(91,3),AMOUNT=T[X],UNITS=0
2945 IF A0$(14,1)<>"I" THEN LET MEMO$=A1$(1,30) ELSE LET MEMO$=DESC$
2950 LET REF1$="WO "+A0$(5,7),REF2$="Whse "+A0$(54,2)
2955 LET REF3$="Type "+A0$(12,2)
2960 GOSUB GLPOST
2990 NEXT X
3000 REM " --- Complete It"
3010 IF A1$(94,1)="Y" THEN GOTO 3400
3020 IF A0$(31,3)<A1$(91,3) THEN LET A0$(31,3)=A1$(91,3)
3100 REM " --- Post To Customer Order Line Item (Partial) "
3110 IF A0$(14,1)<>"N" OR P9$(2,1)<>"Y" THEN GOTO 3180
3120 DIM W[14]
3130 PRECISION IVS01_PRECISION
3140 EXTRACT (ARE13_DEV,KEY=N0$+"  "+A0$(34,16),DOM=3180)IOL=ARE13A
3150 LET W[0]=A[5],W[4]=A[4]
3160 WRITE (ARE13_DEV,KEY=W0$(1,20))IOL=ARE13A
3170 PRECISION 2
3180 LET A[2]=A[2]+A[4],A[3]=A[3]+A[4]*A[5],A1$(91,5)="",A[4]=0,A[5]=0
3190 GOTO 3780
3400 REM " --- Complete, Complete"
3410 GOSUB 6700
3420 LET A[2]=A[2]+A[4],A[3]=A[3]+A[4]*A[5]
3430 LET A0$(15,1)="C",A0$(31,3)=A1$(91,3)
3440 REM " --- Post To Customer Order (Complete) "
3450 IF A0$(14,1)<>"N" OR P9$(2,1)<>"Y" THEN GOTO 3520
3460 DIM W[14]
3470 PRECISION IVS01_PRECISION
3480 EXTRACT (ARE13_DEV,KEY=N0$+"  "+A0$(34,16),DOM=3520)IOL=ARE13A
3490 LET W[0]=A[5],W[4]=A[4]
3500 WRITE (ARE13_DEV,KEY=W0$(1,20))IOL=ARE13A
3510 PRECISION 2
3520 LET A[4]=0,A[5]=0
3600 REM " --- Reset IVM-02 Committed If All Is Not Issued "
3610 REMOVE (WOE13_DEV,KEY=A0$(1,11)+"000",DOM=3620)
3620 READ (WOE23_DEV,KEY=A0$(1,11),DOM=3630)
3630 LET K$=KEY(WOE23_DEV,END=3720)
3640 IF K$(1,11)<>A0$(1,11) THEN GOTO 3720
3650 READ (WOE23_DEV,KEY=K$)IOL=WOE23A
3660 IF W[0]=W[1] THEN GOTO 3700
3670 LET ACTION$="UC",ITEMS$[0]=N0$,ITEMS$[1]=W1$(19,2),ITEMS$[2]=W1$(21,20)
3680 LET REFS[0]=W[0]-W[1],ITEMS$[3]=""
3690 GOSUB UPDATE_INVENTORY
3700 REMOVE (WOE23_DEV,KEY=K$,DOM=3710)
3710 GOTO 3630
3720 REMOVE (WOE15_DEV,KEY=A0$(1,11)+"000",DOM=3730)
3730 READ (WOE25_DEV,KEY=A0$(1,11),DOM=3740)
3740 LET K2$=KEY(WOE25_DEV,END=3780)
3750 IF K2$(1,11)<>A0$(1,11) THEN GOTO 3780
3760 REMOVE (WOE25_DEV,KEY=K2$)
3770 GOTO 3740
3780 PRECISION IVS01_PRECISION
3790 WRITE (WOE01_DEV,KEY=A0$(1,11))IOL=WOE01A
3800 REMOVE (WOE04_DEV,KEY=K0$,DOM=3810)
3810 GOTO 1100
4000 REM " --- End"
4010 IF GL$="Y" THEN CALL "GLC.CA",STATUS
4100 GOTO 9900
5600 REM " --- Update Inventory Information "
5610 UPDATE_INVENTORY: 
5620 CALL "IVC.UA",ACTION$,IVFILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEMS$[ALL],REFS$[ALL],REFS[ALL],STATUS
5630 IF STATUS THEN GOTO 9900
5640 RETURN 
6600 REM " --- Item Distribution Code"
6620 DIM Q8$(80,"0")
6640 FIND (ARM10_DEV,KEY=N0$+"D"+C1$(13,2),DOM=6690)*,Q8$(1)
6660 LET Y2$=Q8$(61,10)
6690 RETURN 
6700 REM " --- Remove Schedule Detail Records"
6710 READ (WOM06_DEV,KEY=A0$(1,2)+A0$(5,7),DOM=6720)
6720 LET K22$=KEY(WOM06_DEV,END=6790)
6730 IF POS(A0$(1,2)+A0$(5,7)=K22$)<>1 THEN GOTO 6790
6740 REMOVE (WOM06_DEV,KEY=K22$)
6750 REMOVE (WOM05_DEV,KEY=K22$(1,2)+K22$(13,6)+K22$(3,10),DOM=6760)
6760 GOTO 6720
6790 RETURN 
6900 REM " --- Standard G/L Posting Routine"
6910 GLPOST: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,WHEN$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

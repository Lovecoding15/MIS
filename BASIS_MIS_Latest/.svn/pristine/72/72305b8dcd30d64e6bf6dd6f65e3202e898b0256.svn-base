0010 REM "PRM - Employee Maintenance - Contrib. Code
0020 REM "Program PRM.MH"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "22Sep98 WGH: Allow Periods Taken = 99999 for inactive (2043,2045,2050,2057)
0040 REM 
0047 SETESC 9000
0050 SETERR 9000
0055 PRECISION P[3]
0060 ON O9 GOTO 0070,4000
0200 REM " --- IOLists
0240 PRM10D: IOLIST D0$,D[ALL]
0250 PRT31A: IOLIST E0$(1),E[ALL],X[ALL],Y[ALL],F[ALL]
0500 REM " --- Init Data
0520 DIM E0$(20),E[5],X[5],Y[5],F[1],D[0]
0540 LET E0$(1)=A0$+"D"
0550 LET O8=1
0700 REM " --- Background
0710 PRINT @(0,L0),'CE'
0730 FOR X=7 TO 16 STEP 9
0740 CALL "SYC.WC",1,0,80,0,0,X,0
0750 NEXT X
0760 PRINT 'SB',@(15,5),"Contribution Code Maintenance",@(1,6),"Contribution:",@(8+M1,8),"Gross",@(8+2*M1),"Basis",@(4+3*M1),"Contribution",@(0,9),"Month-To-Date:",@(2,10),"1st Quarter:",@(2,11),"2nd Quarter:",@(2,12),"3rd Quarter:",@(2,13),"4th Quarter:",@(1,14),"Year-To-Date:",@(3,15),"Next Month:",
0770 PRINT @(15,16),"Contribution Information",@(0,17),"Periods Taken:",@(4,18),"Cont Rate:",@(3,19),"Cont Limit:",
0790 PRINT 'SF',
0800 REM " --- Options
0900 REM " --- Init File
0920 READ (PRT31_DEV,KEY=A0$+"D",DOM=0930)
1000 REM " --- Key
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(PRT31_DEV,END=1050)
1030 IF V2$(1,12)<>A0$+"D" THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(13)
1050 LET V0$="S",V1$="CEK",V0=2,V1=15,V2=6,O9=0,V4$="Enter Valid Contribution Code, <F3>=Lookup"
1060 GOSUB 7000
1062 IF V3<>3 THEN GOTO 1070
1064 LET V4=1,V6=4,V7=2,V8=6,V9=16,V5$="D"
1066 CALL "SYC.LZ",V1,V2,V4,PRM10_DEV,V6,V7,V8,V9,V$,V5$,N0$
1068 IF V$="" THEN GOTO 1000
1070 IF V3=4 THEN RUN "PRM.MA"
1080 IF V$="" THEN GOTO 0900
1090 LET E0$(13)=V$
1200 REM " --- Find Key
1240 FIND (PRM10_DEV,KEY=N0$+"D"+E0$(13,2),DOM=1000)IOL=PRM10D
1250 PRINT @(V1+3,V2),D0$(6,16),
1260 FIND (PRT31_DEV,KEY=E0$(1,14),DOM=1400)IOL=PRT31A
1300 REM " --- Display
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record
1420 CALL "SYC.YN",1,"Is This A New Record?",1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-up
1510 DIM E[5],F[1],X[5],Y[5]
2000 REM " --- Periods
2020 LET V0$="Z",V1$="C",V2$=E0$(15,5),V4$="Enter OVERRIDE Period Selection  (1,2,3,4 or 5), '99999'=Inactive.",V0=5,V1=15,V2=17,I0=0,O9=1
2040 GOSUB 7000
2043 ON V3 GOTO 2045,2000,2090,2000,2090
2045 IF LEN(V$)=0 OR V$="99999" THEN GOTO 2060
2047 FOR X=1 TO 5
2050 IF POS(V$(X,1)="012345")=0 THEN EXITTO 2000
2055 NEXT X
2060 LET E0$(15,5)=V$
2090 GOTO 3900
2100 REM " --- Rate
2120 LET V0$="N",V2$=STR(F[0]),V3$=M3$,V1=15,V2=18
2140 GOSUB 7000
2160 LET F[0]=V
2190 GOTO 3900
2200 REM " --- Limit
2220 LET V0$="N",V2$=STR(F[1]),V3$=M1$,V1=15,V2=19
2240 GOSUB 7000
2260 LET F[1]=V
2290 GOTO 3900
3900 REM " --- Next Field
3910 IF V3=4 THEN GOTO 3990
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,4000
4000 REM " --- Options
4020 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)? ",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF O9=0 THEN IF V$="" OR V$="Y" THEN GOTO 1000
4060 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write
4120 WRITE (PRT31_DEV,KEY=E0$(1,14))IOL=PRT31A
4190 GOTO 1000
4200 REM " --- Delete
4210 IF E[1]+E[2]+E[3]+E[4]+E[5]<>0 THEN GOTO 4000
4220 REMOVE (PRT31_DEV,KEY=E0$(1,14),DOM=4230)
4290 GOTO 1000
5000 REM " --- Display
5020 LET E0=0,X0=0,Y0=0
5030 FOR X=0 TO 4
5040 PRINT @(15,9+X),E[X]:M1$,X[X]:M1$,Y[X]:M1$,
5050 IF X>0 THEN LET E0=E0+E[X],X0=X0+X[X],Y0=Y0+Y[X]
5060 NEXT X
5070 PRINT @(15,14),E0:M1$,X0:M1$,Y0:M1$,@(15,15),E[5]:M1$,X[5]:M1$,Y[5]:M1$,
5080 PRINT @(15,17),E0$(15,5),@(15,18),F[0]:M3$,@(15,19),F[1]:M1$,
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 RUN "PRM.MA"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

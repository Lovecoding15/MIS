0010 REM "DDX - Search & Replace Dictionary Element"
0020 REM "Program DDX.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="DDM-01",FILES$[2]="DDM-03",FILES$[3]="DDM-04"
0130 LET FILES$[4]="DDM-06",FILES$[5]="DDM-06",FILES$[6]="SYM-03"
0135 LET FILES$[7]="SYS-01",FILES$[8]="DDM-10",FILES$[9]="DDM-20"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET DDM01_DEV=CHANNELS[1],DDM03_DEV=CHANNELS[2],DDM04_DEV=CHANNELS[3]
0185 LET DDM06_DEV1=CHANNELS[4],DDM06_DEV2=CHANNELS[5],SYM03_DEV=CHANNELS[6]
0190 LET SYS01_DEV=CHANNELS[7],DDM10_DEV=CHANNELS[8],DDM20_DEV=CHANNELS[9]
0200 REM " --- IOLists
0210 DDM01A: IOLIST X0$(1),X1$(1),X[ALL]
0230 DDM03A: IOLIST B0$(1),B1$(1),B[ALL]
0240 DDM04A: IOLIST C0$(1),C1$(1),C[ALL]
0260 DDM06A: IOLIST XREF$
0280 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="DD",N4$=F5$
0500 REM " --- Initializations
0510 DIM A0$(6),A1$(77),A2$(30),A[2],B0$(7),B1$(64),B[2],XREF$(57)
0520 DIM C0$(10),C1$(122),C[2],X0$(12),X1$(105),X[2],O[2],DDFILES[8]
0530 LET BEGNAME$="",ENDNAME$="",REVISION$=F0$(18,6)
0560 LET DDFILES[1]=SYS01_DEV,DDFILES[3]=DDM01_DEV,DDFILES[4]=DDM06_DEV1
0570 LET DDFILES[5]=DDM10_DEV,DDFILES[6]=DDM20_DEV
0700 REM " --- Background
0750 PRINT 'SB',@(0,4),"From Data Name",@(2,5),"To Data Name",'SF',
1000 REM " --- From Name
1005 PRINT 'CF'
1010 LET V0$="S",V1$="C^",V2$=BEGNAME$,V3$="",V0=12,V1=15,V2=4,I0=0
1020 LET V4$="Enter the data name to be replaced (<F3>=Lookup)"
1030 GOSUB 7000
1040 ON V3 GOTO 1060,1000,3900,1050,9900
1050 LET ROW=V2+2
1055 CALL "DDC.LA",DDM01_DEV,ROW,V$
1060 LET X0$(1)=V$
1065 FIND (DDM01_DEV,KEY=X0$,DOM=1000)IOL=DDM01A
1070 PRINT @(V1,V2),X0$," ",X1$(1,30)
1080 LET BEGNAME$=X0$
1090 GOTO 3900
1200 REM " --- To Name
1210 LET V0$="S",V1$="C^",V2$=ENDNAME$,V3$="",V0=12,V1=15,V2=5
1220 LET V4$="Enter the replacement data name (<F1>=Maintenance/<F3>=Lookup)"
1230 GOSUB 7000
1240 ON V3 GOTO 1400,1250,3900,1300,9900
1250 LET WINDOW$="YES",ROW=V2+1
1260 CALL "DDC.AA",WINDOW$,3,ROW,V$,DDFILES[ALL]
1290 GOTO 1400
1300 REM 
1310 LET ROW=V2+1
1320 CALL "DDC.LA",DDM01_DEV,ROW,V$
1400 REM 
1410 LET X0$(1)=V$
1420 IF X0$=BEGNAME$ THEN GOTO 1200
1430 FIND (DDM01_DEV,KEY=X0$,DOM=1200)IOL=DDM01A
1440 PRINT @(V1,V2),X0$," ",X1$(1,30)
1450 LET ENDNAME$=X0$
1490 GOTO 3900
3900 REM " --- Next field
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,3990
3990 LET I0=0
4000 REM " --- Correct?
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="S",V1$="CR",V2$="Y",V3$="",V0=1,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Position DDM-06
4110 CALL "SYC.NB","Updating",12,COLUMN
4200 REM " --- Read next DDM-06 record
4210 LET XREF$(1)=BEGNAME$
4220 READ (DDM06_DEV1,KEY=XREF$,DOM=4230)
4230 LET K$=KEY(DDM06_DEV1,END=4900)
4240 IF K$(1,12)<>BEGNAME$ THEN GOTO 4900
4250 READ (DDM06_DEV1)
4300 REM " --- Read DDM-04 Field Record
4310 LET C0$(1)=K$(48,10)
4320 FIND (DDM04_DEV,KEY=C0$)IOL=DDM04A
4325 IF C1$(1,12)<>BEGNAME$ THEN GOTO 4200
4330 PRINT @(COLUMN,11),C0$(1,6)," ",C0$(7,1)," ",C0$(8)
4335 LET O0$=C0$,O1$=C1$,O[0]=C[1],O[1]=C[1],O[2]=C[2]
4340 GOSUB 6200
4345 LET C1$(1,12)=ENDNAME$,C1$(47,6)=REVISION$
4350 WRITE (DDM04_DEV,KEY=C0$)IOL=DDM04A
4370 GOSUB 6300
4380 GOSUB 6800
4390 GOTO 4200
4900 REM " --- All done
4910 PRINT @(0,6),'CE',
4920 LET BEGNAME$="",ENDNAME$=""
4990 GOTO 1000
6200 REM " --- Remove old field cross-reference
6210 LET X0$(1)=O1$(1),X1$(1)=""
6220 LET X1$(37,1)=O1$(53,1),X[0]=O[0]
6230 FIND (DDM01_DEV,KEY=X0$,DOM=6240)IOL=DDM01A
6240 LET LENGTH=X[0]
6250 IF O[1]>0 THEN LET LENGTH=LENGTH*O[1]
6255 IF FNP$(O1$(17,30))<>"" THEN LET X1$(1,30)=O1$(17,30)
6260 LET XREF$(1)=X0$(1,12)+X1$(1,30)+X1$(37,1)+STR(LENGTH:"0000")+O0$
6270 REMOVE (DDM06_DEV2,KEY=XREF$,DOM=6280)
6290 RETURN 
6300 REM " --- Write new field cross-reference
6310 LET X0$(1)=C1$(1),X1$(1)=""
6320 LET X[0]=0
6330 FIND (DDM01_DEV,KEY=X0$,DOM=6340)IOL=DDM01A
6340 LET LENGTH=X[0]
6350 IF C[1]>0 THEN LET LENGTH=LENGTH*C[1]
6355 IF FNP$(C1$(17,30))<>"" THEN LET X1$(1,30)=C1$(17,30)
6360 LET XREF$(1)=X0$(1,12)+X1$(1,30)+X1$(37,1)+STR(LENGTH:"0000")+C0$
6370 WRITE (DDM06_DEV2,KEY=XREF$)IOL=DDM06A
6390 RETURN 
6800 REM " --- Update Last Revision Date on Header/Record ID Records
6810 LET A0$(1)=C0$(1,6),B0$(1)=C0$(1,7)
6820 FIND (SYM03_DEV,KEY=A0$)IOL=SYM03A
6830 FIND (DDM03_DEV,KEY=B0$)IOL=DDM03A
6840 LET A1$(31,6)=REVISION$,B1$(31,6)=REVISION$
6850 WRITE (SYM03_DEV,KEY=A0$)IOL=SYM03A
6860 WRITE (DDM03_DEV,KEY=B0$)IOL=DDM03A
6890 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8090 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

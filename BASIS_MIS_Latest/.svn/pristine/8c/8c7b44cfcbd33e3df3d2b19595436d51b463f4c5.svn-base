0010 REM "ARM - Customer Maintenance (Open Invoices)"
0020 REM "Program ARM.ME"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0200 REM " --- IOLists" 
0210 ART01A: IOLIST X0$,X[ALL]
0310 ART11A: IOLIST W0$,W1$,W[ALL]
0400 REM " --- Parameters"
0450 LET WIDTH=80,HEIGHT=18,WIN_X=0,WIN_Y=5,PAGES=100
0460 LET TITLE$="Invoice Detail Inquiry",MAXROW=HEIGHT-5
0500 REM " --- Initializations"
0510 DIM X0$(50),X[1],W0$(19),W1$(11),W[1],J0$(40,"0")
0530 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0550 LET ROW=2,MASK$=M1$
0590 CALL "SYC.RM",MASK$,MASK,9,11
0600 REM " --- Print positions"
0610 DIM O[3]
0620 LET O[3]=WIDTH-2-MASK
0630 FOR X=2 TO 0 STEP -1
0640 LET O[X]=O[X+1]-MASK
0650 NEXT X
0700 REM " --- Background"
0705 PRINT @(0,WIN_Y+1),'CE',
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 LET HEADING$(2)="Invoice  Date   Due   TC Type"
0730 LET HEADING$(O[0]+MASK-8)="Invoice",HEADING$(O[1]+MASK-8)="Applied"
0740 LET HEADING$(O[2]+MASK-9)="Discount",HEADING$(O[3]+MASK-8)="Balance"
0750 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0760 PRINT @(0,0),'CS','SB','BR',HEADING$,'ER','SF',
0770 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
0800 REM " --- Options"
0810 PRINT 'CF','SB',@(14,ROW),"Beginning Invoice Number: ",@(8,ROW+1),"Display Paid, Un-Paid, Or Both:",'SF'
0820 LET V4$="Enter a Beginning Invoice Number (<Enter>=All)"
0825 PRINT @(0,HEIGHT-4),'CL',@(FNC(V4$,WIDTH-2),HEIGHT-4),V4$,
0830 LET V0$="Z",V1$="",V2$="",V3$="",V4$="",V0=7,V1=40,V2=ROW
0835 GOSUB 7000
0840 ON V3 GOTO 0845,0820,0820,0820,9900
0845 LET FIRST$=V$
0850 LET V4$="P=Paid Invoices   U=Unpaid Invoices   B=Both"
0855 PRINT @(0,HEIGHT-4),'CL',@(FNC(V4$,WIDTH-2),HEIGHT-4),V4$,
0860 LET V0$="S",V1$="^",V2$="B",V3$="PUB",V4$="",V0=1,V1=40,V2=ROW+1
0865 GOSUB 7000
0870 ON V3 GOTO 0875,0850,0820,0850,9900
0875 IF POS(V$="PUB")=0 THEN GOTO 0850
0880 LET OPTION$=V$
0890 PRINT @(0,ROW),'CL',@(0,ROW+1),'CL',@(0,HEIGHT-4),'CL',
0900 REM " --- Position ART-01"
0910 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,5]
0920 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,ROW=2,T0$=""
0930 LET INVTOTAL=0,APPTOTAL=0,DISTOTAL=0,BALTOTAL=0,DETAIL$="Y"
0980 LET X0$(1)=A0$(1,2)+"  "+A0$(3),X0$(11)=FIRST$
0990 READ (ART01_DEV,KEY=X0$(1,19),DOM=1000)IOL=ART01A
1000 REM " --- Read next invoice"
1010 LET K$=KEY(ART01_DEV,END=1900)
1020 IF POS(N0$=K$)<>1 THEN GOTO 1900
1030 IF K$(5,6)<>A0$(3) THEN GOTO 1900
1100 REM 
1110 LET FKEY=0
1150 REM " --- Invoice header"
1160 READ (ART01_DEV,KEY=K$)IOL=ART01A
1170 GOSUB 6500
1180 IF OPTION$="P" THEN IF BALANCE<>0 THEN GOTO 1800
1190 IF OPTION$="U" THEN IF BALANCE=0 THEN GOTO 1800
1200 REM " --- Display invoice totals"
1210 IF T0$=K$(11,7) THEN GOTO 1250
1220 GOSUB 6200
1230 IF FKEY=4 THEN GOTO 0800
1300 REM " --- Header window record"
1310 DIM G1$(WIDTH-2)
1320 IF L+1>MAXROW THEN GOSUB 5400
1330 IF FKEY=4 THEN GOTO 0800
1340 LET G1$(2)=X0$(11,7),G1$(10)=FNH$(X0$(24,3))
1350 LET G1$(17)=FNH$(X0$(27,3)),G1$(24)=X0$(22,2),G1$(27)=X0$(33,1)
1355 LET G1$(O[0])=STR(INVOICE:MASK$)
1356 IF X[1]<>0 THEN LET G1$(O[2])=STR(X[1]:MASK$)
1360 IF NUMBER>0 THEN GOTO 1380
1370 LET G1$(O[3])=STR(BALANCE:MASK$)
1380 LET LLIST$[PAGE,X1,1]=G1$,X0=X0+1,X1=X1+1,L=L+1
1390 LET INVTOTAL=INVTOTAL+INVOICE,BALTOTAL=BALTOTAL+INVOICE
1400 REM " --- Skip detail section if no detail
1410 FIND (ART11_DEV,KEY=X0$(1,17)+"01",DOM=NO_DETAIL)
1420 GOTO 1500
1430 NO_DETAIL: 
1440 LET DETAIL$="N"
1450 GOTO 1800
1500 REM " --- Detail window record"
1505 READ (ART11_DEV,KEY=X0$(1,17),DOM=1510)
1510 LET K11$=KEY(ART11_DEV,END=1800)
1515 IF K11$(1,17)<>X0$(1,17) THEN GOTO 1800
1520 READ (ART11_DEV,KEY=K11$)IOL=ART11A
1530 IF W1$(1,1)=" " THEN GOTO 1700
1540 IF L+1>MAXROW THEN GOSUB 5400
1545 IF FKEY=4 THEN GOTO 0800
1550 DIM G1$(WIDTH-2)
1560 LET G1$(10)=FNH$(W1$(2,3))
1570 IF W1$(1,1)="C" THEN LET G1$(17)=FNP$(W1$(5,7))
1580 LET G1$(27)=W1$(1,1),G1$(O[1])=STR(W[0]:MASK$)
1590 IF W[1]<>0 THEN LET G1$(O[2])=STR(W[1]:MASK$)
1670 LET LLIST$[PAGE,X1,1]=G1$,X0=X0+1,X1=X1+1,L=L+1
1675 LET APPTOTAL=APPTOTAL+W[0],DISTOTAL=DISTOTAL+W[1],BALTOTAL=BALTOTAL+W[0]+W[1]
1700 REM " --- Loop up for next Detail Record"
1710 GOTO 1510
1780 REM " --- Detail processing completed"
1790 IF FKEY=4 THEN GOTO 0800
1800 REM " --- Loop back for next invoice"
1890 GOTO 1000
1900 REM " --- No more invoices"
1910 IF X0>0 THEN GOTO 2000
1915 CALL "SYC.WB",NAME$,0
1920 LET V0$="Sa",V1$="",V2$="",V3$="",V4$=""
1925 LET V0=1,V1=38,V2=INT(HEIGHT/2)-1
1930 PRINT @(1,V2),"No records found (<Enter>=Continue):"
1940 GOSUB 7000
1990 GOTO 0800
2000 REM " --- Display last page"
2010 GOSUB 6200
2015 IF FKEY=4 THEN GOTO 0800
2020 GOSUB 6000
2025 IF FKEY=4 THEN GOTO 0800
2030 IF X1=0 THEN GOTO 4000
2040 LET ANSWER$="|EOF"
2050 GOSUB 5400
4000 REM " --- All done"
4090 GOTO 0800
5400 REM " --- Window processing"
5410 LET MODE=5,X1=1,L=1
5420 CALL "SYC.SA",MODE,LLIST$[ALL],MORE$[ALL],ANSWER$,PAGE,MAXROW,NAME$,HEIGHT,WIDTH,FKEY
5460 IF FKEY=4 THEN GOTO 5490
5470 IF FKEY<>-17 THEN LET PAGE=PAGE+1
5480 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAXROW,5,SHEIGHT-2
5490 RETURN 
6000 REM " --- Customer Total"
6020 IF L+1>MAXROW THEN GOSUB 5400
6030 IF FKEY=4 THEN GOTO 6090
6040 DIM G1$(WIDTH-2)
6050 LET G1$(14)="Customer Total",G1$(O[0])=STR(INVTOTAL:MASK$)
6060 LET G1$(O[1])=STR(APPTOTAL:MASK$),G1$(O[2])=STR(DISTOTAL:MASK$),G1$(O[3])=STR(BALTOTAL:MASK$)
6070 LET LLIST$[PAGE,X1,1]=G1$,X0=X0+1,X1=X1+1,L=L+1
6090 RETURN 
6200 REM " --- Invoice Total"
6210 IF T0$="" THEN GOTO 6270
6215 IF DETAIL$="N" THEN LET DETAIL$="Y"; GOTO 6270
6220 IF L+1>MAXROW THEN GOSUB 5400
6230 IF FKEY=4 THEN GOTO 6290
6240 DIM G1$(WIDTH-2)
6250 LET G1$(15)="Invoice Total",G1$(O[3])=STR(LASTBALANCE:MASK$)
6260 LET LLIST$[PAGE,X1,1]=G1$,X0=X0+1,X1=X1+1,L=L+1
6270 LET T0$=K$(11,7),LASTBALANCE=BALANCE
6290 RETURN 
6500 REM " --- Retrieve payment and adjustments
6510 LET INVOICE=X[0],DISCOUNT=0,APPLIED=0,NUMBER=0
6515 READ (ART11_DEV,KEY=X0$(1,17),DOM=6520)
6520 LET K11$=KEY(ART11_DEV,END=6580)
6530 IF K11$(1,17)<>X0$(1,17) THEN GOTO 6580
6540 DIM W[1]
6550 READ (ART11_DEV,KEY=K11$)IOL=ART11A
6560 LET APPLIED=APPLIED+W[0]
6565 LET DISCOUNT=DISCOUNT+W[1]
6570 LET NUMBER=NUMBER+1
6575 GOTO 6520
6580 LET BALANCE=INVOICE+APPLIED+DISCOUNT
6590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8070 DEF FNH$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 LET O0=0
9230 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to customer maintenance menu"
9910 CALL "SYC.WD",NAME$
9950 RUN "ARM.MA"
9999 END

0010 REM "SYC - Printer Selection"
0020 REM "Program SYC.QP"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SYM-22",FILES$[3]="SYM-07"
0140 LET FILES$[4]="SYM-17",FILES$[5]="ARM-07"
0150 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],SYM22_DEV=CHANNELS[2]
0180 LET SYM07_DEV=CHANNELS[3],SYM17_DEV=CHANNELS[4]
0200 REM " --- IOLIST's"
0210 SYM07A: IOLIST A0$(1),A1$(1)
0220 SYM17A: IOLIST B0$(1),B1$(1)
0230 ARM07A: IOLIST C0$,C1$(1),C2$(1)
0260 SYS01T: IOLIST TERMINAL$,F0$(1),F1$,F2$,F3$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM F0$(25),INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0420 LET SOURCE$=OPTION$[1],OPTION$[1]="",PRINTERS$="",OP$="N",N0$=F0$(16,2)
0425 IF SOURCE$="ORDER" THEN GOTO 0450
0430 IF SOURCE$="INVOICE" THEN GOTO 0450
0435 IF SOURCE$="RECEIPT" THEN GOTO 0450
0440 LET SOURCE$=""
0445 GOTO 0500
0450 CALL "SYC.VA","OP",INFO$[ALL]
0460 LET OP$=INFO$[20]
0465 IF OP$="N" THEN GOTO 9900
0470 LET PRINTERS=1,SELECTION=15
0475 IF SOURCE$="RECEIPT" THEN LET PRINTERS=19,SELECTION=27
0500 REM " --- Initializations"
0510 DIM A0$(2),A1$(30),B0$(3),B1$(30),C1$(32),C2$(42)
0550 LET PAGES=3,MAXROW=15,WIDTH=40,HEIGHT=12,WIN_X=0,WIN_Y=5,SWIN_X=40
0560 LET MAX_ROW=HEIGHT-5,TITLE$="Available Printers",D_STATUS=0
0570 LET STITLE$="Printer Options",SELECTION$=""
0600 REM " --- Additional file opens"
0610 IF OP$="N" THEN GOTO 0650
0615 CALL "SYC.DA",1,5,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS THEN GOTO 9900
0625 LET ARM07_DEV=CHANNELS[5]
0630 FIND (ARM07_DEV,KEY=N0$+FID(0),DOM=9900)IOL=ARM07A
0640 LET PRINTERS$=C2$(PRINTERS,8)
0650 REM " --- Window arrays"
0670 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),SHEADING$(WIDTH-2)
0680 LET HEADING$(2)="Printer",SHEADING$(2)="Printer Option"
0690 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0730 CALL "SYC.WA",0,WIDTH,HEIGHT,SWIN_X,WIN_Y,STITLE$,SNAME$
0740 PRINT @(0,0),'SB','BR',SHEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0750 CALL "SYC.WB",NAME$,0
1000 REM " --- Initializations for printer window"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,HEIGHT-2]
1020 LET PAGE=1,L=1,X1=1,SELECTION$=F0$(13,2),PRINTER=1
1080 CALL "SYC.WB",NAME$,0
1090 READ (SYM22_DEV,KEY=F0$(4,3),DOM=1100)
1100 REM " --- Read next printer for this operator"
1105 IF SOURCE$<>"" THEN GOTO 1150
1110 LET K$=KEY(SYM22_DEV,END=2000)
1120 IF POS(F0$(4,3)=K$)<>1 THEN GOTO 2000
1130 READ (SYM22_DEV)
1140 GOTO 1200
1150 IF PRINTER>8 THEN GOTO 2000
1160 LET K$=F0$(4,3)+PRINTERS$(PRINTER,2),PRINTER=PRINTER+2
1200 REM " --- Retrieve printer"
1210 LET A0$(1)=K$(4),A1$(1)="Print To File"
1220 IF A0$="PF" THEN GOTO 1300
1230 FIND (SYM07_DEV,KEY=A0$,DOM=1900)IOL=SYM07A
1300 REM " --- Display page?"
1310 IF L<MAX_ROW THEN GOTO 1400
1320 LET XMODE=45,XNAME$=NAME$
1330 GOSUB 6200
1340 IF FKEY=1 THEN GOTO 1000
1350 IF FKEY=4 THEN GOTO 9900
1360 IF SELECTION$="PRIOR" OR SELECTION$="EXIT" OR SELECTION$="STARTOVER" THEN GOTO 9900
1390 IF SELECTION$<>"" THEN GOTO 2100
1400 REM " --- Printer window record"
1410 DIM G1$(WIDTH-2)
1420 LET G1$(2)=A0$,G1$(5)=A1$,LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$
1500 REM " --- Print to file?"
1510 IF A0$<>"PF" THEN GOTO 1600
1520 LET G1$(2)="This option will allow you to print",MORE$[PAGE,X1,3]=G1$
1530 LET G1$(2)="to a file. Retrieve the file with",MORE$[PAGE,X1,4]=G1$
1540 LET G1$(2)="<F1> from any menu or using the",MORE$[PAGE,X1,5]=G1$
1550 LET G1$(2)=$22$+"Reports Display"+$22$+" option on the"
1560 LET MORE$[PAGE,X1,6]=G1$,G1$(2)="SpeedSearch menu.",MORE$[PAGE,X1,7]=G1$
1590 GOTO 1800
1600 REM " --- Position options file"
1610 LET X=3
1690 READ (SYM17_DEV,KEY=A0$,DOM=1700)
1700 REM " --- Read next options record"
1710 LET K$=KEY(SYM17_DEV,END=1800)
1720 IF POS(A0$=K$)<>1 THEN GOTO 1800
1730 READ (SYM17_DEV)IOL=SYM17A
1740 LET G1$(2)=B0$(3),G1$(4)=B1$,MORE$[PAGE,X1,X]=G1$
1750 LET X=X+1
1790 IF X<HEIGHT-3 THEN GOTO 1700
1800 REM " --- Done with options"
1810 LET MORE$[PAGE,X1,0]=STR(HEIGHT-2),LLIST$[PAGE,X1,5]=SNAME$
1820 LET X1=X1+1,X0=X0+1,L=L+1
1900 REM " --- Loop back for next printer"
1990 GOTO 1100
2000 REM " --- Display last page"
2010 IF X0=0 THEN GOTO 9900
2020 LET XMODE=45,XNAME$=NAME$,SELECTION$=SELECTION$+"|EOF"
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 1000
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$="PRIOR" OR SELECTION$="EXIT" OR SELECTION$="STARTOVER" THEN GOTO 9900
2090 IF SELECTION$="" THEN GOTO 9900
2100 REM " --- Switch to options window"
2110 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,HEIGHT-2]
2120 LET F0$(13,2)=SELECTION$,PAGE=1,L=1,X0=0,X1=1,SELECTION$=F0$(25,1)
2130 CALL "SYC.WB",SNAME$,0
2190 READ (SYM17_DEV,KEY=F0$(13,2),DOM=2200)
2200 REM " --- Read next option"
2210 LET K$=KEY(SYM17_DEV,END=2500)
2220 IF POS(F0$(13,2)=K$)<>1 THEN GOTO 2500
2230 READ (SYM17_DEV)IOL=SYM17A
2300 REM " --- Display page?"
2310 IF L<MAX_ROW THEN GOTO 2400
2320 LET XMODE=5,XNAME$=SNAME$
2330 GOSUB 6200
2340 IF FKEY=1 THEN GOTO 1000
2350 IF FKEY=4 THEN GOTO 9900
2360 IF SELECTION$="PRIOR" THEN GOTO 1000
2370 IF SELECTION$="EXIT" OR SELECTION$="STARTOVER" THEN GOTO 9900
2390 IF SELECTION$<>"" THEN GOTO 2600
2400 REM " --- Options window record"
2410 DIM G1$(WIDTH)
2420 LET G1$(2)=B0$(3),G1$(4)=B1$,LLIST$[PAGE,X1,1]=G1$
2430 LET LLIST$[PAGE,X1,2]=B0$(3),X1=X1+1,X0=X0+1,L=L+1
2490 GOTO 2200
2500 REM " --- Done with options"
2510 IF X0=0 THEN GOTO 3000
2520 LET XMODE=5,XNAME$=SNAME$,SELECTION$=SELECTION$+"|EOF"
2530 GOSUB 6200
2540 IF FKEY=1 THEN GOTO 1000
2550 IF FKEY=4 THEN GOTO 9900
2560 IF SELECTION$="PRIOR" THEN GOTO 1000
2565 IF SELECTION$="" THEN GOTO 9900
2570 IF SELECTION$="EXIT" THEN GOTO 9900
2575 IF SELECTION$="STARTOVER" THEN GOTO 9900
2600 REM 
2610 LET F0$(25,1)=SELECTION$
2690 GOTO 4000
3000 REM " --- No options"
3010 LET F0$(25,1)=""
4000 REM " --- All done"
4010 IF SOURCE$<>"" THEN GOTO 4050
4020 WRITE (SYS01_DEV,KEY=TERMINAL$)IOL=SYS01T
4030 GOTO 9900
4050 LET C2$(SELECTION,2)=F0$(13,2),OPTION$[1]=F0$(13,2)+F0$(25,1)
4060 WRITE (ARM07_DEV,KEY=C0$)IOL=ARM07A
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,XNAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$=""
9910 FOR X=1 TO FILES
9915 CLOSE (CHANNELS[X],ERR=9916)
9920 NEXT X
9925 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
9930 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9935 IF SOURCE$="" THEN LET OPTION$[1]="EXIT"
9950 EXIT 
9999 END

0010 REM "OPR - INVOICE HISTORY REPORT"
0020 REM "Program OPR.KA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ART-03",FILES$[2]="ART-13",FILES$[3]="ARM-01"
0135 LET FILES$[4]="IVM-01",FILES$[5]="ART-23",FILES$[6]="SYS-01"
0140 LET FILES$[7]="ARM-02",FILES$[8]="IVM-10",FILES$[9]="ARM-10"
0145 CALL "SYC.DA",1,1,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0155 LET ART03_DEV=CHANNELS[1],ART13_DEV=CHANNELS[2],ARM01_DEV=CHANNELS[3]
0160 LET IVM01_DEV=CHANNELS[4],ART23_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0165 LET ARM02_DEV=CHANNELS[7],IVM10_DEV=CHANNELS[8],ARM10_DEV=CHANNELS[9]
0200 REM " --- IOLISTS"
0210 ARM01A: IOLIST *,X0$
0215 ARM10F: IOLIST X2$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0290 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,I5$,M1$,M2$,M3$
0400 REM " --- Parameters "
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0425 LET N9$=N0$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N9$+"IV00",DOM=9800)IOL=IVS01A
0450 DIM P[2],I[4]
0460 FOR X=0 TO 3
0470 LET I[X]=NUM(I2$(X*2+1,2))
0480 NEXT X
0490 LET I[0]=NUM(I2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1)),P[0]=NUM(P2$(1,2))
0500 REM " --- Initialize Data "
0510 LET H0=0,NUMBER=1
0520 LET L9=30,L=0
0530 DIM J$(80,"-"),J1$(40," ")
0540 DIM A[5],W[7]
0550 LET M0=LEN(M0$)
0700 REM " --- Background "
0710 FOR X=4 TO 18 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(20,3),"Beginning Customer: ",@(23,5),"Ending Customer: ",@(16,7),"Beginning Invoice Date: ",@(19,9),"Ending Invoice Date: ",@(27,11),"Salesperson: ",@(25,13),"Selected Item: ",@(10,15),"Below Minimum Gross Profit %: ",'SF',
0800 REM " --- Print Options "
1000 REM " --- Beginning Customer "
1010 LET V0$="M",V1$="CE",V3$=M0$,V4$="Enter A Valid Customer Number (<F1>=First/<F3>=Lookup)",V0=6,V1=40,V2=3,I0=0
1015 IF S0$<>"" THEN LET V2$=S0$(1,P[0])
1020 GOSUB 7000
1030 IF V3=4 THEN GOTO 9900
1040 IF V3=3 THEN CALL "SYC.LC",V$; IF V3=4 THEN GOTO 1000
1050 IF V3=1 OR V$="" THEN PRINT @(V1,V2),"First",'CL',; LET S0$=""; GOTO 1090
1060 FIND (ARM01_DEV,KEY=N0$+V$,DOM=1000)IOL=ARM01A
1070 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$),@(V1+M0+1,V2),X0$(1,30),
1075 LET H4$=FNF$(V$(1,P[0]),M0$)
1080 LET S0$=V$
1090 GOTO 3900
1200 REM " --- Ending Customer "
1210 LET V0$="M",V1$="CE",V3$=M0$,V4$="Enter A Valid Customer Number (<F1>=Last/<F3>=Lookup)",V0=6,V1=40,V2=5
1215 IF S1$<>"" THEN LET V2$=S1$(1,P[0])
1220 GOSUB 7000
1225 IF V3=2 OR V3=4 THEN GOTO 1000
1230 IF V3=3 THEN CALL "SYC.LC",V$; IF V3=4 THEN GOTO 1200
1240 IF V3=1 OR V$="" THEN PRINT @(V1,V2),"Last",'CL',; LET S1$=""; GOTO 1290
1255 IF V$<S0$ THEN GOTO 1200
1260 FIND (ARM01_DEV,KEY=N0$+V$,DOM=1200)IOL=ARM01A
1270 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$),@(V1+M0+1,V2),X0$(1,30),
1275 LET H5$=FNF$(V$(1,P[0]),M0$)
1280 LET S1$=V$
1290 GOTO 3900
1400 REM " --- Begining Invoice Date "
1410 LET V0$="D",V1$="C",V2$=S2$,V3$="",V4$="<F1>=First",V0=7,V1=40,V2=7
1430 GOSUB 7000
1440 IF V3=1 OR V$="" THEN PRINT @(V1,V2),'CL',"First"; LET S2$=""; GOTO 1490
1460 LET S2$=V$,H6$=FNB$(V$)
1490 GOTO 3900
1600 REM " --- Ending Invoice Date "
1610 LET V0$="D",V1$="C",V2$=S3$,V3$="",V4$="<F1>=Last",V0=7,V1=40,V2=9
1630 GOSUB 7000
1640 IF V3=1 OR V$="" THEN PRINT @(V1,V2),'CL',"Last"; LET S3$=""; GOTO 1690
1650 IF V$<S2$ THEN GOTO 1600
1660 LET S3$=V$,H7$=FNB$(V$)
1690 GOTO 3900
1800 REM " --- Salesperson "
1805 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
1807 LET DESCRIPTION$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3
1810 LET DESCRIPTION$[1]="Name",FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
1815 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,RECORD$="F",TITLE$="Salesperson Lookup"
1820 LET V0$="S",V1$="C",V2$=S4$,V3$="",V4$="Enter A Valid Salesperson Code (<F1>=All/<F3>=Lookup)",V0=3,V1=40,V2=11
1830 GOSUB 7000
1831 IF V3<>3 THEN GOTO 1840
1838 CALL "SYC.LK",ARM10_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
1840 IF V3=4 OR V3=2 THEN GOTO 1890
1845 IF V3=1 THEN LET V$=""
1850 IF V$="" THEN PRINT @(V1,V2),"All",'CL'; GOTO 1880
1855 LET V$=V$+"   ",V$=V$(1,3)
1860 FIND (ARM10_DEV,KEY=N0$+"F"+V$,DOM=1800)IOL=ARM10F
1870 PRINT @(V1,V2),X2$(POSITION[0],LENGTH[0]),@(V1+4,V2),X2$(7,20)
1875 LET H8$=V$
1880 LET S4$=V$
1890 GOTO 3900
2000 REM " --- Selected Item "
2020 LET V0$="S",V1$="C",V2$=S5$,V3$="",V4$="Enter An Item To Be Searched  (<F1>=Include All/<F3>=Lookup)",V0=I[0],V1=40,V2=13
2030 PRINT @(V1,V2),'CL',
2040 GOSUB 7000
2042 IF V3=3 THEN CALL "SYC.LI",V$; IF V3=4 THEN GOTO 2000 ELSE PRINT @(V1,V2),V$,
2060 IF V3=1 OR V$="" THEN PRINT @(V1,V2),'CL',"All"; GOTO 2080
2070 LET V$=V$+J1$(1,I[0]),V$=V$(1,I[0])
2080 LET S5$=V$
2085 IF S5$<>"" THEN LET H9$=S5$
2090 GOTO 3900
2200 REM " --- Below Minimum GP% "
2220 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Enter The Minimum Gross Profit Percentage (<F1>=100)",V0=7,V1=40,V2=15
2225 PRINT @(V1,V2),'CL',
2230 GOSUB 7000
2240 IF V3=1 THEN LET V$=""
2250 IF V$="" THEN LET V$="100"
2260 LET S6=NUM(V$,ERR=2200)
2270 PRINT @(V1,V2),S6:"-###.00%"
2290 GOTO 3900
3900 REM " --- New Field "
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,2200,4000
4000 REM " --- Correct? "
4020 LET V4$="Is The Above Information Correct",V0=1,V1=FNV(V4$),V2=22
4025 CALL "SYC.YN",1,V4$,2,V$,V3
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4110 CALL "SYC.NB","Printing:",15,COLUMN
4190 RUN "OPR.KB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

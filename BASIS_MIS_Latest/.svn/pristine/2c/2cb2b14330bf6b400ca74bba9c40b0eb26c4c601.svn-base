0010 REM "OPR - Sales Commission Report"
0020 REM "Program OPR.MA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "13Jan99 JAL: Ending salesperson does not handle short codes (1465)"
0040 REM 
0050 BEGIN 
0060 PRECISION 4
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES=8
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ARM-10",FILES$[3]="ART-01"
0130 LET FILES$[4]="ART-05",FILES$[5]="ART-07",FILES$[6]="ART-11"
0135 LET FILES$[7]="ARW-09",FILES$[8]="SYS-01"
0150 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BARCH,STATUS
0155 IF STATUS>0 THEN GOTO 9900
0160 LET ARM01_DEV=CHANNELS[1],ARM10_DEV=CHANNELS[2],ART01_DEV=CHANNELS[3]
0165 LET ART05_DEV=CHANNELS[4],ART07_DEV=CHANNELS[5],ART11_DEV=CHANNELS[6]
0170 LET ARW09_DEV=CHANNELS[7],SYS01_DEV=CHANNELS[8]
0200 REM " --- IOLISTS"
0250 ARM10F: IOLIST X0$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters "
0405 DIM P[2]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0450 LET P[0]=NUM(P2$(1,2))
0500 REM " --- Initialize Data "
0510 DIM J1$(80," ")
0550 LET M4$="-##0.00%",M0=LEN(M0$),M1=LEN(M1$),M3=LEN(M3$),M4=LEN(M4$)
0700 REM " --- Background "
0710 FOR X=4 TO 14 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(24,3),"Beginning Date:",@(27,5),"Ending Date:",@(17,7),"Beginning Salesperson:",@(20,9),"Ending Salesperson:",@(6,11),"Commission Based On Sales/Profit:",@(32,13),"Option:",'SF',
0800 REM " --- Options "
1000 REM " --- From Date "
1010 PRINT 'CF'
1020 LET V0$="D",V1$="C",V2$=P8$,V4$="Enter The Starting Date (<F1>=First Date)",V0=6,V1=40,V2=3,I0=0
1030 GOSUB 7000
1040 IF V3=4 THEN GOTO 9900
1060 IF V3=1 THEN LET V$=""
1070 LET P8$=V$
1080 IF P8$="" THEN PRINT @(40,3),"First",'CL',
1090 GOTO 3900
1200 REM " --- Through Date "
1220 LET V0$="D",V1$="C",V2$=P9$,V4$="Enter The Ending Date (<F1>=Last Date)",V0=6,V1=40,V2=5
1230 GOSUB 7000
1240 IF V3=2 THEN GOTO 3900
1250 IF V3=1 THEN LET V$=""
1260 LET P9$=V$
1270 IF P9$="" THEN PRINT @(40,5),"Last",'CL',
1290 GOTO 3900
1300 REM " --- Beginning Salesperson "
1320 LET V0$="S",V1$="C",V2$=S0$,V3$="",V4$="Enter A Valid Salesperson Code (<F1>=First/<F3>=Lookup)",V0=3,V1=40,V2=7,X3$=""
1325 PRINT @(V1+3,V2),"  "
1330 GOSUB 7000
1335 IF V3=2 THEN GOTO 3900
1342 IF V3<>3 THEN GOTO 1350
1344 GOSUB 6100
1346 IF V$="" THEN GOTO 1300
1350 IF V3=1 THEN LET V$=""
1360 IF V$="" THEN PRINT @(V1,V2),'CL',"First"; GOTO 1390
1365 LET V$=V$+J1$,V$=V$(1,3)
1370 FIND (ARM10_DEV,KEY=N0$+"F"+V$,DOM=1300)IOL=ARM10F
1380 PRINT @(V1,V2),'CL',X0$(4,3)," ",X0$(7,20)
1385 LET X3$=X0$(7,20)
1390 LET S0$=V$
1395 GOTO 3900
1400 REM " --- Ending Salesperson "
1420 LET V0$="S",V1$="C",V2$=S1$,V3$="",V4$="Enter A Valid Salesperson Code (<F1>=Last/<F3>=Lookup)",V0=3,V1=40,V2=9,X4$=""
1430 GOSUB 7000
1435 IF V3=2 THEN GOTO 3900
1442 IF V3<>3 THEN GOTO 1450
1444 GOSUB 6100
1446 IF V$="" THEN GOTO 1400
1450 IF V3=1 THEN LET V$=""
1460 IF V$="" THEN PRINT @(V1,V2),'CL',"Last"; GOTO 1490
1465 LET V$=V$+J1$,V$=V$(1,3)
1470 FIND (ARM10_DEV,KEY=N0$+"F"+V$,DOM=1400)IOL=ARM10F
1480 PRINT @(V1,V2),'CL',X0$(4,3)," ",X0$(7,20)
1485 LET X4$=X0$(7,20)
1490 LET S1$=V$
1495 GOTO 3900
1600 REM " --- Sales/Gross Profit "
1620 LET V0$="S",V1$="C",V2$=S2$,V3$="",V4$="S=Gross Sales     P=Gross Profit",V0=1,V1=40,V2=11
1630 IF V2$="" THEN LET V2$="S"
1640 GOSUB 7000
1650 IF V3=2 THEN GOTO 3900
1660 IF POS(V$="SP")=0 THEN GOTO 1600
1680 LET S2$=V$
1690 GOTO 3900
1800 REM " --- Options "
1820 LET V0$="S",V1$="CR",V2$=P7$,V3$="BP",V4$="B=Booked Invoices     P=Paid Invoice",V0=1,V1=40,V2=13
1830 IF V2$="" THEN LET V2$="B"
1840 GOSUB 7000
1850 IF V3=2 THEN GOTO 3900
1855 IF POS(V$="BP")=0 THEN GOTO 1800
1860 LET P7$=V$
1890 GOTO 3900
3900 REM " --- Next Field "
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1300,1400,1600,1800,4000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct"
4025 CALL "SYC.YN",1,V4$,2,V$,V3
4040 IF V3=4 THEN GOTO 9900
4080 ON POS(V$="YN") GOTO 4000,4090,1000
4090 RUN "OPR.MB"
6100 REM " --- Salesperson Lookup"
6110 DIM DESCRIPTION$[1],LENGTH[1],POSITION[1],FIELD[1]
6120 LET NUMBER=1,TITLE$="Salesperson Lookup",RECORD$="F"
6130 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Salesperson"
6140 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3,V$="SKIP"
6150 LET FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20,CHANNEL=ARM10_DEV
6160 LET ROW=V2+1,COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6170 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "GLU - Close Fiscal Period"
0020 REM "Program GLU.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=1
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0150 LET FILES$[1]="SYS-01"
0160 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0190 LET SYS01_DEV=CHANNELS[1]
0200 REM " --- IOLIST's"
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 GLS01A: IOLIST P0$,P1$,P2$(1),P3$(1),P4$(1),M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 DIM P2$(10),P3$(325),P4$(32),P[4]
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N3$=F5$
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0450 LET M0=LEN(M0$),MP=13
0480 FOR X=0 TO 4
0485 LET P[X]=NUM(P2$(X*2+1,2),ERR=0490)
0490 NEXT X
0495 LET P[2]=FNYY_YEAR(P2$(5,2))
0500 REM " --- Initializations"
0510 DIM JN$(MP,"N"),J1$(MP*3)
0550 LET PERIOD=P[1],YEAR=P[2],M4$="00"
0570 LET PERIOD$=STR(PERIOD:"00"),YEAR$=FNYEAR_YY21$(YEAR)
0580 LET DESCRIPTION$=P3$((PERIOD-1)*16+27,16)
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,6,0
0730 PRINT 'SB',@(32,3),"Period:",@(34,5),"Year:",'SF',
0740 PRINT @(40,3),PERIOD$," ",DESCRIPTION$,@(40,5),FNYY21_YY$(YEAR$),
0900 REM " --- All transaction files empty?"
0910 CALL "GLC.IA",N0$,STATUS
0920 IF STATUS<>0 THEN GOTO 9900
1000 REM " --- Period"
1010 LET V0$="N",V1$="C",V2$=PERIOD$,V3$=M4$,V0=2,V1=40,V2=3
1020 LET V4$="Enter The Fiscal Period To Be Closed (1 Through "+STR(P[0])+")"
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1000,9900
1050 IF V<1 OR V>P[0] THEN GOTO 1000
1070 LET PERIOD$=STR(V:M4$),PERIOD=V,DESCRIPTION$=P3$((V-1)*16+27,16)
1080 PRINT @(V1,V2),PERIOD$," ",DESCRIPTION$,
1200 REM " --- Year"
1210 LET V0$="C",V1$="CY",V2$=YEAR$,V3$="",V0=2,V1=40,V2=5
1220 LET V4$="Enter The Fiscal Year For The Period To Be Closed"
1230 GOSUB 7000
1240 ON V3 GOTO 1250,1200,1000,1200,1250
1250 IF V$="" THEN GOTO 1200
1260 LET YEAR$=V$,YEAR=FNYY_YEAR(YEAR$)
1270 PRINT @(V1,V2),FNYY21_YY$(YEAR$),
2000 REM " --- Valid year?"
2010 IF YEAR=P[2] THEN GOTO 2100
2020 IF YEAR>P[2] THEN GOTO 2300
2030 LET V4$="Year"
2040 IF YEAR=P[2]-1 THEN IF P4$(6,1)="N" THEN LET V4$="Period"
2080 LET V4$="This Fiscal "+V4$+" Has Already Been Closed"
2090 GOTO 3900
2100 REM " --- Valid period?"
2110 IF PERIOD>P[1] THEN GOTO 2300
2120 IF P3$(273+PERIOD,1)<>"Y" THEN GOTO 2200
2130 LET V4$="This Period Is Already Closed Or Locked"
2190 GOTO 3900
2200 REM " --- Closing last period?"
2210 IF PERIOD<>P[0] THEN GOTO 4000
2220 IF P4$(6,1)="Y" THEN GOTO 4000
2230 LET V4$="Fiscal "+STR(1900+P[2]-1)+" Must Be Closed First"
2290 GOTO 3900
2300 REM " --- Future"
2310 LET V4$="Future Periods May Not Be Closed"
3900 REM " --- Display Warning/Error"
3910 DIM MESSAGE$[1]
3920 LET MESSAGE$[0]=V4$+" (<Enter>=Continue)"
3930 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
3990 GOTO 1000
4000 REM " --- Correct?"
4010 LET V4$="OK To Close This Fiscal Period"
4020 CALL "SYC.YN",0,V4$,0,V$,V3
4030 IF V3=4 THEN GOTO 9900
4040 IF V$<>"YES" THEN GOTO 1000
4050 REM " --- Disallow 'M'enu option in Error Routine
4060 LET EXIT_CTRL=1
4100 REM " --- Close period"
4110 FIND (SYS01_DEV,KEY=P0$)IOL=GLS01A
4120 LET P3$(273+PERIOD,1)="Y",P3$((PERIOD-1)*3+287,3)=F0$(7,3)
4130 IF PERIOD=P[0] THEN GOTO 4200
4140 IF PERIOD=P[1] THEN LET PERIOD=PERIOD+1,P2$(3,2)=STR(PERIOD:"00")
4190 GOTO 4300
4200 REM " --- Closing last period of year"
4210 LET P3$(274,MP)=JN$,P3$(287,MP*3)=J1$(1,MP*3),P4$(6,1)="N"
4220 LET P2$(3,2)="01",P[2]=P[2]+1,P2$(5,2)=FNYEAR_YY21$(P[2])
4300 REM " --- Update fiscal calendar"
4310 WRITE (SYS01_DEV,KEY=P0$)IOL=GLS01A
4390 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "SYC - Days Between Dates/New Date From Date And # Of Days"
0020 REM "Program SYC.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "23Sep99 WGH: Improve date validation checking (600-640)
0040 REM 
0042 REM " --- Returns number of days between BEGDATE$ and ENDDATE$ if"
0044 REM " --- both are passed -OR- returns ENDDATE$ equal to BEGDATE$"
0046 REM " --- plus DIFFERENCE if both are passed"
0048 REM 
0050 REM "BEGDATE$  : Starting date YYMMDD or packed YMD (Input)"
0052 REM "ENDDATE$  : Ending date YYMMDD or packed YMD (Input/Returned)"
0054 REM "            or BEGDATE$ + DIFFERENCE (Returned)"
0056 REM "DIFFERENCE: Number of days to add to BEGDATE$ (Input)"
0058 REM "            or number of days between BEGDATE$/ENDDATE$ (Returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER BEGDATE$,ENDDATE$,DIFFERENCE
0500 REM " --- Initialize Data"
0550 LET V0$=BEGDATE$,V1$=ENDDATE$,DAYS=DIFFERENCE
0560 LET BEGDATE$="N",ENDDATE$="N",DIFFERENCE=0
0570 LET DAYS=INT(DAYS),BEGLEN=0,ENDLEN=0
0600 REM " --- Validate Input Formate"
0610 IF LEN(V0$)<>3 AND LEN(V0$)<>6 THEN GOTO 9900
0620 IF LEN(V1$)>0 AND LEN(V1$)<>3 AND LEN(V1$)<>6 THEN GOTO 9900
0630 IF LEN(V0$)=6 AND POS(V0$(1,1)=" 0123456789ABCDEFGHIJ")=0 THEN GOTO 9900
0640 IF LEN(V1$)=6 AND POS(V1$(1,1)=" 0123456789ABCDEFGHIJ")=0 THEN GOTO 9900
0800 REM " --- Standardize Date Formate To YYMMDD"
0810 IF LEN(V0$)=3 THEN LET BEGLEN=3,V0$=FNYY_YY21$(FNM$(FNC$(V0$))) ELSE LET V0$=FNYY_YY21$(V0$)
0820 IF LEN(V1$)=3 THEN LET ENDLEN=3,V1$=FNYY_YY21$(FNM$(FNC$(V1$)))
0830 IF LEN(V1$)=6 THEN LET V1$=FNYY_YY21$(V1$)
0840 IF V1$="" AND BEGLEN=3 THEN LET ENDLEN=3
1000 REM " --- Check Validity Of Dates"
1020 LET WHEN$=V0$
1030 GOSUB 6000
1040 IF STATUS THEN GOTO 9900
1060 IF V1$="" THEN GOTO 1200
1070 LET WHEN$=V1$
1080 GOSUB 6000
1090 IF STATUS THEN GOTO 9900
1200 REM " --- Branch"
1210 IF DAYS<>0 THEN GOTO 3000
1220 IF LEN(V1$)=0 THEN GOTO 3000
2000 REM " --- Days Between Dates"
2010 LET WHEN$=V0$
2020 GOSUB 6000
2030 LET DAYS=JULIAN
2040 IF V1$="" THEN GOTO 4000
2050 LET WHEN$=V1$
2060 GOSUB 6000
2070 LET DAYS=ABS(JULIAN-DAYS)
2090 GOTO 4000
3000 REM " --- Calculate New Date As YYMMDD"
3010 LET Y=1900+FNYY_YEAR(V0$(1,2)),M=NUM(V0$(3,2)),D=NUM(V0$(5,2))
3020 LET V1$=DATE(JUL(Y,M,D)+DAYS:"%Yz%Mz%Dz")
4000 REM " --- Done - Successful"
4010 IF BEGLEN=3 THEN LET V0$=FND$(FNN$(V0$)) ELSE LET V0$=FNYY_YY21$(V0$)
4020 IF ENDLEN=3 THEN LET V1$=FND$(FNN$(V1$)) ELSE LET V1$=FNYY_YY21$(V1$)
4030 LET BEGDATE$=V0$,ENDDATE$=V1$,DIFFERENCE=DAYS
4090 GOTO 9900
6000 REM " --- Convert YYMMDD to Julian date"
6010 LET STATUS=99,JULIAN=0
6020 IF LEN(WHEN$)<>6 THEN GOTO 6090
6030 LET Y=1900+FNYY_YEAR(WHEN$(1,2)),M=NUM(WHEN$(3,2)),D=NUM(WHEN$(5,2))
6040 LET JULIAN=JUL(Y,M,D,ERR=6090),STATUS=0
6090 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8080 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8090 DEF FNN$(Q$)=Q$(3,4)+Q$(1,2)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9950 EXIT 
9999 END

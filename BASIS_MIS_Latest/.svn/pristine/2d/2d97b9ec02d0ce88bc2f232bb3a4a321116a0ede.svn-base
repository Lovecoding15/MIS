0010 REM "GLR - Account/Financial Report Xref"
0020 REM "Program GLR.GB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "11Jun98 WGH: May not print the last Chart Of Accounts break (1510-1520)"
0032 REM "07May99 WGH: Don't change Assignment Numbers to fill gaps in the sequence (2020,2050,2070-2080,2120,2150,2170-2180)"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$(1),A1$(1)
0220 GLM11A: IOLIST B0$(1),B1$(1)
0230 GLM21A: IOLIST C0$(1),C1$(1)
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY=N0$+N2$+"01",DOM=9800)
0420 CALL "GLC.RG",GLM10_DEV,N0$,BREAK$,DESC$
0500 REM " --- Initializations"
0510 DIM A0$(2+P[4]),A1$(40),B0$(14),B1$(60),C0$(14),C1$(60),O[6]
0550 LET M=MAX(8,M0)
0600 REM " --- Print positions"
0610 LET O[0]=5,O[1]=O[0]+M+1,O[6]=H0-13
0620 FOR X=5 TO 2 STEP -1
0630 LET O[X]=O[X+1]-15
0640 NEXT X
0670 LET WIDTH=O[2]-O[1]
0680 IF WIDTH>35 THEN LET WIDTH=35
0700 REM " --- Background"
0750 CALL "SYC.NB","Printing",M0,COLUMN
0900 REM " --- Position file"
0990 READ (GLM01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next GLM-01 record"
1010 LET K$=KEY(GLM01_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 READ (GLM01_DEV)IOL=GLM01A
1040 PRINT @(COLUMN,11),FNF$(A0$(3,P[3]),M0$)
1200 REM " --- Chart of Account Break?"
1210 IF BREAK$<>"" THEN IF A0$(3,P[4])<BREAK$(1,P[4]) THEN GOTO 2000
1220 IF BREAK$="" THEN GOTO 2000
1400 REM " --- Print subtotal and new heading"
1420 LET TITLE$=FNP$(DESC$(1,20))
1430 IF L+4>L9 THEN GOTO 1490
1440 GOSUB 5200
1450 GOTO 1500
1490 GOSUB 5000
1500 REM " --- Strip current heading"
1510 IF LEN(BREAK$)>=P[4] THEN LET BREAK$=BREAK$(P[4]+1),DESC$=DESC$(21)
1520 IF LEN(BREAK$)<P[4] THEN LET BREAK$="",DESC$=""
1530 IF BREAK$<>"" THEN IF POS(" "<>BREAK$(1,P[4]))=0 THEN GOTO 1500
1590 GOTO 1200
2000 REM " --- Format print line"
2005 IF CONSOLIDATED$="Y" THEN GOTO CONSOLIDATED
2010 DIM BUF$[10]
2030 READ (GLM11_DEV,KEY=A0$,DOM=2040)
2040 LET K11$=KEY(GLM11_DEV,END=2500)
2050 IF POS(A0$=K11$(1,12))<>1 THEN GOTO 2500
2060 READ (GLM11_DEV,KEY=K11$)IOL=GLM11A
2070 IF FNP$(B1$(1,6))<>"" THEN LET BUF$[NUM(B0$(13,2))]=B1$(1,2)+"-"+B1$(3,4)
2090 GOTO 2040
2100 REM " --- Format print line: Consolidated"
2105 CONSOLIDATED: 
2110 DIM BUF$[10]
2130 READ (GLM21_DEV,KEY=A0$,DOM=2140)
2140 LET K21$=KEY(GLM21_DEV,END=2500)
2150 IF POS(A0$=K21$(1,12))<>1 THEN GOTO 2500
2160 READ (GLM21_DEV,KEY=K21$)IOL=GLM21A
2170 IF FNP$(C1$(1,6))<>"" THEN LET BUF$[NUM(C0$(13,2))]=C1$(1,2)+"-"+C1$(3,4)
2190 GOTO 2140
2500 REM " --- Print detail lines"
2510 IF L+2>L9 THEN GOSUB 5000
2520 PRINT (7)@(O[0]),FNF$(A0$(3,P[3]),M0$),@(O[1]),A1$(1,WIDTH),@(O[2]),BUF$[1],@(O[3]),BUF$[2],@(O[4]),BUF$[3],@(O[5]),BUF$[4],@(O[6]),BUF$[5]
2530 PRINT (7)@(O[2]),BUF$[6],@(O[3]),BUF$[7],@(O[4]),BUF$[8],@(O[5]),BUF$[9],@(O[6]),BUF$[10]
2590 LET L=L+2
3900 REM " --- Loop back for next GLM-01 record"
3990 GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)""
5050 PRINT (7)@(O[2]),"Assignment 1",@(O[3]),"Assignment 2",@(O[4]),"Assignment 3",@(O[5]),"Assignment 4",@(O[6]),"Assignment 5"
5060 PRINT (7)@(O[0]),"Account",@(O[1]),"Description",@(O[2]),"Assignment 6",@(O[3]),"Assignment 7",@(O[4]),"Assignment 8",@(O[5]),"Assignment 9",@(O[6]),"Assignment 10"
5070 LET L=L+3
5200 REM " --- Subheading"
5220 PRINT (7)""
5230 PRINT (7)TITLE$
5240 PRINT (7)""
5250 LET L=L+3
5290 RETURN 
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

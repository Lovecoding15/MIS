0010 REM "ARM - Customer Maintenance (Profile Maintenance)"
0020 REM "Program ARM.MC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0220 ARM02A: IOLIST B0$,B1$(1),B[ALL]
0230 ARM04B: IOLIST C0$
0240 ARM04C: IOLIST C0$
0250 ARM04F: IOLIST C0$
0260 ARM04G: IOLIST C0$
0270 ARM04H: IOLIST C0$
0280 ARM04J: IOLIST C0$
0290 ARM04K: IOLIST C0$
0300 ARM10A: IOLIST X0$
0310 ARM10B: IOLIST X0$
0340 ARM10D: IOLIST X0$
0360 ARM10F: IOLIST X0$
0370 ARM10G: IOLIST X0$
0380 ARM10H: IOLIST X0$
0385 ARM10I: IOLIST X0$,X0
0390 ARM10L: IOLIST X0$
0395 ARM10M: IOLIST X$
0700 REM " --- Background"
0711 PRINT @(0,6),'CE'
0716 PRINT 'SB',@(15,5),"Customer Profile",'SF'
0720 PRINT 'SB',@(3,6),"Slsprsn No:",@(3,7),"Terms Code:",@(3,8)," Territory:",@(4,9),"Dist Code:",@(0,10),"  Std Message:",@(48,6),"Finance Charge?:",@(48,7),"Sales Analysis?:",@(48,8),"    Statements?:",@(48,9),"   Pricing Code:",@(53,10),"Label Code:",@(5,12),"Tax Code:",@(51,12),"Credit Limit:",@(0,11),"Sale Discount:",@(50,11),"Customer Type:",
0730 IF OP$<>"Y" THEN GOTO 0750
0735 IF P3$(77,1)<>"Y" THEN GOTO 0750
0740 PRINT @(33,13),"Retain Invoice Detail History?:",
0750 IF C9$(7,1)<>"Y" THEN GOTO 0770
0760 PRINT @(2,13),"Credit Hold: ",
0790 PRINT 'SF',
1000 REM " --- Find Record"
1010 DIM B[10],B1$(64),SAVEB1$(64)
1020 LET B0$=A0$+"  ",B1$(1,3)=P3$(55,3),B1$(4,2)=P3$(60,2),B1$(6,2)=P3$(50,2),B1$(10,2)="YI",B1$(20,1)="Y",B1$(21,3)=P3$(52,3),B1$(32,2)=P3$(58,2),B1$(38,1)=P3$(78,1),B1$(8,2)=P3$(62,2)
1030 IF C9$(7,1)="Y" AND C9$(25,1)="Y" THEN LET B1$(39,1)="Y"
1040 EXTRACT (ARM02_DEV,KEY=B0$,DOM=1050)IOL=ARM02A; GOTO 1060
1050 GOSUB 5000; GOTO 2000
1060 LET SAVEB1$(1)=B1$
1070 GOSUB 5000; GOTO 4000
1800 REM " --- Initialize"
1810 LET O0=0
2000 REM " --- Salesperson"
2020 LET V0$="S",V1$="KCR",V2$=B1$(1,3),V0=3,V1=15,V2=6,I0=0,V4$="Enter A Valid Salesperson Code (<F3>=Lookup)"
2030 GOSUB 7000
2032 IF V3<>3 THEN GOTO 2038
2034 LET V4=1,V5=ARM10_DEV,V6=4,V7=3,V8=7,V9=20,V5$="F"
2035 IF V3=4 THEN GOTO 4000
2036 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
2038 IF V3=4 THEN GOTO 4000
2040 LET V$=V$+FILL(3-LEN(V$))
2050 FIND (ARM10_DEV,KEY=N0$+"F"+V$,DOM=2000)IOL=ARM10F
2055 LET B1$(1,3)=V$
2060 PRINT @(V1+4,V2),X0$(7,20),
2090 GOTO 3900
2100 REM " --- Terms Code"
2120 LET V0$="S",V1$="KCR",V2$=B1$(4,2),V0=2,V1=15,V2=7,V4$="Enter A Valid Terms Code (<F3>=Lookup)"
2130 GOSUB 7000
2132 IF V3<>3 THEN GOTO 2140
2134 LET V4=1,V5=ARM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="A"
2136 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
2140 LET B1$(4,2)=V$
2150 FIND (ARM10_DEV,KEY=N0$+"A"+B1$(4,2),DOM=2100)IOL=ARM10A
2160 PRINT @(V1+3,V2),X0$(6,20),
2170 IF C9$(7,1)="Y" AND X0$(27,1)="Y" THEN LET B1$(39,1)="Y"
2190 GOTO 3900
2200 REM " --- Territory"
2220 LET V0$="S",V1$="KCR",V2$=B1$(21,3),V0=3,V1=15,V2=8,V4$="Enter A Valid Territory Code (<F3>=Lookup)"
2230 GOSUB 7000
2232 IF V3<>3 THEN GOTO 2240
2233 LET V$=STR(NUM(V$,ERR=2234):J0$(1,V0))
2234 LET V4=1,V5=ARM10_DEV,V6=4,V7=3,V8=7,V9=20,V5$="H"
2236 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
2240 LET B1$(21,3)=V$
2250 FIND (ARM10_DEV,KEY=N0$+"H"+B1$(21,3),DOM=2200)IOL=ARM10H
2260 PRINT @(V1+4,V2),X0$(7,20),
2290 GOTO 3900
2300 REM " --- Dist Code"
2320 LET V0$="S",V1$="KRC",V2$=B1$(8,2),V0=2,V1=15,V2=9,V4$="Enter A Valid Distribution Code (<F3>=Lookup)"
2330 GOSUB 7000
2331 IF V3=4 AND B1$(8,2)="  " THEN GOSUB 6000
2332 IF V3<>3 THEN GOTO 2340
2334 LET V4=1,V5=ARM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="D"
2336 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
2340 LET B1$(8,2)=V$
2350 FIND (ARM10_DEV,KEY=N0$+"D"+B1$(8,2),DOM=2300)IOL=ARM10D
2360 PRINT @(V1,V2),B1$(8,2),"  ",X0$(6,20),
2390 GOTO 3900
2400 REM " --- Fin Chg"
2420 LET V0$="Y",V2$=B1$(10,1),V3$="",V0=1,V1=65,V2=6,V4$="Y=Yes    N=No"
2440 GOSUB 7000
2460 LET B1$(10,1)=V$
2490 GOTO 3900
2500 REM " --- Sales Analysis"
2505 IF SA$="Y" THEN GOTO 2520
2510 LET B1$(11,1)="N"
2515 PRINT @(65,15),"N",
2518 GOTO 3900
2520 LET V0$="S",V2$=B1$(11,1),V0=1,V1=65,V2=7,V4$="P=Product Level    I=Item Level    N=No Sales Analysis",V1$="R",V3$="PNI"
2540 GOSUB 7000
2560 LET B1$(11,1)=V$
2590 GOTO 3900
2600 REM " --- Sale Discount"
2620 LET V0$="S",V1$="KCR",V2$=B1$(6,2),V0=2,V1=15,V2=11,V4$="Enter A Valid Sale Discount Code (<F3>=Lookup)"
2630 GOSUB 7000
2632 IF V3<>3 THEN GOTO 2640
2634 LET V4=1,V5=ARM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="I"
2636 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
2640 LET B1$(6,2)=V$
2650 FIND (ARM10_DEV,KEY=N0$+"I"+B1$(6,2),DOM=2600)IOL=ARM10I
2660 PRINT @(V1+3,V2),X0$(6,20),
2690 GOTO 3900
2700 REM " --- Std Message"
2720 LET V0$="S",V1$="KCR",V2$=B1$(30,2),V0=2,V1=15,V2=10,V4$="Enter A Valid Standard Message Code (<F3>=Lookup)"
2730 GOSUB 7000
2732 IF V3=3 THEN CALL "ARC.LM",V$
2740 LET B1$(30,2)=V$
2745 IF B1$(30,2)="  " THEN GOTO 2790
2750 FIND (ARM10_DEV,KEY=N0$+"G"+B1$(30,2)+"1",DOM=2700)IOL=ARM10G
2760 PRINT @(V1,V2),B1$(30,2),@(V1+3,V2),X0$(7,20),
2790 GOTO 3900
2800 REM " --- Statements"
2820 LET V0$="Y",V2$=B1$(20,1),V0=1,V1=65,V2=8,V4$="Y=Yes      N=No",V1$="C"
2840 GOSUB 7000
2860 LET B1$(20,1)=V$
2890 GOTO 3900
2900 REM " --- Price Codes"
2920 LET V0$="S",V1$="C",V2$=B1$(24,4),V3$="",V4$="Enter Valid Price Code (<F1>=None/<F3>=Lookup)",V0=4,V1=65,V2=9
2940 GOSUB 7000
2945 IF V3=1 THEN LET V$=""
2950 IF V3=3 THEN CALL "SYC.LZ",V1,V2,1,ARM10_DEV,4,V0,8,20,V$,"M",N0$
2960 LET V$=V$+"    ",V$=V$(1,4); DIM X$(30); LET X$(8)="No Price Code"
2962 IF POS(" "<>V$)=0 THEN GOTO 2970
2965 FIND (ARM10_DEV,KEY=N0$+"M"+V$,DOM=2900)IOL=ARM10M
2970 LET B1$(24,4)=V$
2980 PRINT @(V1,V2),B1$(24,4)," ",X$(8,10)
2990 GOTO 3900
3000 REM " --- Labels"
3020 LET V0$="S",V1$="",V2$=B1$(28,2),V0=2,V1=65,V2=10
3030 GOSUB 7000
3060 LET B1$(28,2)=V$
3090 GOTO 3900
3100 REM " --- Credit Limit"
3110 IF C9$(7,1)="Y" THEN GOTO 3190
3120 LET V0$="N",V1$="C",V3$=M1$,V2$=STR(B[6]),V1=65,V2=12,V4$="Enter Zero ("+$22$+"0"+$22$+") If Limit Is Not Applicable"
3130 GOSUB 7000
3160 LET B[6]=V
3190 GOTO 3900
3200 REM " --- Tax Code"
3220 LET V0$="S",V1$="KCR",V2$=B1$(32,2),V0=2,V1=15,V2=12,V4$="Enter A Valid Tax Code (<F3>=Lookup)"
3230 GOSUB 7000
3232 IF V3<>3 THEN GOTO 3240
3234 LET V4=1,V5=ARM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="B"
3236 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
3240 LET B1$(32,2)=V$
3250 FIND (ARM10_DEV,KEY=N0$+"B"+B1$(32,2),DOM=3200)IOL=ARM10B
3260 PRINT @(V1+3,V2),X0$(6,20),
3290 GOTO 3900
3300 REM " --- Sales Analysis by Distribution Code"
3310 GOTO 3900
3320 LET V0$="Y",V1$="SR",V2$=B1$(37,1),V3$="",V4$="Y=Include S/A By Distribution Code   N=Omit S/A By Distribution Code",V0=1,V1=65,V2=11
3330 GOSUB 7000
3340 LET B1$(37,1)=V$
3350 GOTO 3900
3400 REM " --- Cust Type"
3420 LET V0$="S",V1$="KCR",V2$=B1$(34,3),V0=3,V1=65,V2=11,V4$="Enter A Valid Customer Type Code (<F3>=Lookup)"
3430 GOSUB 7000
3432 IF V3<>3 THEN GOTO 3440
3434 LET V4=1,V5=ARM10_DEV,V6=4,V7=3,V8=7,V9=20,V5$="L"
3436 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
3440 IF V3=4 THEN GOTO 4000
3450 LET B1$(34,3)=V$
3460 FIND (ARM10_DEV,KEY=N0$+"L"+B1$(34,3),DOM=3400)IOL=ARM10L
3470 PRINT @(V1+4,V2),X0$(7,10)
3490 GOTO 3900
3500 REM " --- Retain Invoice Detail History for this Customer?"
3505 IF P3$(77,1)="Y" THEN GOTO 3515
3510 LET B1$(38,1)=P3$(78,1); GOTO 3900
3515 IF POS(B1$(38,1)="YN")=0 THEN LET B1$(38,1)=P3$(78,1)
3520 LET V0$="S",V1$="C",V2$=B1$(38,1),V0=1,V1=65,V2=13,V4$="Retain Invoice Detail History For This Customer (Y/N)?"
3530 GOSUB 7000
3540 IF V$=" " THEN LET V$="N"; PRINT @(V1,V2),'CL',V$
3550 IF POS(V$="YN")=0 THEN GOTO 3500
3560 LET B1$(38,1)=V$
3590 GOTO 3900
3600 REM " --- Credit Hold"
3605 GOTO 3900; REM "Set this from Credit Mgr only
3610 IF C9$(7,1)<>"Y" THEN GOTO 3900
3615 IF B1$(39,1)=" " THEN LET B1$(39,1)="N"
3620 LET V0$="S",V2$=B1$(39,1),V3$="YNE",V0=1,V1=15,V2=13,V4$="Y=Yes, N=No, E=Exempt From Credit Hold"
3630 PRINT @(V1,V2),FILL(24)
3640 GOSUB 7000
3650 LET B1$(39,1)=V$
3660 IF B1$(39,1)="Y" THEN PRINT @(V1,V2),"***Credit Hold***" ELSE IF B1$(39,1)="E" THEN PRINT @(V1,V2),"Exempt From Credit Check" ELSE PRINT @(V1,V2),B1$(39,1)
3690 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 IF ABS(I0)>1000 THEN GOTO 3990
3960 ON I0 GOTO 2000,2100,2200,2300,2700,2600,3200,3600,2400,2500,2800,2900,3000,3400,3100,3500,3990
4000 REM " --- Options"
4010 GOSUB 6000
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/##/Restore)?",V0=7,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4027 LET I=NUM(V$,ERR=4030)
4028 IF I>0 THEN GOTO 4800
4040 IF V$="R" OR V$="RESTORE" THEN GOTO 4700
4050 ON POS(V$="YN") GOTO 4000,4100,1800
4100 REM " --- Write"
4101 IF O0=1 THEN GOTO 4195
4105 WRITE (ARM02_DEV,KEY=B0$)IOL=ARM02A
4110 GOSUB 6700
4115 LET C0$=N0$+"B"+B1$(1,3)+A0$(3); REM "Salesperson"
4120 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04B
4125 LET C0$=N0$+"C"+B1$(21,3)+A0$(3); REM "Territory"
4130 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04C
4135 LET C0$=N0$+"F"+B1$(4,2)+A0$(3); REM "Terms"
4140 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04F
4145 LET C0$=N0$+"G"+B1$(6,2)+A0$(3); REM "Discount"
4150 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04G
4155 LET C0$=N0$+"H"+B1$(8,2)+A0$(3); REM "Distrubution"
4160 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04H
4165 LET C0$=N0$+"J"+B1$(24,4)+A0$(3); REM "Pricing"
4170 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04J
4175 LET C0$=N0$+"K"+B1$(32,2)+A0$(3); REM "Tax"
4180 WRITE (ARM04_DEV,KEY=C0$)IOL=ARM04K
4190 LET O0=1
4195 RUN "ARM.MA"
4700 REM " --- Restore"
4720 FIND (ARM02_DEV,KEY=B0$,DOM=4020)IOL=ARM02A
4740 GOTO 0700
4800 REM " --- ##"
4810 LET O0=0
4820 LET I0=I-2
4890 GOTO 3900
5000 REM " --- Display"
5020 PRINT @(15,6),B1$(1,3),@(15,7),B1$(4,2),@(15,8),B1$(21,3),@(15,9),B1$(8,2),@(15,10),B1$(30,2),@(65,6),B1$(10,1),@(65,7),B1$(11,1),@(65,8),B1$(20,1),@(65,9),B1$(24,4),@(65,10),B1$(28,2),@(65,12),B[6]:M1$,@(15,11),B1$(6,2),@(65,11),B1$(34,3),@(15,12),B1$(32,2),
5030 IF P3$(77,1)="Y" THEN PRINT @(65,13),B1$(38,1),
5040 IF C9$(7,1)="Y" THEN IF B1$(39,1)="Y" THEN PRINT @(15,13),"***Credit Hold***" ELSE IF B1$(39,1)="E" THEN PRINT @(15,13),"Exempt From Credit Check" ELSE PRINT @(15,13),B1$(39,1)
5090 RETURN 
6000 REM " --- Verify Dist Code"
6010 FIND (ARM10_DEV,KEY=N0$+"D"+B1$(8,2),DOM=6030)IOL=ARM10D
6020 RETURN 
6030 LET TITLE=2
6035 LET V4$="A Valid Distribution Code Must Be Defined!"
6040 GOSUB SHOW_MESSAGE
6045 IF V3=4 THEN GOTO 6100
6050 LET I0=2
6070 EXITTO 3900
6100 REM " --- Verify Default Dist Code"
6105 DIM X$(30)
6110 READ (ARM10_DEV,KEY=N0$+"D",DOM=6120)
6120 LET X$=KEY(ARM10_DEV,ERR=6125)
6125 IF X$(3,1)="D" THEN DIM X$(30) ELSE GOTO 6200
6130 IF P3$(62,2)<>FILL(2) AND P3$(62,2)<>B1$(8,2) THEN LET B1$(8,2)=P3$(62,2)
6140 GOTO 6050
6200 REM " --- !!!  Whooops - No Distribution Codes in Masterfile"
6205 DIM MESSAGE$[1]
6210 LET MESSAGE$[0]="There Are No Distribution Codes Defined In The Masterfile!  Customer"
6220 LET MESSAGE$[1]="Must Be Re-Entered After Codes Are Defined (<Enter>=Continue)"
6225 LET V3=0
6230 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
6250 LET O0=3; REM "When O0=3, ARM.MB will delete the current customer"
6260 RUN "ARM.MB"
6700 REM " --- Remove X-References"
6730 REMOVE (ARM04_DEV,KEY=N0$+"B"+SAVEB1$(1,3)+A0$(3),DOM=6740); REM "Salesperson"
6740 REMOVE (ARM04_DEV,KEY=N0$+"C"+SAVEB1$(21,3)+A0$(3),DOM=6750); REM "Territory"
6750 REMOVE (ARM04_DEV,KEY=N0$+"F"+SAVEB1$(4,2)+A0$(3),DOM=6760); REM "Terms"
6760 REMOVE (ARM04_DEV,KEY=N0$+"G"+SAVEB1$(6,2)+A0$(3),DOM=6770); REM "Discount"
6770 REMOVE (ARM04_DEV,KEY=N0$+"H"+SAVEB1$(8,2)+A0$(3),DOM=6780); REM "Distrubution"
6780 REMOVE (ARM04_DEV,KEY=N0$+"J"+SAVEB1$(24,4)+A0$(3),DOM=6790); REM "Pricing"
6790 REMOVE (ARM04_DEV,KEY=N0$+"K"+SAVEB1$(32,2)+A0$(3),DOM=6795); REM "Tax"
6795 RETURN 
6900 REM " --- Standard Warning/Error"
6905 SHOW_MESSAGE: 
6910 DIM MESSAGE$[1]
6920 LET MESSAGE$[0]=V4$+" (<Enter>=Continue)"
6930 CALL "SYC.XA",TITLE,MESSAGE$[ALL],0,22,-1,V$,V3
6940 IF TITLE=1 THEN EXITTO 9900
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 LET O0=0; RUN "ARM.MA"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

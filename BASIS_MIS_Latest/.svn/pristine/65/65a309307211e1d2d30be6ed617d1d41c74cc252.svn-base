0010 REM "IVR - Inventory Stock Movement Report"
0020 REM "Program IVR.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=9
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0130 LET FILES$[4]="IVM-03",FILES$[5]="IVM-10",FILES$[6]="IVM-12"
0140 LET FILES$[7]="IVM-14",FILES$[8]="IVM-15",FILES$[9]="IVM-03"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0180 LET IVM03_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],IVM12_DEV=CHANNELS[6]
0190 LET IVM14_DEV=CHANNELS[7],IVM15_DEV=CHANNELS[8],IVM03_DEV2=CHANNELS[9]
0200 REM " --- IOLIST's"
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0275 IVS01C: IOLIST X$,ABCP[ALL],FACTORS[ALL]
0280 GLS01A: IOLIST G0$,G1$,G2$,G3$
0400 REM " --- Parameters"
0405 DIM P[7],G[1],ABCP[25],FACTORS[25]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="IV"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0425 FIND (SYS01_DEV,KEY=N0$+N2$+"02",DOM=9800)IOL=IVS01C
0430 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0435 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1)),G[0]=NUM(G2$(1,2))
0440 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0445 LET P[6]=NUM(I4$(1,2)),P[7]=FNYY_YEAR(I4$(3,2))
0450 LET PERIOD$=I4$(1,2),YEAR$=I4$(3,2),SUBTOTALS$="N"
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM FILES[15],PARAMS$[3],OPTIONS$[7]
0520 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A[12]
0550 LET FILES[1]=IVM01_DEV,FILES[10]=IVM10_DEV,FILES[12]=IVM12_DEV,FILES[14]=IVM14_DEV,FILES[15]=IVM15_DEV
0555 LET PARAMS$[0]=N0$,PARAMS$[1]=I1$,PARAMS$[2]=I2$,PARAMS$[3]=I3$
0600 REM " --- Resize masks"
0620 CALL "SYC.RM",M2$,M2,10,15
0700 REM " --- Background"
0710 PRINT 'SB',@(28,3),"For Period: ",'SF',PERIOD$," ",G3$(11+P[6]*16,16),'SB',@(30,4),"For Year: ",'SF',FNYY21_YY$(YEAR$),
0720 CALL "IVC.IB","BD","",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0730 PRINT 'SB',@(29,13),"Subtotals: ",'SF',"N",
1000 REM " --- For Period"
1010 LET V0$="Z",V1$="C",V2$=PERIOD$,V3$="",V4$="Enter Fiscal Period To Run Report For",V0=2,V1=40,V2=3,I0=0
1020 GOSUB 7000
1030 ON V3 GOTO 1040,1040,1090,1040,4000
1040 LET PERIOD$=V$
1050 PRINT @(43,3),G3$(11+NUM(V$)*16,16)
1090 GOTO 3900
1100 REM " --- For Year"
1110 LET V0$="D",V1$="CY",V2$=YEAR$,V3$="",V4$="Enter Fiscal Year To Run Report For",V0=2,V1=40,V2=4
1120 GOSUB 7000
1130 ON V3 GOTO 1140,1140,1190,1140,4000
1140 LET YEAR$=V$
1190 GOTO 3900
1200 REM " --- Process input"
1210 CALL "IVC.IB","I","",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
1220 ON V3 GOTO 1230,1230,3900,1230,4000
1290 GOTO 3900
1300 REM " --- Subtotals"
1310 LET V0$="Y",V1$="C",V2$=SUBTOTALS$,V3$="",V4$="Y=Totals By Product, Warehouse And Report, N=No Totals",V0=1,V1=40,V2=13
1320 GOSUB 7000
1330 ON V3 GOTO 1340,1340,1390,1340,4000
1340 LET SUBTOTALS$=V$
1390 GOTO 3900
3900 REM " --- Next field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,1200,1300,3990
4000 REM " --- Correct?"
4010 GOSUB 5000
4020 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4030 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4060,1000
4060 LET LEVEL$=OPTIONS$[0],BEGWHSE$=OPTIONS$[1],ENDWHSE$=OPTIONS$[2]
4070 LET BEGPROD$=OPTIONS$[3],ENDPROD$=OPTIONS$[4],BEGITEM$=OPTIONS$[5]
4080 LET ENDITEM$=OPTIONS$[6],TYPE$=OPTIONS$[7]
4090 RUN "IVR.CB"
5000 REM " --- Display Screen"
5010 CALL "IVC.IB","D","",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

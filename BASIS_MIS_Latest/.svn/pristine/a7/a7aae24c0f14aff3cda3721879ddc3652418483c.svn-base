0010 REM "IVC - Generate Lot/Serial Numbers"
0020 REM "Program IVC.SN"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "SYS01_DEV    : System File Channel (input)
0050 REM "ROW          : Current input row (input)"
0055 REM "QUANTITY     : Default quantity (input)"
0060 REM "SERIAL$[ALL] : Generated Serial Numbers (returned)"
0065 REM "             : SERIAL$[0]=Number of Lot/Serial Numbers (returned)"
0070 REM "STATUS       : 0=No error (returned)"
0075 REM 
0080 SETERR 9000
0085 SETESC 9900
0090 ENTER SYS01_DEV,ROW,QUANTITY,SERIAL$[ALL],STATUS
0200 REM " --- IOLIST's"
0260 IVS01A: IOLIST X$,X$,P2$,P3$
0270 SYS01T: IOLIST X$,F0$
0400 REM " --- Parameters"
0405 DIM P[3]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),STATUS=1
0430 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 IF POS(P3$(17,1)="SL")=0 THEN GOTO 9900
0450 LET P[3]=NUM(P2$(7,2))
0500 REM " --- Initializations"
0550 LET STATUS=0,M2$="#,##0",TITLE$="Generate Lot/Serial Numbers"
0560 LET HEIGHT=6,WIN_Y=MAX(ROW-HEIGHT-2,2),WIDTH=46+P[3]
0570 LET WIN_X=INT((80-WIDTH)/2),NUMBER=QUANTITY
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT 'SB',@(1,0),"First Lot/Serial Number:",@(4,1),"Quantity To Generate:",'SF'
1000 REM " --- Lot/Serial Number"
1010 LET V0$="S",V1$="CERK",V2$=SER$,V3$="",V0=P[3],V1=26,V2=0,I0=0
1020 LET V4$="Enter The First Lot/Serial Number"
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,3900,1000,3900
1050 LET SER$=V$
1090 GOTO 3900
1200 REM " --- Quantity"
1220 LET V0$="N",V1$="C",V2$=STR(NUMBER),V3$=M2$,V0=7,V1=26,V2=1
1230 LET V4$="Enter The Number Of Lot/Serial Numbers Needed"
1240 GOSUB 7000
1250 ON V3 GOTO 1260,1200,1200,1200,3900
1260 IF V<1 THEN GOTO 1200
1270 LET NUMBER=V
1280 PRINT @(V1,V2),V:M2$,
1290 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 4900
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,3990
3990 LET I0=0
4000 REM " --- Options"
4010 LET V0$="Y",V4$="Is The Above Information Correct (Y/N)?"
4020 LET V1$="KC^",V2$="Y",V3$="",V0=1,V1=FNC(V4$,WIDTH)+LEN(V4$)+1,V2=4
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4050 IF V$="N" THEN LET SERIAL$[0]="0"
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Setup Variables"
4110 IF NUMBER<1 THEN GOTO 4900
4120 LET SER=LEN(SER$)
4130 IF SER<1 THEN LET SER=LEN(STR(NUMBER)),SER$=STR(1:FILL(SER,"0"))
4140 DIM A$[SER],SERIAL$[NUMBER]
4150 FOR X=1 TO SER
4160 LET A$[X]=SER$(X,1)
4170 NEXT X
4180 LET SERIAL$[0]=STR(NUMBER),SERIAL$[1]=SER$,Y0=SER
4190 IF NUMBER<2 THEN GOTO 9900
4200 REM " --- Create Loop"
4210 FOR X=2 TO NUMBER
4220 IF Y0<1 THEN EXITTO 4900
4230 LET X0=NUM(A$[Y0],ERR=4250)
4240 GOTO 4260
4250 LET Y0=Y0-1
4255 GOTO 4230
4260 IF X0=9 THEN LET A$[Y0]="0",Y0=Y0-1; GOTO 4230
4265 LET A$[Y0]=STR(X0+1:"0"),Y0=SER
4270 FOR Y=1 TO SER
4275 LET SERIAL$[X]=SERIAL$[X]+A$[Y]
4280 NEXT Y
4290 NEXT X
4300 REM " --- All done"
4390 GOTO 9900
4900 REM " --- Don't return any serial numbers"
4910 LET SERIAL$[0]="0"
4990 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to calling program"
9910 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9950 EXIT 
9999 END

0010 REM "SYX - Copy Data From Firm to Firm Within a File"
0020 REM "Program SYX.RA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=6
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="SYM-03"
0130 LET FILES$[3]="SYM-04",FILES$[4]="SYM-06"
0140 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SYM03_DEV=CHANNELS[2]
0170 LET SYM04_DEV=CHANNELS[3],SYM06_DEV=CHANNELS[4]
0200 REM " --- IOLIST's"
0210 SYM03A: IOLIST C0$(1),C1$(1),C2$(1),C[ALL]
0220 SYM06A: IOLIST X0$(1),X1$(1)
0230 SYM04A: IOLIST D0$(1),D1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="SY",N3$=F5$
0500 REM " --- Initializations"
0510 DIM A0$(2),A1$(40),B0$(2),B1$(40),C0$(6),C1$(64),C2$(30),C[2]
0520 DIM D0$(2),D1$(64),X0$(2),X1$(40)
0550 LET A0$(1)=N0$,FILENAME$=""
0700 REM " --- Background"
0720 PRINT 'SB',@(9,3),"File:",@(4,4),"From Firm:",@(6,5),"To Firm:",'SF',
1000 REM " --- File Name"
1010 PRINT 'CF',
1015 LET V0$="S",V1$="CE",V2$=FILENAME$,V0=6,V1=15,V2=3,I0=0
1020 LET V4$="Enter a valid file name (<F3>=Lookup)"
1025 GOSUB 7000
1030 ON V3 GOTO 1050,1000,1000,1040,9900
1040 GOSUB 6000
1050 FIND (SYM03_DEV,KEY=V$,DOM=1000)IOL=SYM03A
1055 LET FILENAME$=C0$,SYSTEM$=C0$(1,2)
1060 FIND (SYM04_DEV,KEY=SYSTEM$,DOM=1070)IOL=SYM04A
1065 IF D1$(39,1)="Y" THEN GOTO 1100
1070 LET V4$=FNP$(D1$(1,35))+" has not been installed (<Enter>=Continue):"
1075 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
1080 GOSUB 7000
1090 GOTO 1000
1100 REM " --- Open the file"
1105 IF D1$(36,1)="Y" THEN GOTO 1130
1110 LET V4$=FNP$(D1$(1,35))+" is not a multi-firm system (<Enter>=Continue):"
1120 GOTO 1075
1130 CLOSE (FROM_DEV,ERR=1135)
1135 CLOSE (TO_DEV,ERR=1140)
1140 LET FILES$[5]=FILENAME$,FILES$[6]=FILENAME$
1150 CALL "SYC.DA",1,5,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
1155 IF STATUS>0 THEN GOTO 1000
1160 LET FROM_DEV=CHANNELS[5],TO_DEV=CHANNELS[6]
1170 PRINT @(V1,V2),FILENAME$," ",C1$(1,30),'CL',
1180 CALL "SYC.JA",FROM_DEV,Q$,X$,TYPE$,K,R,L,N,V
1190 GOTO 3900
1200 REM " --- From Firm"
1210 LET V0$="Z",V1$="C",V2$=A0$,V0=2,V1=15,V2=4
1220 LET V4$="Enter a Valid Company Number (<F3>=Lookup)"
1230 GOSUB 7000
1240 ON V3 GOTO 1260,1200,3900,1250,9900
1250 CALL "SYC.LX",V$
1260 LET A0$(1)=V$,X0$(1)=V$
1270 FIND (SYM06_DEV,KEY=X0$,DOM=1200)IOL=SYM06A
1280 PRINT @(V1,V2),A0$," ",X1$,'CL',
1290 GOTO 3900
1400 REM " --- To Firm"
1410 LET V0$="Z",V1$="C",V2$=B0$,V0=2,V1=15,V2=5
1420 LET V4$="Enter a Valid Company Number (<F3>=Lookup)"
1430 GOSUB 7000
1440 ON V3 GOTO 1460,1400,3900,1450,9900
1450 CALL "SYC.LX",V$
1460 LET B0$(1)=V$,X0$(1)=V$
1470 FIND (SYM06_DEV,KEY=B0$,DOM=1400)IOL=SYM06A
1480 PRINT @(V1,V2),B0$," ",X1$,'CL',
1490 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,4000
4000 REM " --- Correct?"
4010 LET V4$="Is the above information correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="C",V2$="N",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4050 IF V$="DELETE" THEN GOTO 4500
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Copy records"
4110 IF A0$<>B0$ THEN GOTO 4150
4120 LET V4$="Records cannot be copied to the same firm (<Enter>=Continue):"
4130 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
4135 GOSUB 7000
4140 GOTO 1000
4150 CALL "SYC.NB","Copying",K,COLUMN
4170 LET TYPE$="D"
4180 IF L=0 THEN LET TYPE$="S"
4190 READ (FROM_DEV,KEY=A0$,DOM=4200)
4200 REM " --- Copy loop"
4210 LET K$=KEY(FROM_DEV,END=4900)
4220 IF POS(A0$=K$)<>1 THEN GOTO 4900
4230 PRINT @(COLUMN,11),K$,
4240 ON POS(TYPE$="DS") GOTO 4900,4300,4400
4300 REM " --- Direct File"
4310 READ RECORD(FROM_DEV,KEY=K$)R$
4320 LET K$(1,2)=B0$,R$(1,2)=B0$
4330 WRITE RECORD(TO_DEV,KEY=K$)R$
4340 READ (FROM_DEV,KEY=A0$+K$(3))
4390 GOTO 4200
4400 REM " --- Sort File"
4410 READ (FROM_DEV)
4420 LET K$(1,2)=B0$
4430 WRITE (TO_DEV,KEY=K$)
4440 READ (FROM_DEV,KEY=A0$+K$(3))
4490 GOTO 4200
4500 REM " --- Delete records"
4510 IF A0$=B0$ THEN GOTO 4550
4515 LET V4$="Firm ID's must be the same to delete records (<Enter>=Continue):"
4520 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
4530 GOSUB 7000
4540 GOTO 4900
4550 LET V4$="All Firm "+A0$+" record(s) will be deleted from "+FILENAME$+". OK to continue"
4560 CALL "SYC.YN",1,V4$,1,V$,V3
4570 IF V$<>"Y" THEN GOTO 4900
4580 CALL "SYC.NB","Deleting",K,COLUMN
4590 READ (FROM_DEV,KEY=A0$,DOM=4600)
4600 REM " --- Delete loop"
4610 LET K$=KEY(FROM_DEV,END=4900)
4615 IF POS(A0$=K$)<>1 THEN GOTO 4900
4620 PRINT @(COLUMN,11),K$,
4630 REMOVE (FROM_DEV,KEY=K$)
4690 GOTO 4600
4900 REM " --- Copy/Delete Completed"
4910 PRINT @(0,10),'CL',@(0,11),'CL',@(0,12),'CL',
4990 GOTO 1000
6000 REM " --- Lookup"
6010 LET RECORD$="",NUMBER=1,TITLE$="ADD+ON File Definitions"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6025 LET DESCRIPTION$[0]="File",DESCRIPTION$[1]="Description"
6030 LET FIELD[0]=1,POSITION[0]=1,LENGTH[0]=6
6040 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6050 LET ROW=3,COLUMN=79-LENGTH[0]-LENGTH[1]-NUMBER*4
6060 LET V$="NO COMPANY"
6070 CALL "SYC.LK",SYM03_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

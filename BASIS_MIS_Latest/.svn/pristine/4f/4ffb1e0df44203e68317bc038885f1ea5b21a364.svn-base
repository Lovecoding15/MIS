0010 REM "OPM - Pricing Table Listing"
0020 REM "Program OPM.CB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "13Aug96 WGH: Use full 3-char Item Class (1130,2040)"
0040 REM 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files"
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS>0 THEN GOTO 9900
0200 REM "--- IOLISTS"
0210 IVM10E: IOLIST A0$,A[ALL]
0500 REM " --- Initialize Data "
0510 DIM HEADINGS$[2]
0550 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$+" Listing",CLOCK$="",L9=55
0560 LET WHEN$=F0$(7,3),L=L9+1,H0=80,H=0,T0=0,M8$="-##0.00%"
0580 CALL "SYC.RM",M2$,M2,9,15
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",7,COLUMN
1000 REM " --- Initial Read"
1020 READ (IVM10_DEV,KEY=N0$+"E",DOM=1030)
1100 REM " --- Main Read"
1110 READ (IVM10_DEV,END=4000)IOL=IVM10E
1120 IF POS(N0$+"E"=A0$)<>1 THEN GOTO 4000
1130 PRINT @(COLUMN,11),A0$(4,3)," ",A0$(7,4),
2000 REM " --- Print"
2010 LET L1=0
2020 IF L>L9 THEN GOSUB 5000
2040 PRINT (7)@(0),A0$(4,3),@(5),A0$(7,4),@(10),A0$(31,1),@(15),A0$(11,20),
2050 FOR X=0 TO 9
2060 IF A[X]=0 THEN IF A[X+10]=0 THEN GOTO 2090
2070 PRINT (7)@(68-M2),A[X]:M2$,@(72),A[X+10]:M8$,'LF',
2080 LET L=L+1,L1=L1+1
2090 NEXT X
2100 IF L1=0 THEN PRINT (7)'LF',; LET L=L+1
2120 PRINT (7)'LF',
2200 LET L=L+1,T0=T0+1
3900 REM 
3990 GOTO 1100
4000 REM " --- Options"
4010 IF L>L9 THEN GOSUB 5000
4020 PRINT (7)""
4030 PRINT (7)T0," Total Records"
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=0
5020 CALL "SYC.HA",7,HEADINGS$[ALL],2,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)"Item",@(5),"Pricing"
5050 PRINT (7)"Cls",@(5),"Cd",@(8),"Method",@(15),"Description",@(59),"Quantity / Percentage"
5060 PRINT (7)""
5090 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

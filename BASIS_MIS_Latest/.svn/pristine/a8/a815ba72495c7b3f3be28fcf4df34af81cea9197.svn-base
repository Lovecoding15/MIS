0010 REM "SYC - Return Day Of Week (Public/Called)"
0020 REM "Program SYC.CB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "WHEN$: YYMMDD or YMD Packed Date (Input)"
0050 REM "DOW$ : Day of week Mon through Sun (Returned)"
0055 REM "DOW  : Day of week 1=Mon through 7=Sun (Returned)"
0060 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER WHEN$,DOW$,DOW
0500 REM " --- Initializations"
0550 LET DAYS$="MonTueWedThuFriSatSun",DATE$="",DOW$="Err",DOW=0
1000 REM " --- Format date to YYMMDD"
1010 IF LEN(WHEN$)=3 THEN LET DATE$=FNYY_YY21$(FNM$(FNC$(WHEN$)))
1020 IF LEN(WHEN$)=6 THEN LET DATE$=FNYY_YY21$(WHEN$)
1030 IF DATE$="" THEN GOTO 4000
1100 REM " --- Convert to Julian"
1110 LET JULIAN=JUL(1900+FNYY_YEAR(DATE$(1,2)),NUM(DATE$(3,2)),NUM(DATE$(5,2)),ERR=4000)
2000 REM " --- Calculate day of week"
2010 LET DOW$=DATE(JULIAN:"%Ds",ERR=4000),DOW=(POS(DOW$=DAYS$,3)-1)/3+1
4000 REM " --- All done"
4090 GOTO 9900
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8080 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9950 EXIT 
9999 END

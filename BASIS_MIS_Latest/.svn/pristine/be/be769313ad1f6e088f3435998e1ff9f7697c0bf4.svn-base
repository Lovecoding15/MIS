0010 REM "WOE - Work Order Material Commitment Entry                   "
0020 REM "Program WOE.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "07Mar97 JJD: Correct spelling of variable passed to IVC.UA (2330)"
0032 REM "30Mar98 KJS: Clarify prompt (1470)"
0040 REM 
0045 REM "O9$="0" NEW ORDER, "1" MODIFIED QTY, "2" REPRINT PICK SLIP
0050 SETERR 9000
0060 ON O9 GOTO 0070,1000
0070 BEGIN 
0080 SETERR 9000
0085 SETESC 9000
0100 REM " --- Open/Lock FIles"
0110 LET FILES=14
0115 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="WOE-13",FILES$[2]="WOE-23",FILES$[3]="WOE-01"
0125 LET FILES$[4]="IVM-01",FILES$[5]="WOE-04",FILES$[6]="SYS-01"
0130 LET FILES$[7]="IVM-03",FILES$[8]="IVM-10",FILES$[9]="IVM-02"
0135 LET FILES$[10]="WOE-22",FILES$[11]="WOM-10",FILES$[12]="WOE-15"
0143 LET FILES$[13]="WOE-25",FILES$[14]="IVM-07"
0150 CALL "SYC.DA",1,1,13,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE13_DEV=CHANNELS[1],WOE23_DEV=CHANNELS[2],WOE01_DEV=CHANNELS[3]
0165 LET IVM01_DEV=CHANNELS[4],WOE04_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0170 LET IVM03_DEV=CHANNELS[7],IVM10_DEV=CHANNELS[8],IVM02_DEV=CHANNELS[9]
0175 LET WOE22_DEV=CHANNELS[10],WOM10_DEV=CHANNELS[11],WOE15_DEV=CHANNELS[12]
0180 LET WOE25_DEV=CHANNELS[13]
0200 REM " --- IOLISTs"
0210 IVM02A: IOLIST B0$,B1$,B2$,B[ALL]
0220 WOE04C: IOLIST C0$
0230 WOE01A: IOLIST A0$,A1$
0240 IVM01A: IOLIST D0$,D1$(1)
0260 WOS01A: IOLIST X$,P1$,P2$,P9$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 IVS01A: IOLIST X$,X$,P2$,P3$
0320 WOE22A: IOLIST W0$,W1$,W[ALL]
0330 WOE13A: IOLIST A0$,A1$
0340 WOE23A: IOLIST U0$,U1$,U[ALL]
0350 BMS01A: IOLIST *,*,M4$,M5$,M4,M5
0360 ARS01A: IOLIST *,*,X$,*,*,P7$
0370 WOM10A: IOLIST X1$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0440 IF P9$(1,1)<>"Y" THEN GOTO 0450
0445 FIND (SYS01_DEV,KEY=N0$+"BM00",DOM=0450)IOL=BMS01A
0450 IF P9$(2,1)<>"Y" THEN GOTO 0485
0455 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=0485)IOL=ARS01A
0457 LET P7=NUM(X$(1,2))
0485 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0487 DIM P[1],DESC[3]
0490 LET P[0]=NUM(P2$(1,2)),IVS01_PRECISION=NUM(P2$(5,1)),P[1]=NUM(P2$(5,1)),P0=P[0],P1=P[1]
0495 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$)
0497 FOR X=1 TO 3; LET DESC[X]=NUM(P2$((X-1)*2+9,2)),DESCLEN=DESCLEN+DESC[X]; NEXT X
0500 REM " --- Init Data"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 DIM A0$(75),A1$(110),A[5],W[7],U[4]
0530 LET K0=1,K9=11
0540 LET A0$(1)=N0$
0550 DIM PARAMS[0],PARAMS$[4],ITEMS$[3],REFS$[11],REFS[5],IVFILES[44]
0560 LET PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3),PARAMS$[2]=P2$
0565 LET PARAMS$[3]=P3$,PARAMS$[4]=P4$
0570 LET IVFILES[0]=SYS01_DEV,IVFILES[1]=IVM01_DEV,IVFILES[2]=IVM02_DEV
0600 REM " --- Additional File Opens "
0610 IF POS(P3$(17,1)="LS")=0 THEN GOTO 0700
0620 CALL "SYC.DA",1,14,14,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS THEN GOTO 9900
0640 LET IVM07_DEV=CHANNELS[14],IVFILES[7]=IVM07_DEV
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,7,0
0720 PRINT 'SB',@(2,3),"WO Number:",@(21),"Type:",@(55),"Status:",@(0,5),"Bill Number:",@(0,6),"Description:",@(53,4),"Category:",@(52,5),"Warehouse:",@(58,6),"U/M:",
0790 PRINT 'SF',
0900 REM " --- Init File"
0990 READ (WOE13_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(WOE13_DEV,END=1050)
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF LEN(V2$)>5 THEN LET V2$=V2$(5,7) ELSE LET V2$=""
1050 LET V0$="Z",V1$="CE",V3$="",V0=7,V1=13,V2=3,V4$="Enter Work Order (<F3>=Lookup)"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 9900
1080 IF V3=3 THEN CALL "SYC.LW",V$,"O"
1090 IF V$="" OR POS("0"<>V$)=0 THEN GOTO 0900
1100 LET A0$(5)=V$,O9$="0"
1110 PRINT @(V1,V2),V$
1300 REM " --- Find Key"
1310 FIND (WOE01_DEV,KEY=A0$(1,11),DOM=1000)IOL=WOE01A
1320 GOSUB 5000
1330 IF A0$(15,1)<>"C" THEN GOTO 1380
1340 LET V0$="S",V1$="C",V2$="",V3$="",V4$="This Work Order Is Closed! (<Enter> =Continue):",V0=1,V1=FNV(V4$),V2=22
1350 PRINT @(V1,V2),'RB',
1360 GOSUB 7000
1370 GOTO 1000
1380 IF POS(A0$(15,1)="PQ")=0 THEN GOTO 1440
1390 DIM MESS$[1]
1400 LET MESS$[0]="Work Order Has Not Been Released. You May"
1410 LET MESS$[1]="Release This From Work Order Entry."
1420 CALL "SYC.XA",0,MESS$[ALL],1,22,-1,V$,V3
1430 GOTO 1000
1440 READ (WOE13_DEV,KEY=A0$(1,11)+"000",DOM=1700)IOL=WOE13A
1450 REM " --- Found One...
1460 FIND (WOE04_DEV,KEY=A0$(1,4)+"D"+A0$(5,7),DOM=1520)
1470 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Warning! Issues Entry is in Process for this Work Order, <Enter>=Continue",V0=1,V1=40+INT(LEN(V4$)/2)+2,V2=22
1480 PRINT @(V1,V2),'RB',
1490 GOSUB 7000
1500 READ (WOE13_DEV,KEY=A0$(1,11)+$FF$,DOM=1510)
1510 GOTO 1000
1520 GOTO 4000
1700 REM " --- New Entry???
1710 LET V4$="Is This A New Record"
1720 CALL "SYC.YN",1,V4$,1,V$,V3
1730 IF V$="N" THEN GOTO 1500
1740 IF V$<>"Y" THEN GOTO 1710
1745 REM " --- Write Header
1750 DIM A1$(70)
1760 LET A1$(1)=A0$(12,4),A1$(49,22)=A0$(54,22),A0$=A0$(1,11)+"000",O9$="0"
1770 WRITE (WOE13_DEV,KEY=A0$)IOL=WOE13A
1780 LET C0$=A0$(1,4)+"C"+A0$(5,7)
1790 WRITE (WOE04_DEV,KEY=C0$)IOL=WOE04C
2000 REM " --- Find Material Requirements From Work Order "
2020 READ (WOE22_DEV,KEY=A0$(1,11)+"B",DOM=2100)
2100 LET K6$=KEY(WOE22_DEV,END=4000)
2120 IF K6$(1,11)<>A0$(1,11) THEN GOTO 4000
2140 READ (WOE22_DEV,KEY=K6$)IOL=WOE22A
2150 IF W1$(91,1)="M" THEN GOTO 2100
2160 DIM U1$(40),U[4]
2180 LET U0$=W0$(1,11)+W0$(13),U1$(1,6)=W1$(1,6),U1$(19)=A1$(49,2)+W1$(27,20)
2200 FIND (WOE23_DEV,KEY=U0$,DOM=2220)
2210 GOTO 2100
2220 LET U[0]=W[2],U[2]=W[7],U[4]=U[2]
2260 PRINT @(0,22),'CL',@(10,22),"Creating Step: ",U0$(12,3),"..."
2300 REM " --- Update Committed Amount "
2310 LET ACTION$="CO",ITEMS$[0]=N0$,ITEMS$[1]=U1$(19,2),ITEMS$[2]=U1$(21,20)
2320 LET REFS[0]=U[0]
2330 CALL "IVC.UA",ACTION$,IVFILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEMS$[ALL],REFS$[ALL],REFS[ALL],STATUS
2380 GOTO 2500
2400 REM " --- No Warehouse Rec..
2420 PRINT 'RB',@(0,22),"Missing Warehouse ",A1$(49,2)," For Item: ",W1$(27,P[0])," Record Not Written, <Enter>:",
2440 LET V0$="S",V1$="C",V2$="",V3$="",V4$="",V0=1,V1=58+P[0],V2=22
2450 GOSUB 7000
2460 GOTO 2100
2500 REM " --- Write It
2520 WRITE (WOE23_DEV,KEY=U0$)IOL=WOE23A
2540 GOTO 2100
4000 REM " --- Run Overlay"
4090 RUN "WOE.CB"
5000 REM " --- Display"
5010 DIM D1$(60)
5020 FIND (IVM01_DEV,KEY=N0$+A0$(56,20),DOM=5030)IOL=IVM01A
5030 DIM X1$(30)
5040 LET X1$(6,20)="** Not On File **",I9$="N"
5050 FIND (WOM10_DEV,KEY=N0$+"A"+A0$(12,2),DOM=5070)IOL=WOM10A
5060 LET I9$=A0$(14,1)
5070 PRINT @(27,3),A0$(12,2),"  ",X1$(6,20),@(63),A0$(15,1),@(63,4),A0$(14,1),@(63,5),A0$(54,2),@(63,6),A0$(50,2)
5080 IF I9$="I" THEN PRINT @(65,4),"Inventory",@(13,5),A0$(56)
5090 IF I9$="N" THEN PRINT @(65,4),"Non-Stocking"
5100 IF I9$="R" THEN PRINT @(65,4),"Recurring"
5110 IF I9$<>"I" THEN PRINT @(13,6),A1$(1,30)
5120 IF I9$="I" THEN PRINT @(13,6),D1$(1,MIN(DESC[1],42))
5130 RETURN 
6000 REM " --- Enter Warehouse Here
6020 PRINT @(0,15),'SB',"Whse: ",'SF'
6030 LET V0$="S",V1$="KCE",V2$=P3$(6,2),V3$="",V4$="Enter Selected Warehouse (<Enter>=Default/<F3>=Lookup)",V0=2,V1=8,V2=15
6040 GOSUB 7000
6050 IF V3<>3 THEN GOTO 6080
6060 LET V4=1,V5=8,V6=4,V7=2,V8=6,V9=20,V5$="C"
6070 CALL "SYC.LZ",V1,V2,V4,IVM10_DEV,V6,V7,V8,V9,V$,V5$,N0$
6080 IF V3=4 THEN LET V$=""; GOTO 6090
6085 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=6000)
6087 LET V1=11,V2=3
6090 PRINT @(0,15),'CL'
6095 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "APM - Vendor Maintenance (Hard Copy Printing)"
0020 REM "Program APM.MG"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 APM01A: IOLIST A0$,A1$
0220 APM02A: IOLIST B0$,B1$,B2$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13]
0230 APM09A: IOLIST X0$,X1$
0240 APT01A: IOLIST X0$,X[0],X[1],X[2]
0250 APT11A: IOLIST X0$,X1$,X[0],X[1],X[2]
0290 APM05A: IOLIST X0$,X1$(1)
0310 APM10A: IOLIST C0$,C1$
0320 APM10B: IOLIST C0$,C1$
0330 APM10C: IOLIST C0$,C1$
0340 APM10D: IOLIST C0$,C1$
0350 GLM01A: IOLIST C0$,C1$
0400 REM " --- Get G/L Installed Parameter
0410 DIM INFO$[20]
0420 CALL "SYC.VA","AP",INFO$[ALL]
0500 REM " --- Init Data
0510 DIM X[8],W[2],HEADING$[2]
0520 LET H0=80,H=0,L9=59,L=L9+1,T0=0
0530 IF H1$="" THEN LET H1$="Vendor Detail Listing"
0550 LET HEADING$[0]=FNP$(N1$),HEADING$[1]=FNP$(H1$)
0560 LET HEADING=2,WHEN$=F0$(7,3),CLOCK$=""
0900 REM " --- Init File
0910 READ (APM01_DEV,KEY=N0$+K0$,DOM=0911)IOL=APM01A; GOTO 1100
1000 REM " ---MAIN READ"
1010 LET NEXT$=KEY(APM01_DEV,END=4000)
1020 IF POS(N0$=NEXT$)<>1 THEN GOTO 4000
1030 IF LEN(K1$)>0 THEN IF NEXT$(3,6)>K1$ THEN GOTO 4000
1040 READ (APM01_DEV,KEY=NEXT$)IOL=APM01A
1050 LET H=0
1100 REM " --- Print name & address
1105 CALL "SYC.NB","Printing",LEN(M0$),COLUMN
1115 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$),
1120 GOSUB 5000
1140 PRINT (7)@(0),"Vendor Number: ",FNF$(A0$(3,P[0]),M0$),@(44),"Fax Phone: ",FNF$(A1$(186,10),M8$),'LF',@(9),"Name: ",A1$(1,30),@(46),"Acct No: ",A1$(176,10),'LF',@(6),"Address: ",A1$(31,24),@(46),"Contact: ",A1$(126,20),'LF',@(15),A1$(55,24),@(41),"Alt Sequence: ",A1$(146,10),'LF',@(15),A1$(79,24),@(42),"Date Opened: ",FNB$(A1$(156,3)),'LF',@(3),"Zip/Postal: ",FNZ$(A1$(103)),@(40),"Hold Invoices: ",A1$(159,1),'LF',@(4),"Telephone: ",FNF$(A1$(112,10),M8$),@(30),"Ext: ",A1$(122,4),@(43),"Federal ID: ",A1$(160,15),'LF',@(43),"Print 1099: "
1150 LET L=L+8
1160 GOTO 2500
1200 REM " --- Detail/History
1220 READ (APM02_DEV,KEY=A0$,DOM=1230)
1240 LET K$=KEY(APM02_DEV,END=1000)
1250 IF K$(1,8)<>A0$ THEN GOTO 1000
1260 READ (APM02_DEV)IOL=APM02A
1270 IF L+8>L9 THEN GOSUB 5000
1290 GOSUB 5200
1300 REM " --- Detail
1320 IF P3$(1,1)<>"Y" THEN GOTO 1400
1325 LET APTYPE$="***Not On File***"
1330 READ (APM10_DEV,KEY=N0$+"A"+B0$(9,2),DOM=1340)IOL=APM10A
1335 LET APTYPE$=C0$(6,20)
1340 PRINT (7)@(6),"AP Type: ",B0$(9,2)," ",APTYPE$
1360 LET L=L+1
1400 IF P3$(2,1)<>"Y" THEN GOTO 1490
1405 LET DISTCDE$="***Not On File***"
1410 READ (APM10_DEV,KEY=N0$+"B"+B1$(1,2),DOM=1420)IOL=APM10B
1415 LET DISTCDE$=C0$(6,20)
1420 PRINT (7)@(4),"Dist Code: ",B1$(1,2)," ",DISTCDE$
1440 LET L=L+1
1490 LET TERMS$="***Not On File***",PYMNT$="***Not On File***",GLNAME$="***Not On File***"
1500 READ (APM10_DEV,KEY=N0$+"C"+B1$(5,2),DOM=1510)IOL=APM10C
1505 LET TERMS$=C0$(6,20)
1510 READ (APM10_DEV,KEY=N0$+"D"+B1$(3,2),DOM=1520)IOL=APM10D
1515 LET PYMNT$=C0$(6,20)
1520 IF GL$<>"Y" THEN GOTO 1540
1530 READ (GLM01_DEV,KEY=N0$+B2$,DOM=1540)IOL=GLM01A
1535 LET GLNAME$=C1$(1,35)
1560 PRINT (7)@(0),"Payment Group: ",B1$(3,2)," ",PYMNT$,'LF',@(3),"Terms Code: ",B1$(5,2)," ",TERMS$; LET L=L+2
1570 IF GL$="Y" THEN PRINT (7)@(3),"Account No: ",FNF$(B2$(1,G[3]),G5$)," ",GLNAME$; LET L=L+1
1580 PRINT (7)J$; LET L=L+1
1600 REM " --- History
1610 IF L+9>L9 THEN GOSUB 5000
1620 IF P3$(3,1)="Y" THEN PRINT (7)@(39),"Open Retention: ",B[1]:M1$,
1640 PRINT (7)@(0),"Open Invoices: ",B[0]:M1$
1700 PRINT (7)@(0),J$,'LF',@(1),"Last Invoice: ",FNB$(B1$(7,3)),'LF',@(1),"Last Payment: ",FNB$(B1$(10,3)),'LF',@(0),J$,'LF',@(4+M1),"Purchases",@(19+M1),"Discounts",@(35+M1),"Payments",@(50+M1),"Calendar-Yr"
1720 PRINT (7)@(2),"Current Ytd: ",B[2]:M1$,@(30),B[5]:M1$,@(45),B[8]:M1$,@(60),B[11]:M1$
1730 PRINT (7)@(3),"Prior Year: ",B[3]:M1$,@(30),B[6]:M1$,@(45),B[9]:M1$,@(60),B[12]:M1$
1740 PRINT (7)@(4),"Next Year: ",B[4]:M1$,@(30),B[7]:M1$,@(45),B[10]:M1$,@(60),B[13]:M1$
1750 LET L=L+9
2000 REM " --- Display Detail
2010 LET T0=0
2020 IF L+2>L9 THEN GOSUB 5000
2030 GOSUB 5400
2040 LET W0$=B0$(9,2)
2050 READ (APT01_DEV,KEY=N0$+W0$+A0$(3,6),DOM=2060)
2100 REM " --- Main Read
2120 LET K$=KEY(APT01_DEV,END=2460)
2140 IF K$(1,10)<>N0$+W0$+A0$(3,6) THEN GOTO 2460
2160 READ (APT01_DEV)IOL=APT01A
2170 IF L+1>L9 THEN GOSUB 5000; GOSUB 5400
2200 IF P3$(3,1)="Y" THEN PRINT (7)@(37+2*M1),X[2]:M1$,
2210 PRINT (7)@(0),X0$(11,10)," ",FNB$(X0$(29,3))," ",FNB$(X0$(35,3))," ",X0$(25,2)," ",X0$(67,1),@(35),X[0]:M1$,X[1]:M1$
2230 LET W0=X[0],W1=X[1],W2=X[2],Q9=0,L=L+1; LET T0=T0+X[0]
2250 READ (APT11_DEV,KEY=X0$(1,20),DOM=2251)
2300 LET K$=KEY(APT11_DEV,END=2400)
2310 IF K$(1,20)<>X0$(1,20) THEN GOTO 2400
2320 READ (APT11_DEV)IOL=APT11A
2342 IF P3$(3,1)="Y" THEN PRINT (7)@(37+2*M1),X[2]:M1$,
2343 IF X1$(1,1)="S" THEN LET R0$="M" ELSE LET R0$=X1$(1,1)
2345 IF L+1>L9 THEN GOSUB 5000; GOSUB 5400
2350 PRINT (7)@(1),R0$," ",X1$(5,7)," ",FNB$(X1$(2,3)),@(35),X[0]:M1$,X[1]:M1$
2355 LET W0=W0+X[0],W1=W1+X[1],W2=W2+X[2],L=L+1,T0=T0+X[0],Q9=1
2370 GOTO 2300
2400 REM " --- Print Total
2405 IF Q9=0 THEN GOTO 2450
2410 IF L+1>L9 THEN GOSUB 5000; GOSUB 5400
2420 IF P3$(3,1)="Y" THEN PRINT (7)@(37+2*M1),W2:M1$,
2430 PRINT (7)@(10),"*Total*",@(35),W0:M1$,W1:M1$
2440 LET L=L+1
2450 GOTO 2100
2460 IF L+1>L9 THEN GOSUB 5000; GOSUB 5400
2470 PRINT (7)@(23),"***Balance",@(35),T0:M1$; LET L=L+1
2480 GOTO 1240
2500 REM " --- Comments
2505 IF Y$<>"Y" THEN GOTO 2600
2510 READ (APM09_DEV,KEY=A0$,DOM=2520)
2520 GOSUB 5500
2540 LET K$=KEY(APM09_DEV,END=2600)
2550 IF K$(1,8)<>A0$(1,8) THEN GOTO 2600
2560 READ (APM09_DEV,KEY=K$)IOL=APM09A
2570 IF L+1>L9 THEN GOSUB 5000; GOSUB 5500
2580 PRINT (7)@(0),X0$(9,2),@(7),X1$
2585 LET L=L+1
2590 GOTO 2540
2600 REM " --- Purchase Info
2610 READ (APM05_DEV,KEY=A0$,DOM=2620)
2620 LET K$=KEY(APM05_DEV,END=2800)
2625 IF LEN(K$)<8 THEN GOTO 2800
2630 IF POS(K$(1,8)=A0$)<>1 THEN GOTO 2800
2635 DIM X1$(160)
2640 READ (APM05_DEV,KEY=K$)IOL=APM05A
2650 IF L+7>L9 THEN GOSUB 5000
2660 PRINT (7)@(0),J$,@(15),"Purchase Address Information"
2680 PRINT (7)@(2),"Purchase No: ",X0$(9,2)
2690 PRINT (7)@(0),"Purchase Name: ",X1$(1,30),@(46),"Telephone: ",FNF$(X1$(112,10),M8$)," Ex: ",X1$(122,4)
2700 PRINT (7)@(6),"Address: ",X1$(31,24),@(46),"Fax Phone: ",FNF$(X1$(146,10),M8$)
2710 PRINT (7)@(15),X1$(55,24),@(48),"Contact: ",X1$(126,20)
2720 PRINT (7)@(15),X1$(79,24)
2730 PRINT (7)@(3),"Zip/Postal: ",FNZ$(X1$(103))
2740 LET L=L+7
2750 GOTO 2620
2900 REM " --- End Display
2990 GOTO 1200
4000 REM " --- End
4010 CLOSE (7,ERR=4020)
4020 LET L9=20,O0=4
4100 RUN O9$
5000 REM " --- Heading
5020 LET L=HEADING+1
5040 CALL "SYC.HA",7,HEADING$[ALL],HEADING,H,H0,WHEN$,CLOCK$,STATUS
5060 PRINT (7)@(2),"Vendor: ",FNF$(A0$(3,P[0]),M0$)
5080 PRINT (7)@(2),"  Name: ",A1$(1,30),'LF'
5090 LET L=L+3
5190 RETURN 
5200 REM " --- Detail/History
5210 PRINT (7)""
5220 PRINT (7)@(0),J$,@(15),"Detail/History"
5230 LET L=L+2
5290 RETURN 
5400 REM " --- Open Inv Heading
5410 PRINT (7)@(0),J$,@(15),"Open Invoices"
5420 IF P3$(3,1)="Y" THEN PRINT (7)@(27+3*M1),"Retention",
5440 PRINT (7)@(0),"Invoice No",@(13),"Date",@(22),"Due",@(29),"Pg H",@(35+M1-7),"Amount",@(35+2*M1-9),"Discount"
5450 LET L=L+2
5490 RETURN 
5500 REM " --- Comments
5510 IF L+2>L9 THEN GOSUB 5000
5520 PRINT (7)@(0),J$,@(15),"Comments"
5530 PRINT (7)@(0),"Seq"
5540 LET L=L+2
5590 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9220 LET O0=2
9290 GOTO 4000
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

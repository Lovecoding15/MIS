0010 REM "SYM - Firm Maintenance"
0020 REM "Program SYM.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=2
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="SYM-06"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SYM06_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 SYM06A: IOLIST A0$(1),A1$(1),A2$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0500 REM " --- Initializations"
0510 DIM A0$(2),A1$(40),A2$(16)
0600 DIM SYS01X$:"RECORD_ID:C(1),AVAILABLE:C(99*),EXCH_RATE:N(7*),PRECISION:N(7*),EXCH_RATE1:N(1*),NUM2:N(1*)"
0610 x1=15
0700 REM " --- Background"
0720 PRINT 'SB',@(6,3),"Firm ID:",@(9,4),"Name:",@(9,5),"Help:",@(2,6),"Batch Entry:",'SF',
0900 REM " --- Position File"
0990 READ (SYM06_DEV,KEY="",DOM=1000)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(SYM06_DEV,END=1030)
1030 LET V0$="Z",V1$="KE",V3$="",V0=2,V1=x1,V2=3
1040 LET V4$="Enter A Valid Firm ID (<F3>=Lookup)"
1050 GOSUB 7000
1060 ON V3 GOTO 1080,1000,1000,1070,4500
1070 CALL "SYC.LX",V$
1080 IF V$="" THEN GOTO 1000
1090 LET A0$(1)=V$
1200 REM " --- Find key"
1210 FIND (SYM06_DEV,KEY=A0$,DOM=1400)IOL=SYM06A
1220 IF POS(A2$(1,1)="YN")=0 THEN LET A2$(1,1)="Y"
1230 IF POS(A2$(2,1)="YN")=0 THEN LET A2$(2,1)="N"
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- New record initializations"
1510 DIM A1$(40),A2$(16)
1520 LET A2$(1,2)="YN"
1590 GOSUB 5000
2000 REM " --- Name"
2010 LET V0$="S",V1$="C",V2$=A1$,V3$="",V0=40,V1=x1,V2=4,I0=0
2020 LET V4$="Enter The Name Of This Firm"
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 LET A1$=V$
2090 GOTO 3900
2200 REM " --- Help"
2201 GOTO 3900
2210 LET V0$="Y",V1$="C",V2$=A2$(1,1),V3$="",V0=1,V1=x1,V2=5
2220 LET V4$="Should On-Line Help Be Enabled For This Firm (Y/N)?"
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 LET A2$(1,1)=V$
2290 GOTO 3900
2400 REM " --- Batch Entry?"
2401 GOTO 3900
2410 LET V0$="Y",V1$="C",V2$=A2$(2,1),V3$="",V0=1,V1=x1,V2=6
2420 LET V4$="Should This Firm Use Batch Entry (Y/N)?"
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 LET A2$(2,1)=V$
2490 GOTO 3900
2600 REM " --- Exchange Rate"
2610 IF A0$="01" THEN GOSUB EXCH_RATE
2690 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,2400,2600,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="C^",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write record"
4110 WRITE (SYM06_DEV,KEY=A0$)IOL=SYM06A
4120 if a0$<>"01" then goto 1000
4130 LET V4$="Convert firm 02 Euros to $ in firm 03 (Y/N)?"
4140 LET V0$="S",V1$="C^",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4150 GOSUB 7000
4160 ON V3 GOTO 4170,4190,4190,4190,4500
4170 IF V$="Y" THEN call "SYM.03"
4190 GOTO 1000
4200 REM " --- Remove record"
4210 REMOVE (SYM06_DEV,KEY=A0$,DOM=4220)
4290 GOTO 1000
4500 REM " --- Print?"
4510 REM CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 LET V$="N"; REM ON V3 GOTO 4550,4500,4500,4500,9900
4550 ON POS(V$="YN") GOTO 4500,4900,9900
4900 REM " --- Report Initializations"
4905 LET PROGRAM$="",NUMBER=3,HEADINGS=3,WIDTH=80,CHANNEL=SYM06_DEV
4915 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4920 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4925 DIM HEADINGS$[HEADINGS]
4930 LET FIELD$[0]="Firm",FIELD[0]=1,POSITION[0]=1
4935 LET LENGTH[0]=2,TYPE$[0]="S",MASK$[0]=""
4940 LET FIELD$[1]="Description",FIELD[1]=2,POSITION[1]=1
4945 LET LENGTH[1]=40,TYPE$[1]="S",MASK$[1]="",TOTAL$="Firms"
4950 LET FIELD$[2]="Help?",FIELD[2]=3,POSITION[2]=1
4955 LET LENGTH[2]=1,TYPE$[2]="Y",MASK$[2]="",N0$="",RECORD$=""
4960 LET FIELD$[3]="Batching?",FIELD[3]=3,POSITION[3]=2
4965 LET LENGTH[3]=1,TYPE$[3]="Y",MASK$[3]="",N0$="",RECORD$=""
4980 LET HEADINGS$[0]="ADD+ON Software",HEADINGS$[1]=F5$+" Listing"
4990 RUN "SYR.AA"
5000 REM " --- Display record"
5010 PRINT @(x1,3),A0$,@(x1,4),A1$,@(x1,5),A2$(1,1),@(x1,6),A2$(2,1),
5020 PRINT @(0,7),'CE',
5030 IF A0$<>"01" THEN RETURN
5040 LET ENTER_RATE=0; GOSUB EXCH_RATE; LET ENTER_RATE=1
5060 PRINT 'SB',@(0,7),"Sale/Exp Rate:",'SF',@(x1,7),RATE$[0]
5070 PRINT 'SB',@(0,8),"   A,L,E Rate:",'SF',@(x1,8),RATE$[1]
5090 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,0
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END
10000 EXCH_RATE: 
10005 dim rate$[1]
10010 LET SYS01X.RECORD_ID$="X",SYS01X.AVAILABLE$=FILL(99)
10020 READ RECORD (SYS01_DEV,KEY=SYS01X.RECORD_ID$,DOM=*NEXT)SYS01X$
10025 LET SAVE_DECIMALS=TCB(14),xrate=0,decimals=8
10030 PRECISION DECIMALS
10035 while xrate<2
10040 EURO_PER_DOLLAR=SYS01X.EXCH_RATE;if xrate then euro_per_dollar=sys01x.exch_rate1
10050 LET RATE$[xrate]=STR(EURO_PER_DOLLAR)
10060 IF ENTER_RATE=0 THEN xrate=xrate+1;continue
10070 LET V0$="S",V1$="",V2$=RATE$[xrate],V3$="",V0=10,V1=x1,V2=7+xrate
10080 LET V4$="Convert Sales/Expense Euros to Dollars (1 DOLLAR*RATE= X EURO)"
10090 if xrate then LET V4$="Enter the rate for Assets, Liabilities & Equity"
10100 GOSUB 7000
10110 IF V3=0 THEN 
10115   v=num(v$,err=10100)
10120   if xrate then LET SYS01X.EXCH_RATE1=V else SYS01X.EXCH_RATE=V
10125   RATE$[xrate]=STR(V),P=POS("."=RATE$[xrate]);print @(v1,v2),rate$[xrate]
10130   if xrate=0 then LET P0=LEN(RATE$[xrate]),SYS01X.PRECISION=MAX(2,P0-P)
10140   LET SYS01X$=FIELD(SYS01X$)
10150   WRITE RECORD (SYS01_DEV,KEY=SYS01X.RECORD_ID$)SYS01X$
10160 FI 
10161 xrate=xrate+1
10165 wend
10170 PRECISION SAVE_DECIMALS
10200 RETURN

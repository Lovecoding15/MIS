0010 REM "ARM - Customer Maintenance (Aging & Sales)"
0020 REM "Program ARM.MD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0220 ARM02A: IOLIST B0$,B1$(1),B[ALL]
0230 ARM06A: IOLIST X0$(1),X1$(1),X[ALL]
0500 REM " --- Initializations"
0510 DIM X0$(8),X1$(16),X[21],O[3]
0540 LET O[0]=16,O[1]=O[0]+M1,O[2]=O[1]+M1,O[3]=O[2]+M1
0550 LET MK1$="-##0.00%",MK2$="###,##0-"
0560 IF LEN(MK1$)<M1 THEN LET MK1$=FILL(80)+MK1$,MK1$=MK1$(LEN(MK1$)-M1+1)
0570 IF LEN(MK2$)<M1 THEN LET MK2$=FILL(80)+MK2$,MK2$=MK2$(LEN(MK2$)-M1+1)
0700 REM " --- Background"
0710 PRINT @(0,L0),'CE',
0720 PRINT 'SB',@(15,5),"Aging & Sales Summary",@(8,7),"Future:",@(7,8),"Current:",@(7,9),"30-Days:",@(7,10),"60-Days:",@(7,11),"90-Days:",@(6,12),"120-Days:",@(7,13),"Balance:",@(36,7),"Credit Limit:",@(33,8),"Avg Days To Pay:",
0725 PRINT @(36,9),"Last Invoice:",@(36,10),"Last Payment:",
0730 PRINT @(O[2]+M1-6,14),"Prior",@(O[3]+M1-5,14),"Next",@(O[0]+M1-4,15),"MTD",@(O[1]+M1-4,15),"YTD",@(O[2]+M1-5,15),"Year",@(O[3]+M1-6,15),"Month",
0740 PRINT @(9,16),"Sales:",@(10,17),"Cost:",@(2,18),"Gross Profit:",@(1,19),"Gross Profit%:",
0750 PRINT @(2,20),"Avg Days Pay:",@(3,21),"High Credit:"
0790 PRINT 'SF',
1000 REM " --- Find Record"
1020 LET B0$=A0$+"  "
1030 GOSUB 6000
1040 FIND (ARM02_DEV,KEY=B0$,DOM=1050)IOL=ARM02A
1050 FIND (ARM06_DEV,KEY=B0$(1,8),DOM=1060)IOL=ARM06A
1060 GOSUB 5000
1070 GOTO 4000
2000 REM " --- History Input"
2100 REM " --- Days in Aging"
2105 LET X0$(1)=B0$(1,8),X1$(1)=""; DIM X[21]
2110 EXTRACT (ARM06_DEV,KEY=B0$(1,8),DOM=2111)IOL=ARM06A
2111 GOSUB 5000
2120 LET V0$="N",V1$="",V2$=STR(X[6]),V3$=M2$,V0=M2,V1=50,V2=8
2130 GOSUB 7000
2140 LET X[6]=V
2150 GOTO 3900
2180 REM " --- Last Invoice"
2190 LET V0$="D",V2$=X1$(1,3),V0=8,V1=50,V2=9,V3$=""
2200 GOSUB 7000
2220 LET X1$(1,3)=V$
2230 GOTO 3900
2300 REM " --- Last Payment"
2310 LET V0$="D",V2$=X1$(4,3),V0=8,V1=50,V2=10,V3$=""
2315 IF V2$="   " THEN LET V2$=""
2320 GOSUB 7000
2330 LET X1$(4,3)=V$
2340 GOTO 3900
2400 REM " --- Sales Figures
2402 LET X1=0,X2=16
2410 FOR Y=0 TO 5
2430 LET V0$="N",V2$=STR(X[Y]),V3$=M1$,V0=M1,V1=O[X1],V2=X2
2440 GOSUB 7000
2450 LET X[Y]=V
2460 IF V3=4 THEN GOTO 2800
2470 IF X=0 AND V3=2 THEN LET I0=I0-1; GOTO 2300
2480 IF V3=2 THEN GOSUB 2600; LET Y=Y-1; GOTO 2430
2490 LET X1=X1+1
2500 IF X1>2 THEN LET X1=0
2510 IF X1=0 THEN LET X2=X2+1
2520 NEXT Y
2530 GOTO 3900
2600 REM " --- Go Back in Matrix"
2610 IF X1=0 AND X2=16 THEN LET I0=I0-1; GOTO 2300
2620 IF X1=0 AND X2=17 THEN LET X1=2; LET X2=16; RETURN 
2630 LET X1=X1-1; RETURN 
2800 REM " --- Write History"
2810 WRITE (ARM06_DEV,KEY=B0$(1,8))IOL=ARM06A
2820 GOTO 4000
3000 REM " --- Avg Days to Pay"
3010 LET X1=0,X2=20
3020 FOR Y=10 TO 14 STEP 2
3030 LET V0$="N",V2$=STR(X[Y]:MK2$),V3$=MK2$,V0=0,V1=O[X1],V2=X2
3040 GOSUB 7000
3050 LET X[Y]=V
3060 IF V3=4 THEN EXITTO 3900
3070 IF Y=10 AND V3=2 THEN EXITTO 3900
3080 IF V3=2 THEN GOSUB 2600; LET Y=Y-2; GOTO 3030
3090 LET X1=X1+1
3100 NEXT Y
3190 GOTO 3900
3200 REM " --- Next Month Sales"
3230 LET V0$="N",V2$=STR(X[8]),V3$=M1$,V0=M1,V1=O[3],V2=16
3240 GOSUB 7000
3250 LET X[8]=V
3290 GOTO 3900
3300 REM " --- Next Month Cost"
3330 LET V0$="N",V2$=STR(X[9]),V3$=M1$,V0=M1,V1=O[3],V2=17
3340 GOSUB 7000
3350 LET X[9]=V
3390 GOTO 3900
3400 REM " --- High Credit"
3410 LET X1=0,X2=21
3420 FOR Y=18 TO 20
3430 LET V0$="N",V2$=STR(X[Y]),V3$=M1$,V0=M1,V1=O[X1],V2=X2
3440 GOSUB 7000
3450 LET X[Y]=V
3460 IF V3=4 THEN EXITTO 3900
3470 IF Y=18 AND V3=2 THEN EXITTO 3900
3480 IF V3=2 THEN GOSUB 2600; LET Y=Y-1; GOTO 3430
3490 LET X1=X1+1
3500 NEXT Y
3520 GOTO 3900
3600 REM "--- Next Month Avg Days to Pay"
3630 LET V0$="N",V2$=STR(X[16]:MK2$),V3$=MK2$,V0=0,V1=O[3],V2=20
3640 GOSUB 7000
3650 LET X[16]=V
3690 GOTO 3900
3700 REM "--- Next Month High Credit"
3730 LET V0$="N",V2$=STR(X[21]),V3$=M1$,V0=M1,V1=O[3],V2=21
3740 GOSUB 7000
3750 LET X[21]=V
3790 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2100,2180,2300,2400,3000,3400,3200,3300,3600,3700,2800,4000
4000 REM " --- Continue"
4010 LET V0$="S",V1$="CS",V2$="",V3$="",V4$="<Enter>=Continue",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4022 LET V3=0
4025 IF V$="HISTORY" THEN LET I0=-1; GOTO 3900
4026 PRINT @(0,22),'CL',
4040 RUN "ARM.MA"
5000 REM "DISPLAY"
5020 PRINT @(16,7),B[0]:M1$,@(16,8),B[1]:M1$,@(16,9),B[2]:M1$,@(16,10),B[3]:M1$,@(16,11),B[4]:M1$,@(16,12),B[5]:M1$,@(16,13),B[0]+B[1]+B[2]+B[3]+B[4]+B[5]:M1$,@(50,7),B[6]:M1$,@(50,8),X[6]:M2$,@(50,9),FNB$(X1$(1,3)),@(50,10),FNB$(X1$(4,3))
5030 PRINT @(O[0],16),X[0]:M1$,@(O[1],16),X[1]:M1$,@(O[2],16),X[2]:M1$,@(O[3],16),X[8]:M1$
5040 PRINT @(O[0],17),X[3]:M1$,@(O[1],17),X[4]:M1$,@(O[2],17),X[5]:M1$,@(O[3],17),X[9]:M1$
5050 PRINT @(O[0],18),X[0]-X[3]:M1$,@(O[1],18),X[1]-X[4]:M1$,@(O[2],18),X[2]-X[5]:M1$,@(O[3],18),X[8]-X[9]:M1$
5053 PRINT @(O[0],20),X[10]:MK2$,@(O[1],20),X[12]:MK2$,@(O[2],20),X[14]:MK2$,@(O[3],20),X[16]:MK2$
5054 PRINT @(O[0],21),X[18]:M1$,@(O[1],21),X[19]:M1$,@(O[2],21),X[20]:M1$,@(O[3],21),X[21]:M1$
5055 IF X[0]<>0 THEN LET T1=(X[0]-X[3])*100/X[0] ELSE LET T1=0
5060 IF X[1]<>0 THEN LET T2=(X[1]-X[4])*100/X[1] ELSE LET T2=0
5065 IF X[2]<>0 THEN LET T3=(X[2]-X[5])*100/X[2] ELSE LET T3=0
5068 IF X[8]<>0 THEN LET T4=(X[8]-X[9])*100/X[8] ELSE LET T4=0
5070 PRINT @(O[0],19),T1:MK1$,@(O[1],19),T2:MK1$,@(O[2],19),T3:MK1$,@(O[3],19),T4:MK1$
5080 IF B1$(39,1)="Y" THEN PRINT @(36,12),"***Credit Hold***"
5090 RETURN 
6000 REM " --- Search for Open Invoices"
6030 READ (ART01_DEV,KEY=N0$+"  "+B0$(3,6),DOM=6035)
6035 LET K$=KEY(ART01_DEV,END=6070)
6040 IF K$(5,6)<>B0$(3,6) THEN GOTO 6060
6050 GOTO 6095
6070 EXTRACT (ARM02_DEV,KEY=B0$,DOM=6075)IOL=ARM02A
6075 FOR X=0 TO 5
6080 LET B[X]=0
6085 NEXT X
6090 WRITE (ARM02_DEV,KEY=B0$)IOL=ARM02A
6095 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 LET O0=0; RUN "ARM.MA"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

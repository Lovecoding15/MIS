0010 REM "GLM - G/L Posting Control Maintenance"
0020 REM "Program GLM.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-03",FILES$[2]="GLM-06",FILES$[3]=""
0140 LET FILES$[4]="SYM-09",FILES$[5]="SYM-19",FILES$[6]="SYS-01"
0160 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM03_DEV=CHANNELS[1],GLM06_DEV=CHANNELS[2]
0185 LET SYM09_DEV=CHANNELS[4],SYM19_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0200 REM " --- IOLists
0210 SYM19A: IOLIST A0$,A1$(1),A2$,A3$,A[ALL]
0220 GLM06A: IOLIST B0$(1),B1$(1),B2$(1),B3$,B[ALL]
0230 GLM03A: IOLIST C0$(1),C1$,C[ALL]
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N4$=F5$,NUMBER=1,M1$="000000"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)
0500 REM " --- Initializations
0510 DIM A1$(30),A[3],B0$(20),B1$(64),B2$(30),B[6],C0$(32),C[2],D0$(32)
0520 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
0530 LET B0$(1)=N0$
0590 LET RECORD$="",I0=0,DESCRIPTION$[1]="Description",M1=LEN(M1$)
0700 REM " --- Background
0750 PRINT 'SB',@(28,3),"Process ID:",@(26,4),"Program Name:",@(27,5),"Description:",@(28,6),"Journal ID:",@(21,7),"Use Audit Control:",@(21,8),"Last Audit Number:",@(22,9),"Use Summary File:",@(21,10),"Summary File Name:",@(21,11),"Print G/L Summary:",@(15,12),"Summarized Posting Memo:",@(19,13),"Detail Transactions:",@(29,14),"Last Used:",@(26,15),"Last Updated:",'SF',
0900 REM " --- Position file"
0990 READ (GLM06_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Process ID"
1005 PRINT 'CF',
1010 LET V2$=""
1020 LET GLM06_K$=KEY(GLM06_DEV,END=1040)
1025 IF GLM06_K$(1,2)<>N0$ THEN GOTO 1040
1030 LET V2$=GLM06_K$(3,10)
1040 LET V0$="S",V1$="C",V3$="",V0=10,V1=40,V2=3,I0=0
1045 LET V4$="Enter Process ID (<F3>=Lookup)"
1050 GOSUB 7000
1055 ON V3 GOTO 1070,1000,1000,1060,4500
1060 GOSUB 6000
1070 LET V$=V$+FILL(V0),V$=V$(1,V0)
1080 IF V$<>GLM06_K$(3,10) THEN READ (GLM06_DEV,KEY=N0$+V$,DOM=1090)
1090 LET B0$(3)=V$
1095 PRINT @(V1,V2),B0$(3,V0)
1100 REM " --- Program Name"
1110 LET V2$=""
1120 LET GLM06_K$=KEY(GLM06_DEV,END=1000)
1125 IF GLM06_K$(1,12)<>B0$(1,12) THEN GOTO 1000
1130 LET V2$=GLM06_K$(13,8)
1140 LET V0$="S",V1$="C",V3$="",V0=8,V1=40,V2=4
1145 LET V4$="Enter Program Name (<F3>=Lookup)"
1150 GOSUB 7000
1155 ON V3 GOTO 1170,1100,1000,1160,1000
1160 GOSUB 6050
1170 LET V$=V$+FILL(V0),V$=V$(1,V0)
1180 READ (GLM06_DEV,KEY=B0$(1,12)+V$,DOM=1100)
1190 LET B0$(13)=V$
1195 PRINT @(V1,V2),B0$(13,V0)
1200 REM " --- Retrieve record
1220 READ (GLM06_DEV,KEY=B0$,DOM=1000)IOL=GLM06A
1300 REM " --- Display record
1320 GOSUB 5000
1390 GOTO 4000
1700 REM " --- Journal ID Code
1710 LET V0=2,V0$="S",V1$="CR",V2$=B1$(1,V0),V3$="",V1=40,V2=6,I0=1
1720 LET V4$="Enter The Journal ID Code To Be Used By This Program (<F3>=Lookup)"
1730 GOSUB 7000
1740 ON V3 GOTO 1765,1700,3900,1750,3900
1750 GOSUB 6100
1760 IF V$="" THEN GOTO 1700
1770 LET V$=V$+"  ",V$=V$(1,2)
1775 FIND (GLM03_DEV,KEY=N0$+V$,DOM=1700)IOL=GLM03A
1780 LET B1$(1,V0)=V$
1785 PRINT @(V1,V2),B1$(1,V0)," ",C0$(5,20),
1790 GOTO 3900
2200 REM " --- Print G/L Summary
2205 IF B1$(38,1)<>"Y" THEN GOTO 3900
2210 LET V4$="Y=Print G/L Summary     N=Don't Print G/L Summary"
2220 LET V0$="Y",V1$="CR",V2$=B1$(40,1),V3$="",V0=1,V1=40,V2=11
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 IF POS(V$="YN")=0 THEN GOTO 2200
2260 LET B1$(40,1)=V$
2270 PRINT @(V1,V2),B1$(40,1),
2290 GOTO 3900
2300 REM " --- Summarized Posting Memo
2310 LET V0$="S",V1$="C",V2$=B2$,V3$="",V0=30,V1=40,V2=12
2315 IF FNP$(V2$)="" THEN LET V2$="Summarized "+A1$
2320 LET V4$="Enter The Default Summarized Posting Memo For This Task"
2330 GOSUB 7000
2340 ON V3 GOTO 2350,2300,3900,2300,3900
2350 LET B2$(1)=V$
2360 PRINT @(V1,V2),B2$,
2390 GOTO 3900
2400 REM " --- Summary/Detail?
2410 LET V4$="Y=Detailed Postings From Task   N=Summarized Postings   A=Use G/L Account"
2420 LET V0$="S",V1$="CR",V2$=B1$(39,1),V3$="YNA",V0=1,V1=40,V2=13
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 IF POS(V$="YNA")=0 THEN GOTO 2400
2460 LET B1$(39,1)=V$
2470 PRINT @(V1,V2),B1$(39,1),
2490 GOTO 3900
3900 REM " --- Next field
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1700,2200,2300,2400,3990
3990 LET I0=0
4000 REM " --- Correct?
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="Y",V1$="CR",V2$="Y",V3$="",V0=1,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record"
4130 WRITE (GLM06_DEV,KEY=B0$)IOL=GLM06A
4190 GOTO 1000
4500 REM " --- Print?
4510 LET V4$="Print A List Of All Records (Y/N)?"
4520 LET V0$="Y",V1$="C",V2$="N",V3$="",V0=1,V1=FNV(V4$),V2=22
4530 GOSUB 7000
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4900,9900
4900 REM " --- Run report overlay
4990 RUN "GLM.BB"
5000 REM " --- Display record
5010 DIM C0$(32),C[2]
5020 FIND (GLM03_DEV,KEY=N0$+B1$(1,2),DOM=5025)IOL=GLM03A
5025 PRINT @(40,5),A1$,@(40,6),B1$(1,2)," ",C0$(5,20),
5030 LET V1=40,V2=7,X$="None"
5035 IF B1$(38,1)="Y" THEN LET X$=B1$(31,6)
5040 GOSUB 5400
5045 PRINT @(40,9),B1$(38,1),@(40,10),X$,@(40,11),B1$(40,1),@(40,12),B2$,@(40,13),B1$(39,1),
5070 IF FNP$(B1$(11,6))<>"" THEN PRINT @(40,14),FNB6$(B1$(11,6))," ",FNT$(B1$(17,4)),
5080 IF FNP$(B1$(21,6))<>"" THEN PRINT @(40,15),FNB6$(B1$(21,6))," ",FNT$(B1$(27,4)),
5090 RETURN 
5400 REM " --- Display G/L Audit Control"
5410 PRINT @(V1,V2),B1$(37,1),
5420 IF B1$(37,1)="Y" THEN PRINT @(V1,V2+1),B[0]:M1$
5430 IF B1$(37,1)="N" THEN PRINT @(V1,V2+1),"None",'CL',
5490 RETURN 
6000 REM " --- Process ID Lookup"
6010 LET NMBR=1
6015 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6020 LET DESCRIPT$[0]="Process",FIELD[0]=1,POSITION[0]=1,LENGTH[0]=10
6025 LET DESCRIPT$[1]="Description",FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6030 LET V$="NO COMPANY"
6035 CALL "SYC.LK",SYM09_DEV,"",NMBR,"Process ID Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6040 RETURN 
6050 REM " --- Process Program Lookup"
6060 LET NMBR=1
6065 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6070 LET DESCRIPT$[0]="Program",FIELD[0]=1,POSITION[0]=11,LENGTH[0]=8
6075 LET DESCRIPT$[1]="Description",FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6080 LET V$="NO COMPANY"
6085 CALL "SYC.LK",SYM19_DEV,B0$(3,10),NMBR,"Process Program Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6090 RETURN 
6100 REM " --- Journal ID Lookup"
6110 LET NMBR=1
6115 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6120 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=3,LENGTH[0]=2
6125 LET DESCRIPT$[1]="Journal",FIELD[1]=1,POSITION[1]=5,LENGTH[1]=20
6130 LET V$="SKIP"
6135 CALL "SYC.LK",GLM03_DEV,"",NMBR,"Journal ID Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6140 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8070 DEF FNT$(Q$)=Q$(1,2)+":"+Q$(3,2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "OPP - Parameter Maintenance For Order Processing"
0020 REM "Program OPP.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="ARM-10"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],ARM10_DEV=CHANNELS[2]
0200 REM " --- IOLists"
0210 ARS01A: IOLIST A0$,A1$,A2$(1),A3$(1),A4$(1),A5$,A6$,A7$,A8$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,X$,G0$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$
0440 LET P7=15,P8=6,P9=20
0450 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0460 LET G0=NUM(G0$(1,2))
0470 LET R9$="N"
0480 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=0500)
0490 LET R9$="Y"
0500 REM " --- Initialize Data"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(80," ")
0520 LET K0=0,I0=0,K9=6,L0=3
0530 LET A0$=N0$+N2$+"00"
0540 DIM A2$(6),A3$(114),A4$(4); REM "DIM A3$ IN PGM ARP.AB TO SAME VALUE
0550 LET A3$(1,3)="NNN"
0560 LET A2$(3,2)=STR(P8:"00")
0570 LET M9$="$#####",M8$="$##.00"
0610 DIM INFO$[20]
0620 CALL "SYC.VA","OP",INFO$[ALL]
0630 LET GL$=INFO$[9]
0640 IF GL$="N" THEN LET A3$(79,1)="N"
0700 REM " --- Background"
0710 PRINT @(0,4),'CE',
0720 PRINT 'SB',@(18,4),"Customer Job Numbers:",@(45),"Months To Retain Jobs:",@(28,5),"Cash Sales:",@(44,5),"Invoice Detail History:",@(9,6),"Standard Cash Customer Number:",@(6,7),"Starting Customer Comment Number:",@(53,7),"Ending Number:",@(0,8),"Invoice History Printer Program To Run:",
0725 PRINT @(12,9),"# Days to Commit in Future: ",@(47,9),"Last Committed Date: ",
0730 PRINT @(18,10),"Minimum Order Amount:",@(49,10),"Average Lead Time:",@(19,11),"Minimum Line Amount:",@(14,12),"Minimum Cash Sale Amount:",@(52),"New Customer Defaults",@(18,13),"Skip Line Code Entry:",@(55),"Sale Discount:",@(21,14),"Default Line Code:",@(59),"Territory:",
0740 PRINT @(28,15),"Backorders:",@(57),"Salesperson:",@(14,16),"Enter Cost For Drop Ship:",@(60),"Tax Code:",@(0,17),"Warehouse Entry Required For Drop Ship:",@(58),"Terms Code:",@(13,18),"Receipt Printer On System:",@(51),"Distribution Code:",
0750 PRINT @(8,19),"Counter Printer Program To Run:",@(48),"Customer Inv Dtl His:",@(8,20),"Receipt Printer Program To Run:"
0755 PRINT @(9,21),"Print GL Sales Detail Report?:"
0790 PRINT 'SF'
1200 REM " --- Find Key"
1220 FIND (SYS01_DEV,KEY=A0$(1,K9),DOM=1400)IOL=ARS01A
1230 IF LEN(A3$)<78 THEN LET A3$=A3$+J1$(1,78-LEN(A3$))
1240 LET P0=NUM(A2$(1,2))
1280 IF POS(" "<>A3$(21))=0 THEN LET A3$(15)=""
1290 IF GL$="N" THEN LET A3$(79,1)="N"
1300 REM " --- Display"
1320 GOSUB 5000
1390 GOTO 4000
2000 REM " --- Job Numbers"
2020 LET V0$="Y",V1$="C",V2$=A3$(12,1),V3$="YN",V4$="Y= Yes, N= No",V0=1,V1=40,V2=4,I0=0
2030 GOSUB 7000
2050 LET A3$(12,1)=V$
2090 GOTO 3900
2100 REM " --- Cash Sales"
2120 LET V0$="Y",V1$="C",V2$=A3$(15,1),V3$="YN",V4$="Should Cash Sales be Allowed?",V0=1,V1=40,V2=5
2130 GOSUB 7000
2150 LET A3$(15,1)=V$
2190 GOTO 3900
2200 REM " --- Cash Number"
2210 IF A3$(15,1)="N" THEN LET A3$(16,6)=""; GOTO 3900
2220 LET V0$="M",V1$="C",V2$=A3$(16,P0),V3$=A1$,V4$="Enter Cash Customer Number",V0=NUM(A2$(1,2)),V1=40,V2=6
2230 GOSUB 7000
2240 IF LEN(V$)<6 THEN LET V$=V$+J0$(1,6-LEN(V$))
2250 LET A3$(16,6)=V$
2290 GOTO 3900
2300 REM " --- Starting Comments Line"
2320 LET V0$="Z",V1$="C",V2$=A3$(24,2),V4$="Enter Starting Comments Line Number (1-99)",V0=2,V1=40,V2=7
2330 GOSUB 7000
2340 IF V$="00" THEN GOTO 2320
2350 LET A3$(24,2)=V$
2390 GOTO 3900
2400 REM " --- Ending Comments Line"
2420 LET V0$="Z",V1$="C",V2$=A3$(26,2),V4$="Enter Ending Comments Line Number (1-99), Max Display is 6 Lines.",V0=2,V1=70,V2=7
2430 GOSUB 7000
2440 IF V$<A3$(24,2) THEN GOTO 2420
2450 LET A3$(26,2)=V$
2460 LET LAST=NUM(A3$(26,2)),FIRST=NUM(A3$(24,2))
2470 IF LAST-FIRST>6 THEN LET V0$="S",V1$="C",V2$="",V3$="",V4$='RB'+"Entry Will be Faster with Maximum 6 Comment Lines. ",V0=1,V1=FNV(V4$),V2=22; GOSUB 7000
2490 GOTO 3900
2500 REM " --- Invoice Detail History"
2520 LET V0$="Y",V1$="C",V2$=A3$(77,1),V3$="",V4$="Keep Invoice Detail History File (Y/N)?",V0=1,V1=68,V2=5
2530 GOSUB 7000
2540 IF V3=4 THEN GOTO 4000 ELSE IF V3=2 THEN GOTO 3900
2550 LET A3$(77,1)=V$
2555 IF A3$(77,1)="N" THEN LET A3$(80,6)=""; PRINT @(40,9),J1$(1,6),
2560 GOTO 3900
2600 REM " --- Minimum Order Amount"
2620 LET V0$="N",V1$="C",V2$=A3$(29,5),V3$=M9$,V4$="Enter Minimum Order Amount In Whole Dollars",V0=5,V1=40,V2=10
2630 GOSUB 7000
2640 IF NUM(V$)<0 THEN GOTO 2620
2650 LET A3$(29,5)=V$
2690 GOTO 3900
2700 REM " --- Minumum Line Amount"
2720 LET V0$="N",V1$="C",V2$=A3$(34,5),V3$=M9$,V4$="Enter Minimum Line Extension Amount In Whole Dollars",V0=5,V1=40,V2=11
2730 GOSUB 7000
2740 IF NUM(V$)<0 THEN GOTO 2720
2750 LET A3$(34,5)=V$
2790 GOTO 3900
2800 REM " --- Minimum Cash Amount"
2810 IF A3$(15,1)="N" THEN PRINT @(40,12),J1$(1,10),; LET A3$(45,5)=""; GOTO 2890
2820 LET V0$="N",V1$="C",V2$=A3$(45,5),V3$=M8$,V4$="Enter Minimum Cash Sale Amount",V0=5,V1=40,V2=12
2830 GOSUB 7000
2840 IF NUM(V$)<0 THEN GOTO 2720
2850 LET A3$(45,5)=V$
2890 GOTO 3900
2900 REM " --- Skip Line Code"
2920 LET V0$="Y",V1$="CR",V2$=A3$(39,1),V4$="Y= Yes, N= No",V0=1,V1=40,V2=13
2930 GOSUB 7000
2950 LET A3$(39,1)=V$
2990 GOTO 3900
3000 REM " --- Default Line Code"
3020 LET V0$="S",V1$="CR",V2$=A3$(40,1),V4$="Enter Default Line Code (<F3>=Lookup)",V0=1,V1=40,V2=14
3030 GOSUB 7000
3040 IF V3=3 THEN LET RECORD$="E"; GOSUB CODE_LOOKUP; IF V$="" THEN GOTO 3000
3050 LET A3$(40,1)=V$
3060 IF A3$(40,1)<>" " THEN GOTO 3090
3070 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Order Entry Will not Operate without a Default Line Code. <Enter> to Continue. ",V0=1,V1=FNV(V4$),V2=22
3075 GOSUB 7000
3080 PRINT @(V1,V2),A3$(40,1),
3090 GOTO 3900
3100 REM " --- Backorders"
3120 LET V0$="Y",V1$="CR",V2$=A3$(42,1),V4$="Y= Yes, N= No",V0=1,V1=40,V2=15
3130 GOSUB 7000
3150 LET A3$(42,1)=V$
3190 GOTO 3900
3200 REM " --- Cost On Dropship"
3220 LET V0$="Y",V1$="CR",V2$=A3$(28,1),V4$="Y= Yes, N= No",V0=1,V1=40,V2=16
3230 GOSUB 7000
3250 LET A3$(28,1)=V$
3290 GOTO 3900
3300 REM " --- Warehouse On Drop Ship"
3320 LET V0$="Y",V1$="R",V2$=A3$(41,1),V4$="Y= Yes, N= No",V0=1,V1=40,V2=17
3330 GOSUB 7000
3350 LET A3$(41,1)=V$
3390 GOTO 3900
3400 REM " --- Months To Retain Jobs"
3410 IF A3$(12,1)="N" THEN PRINT @(68,4),"NA",; GOTO 3900
3420 LET V0$="N",V1$="C",V2$=A3$(43,2),V3$="##",V4$="Enter Number Of Months To Retain Job Numbers",V0=2,V1=68,V2=4
3430 GOSUB 7000
3450 LET A3$(43,2)=V$
3490 GOTO 3900
3500 REM " --- Receipt Printer"
3520 LET V0$="Y",V1$="CR",V2$=A3$(64,1),V4$="Y= Yes, N= No",V0=1,V1=40,V2=18
3530 GOSUB 7000
3550 LET A3$(64,1)=V$
3590 GOTO 3900
3600 REM " --- Counter Printer Program"
3620 LET V0$="S",V1$="CR",V2$=A3$(65,6),V4$="Enter Program To Run",V0=6,V1=40,V2=19
3625 IF V$="N" THEN LET A3$(71,6)=""; PRINT @(40,20),J1$(1,10),
3630 GOSUB 7000
3650 LET A3$(65,6)=V$
3690 GOTO 3900
3700 REM " --- Receipt Printer Program"
3710 IF A3$(64,1)="N" THEN GOTO 3790
3720 LET V0$="S",V1$="CR",V2$=A3$(71,6),V4$="Enter Program To Run",V0=6,V1=40,V2=20
3730 GOSUB 7000
3750 LET A3$(71,6)=V$
3790 GOTO 3900
3800 REM " --- Print GL Detail Report?"
3810 LET V0$="Y",V1$="C",V2$=A3$(79,1),V3$="",V4$="Print the General Ledger Detail Report with the Sales Register? ",V0=1,V1=40,V2=21
3815 IF GL$="N" THEN LET A3$(79,1)="N"; PRINT @(V1,V2),A3$(79,1),; GOTO 3890
3820 GOSUB 7000
3849 LET A3$(79,1)=V$
3890 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2000,3400,2100,2500,2200,2300,2400,6300,6400,2600,6500,2700,2800,2900,3000,3100,3200,3300,3500,3600,3700,3800,4400,4500,4600,4700,4800,4900,4300,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4025 IF V3=4 THEN GOTO 9900
4030 IF V$="DELETE" THEN GOTO 4200
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4110 EXTRACT (SYS01_DEV,KEY=A0$(1,K9),DOM=4120)
4120 WRITE (SYS01_DEV,KEY=A0$(1,K9))IOL=ARS01A
4190 RUN "SYS.AA"
4200 REM " --- Remove"
4290 GOTO 9900
4300 REM " --- Default For New Customers For Retention Of Invoice Detail History"
4305 IF A3$(77,1)<>"N" THEN GOTO 4320 ELSE LET A3$(78,1)="N"
4310 PRINT @(70,19),A3$(78,1),; GOTO 3900
4320 LET V0$="Y",V1$="CR",V2$=A3$(78,1),V3$="",V0=1,V1=70,V2=19
4330 LET V4$="Enter Default For Retention Of Cust Invoice Detail History (Y/N)?"
4340 GOSUB 7000
4350 LET A3$(78,1)=V$
4390 GOTO 3900
4400 REM " --- Sale Discount"
4420 LET V0$="S",V1$="CR",V2$=A3$(50,2),V3$="",V4$="Enter Default Sales Discount Code (<F3>=Lookup)",V0=2,V1=70,V2=13
4430 GOSUB 7000
4435 ON V3 GOTO 4450,4400,4490,4440,4490
4440 LET RECORD$="I"; GOSUB CODE_LOOKUP; IF V$="" THEN GOTO 4400
4450 LET V$=V$+FILL(V0),V$=V$(1,V0)
4460 FIND (ARM10_DEV,KEY=N0$+"I"+V$,DOM=4400)
4470 LET A3$(50,2)=V$
4480 PRINT @(V1,V2),A3$(50,2),
4490 GOTO 3900
4500 REM " --- Territory
4520 LET V0$="S",V1$="CR",V2$=A3$(52,3),V3$="",V4$="Enter Default Territory Code (<F3>=Lookup)",V0=3,V1=70,V2=14
4530 GOSUB 7000
4535 ON V3 GOTO 4550,4500,4590,4540,4590
4540 LET RECORD$="H"; GOSUB CODE_LOOKUP; IF V$="" THEN GOTO 4500
4550 LET V$=V$+FILL(V0),V$=V$(1,V0)
4560 FIND (ARM10_DEV,KEY=N0$+"H"+V$,DOM=4500)
4570 LET A3$(52,3)=V$
4580 PRINT @(V1,V2),A3$(52,3),
4590 GOTO 3900
4600 REM " --- Salesperson"
4620 LET V0$="S",V1$="CR",V2$=A3$(55,3),V3$="",V4$="Enter Default Salesperson (<F3>=Lookup)",V0=3,V1=70,V2=15
4630 GOSUB 7000
4635 ON V3 GOTO 4650,4600,4690,4640,4690
4640 LET RECORD$="F"; GOSUB CODE_LOOKUP; IF V$="" THEN GOTO 4600
4650 LET V$=V$+FILL(V0),V$=V$(1,V0)
4660 FIND (ARM10_DEV,KEY=N0$+"F"+V$,DOM=4600)
4670 LET A3$(55,3)=V$
4680 PRINT @(V1,V2),A3$(55,3),
4690 GOTO 3900
4700 REM " --- Tax"
4720 LET V0$="S",V1$="CR",V2$=A3$(58,2),V3$="",V4$="Enter Default Tax Code (<F3>=Lookup)",V0=2,V1=70,V2=16
4730 GOSUB 7000
4735 ON V3 GOTO 4750,4700,4790,4740,4790
4740 LET RECORD$="B"; GOSUB CODE_LOOKUP; IF V$="" THEN GOTO 4700
4750 LET V$=V$+FILL(V0),V$=V$(1,V0)
4760 FIND (ARM10_DEV,KEY=N0$+"B"+V$,DOM=4700)
4770 LET A3$(58,2)=V$
4780 PRINT @(V1,V2),A3$(58,2),
4790 GOTO 3900
4800 REM " --- Terms"
4820 LET V0$="S",V1$="CR",V2$=A3$(60,2),V3$="",V4$="Enter Default Terms Code (<F3>=Lookup)",V0=2,V1=70,V2=17
4830 GOSUB 7000
4835 ON V3 GOTO 4850,4800,4890,4840,4890
4840 LET RECORD$="A"; GOSUB CODE_LOOKUP; IF V$="" THEN GOTO 4800
4850 LET V$=V$+FILL(V0),V$=V$(1,V0)
4860 FIND (ARM10_DEV,KEY=N0$+"A"+V$,DOM=4800)
4870 LET A3$(60,2)=V$
4880 PRINT @(V1,V2),A3$(60,2),
4890 GOTO 3900
4900 REM " --- Distribution"
4920 LET V0$="S",V1$="CR",V2$=A3$(62,2),V3$="",V4$="Enter Default Distribution Code (<F3>=Lookup)",V0=2,V1=70,V2=18
4930 GOSUB 7000
4935 ON V3 GOTO 4950,4900,4990,4940,4990
4940 LET RECORD$="D"; GOSUB CODE_LOOKUP; IF V$="" THEN GOTO 4900
4950 LET V$=V$+FILL(V0),V$=V$(1,V0)
4960 FIND (ARM10_DEV,KEY=N0$+"D"+V$,DOM=4900)
4970 LET A3$(62,2)=V$
4980 PRINT @(V1,V2),A3$(62,2),
4990 GOTO 3900
5000 REM " --- Display"
5020 PRINT @(40,4),A3$(12,1),@(68,4),A3$(43,2),@(40,5),A3$(15,1),@(68,5),A3$(77,1),@(40,6),NUM(A3$(16,P0)):A1$,@(40,7),A3$(24,2),@(70,7),A3$(26,2),
5025 IF A3$(77,1)="Y" THEN PRINT @(40,8),A3$(80,6),
5027 PRINT @(40,9),A3$(86,3),@(70,9),FNB$(A3$(89)),
5030 PRINT @(40,10),NUM(A3$(29,5)):M9$,@(70,10),NUM(A3$(92,2)):"#0",@(40,11),NUM(A3$(34,5)):M9$,@(40,12),NUM(A3$(45,5)):M8$,@(40,13),A3$(39,1),@(70,13),A3$(50,2),@(40,14),A3$(40,1),@(70,14),A3$(52,3),@(40,15),A3$(42,1),@(70,15),A3$(55,3),@(40,16),A3$(28,1),@(70,16),A3$(58,2),@(40,17),A3$(41,1),@(70,17),A3$(60,2),@(40,18),A3$(64,1),@(70,18),A3$(62,2),@(40,19),A3$(65,6),@(70,19),A3$(78,1),@(40,20),A3$(71,6),
5040 PRINT @(40,21),A3$(79,1),
5090 RETURN 
6000 REM " --- Code Lookup"
6005 CODE_LOOKUP: 
6010 LET NUMBER=1
6015 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6020 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6030 ON POS(RECORD$="ABDEFHI") GOTO 6090,6040,6040,6040,6050,6060,6060,6040
6040 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2,FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6045 GOTO 6070
6050 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=1,FIELD[1]=1,POSITION[1]=5,LENGTH[1]=20
6055 GOTO 6070
6060 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3,FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
6070 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,V$="SKIP",ROW=V2+2
6080 CALL "SYC.LK",ARM10_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
6300 REM " --- Invoice History Program"
6310 IF A3$(77,1)="N" THEN LET A3$(80,6)=""; PRINT @(40,8),J1$(1,6),; GOTO 6390
6320 LET V0$="S",V1$="CR",V2$=A3$(80,6),V4$="Enter Program To Run",V0=6,V1=40,V2=8
6330 GOSUB 7000
6350 LET A3$(80,6)=V$
6390 GOTO 3900
6400 REM " --- Number Of Days In Future To Commit"
6410 LET V0$="Z",V1$="C",V2$=A3$(86,3),V3$="000",V4$="Commit Inventory for All Orders With Ship Date Within This Range",V0=3,V1=40,V2=9
6420 GOSUB 7000
6430 LET A3$(86,3)=V$
6490 GOTO 3900
6500 REM " --- Average Lead Time"
6510 LET V0$="Z",V1$="C",V2$=A3$(92,2),V3$="",V4$="Enter Default # of Days Between Order Date and Ship Date, <Enter>=Same.",V0=2,V1=70,V2=10
6520 GOSUB 7000
6530 LET A3$(92,2)=V$
6590 GOTO 3900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 4190
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

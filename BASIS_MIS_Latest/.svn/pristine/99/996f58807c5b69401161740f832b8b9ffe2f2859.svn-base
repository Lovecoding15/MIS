0010 REM "SYC - Sub Menu Processing Using Lightbar"
0020 REM "Program SYC.OA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "MENUS       : Number of menus to load/process (input)"
0050 REM "MENUS$[n]   : Menu names in sequence to display (input)"
0055 REM "WIN_X       : Column location for menu window (input)"
0060 REM "WIN_Y       : Row location for menu window (input)"
0065 REM "DESCRIPTION$: Description of selected task (returned)"
0070 REM "SELECTION$  : Program name for selected task (returned)"
0075 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SYM-01",FILES$[3]="SYM-11"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],SYM01_DEV=CHANNELS[2],SYM11_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0430 LET MAXOPT=15,MAXLINES=20,SECURITY$=F0$(15,1)
0500 REM " --- Initializations"
0510 DIM OPTLIST$[2,MAXLINES,6],MORE$[2,MAXLINES,1]
0550 LET OPTIONS=0,WIDTH=0,TITLE$="",DESCRIPTION$="",SELECTION$="",MENU$=""
1000 REM " --- Load menus"
1010 FOR M=1 TO MENUS
1020 LET PGM$=MENUS$[M]
1030 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTIONS,OPTLIST$[ALL],MENU$,X,X$,NAME$
1040 IF TITLE$="" THEN LET TITLE$=MENU$
1050 NEXT M
1090 IF OPTIONS<1 THEN GOTO 4000
1100 REM " --- Find longest option"
1110 FOR M=1 TO OPTIONS
1120 LET WIDTH=MAX(WIDTH,LEN(OPTLIST$[1,M,1]))
1130 NEXT M
1200 REM " --- Calculate window dimensions and location"
1220 IF LEN(TITLE$)+4>WIDTH THEN LET WIDTH=LEN(TITLE$)+4
1230 LET HEIGHT=OPTIONS+2,WIDTH=WIDTH+4
1240 IF WIN_Y+HEIGHT>22 THEN LET WIN_Y=24-HEIGHT
1250 IF WIN_X=0 THEN LET WIN_X=INT((80-WIDTH)/2)
1260 IF WIN_X+WIDTH>79 THEN LET WIN_X=79-WIDTH
1500 REM " --- Display menu window"
1510 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
2000 REM " --- Lightbar selection of menu option"
2010 LET MAXROW=HEIGHT-1,PAGE=1,XMODE=0,SELECTION$="",SELECTION=0
2020 CALL "SYC.SA",XMODE,OPTLIST$[ALL],MORE$[ALL],OPTION$,PAGE,MAXROW,NAME$,HEIGHT,WIDTH,FKEY
2030 IF FKEY=4 THEN GOTO 4000
2040 IF OPTION$="END" THEN GOTO 4000
2050 IF OPTION$="PRIOR" THEN GOTO 4000
2110 FOR X=1 TO OPTIONS
2120 IF OPTION$<>OPTLIST$[1,X,2] THEN GOTO 2150
2130 LET SELECTION$=FNP$(OPTION$),SELECTION=X,PASSWORD$=FNP$(OPTLIST$[1,X,6])
2140 LET DESCRIPTION$=FNP$(FNS$(OPTLIST$[1,X,1]))
2190 NEXT X
2500 REM " --- Security?"
2510 IF SELECTION=0 THEN GOTO 4000
2520 IF SECURITY$="" AND PASSWORD$="" THEN GOTO 4000
2530 CALL "SYC.QX","M",SECURITY$,SELECTION,OPTLIST$[ALL],STATUS
2540 IF STATUS<>0 THEN GOTO 2000
4000 REM " --- All done"
4090 GOTO 9900
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNS$(Q$)=CVS(Q$,1)
8085 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 FOR X=1 TO FILES
9915 CLOSE (CHANNELS[X],ERR=9916)
9920 NEXT X
9940 CALL "SYC.WD",NAME$
9945 IF SELECTION$="END" THEN LET SELECTION$=""
9950 EXIT 
9999 END

0010 REM "ARM - Tax Code Maintenance Listing"
0020 REM "Program ARM.JB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0095 PRECISION 4
0100 REM " --- Open/Lock Files"
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 ARM10B: IOLIST A0$(1),A[ALL]
0220 ARM10B2: IOLIST B0$,B[ALL]
0230 GLM01A: IOLIST C0$,C1$(1)
0500 REM " --- Initializations"
0510 DIM A[1],B0$(60),B[1],C1$(35),HEADINGS$[2],O[4],J$(20,"-")
0550 LET WIDTH=132,PAGE=0,HEADINGS$[0]=N1$,HEADINGS$[1]=N4$+" Listing"
0560 LET L9=59,L=L9+1,NUMBER=0,HEADINGS=2,WHEN$=F0$(7,3),CLOCK$=""
0570 LET O[0]=26+M8,O[1]=O[0]+M1+1,O[2]=O[1]+29,O[3]=O[2]+M8+1,O[4]=O[3]+10
0580 LET GLHEADER$="GL Account",G5=LEN(G5$),DW=MIN(35,WIDTH-(O[4]+G5+1))
0590 IF GL$<>"Y" THEN LET GLHEADER$=""
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",2,COLUMN
0900 REM " --- Position file"
0990 READ (ARM10_DEV,KEY=A0$(1,3),DOM=1000)
1000 REM " --- Next record"
1010 LET K$=KEY(ARM10_DEV,END=4000)
1020 IF POS(N0$+"B"=K$)<>1 THEN GOTO 4000
1030 READ (ARM10_DEV)IOL=ARM10B
1040 PRINT @(COLUMN,11),A0$(4,2),
1050 LET TOTAL=A[0],NUMBER=NUMBER+1,X$="",TAXABLE$="No"
1500 REM " --- GL Account?"
1510 DIM C1$(35)
1520 LET ACCOUNT$="",C0$=N0$+A0$(46,G[4])
1530 IF GL$<>"Y" THEN GOTO 2000
1540 IF FNP$(A0$(46,G[4]))="" THEN GOTO 2000
1550 FIND (GLM01_DEV,KEY=C0$,DOM=1560)IOL=GLM01A
1560 LET ACCOUNT$=FNF$(A0$(46,G[3]),G5$)+" "+C1$(1,DW)
2000 REM " --- Format detail line and accumlate total"
2010 FOR X=1 TO 10
2020 LET V$=A0$(26+2*(X-1),2),X$=X$+V$+" ",B0$(1)=N0$+RECORD$+V$
2030 IF FNP$(V$)="" THEN GOTO 2060
2040 FIND (ARM10_DEV2,KEY=B0$(1,K9),DOM=2060)IOL=ARM10B2
2050 LET TOTAL=TOTAL+B[0]
2060 NEXT X
2070 IF A0$(56,1)="Y" THEN LET TAXABLE$="Yes"
3000 REM " --- Print detail line"
3010 IF L>L9 THEN GOSUB 5000
3020 PRINT (7)A0$(4,2),@(5),A0$(6,20),@(24),A[0]:M8$,@(O[0]),A[1]:M1$,@(O[1]),X$,@(O[2]),TOTAL:M8$,@(O[3]+2),TAXABLE$,@(O[4]),ACCOUNT$
3030 LET L=L+1
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF L>L9 THEN GOSUB 5000
4020 PRINT (7)""
4030 PRINT (7)STR(NUMBER)," Total Tax Code(s)"
4090 GOTO 9900
5000 REM " --- Report heading"
5010 LET L=HEADINGS+4
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)"Tax",@(O[0]+M1-8),"Maximum",@(O[1]),J$(1,9)," Tax Codes ",J$(1,9),@(O[2]+M8-6),"Total",@(O[3]),"Freight"
5050 PRINT (7)"Code Description",@(24+M8-7),"Percent",@(O[0]+M1-8),"Tax",@(O[1]),"01 02 03 04 05 06 07 08 09 10",@(O[2]+M8-7),"Percent",@(O[3]),"Taxable?",@(O[4]),GLHEADER$
5060 PRINT (7)""
5090 RETURN 
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

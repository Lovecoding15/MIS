0010 REM "TMC - Called Routine: Contact Hardcopy - Richmark
0020 REM "Program TMC.JA
0030 REM "ADD+ON SOFTWARE VERSION 5.1
0035 REM "12/04/91 by Jerry Koch
0039 REM "MAS * DCF * 8/27/93 * RICHMARK
0040 REM "Copyright(C)1991 MicroAccounting Systems, Inc. ALL RIGHTS RESERVED
0041 REM 
0042 REM "01/06/97 KJS WA: #221 ADS"
0079 REM 
0080 SETERR 9000
0090 ENTER CONTACT$,A1LEN,A2LEN,A3LEN,A4LEN,CHAN[ALL]
0100 REM "OPEN FILES
0101 LET CHAN[7]=UNT; CALL "SYC.GA",CHAN[7],1,"","",V1
0102 IF V1=1 THEN GOTO 9900
0200 REM IOLISTS
0210 IOLIST A0$,A1$(1),A2$(1),A3$(1),A4$(1)
0280 IOLIST X$,X[0],X[1],X[2],X[3],X[4],X[5],X[6],X[7]
0400 REM " --- Parameters"
0401 GOSUB 62000; REM "WA: #221
0410 REM "WA: #221 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=0270
0420 LET N0$=PRM$[FID,TFIRM],N1$=PRM$[FID,TFNAME],N2$="AR",N3$=PRM$[FID,TPNAME]; REM "WA: #221
0425 FIND (CHAN[13],KEY=N0$+"TM00",DOM=9800)P0$,P1$,P2$,*,*
0430 LET I2$=PRM$[AR,TWO],P3$=PRM$[AR,THREE],P4$=PRM$[AR,FOUR],M0$=PRM$[AR,KEYMASK]; REM "WA: #221 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9900)*,*,I2$,P3$,P4$,M0$
0440 LET PERIOD=NUM(PRM$[AR,CPERIOD]); REM "WA: #221
0495 REM "WA: #221 CLOSE (SYS01_DEV); LET X$=STBL("!CLEAR","SYS01_DEV",ERR=0496)
0500 REM --INIT DATA--
0505 DIM A1$(A1LEN),A2$(A2LEN),A3$(A3LEN),A4$(A4LEN)
0510 LET H0=40,H1=70,H=0,H1$=N1$,N2$=N3$,L9=50,L=L9+1
0520 LET PAGE=1
0530 DIM J1$(80),J0$(10,"0"),J$(80,"-"),B[6]
0540 LET M7$="00000",M8$="(###)-###-####"
0560 LET L0=1,X1=1,X0=0
0561 DIM P[2]
0562 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(3,2)),P[2]=NUM(P3$(4,1))
0563 IF P[2]=9 THEN LET M7$=M7$+"-0000"
0580 LET M1$="-###,###",M2$="-###,###.00"
0600 REM "SET PARAMETERS
0800 READ (CHAN[1],KNUM=0,KEY=CONTACT$,DOM=4050)IOL=0210
0805 LET CUST$=A0$(253,6)
0850 PRINT @(0,22),'CL',@(10,22),"Printing ..."
1000 REM "--- Init Read
1050 READ (CHAN[1],KNUM=0,KEY=CONTACT$,DOM=4050)IOL=0210
1100 GOSUB 5000
1110 IF P0$(16,1)="Y" THEN PRINT (CHAN[7])@(0),"Firm: ",A0$(3,20),@(27),"Cont: ",A0$(23,15), ELSE PRINT (CHAN[7])@(0),"Cont: ",A0$(3,20),@(27),"Firm: ",A0$(23,15),
1120 PRINT (CHAN[7])@(49),"Tele: ",FNF$(A0$(38,10),M8$),@(70),"Ext: ",A0$(48,4),'LF',@(1),"Cont: ",A0$(52,30),@(50),"Fax: ",FNF$(A0$(379,10),M8$)
1130 PRINT (CHAN[7])@(0),"Title: ",A0$(82,30),@(38),"Territory: ",A0$(285,3),@(59),"Init Ent: ",FNB$(A0$(331,8)),'LF',@(1),"Firm: ",A0$(112,30),@(38),"Next Call: ",FNB$(A0$(288,8)),@(59),"Last Ent: ",FNB$(A0$(339,8))
1140 PRINT (CHAN[7])@(0),"Addr1: ",A0$(142,30),@(40),"Ord Rep: ",A0$(296,3),@(61),"Source: ",A0$(348,4),'LF',@(0),"Addr2: ",A0$(172,30),@(41),"Ord Dt: ",FNB$(A0$(299,8)),@(63),"Intr: ",A0$(353,4)
1150 PRINT (CHAN[7])@(1),"City: ",A0$(202,25),@(40),"CSR Rep: ",A0$(307,3),@(59),"Send Lit: ",A0$(360,1),'LF',@(0),"State: ",A0$(227,2),@(11),"Zip: ",FNZ$(A0$(229)),@(39),"CSR Code: ",A0$(311,4),@(59),"Lit Code: ",A0$(358,2)
1160 PRINT (CHAN[7])@(0),"Cntry: ",A0$(238,15),@(41),"Logged: ",FNB$(A0$(315,8)),@(61),"Lit Dt: ",FNB$(A0$(361,8)),'LF',@(1),"Cust: ",FNF$(A0$(253,P[0]),M0$),@(16),"Terms:  ",A0$(267,2),@(41),"Recall: ",FNB$(A0$(323,8)),@(63),"Dear: ",A0$(369,10),'LF',@(2),"Type: ",A0$(260,3),@(17),"Disc: ",A0$(264,2),@(40),"1st Inv: ",FNB$(A0$(269,8)),@(59),"Last Inv: ",FNB$(A0$(277,8))
1170 PRINT (CHAN[7])@(0),J$
1190 LET L=L+11
1200 REM "--- Set Parameters (User Def'd Fields)
1210 LET X9=21,X8=LEN(P1$)/X9-1,X2$="",X3$="",X4$=""
1215 DIM LINES$[7](80)
1220 LET X0=1,Y0=0
1230 FOR X=1 TO X9
1240 LET X0$=P1$((X-1)*(X8+1)+1,X8+1)
1250 IF X0$(1,1)="S" THEN LET X1$=X1$+STR(X:"00")
1260 IF X0$(1,1)="D" THEN LET X2$=X2$+STR(X:"00")
1270 IF X0$(1,1)="Y" THEN LET X3$=X3$+STR(X:"00")
1280 IF X0$(1,1)="N" THEN LET X4$=X4$+STR(X:"00")
1290 IF POS(X0$(1,1)="SDYN")=0 THEN GOTO 1340
1300 IF Y0>7 THEN LET Y0=0,X0=X0+40
1310 LET X0$(2,X8)=J1$(1,X8-LEN(CVS(X0$(2,X8),2)))+X0$(2)
1320 LET LINES$[Y0](X0)=X0$(2,X8)+":"
1330 LET Y0=Y0+1
1340 NEXT X
1400 REM USER DEFINED DATA
1410 LET X0=14,Y0=0,X=1
1420 GOSUB 6400; IF V3$<>"" THEN LET Y$=STR(NUM(Y$):V3$)
1430 IF POS(X0$(1,1)="SDYN")=0 THEN GOTO 1530
1470 LET LINES$[Y0](X0,LEN(Y$))=Y$
1500 REM NEXT FIELD
1510 LET Y0=Y0+1
1520 IF Y0>7 THEN LET Y0=0,X0=X0+40
1530 LET X=X+1
1540 IF X<=X9 THEN GOTO 1420
1600 REM PRINT LINES$
1610 FOR I=0 TO 7
1620 PRINT (CHAN[7])@(0),LINES$[I]
1630 LET L=L+1
1640 NEXT I
2000 REM COMMENTS
2005 GOSUB 5100
2006 LET DATE$=""
2010 READ (CHAN[2],KEY=CONTACT$,DOM=2011)
2020 LET K$=KEY(CHAN[2],END=2100)
2025 IF POS(CONTACT$=K$)<>1 THEN GOTO 2100
2030 READ (CHAN[2],KEY=K$)X0$,X1$
2040 IF L>L9 THEN GOSUB 5100
2050 IF X0$(23,8)<>DATE$ THEN PRINT (CHAN[7])@(5),FNB$(STR(99999999-NUM(X0$(23,8)))),
2060 PRINT (CHAN[7])@(15),X1$(1,65)
2070 LET DATE$=X0$(23,8)
2080 LET L=L+1
2090 GOTO 2020
4000 REM END
4010 PRINT (CHAN[7])'LF',"** END OF INFORMATION ** "
4080 CLOSE (CHAN[7])
4090 GOTO 9900
5000 REM "HEADING"
5020 LET H=H+1,L=0
5040 PRINT (CHAN[7])'FF',@(INT(H0-LEN(H1$)/2)),H1$,'LF',@(0),FNK$(PRM$[FID,TDATE]),@(10),FNE$(Q$),@(INT(H0-LEN(H2$)/2)),H2$,@(H1),"PAGE ",H,'LF'; REM "WA: #221
5050 PRINT (CHAN[7])@(0),"ID: ",PRM$[FID,TOPCODE],@(20),"Telemarketing Prospect Maintenance Hardcopy",'LF'; REM "WA: #221
5090 RETURN 
5100 REM COMMENTS HEADER
5120 IF L+3>L9 THEN GOSUB 5000
5130 PRINT (CHAN[7])'LF',J$(1,10)," Comments ",J$(1,60)
5140 PRINT (CHAN[7])""
5150 LET L=L+3
5190 RETURN 
6400 REM "--- Get User Defined Data Elements
6405 REM IF X0>59 THEN LET V0=10 ELSE LET V0=20
6407 LET V0=25
6410 LET Y$="",X0$=P1$((X-1)*(X8+1)+1,X8+1),V4$="",V3$=""
6420 IF X0$(1,1)="S" THEN LET Y=INT(POS(STR(X:"00")=X1$,2)/2),Y$=A1$(Y*25+1,V0),V4$="Enter up to "+STR(V0)+" characters"
6425 IF X0$(1,1)="D" THEN LET Y=INT(POS(STR(X:"00")=X2$,2)/2),Y$=FNU$(A2$(Y*8+1,8)),V0=8,V4$="Enter a Date"
6430 IF X0$(1,1)="Y" THEN LET Y=INT(POS(STR(X:"00")=X3$,2)/2),Y$=A3$(Y+1,1),V4$="Y=Yes, N=No",V0=1
6435 IF X0$(1,1)="N" THEN LET Y=INT(POS(STR(X:"00")=X4$,2)/2),Y$=A4$(Y*10+1,10),V0=M1,V3$=M1$
6440 IF POS(" "<>Y$)=0 THEN LET Y$=""
6445 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8005 REM "-- Special Date functions for YYYYMMDD Dates: FNU=Unpack (trims Century), FNP=Pack (add Century), FNB=Display YYYYMMDD as MM/DD/YY
8010 DEF FNA$(Q$,Q2$)=STR((ASC(Q$)-32)*POS(" "<>Q2$(2,1)):"00")
8020 DEF FNB$(Q1$)=Q1$(5,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(7,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(3,2)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(NUM(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8080 DEF FNH(Q$)=ASC(Q$)-32
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8090 DEF FNI$(Q)=CHR(Q+32)
8091 DEF FNJ$(Q$,Q2$)=STR((ASC(Q$)-32)*POS(" "<>Q2$(2,1)):"00")
8092 DEF FNK$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8094 DEF FNP$(Q1$)="19"+Q1$
8096 DEF FNU$(Q1$)=Q1$(5,4)+Q1$(3,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM "END
9998 EXIT 
62000 REM " --- Set-up ADD+ON Environment (WA: #221)"
62010 DIM PRM$[0]; CALL "ADC.01",PRM$[ALL],STATUS
62020 IF STATUS<>0 THEN EXITTO 9900
62030 LET FID=0,AP=1,AR=2,BM=3,DC=4,FA=5,GL=6,IV=7,MP=8,OP=9,PO=10,PR=11,SA=12,SH=13,SY=14,WO=15,TS=21,TB=22,DC=23,V2=24,EF=25; REM " ---- Module Id Numbers"
62040 DIM J$(80,"-"),J0$(40,"0"),J1$(80)
62050 LET INSTALL=0,KEYLEN=1,KEYMAX=2,KEYMASK=3,CPERIOD=4,CYEAR=5,DMASK=6,UMASK=7,RMASK=8,TWO=9,THREE=10,FOUR=11,EMASK=12,TOPCODE=1,TDATE=2,TMENU=3,TPRINTER=4,TSECURE=5,TFIRM=6,TPMODE=7,TPROG=8,TPOPTS=9,TFNAME=10,TPNAME=11,TLDATE=12
62090 RETURN 
65000 END

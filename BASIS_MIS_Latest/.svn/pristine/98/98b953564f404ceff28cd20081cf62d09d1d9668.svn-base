0010 REM "SHC - UPS Freight Calculation"
0020 REM "Program SHCACA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Feb97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Feb97 CAJ: Modified for 1997 SpeedShip Update"
0040 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER A0$,A1$,W0$,W1$,W[ALL],CHARGE,ZIPLENGTH,ZIPCODE$,ZONE$,SERVICE$,RESERVED$,RATEFILE$,ZONEFILE$,SPEEDSHIP$
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]=RATEFILE$
0140 IF W1$(53,1)="Y" OR W1$(55,1)="Y" THEN LET FILES$[2]="SHM-10"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET SHM01_DEV=CHANNELS[1],SHM10_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 SHM01A: IOLIST X$,F[ALL]
0220 SHM10C: IOLIST X$,R[ALL]
0400 REM " --- Parameters"
0410 LET FIRM$=A0$(1,2),CARRIER$=W1$(11,1),TYPE$=W1$(61,1),M=36
0420 IF CARRIER$="B" THEN LET CARRIER$="A"
0500 REM " --- Initializations"
0510 DIM F[M],Z[M],R[20]
0550 LET CHARGE=-1,RESIDENTIAL=0,EASRATE=0,EASMINIMUM=0
0600 REM " --- Additional file opens"
0610 IF W1$(53,1)<>"Y" AND W1$(55,1)<>"Y" THEN GOTO 0800
0620 FIND (SHM10_DEV,KEY=FIRM$+"C"+W1$(4,7),DOM=0660)IOL=SHM10C
0650 IF W1$(55,1)="Y" THEN LET RESIDENTIAL=R[2]
0660 IF POS(W1$(61,1)="CI")=0 THEN GOTO 0800
0670 IF W1$(53,1)<>"Y" THEN GOTO 0800
0680 LET EASRATE=R[17],EASMINIMUM=R[18]
0800 REM " --- Zones"
0810 ON POS(TYPE$="123CE") GOTO 0820,0840,0870,0900,0950,0840
0820 LET L=6,Z[0]=2,Z[1]=3,Z[2]=4,Z[3]=5,Z[4]=6,Z[5]=7,Z[6]=8
0835 GOTO 1000
0840 LET L=23,Z[0]=102,Z[1]=103,Z[2]=104,Z[3]=105,Z[4]=106,Z[5]=107
0845 LET Z[6]=108,Z[7]=124,Z[8]=125,Z[9]=126,Z[10]=132,Z[11]=133
0850 LET Z[12]=134,Z[13]=135,Z[14]=136,Z[15]=137,Z[16]=138,Z[17]=142
0855 LET Z[18]=143,Z[19]=144,Z[20]=145,Z[21]=146,Z[22]=147,Z[23]=148
0860 GOTO 1000
0870 LET L=16,Z[0]=202,Z[1]=203,Z[2]=204,Z[3]=205,Z[4]=206,Z[5]=207
0875 LET Z[6]=208,Z[7]=224,Z[8]=225,Z[9]=226,Z[10]=242,Z[11]=243
0880 LET Z[12]=244,Z[13]=245,Z[14]=246,Z[15]=247,Z[16]=248
0885 GOTO 1000
0900 LET L=6,Z[0]=302,Z[1]=303,Z[2]=304,Z[3]=305
0910 LET Z[4]=306,Z[5]=307,Z[6]=308
0920 GOTO 1000
0950 LET L=9,Z[0]=51,Z[1]=52,Z[2]=53,Z[3]=54,Z[4]=55
0960 LET Z[5]=56,Z[6]=71,Z[7]=72,Z[8]=81,Z[9]=82
0970 GOTO 1000
1000 REM " --- Valid zone?"
1010 LET ZONE=NUM(ZONE$,ERR=9900)
1020 FOR X=0 TO L
1030 IF ZONE=Z[X] THEN EXITTO 1100
1040 NEXT X
1090 GOTO 9900
1100 REM " --- Dimensional weight?"
1110 IF W1$(127,1)="L" THEN GOTO 2000
1120 LET WEIGHT=W[0],W1$(88,1)="A"
1130 IF W[20]>W[0] THEN LET WEIGHT=W[20],W1$(88,1)="D"
1200 REM " --- Valid weight?"
1210 IF WEIGHT<=0 THEN GOTO 9900
1220 IF WEIGHT>150 THEN GOTO 9900
1230 IF W1$(16,1)<>"Y" THEN GOTO 1300
1240 IF WEIGHT<30 THEN LET WEIGHT=30
2000 REM " --- Retrieve shipping rate for weight/zone"
2010 LET ZONE=X,K$=FIRM$+CARRIER$+STR(WEIGHT:"000")
2020 IF W1$(127,1)="L" THEN LET K$=FIRM$+CARRIER$+"LTR"
2030 READ (SHM01_DEV,KEY=K$,DOM=2100)IOL=SHM01A
2090 GOTO 2200
2100 REM " --- Round up for Canadian/Mexican shipments over 50 lbs"
2110 IF W1$(127,1)="L" THEN GOTO 4000
2120 IF POS(TYPE$="CM")=0 THEN GOTO 4000
2130 IF WEIGHT<51 THEN GOTO 4000
2140 LET K$=KEY(SHM01_DEV,END=4000)
2150 IF POS(FIRM$+CARRIER$=K$)=0 THEN GOTO 4000
2160 FIND (SHM01_DEV,KEY=K$,DOM=4000)IOL=SHM01A
2200 REM " --- Assign charge"
2210 LET CHARGE=F[ZONE]+RESIDENTIAL
2400 REM " --- Does Extended Area Surcharge Apply (International Only)?"
2410 IF POS(W1$(61,1)="CI")=0 THEN GOTO 4000
2420 IF W1$(53,1)<>"Y" THEN GOTO 4000
2430 LET SURCHARGE=W[0]*EASRATE,SURCHARGE=MAX(EASCHARGE,EASMINIMUM)
2440 LET CHARGE=CHARGE+SURCHARGE
4000 REM " --- All done"
4090 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9910 CLOSE (SHM01_DEV,ERR=9920)
9920 CLOSE (SHM10_DEV,ERR=9930)
9950 EXIT 
9999 END

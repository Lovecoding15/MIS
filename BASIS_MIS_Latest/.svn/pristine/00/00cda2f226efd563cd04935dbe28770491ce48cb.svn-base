0010 REM "POX - Rebuild Purchase Order History Cross-References"
0020 REM "Program POX.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Jun98 WGH: Remove history for null PO receipts (230,1070-1080,1150,1420-1480)"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="POM-02",FILES$[2]="POT-04",FILES$[3]="POT-14"
0130 LET FILES$[4]="POT-25",FILES$[5]="SYS-01"
0140 LET FILES$[6]="POT-24",FILES$[7]="POT-34",FILES$[8]="POT-35"
0145 LET FILES$[9]="POT-44"
0150 LET OPTIONS$[6]="C",OPTIONS$[7]="C",OPTIONS$[8]="C",OPTIONS$[9]="C"
0160 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET POM02_DEV=CHANNELS[1],POT04_DEV=CHANNELS[2],POT14_DEV=CHANNELS[3]
0185 LET POT25_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5]
0200 REM " --- IOLists
0210 POM02A: IOLIST Y0$(1),Y1$(1)
0220 POT04A: IOLIST A0$(1),A1$(1)
0230 POT14A: IOLIST B0$(1),B1$,B2$,B3$(1),B4$,B5$,B6$,B[ALL]
0240 POT24A: IOLIST POT24$(1)
0250 POT34A: IOLIST POT34$(1)
0260 POT44A: IOLIST POT44$(1)
0270 POT25A: IOLIST A0$(1),A1$(1)
0280 POT35A: IOLIST POT35$(1)
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0500 REM " --- Initializations
0510 DIM A0$(22),A1$(160),B0$(25),B1$(48),B2$(32),B3$(22),Y0$(4),Y1$(32)
0520 DIM POT24$(53),POT34$(53),POT44$(31)
0700 REM " --- Background
0710 CALL "SYC.WC",1,0,80,1,0,4,0
0720 PRINT 'SB',@(28,3),"Processing:",'SF',
0800 REM " --- Options
0810 LET V4$="Do You Wish To Rebuild Purchase Order Cross-References"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM " --- Clear Files
0910 CALL "SYC.DA",1,6,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0920 IF STATUS THEN GOTO 9900
0930 LET POT24_DEV=CHANNELS[6],POT34_DEV=CHANNELS[7],POT35_DEV=CHANNELS[8]
0940 LET POT44_DEV=CHANNELS[9]
0980 CALL "SYC.NB","Updating",24,COLUMN
0990 PRINT @(40,3),"Receipts",'CL',
1000 REM " --- Receipt History X-Refs (POT-04 & -14 ==> POT-24,-34 & -44)
1010 READ_POT04: 
1020 READ (POT04_DEV,END=DONE_POT04)IOL=POT04A
1030 PRINT @(COLUMN,11),A0$(1,2)," ",A0$(3,6)," ",A0$(9),
1040 LET POT24$(1)=A0$(1,2)+FILL(22)+A0$(3,6)+A1$(29,6)+A0$(9,7)+A0$(16,7)
1050 LET POT34$(1)=A0$(1,2)+FILL(22)+A1$(29,6)+A0$(3,6)+A0$(9,7)+A0$(16,7)
1060 LET POT44$(1)=A0$(1,2)+A0$(3,6)+A1$(29,6)+A0$(9,7)+A0$(16,7)
1070 DIM B[12]
1080 LET TOTQTY=0
1090 READ (POT14_DEV,KEY=A0$,DOM=READ_POT14)
1100 REM " --- Process line items
1110 READ_POT14: 
1120 LET K14$=KEY(POT14_DEV,END=DONE_POT14)
1130 IF K14$(1,22)<>A0$ THEN GOTO DONE_POT14
1140 READ (POT14_DEV)IOL=POT14A
1150 LET POT44$(29,3)=B0$(23,3),TOTQTY=TOTQTY+ABS(B[7])
1160 WRITE (POT44_DEV,KEY=POT44$)IOL=POT44A
1165 REM " --- Only write "S" type lines to POT-24 and POT-34
1170 IF B0$(1,2)+B1$(1,2)=Y0$ THEN GOTO WRITE_XREFS; REM "No need to re-read
1180 LET Y0$(1)=B0$(1,2)+B1$(1,2),Y1$(1)=""
1190 FIND (POM02_DEV,KEY=Y0$,DOM=READ_POT14)IOL=POM02A
1200 REM " --- Write to POT-24 & POT-34?
1205 WRITE_XREFS: 
1240 IF Y1$(21,1)<>"S" THEN GOTO NEXT_POT14
1250 LET POT24$(3,22)=B3$(3,20)+B3$(1,2),POT24$(51,3)=B0$(23,3)
1260 LET POT34$(3,22)=B3$(3,20)+B3$(1,2),POT34$(51,3)=B0$(23,3)
1270 WRITE (POT24_DEV,KEY=POT24$)IOL=POT24A
1280 WRITE (POT34_DEV,KEY=POT34$)IOL=POT34A
1300 REM " --- Loop up for next detail
1305 NEXT_POT14: 
1390 GOTO READ_POT14
1400 REM " --- Done with detail
1410 DONE_POT14: 
1420 IF TOTQTY<>0 THEN GOTO 1490
1430 REMOVE (POT04_DEV,KEY=A0$)
1440 READ (POT14_DEV,KEY=A0$,DOM=1450)
1450 LET K14$=KEY(POT14_DEV,END=1490)
1460 IF K14$(1,22)<>A0$ THEN GOTO 1490
1470 REMOVE (POT14_DEV,KEY=K14$)
1480 GOTO 1450
1490 GOTO READ_POT04
1900 REM " --- Done with Receipt History
1910 DONE_POT04: 
2000 REM " --- Rebuild POT-35, PO/Inv x-ref
2005 READ_POT25: 
2010 CALL "SYC.NB","Updating",30,COLUMN
2020 PRINT @(40,3),"PO/Inv",'CL',
2110 DIM A0$(26),A1$(31),POT35$(40)
2120 READ (POT25_DEV,END=DONE_POT25)IOL=POT25A
2130 PRINT @(COLUMN,11),A0$(1,2)," ",A0$(3,2)," ",A0$(5,6)," ",A0$(11),
2160 LET POT35$(1)=A0$(1,2)+A0$(5,6)+A1$(1,7)+A1$(11,7)+A0$(3,2)+A0$(11,16)
2170 WRITE (POT35_DEV,KEY=POT35$)IOL=POT35A
2180 GOTO 2020
2190 DONE_POT25: 
4000 REM " --- End"
4090 GOTO 9900
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "PRR - Check Register"
0020 REM "Program PRR.HA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "25Sep96 JAL: Heading omits firm name and task description (570)"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=11
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRE-01",FILES$[2]="PRE-11",FILES$[3]="PRE-21"
0130 LET FILES$[4]="PRE-31",FILES$[5]="PRE-41",FILES$[6]="PRM-01"
0135 LET FILES$[7]="PRM-10",FILES$[8]="PRW-01",FILES$[9]="SYS-01"
0160 CALL "SYC.DA",1,1,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRE01_DEV=CHANNELS[1],PRE11_DEV=CHANNELS[2],PRE21_DEV=CHANNELS[3]
0185 LET PRE31_DEV=CHANNELS[4],PRE41_DEV=CHANNELS[5],PRM01_DEV=CHANNELS[6]
0190 LET PRM10_DEV=CHANNELS[7],PRW01_DEV=CHANNELS[8],SYS01_DEV=CHANNELS[9]
0200 REM " --- IOLists
0210 PRE01A: IOLIST A0$
0220 PRE11A: IOLIST W0$,W[ALL]
0230 PRE21A: IOLIST W0$,W[ALL]
0240 PRE31A: IOLIST W0$,W[ALL]
0260 PRM01A: IOLIST B0$,B1$
0270 PRM10ABC: IOLIST X$; REM "Used for PRM10A, PRM10B, PRM10C
0280 PRM10H: IOLIST X0$
0290 PRW01A: IOLIST C0$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0320 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0330 PRS01B: IOLIST *,P9$
0400 REM " --- Parameters
0405 DIM P[6],G[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N3$=F5$,SOURCE$=PGM(-2)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0435 FIND (SYS01_DEV,KEY=N0$+N2$+"01",DOM=9800)IOL=PRS01B
0440 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[3]=NUM(P3$(8,1))
0465 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0470 FOR X=0 TO 4
0475 LET G[X]=NUM(G2$(X*2+1,2),ERR=0480)
0480 NEXT X
0485 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[3]=NUM(P3$(8,1)),P[5]=NUM(P4$(1,2)),P[6]=FNYY_YEAR(P4$(3,2))
0500 REM " --- Init Data
0510 LET L9=59,L=L9+1,HEADINGS=3,WIDTH=132,WHEN$=F0$(7,3),CLOCK$=""
0520 DIM T[8],R[8],X[8],W[2],HEADINGS$[HEADINGS]
0570 LET GLDATE$=P9$(1,3),HEADINGS$[0]=F4$,HEADINGS$[1]=F5$
0575 IF LEN(M1$)>12 THEN LET M1$="####,##0.00-"
0580 LET X=POS("CR"=M1$)
0585 IF X>1 THEN LET M1$=M1$(1,X-1)+"-"
0590 LET M0=25,M1=LEN(M1$),UPDATE$="PRU.CA"
0600 REM " --- Additional File Opens
0650 LET GL$="N",STATUS=0
0655 CALL "GLC.BA",ERR=0660,SOURCE$,N2$,GLW11$,GL$,STATUS
0660 IF STATUS<>0 THEN GOTO 9900
0665 IF GL$<>"Y" THEN GOTO 0700
0670 LET FILES$[9]="GLM-01",FILES$[10]=GLW11$,OPTIONS$[10]="C"
0675 CALL "SYC.DA",1,9,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS>0 THEN GOTO 9900
0690 LET GLM01_DEV=CHANNELS[9],GLW11_DEV=CHANNELS[10]
0800 REM " --- Standard stock loaded?
0810 LET V4$="Has Standard Stock Been Loaded In The Printer"
0820 CALL "SYC.YN",1,V4$,2,V$,V3
0830 IF V3=4 THEN GOTO 9900
0840 IF V$<>"Y" THEN GOTO 9900
0850 REM " --- Run Report?
0860 LET V4$="Do You Want To Print The "+N3$
0870 CALL "SYC.YN",1,V4$,2,V$,V3
0880 IF V3=4 THEN GOTO 9900
0890 IF V$<>"Y" THEN GOTO 9900
0900 REM " --- File Initializations
0910 CALL "SYC.NB","Printing",8,COLUMN
0920 CALL "SYC.GA",7,1,"","",STATUS
0930 IF STATUS=1 THEN GOTO 9900
0940 GOSUB 5800
1000 REM " --- Init Read
1020 READ (PRW01_DEV,KEY=N0$,DOM=1100)
1100 REM " --- Main Read
1120 LET K$=KEY(PRW01_DEV,END=4000)
1130 IF K$(1,2)<>N0$ THEN GOTO 4000
1140 READ (PRW01_DEV)IOL=PRW01A
1180 PRINT @(COLUMN,11),C0$(3,7),
1200 REM " --- Type
1210 IF C0$(10,1)="C" THEN IF LEN(C0$)=25 THEN GOTO 1240
1220 IF C0$(10,1)<>"V" THEN GOTO 1300
1240 IF L+2>L9 THEN GOSUB 5000
1260 PRINT (7)'LF',@(5),C0$(3,7),"  *Void*"
1280 LET L=L+2
1290 GOTO 1100
1300 REM " --- Get Employee
1320 LET A0$=N0$+C0$(14,9)+C0$(10,1)+C0$(23,2)+C0$(3,7)
1340 IF C0$(10,1)="C" THEN LET A0$(15,7)=""
1360 READ (PRE01_DEV,KEY=A0$,DOM=1100)IOL=PRE01A
1380 IF A0$(12,1)="V" THEN GOTO 1100
1400 REM " --- Get Name
1410 LET X1$="**** Not On File ****"
1420 DIM B1$(250)
1450 FIND (PRM01_DEV,KEY=A0$(1,11),DOM=1470)IOL=PRM01A
1460 LET Q=POS("  "=B1$(1,16))-1
1470 IF Q<1 THEN LET Q=16
1480 LET X1$=B1$(1,Q)+", "+B1$(17,14)
1600 REM " --- Print Heading
1620 IF L+2>L9 THEN GOSUB 5000
1640 PRINT (7)'LF',@(0),FNF$(A0$(3,P[0]),M0$)," ",X1$
1650 LET L=L+2
2000 REM " --- Detail
2010 DIM X[8]
2100 REM " --- Earnings
2110 READ_PRE11: 
2120 READ (PRE11_DEV,KEY=A0$(1,21),DOM=NEXT_PRE11)
2130 NEXT_PRE11: 
2140 LET K$=KEY(PRE11_DEV,END=READ_PRE21)
2150 IF K$(1,21)<>A0$(1,21) THEN GOTO READ_PRE21
2160 READ (PRE11_DEV)IOL=PRE11A
2170 GOSUB ACCUMULATE
2180 GOTO NEXT_PRE11
2200 REM " --- Deductions
2210 READ_PRE21: 
2230 READ (PRE21_DEV,KEY=A0$(1,21),DOM=NEXT_PRE21)
2240 NEXT_PRE21: 
2250 LET K$=KEY(PRE21_DEV,END=READ_PRE31)
2260 IF K$(1,21)<>A0$(1,21) THEN GOTO READ_PRE31
2270 READ (PRE21_DEV)IOL=PRE21A
2280 GOSUB ACCUMULATE
2290 GOTO NEXT_PRE21
2300 REM " --- Taxes
2310 READ_PRE31: 
2330 READ (PRE31_DEV,KEY=A0$(1,21),DOM=NEXT_PRE31)
2340 NEXT_PRE31: 
2350 LET K$=KEY(PRE31_DEV,END=PRINT)
2360 IF K$(1,21)<>A0$(1,21) THEN GOTO PRINT
2370 READ (PRE31_DEV)IOL=PRE31A
2380 GOSUB ACCUMULATE
2390 GOTO NEXT_PRE31
2500 REM " --- Print
2505 PRINT: 
2510 IF L+1>L9 THEN GOSUB 5000
2520 PRINT (7)@(5),C0$(3,7),"  ",FNB$(C0$(11,3)),@(M0),X[1]:M1$,X[2]:M1$,X[3]:M1$,X[4]:M1$,X[5]:M1$,X[6]:M1$,X[7]:M1$,X[8]:M1$
2540 LET L=L+1
2590 GOTO 1100
4000 REM " --- Totals
4010 IF PAGE=0 THEN GOSUB 5000
4020 PRINT (7)'LF',@(9),"Grand Totals:",@(M0),T[1]:M1$,T[2]:M1$,T[3]:M1$,T[4]:M1$,T[5]:M1$,T[6]:M1$,T[7]:M1$,T[8]:M1$
4070 LET ACTION$="D"
4090 RUN "PRR.HB"
5000 REM " --- Report Heading
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)"Employee Number & Name"
5110 PRINT (7)"      Check     Date",
5150 FOR X=1 TO LEN(U1$)/10
5160 PRINT (7)@(M0-11+M1*X),U1$(10*(X-1)+1,10),
5170 NEXT X
5180 PRINT (7)""
5185 LET L=L+3
5190 RETURN 
5800 REM " --- Build Headings
5810 LET U0$="",U1$=""
5820 PRINT @(COLUMN,11),"Headings",
5850 READ (PRM10_DEV,KEY=N0$+"H",DOM=5860)
5860 LET K$=KEY(PRM10_DEV,END=5980)
5870 IF K$(1,3)<>N0$+"H" THEN GOTO 5980
5880 READ (PRM10_DEV)IOL=PRM10H
5885 IF K$(4,1)>"C" THEN GOTO 5895
5890 LET U0$=U0$+X0$(4,3),U2$=U2$+X0$(17,2)
5895 IF K$(4,1)<>"F" THEN GOTO 5860
5897 LET U3$=U3$+X0$(5,2),X$=X0$(7,10)
5900 FOR X=1 TO 10
5910 IF X$(10,1)<>" " THEN EXITTO 5950
5920 LET X$=" "+X$(1,9)
5930 NEXT X
5950 LET U1$=U1$+X$
5960 GOTO 5860
5980 DIM X$(80)
5985 LET X$(1)=U1$,X$(71)="     Total",U1$=X$
5987 PRINT @(COLUMN,11),"        ",
5990 RETURN 
6100 REM " --- Accumulate
6105 ACCUMULATE: 
6160 LET X=0,SIGN=-1
6170 FIND (PRM10_DEV,KEY=N0$+W0$(22,1)+W0$(25,2),DOM=6300)IOL=PRM10ABC
6180 LET X=POS(W0$(22,1)+X$(22,2)=U0$)
6190 IF X=0 THEN GOTO PRINT
6200 LET X$=U2$(INT(X/3)*2+1,2)
6220 LET X=POS(X$=U3$)
6240 IF X>0 THEN LET X=INT(X/2)+1
6300 IF X>7 THEN LET X=0
6305 IF X=0 THEN LET X=1
6310 IF W0$(22,1)="A" THEN LET SIGN=1
6320 LET X[X]=X[X]+W[2],T[X]=T[X]+W[2],R[X]=R[X]+W[2]
6340 LET X[8]=X[8]+SIGN*W[2],T[8]=T[8]+SIGN*W[2],R[8]=R[8]+SIGN*W[2]
6350 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

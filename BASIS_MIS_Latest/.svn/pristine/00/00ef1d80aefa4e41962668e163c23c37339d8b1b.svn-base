0010 REM "TMU - TeleMarketing Contact Purge - I
0020 REM "Program TMU.AA
0030 REM "ADD+ON SOFTWARE VERSION 5.0.2 - 07/24/91"
0035 REM "12/09/91 by Jerry Koch
0040 REM "COPYRIGHT (C) 1991 MicroAccounting Systems, Inc. ALL RIGHTS RESERVED"
0041 REM 
0042 REM "01/06/97 KJS WA: #221 ADS"
0079 REM 
0080 BEGIN 
0090 SETERR 9000
0100 REM "OPEN/LOCK FILES"
0110 OPEN (1)"TMM-01"
0120 OPEN (2)"TMM-02"
0160 OPEN (6)"SYS-01"
0200 REM "IOLISTS"
0260 IOLIST P0$,P1$,P2$,P3$,P4$
0400 REM " --- Parameters"
0401 GOSUB 62000; REM "WA: #221
0420 LET N0$=PRM$[FID,TFIRM],N1$=PRM$[FID,TFNAME],N2$="TM",N4$=PRM$[FID,TPNAME]; REM "WA: #221
0430 FIND (6,KEY=N0$+N2$+"00",DOM=9800)IOL=0260
0440 LET M0$="000000",R9$=PRM$[AR,INSTALL]; REM "WA: #221, I2$="06009"
0490 LET M1$="###,###.00-",M1=LEN(M1$)
0500 REM "INIT DATA"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 LET K0=0,I0=0,K9=6
0530 LET P9=24,P8=6
0540 LET P9$=FNP$(STR(NUM(PRM$[FID,TLDATE](1,2))-1:"00")+PRM$[FID,TLDATE](3,4)),P8$="1"; REM "WA: #221
0550 DIM DATE$[5]
0560 LET DATE$[1]="Last Edited Date",DATE$[2]="Marketing Date",DATE$[3]="Order Date",DATE$[4]="CSR Logged Date",DATE$[5]="Any Date"
0700 REM "BACKGROUND"
0720 PRINT 'SB',@(28,3),"Purge Date:",@(0,4),J$,@(30,5),"Based On:",@(0,6),J$,@(0,8),J$,
0790 PRINT 'SF',
0800 REM "OPTIONS"
0900 REM "INIT FILE"
1000 REM "KEY"
1010 PRINT 'CF',
1600 REM "--- Purge Date
1620 LET V0$="D",V1$="C6",V2$=FNU$(P9$),V3$="",V4$="Purge All Contacts Through This Date",V0=6,V1=40,V2=3
1640 GOSUB 7000
1650 IF V$<PRM$[FID,TLDATE] THEN GOTO 1680; REM "WA: #221
1660 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Purge Date Must Be Prior To Today! <Cr> ",V0=1,V1=41+INT(LEN(V4$)/2),V2=22
1665 GOSUB 7000
1670 GOTO 1600
1680 LET P9$=FNP$(V$)
1690 GOTO 3900
1700 REM "--- Based On
1710 PRINT @(40,5),'CL',@(20,10),"1 = Last Edited Date",@(20,11),"2 = Marketing Date",@(20,12),"3 = Order Date",@(20,13),"4 = CSR Logged Date",@(20,14),"5 = All Of The Above (Keep Contact If Any",@(24,15),"Date Is More Recent Than Purge Date)",
1720 LET V0$="S",V1$="C",V2$=P8$,V3$="12345",V4$="Enter 1-5 For Date Comparison",V0=1,V1=40,V2=5
1740 GOSUB 7000
1750 FOR X=10 TO 15; PRINT @(0,X),'CL',; NEXT X
1760 PRINT @(V1+V0+1,V2),DATE$[NUM(V$,ERR=1700)],
1780 LET P8$=V$
1790 GOTO 3900
3900 REM "NEXT FIELD"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 IF V3=4 THEN GOTO 4000
3960 ON I0 GOTO 1600,1700,4000
4000 REM "CORRECT?"
4010 PRINT @(10,10),"Warning: Information Through "+FNB$(P9$)+" Will Be Purged!",
4020 LET V0$="S",V1$="CE^",V2$="",V3$="",V4$="Is The Above Information Correct? <Yes/No>: ",V0=3,V1=62,V2=22,I0=0
4025 GOSUB 7000
4030 LET V$=CVS(V$,4)
4035 PRINT @(0,10),'CL',
4040 IF V$="N" OR V$="NO" THEN GOTO 1600
4045 IF V$="END" OR V3=4 THEN GOTO 9900
4050 IF V$<>"YES" THEN GOTO 4000
4500 REM "--- Run Overlay
4510 PRINT 'SB',@(30,7),"Updating:",'SF',
4590 RUN "TMU.AB"
5000 REM "DISPLAY"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8005 REM "-- Special Date functions for YYYYMMDD Dates: FNU=Unpack (trims Century), FNP=Pack (add Century), FNB=Display YYYYMMDD as MM/DD/YY
8010 DEF FNA$(Q$,Q2$)=STR((ASC(Q$)-32)*POS(" "<>Q2$(2,1)):"00")
8020 DEF FNB$(Q1$)=Q1$(5,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(7,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(3,2)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(NUM(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8080 DEF FNH(Q$)=ASC(Q$)-32
8090 DEF FNV$(Q)=CHR(Q+32)
8094 DEF FNP$(Q1$)=STR(FNY(Q1$+"  ")*(LEN(Q1$)>2):"####")+Q1$(3*(LEN(Q1$)>2)+(LEN(Q1$)=0))
8096 DEF FNU$(Q1$)=Q1$(5,4)+Q1$(3,2)
8098 DEF FNY(Q$)=(ASC(Q$(1,1))-(65*(Q$(1,1)>="A"))-48*(Q$(1,1)<"A"))*10+NUM(Q$(2,1))+1900+(100*(Q$(1,1)>="A"))
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
62000 REM " --- Set-up ADD+ON Environment (WA: #221)"
62010 DIM PRM$[0]; CALL "ADC.01",PRM$[ALL],STATUS
62020 IF STATUS<>0 THEN EXITTO 9900
62030 LET FID=0,AP=1,AR=2,BM=3,DC=4,FA=5,GL=6,IV=7,MP=8,OP=9,PO=10,PR=11,SA=12,SH=13,SY=14,WO=15,TS=21,TB=22,DC=23,V2=24,EF=25; REM " ---- Module Id Numbers"
62040 DIM J$(80,"-"),J0$(40,"0"),J1$(80)
62050 LET INSTALL=0,KEYLEN=1,KEYMAX=2,KEYMASK=3,CPERIOD=4,CYEAR=5,DMASK=6,UMASK=7,RMASK=8,TWO=9,THREE=10,FOUR=11,EMASK=12,TOPCODE=1,TDATE=2,TMENU=3,TPRINTER=4,TSECURE=5,TFIRM=6,TPMODE=7,TPROG=8,TPOPTS=9,TFNAME=10,TPNAME=11,TLDATE=12
62090 RETURN 
65000 END

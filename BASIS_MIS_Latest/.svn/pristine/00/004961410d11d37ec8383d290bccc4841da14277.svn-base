0010 REM "ARM - Customer Detail Listing"
0020 REM "Program ARM.WA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=11
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ARM-02",FILES$[3]="ARM-03"
0130 LET FILES$[4]="ARM-04",FILES$[5]="ARM-05",FILES$[6]="ARM-06"
0135 LET FILES$[7]="ARM-09",FILES$[8]="ARM-10",FILES$[9]="ART-01"
0140 LET FILES$[10]="ART-11",FILES$[11]="SYS-01"
0160 CALL "SYC.DA",1,1,11,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET ARM01_DEV=CHANNELS[1],ARM02_DEV=CHANNELS[2],ARM03_DEV=CHANNELS[3]
0180 LET ARM04_DEV=CHANNELS[4],ARM05_DEV=CHANNELS[5],ARM06_DEV=CHANNELS[6]
0185 LET ARM09_DEV=CHANNELS[7],ARM10_DEV=CHANNELS[8],ART01_DEV=CHANNELS[9]
0190 LET ART11_DEV=CHANNELS[10],SYS01_DEV=CHANNELS[11]
0200 REM " --- IOLists
0210 ARM01A: IOLIST A0$(1),A1$(1)
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,X$,M0$,M1$,M2$,M3$
0265 ARS01B: IOLIST C9$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0405 DIM P[2],C9$(36)
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$,C9$(7,1)="N"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"01",DOM=0450)IOL=ARS01B
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0500 REM " --- Init Data
0510 DIM J0$(20,"0"),J$(80,"-")
0520 DIM A0$(8),A1$(282),B0$(10),B1$(64),B[10],E[5]
0540 LET A0$(1,2)=N0$,CLS$="N"
0550 LET M8$="(###)-###-####",M9$="###-##-####",M7$="00000"
0560 CALL "SYC.RM",M1$,M1,11,14
0570 IF P[2]=9 THEN LET M7$="00000-0000"
0600 REM " --- Other file opens"
0700 REM " --- Background
0710 PRINT 'CF'
0720 FOR X=4 TO 10 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0750 PRINT 'SB',@(20,3),"Beginning Customer:",@(23,5),"Ending Customer:",@(22,7),"Include Comments:",@(30,9),"Printing:",'SF',
1000 REM " --- Beginning Customer"
1005 PRINT 'CF'
1010 IF LEN(K0$)=0 THEN DIM K0$(P[0])
1020 LET V0$="M",V1$="CE",V2$=K0$(1,P[0]),V3$=P1$,V4$="Beginning Customer Number (<F1>=First/<F3>=Lookup)",V0=6,V1=40,V2=3,I0=0
1030 GOSUB 7000
1040 IF V3=4 THEN GOTO 9900
1045 IF V3=3 THEN CALL "SYC.LC",V$
1050 IF V3=1 OR V$="" OR V$=J0$(1,LEN(V$)) THEN PRINT @(V1,V2),"First",'CL',; LET K0$=""; GOTO 1090
1060 FIND (ARM01_DEV,KEY=N0$+V$,DOM=1000)IOL=ARM01A
1070 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$)," ",A1$(1,30),'CL',
1080 LET K0$=V$
1090 GOTO 3900
1100 REM " --- Ending Customer"
1110 IF LEN(K1$)=0 THEN DIM K1$(P[0])
1120 LET V0$="M",V1$="CE",V2$=K1$(1,P[0]),V3$=P1$,V4$="Ending Customer Number (<F1>=Last/<F3>=Lookup)",V0=6,V1=40,V2=5
1130 GOSUB 7000
1140 IF V3=4 THEN GOTO 9900
1145 IF V3=3 THEN CALL "SYC.LC",V$
1150 IF V3=1 OR V$="" OR V$=J0$(1,LEN(V$)) THEN PRINT @(V1,V2),"Last",'CL',; LET K1$=""; GOTO 1190
1155 IF V$<K0$ THEN GOTO 1100
1160 FIND (ARM01_DEV,KEY=N0$+V$,DOM=0850)IOL=ARM01A
1170 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$)," ",A1$(1,30),'CL',
1180 LET K1$=V$
1190 GOTO 3900
1200 REM " --- Include Comments?"
1210 LET V0$="Y",V1$="C",V2$="Y",V3$="",V0=1,V1=40,V2=7
1215 LET V4$="Include Comments on Listing (Y=Yes, N=No)?"
1220 GOSUB 7000
1230 IF V3=4 THEN GOTO 9900
1240 LET Y$=V$
1290 GOTO 3900
3900 REM " --- Next Field
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 IF ABS(I0)>1000 THEN GOTO 3990
3960 ON I0 GOTO 1000,1100,1200,3990
4000 REM " --- Overlay
4010 LET V0$="Y",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V$="Y" THEN GOTO 4100
4040 IF V$="N" THEN GOTO 1000
4050 IF V3=4 THEN GOTO 9900
4060 GOTO 4000
4100 REM " --- Init File
4110 CALL "SYC.GA",7,1,"","",STATUS
4120 IF STATUS THEN GOTO 9900
4130 LET O9=1,O9$="SYS.AA"
4190 RUN "ARM.MG"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

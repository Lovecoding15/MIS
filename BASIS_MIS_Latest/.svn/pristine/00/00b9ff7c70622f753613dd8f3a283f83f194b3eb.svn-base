0010 REM "PRR - Detail Report
0020 REM "Program PRR.XA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "11Mar97 WGH: Print Earnings total on separate line when there are multiple Earnings lines (2265-2270)"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=10
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRE-01",FILES$[2]="PRE-11",FILES$[3]="PRE-21"
0130 LET FILES$[4]="PRE-31",FILES$[5]="PRE-41",FILES$[6]="PRE-51"
0135 LET FILES$[7]="PRM-01",FILES$[8]="PRM-02",FILES$[9]="PRM-10"
0145 LET FILES$[10]="SYS-01"
0160 CALL "SYC.DA",1,1,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRE01_DEV=CHANNELS[1],PRE11_DEV=CHANNELS[2],PRE21_DEV=CHANNELS[3]
0185 LET PRE31_DEV=CHANNELS[4],PRE41_DEV=CHANNELS[5],PRE51_DEV=CHANNELS[6]
0190 LET PRM01_DEV=CHANNELS[7],PRM02_DEV=CHANNELS[8],PRM10_DEV=CHANNELS[9]
0195 LET SYS01_DEV=CHANNELS[10]
0200 REM " --- IOLists
0210 PRE01A: IOLIST A0$
0220 PRE11A: IOLIST W0$,A[ALL]
0230 PRE21A: IOLIST W0$,A[ALL]
0235 PRE31A: IOLIST W0$,A[ALL]
0240 PRE41A: IOLIST W0$,A[ALL]
0245 PRE51A: IOLIST W0$,A[ALL]
0250 PRM01A: IOLIST B0$,B1$
0260 PRM02A: IOLIST *,PAYRATE
0265 PRM10ABCDEF: IOLIST Y0$; REM "Used for PRM10A, PRM10B, PRM10C, PRM10D, PRM10E, PRE10F
0280 PRM10E: IOLIST X0$
0290 PRM10F: IOLIST *,PAYRATE
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0320 PRS01B: IOLIST *,X$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N3$=F5$,WHEN$=F0$(7,3)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0450 DIM P[4]
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[3]=NUM(P3$(8,1))
0500 REM " --- Init Data
0510 LET H=0,H1$=N1$,H2$=N3$
0520 LET L9=59,L=L9+1,HEADINGS=4,WIDTH=80,CLOCK$=""
0540 DIM A[2],HEADINGS$[HEADINGS]
0542 LET X=POS("CR"=M1$)
0544 IF X<>0 THEN LET M1$=M1$(1,X-1)+"-"
0546 IF LEN(M1$)>13 THEN LET M1$="#####,###.00-"
0550 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$)
0570 LET P[4]=NUM(P3$(7,1))
0590 FIND (SYS01_DEV,KEY=N0$+N2$+"01",DOM=9800)IOL=PRS01B
0592 LET H4$="Pay-Period Ending "+FNB$(X$(1,3))
0610 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$,HEADINGS$[2]=H4$
0660 LET H8$="N",X=POS(F0$(13,1)="LP")
0670 IF X>0 THEN LET H8$="Y"
0700 REM " --- Background
0720 FOR X=4 TO 8 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0760 PRINT 'SB',@(18,3),"From Employee Number:",@(18,5),"Thru Employee Number:",
0790 PRINT 'SF',
0800 REM " --- From
0820 LET V0$="M",V1$="EC",V2$=P9$,V3$=P1$,V4$="Enter A Starting Employee Number, <F3>=First",V0=9,V1=40,V2=3
0840 GOSUB 7000
0850 IF V3=4 THEN GOTO 9900
0860 IF V3=3 THEN LET V$=""
0870 LET P9$=V$
0880 IF P9$="" THEN PRINT @(V1,V2),"First",'CL',
0900 REM " --- Thru
0920 LET V0$="M",V1$="C",V2$=P8$,V3$=P1$,V4$="Enter A Ending Employee Number, <F3>=Last",V0=9,V1=40,V2=5
0940 GOSUB 7000
0950 IF V3=2 THEN GOTO 0800
0960 IF V3=3 THEN LET V$=""
0970 LET P8$=V$
0972 IF P8$<>"" THEN IF P8$<P9$ THEN GOTO 0900
0980 IF P8$="" THEN PRINT @(V1,V2),"Last",'CL',
0982 LET X0$="First",X1$="Last"
0984 IF P9$<>"" THEN LET X0$=FNF$(P9$(1,P[0]),M0$)
0986 IF P8$<>"" THEN LET X1$=FNF$(P8$(1,P[0]),M0$)
0988 LET H5$="From "+X0$+" Thru "+X1$
0990 LET V0$="Y",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
0991 GOSUB 7000
0992 IF V3=4 THEN GOTO 9900
0994 IF V$="N" THEN GOTO 0800
0996 IF V$<>"Y" THEN GOTO 0990
0998 CALL "SYC.NB","Printing:",M0,COLUMN
1000 REM " --- Init Read
1002 CALL "SYC.GA",7,1,"","",STATUS
1004 IF STATUS=1 THEN GOTO 9900
1010 PRECISION P[4]
1020 READ (PRE01_DEV,KEY=N0$+P9$,DOM=1100)
1100 REM " --- Main Read
1105 NEXT_PRE01: 
1120 LET K$=KEY(PRE01_DEV,END=4000)
1130 IF K$(1,2)<>N0$ THEN GOTO 4000
1135 IF P8$<>"" THEN IF K$(1,11)>N0$+P8$ THEN GOTO 4000
1140 READ (PRE01_DEV)IOL=PRE01A
1180 IF H8$="Y" THEN PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$),
1400 REM " --- Get Name
1410 LET X1$="Not On File"
1420 DIM B1$(250)
1450 FIND (PRM01_DEV,KEY=A0$(1,11),DOM=1470)IOL=PRM01A
1455 LET X=POS("  "=B1$(1,16))-1
1457 IF X<1 THEN LET X=16
1460 LET X1$=B1$(1,X)+", "+B1$(17,1)+"."
1470 LET X9$=""
1480 FIND (PRM10_DEV,KEY=N0$+"E"+B1$(221,2),DOM=1490)
1482 GOTO 1500
1490 LET X9$="MISSING DEPARTMENT"
1500 REM " --- Breaks
1520 IF K$(3,P[3])<>SAVEK$ THEN GOSUB 6000
1600 REM " --- Print Heading
1620 IF L+4>L9 THEN GOSUB 5200
1630 PRINT (7)'LF',@(0),"*  ",FNF$(A0$(3,P[0]),M0$)," ",X1$,
1640 IF A0$(12,1)<>"C" THEN GOTO NOT_COMPUTER
1650 IF A0$(26,7)<>FILL(7) THEN PRINT (7)@(33+M3+M0),A0$(12,1),A0$(26,7)
1660 GOTO PAY_RATE
1670 NOT_COMPUTER: 
1680 PRINT (7)@(33+M3+M0),A0$(12,1),A0$(15,7),
1690 LET L=L+1
1800 REM " --- Pay Rate
1805 PAY_RATE: 
1820 LET PAYRATE=0
1840 FIND (PRM02_DEV,KEY=A0$(1,11)+B1$(223,2),DOM=1850)IOL=PRM02A
1845 GOTO 1860
1850 FIND (PRM10_DEV,KEY=A0$(1,2)+"F"+B1$(223,2),DOM=1860)IOL=PRM10F
1860 PRINT (7)@(32+M0),PAYRATE:M3$
1870 LET L=L+2
1900 LET L1=1,L2=0,L3=0
1920 DIM S[6]
2000 REM " --- Detail
2100 REM " --- Earnings
2110 EARNINGS: 
2115 IF L+3>L9 THEN GOSUB 5000
2120 PRINT (7)@(2),"Earnings",@(80-2*M1-M2-5),"Rate",@(80-2*M1-6),"Units",@(80-M1-7),"Amount",
2150 READ (PRE11_DEV,KEY=A0$(1,21),DOM=NEXT_PRE11)
2160 NEXT_PRE11: 
2170 LET K$=KEY(PRE11_DEV,END=DEDUCTIONS)
2180 IF K$(1,21)<>A0$(1,21) THEN GOTO DEDUCTIONS
2190 READ (PRE11_DEV)IOL=PRE11A
2200 GOSUB DESCRIPTIONS
2210 IF L+1>L9 THEN GOSUB 5000
2220 PRINT (7)'LF',@(16),Y0$(6,16),@(80-2*M1-M2-M3),A[0]:M3$,A[1]:M2$,A[2]:M1$,
2230 LET L=L+1,L1=0,L3=L3+1
2240 LET S[0]=S[0]+A[1],S[1]=S[1]+A[2]
2250 GOTO NEXT_PRE11
2255 REM " --- Deductions
2260 DEDUCTIONS: 
2265 IF L3<>1 THEN PRINT (7)'LF',@(80-2*M1-M2),S[0]:M2$,; LET L=L+1
2270 PRINT (7)@(80-M1),S[1]:M1$
2275 LET L=L+1
2277 IF L+2>L9 THEN GOSUB 5000
2280 PRINT (7)@(2),"Deductions",@(80-2*M1-M3-6),"Basis",@(80-2*M1-5),"Rate",@(80-M1-7),"Amount"
2290 LET L2=1,L=L+1
2300 REM " --- Voluntary
2305 VOLUNTARY: 
2310 PRINT (7)@(4),"Voluntary",
2320 LET L1=1
2330 READ (PRE21_DEV,KEY=A0$(1,21),DOM=NEXT_PRE21)
2340 NEXT_PRE21: 
2350 LET K$=KEY(PRE21_DEV,END=TAXES)
2360 IF K$(1,21)<>A0$(1,21) THEN GOTO TAXES
2370 READ (PRE21_DEV)IOL=PRE21A
2400 GOSUB DESCRIPTIONS
2410 IF L+1>L9 THEN GOSUB 5000
2430 IF L1=0 THEN PRINT (7)""; LET L=L+1
2440 PRINT (7)@(16),Y0$(6,16),@(80-3*M1-M3),A[0]:M1$,A[1]:M3$,A[2]:M1$,
2450 LET L1=0
2460 LET S[2]=S[2]+A[2]
2470 GOTO NEXT_PRE21
2500 REM " --- Taxes
2505 TAXES: 
2507 PRINT (7)@(80-M1),S[2]:M1$
2510 PRINT (7)@(4),"Taxes",
2520 LET L=L+1,L1=1
2530 READ (PRE31_DEV,KEY=A0$(1,21),DOM=NEXT_PRE31)
2540 NEXT_PRE31: 
2550 LET K$=KEY(PRE31_DEV,END=EMPLOYER)
2560 IF K$(1,21)<>A0$(1,21) THEN GOTO EMPLOYER
2570 READ (PRE31_DEV)IOL=PRE31A
2600 GOSUB DESCRIPTIONS
2610 IF L+1>L9 THEN GOSUB 5000
2630 IF L1=0 THEN PRINT (7)""; LET L=L+1
2640 PRINT (7)@(16),Y0$(6,16),@(80-3*M1-M3),A[0]:M1$,A[1]:M3$,A[2]:M1$,
2650 LET L1=0
2660 LET S[3]=S[3]+A[2]
2670 GOTO NEXT_PRE31
2700 REM " --- Employer
2705 EMPLOYER: 
2707 PRINT (7)@(80-M1),S[3]:M1$
2710 PRINT (7)@(4),"Employer",
2720 LET L=L+1,L1=1
2730 READ (PRE41_DEV,KEY=A0$(1,21),DOM=NEXT_PRE41)
2740 NEXT_PRE41: 
2750 LET K$=KEY(PRE41_DEV,END=ACCRUALS)
2760 IF K$(1,21)<>A0$(1,21) THEN GOTO ACCRUALS
2770 READ (PRE41_DEV)IOL=PRE41A
2800 GOSUB DESCRIPTIONS
2810 IF L+1>L9 THEN GOSUB 5000
2830 IF L1=0 THEN PRINT (7)""; LET L=L+1
2840 PRINT (7)@(16),Y0$(6,16),@(80-3*M1-M3),A[0]:M1$,A[1]:M3$,A[2]:M1$,
2850 LET L1=0
2860 LET S[4]=S[4]+A[2]
2870 GOTO NEXT_PRE41
2900 REM " --- Accruals
2905 ACCRUALS: 
2907 PRINT (7)@(80-M1),S[4]:M1$
2910 PRINT (7)@(4),"Accruals",
2925 LET L=L+1,L1=1
2930 READ (PRE51_DEV,KEY=A0$(1,21),DOM=NEXT_PRE51)
2940 NEXT_PRE51: 
2950 LET K$=KEY(PRE51_DEV,END=NET)
2960 IF K$(1,21)<>A0$(1,21) THEN GOTO NET
2970 READ (PRE51_DEV)IOL=PRE51A
3000 GOSUB DESCRIPTIONS
3010 IF L+1>L9 THEN GOSUB 5000
3030 IF L1=0 THEN PRINT (7)""; LET L=L+1
3040 PRINT (7)@(16),Y0$(6,16),@(80-3*M1-M3),A[0]:M1$,A[1]:M3$,A[2]:M1$,
3050 LET L1=0
3060 LET S[5]=S[5]+A[2]
3070 GOTO NEXT_PRE51
3200 REM " --- Net
3205 NET: 
3210 PRINT (7)""
3220 LET L=L+1,L1=1
3230 IF L1=0 THEN PRINT (7)""; LET L=L+1
3235 IF L+2>L9 THEN GOSUB 5000
3240 PRINT (7)@(2),"Net Pay",@(80-M1),S[1]-S[2]-S[3]:M1$
3250 LET L=L+2
3260 GOTO NEXT_PRE01
4000 REM " --- Totals
4100 GOTO 9900
5000 REM " --- Heading"
5010 LET HEADINGS$[3]=H5$
5020 LET L=HEADINGS+1
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)@(0),"Employee"
5110 PRINT (7)@(0),"Number & Name",@(27+M0+M3),"Rate",@(33+M0+M3),"Check#",@(74),"Total"
5180 LET L=L+2
5190 RETURN 
5200 REM " --- Sub Heading
5210 IF L+2>L9 THEN GOSUB 5000
5220 IF P[3]=0 THEN RETURN 
5240 PRINT (7)'LF',@(0),"** ",SAVEK$," ",CODEDESC$
5280 LET L=L+2
5290 RETURN 
6000 REM " --- Dept Break
6100 IF P[3]=0 THEN RETURN 
6140 LET SAVEK$=K$(3,P[3])
6160 LET CODEDESC$=""
6170 FIND (PRM10_DEV,KEY=N0$+"E"+B1$(221,2),DOM=6200)IOL=PRM10E
6180 LET CODEDESC$=X0$(6,20)
6200 GOSUB 5200
6290 RETURN 
6600 REM " --- Descriptions
6610 DESCRIPTIONS: 
6630 LET RECID$=K$(22,1)
6640 IF RECID$="E" THEN LET RECID$="A"
6650 DIM Y0$(30); LET Y0$(6,16)=W0$(25,2)+" Not On File***"
6660 FIND (PRM10_DEV,KEY=N0$+RECID$+W0$(25,2),DOM=6662)IOL=PRM10ABCDEF
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

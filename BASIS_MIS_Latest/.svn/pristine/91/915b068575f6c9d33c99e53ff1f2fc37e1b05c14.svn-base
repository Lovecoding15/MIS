0010 REM "APC - Vendor SpeedSearch (Purchase History)"
0020 REM "Program APC.QE"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APM-01",FILES$[2]="APM-02",FILES$[3]="APM-10"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0170 LET APM01_DEV=CHANNELS[1],APM02_DEV=CHANNELS[2],APM10_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 APM01A: IOLIST A0$(1),A1$(1)
0220 APM02A: IOLIST B0$(1),B1$(1),B2$(1),B[ALL]
0230 APM10A: IOLIST X0$(1)
0260 APS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 DIM P[1],X$(128)
0420 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2)
0425 LET TNAME$=FNP$(X$(3,8)),TWIDTH=NUM(X$(35,2)),THEIGHT=NUM(X$(37,2))
0430 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0435 LET SNAME$=FNP$(X$(19,8)),SWIDTH=NUM(X$(43,2)),SHEIGHT=NUM(X$(45,2))
0440 LET ONAME$=FNP$(X$(27,8)),OWIDTH=NUM(X$(83,2)),OHEIGHT=NUM(X$(85,2))
0445 LET MAXOPT=NUM(X$(57,2)),SYS01_DEV=NUM(STBL("!SYS01"))
0450 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0455 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0480 FIND (SYS01_DEV,KEY=N0$+"AP"+"00",DOM=9900)IOL=APS01A
0490 LET P[0]=NUM(P2$(1,2))
0500 REM " --- Initializations"
0510 DIM A0$(8),A1$(195),B0$(10),B1$(25),B2$(10),B[13],OPTLIST$[2,MAXOPT,6]
0520 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0540 LET D_STATUS=0,MAXROW=HEIGHT-5,PGM$=PGM(-2),M9$="(###) ###-####"
0550 LET PREFIX$=OPTION$[3],A0$(1)=N0$+PREFIX$,APTYPE$="AP Type",ROW=2
0560 IF P3$(1,1)<>"Y" THEN LET APTYPE$="",ROW=1
0600 REM " --- Resize masks"
0610 CALL "SYC.RM",M1$,M1,15,15
0650 REM " --- Option Window"
0660 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,ONAME$
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0720 LET HEADING$(FNC(TITLE$,WIDTH-2))=TITLE$
0730 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-10)="F4=End"
0740 PRINT @(0,0),'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0750 PRINT 'SB',@(3,1),APTYPE$,@(34),"Last Invoice",@(3,ROW),"Balance",@(34,2),"Last Payment",@(16,4),"Prior YTD",@(29),"Current YTD",@(47),"Next YTD",@(1,5),"Purchases",@(1,6),"Discounts",@(2,7),"Payments",@(2,8),"Calendar",'SF',
0760 CALL "SYC.WB",SNAME$,0
0770 PRINT 'CS','SB',@(1,0),"Contact",@(33),"Opened",@(3,1),"Phone",@(32),"Acct ID",@(5,2),"Fax",@(29),"Federal ID",'SF',
0900 REM " --- Position file"
0930 CALL "SYC.WB",TNAME$,0
0940 PRINT 'CS',
0990 GOTO 1200
1000 REM " --- Process key field"
1010 GOSUB 6000
1015 CALL "SYC.WB",TNAME$,0
1030 PRINT @(1,0),'SB',"Vendor:",'SF',@(0,1),'CL',@(0,2),'CL',
1035 LET V2$="",V2$=KEY(APM01_DEV,END=1050)
1040 IF POS(A0$(1,2)=V2$)<>1 THEN LET V2$=""
1045 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1050 LET V0$="M",V1$="KE",V3$=M0$,V0=6,V1=9,V2=0,V4$=""
1055 PRINT @(V1+P[0]+1,V2),'CL',
1060 GOSUB 7000
1063 IF V3=6 THEN GOTO 4000
1065 IF V3=5 THEN GOTO 4000
1070 IF V3=4 THEN LET OPTION$[1]="EXIT"; GOTO 4000
1080 IF V3=3 THEN CALL "SYC.LV",V$
1090 IF V$="" THEN GOTO 0900
1092 LET A0$(3)=V$
1200 REM " --- Retrieve records for display"
1210 DIM B[13]
1220 FIND (APM01_DEV,KEY=A0$,DOM=1000)IOL=APM01A
1290 READ (APM02_DEV,KEY=A0$,DOM=1300)
1300 REM " --- Read next APM-02 record"
1310 LET K$=KEY(APM02_DEV,END=2000)
1320 IF POS(A0$=K$)<>1 THEN GOTO 2000
1330 READ (APM02_DEV)IOL=APM02A
1400 REM " --- Read AP Type record in APM-10"
1410 DIM X0$(32)
1420 IF P3$(1,1)<>"Y" THEN GOTO 1500
1430 LET X0$(1)=B0$(1,2)+"A"+B0$(9,2)+"(Not On File)"
1440 FIND (APM10_DEV,KEY=X0$(1,5),DOM=1500)IOL=APM10A
1500 REM " --- Store last record read"
1510 DIM X$(WIDTH-2)
1520 CALL "SYC.WB",TNAME$,0
1530 LET X$(1)='SB'+" Vendor: "+'SF'+FNF$(A0$(3,P[0]),M0$)+" "+A1$(1,30)
1540 LET X1$=A0$(3,P[0]),X2$=PARAM$
1550 CALL "SYC.QB",X$,X1$,X2$
1600 REM " --- Format display fields"
1610 LET EXTENSION$="",INVOICE$="None    ",PAYMENT$="None    ",OPENED$=""
1620 LET OPENED$="",PHONE$="",FAX$=""
1630 IF FNP$(B1$(7,3))<>"" THEN LET INVOICE$=FNB$(B1$(7,3))
1640 IF FNP$(B1$(10,3))<>"" THEN LET PAYMENT$=FNB$(B1$(10,3))
1650 IF PHONE$<>"" THEN IF FNP$(A1$(122,4))<>"" THEN LET PHONE$=PHONE$+" x"+A1$(122,4)
1660 IF FNP$(A1$(186,10))<>"" THEN LET FAX$=FNF$(A1$(186,10),M9$)
1670 IF FNP$(A1$(156,3))<>"" THEN LET OPENED$=FNB$(A1$(156,3))
1700 REM " --- Display record"
1710 CALL "SYC.WB",NAME$,0
1720 IF P3$(1,1)="Y" THEN PRINT @(11,1),B0$(9,2)," ",X0$(6,19),
1730 PRINT @(11,ROW),B[0]:M1$,@(47,1),INVOICE$,@(47,2),PAYMENT$,@(11,5),B[3]:M1$,B[2]:M1$,B[4]:M1$,@(11,6),B[6]:M1$,B[5]:M1$,B[7]:M1$,@(11,7),B[9]:M1$,B[8]:M1$,B[10]:M1$,@(11,8),B[12]:M1$,B[11]:M1$,B[13]:M1$,
1740 CALL "SYC.WB",SNAME$,0
1750 PRINT 'CF',@(9,0),A1$(126,20),@(40),OPENED$,@(9,1),PHONE$,@(40),A1$(176,10),@(9,2),FAX$,@(40),A1$(160,15),
1800 REM " --- More records?"
1810 IF P3$(1,1)<>"Y" THEN GOTO 2000
1820 LET K$=KEY(APM02_DEV,END=2000)
1830 IF POS(A0$=K$)<>1 THEN GOTO 2000
1900 REM " --- Prompt for next record"
1910 CALL "SYC.WB",NAME$,0
1920 LET V0$="S",V1$="",V2$="",V3$="",V4$="",V0=1,V1=43,V2=HEIGHT-4
1930 PRINT 'SF',@(11,V2),"Press <Enter> For Next AP Type",
1940 GOSUB 7000
1950 PRINT @(0,V2),'CL',
1960 IF V3=6 THEN GOTO 4000
1970 IF V3=5 THEN GOTO 4000
1980 IF V3=4 THEN GOTO 2000
1990 GOTO 1300
2000 REM " --- Process option window"
2010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
2020 LET ACTION$="O",CHOICE1$=A0$(3),CHOICE2$=""
2030 GOSUB 5000
2040 IF CONTROL=1 THEN GOTO 1000
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CF',
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9950 EXIT 
9999 END

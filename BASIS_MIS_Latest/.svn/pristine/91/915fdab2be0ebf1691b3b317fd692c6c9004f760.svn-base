0010 REM "ARE - INVOICE ENTRY"
0020 REM "Program ARE.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETESC 9000
0055 SETERR 9000
0060 ON O0 GOTO 0070,1000
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARE-02",FILES$[2]="ARM-01",FILES$[3]="ARM-02"
0130 LET FILES$[4]="ARM-04",FILES$[5]="ARM-10",FILES$[6]="ART-01"
0135 LET FILES$[7]="SYS-01"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET ARE02_DEV=CHANNELS[1],ARM01_DEV=CHANNELS[2],ARM02_DEV=CHANNELS[3]
0185 LET ARM04_DEV=CHANNELS[4],ARM10_DEV=CHANNELS[5],ART01_DEV=CHANNELS[6]
0190 LET SYS01_DEV=CHANNELS[7]
0200 REM " --- IOLists
0210 ARM01A: IOLIST A0$,A1$
0220 ARM02A: IOLIST *,Y0$
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[2],G[4]
0460 FOR X=0 TO 4
0470 LET G[X]=NUM(G2$(X*2+1,2),ERR=0480)
0480 NEXT X
0485 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET X=0
0495 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0500 REM " --- Init Data
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 DIM A0$(8),B0$(45),B[4]
0530 LET K0=1,K9=8,L0=6,L1=7,L9=20
0540 LET A0$(1,2)=N0$
0550 LET M8$="(###)-###-####",M9$="###-##-####",G5=LEN(G5$),M1=LEN(M1$)
0700 REM " --- Background
0710 CALL "SYC.WC",1,0,80,0,0,5,0
0720 PRINT 'SB',@(5,3),"Customer:",@(9,4),"Name:",@(2,6),"Invoice Nbr:",@(4,7),"Dist Code:",@(3,8),"Terms Code:",@(1,9),"Invoice Date:",@(2,10),"Invoice Due:",@(1,11),"Discount Due:",@(1,12),"Invoice Type:",@(7,13),"Amount:",@(5,14),"Discount:",'SF',
0900 REM " --- Init File
0910 IF V$="END" THEN GOTO 1000
0911 IF V$="A" THEN GOTO 1100
0920 READ (ARM01_DEV,KEY=A0$(1,2),DOM=0930)
1000 REM " --- Key
1010 PRINT 'CF',
1015 PRINT @(15,4),'CL',@(15,3),'CL',
1020 LET V2$="",V2$=KEY(ARM01_DEV,END=1050)
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1050 LET V0$="M",V1$="KEC",V3$=P1$,V4$="Enter A Valid Customer Number (<F3>=Lookup)",V0=6,V1=15,V2=3
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 9900
1080 IF V3=3 THEN GOTO 2200
1090 IF V$="" THEN GOTO 0900
1100 LET A0$(3)=V$
1200 REM " --- Find Key
1220 FIND (ARM01_DEV,KEY=A0$(1,K9),DOM=1000)IOL=ARM01A
1230 DIM Y0$(20)
1240 FIND (ARM02_DEV,KEY=A0$+"  ",DOM=1300)IOL=ARM02A
1300 REM " --- Display
1320 GOSUB 5000
1340 GOTO 4000
2200 REM " --- Alpha Lookup
2240 CALL "SYC.LC",V$
2260 IF V$="" THEN GOTO 1015
2280 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$),'CL',
2290 GOTO 1100
4000 REM " --- Overlay
4020 LET O0=1
4100 RUN "ARE.BB"
5000 REM " --- Display
5020 PRINT 'SB',@(15,3),FNF$(A0$(3,P[0]),M0$),@(15,4),A1$(1,30),'SF',
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

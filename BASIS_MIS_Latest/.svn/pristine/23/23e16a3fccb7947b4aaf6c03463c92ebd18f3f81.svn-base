0010 REM "APE - MANUAL CHECK ENTRY 3"
0020 REM "Program APE.CC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "06Jan99 Caj: Use APE-22 not APE-02 for invoice number for temp vendors (1555,1560)
0032 REM "06Jan99 Caj: Fix possible Error 47 on Line 5620 (240,500,510)
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0060 ON O9 GOTO 0070,4000
0200 REM " --- IOLISTS"
0210 APE02A: IOLIST A0$(1),A1$,A[0],A[1],A[2]
0220 APM01A: IOLIST B0$,B1$
0230 APM02A: IOLIST C0$,C1$,C2$
0240 APM03A: IOLIST D0$,D1$(1)
0260 APM09A: IOLIST K0$,K1$
0370 APE05A: IOLIST X0$,X1$,X[0],X[1],X[2]
0500 REM " --- Initializations
0510 DIM D1$(102)
0900 REM " --- DISPLAY"
0910 IF O9=0 THEN GOTO 1000
0920 GOSUB 5200
1000 REM " --- VENDOR"
1010 PRINT 'CF',
1020 IF POS(A0$(31,1)="MR")=0 THEN GOTO 2000
1025 IF POS(A0$(31,1)="R")<>0 AND S$="Y" THEN GOTO 1200
1030 LET V2$=A0$(12,P[0])
1040 IF V2$(1,4)="TEMP" THEN LET V2$=""
1050 LET V0$="M",V1$="KE",V3$=P1$,V0=6,V1=15,V2=5
1055 LET V1$=V1$+"C",V4$="Valid Vendor Number (<F1>=Temporary Vendor/<F3>=Lookup)"
1060 GOSUB 7000
1070 IF V3=2 THEN RUN "APE.CB"
1075 IF V3=4 THEN GOTO 9900
1080 IF V3=1 THEN GOTO 1400
1085 IF V3=3 THEN CALL "SYC.LV",V$
1090 IF V$="" THEN GOTO 1000
1100 LET A0$(12,6)=V$
1110 PRINT @(V1,V2),FNF$(A0$(12,P[0]),M0$),
1200 REM " --- FIND KEY"
1205 DIM C1$(22)
1210 IF A0$(12,6)="TEMP  " THEN GOSUB 5700; GOTO 2000
1220 FIND (APM01_DEV,KEY=N0$+A0$(12,6),DOM=1000)IOL=APM01A
1240 FIND (APM02_DEV,KEY=N0$+A0$(12,6)+A0$(3,2),DOM=1241)IOL=APM02A; GOTO 1250
1242 IF P3$(1,1)<>"Y" THEN GOTO 1250
1244 LET V0$="S",V1$="C",V2$="",V3$="",V4$="'"+A0$(3,2)+"' AP Type Not Defined For Vendor "+FNF$(A0$(12,P[0]),M0$)+", <Enter> To Continue:",V0=1,V1=FNV(V4$),V2=22
1245 PRINT @(0,0),'RB',
1246 GOSUB 7000
1248 IF POS(A0$(31,1)="R")<>0 AND S$="Y" THEN RUN "APE.CB" ELSE GOTO 1000
1250 READ (APE05_DEV,KEY=A0$(1,17),DOM=1255)
1260 LET K$=KEY(APE05_DEV,END=1300)
1270 READ (APE05_DEV,KEY=K$)IOL=APE05A
1275 IF X0$(1,17)=A0$(1,17) AND X0$(31,1)="R" AND A0$(31,1)="R" THEN GOTO 6700
1280 IF X0$(1,17)=A0$(1,17) THEN IF A0$(31,1)<>"R" AND S$<>"Y" THEN GOTO 6600 ELSE GOTO 1300
1290 IF X0$(1,17)<>A0$(1,17) THEN IF A0$(31,1)<>"M" THEN GOTO 6500
1300 REM " --- DISPLAY"
1320 GOSUB 5000
1330 GOSUB 6000
1335 GOSUB 6200
1340 GOTO 2000
1400 REM " --- TEMP VENDOR"
1410 IF P3$(1,1)<>"Y" THEN PRINT @(15,3),'CL',
1420 LET V0$="Y",V1$="KC",V2$="",V3$="",V4$="Is This A Temporary Vendor (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
1430 GOSUB 7000
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- SET-UP"
1505 DIM D0$(14),D1$(105)
1510 GOSUB 6000
1520 PRINT 'SB',@(15,5),"Temp",'CL','SB',@(6,15),"Address:",'SF',
1525 GOSUB 5700
1530 LET A0$(12,6)="TEMP"
1540 IF A0$(31,1)="R" THEN IF O2=1 THEN GOTO 1900 ELSE GOTO 6500
1550 IF A0$(31,1)="M" THEN IF O2=1 THEN GOTO 6600
1555 READ (APE22_DEV,KEY=A0$(1,17)+"0",DOM=1560)
1560 LET K2$=KEY(APE22_DEV,END=1600)
1562 IF LEN(K2$)<28 THEN GOTO 1600
1565 IF K2$(28,2)<>"00" THEN GOTO 1600
1570 READ (APM03_DEV,KEY=K2$(1,4)+K2$(18,10),DOM=1600)IOL=APM03A
1575 IF D0$(1)<>K2$(1,4)+K2$(18,10) THEN GOTO 1600
1580 GOSUB 5600
1590 GOTO 1900
1600 REM " --- VENDOR NAME"
1610 PRINT 'SB',@(6,15),"Address:",'SF',
1620 LET V0$="S",V1$="R",V2$=D1$(1,30),V0=30,V1=25,V2=5
1640 GOSUB 7000
1650 IF NUM(V$,ERR=1660)=0 THEN GOTO 1600
1660 LET A1$=V$
1670 LET D1$(1,30)=V$
1690 IF V3=2 THEN GOTO 1000
1700 REM " --- ADDRESS"
1710 FOR X=0 TO 2
1720 LET V0$="S",V2$=D1$(31+X*24,24),V0=24,V1=15,V2=15+X
1730 GOSUB 7000
1740 LET D1$(31+X*24,24)=V$
1750 IF V3=4 THEN EXITTO 1900
1760 IF V3<>2 THEN GOTO 1795
1770 IF V3=2 THEN EXITTO 1600
1780 LET X=X-1
1790 GOTO 1720
1795 NEXT X
1900 REM " --- OPTIONS"
1910 LET V0$="Y",V1$="KC",V2$="",V3$="",V4$="Is The Name And Address Information Correct (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
1920 GOSUB 7000
1930 IF V$="" THEN GOTO 1960
1950 ON POS(V$="YN") GOTO 1900,1960,1600
2000 REM " --- CHECK DATE"
2010 IF POS(A0$(31,1)="R")<>0 AND S$="Y" THEN LET V1=1,V2=8,I0=0; GOTO 2070
2020 LET V0$="D",V1$="R",V2$=F0$(7,3),V0=8,V1=1,V2=8,I0=0,STATUS=0
2040 GOSUB 7000
2043 IF V3=4 THEN GOTO 9900
2045 IF V3=2 THEN LET V3=0
2050 IF GL$="Y" THEN CALL "GLC.EA",V$,"Y",PERIOD$,YEAR$,STATUS
2060 IF STATUS>99 THEN GOTO 2000
2065 LET A0$(28,3)=V$
2070 PRINT @(V1,V2),FNB$(A0$(28,3))
2090 GOTO 3900
3900 REM " --- SEQUENCE CONTROL"
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2000,4000
4000 REM " --- CORRECT"
4010 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Heading Information Correct (Y/N/Delete)? ",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4040 ON POS(V$="YN") GOTO 4000,4100,4050
4050 IF S$="Y" THEN GOTO 2020
4052 IF O9=0 THEN GOTO 1000
4055 IF V$="N" THEN GOTO 2000
4060 GOTO 4000
4100 WRITE (APE02_DEV,KEY=A$)IOL=APE02A
4110 PRINT @(0,15),'CE'
4120 IF A0$(31,1)<>"M" THEN RUN "APE.CB"
4130 LET O0=0
4140 RUN "APE.CD"
4200 REM " --- Delete
4202 REM " -- Remove Check Record
4205 REMOVE (APE02_DEV,KEY=A$(1,11),DOM=4220)
4220 REM " -- Remove Invoice Records
4225 READ (APE22_DEV,KEY=A$(1,11),DOM=4230)
4230 LET K$=KEY(APE22_DEV,END=4255)
4235 IF LEN(K$)<11 THEN GOTO 4255
4240 IF K$(1,11)<>A$ THEN GOTO 4255
4245 REMOVE (APE22_DEV,KEY=K$)
4250 GOTO 4230
4255 REM " -- Remove Distribution Records"
4260 READ (APE12_DEV,KEY=A$(1,11),DOM=4265)
4265 LET K$=KEY(APE12_DEV,END=4290)
4270 IF LEN(K$)<11 THEN GOTO 4290
4275 IF K$(1,11)<>A$ THEN GOTO 4290
4280 REMOVE (APE12_DEV,KEY=K$)
4285 GOTO 4265
4290 IF A0$(12,4)="TEMP" THEN IF A0$(31,1)="M" THEN REMOVE (APM03_DEV,KEY=D0$,DOM=4291)
4400 PRINT @(0,15),'CE',
4495 RUN "APE.CB"
5000 REM " --- Display
5070 PRINT @(15,5),FNF$(A0$(12,P[0]),M0$)," ",B1$(1,30),
5090 RETURN 
5200 REM " --- Display Date
5220 PRINT @(15,7),FNB$(A0$(28,3)),
5290 RETURN 
5600 REM " --- Temp Vendor
5610 PRINT @(15,5),'SB',"Temp  ",@(6,15),"Address:",'SF',
5620 PRINT @(25,5),D1$(1,30),@(15,15),D1$(31,24),@(15,16),D1$(55,24),@(15,17),D1$(79,24),
5690 RETURN 
5700 REM " --- Find Temp Vend address and name
5705 LET O2=0
5710 READ (APE05_DEV,KEY=A0$(1,11),DOM=5720)
5720 LET S0$=KEY(APE05_DEV,END=5790)
5725 IF S0$(1,17)<>A0$(1,17) THEN GOTO 5790
5730 READ (APM03_DEV,KEY=S0$(1,4)+S0$(18,10),DOM=5790)IOL=APM03A
5740 IF S0$(1,4)+S0$(18,10)<>D0$ THEN GOTO 5790
5745 LET O2=1
5750 IF A0$(31,1)="R" THEN GOSUB 5600
5790 IF O2=0 THEN PRINT @(0,15),'CL',
5795 RETURN 
6000 REM " --- Clear Screen
6020 PRINT @(0,L0),'CE'
6040 LET L=L1
6090 RETURN 
6200 REM " --- Comments
6210 LET L2=15
6215 PRINT 'SB',
6220 READ (APM09_DEV,KEY=N0$+A0$(12,6),DOM=6230)
6230 LET K$=KEY(APM09_DEV,END=6290)
6235 IF K$(1,8)<>A0$(1,2)+A0$(12,6) THEN GOTO 6290
6240 READ (APM09_DEV,KEY=K$)IOL=APM09A
6250 IF L2>20 THEN GOTO 6300
6260 PRINT @(14,L2),">",K1$,"<"
6270 LET L2=L2+1
6280 GOTO 6230
6290 PRINT 'SF',
6295 RETURN 
6300 REM " --- Comments Continued
6320 LET V0$="S",V1$="C",V2$="",V3$="",V4$="<Enter> For More Comments, <F4>=End:",V0=1,V1=FNV(V4$),V2=22
6330 GOSUB 7000
6340 IF V3=4 THEN GOTO 6390
6350 PRINT @(0,15),'CE','SB',
6360 LET L2=15
6370 GOTO 6260
6390 RETURN 
6400 REM " --- Full screen
6410 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="<Enter> To Continue/<F4> To End: ",V0=1,V1=FNV(V4$),V2=22
6420 GOSUB 7000
6430 IF V3=4 THEN LET V$="END"
6450 LET L=L1
6490 RETURN 
6500 REM " --- Display messages
6520 LET V4$="Check Does Not Exist For This Vendor"
6590 GOTO 6900
6620 LET V4$="Check Already Exists For This Vendor"
6690 GOTO 6900
6720 LET V4$="Check Has Already Been Reversed"
6790 GOTO 6900
6900 REM 
6910 DIM MESSAGE$[1]
6920 LET MESSAGE$[0]=V4$+" (<Enter>=Continue)"
6930 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
6990 GOTO 1050
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

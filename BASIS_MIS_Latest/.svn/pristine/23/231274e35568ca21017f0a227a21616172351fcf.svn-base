0010 REM "SYX - SpeedInstall Utility (Detail)"
0020 REM "Program SYX.JB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0210 SYM99A: IOLIST A0$,A1$(1),A2$,A[0],A[1],A[2],A[3],A[4]
0220 SYM03A: IOLIST B0$,B1$(1),B2$,B[0],B[1],B[2]
0500 REM " --- Initializations"
0520 DIM A0$(7),A1$(64),A[4],B1$(77),B[2]
0530 LET L0=8,L9=21,L=L0
0540 LET T0$=""
0550 DIM WARN$(30); LET WARN$(1)="***** File Not Found *****"
0900 REM " --- Display any existing lines"
0920 GOSUB 5100
0930 IF Q1=0 THEN GOTO 1400
0990 GOTO 4000
1000 REM " --- Overflow"
1010 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L0),'LD',
1020 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1120 LET K$=KEY(SYM99_DEV,END=4000)
1140 READ (SYM99_DEV)IOL=SYM99A
1160 GOSUB 5000
1170 IF MOD$<>"Y" THEN GOTO 1100
1180 LET I0=2
1190 GOTO 3300
2000 REM " --- Install?"
2010 LET V0$="Y",V1$="C",V2$=A1$(32,1),V3$="",V0=1,V1=75,V2=L,I0=1
2015 IF I_R$="I" THEN LET V4$="Install" ELSE LET V4$="Remove"
2020 LET V4$=V4$+" This File/Record (Y/N)?"
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3200,2000,3200
2050 LET A1$(32,1)=V$
2090 GOTO 3200
3200 REM " --- Sequence Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 2000,2000,3300
3300 REM " --- Line Correct?"
3310 LET V4$="<Enter>=Continue    <F2>=Change"
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3300,3200,3300,3350
3350 IF V$="" THEN GOTO 3400
3390 GOTO 3300
3400 REM " --- Write"
3420 WRITE (SYM99_DEV,KEY=A0$)IOL=SYM99A
3450 LET L=L+1
3460 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
4000 REM " --- Options"
4010 LET V4$="Is The Above Information Correct (Y/N/List/Review)?"
4020 LET V0$="S",V1$="C",V2$="",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4150
4050 IF V$="L" OR V$="LIST" THEN GOTO 4400
4070 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4080 IF LEN(V$)=6 THEN GOTO 4350
4090 ON POS(V$="YN") GOTO 4000,4100,4300
4100 REM " --- Confirm OK To Install/Remove Records"
4110 IF I_R$="I" THEN LET X$="Install" ELSE LET X$="Remove"
4120 LET PROMPT$="Are You Ready To "+X$+" These Records"
4130 CALL "SYC.YN",0,PROMPT$,0,V$,V3
4140 IF V$="YES" THEN RUN "SYX.JC"
4150 REM " --- Return to Header Overlay
4160 RUN "SYX.JA"
4300 REM " --- Not Correct"
4310 LET V4$="Correct Which File Entry?"
4320 LET V0$="S",V1$="C",V2$="",V3$="",V0=6,V1=FNV(V4$),V2=22
4330 GOSUB 7000
4340 ON V3 GOTO 4350,4300,4300,4300,4390
4350 REM " --- Verify File For Restart"
4360 READ (SYM99_DEV,KEY=V$,DOM=4370)
4370 LET A0$=KEY(SYM99_DEV,END=4390)
4380 IF POS(V$=A0$)=1 THEN GOTO 4650
4390 GOTO 4000
4400 REM " --- List"
4410 DIM A0$(7)
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4620 DIM A0$(7)
4650 REM " --- Restart"
4660 READ (SYM99_DEV,KEY=A0$(1,6),DOM=4670)
4670 GOSUB 5400
4690 GOTO 1000
5000 REM " --- Display"
5005 LET Q1=1,MOD$="Y"
5010 DIM INFO$[20]
5015 CALL "SYC.VA",A1$(33,2),INFO$[ALL]
5020 IF INFO$[3]<>"Y" THEN LET MOD$="N"; GOTO 5090
5025 IF A0$(1,6)=T0$ THEN GOTO 5080
5030 LET T0$=A0$(1,6),B1$(1)="Not On File"
5035 READ (SYM03_DEV,KEY=A0$(1,6),DOM=5040)IOL=SYM03A
5040 LET DESC$=B1$(1,30)
5045 LET FILES$[5]=T0$
5050 CALL "SYC.DA",1,5,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
5055 LET SOURCE_DEV=CHANNELS[5]
5060 IF STATUS>0 THEN LET DESC$=WARN$
5070 CLOSE (SOURCE_DEV,ERR=5080)
5080 PRINT @(0,L),'CL',@(0),A0$(1,6),@(7),DESC$,@(38),A0$(7,1),@(41),A1$(1,30),@(72),A1$(33,2),@(75),A1$(32,1),
5090 RETURN 
5100 REM " --- List"
5105 LET Q1=0
5110 GOSUB 5400
5120 READ (SYM99_DEV,KEY=A0$,DOM=5130)
5130 LET K$=KEY(SYM99_DEV,END=5300)
5160 READ (SYM99_DEV)IOL=SYM99A
5190 IF L<=L9 THEN GOTO 5250
5200 REM " --- Continue"
5210 LET V4$="<Enter>=Continue:"
5215 LET V0$="S",V1$="CE",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5230 IF V3=4 THEN GOTO 5300
5240 GOSUB 5400
5250 GOSUB 5000
5260 IF MOD$="Y" THEN LET L=L+1
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5420 PRINT @(0,L0),'CE',
5440 LET L=L0
5490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

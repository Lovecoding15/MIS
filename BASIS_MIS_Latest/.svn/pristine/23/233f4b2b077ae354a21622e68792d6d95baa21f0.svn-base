0001 REM <ec_sn.bbx> 
0010 REM Generate new serial number, check for duplicates (E-Commerce)
0020 SETERR ERR_EXIT
0030 SETESC ERR_EXIT
0040 ENTER FIRM$,ITEM$,SERIALNUM$,SN_PRFX$
0050 DIM NEWSN$:"SERIAL_PFX:C(3),AVAILABLE:C(7),SERIAL_CNTR:N(6)"
0060 LET FILES=4,C=1; DIM CHAN[FILES]
0070 CALL "ec_open::SERIALNUMBERS"; LET CHAN[C]=SERIALNUMBERS,C=C+1
0080 CALL "ec_open::ARE83"; LET CHAN[C]=ARE83,C=C+1
0090 CALL "ec_open::CSM01"; LET CHAN[C]=CSM01,C=C+1
0100 CALL "ec_open::SNM01"; LET CHAN[C]=SNM01,C=C+1
0120 IF CVS(SN_PRFX$,1+2)<>"" THEN GOTO GET_SERIAL_NBR
0140 READ (CSM01,KEY=FIRM$+ITEM$(1,3),DOM=ERR_EXIT)*,*,*,CS3$
0150 LET SN_PRFX$=CS3$(1,3)
0160 GET_SERIAL_NBR: 
0170 EXTRACT (SERIALNUMBERS,KEY=SN_PRFX$,ERR=ERR_EXIT)SERIALNUMBERS$
0180 LET SERIALNUM$=SERIALNUMBERS.SERIAL_PFX$+STR(SERIALNUMBERS.SERIAL_CNTR:"000000")
0190 IF SERIALNUMBERS.SERIAL_PFX$="TAO" THEN LET SERIALNUM$=SERIALNUM$(4); REM No prefix for Taos
0200 LET SERIALNUMBERS.SERIAL_CNTR=SERIALNUMBERS.SERIAL_CNTR+1
0210 WRITE (SERIALNUMBERS,KEY=SN_PRFX$,ERR=ERR_EXIT)SERIALNUMBERS$
0220 REM "--- Check for Duplicate serial number"
0230 READ (ARE83,KEY="",DOM=ARE83READ)
0240 ARE83READ: READ (ARE83,END=ARE83END)*,*,S$
0250 IF POS(SERIALNUM$=S$)=1 THEN GOTO ERR_EXIT ELSE GOTO ARE83READ
0260 ARE83END: 
0270 LET SERIALNUM$=SERIALNUM$+FILL(20-LEN(SERIALNUM$))
0280 READ (SNM01,KEY=SERIALNUM$,DOM=CLOSE_EXIT)
0290 ERR_EXIT: 
0300 LET SERIALNUM$=""
0310 CLOSE_EXIT: 
0320 IF FILES=0 THEN GOTO PGM_EXIT
0330 FOR I=1 TO FILES
0340 CLOSE (CHAN[I],ERR=NEXTI)
0350 NEXTI: NEXT I
0360 PGM_EXIT: EXIT
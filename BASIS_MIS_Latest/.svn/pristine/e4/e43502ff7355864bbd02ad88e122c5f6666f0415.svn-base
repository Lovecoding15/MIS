0010 REM "TMX - TM JOB HISTORY REBUILD"
0020 REM "PROGRAM TMX.CA"
0040 REM 
0041 REM " * MAS * BAO * 12/14/94 - REBUILD TMM-04
0042 REM "01/06/97 KJS WA: #221 ADS"
0079 REM 
0080 BEGIN 
0090 SETERR 9000
0100 REM "OPEN/LOCK FILES"
0110 OPEN (1)"TMM-04"
0120 OPEN (2)"ART-04"
0130 OPEN (3)"OPE-01"
0140 OPEN (4)"TMM-01"
0160 OPEN (6)"SYS-01"
0200 REM "IOLISTS"
0210 IOLIST A0$,A1$,A[0],A[1],A[2]
0220 IOLIST B0$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10]
0230 IOLIST OP$[ALL]
0400 REM " --- Parameters"
0401 GOSUB 62000; REM "WA: #221
0420 LET N0$=PRM$[FID,TFIRM],N1$=PRM$[FID,TFNAME],N2$="TM",N4$=PRM$[FID,TPNAME]; REM "WA: #221
0500 REM "INIT DATA"
0520 DIM A[2],B[10],OP$[7],J$(80)
0700 REM "BACKGROUND"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,6,0
0730 PRINT 'SB',@(30,5),"Updating:",'SF',
0800 REM "OPTIONS"
0810 PRINT @(10,3),"Do You Wish To Rebuild TM Job History File? <Yes/No>: ",
0820 LET V0$="S",V1$="",V2$="",V3$="",V4$="",V0=3,V1=70,V2=3
0830 GOSUB 7000
0840 IF V$="NO" THEN GOTO 9900
0860 IF V$<>"YES" THEN GOTO 0800
0870 PRINT @(0,3),'CL',
0900 REM "CLEAR FILES"
0920 LET V=1
0940 CALL "SYC.FA",V
1100 REM "MAIN READ"
1110 LET K$=KEY(2,END=4000)
1112 IF LEN(K$)<20 THEN READ (2); GOTO 1110
1115 IF K$(18,3)<>"000" THEN READ (2,KEY=K$(1,17)+$FF$,DOM=1110); GOTO 1110
1120 READ (2,END=2000)IOL=0220
1140 PRINT @(40,5),B0$(1,20),
1145 LET OP$[7]=J$(1,30)
1150 READ (3,KEY=B0$(1,10)+B0$(42,7),DOM=1152)IOL=0230; REM "OPE-01"
1151 LET OP$[7]=OP$[7]+J$(1,30),OP$[7]=OP$[7](1,30)
1160 READ (4,KEY=N0$+B0$(5,6),KNUM=2,DOM=1161); REM "TMM-01"
1165 DIM TM$(30)
1170 LET K4$=KEY(4,END=1190)
1180 IF POS(N0$+B0$(5,6)=K4$)<>1 THEN GOTO 1190
1185 READ (4)TM$
1190 LET TM$=TM$(3,20)
1200 REM "ACCUM LABELS
1220 LET LABELS=0
1230 LET K2$=KEY(2,END=1250)
1231 IF LEN(K2$)<20 THEN GOTO 1250
1233 IF K2$(1,17)<>K$(1,17) THEN GOTO 1250
1235 READ (2)X0$,*,*,*,*,*,QTY
1237 IF X0$(21,1)="L" THEN LET LABELS=LABELS+QTY
1240 GOTO 1230
1250 REM "WRITE RECORD
1260 LET A0$=N0$+TM$+FNJ$(B0$(24,3))+B0$(11,7),A1$=B0$(77,3)+B0$(42,7)+B0$(24,3)+OP$[7]+J$(1,10),A[0]=LABELS,A[1]=B[6]
1280 WRITE (1,KEY=A0$)IOL=0210
1290 GOTO 1100
4000 REM "END"
4100 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR((ASC(Q$)-32)*POS(" "<>Q2$(2,1)):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(NUM(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8080 DEF FNH(Q$)=ASC(Q$)-32
8090 DEF FNV$(Q)=CHR(Q+32)
8100 DEF FNJ$(Q1$)=FNA$(Q1$(1),Q1$)+FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
62000 REM " --- Set-up ADD+ON Environment (WA: #221)"
62010 DIM PRM$[0]; CALL "ADC.01",PRM$[ALL],STATUS
62020 IF STATUS<>0 THEN EXITTO 9900
62030 LET FID=0,AP=1,AR=2,BM=3,DC=4,FA=5,GL=6,IV=7,MP=8,OP=9,PO=10,PR=11,SA=12,SH=13,SY=14,WO=15,TS=21,TB=22,DC=23,V2=24,EF=25; REM " ---- Module Id Numbers"
62040 DIM J$(80,"-"),J0$(40,"0"),J1$(80)
62050 LET INSTALL=0,KEYLEN=1,KEYMAX=2,KEYMASK=3,CPERIOD=4,CYEAR=5,DMASK=6,UMASK=7,RMASK=8,TWO=9,THREE=10,FOUR=11,EMASK=12,TOPCODE=1,TDATE=2,TMENU=3,TPRINTER=4,TSECURE=5,TFIRM=6,TPMODE=7,TPROG=8,TPOPTS=9,TFNAME=10,TPNAME=11,TLDATE=12
62090 RETURN 
65000 END

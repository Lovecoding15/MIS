0010 REM "APM - Vendor Maintenance (Open Invoices)"
0020 REM "Program APM.MF"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0200 REM " --- IOLIST's"
0210 APT01A: IOLIST X0$,X[0],X[1],X[2]
0220 APM10A: IOLIST APDESC$(1)
0310 APT11A: IOLIST W0$,W1$(1),W[0],W[1],W[2]
0400 REM " --- Parameters"
0450 LET WIDTH=80,HEIGHT=18,WIN_X=0,WIN_Y=5,PAGES=20
0460 LET TITLE$="Open Invoices",MAXROW=HEIGHT-5
0500 REM " --- Initializations"
0510 DIM X0$(73),X[2],W0$(22),W1$(11),W[2],J0$(40,"0"),J1$(40)
0530 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),APDESC$(32)
0550 LET ROW=2,MASK$=M1$
0590 CALL "SYC.RM",MASK$,MASK,9,11
0600 REM " --- Print positions"
0610 DIM O[2]
0620 LET O[2]=WIDTH-2-MASK,O[1]=O[2]-MASK,O[0]=O[1]-MASK
0700 REM " --- Background"
0705 PRINT @(0,11),'CE',
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0715 LET HEADING$(2)="Invoice No    Trans    Date   Due   PG H"
0720 LET HEADING$(O[0]+MASK-7)="Amount",HEADING$(O[1]+MASK-9)="Discount"
0725 IF P3$(3,1)="Y" THEN LET HEADING$(O[2]+MASK-10)="Retention"
0730 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0735 PRINT @(0,0),'CS','SB','BR',HEADING$,'ER','SF',
0740 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
0750 IF P3$(1,1)="Y" THEN PRINT 'CF','SB',@(31,ROW),"AP Type: ",'SF',
0760 PRINT 'CF','SB',@(14,ROW+1),"Beginning Invoice Number: ",@(8,ROW+2),"Display Paid, Un-Paid, Or Both:",'SF'
0800 REM " --- AP Type"
0805 IF P3$(1,1)<>"Y" THEN LET APTYPE$="  "; GOTO 0850
0810 LET V4$="Enter AP Type (<F3>=Lookup)"
0815 PRINT @(0,HEIGHT-4),'CL',@(FNC(V4$,WIDTH-2),HEIGHT-4),V4$,
0820 READ (APM02_DEV,KEY=A0$,DOM=0822)
0822 LET V2$="",V2$=KEY(APM02_DEV,END=0824)
0824 IF V2$(1,8)<>A0$(1,8) THEN LET V2$=""
0826 IF V2$<>"" THEN LET V2$=V2$(9,2)
0828 LET V0$="S",V1$="",V3$="",V0=2,V1=40,V2=ROW,V4$=""
0830 GOSUB 7000
0833 ON V3 GOTO 0840,0800,0800,0835,9900
0835 GOSUB 5200
0840 LET V$=V$+"  ",APTYPE$=V$(1,2)
0842 READ (APM10_DEV,KEY=N0$+"A"+APTYPE$,DOM=0800)IOL=APM10A
0844 READ (APM02_DEV,KEY=A0$+APTYPE$,DOM=0800)
0846 PRINT @(V1,V2),'CL',APTYPE$," ",APDESC$(6,20),
0850 REM " --- Beginning Invoice Number"
0853 LET V4$="Enter a Beginning Invoice Number (<Enter>=All)"
0855 PRINT @(0,HEIGHT-4),'CL',@(FNC(V4$,WIDTH-2),HEIGHT-4),V4$,
0860 LET V0$="S",V1$="",V2$="",V3$="",V4$="",V0=10,V1=40,V2=ROW+1
0875 GOSUB 7000
0880 ON V3 GOTO 0885,0850,0820,0850,9900
0885 LET FIRST$=V$
0900 REM " --- Paid, Unpaid or Both?"
0903 LET V4$="P=Paid Invoices   U=Unpaid Invoices   B=Both"
0905 PRINT @(0,HEIGHT-4),'CL',@(FNC(V4$,WIDTH-2),HEIGHT-4),V4$,
0910 LET V0$="S",V1$="",V2$="B",V3$="PUB",V4$="",V0=1,V1=40,V2=ROW+2
0915 GOSUB 7000
0920 ON V3 GOTO 0925,0900,0850,0900,9900
0925 IF POS(V$="PUB")=0 THEN GOTO 0900
0930 LET OPTION$=V$
0940 PRINT @(0,ROW),'CL',@(0,ROW+1),'CL',@(0,ROW+2),'CL',@(0,HEIGHT-4),'CL',
0950 REM " --- Position ART-01"
0960 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,5]
0965 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,ROW=2,T0$=""
0970 LET INVTOTAL=0,BALTOTAL=0
0980 LET X0$(1)=A0$(1,2)+APTYPE$+A0$(3),X0$(11)=FIRST$
0990 READ (APT01_DEV,KEY=X0$(1,22),DOM=1000)IOL=APT01A
1000 REM " --- Read next invoice"
1010 LET K$=KEY(APT01_DEV,END=1900)
1020 IF POS(N0$=K$)<>1 THEN GOTO 1900
1030 IF K$(3,2)<>APTYPE$ THEN GOTO 1900
1040 IF K$(5,6)<>A0$(3) THEN GOTO 1900
1100 REM " --- Header or detail?"
1110 LET FKEY=0
1150 REM " --- Invoice header"
1160 READ (APT01_DEV,KEY=K$)IOL=APT01A
1170 GOSUB 6500
1180 IF OPTION$="P" THEN IF BALANCE<>0 THEN GOTO 1800
1190 IF OPTION$="U" THEN IF BALANCE=0 THEN GOTO 1800
1200 REM " --- Display invoice totals"
1210 IF T0$=K$(11,10) THEN GOTO 1250
1220 GOSUB 6200
1230 IF FKEY=4 THEN GOTO 0750
1250 REM " --- Reposition to invoice header if OK to display"
1290 IF NUMBER>0 THEN READ (APT01_DEV,KEY=X0$(1,22))
1300 REM " --- Header window record"
1310 DIM DISP$(WIDTH-2)
1315 IF L+1>MAXROW THEN GOSUB 5400
1320 IF FKEY=4 THEN GOTO 0750
1325 LET DISP$(2)=X0$(11,10),DISP$(13)="   Invoice"
1330 IF X0$(68,1)="M" THEN LET DISP$(13,2)="MI"
1335 LET DISP$(24)=FNH$(X0$(29,3)),DISP$(31)=FNH$(X0$(35,3))
1340 LET DISP$(38)=X0$(25,2),DISP$(41)=X0$(67,1)
1345 LET DISP$(O[0])=STR(X[0]:MASK$),DISP$(O[1])=STR(X[1]:MASK$)
1350 IF P3$(3,1)="Y" THEN LET DISP$(O[2])=STR(X[2]:MASK$)
1380 LET LLIST$[PAGE,X1,1]=DISP$,X0=X0+1,X1=X1+1,L=L+1
1390 LET INVTOTAL=INVTOTAL+INVOICE,BALTOTAL=BALTOTAL+BALANCE
1400 LET DETAIL$="N"
1500 REM " --- Detail window record"
1505 READ (APT11_DEV,KEY=X0$(1,20),DOM=1506)IOL=APT11A
1510 LET K11$=KEY(APT11_DEV,END=1000)
1520 IF K11$(1,20)<>X0$(1,20) THEN GOTO 1000
1525 LET DETAIL$="Y"
1530 READ (APT11_DEV,KEY=K11$)IOL=APT11A
1540 IF L+1>MAXROW THEN GOSUB 5400
1543 IF FKEY=4 THEN GOTO 0750
1545 DIM DISP$(WIDTH-2)
1550 LET X$=W1$(1,11)
1555 GOSUB 6400
1560 LET DISP$(13)=TRANS$,DISP$(24)=FNH$(X$(2,3))
1580 LET DISP$(O[0])=STR(W[0]:MASK$),DISP$(O[1])=STR(W[1]:MASK$)
1590 IF P3$(3,1)="Y" THEN LET DISP$(O[2])=STR(W[2]:MASK$)
1670 LET LLIST$[PAGE,X1,1]=DISP$,X0=X0+1,X1=X1+1,L=L+1
1700 REM " --- Detail processing completed"
1710 IF FKEY=4 THEN GOTO 0750
1800 REM " --- Loop back for next invoice"
1890 GOTO 1510
1900 REM " --- No more invoices"
1910 IF X0>0 THEN GOTO 2000
1915 CALL "SYC.WB",NAME$,0
1920 LET V0$="Sa",V1$="",V2$="",V3$="",V4$=""
1925 LET V0=1,V1=35,V2=INT(HEIGHT/2)-1
1930 PRINT @(1,V2),"No records found (<Enter>=Continue):"
1940 GOSUB 7000
1990 GOTO 0750
2000 REM " --- Display last page"
2010 GOSUB 6200
2015 IF FKEY=4 THEN GOTO 0750
2020 GOSUB 6000
2025 IF FKEY=4 THEN GOTO 0750
2030 IF X1=0 THEN GOTO 4000
2040 LET ANSWER$="|EOF"
2050 GOSUB 5400
4000 REM " --- All done"
4090 GOTO 0750
5200 REM " --- A/P Type Lookup"
5210 DIM DESCRIPTION$[1],FIELD[1],POSITION[1],LENGTH[1]
5220 LET NUMBER=1,COLUMN=79,LROW=3,TITLE$="AP Type Lookup"
5230 LET DESCRIPTION$[0]="Type",FIELD[0]=1,CHANNEL=APM10_DEV
5240 LET POSITION[0]=4,LENGTH[0]=2
5250 LET DESCRIPTION$[1]="Description",FIELD[1]=1
5260 LET POSITION[1]=6,LENGTH[1]=20
5270 CALL "SYC.LK",CHANNEL,"A",NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,LROW,V$
5290 RETURN 
5400 REM " --- Middle window processing"
5410 LET MODE=5,X1=1,L=1
5420 CALL "SYC.SA",MODE,LLIST$[ALL],MORE$[ALL],ANSWER$,PAGE,MAXROW,NAME$,HEIGHT,WIDTH,FKEY
5460 IF FKEY=4 THEN GOTO 5490
5470 IF FKEY<>-17 THEN LET PAGE=PAGE+1
5480 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAXROW,5,SHEIGHT-2
5490 RETURN 
6000 REM " --- Vendor Total"
6020 IF L+1>MAXROW THEN GOSUB 5400
6030 IF FKEY=4 THEN GOTO 6290
6040 DIM DISP$(WIDTH-2)
6050 LET DISP$(17)="Vendor Total",DISP$(O[0])=STR(BALTOTAL:MASK$)
6070 LET LLIST$[PAGE,X1,1]=DISP$,X0=X0+1,X1=X1+1,L=L+1
6090 RETURN 
6200 REM " --- Invoice Total"
6210 IF T0$="" THEN GOTO 6270
6215 IF DETAIL$="N" THEN GOTO 6270
6220 IF L+1>MAXROW THEN GOSUB 5400
6230 IF FKEY=4 THEN GOTO 6290
6240 DIM DISP$(WIDTH-2)
6250 LET DISP$(16)="Invoice Total",DISP$(O[0])=STR(LASTBALANCE:MASK$)
6260 LET LLIST$[PAGE,X1,1]=DISP$,X0=X0+1,X1=X1+1,L=L+1
6270 LET T0$=K$(11,7),LASTBALANCE=BALANCE
6290 RETURN 
6400 REM " --- Transaction Type"
6410 LET TRANS$=""
6420 IF X$(1,1)="A" THEN LET TRANS$="    Adjust"
6430 IF X$(1,1)="C" THEN LET TRANS$="CC "+X$(5,7)
6440 IF X$(1,1)="M" THEN LET TRANS$="MC "+X$(5,7)
6450 IF X$(1,1)="R" THEN LET TRANS$="RC "+X$(5,7)
6460 IF X$(1,1)="S" THEN LET TRANS$="MI "+X$(5,7)
6490 RETURN 
6500 REM " --- Retrieve payment and adjustments"
6510 LET INVOICE=X[0],APPLIED=0,NUMBER=0
6515 READ (APT11_DEV,KEY=X0$(1,20),DOM=6516)
6520 LET K11$=KEY(APT11_DEV,END=6580)
6530 IF K11$(1,20)<>X0$(1,20) THEN GOTO 6580
6540 DIM W[2]
6545 READ (APT11_DEV,KEY=K11$)IOL=APT11A
6550 LET NUMBER=NUMBER+1
6560 IF W1$(1,1)="A" THEN LET INVOICE=INVOICE+W[0]
6570 IF W1$(1,1)<>"A" THEN LET APPLIED=APPLIED+W[0]
6575 GOTO 6520
6580 LET BALANCE=INVOICE+APPLIED
6590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8070 DEF FNH$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9220 LET O0=0
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to customer maintenance menu"
9910 CALL "SYC.WD",NAME$
9950 RUN "APM.MA"
9999 END

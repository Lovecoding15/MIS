0010 REM "IVC - Inventory SpeedSearch (Transaction History)"
0020 REM "Program IVC.QD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0039 REM "16Jul97 JWK: Files left open causes error 16 (9910)
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open/Lock Files"
0110 LET FILES=8
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="IVM-01",FILES$[2]="IVM-03",FILES$[3]="IVM-10"
0140 LET FILES$[4]="IVT-04",FILES$[5]="IVM-14"
0150 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET IVM01_DEV=CHANNELS[1],IVM03_DEV=CHANNELS[2],IVM10_DEV=CHANNELS[3]
0180 LET IVT04_DEV=CHANNELS[4],IVM14_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1)
0220 IVT04A: IOLIST B0$(1),B1$(1),B2$(1),B3$,B[ALL]
0230 IVM10C: IOLIST X0$(1)
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$,M4$,M5$
0270 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$
0280 APS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$
0290 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0405 DIM P[5],G[4],INFO$[20]
0410 LET X$=STBL("!SPEEDSEARCH"),N0$=X$(1,2),MAXPAGE=NUM(X$(66,2))
0414 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0416 LET SNAME$=FNP$(X$(19,8)),SWIDTH=NUM(X$(43,2)),SHEIGHT=NUM(X$(45,2))
0420 LET MAXOPT=NUM(X$(57,2)),SYS01_DEV=NUM(STBL("!SYS01")),ONAME$=FNP$(X$(27,8))
0422 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0425 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0430 LET PAGES=INT((DSZ-5000)/4000),TNAME$=FNP$(X$(3,8))
0435 IF PAGES<3 THEN GOTO 9900
0440 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0445 FIND (SYS01_DEV,KEY=N0$+"IV"+"00",DOM=9900)IOL=IVS01A
0450 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(5,1)),AR$="N",AP$="N",GL$="N"
0455 LET P[3]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2)),P[5]=NUM(P2$(13,2))
0460 CALL "SYC.VA","AR",INFO$[ALL]
0465 LET AR$=INFO$[20]
0470 CALL "SYC.VA","AP",INFO$[ALL]
0475 LET AP$=INFO$[20]
0480 CALL "SYC.VA","IV",INFO$[ALL]
0485 LET GL$=INFO$[9]
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM A0$(22),A1$(60),A[12],OPTLIST$[2,MAXOPT,6],FILES[4],PARAMS[6]
0520 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),MASKS$[3],X0$(32),IFILES[14]
0530 DIM ITEM$[6]
0540 LET D_STATUS=0,MAXROW=HEIGHT-5,PAGE=1,H0=WIDTH-2,A0$(1)=N0$+OPTION$[3]
0545 LET FILES[0]=IVM01_DEV,FILES[1]=IVM10_DEV,MASKS$[0]=N0$,M1=LEN(M1$)
0550 LET M2=LEN(M2$),M3=LEN(M3$),IFILES[1]=IVM01_DEV,IFILES[14]=IVM14_DEV
0555 LET PARAMS[0]=P[0],PARAMS[4]=P[3],PARAMS[5]=P[4],PARAMS[6]=P[5],DONE=0
0560 LET PGM$=PGM(-2),OPTION=0
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,ONAME$
0615 REM " --- Additional File Opens"
0620 IF AR$="N" THEN GOTO 0635
0625 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=0645)IOL=ARS01A
0630 LET PARAMS[1]=NUM(P2$(1,2)),MASKS$[1]=M0$,FILES$[6]="ARM-01"
0635 IF AP$="N" THEN GOTO 0650
0640 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=0670)IOL=APS01A
0645 LET PARAMS[2]=NUM(P2$(1,2)),MASKS$[2]=M0$,FILES$[7]="APM-01"
0650 IF GL$="N" THEN GOTO 0700
0655 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=0700)IOL=GLS01A
0660 LET PARAMS[3]=NUM(G2$(7,2)),MASKS$[3]=G5$,FILES$[8]="GLM-01"
0670 CALL "SYC.DA",1,6,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS>0 THEN GOTO 9900
0690 LET FILES[2]=CHANNELS[6],FILES[3]=CHANNELS[7],FILES[4]=CHANNELS[8]
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0720 LET HEADING$(2)="  Date   Type",HEADING$(H0-M1-M3-9)="Quantity"
0730 LET HEADING$(H0-M1-5)="Cost",HEADING$(H0-10)="Extension"
0740 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0750 PRINT @(0,0),'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",SNAME$,0
0920 PRINT 'CF',
0930 CALL "SYC.WB",TNAME$,0
0940 PRINT 'CS',
0990 GOTO 1100
1000 REM " --- Process key field"
1010 GOSUB 6000
1015 CALL "SYC.WB",TNAME$,0
1020 PRINT @(0,0),'CS','SB'," Item:",'SF',
1025 LET V2$="",V2$=KEY(IVM01_DEV,END=1040)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1035 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1040 LET ACTION$="V",V1=7,V2=0,V4$=""
1045 CALL "IVC.IA",ACTION$,V1,V2,IFILES[ALL],N0$,V2$,V4$,PARAMS[ALL],ITEM$[ALL],A[ALL],V3
1050 ON V3 GOTO 1060,1000,1000,1000,3800,3900,3800
1060 LET V$=FNP$(ITEM$[0])
1070 IF V$="" THEN GOTO 0900
1080 LET A0$(3)=V$
1100 REM " --- Retrieve record for display"
1110 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,SHEIGHT-2],X$(WIDTH-2)
1120 DIM ITEMDESC$(WIDTH-2-P[0]-8),B0$(32),B1$(96),B2$(16),B[4]
1125 LET LINE=1,L=1,X0=0,X1=1,PAGE=1
1130 READ (IVM01_DEV,KEY=A0$,DOM=1000)IOL=IVM01A
1135 LET ITEMDESC$(1)=FNITEM$(A1$,P[3],P[4],P[5])
1140 PRINT @(0,0),'CL','SB'," Item: ",'SF',A0$(3,P[0])," ",ITEMDESC$
1145 LET X$="",X1$=A0$(3,P[0]),X2$=PARAM$,BEGDATE$="",ENDDATE$="",WHSE$=""
1150 CALL "SYC.QB",X$,X1$,X2$
1160 READ (IVM03_DEV,KEY=A0$(1,2)+"G"+A0$(3),DOM=1170)
1170 LET K$=KEY(IVM03_DEV,END=1200)
1180 IF POS(A0$(1,2)+"G"+A0$(3)=K$) THEN LET WHSE$=K$(24,2)
1200 REM " --- Warehouse ID"
1205 PRINT @(1,1),'CL','SB',"Whse:",'SF',
1210 LET V0$="S",V1$="KE",V2$=WHSE$,V3$="",V4$="",V0=2,V1=7,V2=1
1215 GOSUB 7000
1240 ON V3 GOTO 1260,1200,1000,1250,3800,3900,3800
1250 CALL "IVC.LK",3,V1,V2,V$
1260 LET WHSE$=V$+"  ",WHSE$=WHSE$(1,2)
1270 FIND (IVM10_DEV,KEY=N0$+"C"+WHSE$,DOM=1200)IOL=IVM10C
1280 PRINT @(V1,V2),WHSE$," ",X0$(6,20),
1300 REM " --- Beginning Date"
1305 PRINT @(1,2),'CL','SB',"Beginning Date:",@(28),"Ending Date:",'SF',
1310 LET V0$="D",V1$="E",V2$=BEGDATE$,V3$="",V4$="",V0=3,V1=17,V2=2
1320 GOSUB 7000
1330 ON V3 GOTO 1340,1300,1200,1300,3800,3900,3800
1340 IF V$="" THEN PRINT @(V1,V2),"First   ",
1345 LET BEGDATE$=V$
1350 REM " --- Ending Date"
1360 LET V0$="D",V1$="E",V2$=ENDDATE$,V3$="",V4$="",V0=3,V1=41,V2=2
1365 GOSUB 7000
1370 ON V3 GOTO 1380,1350,1300,1350,3800,3900,3800
1380 IF V$="" THEN PRINT @(V1,V2),"Last    ",
1390 LET ENDDATE$=V$
1400 REM " --- Position file"
1480 LET B0$(1)=A0$(1,2)+WHSE$+A0$(3)+BEGDATE$
1490 READ (IVT04_DEV,KEY=B0$,DOM=1500)
1500 REM " --- Read next IVT-04 record"
1510 LET K$=KEY(IVT04_DEV,END=1900)
1520 IF POS(N0$+WHSE$+A0$(3)=K$)<>1 THEN GOTO 1900
1525 IF ENDDATE$<>"" THEN IF B0$(25,3)>ENDDATE$ THEN GOTO 1900
1540 READ (IVT04_DEV,KEY=K$)IOL=IVT04A
1550 REM " --- Display page?"
1555 IF L<MAXROW THEN GOTO 1600
1560 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
1565 GOSUB 5000
1570 IF DONE THEN GOTO 4000
1585 IF ANSWER$<>"" THEN GOTO 3000
1590 LET L=1,X1=1
1600 REM " --- Middle window record"
1610 DIM G1$(WIDTH-2),DESCRIPTION$[2]
1620 CALL "IVC.TD",B1$,FILES[ALL],PARAMS[ALL],MASKS$[ALL],TYPE$,DESCRIPTION$[ALL]
1670 LET G1$(2)=FNB$(B0$(25,3))+" "+TYPE$,G1$(H0-M1-M3-M2)=STR(B[0]:M2$)
1680 LET G1$(H0-M1-M3)=STR(B[1]:M3$),G1$(H0-M1)=STR(B[0]*B[1]:M1$)
1690 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$(3)
1700 REM " --- Bottom window record"
1710 DIM G2$(SWIDTH-2)
1715 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2)
1720 LET G2$(2)=DESCRIPTION$[0],MORE$[PAGE,X1,1]=G2$
1740 LET G2$(2)=DESCRIPTION$[1],MORE$[PAGE,X1,2]=G2$
1750 LET G2$(2)=DESCRIPTION$[2],MORE$[PAGE,X1,3]=G2$
1790 LET LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
1800 REM " --- Loop back for next record"
1890 GOTO 1500
1900 REM " --- No more transactions"
1910 IF X0>0 THEN GOTO 2000
1920 DIM MESSAGE$[1]
1930 LET MESSAGE$[0]="No transactions found (<Enter>=Continue)"
1940 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
1960 IF V3=5 THEN GOTO 4000
1970 IF V3=6 THEN GOTO 4000
1990 GOTO 3000
2000 REM " --- Display last page"
2010 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2020 GOSUB 5000
2030 IF DONE THEN GOTO 4000
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$=A0$(3),CHOICE2$=""
3030 GOSUB 5000
3040 IF CONTROL=1 THEN GOTO 1000
3090 GOTO 4000
3800 REM " --- Exit"
3810 LET OPTION$[1]="EXIT"
3890 GOTO 4000
3900 REM " --- Startover"
3910 LET OPTION$[1]="STARTOVER"
4000 REM " --- All done"
4010 GOSUB 6000
4020 CALL "SYC.WB",TNAME$,0
4030 PRINT @(0,0),'CS',
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CS',
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9950 EXIT 
9999 END

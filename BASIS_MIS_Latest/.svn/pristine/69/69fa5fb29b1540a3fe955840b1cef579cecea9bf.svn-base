
// Fax a text file using Protofax <faxfile>

get_infile:
screen_desc$="FAX A FILE";
display/form="MAS_HEAD" screen_desc=screen_desc$ printer="";

infile$="";
prompt for /prompt="Name of file to fax? "
	/nobox/row=8/nodrop/len=40/help="Include full path (F2=exit)"
	infile$;
if ctl(6) or infile$="" then goto the_end;
faxtext$="";msg$="";
pause/message "Reading file..";
#3glbegin
let inchan=unt; open (inchan,err=infile_err)infile$
read_infile: 
read (inchan,end=end_infile)f$
let f$=cvs(f$,2)
len_check: if len(f$)<79 then goto len_ok
let sp=pos("  "=f$); if sp=0 then goto len_ok
if sp=1 then let f$=f$(2) else let f$=f$(1,sp)+f$(sp+2)
goto len_check
len_ok: let faxtext$=faxtext$+f$+$0a$; let lines=lines+1
if pos("~~pagebreak~~"=f$) then let lines=0
goto read_infile
infile_err: 
if err=12 then let msg$="File not found: "+infile$ else let msg$="Error "+str(err)+" opening "+infile$
end_infile: 
close (inchan)
if lines/66>50 then msg$="File too long to fax - max is 50 pages"
#3glend

if msg$<>"" then { pause/message=msg$;goto get_infile;}

faxnum$="";
get_faxnum:
prompt for /prompt="         Fax number? "
	/nobox/row=10/nodrop/len=40/help="(F2=exit)"
	faxnum$;
if ctl(6) or faxnum$="" then goto the_end;
f$="";a=1;
repeat {if a>len(faxnum$) then break;
	if pos(faxnum$(a,1)="0123456789") 
		then f$=f$+faxnum$(a,1);
	a=a+1;
	}
if len(f$)<7 then goto get_faxnum;
If f$(1,1)="1" or f$(1,3)="011" or len(f$)=7 then goto faxok;
if len(f$)<>10 then f$="011"+f$;
if len(f$)=10 then 
	{ if f$(1,3)="525" then f$="011"+f$; 
	  else f$="1"+f$;}
faxok:
faxnum$=f$;
choose/yesno/nobox/prompt=faxnum$+" OK? " 
// /row=12/drop/nobreak/black yorn;
	 /default=yes/row=12/drop/nobreak yorn;
if yorn=0 then goto get_faxnum;

to$="";
prompt for /prompt="             Fax to? "
	/nobox/row=12/nodrop/len=40/help="Recipient's name - optional (F2=exit)"
	to$;
if ctl(6) then goto the_end;
comp$="";
prompt for /prompt="            Company? "
	/nobox/row=14/nodrop/len=40/help="optional (F2=exit)"
	comp$;
if ctl(6) then goto the_end;
from$="";
prompt for /prompt="               From? "
	/nobox/row=16/nodrop/len=40/help="Your name - optional (F2=exit)"
	from$;
if ctl(6) then goto the_end;
confirm_to:
email$=cvs(info(3,2),3)+"@basis.cloud";
prompt for /prompt=" Your email address? "
	/nobox/row=18/nodrop/len=40/help="Required for fax confirmation (F2=exit)"
	email$;
if ctl(6) then goto the_end;
if pos("@"=email$)=0 then goto confirm_to;
subj$=to$+" -  "+comp$;
if cvs(subj$,3)="-" then subj$=infile$;
#3glbegin
let pagebreak$="~~pagebreak~~"+$0A$,faxfile$="/u/docfiles/pub/"+cvs(info(3,2),2)+"_fax.txt"
erase faxfile$,err=makefile
makefile: 
string faxfile$
let faxchan=unt; open (faxchan)faxfile$
let fax$="To: fax@basis.cloud"+$0A$+"From: "+email$+$0A$+"Subject: "+faxnum$+"; "+subj$+$0A$+$0A$
let fax$=fax$+"   DATE  "+date(0:"%Ml %Dz, %Yd")+$0A$+$0A$
let fax$=fax$+"     TO  "+to$+$0A$+"COMPANY  "+comp$+$0A$+$0a$
let fax$=fax$+"   FROM  "+from$+$0A$+pagebreak$
let fax$=fax$+faxtext$
write record(faxchan)fax$
close (faxchan,err=end_faxfile)
let a=scall("/usr/lib/sendmail -t < "+faxfile$)
end_faxfile:
#3glend

pause/message="Check your email for confirmation of this fax.";
goto get_infile;

the_end:
exit ;

//

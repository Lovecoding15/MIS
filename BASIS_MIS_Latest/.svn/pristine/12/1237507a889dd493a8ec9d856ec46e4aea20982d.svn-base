0010 REM "WOE - Time Sheet Transaction Entry (Detail)"
0020 REM "Program WOE.BB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 15Dec97 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "23Jul96 JJD: Calculate actual pay properly (220,3265,6020,6140,6155,6160,6210-6260)"
0032 REM "17Sep96 JJD: Establish proper precision (6960,6980)"
0033 REM "17Sep96 JJD: Check pay rate validity when entered (2110)"
0034 REM "07Nov96 JJD: Use Payroll code info when determining pay rate. (2945,6220-6240)"
0035 REM "26Nov96 JJD: Calculate timesheet extended dollar amount in correct precision (6820,6840,6860,6870)"
0036 REM "01Oct97 WGH: Standard labor rate may be wrongly used instead of Actual payroll rate (2945-2960,3235-3247,3265,6120-6135,6150,6160-6165,6180,6220,6235)"
0037 REM "19Feb99 WGH: Use WOC.DA for WO Operations <F3>=lookup (2650,6300-6390)
0040 REM 
0055 REM "L9=MAX LINES, L1=FIRST LINES, L0=HEADER LINE, L=CURRENT LINE
0056 REM "W9$=WO #
0080 SETERR 9000
0090 SETESC 9000
0095 PRECISION IVS01_PRECISION
0200 REM " --- IOLISTs
0210 TIMESHEETB: IOLIST W0$(1),W1$(1),W[ALL]
0220 PRM02A: IOLIST D0$,D[ALL]
0230 WOE02A: IOLIST L0$,L1$,L[ALL]
0240 IVM01A: IOLIST B0$,B1$(1)
0250 EMPCODE: IOLIST *,Y1$
0260 OPCODE: IOLIST *,W1,*,W2
0270 PRM10: IOLIST Y0$,Y[ALL]
0280 WOE01A: IOLIST X$,X1$
0310 TIMESHEETA: IOLIST A0$,A1$,A[ALL]
0500 REM " --- Initializations"
0505 PRECISION IVS01_PRECISION
0510 DIM W0$(P2),W1$(28),W[11],Y[2]
0520 LET W0$(1)=A0$(1,P9),L=L1,L0=6,L1=7,L9=19
0710 PRINT @(0,L1),'CE'
0800 REM " --- Add Them Up
0810 LET T0=0,T9$=""
0820 READ (TIMESHEET_DEV,KEY=A0$(1,P2),DOM=0830)
0830 LET K$=KEY(TIMESHEET_DEV,END=0900)
0840 IF K$(1,P2-2)<>A0$(1,P2-2) THEN GOTO 0900
0850 READ (TIMESHEET_DEV)IOL=TIMESHEETB
0860 LET T0=T0+W[0]+W[4]
0870 GOTO 0830
0900 PRINT @(77-M2,4),T0:M2$
0990 GOSUB 5100
1020 IF L>L9 THEN PRINT @(0,21),'CL',@(0,22),'CL',@(0,L1),'LD','LD'; LET L=L-2; GOTO 1020
1100 REM " --- Get Next Key"
1120 LET K$=KEY(TIMESHEET_DEV,END=1400)
1130 IF K$="" THEN GOTO 1400
1135 IF K$(1,2)<>N0$ THEN GOTO 1400
1140 IF K$(1,P9)<>W0$(1,P9) THEN GOTO 1400
1150 READ (TIMESHEET_DEV)IOL=TIMESHEETB
1160 GOTO 1490
1400 REM " --- Seq"
1405 IF L>L9 THEN PRINT @(0,21),'CL',@(0,22),'CL',@(0,L1),'LD','LD'; LET L=L-2; GOTO 1405
1410 IF V3<>2 THEN LET W0=NUM(W0$(P2-1,2))+1
1420 LET V0$="Z",V1$="KER",V2$=STR(W0),V3$="",V0=2,V1=0,V2=L,I0=0,O9=1
1430 GOSUB 7000
1440 IF V3=4 THEN PRINT @(V1,V2),"    "; GOTO 4000
1450 IF V$="00" THEN GOTO 1400
1460 LET W0$(P2-1,2)=V$
1480 FIND (TIMESHEET_DEV,KEY=W0$(1,P2),DOM=1600)IOL=TIMESHEETB
1490 IF W0$(1,P2)<=K9$ THEN GOSUB 5400
1500 GOSUB 5000
1520 LET T0=T0-(W[0]+W[4]); REM "BACK OFF EARNINGS...
1530 GOTO 3700
1600 REM " --- Setup"
1620 DIM W[11]
1630 REM " --- Set Up Defaults
1640 LET W1$(1,6)="",W1$(16)="",I0=0
1660 LET W1$(24,2)=R$,W1$(26,2)=R1$
2000 REM " --- Employee
2010 IF POS(TS_SEQ$="DE")>0 THEN GOTO 2200
2020 LET V0$="M",V1$="KCRE",V2$=W1$(7,P[0]),V3$=M0$,V4$="Enter A Valid Employee Number (<F3>=Lookup)",V0=9,V1=3,V2=L
2030 GOSUB 7000
2035 IF V3=2 AND W[0]=0 THEN GOTO 1400
2040 IF V3=4 THEN GOTO 4000
2050 IF V3<>3 THEN GOTO 2090
2080 CALL "WOC.LP",V$
2090 IF V$="" THEN GOTO 2000
2095 FIND (EMPCODE_DEV,KEY=N0$+V$,DOM=2000)IOL=EMPCODE
2100 IF P3$(4,1)="Y" THEN LET R1$=Y1$(223,2),R2$=Y1$(225,1)
2105 LET W1$(7,9)=V$,W1$(26,2)=R1$,W1$(24,2)=R$
2130 GOSUB 6700
2140 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$),@(V1,L+1),Y1$(1,30)
2190 GOTO 3600
2200 REM " --- W/O # ("DE" Selections)
2205 LET V0$="Z",V1$="CREK",V2$=W1$(7,7),V3$="",V4$="Enter WO #, <F3>=Lookup",V0=7,V1=3,V2=L
2210 GOSUB 7000
2215 IF V3=2 AND W[0]=0 THEN GOTO 1400
2220 IF V3=4 THEN GOTO 4000
2225 IF V3=3 THEN GOSUB 6500
2230 PRINT @(V1,L+1),'CL'
2235 FIND (WOE01_DEV,KEY=A0$(1,2)+"  "+V$,DOM=2200)IOL=WOE01A
2240 IF POS(X$(15,1)="CPQ")=0 THEN GOTO 2270
2245 IF X$(15,1)="C" THEN LET STAT$="Closed" ELSE IF X$(15,1)="P" THEN LET STAT$="Planned" ELSE LET STAT$="Quoted"
2250 DIM MESS$[1]
2255 LET MESS$[0]="Work Order Is "+STAT$+"!! <Enter> To Reselect:"
2260 CALL "SYC.XA",2,MESS$[ALL],0,22,-1,V$,V3
2265 GOTO 2200
2270 DIM B1$(60)
2275 FIND (IVM01_DEV,KEY=N0$+X$(56,20),DOM=2280)IOL=IVM01A
2280 IF X$(14,1)<>"I" THEN PRINT @(V1,L+1),X1$(1,30), ELSE PRINT @(V1,L+1),B1$(1,MIN(DESC[1],30)),
2285 LET W1$(7,7)=V$,W9$=V$
2290 GOTO 3600
2400 REM " --- Start Time
2410 IF P3$(13,1)<>"Y" THEN GOTO 3600
2420 IF POS(" "<>W1$(16,4))=0 THEN LET W1$(16,4)=T9$
2430 LET V0$="T",V1$="K",V2$=W1$(16,4),V3$="",V4$="",V0=4,V1=Q[4],V2=L
2440 IF NUM(W1$(16,4),ERR=2450)>0 THEN PRINT @(V1,V2),FNE$(W1$(16,4))
2450 GOSUB 7000
2460 LET W1$(16,4)=V$
2470 GOTO 3600
2500 REM " --- Stop Time
2505 IF P3$(13,1)<>"Y" THEN GOTO 3600
2510 LET V0$="T",V1$="K",V2$=W1$(20,4),V3$="",V4$="",V0=4,V1=Q[5],V2=L
2515 GOSUB 7000
2520 IF V3=2 THEN GOTO 3600
2525 LET W1$(20,4)=V$
2530 LET T9$=V$
2535 GOSUB 6900
2540 IF V=0 THEN GOTO 2555
2545 LET W[0]=V-W[4]
2550 GOSUB 6800
2555 GOTO 3600
2600 REM " --- Step
2610 LET V0$="Z",V1$="CRE",V2$=W1$(1,3),V3$="",V4$="Enter Operation Sequence # From Work Order, <F3>=Lookup",V0=3,V1=Q[0],V2=L
2620 GOSUB 7000
2630 IF V3=2 THEN GOTO 3600
2640 IF V3=4 THEN GOTO 4000
2650 IF V3=3 THEN LET V$=W9$; CALL "WOC.DA",V$,SYS01_DEV,OPCODE_DEV,WOE02_DEV
2660 DIM L[13]
2670 FIND (WOE02_DEV,KEY=N0$+"  "+W9$+"A"+V$,DOM=2600)IOL=WOE02A
2680 IF L1$(91,1)="M" THEN LET V0$="S",V1$="C",V2$="",V3$="",V4$="You May Not Apply Labor To A Comment Line. <Enter> To Continue. ",V0=1,V1=FNV(V4$),V2=22; GOSUB 7000; GOTO 2600
2690 LET W1$(1,3)=V$,W1$(4,3)=L1$(1,3),L1$=L1$(7,20)
2700 FIND (OPCODE_DEV,KEY=N0$+W1$(4,3),DOM=2710)IOL=OPCODE
2710 PRINT @(V1,V2),V$,@(Q[1],L),W1$(4,3),@(40,L+1)," ",L1$
2720 GOSUB 6600
2730 GOTO 3600
2800 REM " --- Pay Code Here
2810 IF P3$(4,1)<>"Y" THEN GOTO 2990
2820 LET V0$="S",V1$="C",V2$=W1$(24,2),V3$="",V4$="Enter An Appropriate Pay Code, <F3>=Lookup",V0=2,V1=Q[2],V2=L
2830 GOSUB 7000
2840 IF V3=2 THEN GOTO 2990
2850 IF V3<>3 THEN GOTO 2930
2860 LET NUMBER=1; GOSUB DIMENSION_LOOKUP_PARAMETERS
2870 LET DESCRIPTION$[0]="Code:",DESCRIPTION$[1]="Description:"
2880 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
2890 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=16
2900 LET ROW=V2+1,COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
2910 LET CHANNEL=PRM10_DEV,RECORD$="A",TITLE$="Code Lookup"
2920 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
2930 LET V$=V$+"  ",V$=V$(1,2)
2940 FIND (PRM10_DEV,KEY=N0$+"A"+V$,DOM=2820)IOL=PRM10
2945 LET OLDPC$=W1$(24,2),W1$(24,2)=V$
2950 GOSUB 6000
2955 IF BAD_CODE$="Y" THEN LET W1$(24,2)=OLDPC$; GOTO 2800
2960 LET OLDPC$=""
2970 PRINT @(V1,V2),V$
2980 PRINT @(0,23),"Pay Code: ",V$," ",Y0$(6,10),
2990 GOTO 3600
3100 REM " --- Title Code Here
3110 IF P3$(4,1)<>"Y" THEN GOTO 3270
3120 LET V0$="S",V1$="C",V2$=W1$(26,2),V3$="",V4$="Enter An Appropriate Title Code, <F3>=Lookup ",V0=2,V1=Q[3],V2=L
3130 GOSUB 7000
3140 IF V3=2 THEN GOTO 3270
3150 IF V3<>3 THEN GOTO 3220
3160 LET NUMBER=1; GOSUB DIMENSION_LOOKUP_PARAMETERS
3170 LET DESCRIPTION$[0]="Code:",DESCRIPTION$[1]="Description:"
3180 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
3190 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
3200 LET ROW=V2+1,COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,CHANNEL=PRM10_DEV,RECORD$="F",TITLE$="Code Lookup"
3210 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
3220 LET V$=V$+"  ",V$=V$(1,2)
3230 FIND (PRM10_DEV,KEY=N0$+"F"+V$,DOM=3120)IOL=PRM10
3235 LET OLDTC$=W1$(26,2),W1$(26,2)=V$
3240 GOSUB 6000
3245 IF BAD_CODE$="Y" THEN LET W1$(26,2)=OLDTC$; GOTO 3100
3247 LET OLDTC$=""
3250 PRINT @(40,23),"Title Code: ",V$," ",Y0$(6,20),
3260 PRINT @(V1,V2),V$
3270 GOTO 3600
3300 REM " --- Hours
3310 IF NUM(W1$(16,4),ERR=3320)>0 AND NUM(W1$(20,4),ERR=3320)>0 THEN GOTO 3350
3320 LET V0$="N",V1$="C",V2$=STR(W[0]),V3$=M2$,V4$="Enter Number of Hours Worked",V0=0,V1=Q[7],V2=L
3330 GOSUB 7000
3340 LET W[0]=V
3350 GOSUB 6800
3360 IF POS(TS_SEQ$="E")<>0 AND P3$(13,1)="Y" THEN PRINT @(Q[5],L),FNE$(W1$(20,4))
3370 GOTO 3600
3400 REM " --- Setup Hours"
3410 LET V0$="N",V1$="C",V2$=STR(W[4]),V3$=M2$,V4$="Enter Number of Setup Hours",V0=0,V1=Q[6],V2=L
3420 GOSUB 7000
3430 LET V9=V
3440 IF P3$(13,1)="Y" THEN GOSUB 6900; IF V<>0 AND V9>V THEN GOTO 3400 ELSE LET W[0]=V-V9
3450 LET W[4]=V9
3460 IF POS(TS_SEQ$="E")<>0 AND P3$(13,1)="Y" THEN PRINT @(Q[5],L),FNE$(W1$(20,4))
3470 GOSUB 5090
3480 GOTO 3600
3500 REM " --- Comlete Quantity"
3510 LET V0$="N",V1$="C",V2$=STR(W[5]),V3$=M2$,V4$="Enter Number of Finished Goods Completed For These Hours",V0=0,V1=Q[8],V2=L
3520 GOSUB 7000
3530 LET W[5]=V
3540 GOSUB 5090
3550 GOTO 3600
3600 REM " --- Seq Control"
3610 IF V3=4 THEN IF I0>1 THEN GOTO 3700
3620 IF V3<>2 THEN GOTO 3650
3630 IF I0>0 THEN LET I0=I0-1
3640 GOTO 3660
3650 LET I0=I0+1
3660 ON I0 GOTO 2000,2600,2800,3100,2400,2500,3400,3300,3500,3700
3700 REM " --- Correct"
3705 PRINT @(78,L),'CL',@(0,22),'CL',@(0,23),'CL',
3710 LET V0$="S",V1$="C",V2$="",V0=1,V1=78,V2=L,K9$=W0$(1,P2),V4$="<Enter>=OK, <F2>=Change, D=Delete",I0=9
3715 GOSUB 7000
3720 IF V3=4 THEN GOTO 3800
3725 GOSUB 5090
3730 IF V3=2 THEN LET W1=0,W2=0; FIND (OPCODE_DEV,KEY=N0$+W1$(4,3),DOM=3600)IOL=OPCODE; GOTO 3600
3735 IF V$="" THEN GOTO 3750
3740 IF V$="D" THEN GOTO 3900
3745 GOTO 3700
3750 PRINT @(0,21),'LD','LD',
3800 REM " --- Write"
3810 WRITE (TIMESHEET_DEV,KEY=W0$(1,P2))IOL=TIMESHEETB
3820 LET L=L+2
3830 LET T0=T0+W[0]+W[4]
3840 PRINT @(77-M2,4),T0:M2$
3850 IF V3=4 THEN GOTO 4000
3860 GOTO 1020
3900 REM " --- Delete"
3910 REMOVE (TIMESHEET_DEV,KEY=W0$(1,P2),DOM=3920)
3920 PRINT @(77-M2,4),T0:M2$
3930 PRINT @(0,L),'LD','LD',@(0,21),'LD','LD',
3940 GOTO 1020
3950 REM " --- Dimension Lookup Parameters "
3960 DIMENSION_LOOKUP_PARAMETERS: 
3970 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
3980 RETURN 
4000 REM " --- Options"
4010 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Is The Detail Information Correct (Y/N/Review/List/Total/Delete)?",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4040 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4050 IF V$="L" OR V$="LIST" THEN GOTO 4400
4060 IF V$="T" OR V$="TOTAL" THEN GOTO 4200
4065 IF V$="DELETE" THEN LET O0=2; GOTO 4190
4070 ON POS(V$="YN") GOTO 4000,4100,1400
4100 REM " --- Check Balances
4120 IF A[0]=T0 THEN GOTO 4180
4130 LET V4$="Out Of Balance By "+STR(A[0]-T0:M2$)+"(<Enter>=Continue/<F1>=Accept)"
4132 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
4134 GOSUB 7000
4140 IF V3<>1 THEN GOTO 4000
4150 LET A[0]=T0
4160 WRITE (TIMESHEET_DEV,KEY=A0$(1,P2))IOL=TIMESHEETA
4180 LET O0=1
4190 RUN "WOE.BA"
4200 REM " --- Add Them Up
4210 LET T0=0
4220 READ (TIMESHEET_DEV,KEY=A0$(1,P2),DOM=4230)
4230 LET K$=KEY(TIMESHEET_DEV,END=4290)
4240 IF K$(1,P2-2)<>A0$(1,P2-2) THEN GOTO 4290
4250 READ (TIMESHEET_DEV)IOL=TIMESHEETB
4260 LET T0=T0+W[0]+W[4]
4270 GOTO 4230
4290 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Entered: "+STR(A[0]:M2$)+" Calculated: "+STR(T0:M2$)+" Diff: "+STR(A[0]-T0:M2$)+" (<Enter>)",V0=1,V1=FNV(V4$),V2=22
4300 GOSUB 7000
4320 GOTO 4000
4400 REM " --- List"
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4620 READ (TIMESHEET_DEV,KEY=A0$(1,P2),DOM=4640)
4640 GOSUB 5400
4690 GOTO 1020
5000 REM " --- Display"
5005 DIM L[13]
5010 IF POS(TS_SEQ$="DE")>0 THEN LET W9$=W1$(7,7)
5015 DIM L1$(20); LET L1$(1)="*** Not On File ***"
5020 FIND (WOE02_DEV,KEY=N0$+"  "+W9$+"A"+W1$(1,3),DOM=5025)IOL=WOE02A; LET L1$=L1$(7,20)
5025 ON POS(TS_SEQ$="DEW")-1 GOTO 5060,5060,5030
5030 DIM Y1$(30); LET Y1$(1)="*** Not On File ***"
5035 FIND (EMPCODE_DEV,KEY=W0$(1,2)+W1$(7,9),DOM=5040)IOL=EMPCODE; GOSUB 6700
5040 PRINT @(3,L),FNF$(W1$(7,P[0]),M0$)
5045 IF POS(" "<>W1$(20,4))>0 THEN LET T9$=W1$(20,4)
5050 GOTO 5072
5060 DIM Y1$(30); LET Y1$(1)="*** Not On File ***"
5065 FIND (WOE01_DEV,KEY=W0$(1,2)+"  "+W9$,DOM=5067)IOL=WOE01A
5067 DIM B1$(60)
5068 FIND (IVM01_DEV,KEY=N0$+X$(56,20),DOM=5069)IOL=IVM01A
5069 IF X$(14,1)<>"I" THEN LET DESC1$=X1$(1,30) ELSE LET DESC1$=B1$(1,MIN(DESC[1],30))
5070 PRINT @(3,L),W9$
5072 IF P3$(4,1)="Y" THEN PRINT @(Q[2],L),W1$(24,2)," ",W1$(26,2)
5075 PRINT @(0,L),W0$(P2-1,2),@(Q[0],L),W1$(1,3),@(Q[1],L),W1$(4,3),@(3,L+1),DESC1$,@(40,L+1)," ",L1$
5082 IF P3$(13,1)<>"Y" THEN GOTO 5090
5085 IF NUM(W1$(16,4),ERR=5087)>0 THEN PRINT @(Q[4],L),FNE$(W1$(16)),
5087 IF NUM(W1$(20,4),ERR=5090)>0 THEN PRINT @(Q[5],L),FNE$(W1$(20)),
5090 PRINT @(Q[6],L),W[4]:M2$,@(Q[7],L),W[0]:M2$,@(Q[8],L),W[5]:M2$,
5095 RETURN 
5100 REM " --- List"
5110 GOSUB 5400
5120 READ (TIMESHEET_DEV,KEY=A0$(1,P2),DOM=5130)
5130 LET K$=KEY(TIMESHEET_DEV,END=5300)
5135 IF K$(1,2)<>N0$ THEN GOTO 5300
5140 IF K$(1,P9)<>W0$(1,P9) THEN GOTO 5300
5150 READ (TIMESHEET_DEV)IOL=TIMESHEETB
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Press <Enter> To Continue:",V0=1,V1=FNV(V4$),V2=22
5215 GOSUB 7000
5220 IF V3=4 THEN GOTO 5300
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+2,K9$=W0$(1,P2)
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5420 PRINT @(0,L1),'CE',
5440 LET L=L1
5490 RETURN 
6000 REM " --- Determine Pay Rate If Actual
6005 LET BAD_CODE$=""
6010 IF P3$(4,1)<>"Y" THEN GOTO 6600
6030 IF TS_SEQ$="D" THEN LET A$=A0$(6,9) ELSE IF TS_SEQ$="E" THEN LET A$=A0$(3,9) ELSE LET A$=W1$(7,9)
6100 REM " --- Can We Get To The Rate?
6120 FIND (PRT01_DEV,KEY=N0$+A$+"A"+W1$(24,2),DOM=6150)
6125 FIND (PRM10_DEV,KEY=N0$+"A"+W1$(24,2),DOM=6150)IOL=PRM10
6130 LET PREMIUM_RATE=Y[1],PAYCODE_RATE=Y[0]
6135 IF P3$(11,1)="A" THEN GOTO 6155
6140 GOTO 6290
6150 LET V0$="S",V1$="C",V2$="",V3$="",V4$=W1$(24,2)+" Is An Invalid Pay Code For This Employee (<Enter>=Change)",V0=1,V1=FNV(V4$),V2=22; GOSUB 7000; LET BAD_CODE$="Y"; EXITTO 2955
6155 DIM D[1]
6160 FIND (PRM02_DEV,KEY=N0$+A$+W1$(26,2),DOM=6180)IOL=PRM02A
6165 FIND (PRM10_DEV,KEY=N0$+"F"+W1$(26,2),DOM=6180)IOL=PRM10
6170 GOTO 6200
6180 LET V0$="S",V1$="C",V2$="",V3$="",V4$=W1$(26,2)+" Is An Invalid Title Code For This Employee (<Enter>=Change)",V0=1,V1=FNV(V4$),V2=22; GOSUB 7000; LET BAD_CODE$="Y"; EXITTO 3245
6200 REM " --- Calculate actual pay rate"    
6210 LET RATE=0
6220 IF R2$="S" AND D[1]<>0 THEN LET RATE=D[0]/D[1] ELSE IF R2$<>"S" THEN LET RATE=D[0]
6230 IF PAYCODE_RATE<>0 THEN LET RATE=PAYCODE_RATE; REM "Override by pay code
6235 IF RATE=0 THEN LET RATE=Y[0]; REM "Use Title rate when needed
6240 IF PREMIUM_RATE<>0 THEN LET RATE=RATE*PREMIUM_RATE; REM "Premium Factor
6250 IF RATE=0 THEN GOTO 6180
6260 LET W[1]=RATE
6280 GOTO 6680
6290 RETURN 
6500 REM " --- W/O Lookup
6510 LET V$=P3$(2,1),V6$="O"
6520 CALL "SYC.LW",V$,V6$
6530 PRINT @(V1,V2),V$
6590 RETURN 
6600 REM " --- Determine Rate From BMM-08 or WOM-08
6660 IF W[1]=0 THEN LET W[1]=W1
6680 GOSUB 6800
6690 RETURN 
6700 REM " --- Condense Name
6710 DIM D9$(30)
6720 LET D9$(1)=Y1$(1,POS("   "=Y1$(1,16)+"   ")-1)+", "+Y1$(17,14)
6740 LET Y1$(1,30)=D9$
6790 RETURN 
6800 REM " --- Extension Here
6810 LET W[2]=W[1]*W2
6820 LET W[3]=ROUND((W[0]+W[4])*W[1],2)+ROUND((W[0]+W[4])*W[2],2)
6880 GOSUB 5090
6890 RETURN 
6900 REM " --- Calc Hours Here
6910 LET V0=NUM(W1$(16,4)),V1=NUM(W1$(20,4))
6920 IF W1$(22,2)<W1$(18,2) THEN LET V1=V1-40
6940 LET V=FNG(V1-V0)
6950 IF V<0 THEN LET V1=V1+2400; GOTO 6940
6970 LET V=V*1
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=Q$(1,2)+":"+Q$(3,2)
8060 DEF FNG(Q)=INT(Q*.01)+MOD(Q,100)/60
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

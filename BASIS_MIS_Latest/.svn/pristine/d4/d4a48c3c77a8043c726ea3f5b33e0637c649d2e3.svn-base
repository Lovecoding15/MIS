0010 REM "WOE - Closed WO Entry (Lot/Serial Processing"
0020 REM "Program WOE.EB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "31Oct98 WGH: Do not reinitialize on return to lead overlay (1420,4140)
0040 REM 
0045 REM "L9=MAX LINES, L1=FIRST LINES, L0=HEADER LINE, L=CURRENT LINE
0050 REM 
0085 SETESC 9000
0090 SETERR 9000
0095 PRECISION IVS01_PRECISION
0200 REM " --- IOLISTs
0210 WOE06A: IOLIST W0$,W1$(1),W[ALL]
0220 IVM07A: IOLIST D0$,D1$,D[ALL]
0400 REM " --- Parameters
0420 LET S9=NUM(P2$(7,2))
0500 REM " --- Init Data
0510 DIM W0$(15),W1$(56),W[5],D[12]
0520 LET W0$(1)=A0$(1,11),L=L1
0540 LET L0=12,L1=L0+1,L9=20,L9$=""
0550 IF P3$(17,1)="L" THEN LET S9$="Lot Number" ELSE LET S9$="Serial Number"
0560 LET SM$="#####0"
0700 REM " --- Background
0710 PRINT @(0,L0-1),'CE','SB',@(10),S9$,@(72-M3-M2*2-9),"Schedule",@(72-M2-M3-6),"Prior",@(72-M3-7),"Current",@(67),"Unit",@(0,L0),"Seq",@(10),"/Comments",@(72-M3-M2*2-9),"Prod Qty",@(72-M2-M3-7),"Closed",@(72-M3-7),"Closed",@(72-5),"Cost",@(74),"Cmpl?",
0900 REM " --- List existing lines"
0910 GOSUB 5100
0920 IF K9$<>"" THEN GOTO 4000
1000 REM " --- Next line"
1020 IF L+1>L9 THEN PRINT @(0,L1),'LD','LD',; LET L=L-2; GOTO 1020
1040 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1120 LET K$=KEY(WOE06_DEV,END=1400)
1140 IF POS(K$(1,11)=W0$)<>1 THEN GOTO 1400
1150 READ (WOE06_DEV)IOL=WOE06A
1155 IF W1$(1,1)="Y" THEN GOTO 1100
1160 GOTO 1490
1400 REM " --- Seq"
1405 PRINT @(0,21),'CL',@(0,22),'CL',
1410 LET W0=NUM(W0$(12,3))+1
1420 LET V0$="Z",V1$="ER",V2$=STR(W0),V3$="",V4$="",V0=3,V1=0,V2=L,I0=0
1430 GOSUB 7000
1440 IF V3=4 THEN GOTO 4000
1450 IF V$="000" THEN GOTO 1400
1460 LET W0$(12,3)=V$
1480 FIND (WOE06_DEV,KEY=W0$(1,14),DOM=1600)IOL=WOE06A
1490 IF W0$(1,14)<=K9$ THEN GOSUB 5400
1500 GOSUB 5000
1530 GOTO 3300
1600 REM " --- Set Up Defaults
1640 LET W1$(1)=""
2000 REM " --- Enter Data
2010 IF W1$(1,1)="Y" THEN GOTO 3200
2020 LET V0$="S",V1$="C",V2$=W1$(37),V3$="",V4$="Enter "+S9$,V0=S9,V1=10,V2=L
2030 IF POS(P3$(17,1)="SL")>0 AND NEEDED>1 THEN LET V4$=V4$+" (<F1> To Auto Generate Numbers)"
2040 GOSUB 7000
2050 IF V3=2 THEN GOTO 1100
2055 IF POS(P3$(17,1)="SL")>0 AND V3=1 AND NEEDED>1 THEN GOSUB 6400; LET V$="L"; GOTO 4400
2060 IF V$=FILL(S9) THEN GOTO 2000
2065 LET W1$(37)=V$
2070 IF POS(" "<>W1$(37))=0 THEN GOTO 2020
2080 FIND (IVM07_DEV,KEY=N0$+A0$(54,20)+W1$(37,20),DOM=2190)IOL=IVM07A
2100 IF P3$(17,1)="S" AND D[0]-D[1]>0 THEN GOTO 2105 ELSE GOTO 2120
2105 LET V4$="WARNING: Serial Number On Hand. Continue: "
2108 PRINT @(V1,V2),'RB',
2110 CALL "SYC.YN",1,V4$,1,V$,V3
2115 IF V$="N" THEN GOTO 2000 ELSE GOTO 2190
2120 IF P3$(17,1)="L" AND D[0]-D[1]>0 THEN GOTO 2125 ELSE GOTO 2190
2125 LET V4$="WARNING: "+STR(D[0]-D[1])+" Items Are Currently On Hand (<Enter>=Continue)"
2130 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
2140 GOSUB 7000
2190 IF P3$(17,1)="S" THEN LET NEEDED=NEEDED+W[0]-1,W[0]=1; PRINT @(72-M2*3-M3,L),W[0]:M2$,
2195 GOTO 3200
2200 REM " --- Production Qty Here
2210 IF W1$(1,1)="Y" THEN GOTO 3200
2215 IF P3$(17,1)="S" AND V3<>2 THEN GOTO 2290
2220 LET V0$="N",V1$="C",V2$=STR(W[0]),V3$=M2$,V4$="Enter Production Qty For This "+S9$,V0=7,V1=72-M3-M2*3,V2=L
2230 IF W[0]=0 AND P3$(17,1)="L" THEN LET V2$=STR(A[0]-A[2]-A[4]),V4$=V4$+", <Enter>="+V2$+"."
2240 GOSUB 7000
2250 IF V3=2 OR V3=4 THEN GOTO 3200
2255 IF P3$(17,1)="S" THEN IF LEN(D2$)>0 THEN IF D2$(19,1)="Y" THEN IF V<0 OR V>1 THEN GOTO 2220
2260 LET W[0]=V
2280 IF W[0]>A[0] THEN GOTO 2200
2290 GOTO 3200
2400 REM " --- Comments
2420 LET V0$="S",V1$="C",V2$=W1$(5,20),V3$="",V4$="Enter Comments",V0=20,V1=10,V2=L+1
2440 GOSUB 7000
2460 LET W1$(5,20)=V$
2490 GOTO 3200
2600 REM " --- Closed Qty Here
2610 IF W[4]=0 THEN IF P3$(17,1)<>"L" THEN LET W[4]=W[0],NEEDED=NEEDED-W[4] ELSE LET W[4]=A[4],NEEDED=NEEDED-W[4]
2620 LET OLDQTY=W[4]
2630 LET V0$="N",V1$="C",V3$=M2$,V4$="",V0=7,V1=72-M3-M2,V2=L
2640 IF P3$(17,1)="S" AND V3<>2 THEN PRINT @(V1,V2),W[4]:M2$; GOTO 2720
2660 LET V2$=STR(W[4]),V4$="Enter Qty To Complete This "+S9$+", <Enter>="+V2$
2680 GOSUB 7000
2690 IF P3$(17,1)="S" THEN IF LEN(D2$)>0 THEN IF D2$(19,1)="Y" THEN IF V<0 OR V>1 THEN LET V3=2; GOTO 2600
2700 LET W[4]=V
2720 IF W[4]=0 THEN LET W[5]=0,W1$(26,1)="" ELSE LET W[5]=A[5]
2740 PRINT @(72-M3,L),W[5]:M3$,
2750 IF OLDQTY<>W[4] THEN LET NEEDED=NEEDED-W[4]+OLDQTY
2790 GOTO 3200
2800 REM " --- Complete?
2810 IF P3$(17,1)="S" AND W[2]+W[4]>0 THEN LET W1$(26,1)="Y"; PRINT @(75,L),W1$(26,1); GOTO 3200
2820 IF W[2]+W[4]=W[0] THEN LET W1$(25,1)="Y"
2830 LET V4$="Is This "+S9$+" Complete (Y/N)?"
2835 LET V0$="Y",V1$="C",V2$=W1$(26,1),V3$="",V0=1,V1=75,V2=L
2840 GOSUB 7000
2860 LET W1$(26,1)=V$
2890 GOTO 3200
3200 REM " --- Seq Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 2000,2400,2200,2600,2800,3300
3300 REM " --- Correct"
3310 PRINT @(78,L),'CL',@(0,22),'CL'
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=W0$(1,14),V4$="<Enter>=OK, <F2>=Change, D=Delete"
3325 IF W1$(25,1)="Y" AND W1$(2,3)<>"   " THEN LET V4$="<Enter>=OK"
3330 GOSUB 7000
3335 IF W1$(25,1)="Y" AND W1$(2,3)<>"   " THEN GOTO 3380
3340 IF V3=2 THEN LET I0=4; GOTO 3200
3350 IF V$="" THEN GOTO 3380
3360 IF V$="D" THEN GOTO 3700
3370 GOTO 3300
3390 PRINT @(0,21),'LD','LD',
3400 REM " --- Write"
3430 WRITE (WOE06_DEV,KEY=W0$(1,14))IOL=WOE06A
3450 LET L=L+2
3470 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3700 REM " --- Delete"
3720 REMOVE (WOE06_DEV,KEY=W0$(1,14),DOM=3740)
3730 LET NEEDED=NEEDED+W[4]
3780 PRINT @(0,L),'CL',@(0,L+1),'CL',@(0,21),'LD','LD',
3790 GOTO 1000
3990 REM " --- Restore Screen
3995 PRINT @(0,L0-1),'CE',
3999 RETURN 
4000 REM " --- Options"
4005 GOSUB 6200
4010 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="Is The Detail Information Correct (Y/N/Review/List)?",V0=1,V1=FNV(V4$),V2=22
4015 GOSUB 7000
4020 IF V$="" THEN GOTO 4100
4040 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4050 IF V$="L" OR V$="LIST" THEN GOTO 4400
4070 ON POS(V$="YN") GOTO 4000,4100,1400
4120 GOSUB 3990
4130 DIM W[13]
4140 LET O9=1; RUN "WOE.EA"
4400 REM " --- List"
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4620 READ (WOE06_DEV,KEY=A0$(1,11),DOM=4630)
4640 GOSUB 5400
4650 LET V3=0
4690 GOTO 1000
5000 REM " --- Display"
5020 PRINT @(0,L),W0$(12,3),@(10),W1$(37),@(10,L+1),W1$(5,20),
5060 PRINT @(72-M3-M2*2,L),W[2]:M2$,W[4]:M2$,W[5]:M3$,@(75),W1$(25,1),
5080 PRINT @(72-M2*3-M3,L),W[0]:M2$,
5090 RETURN 
5100 REM " --- List"
5110 GOSUB 5400
5120 READ (WOE06_DEV,KEY=A0$(1,11),DOM=5130)
5130 LET K$=KEY(WOE06_DEV,END=5300)
5135 IF K$(1,2)<>N0$ THEN GOTO 5300
5140 IF K$(1,11)<>W0$(1,11) THEN GOTO 5300
5150 READ (WOE06_DEV)IOL=WOE06A
5160 IF W1$(1,1)="Y" THEN GOTO 5130
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Press <Enter> To Continue",V0=1,V1=FNV(V4$),V2=22
5215 GOSUB 7000
5220 IF V3=4 THEN GOTO 5300
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+2,K9$=W0$(1,14)
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5420 PRINT @(0,L1),'CE',
5440 LET L=L1
5490 RETURN 
6000 REM " --- Display error message"
6010 DIM MESSAGE$[2]
6020 LET MESSAGE$[0]="An error was encountered during the "+S9$,MESSAGE$[1]="generation phase (<Enter>=Continue)"
6030 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
6090 RETURN 
6200 REM " --- Verify Qty Against Header Here
6210 DIM MESS$[2]
6220 LET X=0,Y=0
6240 READ (WOE06_DEV,KEY=A0$(1,11),DOM=6250)
6250 LET K8$=KEY(WOE06_DEV,END=6300)
6260 IF POS(A0$(1,11)=K8$)<>1 THEN GOTO 6300
6270 READ (WOE06_DEV)IOL=WOE06A
6275 IF W1$(1,1)="Y" THEN GOTO 6250
6280 LET X=X+W[4]
6290 IF W[4]+W[2]<W[0] THEN LET Y=Y+W[0]-W[4]-W[2]
6295 GOTO 6250
6300 LET NEEDED=A[4]-X
6305 IF X=A[4] THEN GOTO 6390
6310 IF X>A[4] THEN GOTO 6340
6315 LET MESS$[0]=STR(A[4]:SM$)+" Serial Numbers Required,",MESS$[1]=STR(X:SM$)+" Entered. <Enter> To Continue: ",MESS=1
6322 IF P3$(17,1)="S" AND Y>1 THEN LET MESS$[1](LEN(MESS$[1])-1,1)=",",MESS$[2]="<F1>=Auto Close Serial Numbers: ",MESS=MESS+1
6324 CALL "SYC.XA",2,MESS$[ALL],MESS,22,-1,V$,V3
6326 IF P3$(17,1)="S" AND Y>1 AND V3=1 THEN CALL "WOC.CA",SERIAL$,WOE06_DEV,SYS01_DEV,A0$(5,7),STATUS ELSE GOTO 6335
6328 IF STATUS<>0 THEN GOTO 6335
6330 GOSUB 6700
6332 EXITTO 4400
6335 LET V$="R"; EXITTO 4600
6340 LET MESS$[0]="Details "+STR(X:M2$)+" Are Greater Than",MESS$[1]="The Header "+STR(A[4]:M2$)+". <Enter> To Continue: "
6350 CALL "SYC.XA",2,MESS$[ALL],1,22,-1,V$,V3
6360 GOTO 6335
6390 RETURN 
6400 REM " --- Auto Generate Numbers"
6410 DIM SERIAL$[1]
6420 LET SERIAL$[0]=STR(NEEDED)
6430 CALL "IVC.SN",SYS01_DEV,V2,NEEDED,SERIAL$[ALL],STATUS
6440 IF STATUS>0 THEN GOSUB 6000; GOTO 6690
6450 IF NUM(W0$(12,3))+NUM(SERIAL$[0])-1>999 THEN GOSUB 6000; GOTO 6690
6460 FOR X0=1 TO NUM(SERIAL$[0])
6470 DIM X0$(20)
6480 LET X0$(1)=SERIAL$[X0]
6490 FIND (IVM07_DEV,KEY=N0$+A0$(54,22)+X0$,DOM=6550)IOL=IVM07A
6500 IF D[0]-D[1]>0 THEN GOTO 6510 ELSE GOTO 6550
6510 LET V4$="Warning!!  Serial Number On Hand, OK: "
6520 PRINT @(V1,V2),'RB',
6530 CALL "SYC.YN",1,V4$,1,V$,V3
6540 IF V$="N" THEN EXITTO 6690 ELSE GOTO 6550
6550 NEXT X0
6610 FOR X0=1 TO NUM(SERIAL$[0])
6620 DIM W[5]
6630 LET W1$(1)="",W1$(25,1)="Y",W1$(37,20)=SERIAL$[X0]
6640 LET W[0]=1,W[4]=1,W[5]=A[5],NEEDED=NEEDED=W[4]
6650 WRITE (WOE06_DEV,KEY=W0$(1,14))IOL=WOE06A
6660 LET W0=NUM(W0$(12,3))+1
6670 LET W0$(12,3)=STR(W0:"000")
6680 NEXT X0
6690 RETURN 
6700 REM " --- Auto Close Serial Numbers"
6705 LET Y=A[4]-X
6710 READ (WOE06_DEV,KEY=A0$(1,11),DOM=6711)
6720 LET K8$=KEY(WOE06_DEV,END=6790)
6730 IF POS(A0$(1,11)=K8$)<>1 THEN GOTO 6790
6740 READ (WOE06_DEV)IOL=WOE06A
6745 IF Y<=0 THEN GOTO 6790
6750 IF W[4]>0 THEN GOTO 6720
6755 IF SERIAL$>W1$(37,20) THEN GOTO 6720
6760 IF W1$(1,1)="Y" THEN GOTO 6720
6765 LET W[4]=1,W1$(25,1)="Y",Y=Y-1
6770 WRITE (WOE06_DEV,KEY=K8$)IOL=WOE06A
6780 GOTO 6720
6790 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

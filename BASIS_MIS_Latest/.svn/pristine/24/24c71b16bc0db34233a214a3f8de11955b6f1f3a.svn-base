0010 REM "TMM - TeleMarketing Masterfile Maintenance (Callable)"
0020 REM "Program: TMMCMA"
0030 REM "ADD+ON SOFTWARE VERSION 5
0031 REM "11/27/91 by Jerry Koch
0032 REM "MAS * DCF * 5/12/93 * RICHMARK COMPANY
0033 REM "MAS * JCM * Racing Strollers * WA:205 (4046)
0040 REM "COPYRIGHT(C)1991 MicroAccounting Systems, Inc. ALL RIGHTS RESERVED
0041 REM 
0042 REM "06/04/96 KJS WA: #211 <F10> hotkey to T/M Maint
0043 REM "01/06/97 KJS WA: #221 ADS"
0080 ENTER XX
0089 REM 
0090 SETERR 9000
0095 REM GOSUB 10000; IF FAIL THEN GOTO 9980
0100 REM "OPEN FILES"
0105 DIM CHAN[16]
0110 LET CHAN[1]=UNT; OPEN (CHAN[1])"TMM-01"
0120 LET CHAN[2]=UNT; OPEN (CHAN[2])"TMM-02"
0130 LET CHAN[3]=UNT; OPEN (CHAN[3],ERR=0151)"ARM-01"
0140 LET CHAN[4]=UNT; OPEN (CHAN[4],ERR=0151)"ARM-02"
0150 LET CHAN[5]=UNT; OPEN (CHAN[5],ERR=0151)"ARM-04"
0160 LET CHAN[6]=UNT; OPEN (CHAN[6])"SYS-01"
0180 LET CHAN[8]=UNT; OPEN (CHAN[8],ERR=0181)"ARM-10"
0185 LET CHAN[9]=UNT; OPEN (CHAN[9])"TMM-01"
0190 LET CHAN[12]=UNT; OPEN (CHAN[12])"TMM-03"
0195 rem LET CHAN[14]=UNT; OPEN (CHAN[14])"TMM-12"; REM Contact Comments
0196 LET CHAN[15]=UNT; OPEN (CHAN[15])"states.dat"; REM State codes 
0198 LET CHAN[16]=UNT; OPEN (CHAN[16])"country.dat"; REM Country names 
0200 REM "IOLISTS"
0210 IOLIST A0$,A1$(1),A2$(1),A3$(1),A4$(1)
0260 IOLIST P0$,P1$,P2$,P3$,P4$
0270 call "templates.pgm::country"
0400 REM " --- Parameters"
0401 GOSUB 62000; REM "WA: #221
0420 LET N0$=PRM$[FID,TFIRM],N1$=PRM$[FID,TFNAME],N2$="TM",N4$=PRM$[FID,TPNAME]; REM "WA: #221
0430 FIND (CHAN[6],KEY=N0$+N2$+"00",DOM=9800)IOL=0260
0440 LET M0$="000000",R9$=PRM$[AR,INSTALL]; REM "WA: #221, I2$="06009"
0490 LET M1$="###,###.00-",M1=LEN(M1$)
0500 REM "INIT DATA"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 LET L1=17,L9=21
0530 LET K0=1,K9=14
0540 LET M7$="00000",M8$="### ### ####"
0550 DIM P[2]
0560 LET P[0]=NUM(PRM$[AR,KEYLEN]),P[1]=NUM(PRM$[AR,KEYMAX]),P[2]=NUM(PRM$[AR,THREE](4,1)); REM "WA: #221
0570 IF P[2]=9 THEN LET M7$=M7$+"-0000"
0580 DIM A0$(450)
0590 LET SRT$="1",A0$(1)=N0$
0595 LET QUIT=0
0610 LET INITIAL$="Y"; GOSUB 15100
0620 CLOSE (CHAN[6]); OPEN (CHAN[6])"TMM-10"
0640 LET CHAN[13]=UNT; OPEN (CHAN[13])"SYS-01"
0700 REM "BACKGROUND"
0703 REM "Can't use SYC.WA because a) it clears the window, and b) it draws aborder, even if MODE=1"
0704 REM "If you have to use SYC.WA, you will have to redraw the first three rows of the screen, see SYS.AA (not fun)"
0705 PRINT 'PUSH','SB',@(22,2),"Telemarketing Masterfile Maintenance",'SF',@(0,3),'CE',; REM "Copy the current window and paint screen
0710 IF P0$(16,1)="Y" THEN PRINT 'SB',@(0,3),"Firm:",@(27),"Cont:", ELSE PRINT 'SB',@(0,3),"Cont:",@(27),"Firm:",
0720 PRINT @(49),"Tele:",@(1,4),"Cont:",@(50),"Fax:",
0730 PRINT 'SB',
0740 CALL "SYC.WC",1,0,80,0,0,5,0
0900 REM "--- Init File
0910 READ (CHAN[1],KEY=N0$,KNUM=NUM(SRT$),DOM=0911)
1000 REM "KEY"
1010 PRINT 'CF',
1015 FIND(CHAN[1],KEY=SORTKEY$,KNUM=NUM(SRT$),ERR=*NEXT)
1020 LET V2$=KEY(CHAN[1],KNUM=NUM(SRT$),END=1050),SORTKEY$=v2$
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF V2$<>"" THEN READ (CHAN[1],KEY=V2$,KNUM=NUM(SRT$))V2$; LET V2$=V2$(3,20)
1050 IF P0$(16,1)="Y" THEN LET V4$="Enter The Look-Up Firm" ELSE LET V4$="Enter The Contact Look-Up Name"
1060 LET V0$="S",V1$="KC",V3$="",V0=20,V1=6,V2=3,V4$=V4$+" (<F3>=Lookup, <F4>=Quit)"
1070 IF R9$="Y" THEN PRINT @(18,23),"<F2>=Create Contact From Customer Masterfile",
1080 GOSUB 7000; PRINT @(V1,V2),CVS(V$,4)
1085 IF V3=4 THEN GOTO 9900
1090 PRINT @(0,23),'CL',@(0,22),
1100 rem IF V3=1 THEN GOSUB 6600; GOTO 1020
1110 IF R9$="Y" AND V3=2 THEN 
1112 LET V2$=V$; CALL "TMC.CA",A0$,A1$,A2$,A3$,A4$,CHAN[ALL],V3
1114 IF V3=4 THEN GOTO 4035 
1116 IF cvs(A0$(3,20),3)="" THEN GOTO 1050 
1118 extracted=1,key0$=a0$(1,2)+a0$(253,6)+a0$(269,6)
1120 GOSUB 5000; GOTO 1600
1124 fi
1130 IF V3=4 OR V$="END" THEN GOTO 9900
1140 IF V3=3 THEN LET V2$=V$,V$=""; CALL "TMC.AA",V$,V3; IF V3=4 THEN GOTO 4035
1150 IF V$="" THEN GOTO 0980 
1160 IF POS(" "<>V$)=0 THEN GOTO 1000
1170 IF NUM(V$,ERR=1171)>0 THEN READ (CHAN[1],KEY=A0$(1,2)+V$,KNUM=0,DOM=1171)iol=0210; PRINT @(V1,V2),a0$(3,20),;goto 1200
1175 READ (CHAN[1],KEY=SORTKEY$,KNUM=NUM(SRT$),DOM=1000)iol=210
1200 REM "FIND KEY"
1220 extracted=0,sortkey$=a0$(1,22)
1240 LET CONTACT$="",key0$=a0$(1,2)+a0$(253,6)+a0$(269,6)
1250 READ (CHAN[12],KEY=key0$,DOM=1251)
1260 LET K12$=KEY(CHAN[12],END=1300)
1270 IF K12$(1,K9)<>key0$(1,K9) THEN GOTO 1300
1280 LET CONTACT$="YES"
1300 REM "DISPLAY"
1340 GOTO 4500
1400 REM "NEW RECORD"
1410 LET V0$="Y",V1$="CR^",V2$="",V3$="",V4$="Is This A New Record? <Y/N>: ",V0=1,V1=41+INT(LEN(V4$)/2),V2=22
1420 GOSUB 7000
1430 LET V$=CVS(V$,4)
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM "SET-UP"
1510 LET A0$(23)="",A1$(1)="",A2$(1)="",A3$(1)="",A4$(1)="",key0$=""
1520 LET A0$(360,1)="Y"; REM "WA: #221
1530 LET CONTACT$="",extracted=1
1600 REM "--- Identification Firm Name
1610 GOSUB 15000; REM "Background"
1615 i0=0;goto 3900
1620 LET V0$="S",V1$="C",V2$=A0$(23,15),V3$="",V4$="Enter The Look-Up Firm For This Contact",V0=15,V1=33,V2=3,I0=0
1630 IF P0$(16,1)="Y" THEN LET V4$="Enter The Look-Up Name For This Contact"
1640 GOSUB 7000; PRINT @(V1,V2),CVS(V$,4)
1680 LET A0$(23,15)=CVS(V$,4),HOLDCONT$=V$
1690 GOTO 3900
1700 REM "--- Phone
1720 LET V0$="H",V1$="C",V2$=A0$(38,14),V3$=M8$,V4$="Enter The Phone Number For This Contact",V0=14,V1=55,V2=3
1730 LET V0$="S",V3$=""
1740 GOSUB 7000
1780 LET A0$(38,14)=FNUSPH$(V$); PRINT @(55,3),A0$(38,14),
1790 GOTO 3900
1800 REM "--- Extension
1805 GOTO 3900; REM Removed extension-added the 4 characters to phone# - 12/8/99
1810 LET V0$="S",V1$="C",V2$=A0$(48,4),V3$="",V4$="Enter The Telephone Extension",V0=4,V1=75,V2=3
1820 GOSUB 7000
1830 LET A0$(48,4)=V$
1840 GOTO 3900
1850 REM "--- Contact Name
1852 goto 3900; rem 'modify using GuiAddOn only
1855 GOSUB contact_list
1856 V4$="Enter Contact Name (<F3>=Lookup)"
1860 LET V0$="S",V1$="C",V2$=A0$(52,30),V3$="",V0=30,V1=7,V2=4
1870 GOSUB 7000;if v3=4 then goto 4000
1875 if v3=3 then gosub 11700;if cvs(v$,3)="" then v$=a0$(52,30)
1877 v$=cvs(v$,3);if v$<>"" and contact_list$<>"" and pos(v$+$0a$=contact_list$)=0 then V4$="Must Exist in Contacts File (<F3>=Lookup <F4>=Quit)";goto 1860
1880 LET A0$(52,30)=V$, A0$(23,15)=CVS(V$,4)
1883 if v$<>"" and contact_list$="" then gosub add_contact
1885 PRINT @(33,3),a0$(23,15),@(v1,v2),a0$(52,30)
1890 GOTO 3900
1900 REM "--- Title
1910 LET V0$="S",V1$="C",V2$=A0$(82,30),V3$="",V4$="Enter This Contact's Title",V0=30,V1=7,V2=6
1920 GOSUB 7000
1930 LET A0$(82,30)=V$
1940 GOTO 3900
1950 REM "--- Company
1955 rem IF A0$(112,30)=J1$(1,30) THEN LET A0$(112,30)=HOLDCOMP$
1960 LET V0$="S",V1$="C",V2$=A0$(112,30),V3$="",V4$="Enter The Contact's Full Company Name",V0=30,V1=7,V2=7
1970 GOSUB 7000;if cvs(v$,3)="" then goto 1960
1980 a0$(112,30)=v$,a0$(3,20)=cvs(v$,4)
1985 PRINT @(6,3),a0$(3,20)
1990 GOTO 3900
2000 REM "--- Address 1
2010 LET V0$="S",V1$="C",V2$=A0$(142,30),V3$="",V4$="Enter The First Address Line",V0=30,V1=7,V2=8
2020 GOSUB 7000
2030 LET A0$(142,30)=V$
2040 GOTO 3900
2050 REM "--- Address 2
2060 LET V0$="S",V1$="C",V2$=A0$(172,30),V3$="",V4$="Enter The Second Address Line",V0=30,V1=7,V2=9
2070 GOSUB 7000
2080 LET A0$(172,30)=V$
2090 GOTO 3900
2100 REM "--- City
2110 LET V0$="S",V1$="C",V2$=A0$(202,25),V3$="",V4$="Enter The City",V0=25,V1=7,V2=12
2120 GOSUB 7000
2130 LET A0$(202,25)=V$
2140 GOTO 3900
2150 REM "--- State
2160 LET V0$="S",V1$="C^",V2$=A0$(227,2),V3$="",V4$="Enter The 2 Character State (<F3>=Lookup)",V0=2,V1=38,V2=12
2170 GOSUB 7000
2172 IF V3<>3 THEN GOTO 2184
2174 LET TITLE$="State Codes",NUMBER=2,V$="SKIP",ROW=V2+2
2176 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
2178 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Name",FIELD[0]=1,POSITION[0]=1,LENGTH[0]=2,FIELD[1]=1,POSITION[1]=3,LENGTH[1]=30,COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
2180 CALL "BTM.10",CHAN[15],NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
2182 IF V$="" THEN GOTO 2150
2184 LET V$=V$+"  ",V$=V$(1,2); IF V$<>"  " THEN FIND (CHAN[15],KEY=V$,DOM=2150)
2188 LET A0$(227,2)=V$; PRINT @(V1,V2),V$
2190 GOTO 3900
2200 REM "--- Zip
2210 LET V0$="S",V1$="C",V2$=A0$(229,P[2]),V3$="",V4$="Enter The Zip Code",V0=P[2],V1=7,V2=13
2220 GOSUB 7000
2225 REM IF LEN(V$)<9 THEN LET V$=V$+J1$(9-LEN(V$))
2230 LET A0$(229,9)=V$
2235 PRINT @(V1,V2),FNZ9$(A0$(229,9)),
2240 GOTO 3900
2250 REM "--- Country
2252 dim country$:fattr(country$)
2254 if a0$(275,2)="  " then a0$(275,2)="US"
2260 readrecord(chan[16],key=a0$(275,2),knum=0,dom=*next)country$
2265 LET V0$="S",V1$="C^",V2$=country.country_name$,V3$="",V4$="Select Country (<F3>=Lookup)",V0=15,V1=25,V2=13
2270 GOSUB 7000
2272 IF V3<>3 THEN GOTO 2284
2274 LET TITLE$="Countries",NUMBER=1,V$="",ROW=V2+2
2276 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
2278 LET DESCRIPTION$[0]="Country",DESCRIPTION$[1]="",FIELD[0]=1,POSITION[0]=3,LENGTH[0]=30,FIELD[1]=0,POSITION[1]=0,LENGTH[1]=0,COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
2280 CALL "BTM.10",CHAN[16],NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
2284 LET V$=pad(v$,30); readrecord(CHAN[16],KEY=V$,knum=1,DOM=2250)country$
2288 LET A0$(275,2)=country.country_code$; PRINT @(V1,V2),V$
2290 GOTO 3900
2300 REM "--- Customer
2301 IF NUM(A0$(253,6)) AND PRM$[FID,TSECURE]<"9" THEN GOTO 2390
2302 IF PRM$[FID,TSECURE]<"5" THEN GOTO 2390
2305 IF R9$="Y" THEN LET V4$="Enter A Valid A/R Customer Number, (<F3>=Lookup)" ELSE LET V4$="Enter A Customer Number For This Contact"
2310 LET V0$="M",V1$="C",V2$=A0$(253,6),V3$=M0$,V0=P[0],V1=7,V2=14
2320 GOSUB 7000
2330 IF R9$<>"Y" THEN GOTO 2380
2332 IF V3<>3 THEN GOTO 2350
2336 CALL "SYC.LC",V$
2340 IF V$="" THEN GOTO 2300
2350 IF POS("0"<>V$)=0 THEN LET A0$(269,6)=m0$,A0$(253,6)=V$; GOTO 2385
2355 FIND (CHAN[3],KEY=N0$+V$,DOM=2300)*,CUST$
2356 IF V$=A0$(253,6) THEN GOTO 2360 ELSE READ (CHAN[1],KEY=N0$+V$,KNUM=0,DOM=2357)
2357 LET K$=KEY(CHAN[1],KNUM=0,END=2359); IF K$(1,LEN(M0$)+2)<>N0$+V$ THEN GOTO 2359 ELSE INPUT @(0,22),'CE',"Customer exists in telemarketing <CR>: ",*,@(0,22),'CL'; READ (CHAN[1],KEY=SORTKEY$,KNUM=NUM(SRT$),DOM=2358)
2358 GOTO 2300
2359 READ (CHAN[1],KEY=SORTKEY$,KNUM=NUM(SRT$),DOM=2360)
2360 IF V3=1 THEN LET A0$(253,6)=V$; GOTO 2380
2365 IF A0$(253,6)=V$ THEN GOTO 2390
2367 IF NUM(A0$(253,6),ERR=2368)<>0 THEN INPUT @(0,22),'CE',"Cannot change customer number - only add or delete <CR>: ",*,@(0,22),'CL'; GOTO 2300
2370 LET A0$(253,6)=V$
2375 CALL "TMC.DA",A0$; GOSUB 5000
2380 LET LINE$="Customer: "+CVS(CUST$(1,30),2); GOSUB 5260
2390 GOTO 3900
2400 REM "--- Cust Type
2402 IF PRM$[FID,TSECURE]<"9" THEN GOTO 2490
2405 IF R9$="Y" THEN LET V4$="Enter A Valid A/R Customer Type, (<F3>=Lookup)",V5$="L" ELSE LET V4$="Enter A Customer Type For This Contact",V5$=""
2410 LET V0$="S",V1$="C^",V2$=A0$(260,3),V3$="",V0=3,V1=28,V2=14
2420 GOSUB 7000
2430 IF R9$<>"Y" THEN GOTO 2480
2432 IF V3<>3 THEN GOTO 2450
2435 GOSUB 11000
2440 IF V$="" THEN GOTO 2400
2450 LET V$=V$+"   ",V$=V$(1,3); FIND (CHAN[8],KEY=N0$+V5$+V$,DOM=2455)X0$; GOTO 2460
2455 IF NUM(A0$(253,6)) THEN GOTO 2400 ELSE LET X0$=""
2460 PRINT @(V1,V2),V$; IF X0$<>"" THEN LET LINE$="Type: "+CVS(X0$(7,20),2); GOSUB 5260
2470 LET A0$(259,4)=V5$+V$
2490 GOTO 3900
2500 REM "--- Discount
2502 IF PRM$[FID,TSECURE]<"4" THEN GOTO 2590
2505 IF R9$="Y" THEN LET V4$="Enter A Valid A/R Discount Code, (<F3>=Lookup)",V5$="I" ELSE LET V4$="Enter A Discount Code For This Contact",V5$=""
2510 LET V0$="S",V1$="C^",V2$=A0$(264,2),V3$="",V0=2,V1=37,V2=14
2520 GOSUB 7000
2530 IF R9$<>"Y" THEN GOTO 2580
2532 IF V3<>3 THEN GOTO 2550
2535 GOSUB 11100
2540 IF V$="" THEN GOTO 2500
2550 LET V$=V$+"  ",V$=V$(1,2); FIND (CHAN[8],KEY=N0$+V5$+V$,DOM=2555)X0$; GOTO 2560
2555 IF NUM(A0$(253,6)) THEN GOTO 2500 ELSE LET X0$=""
2560 PRINT @(V1,V2),V$; IF X0$<>"" THEN LET LINE$="Disc: "+CVS(X0$(6,20),2); GOSUB 5260
2580 LET A0$(263,3)=V5$+V$
2590 GOTO 3900
2600 REM "--- Terms
2601 REM IF A0$(267,2)<>"  " AND PRM$[FID,TSECURE]<"9" THEN GOTO 2690; REM "WA: #221
2602 IF PRM$[FID,TSECURE]<"5" THEN GOTO 2690
2605 IF R9$="Y" THEN LET V4$="Enter A Valid A/R Terms Code (<F3>=Lookup)",V5$="A" ELSE LET V4$="Enter A Terms Code For This Contact",V5$=""
2610 LET V0$="S",V1$="C",V2$=A0$(267,2),V3$="",V0=2,V1=20,V2=14
2620 GOSUB 7000
2630 IF R9$<>"Y" THEN GOTO 2680
2632 IF V3<>3 THEN GOTO 2650
2635 GOSUB 11200
2640 IF V$="" THEN GOTO 2600
2650 FIND (CHAN[8],KEY=N0$+V5$+V$,DOM=2655)X0$; GOTO 2660
2655 IF NUM(A0$(253,6)) THEN GOTO 2600 ELSE LET X0$=""
2660 PRINT @(V1,V2),V$; IF X0$<>"" THEN LET LINE$="Terms: "+CVS(X0$(6,20),2); GOSUB 5260
2680 LET A0$(266,3)=V5$+V$
2690 GOTO 3900
2700 REM "--- TERRITORY
2705 if a0$(1,2)<>"01" then goto 3900
2710 REM Used as vertical mkt id 1/17/2000
2720 LET V0$="S",V1$="C^",V2$=A0$(285,3),V3$="",V4$="Enter The Territory For This Contact (<F3>=Lookup)",V0=3,V1=51,V2=6
2740 GOSUB 7000
2750 IF V3<>3 THEN GOTO 2770
2755 GOSUB 11300
2765 IF V$="" THEN GOTO 2700
2780 LET A0$(285,3)=V$
2783 FIND (CHAN[8],KEY=N0$+"H"+A0$(285,3),DOM=2700)X0$
2784 PRINT @(V1,V2),V$
2786 LET LINE$="Terr: "+CVS(X0$(7,20),2); GOSUB 5260
2790 GOTO 3900
2800 REM "--- Marketing Date
2805 goto 3900
2810 REM IF POS(" "<>A0$(285,3))=0 THEN GOTO 3900
2820 LET V0$="D",V1$="C6",V2$=FNU$(A0$(288,8)),V3$="",V4$="Enter The Marketing Callback Date (<F1>="+CVS(P0$(7,3),2)+" Days)",V0=6,V1=49,V2=7
2840 GOSUB 7000
2850 IF V3<>1 THEN GOTO 2875
2860 LET V$=PRM$[FID,TLDATE]; REM "WA: #221
2870 CALL "TMC.BA",V$,P0$(7,3)
2875 IF V$<>"" THEN PRINT @(V1,V2),FNB$(FNP$(V$)),
2880 LET A0$(288,8)=FNP$(V$)
2885 IF POS(" "<>V$)=0 THEN LET A0$(288,8)=""
2890 GOTO 3900
2900 REM "--- Order Rep (changed to Salesperson 12/8/99)
2902 IF PRM$[FID,TSECURE]<"5" THEN GOTO 2990
2920 LET V0$="S",V1$="C",V2$=A0$(296,3),V3$="",V4$="Enter The Salesperson (<F3>=Lookup)",V0=3,V1=51,V2=8
2940 GOSUB 7000; IF V3<>3 THEN GOTO 2974
2970 IF POS(" "<>V$)=0 THEN LET A0$(299,8)=""; PRINT @(49,8),FNB$(A0$(299,8)),
2972 GOSUB 10900; IF V$="" THEN GOTO 2900
2974 LET A0$(296,3)=V$; FIND (CHAN[8],KEY=N0$+"F"+A0$(296,3),DOM=2976)X0$; GOTO 2980
2976 IF NUM(A0$(253,6)) THEN GOTO 2900 ELSE LET X0$=""
2980 PRINT @(V1,V2),V$,; IF X0$<>"" THEN LET LINE$="Salesperson: "+CVS(X0$(7,20),2); GOSUB 5260
2990 GOTO 3900
3000 REM "--- Order Date
3005 goto 3900
3010 IF POS(" "<>A0$(296,3))=0 THEN GOTO 3900
3020 LET V0$="D",V1$="C6",V2$=FNU$(A0$(299,8)),V3$="",V4$="Enter The Ordering Date (<F1>=Today)",V0=6,V1=49,V2=9
3040 GOSUB 7000
3050 IF V3=1 THEN LET V$=PRM$[FID,TLDATE]; PRINT @(V1,V2),FNB$(FNP$(V$)),; REM  "WA: #221
3080 LET A0$(299,8)=FNP$(V$)
3090 GOTO 3900
3100 REM "--- CSR Rep
3110 GOTO 3900; REM Skip this, we don't use it-12/8/99
3120 LET V0$="S",V1$="C",V2$=A0$(307,3),V3$="",V4$="Enter The Customer Service Rep For This Contact",V0=3,V1=49,V2=10
3140 GOSUB 7000
3180 LET A0$(307,3)=V$
3190 GOTO 3900
3200 REM "--- Literature Date
3205 GOTO 3900; REM Skip this, we don't use it-12/8/99
3210 LET V0$="D",V1$="C6",V2$=FNU$(A0$(361,8)),V3$="",V4$="Enter The Date To Send Literature",V0=8,V1=69,V2=12,V5$="A"
3220 GOSUB 7000
3240 IF V$="" THEN LET V$=J1$(1,8)
3250 PRINT @(V1,V2),FNB$(FNP$(V$)),
3280 LET A0$(361,8)=FNP$(V$)
3290 GOTO 3900
3300 REM "--- BSG ID number (something to do with VAT tax?)
3305 if a0$(1,2)<>"02" then goto 3900
3320 LET V0$="S",V1$="C",V2$=A0$(315,16),V3$="",V4$="Enter the ID",V0=16,V1=51,V2=14
3340 GOSUB 7000
3360 a0$(315,16)=v$;PRINT @(V1,V2),V$,
3390 GOTO 3900
3500 REM "--- Source Code
3505 goto 3900;rem if a0$(1,2)<>"01" then goto 3900
3510 LET V0$="S",V1$="C",V2$=A0$(348,4),V3$="",V4$="Enter A Valid Source Code, (<F3>=Lookup)",V0=4,V1=69,V2=8,V5$="B"
3520 GOSUB 7000
3530 IF V3<>3 THEN GOTO 3550
3535 GOSUB 11400
3540 IF V$="" THEN GOTO 3500
3550 LET V$=V$+"    ",V$=V$(1,4)
3560 FIND (CHAN[6],KEY=N0$+V5$+V$,DOM=3500)X0$
3565 PRINT @(V1,V2),V$
3570 LET LINE$="Source: "+CVS(X0$(8,20),2); GOSUB 5260
3580 LET A0$(347,5)=V5$+V$
3590 GOTO 3900
3600 REM "--- Interest
3605 goto 3900;rem if a0$(1,2)<>"01" then goto 3900
3610 LET V0$="S",V1$="C",V2$=A0$(353,4),V3$="",V4$="Enter A Valid Interest Code, (<F3>=Lookup)",V0=4,V1=69,V2=9,V5$="C"
3620 GOSUB 7000
3630 IF V3<>3 THEN GOTO 3650
3635 GOSUB 11500
3640 IF V$="" THEN GOTO 3600
3650 LET V$=V$+"    ",V$=V$(1,4)
3660 FIND (CHAN[6],KEY=N0$+V5$+V$,DOM=3600)X0$
3665 PRINT @(V1,V2),V$
3670 LET LINE$="Interest: "+CVS(X0$(8,20),2); GOSUB 5260
3680 LET A0$(352,5)=V5$+V$
3690 GOTO 3900
3700 REM "--- Lit Code
3705 goto 3900;rem IF A0$(360,1)<>"Y" THEN GOTO 3900
3710 LET V0$="S",V1$="C",V2$=A0$(358,2),V3$="",V4$="Enter A Valid Literature Code, (<F3>=Lookup)",V0=2,V1=69,V2=11,V5$="D"
3720 GOSUB 7000
3730 IF V3<>3 THEN GOTO 3750
3735 GOSUB 11600
3740 IF V$="" THEN GOTO 3700
3750 LET V$=V$+"  ",V$=V$(1,2)
3760 FIND (CHAN[6],KEY=N0$+V5$+V$,DOM=3700)X0$
3765 PRINT @(V1,V2),V$
3770 LET LINE$="Literature: "+CVS(X0$(6,20),2); GOSUB 5260
3780 LET A0$(357,3)=V5$+V$
3790 GOTO 3900
3800 REM "--- Fax Phone
3820 LET V0$="H",V1$="C",V2$=A0$(375,14),V3$=M8$,V4$="Enter The Fax Number For This Contact",V0=14,V1=55,V2=4
3825 LET V0$="S",V3$=""
3840 GOSUB 7000
3880 LET A0$(375,14)=FNUSPH$(V$); PRINT @(55,4),A0$(375,14),
3890 GOTO 3900
3900 REM "NEXT FIELD"
3905 REM IF V3=4 THEN LET QUIT=1; GOTO 4100 ELSE LET QUIT=0
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 IF V3=4 THEN GOTO 4000
3950 LET I0=I0+1
3960 ON I0 GOTO 1600,1700,1800,1850,3800,1900,1950,2000,2050,10200,10250,2100,2150,2200,2250,2300,2600,2400,2500,2700,2800,2900,3000,3100,5400,3300,3500,3600,5500,3700,3200,5700,5900,5800,6000,4000
3990 LET I0=K0
4000 REM " --- Correct?"
4015 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4030 LET V0$="S",V1$="KC^",V2$="Y",V3$="",V0=7,V1=FNV(V4$),V2=22
4035 GOSUB 7000
4040 IF V$="DELETE" THEN GOTO 4200
4050 IF V$="N" THEN extract (CHAN[9],KEY=key0$,KNUM=0,DOM=1400)IOL=0210;extracted=1;gosub 5000;goto 1600
4060 IF V$<>"Y" THEN GOTO 4000
4100 REM "WRITE"
4105 if extracted=0 then goto 4190
4110 a0$(253,6)=str(num(a0$(253,6)):m0$),a0$(269,6)=str(num(a0$(269,6)):m0$)
4112 if a0$(253,6)=m0$ and a0$(269,6)=m0$ then gosub next_tm_number
4114 if a0$(253,6)<>m0$ then a0$(269,6)=m0$ 
4116 if key0$<>"" and key0$<>n0$+a0$(253,6)+a0$(269,6) then call "TMC.GA",a0$,key0$,chan[all];key0$=a0$(1,2)+a0$(253,6)+a0$(269,6)
4118 CALL "TMC.EA",A0$
4120 WRITE (CHAN[9],ERR=4140)IOL=0210
4125 if ctype$<>a0$(260,3) and pos("000000"=key0$)<>3 then call "cust_web_updt.pgm",err=*next,key0$(1,8)
4130 extracted=0;GOTO 4190
4140 IF ERR<>11 THEN GOTO 9900
4150 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Duplicate Multi-Key Record. <Cr> And Change Field.",V0=1,V1=41+INT(LEN(V4$)/2),V2=22
4160 GOSUB 7000
4170 GOTO 4000
4190 IF QUIT THEN GOTO 9900 ELSE GOTO 4500
4200 REM "DELETE"
4210 LET V0$="S",V1$="C^",V2$="",V3$="",V4$="Confirm: Delete This Contact And Related Comments? <Yes/No>: ",V0=3,V1=41+INT(LEN(V4$)/2),V2=22
4212 IF PRM$[FID,TSECURE]<"9" THEN LET V4$="Delete option is restricted to the administrator"
4220 GOSUB 7000; IF PRM$[FID,TSECURE]<"9" THEN GOTO 4000
4225 IF V$="N" OR V$="NO" THEN GOTO 4000
4230 IF V$<>"YES" THEN GOTO 4200
4240 LET CHANL=CHAN[2]; REMOVE (CHAN[9],KEY=key0$,DOM=4241)
4250 READ (CHANL,KEY=key0$,DOM=4251)
4260 LET K1$=KEY(CHANL,END=SET_CHANL)
4270 IF POS(key0$=K1$)<>1 THEN GOTO SET_CHANL
4280 REMOVE (CHANL,KEY=K1$,DOM=4181)
4285 GOTO 4260
4290 SET_CHANL: IF CHANL=CHAN[12] THEN GOTO 1000 ELSE LET CHANL=CHAN[12]; GOTO 4250
4500 REM "OPTIONS"
4501 GOSUB 15500; REM "Clear screen"
4505 DIM MENUS$[4]
4510 LET MENUS=1,MENUS$[MENUS]="TMMCMA",WIN_X=45,WIN_Y=7
4515 IF P0$(19,1)="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="TMC.FA"
4520 LET O0=1,O9=0
4530 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4535 IF SELECTION$="" THEN GOTO 1000
4540 IF SELECTION$="TMC.MA" THEN GOTO 12100; REM "Comments
4550 IF SELECTION$="TMC.FA" THEN CALL "TMC.FA",A0$,V3; IF V3=4 THEN GOTO 4500 else goto 4110
4555 IF SELECTION$="TMC.HA" THEN CALL "TMC.HA",key0$,V3,CHAN[ALL],a0$; IF V3=4 THEN GOTO 12200 ELSE GOTO 4500; REM "Contacts"
4560 IF SELECTION$="TMC.IA" THEN CALL "TMC.IA",A0$(253,6),N0$; GOTO 4500; REM "Prev orders"
4565 IF V$="DELETE" THEN GOTO 4200
4570 IF SELECTION$="PROFILE" then GOSUB 15000; GOTO 4000; REM "WA: #221
4575 IF SELECTION$="BEC.01" THEN user_code$=fill(2);CALL "BEC.01",N0$,A0$(253,6),user_code$; GOTO 4500
4580 IF SELECTION$="BEM.01" THEN CALL "BEM.01",N0$,A0$(253,6),A0$(296,6); GOTO 4500
4585 IF SELECTION$="BVF.01" THEN CALL "BVF.01",N0$,A0$(253,6); GOTO 4500
4590 IF SELECTION$="TMC.OA" THEN CALL "TMC.OA",A0$(253,6),V3; IF V3=1 THEN GOTO 4590 ELSE IF V3=4 THEN GOTO 12200 ELSE GOTO 4500; REM "Serial Numbers"
4595 IF SELECTION$="FIRM" THEN GOTO 1000
4600 IF SELECTION$="QUIT" THEN LET V3=4; GOTO 12200
4610 IF V3=4 THEN GOTO 9900
4620 GOTO 4500
5000 REM "--- Display
5005 REM "CALL "TMC.DA",A0$
5010 PRINT @(6,3),A0$(3,20),@(33,3),A0$(23,15),@(55,3),A0$(38,14),@(7,4),A0$(52,30),@(55,4),A0$(375,14),
5020 PRINT @(7,6),A0$(82,30),@(51,6),A0$(285,3),@(7,7),A0$(112,30),
5021 rem PRINT @(7,6),A0$(82,30),@(49,6),A0$(285,3),@(69,6),FNB$(A0$(331,8)),@(7,7),A0$(112,30),@(49,7),FNB$(A0$(288,8)),@(69,7),FNB$(A0$(339,8)),@(69,8),A0$(348,4),
5030 PRINT @(7,8),A0$(142,30),@(7,9),A0$(172,30),@(51,10),A0$(360,1),
5035 PRINT @(51,8),A0$(296,3);rem ,@(49,9),FNB$(A0$(299,8))
5040 PRINT @(7,10),A0$(389,30),@(7,11),A0$(419,30)
5050 PRINT @(7,12),A0$(202,25),@(38),A0$(227,2),
5055 DIM Y0$(26); FIND (CHAN[8],KEY=N0$+"A"+A0$(267,2),DOM=5056)Y0$
5060 REM PRINT @(7,13),FNF$(A0$(253,P[0]),M0$),@(27,13),A0$(267,2),@(49,14),FNB$(A0$(269,8)),@(69,14),FNB$(A0$(277,8)),
5061 PRINT @(7,13),FNZ9$(A0$(229,9)),@(25,13),A0$(238,15),
5065 PRINT @(7,14),FNF$(A0$(253,P[0]),M0$),@(20,14),A0$(267,2),@(28,14),A0$(260,3),@(37,14),A0$(264,2),@(51,14),a0$(315,16),
5080 ctype$=a0$(260,3)
5100 REM "--- Display User Defined Fields
5110 LET X0=14,Y0=L1,X=1
5120 GOSUB 6400; IF V3$<>"" THEN LET Y$=STR(NUM(Y$):V3$)
5130 IF POS(X0$(1,1)="SDYN")=0 THEN GOTO 5230
5180 PRINT @(X0,Y0),Y$,
5200 REM "--- Next Field
5210 LET Y0=Y0+1
5220 IF Y0>L9 THEN LET Y0=L1,X0=X0+40
5230 LET X=X+1
5240 IF X<=X9 THEN GOTO 5120
5250 RETURN 
5260 REM "--- Display Code Info
5270 PRINT @(0,16),'CL',@(10,16),LINE$
5290 RETURN 
5400 REM "--- CSR Code (was 3200)
5405 goto 3900; rem IF POS(" "<>A0$(307,3))=0 THEN GOTO 3900
5410 LET V0$="S",V1$="C",V2$=A0$(311,4),V3$="",V4$="Enter A Valid CSR Code (<F3>=Lookup)",V0=4,V1=49,V2=11,V5$="A"
5420 GOSUB 7000
5430 IF V3<>3 THEN GOTO 5450
5435 rem GOSUB 11700
5440 IF V$="" THEN GOTO 5400
5450 LET V$=V$+"    ",V$=V$(1,4)
5460 FIND (CHAN[6],KEY=N0$+V5$+V$,DOM=5400)X0$
5470 LET LINE$="CSR Code: "+CVS(X0$(8,20),2); GOSUB 5260
5480 LET A0$(310,5)=V5$+V$
5485 PRINT @(V1,V2),A0$(311,4)
5490 GOTO 3900
5500 REM "--- Send Lit?
5505 if a0$(1,2)<>"01" then goto 3900
5520 LET V0$="S",V1$="C^",V2$=A0$(360,1),V3$="",V4$="Cue This Contact For Literature Mailing?",V0=1,V1=51,V2=10
5540 GOSUB 7000; LET V$=V$+" ",V$=V$(1,1); IF POS(V$=" YN")=0 THEN GOTO 5500
5570 REM " IF V$="N" THEN LET A0$(261,8)="", A0$(358,2)=""; PRINT @(69,9),"  ",@(69,10),"        ",
5580 LET A0$(360,1)=V$
5590 GOTO 3900
5600 REM "--- Lit Date
5605 GOTO 3900
5610 IF A0$(360,1)<>"Y" THEN GOTO 3900
5620 LET V0$="D",V1$="C6",V2$=FNU$(A0$(361,8)),V3$="",V4$="Date To Send Literature",V0=8,V1=69,V2=11
5640 GOSUB 7000
5680 LET A0$(361,8)=FNP$(V$)
5690 GOTO 3900
5700 REM "--- Dear
5705 goto 3900;rem if a0$(1,2)<>"01" then goto 3900
5720 LET V0$="S",V1$="C",V2$=A0$(369,6),V3$="",V4$="Enter The Salutation/Title For Beginning Letters",V0=6,V1=69,V2=13
5740 GOSUB 7000
5780 LET A0$(369,6)=V$
5790 GOTO 3900
5800 REM "--- Last Invoice Date
5805 goto 3900;rem if a0$(1,2)<>"01" then goto 3900
5820 LET V0$="D",V1$="C6",V2$=FNU$(A0$(277,8)),V3$="",V4$="Enter The Last Invoice Date",V0=6,V1=69,V2=14
5840 GOSUB 7000
5850 REM "IF V3<>1 THEN GOTO 5875
5860 REM "LET V$=PRM$[FID,TLDATE]; REM "WA: #221
5870 REM "CALL "TMC.BA",V$,P0$(7,3)
5875 IF V$<>"" THEN PRINT @(V1,V2),FNB$(FNP$(V$)),
5880 LET A0$(277,8)=FNP$(V$)
5890 GOTO 3900
5900 REM "--- First Invoice Date
5905 goto 3900;rem if a0$(1,2)<>"01" then goto 3900
5920 LET V0$="D",V1$="C6",V2$=FNU$(A0$(269,8)),V3$="",V4$="Enter The First Invoice Date",V0=6,V1=49,V2=14
5940 GOSUB 7000
5975 IF V$<>"" THEN PRINT @(V1,V2),FNB$(FNP$(V$)),
5980 LET A0$(269,8)=FNP$(V$)
5990 GOTO 3900
6000 REM "--- Routines for User Defined Fields
6004 rem ' removed the second half of this test, it had the impact of excluding non account contacts from accessing user defined fields
6005 rem 'if a0$(1,2)<>"01" or pos(m0$=key0$)=3 then goto 3900
6006 if a0$(1,2)<>"01" then goto 3900
6020 LET X0=14,Y0=L1,X=1,SKIP=0
6030 REM IF I0>23 THEN LET SKIP=I0-23,I0=23
6050 REM "--- Set Correct Element
6060 GOSUB 6400
6070 IF POS(X0$(1,1)="SDYN")=0 THEN IF V3=2 THEN GOTO 6350 ELSE GOTO 6250
6080 IF SKIP>0 THEN LET SKIP=SKIP-1; GOTO 6200
6100 REM "--- User Defined Field Entry
6120 LET V0$=X0$(1,1),V1$="C6^",V2$=Y$,V1=X0,V2=Y0
6130 IF V0$="Y" THEN LET V0$="S"
6140 GOSUB 7000; IF X0$(1,1)="Y" THEN LET V$=V$+" ",V$=V$(1,1); IF POS(V$=" YN")=0 THEN GOTO 6100
6160 LET Y$=V$
6170 GOSUB 6450
6180 IF V3=4 THEN GOTO 3900
6190 IF V3=2 THEN GOTO 6300
6200 REM "--- Next User Defined Field
6210 LET Y0=Y0+1
6220 IF Y0>L9 THEN LET Y0=L1,X0=X0+40
6250 LET X=X+1
6260 IF X>X9 THEN GOTO 4000
6290 GOTO 6050
6300 REM "--- Previous User Defined Field
6310 LET Y0=Y0-1
6320 IF Y0<L1 THEN LET Y0=L9,X0=X0-29
6350 LET X=X-1
6360 IF X<1 THEN GOTO 3900
6390 GOTO 6050
6400 REM "--- Get User Defined Data Elements
6405 LET V0=25
6410 LET Y$="",X0$=P1$((X-1)*(X8+1)+1,X8+1),V4$="",V3$=""
6420 IF X0$(1,1)="S" THEN LET Y=INT(POS(STR(X:"00")=X1$,2)/2),Y$=A1$(Y*25+1,V0),V4$="Enter Up To "+STR(V0)+" Characters"
6425 IF X0$(1,1)="D" THEN LET Y=INT(POS(STR(X:"00")=X2$,2)/2),Y$=FNU$(A2$(Y*8+1,8)),V0=8,V4$="Enter A Date"
6430 IF X0$(1,1)="Y" THEN LET Y=INT(POS(STR(X:"00")=X3$,2)/2),Y$=A3$(Y+1,1),V4$="Y=Yes, N=No",V0=1
6435 IF X0$(1,1)="N" THEN LET Y=INT(POS(STR(X:"00")=X4$,2)/2),Y$=A4$(Y*10+1,10),V0=M1,V3$=M1$
6440 IF POS(" "<>Y$)=0 THEN LET Y$=""
6445 RETURN 
6450 REM "--- Store User Defined Data Elements
6460 IF X0$(1,1)="S" THEN LET A1$(Y*25+1,25)=Y$
6465 IF X0$(1,1)="D" THEN LET A2$(Y*8+1,8)=FNP$(Y$)
6470 IF X0$(1,1)="Y" THEN LET A3$(Y+1,1)=Y$
6475 IF X0$(1,1)="N" THEN LET A4$(Y*10+1,10)=Y$
6490 RETURN 
6600 REM "--- Change Sort
6605 DIM SORT$[4]; LET SORT$[0]="Cont",SORT$[1]="Firm",SORT$[2]=CVS(P2$(1,20),2),SORT$[3]=CVS(P2$(21,20),2),SORT$[4]=CVS(P2$(41,20),2)
6610 IF P0$(16,1)="Y" THEN LET SORT$[0]="Firm",SORT$[1]="Name"
6615 LET SORTS$="0="+SORT$[0]+", 1="+SORT$[1]+", 2="+SORT$[2]+", 3="+SORT$[3]+", 4="+SORT$[4]
6620 IF LEN(SORTS$)<80 THEN PRINT @(40-INT(LEN(SORTS$)/2),23),SORTS$, ELSE PRINT @(0,23),SORTS$(1,79),
6630 LET V0$="S",V1$="C",V2$=SRT$,V3$="01234",V4$="Enter Sort Sequence:",V0=1,V1=41+INT(LEN(V4$)/2),V2=22
6635 GOSUB 7000
6640 PRINT @(0,23),'CL',@(0,22),
6645 IF V3=4 THEN GOTO 6690
6650 LET SRT$=V$
6660 LET V0$="S",V2$="",V3$="",V4$="Enter "+SORT$[NUM(SRT$)]+" To Start: ",V0=11,V1=41+INT(LEN(V4$)/2),V2=22
6670 GOSUB 7000
6680 READ (CHAN[1],KEY=N0$+V$,KNUM=NUM(SRT$),DOM=6681)
6690 RETURN 
6700 REM " --- Contact List"
6710 contact_list:
6715 contact_list$="",done=0;READ (CHAN[12],KEY=key0$,DOM=*next)
6720 repeat 
6725 x0$="";read(CHAN[12],END=*next)x0$
6730 IF pos(key0$=x0$) then contact_list$=contact_list$+cvs(x0$(18,30),3)+$0a$ else done=1
6780 until done
6790 RETURN 
6800 add_contact: DIM x0$(332); LET x0$(1)=key0$+"010"+v$
6810 x0$(89,1)=a0$(360,1);rem 'send lit flag
6820 write(chan[12],key=x0$(1,17))x0$
6890 return
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8005 REM "-- Special Date functions for YYYYMMDD Dates: FNU=Unpack (trims Century), FNP=Pack (add Century), FNB=Display YYYYMMDD as MM/DD/YY
8010 DEF FNA$(Q$,Q2$)=STR((ASC(Q$)-32)*POS(" "<>Q2$(2,1)):"00")
8020 DEF FNB$(Q1$)=Q1$(5,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(7,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(3,2)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(NUM(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8080 DEF FNH(Q$)=ASC(Q$)-32
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8094 DEF FNP$(Q1$)=STR(FNY(Q1$+"  ")*(LEN(Q1$)>2):"####")+Q1$(3*(LEN(Q1$)>2)+(LEN(Q1$)=0))
8096 DEF FNU$(Q1$)=Q1$(5,4)+Q1$(3,2)
8098 DEF FNY(Q$)=(ASC(Q$(1,1))-(65*(Q$(1,1)>="A"))-48*(Q$(1,1)<"A"))*10+NUM(Q$(2,1))+1900+(100*(Q$(1,1)>="A"))
8099 DEF FNW$(Q$)=Q$(1,POS(" "<>Q$,-1))
8100 DEF FNZ9$(Q$)
8105 LET Q=NUM(Q$,ERR=8110); IF LEN(CVS(Q$,2))=9 THEN LET Q$=Q$(1,5)+"-"+Q$(6,4)
8110 RETURN Q$
8115 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Exit"
9910 FOR I=1 TO 12
9920 CLOSE (CHAN[I],ERR=9921)
9930 NEXT I
9940 PRINT (0,ERR=9941)'POP',
9960 LET X$=STBL("TMMCMA","")
9979 REM "Fail enters here
9980 PRINT (0,ERR=9951)'CURSOR'("ON"),
9990 EXIT 
10000 REM " --- Have we been called by ourselves?"
10010 LET FAIL=1,X$=""
10020 LET X$=STBL("TMMCMA",ERR=10021)
10030 IF X$="IN TMMCMA" THEN GOTO 10090
10040 LET X$=STBL("TMMCMA","IN TMMCMA")
10050 LET FAIL=0
10090 RETURN 
10200 REM "--- Address 3
10210 LET V0$="S",V1$="C",V2$=A0$(389,30),V3$="",V4$="Enter The Third Address Line",V0=30,V1=7,V2=10
10220 GOSUB 7000
10230 LET A0$(389,30)=V$
10240 GOTO 3900
10250 REM "--- Address 4
10260 LET V0$="S",V1$="C",V2$=A0$(419,30),V3$="",V4$="Enter The Fourth Address Line",V0=30,V1=7,V2=11
10270 GOSUB 7000
10280 LET A0$(419,30)=V$
10290 GOTO 3900
10800 REM " --- Function to format US phone numbers
10810 DEF FNUSPH$(TMPPH$)
10820 LET TMP$="",TLEN=LEN(TMPPH$); IF TLEN THEN FOR I=1 TO TLEN; IF POS(TMPPH$(I,1)="0123456789") THEN LET TMP$=TMP$+TMPPH$(I,1) FI; NEXT I
10830 IF LEN(TMP$)=10 AND CVS(A0$(238,15),2)="" THEN LET TMPPH$=TMP$(1,3)+"."+TMP$(4,3)+"."+TMP$(7)
10840 RETURN TMPPH$
10900 REM " --- Salesperson Lookup
10910 LET NMBR=1
10920 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
10930 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3
10940 LET DESCRIPT$[1]="Salesperson",FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
10950 CALL "SYC.LK",CHAN[8],"F",NMBR,"Salesperson Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
10960 RETURN 
11000 REM " --- Customer Type Lookup
11010 LET NMBR=1
11020 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11030 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3
11040 LET DESCRIPT$[1]="Customer Type",FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
11050 CALL "SYC.LK",CHAN[8],"L",NMBR,"Customer Type Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11090 RETURN 
11100 REM " --- Discount Code Lookup
11110 LET NMBR=1
11120 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11130 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
11140 LET DESCRIPT$[1]="Sales Discount",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
11150 CALL "SYC.LK",CHAN[8],"I",NMBR,"Discount Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11190 RETURN 
11200 REM " --- Terms Code Lookup
11210 LET NMBR=1
11220 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11230 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
11240 LET DESCRIPT$[1]="Terms",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
11250 CALL "SYC.LK",CHAN[8],"A",NMBR,"Terms Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11290 RETURN 
11300 REM " --- Territory Code Lookup
11310 LET NMBR=1
11320 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11330 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3
11340 LET DESCRIPT$[1]="Territory",FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
11350 CALL "SYC.LK",CHAN[8],"H",NMBR,"Territory Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11390 RETURN 
11400 REM " --- Source Code Lookup
11410 LET NMBR=1
11420 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11430 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=4
11440 LET DESCRIPT$[1]="Lead Source",FIELD[1]=1,POSITION[1]=8,LENGTH[1]=20
11450 CALL "SYC.LK",CHAN[6],"B",NMBR,"Source Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11490 RETURN 
11500 REM " --- Interest Code Lookup
11510 LET NMBR=1
11520 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11530 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=4
11540 LET DESCRIPT$[1]="Interest",FIELD[1]=1,POSITION[1]=8,LENGTH[1]=20
11550 CALL "SYC.LK",CHAN[6],"C",NMBR,"Interest Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11590 RETURN 
11600 REM " --- Literature Code Lookup
11610 LET NMBR=1
11620 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11630 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
11640 LET DESCRIPT$[1]="Liturature",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
11650 CALL "SYC.LK",CHAN[6],"D",NMBR,"Liturature Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11690 RETURN 
11700 REM " --- Contact Name Lookup
11710 LET NMBR=0,v$="SKIP"
11720 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11730 LET DESCRIPT$[0]="Name",FIELD[0]=1,POSITION[0]=18,LENGTH[0]=30
11740 rem LET DESCRIPT$[1]="",FIELD[1]=1,POSITION[1]=8,LENGTH[1]=20
11750 CALL "SYC.LK",CHAN[12],key0$(3),NMBR,"Contact Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11790 RETURN 
12000 REM " --- Options"
12001 LET V4$="<F1>=Comments, "; IF CONTACT$="YES" THEN LET V4$=V4$+'BR'+"<F2>=Add'l Contacts"+'ER' ELSE LET V4$=V4$+"<F2>=Add'l Contacts"
12002 REM LET V4$=V4$+", <F3>=Prev Orders"
12003 IF P0$(19,1)="Y" THEN LET V4$=V4$+", <C>=Create New Cust"
12004 LET V4$=V4$+", <F4>=quit"
12005 PRINT @(40-INT(LEN(V4$)/2),23),V4$,
12006 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="<Return Key> for Next Firm, <N> to Edit Firm, <P> to Print Report   .",V0=6,V1=41+INT(LEN(V4$)/2),V2=22
12007 GOSUB 7000
12010 PRINT @(0,23),'CL',@(0,22),
12021 LET I0=NUM(V$,ERR=4081)-2; IF I0>50 THEN GOTO 4000 ELSE GOTO 3900
12070 IF SELECTION$="" THEN GOTO 1000
12080 IF SELECTION$="SYC.KA" THEN GOTO 4100
12090 RUN SELECTION$,ERR=4000
12100 REM "Comments"
12110 LET FLAG$=""; CALL "TMC.MA",ERR=4000,key0$,FLAG$,A0$(253,6),V3,CHAN[ALL]; REM "WA:205
12120 IF FLAG$<>"1" THEN GOTO 12140
12130 LET A0$(339,8)=FNP$(PRM$[FID,TLDATE]); REM "WA: #221
12140 IF V3=2 OR V3=3 THEN GOTO 4050 ELSE GOTO 4500; REM IF V3=4 THEN GOTO 12200 ELSE GOTO 4000
12200 REM "Quit here
12210 IF V3=4 THEN LET QUIT=1; GOTO 4100 ELSE LET QUIT=0; GOTO 4000
13000 next_tm_number:
13010 dim ctr$:"firm_id:c(2),rec_id:c(3),tm_number:c(6),available:c(19)"
13015 ctrkey$=n0$+"T  ",ctrfile=chan[6],ok=0
13020 extractrecord(ctrfile,key=ctrkey$,err=*next)ctr$;ok=2
13025 if ok<2 and err=0 then wait 1;ok=ok+1;retry
13030 a0$(269,6)=ctr.tm_number$
13040 ctr.tm_number$=str(num(ctr.tm_number$)+1:m0$)
13050 ctr$=field(ctr$)
13060 writerecord(ctrfile,key=ctrkey$)ctr$
13090 return
15000 REM "Background"
15020 PRINT 'SB',@(0,6),"Title:",@(40),"Territory:",@(59),"",@(1,7),"Firm:",@(38)
15021 rem PRINT 'SB',@(0,6),"Title:",@(38),"Territory:",@(59),"Init Ent:",@(1,7),"Firm:",@(38),"Next Call:",@(59),"Last Ent: "
15030 PRINT @(0,8),"Addr1:",@(40),"Slsperson:",@(61),"",@(0,9),"Addr2:",@(41),
15031 rem PRINT @(0,8),"Addr1:",@(38),"Slsperson:",@(61),"Source:",@(0,9),"Addr2:",@(41),"Ord Dt:",@(63),"Intr: ",
15040 PRINT @(0,10),"Addr3:",@(40),"",@(41),"Send Lit:",@(0,11),"Addr4:",@(39)
15041 REM PRINT @(1,10),"City:",@(40),"CSR Rep:",@(59),"Send Lit:",@(0,11),"State:",@(11),"Zip:",@(39),"CSR Code: ",@(59),"Lit Code: ",
15050 PRINT @(0,12)," City:",@(34),"St:",@(0,13),"  ZIP:",@(18),"Cntry:",
15060 PRINT @(0,14),"Cust#: ",@(14),"Trms: ",@(23),"Typ:",@(32,14),"Dsc:",@(43),"UST ID:",
15070 CALL "SYC.WC",1,0,80,0,0,15,0
15080 PRINT 'SB',
15100 REM "--- Set Parameters (User Def'd Fields)
15110 LET X9=21,X8=LEN(P1$)/X9-1,X2$="",X3$="",X4$=""
15111 LET X9=10; REM WA# 209
15120 LET X0=0,Y0=L1
15130 FOR X=1 TO X9
15140 LET X0$=P1$((X-1)*(X8+1)+1,X8+1)
15150 IF X0$(1,1)="S" THEN LET X1$=X1$+STR(X:"00")
15160 IF X0$(1,1)="D" THEN LET X2$=X2$+STR(X:"00")
15170 IF X0$(1,1)="Y" THEN LET X3$=X3$+STR(X:"00")
15180 IF X0$(1,1)="N" THEN LET X4$=X4$+STR(X:"00")
15190 IF POS(X0$(1,1)="SDYN")=0 THEN GOTO 15240
15200 IF Y0>L9 THEN LET Y0=L1,X0=X0+40
15210 LET X0$(2,X8)=J1$(1,X8-LEN(CVS(X0$(2,X8),2)))+X0$(2)
15220 IF INITIAL$<>"Y" THEN PRINT @(X0,Y0),X0$(2,X8),":",
15230 LET Y0=Y0+1
15240 NEXT X
15250 rem IF INITIAL$="Y" THEN DIM A1$(LEN(X1$)/2*25),A2$(LEN(X2$)/2*8),A3$(LEN(X3$)/2),A4$(LEN(X4$)/2*10)
15255 IF INITIAL$="Y" THEN DIM A1$(50),A2$(8),A3$(8),A4$(LEN(X4$)/2*10)
15260 LET A1LEN=LEN(A1$),A2LEN=LEN(A2$),A3LEN=LEN(A3$),A4LEN=LEN(A4$)
15270 IF INITIAL$="Y" THEN LET INITIAL$=""; GOTO 15290
15275 PRINT 'SF',
15280 GOSUB 5000
15290 RETURN 
15500 REM "Clear lines"
15510 FOR X=6 TO 21
15520 PRINT @(0,X),'CL',
15530 NEXT X
15590 RETURN 
62000 REM " --- Set-up ADD+ON Environment"
62010 DIM PRM$[0]; CALL "ADC.01",PRM$[ALL],STATUS
62020 IF STATUS<>0 THEN EXITTO 9900
62030 LET FID=0,AP=1,AR=2,BM=3,DC=4,FA=5,GL=6,IV=7,MP=8,OP=9,PO=10,PR=11,SA=12,SH=13,SY=14,WO=15,TS=21,TB=22,DC=23,V2=24,EF=25; REM " ---- Module Id Numbers"
62040 DIM J$(80,"-"),J0$(40,"0"),J1$(80)
62050 LET INSTALL=0,KEYLEN=1,KEYMAX=2,KEYMASK=3,CPERIOD=4,CYEAR=5,DMASK=6,UMASK=7,RMASK=8,TWO=9,THREE=10,FOUR=11,EMASK=12,TOPCODE=1,TDATE=2,TMENU=3,TPRINTER=4,TSECURE=5,TFIRM=6,TPMODE=7,TPROG=8,TPOPTS=9,TFNAME=10,TPNAME=11,TLDATE=12
62090 RETURN 
65000 END

0010 REM "IVR - Inventory Vendor Listing (Input Overlay)"
0020 REM "Program IVR.IA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=10
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="IVM-01",FILES$[2]="IVM-02",FILES$[3]="IVM-03"
0130 LET FILES$[4]="IVM-05",FILES$[5]="IVM-10",FILES$[6]="IVM-12"
0135 LET FILES$[7]="IVM-14",FILES$[8]="SYS-01",FILES$[9]="IVM-15"
0140 LET FILES$[10]="APM-01"
0160 CALL "SYC.DA",1,1,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET IVM01_DEV=CHANNELS[1],IVM02_DEV=CHANNELS[2],IVM03_DEV=CHANNELS[3]
0185 LET IVM05_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],IVM12_DEV=CHANNELS[6]
0190 LET IVM14_DEV=CHANNELS[7],SYS01_DEV=CHANNELS[8],IVM15_DEV=CHANNELS[9]
0200 REM " --- IOLists
0240 APM01A: IOLIST X0$,X1$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 IVS01C: IOLIST X$,ABCP[ALL],ABCF[ALL]
0290 APS01A: IOLIST X$,P5$,P6$,X$,X$,M9$
0300 GLS01A: IOLIST G0$,G1$,G2$,G3$,G4$
0400 REM " --- Parameters"
0405 DIM P[6],G[1],U[5],DESC[3],ABCP[25],ABCF[25],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0435 FIND (SYS01_DEV,KEY=N0$+N2$+"02",DOM=9800)IOL=IVS01C
0440 CALL "SYC.VA","AP",INFO$[ALL]
0445 IF INFO$[20]<>"Y" THEN GOTO 4900
0460 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0465 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0470 LET VENDLEN=NUM(P6$(1,2)),G[0]=NUM(G2$(1,2)),P[0]=NUM(P2$(1,2))
0480 LET P[1]=NUM(P2$(5,1)),P[3]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2))
0490 LET P[5]=NUM(P2$(13,2))
0500 REM " --- Init Data"
0510 DIM CHANNS[15],PARMS$[4],OPTN$[7]
0550 LET CHANNS[0]=SYS01_DEV,CHANNS[1]=IVM01_DEV,CHANNS[10]=IVM10_DEV
0560 LET CHANNS[14]=IVM14_DEV,CHANNS[15]=IVM15_DEV,PARMS$[0]=N0$
0570 LET PARMS$[1]=P1$,PARMS$[2]=P2$,PARMS$[3]=P3$,PARMS$[4]=P4$
0600 REM " --- Additional Opens"
0610 CALL "SYC.DA",1,10,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS THEN GOTO 9900
0630 LET APM01_DEV=CHANNELS[10]
0700 REM " --- Background"
0710 PRINT 'SB',@(22,3),"Beginning Vendor:",
0720 CALL "SYC.WC",1,0,80,0,0,4,0
0730 PRINT 'SB',@(25,5),"Ending Vendor:",
0740 CALL "SYC.WC",1,0,80,0,0,6,0
0750 CALL "IVC.IB","BL","DW",7,CHANNS[ALL],PARMS$[ALL],OPTN$[ALL],V3
0760 GOSUB 5000
1000 REM " --- Beginning Vendor"
1010 LET V0$="M",V1$="KCE",V2$=BEG_VEND$,V3$=M9$,V4$="Enter A Valid Vendor Number (<F1>=First/<F3>=Lookup)",V0=6,V1=40,V2=3,I0=0
1020 GOSUB 7000
1030 ON V3 GOTO 1060,1040,1090,1050,3900
1040 LET BEG_VEND$=""; PRINT @(V1,V2),'CL',@(V1,V2),"First",; GOTO 1090
1050 CALL "SYC.LV",V$
1055 IF V$="" THEN GOTO 1000
1060 IF V$="" THEN GOTO 1040
1065 FIND (APM01_DEV,KEY=N0$+V$,DOM=1000)IOL=APM01A
1070 LET BEG_VEND$=V$
1080 PRINT @(40,3),FNF$(BEG_VEND$,M9$)," ",X1$(1,MIN(79-V1-M9-1,30)),'CL',
1090 GOTO 3900
1100 REM " --- Ending Vendor"
1110 LET V0$="M",V1$="KCE",V2$=END_VEND$,V3$=M9$,V4$="Enter A Valid Vendor Number (<F1>=Last/<F3>=Lookup)",V0=6,V1=40,V2=5
1120 GOSUB 7000
1130 ON V3 GOTO 1160,1140,1190,1150,3900
1140 LET END_VEND$=""; PRINT @(V1,V2),'CL',@(V1,V2),"Last",; GOTO 1190
1150 CALL "SYC.LV",V$
1155 IF V$="" THEN GOTO 1100
1160 IF V$="" THEN GOTO 1140
1165 IF BEG_VEND$<>"" THEN IF V$<BEG_VEND$ THEN GOTO 1100
1170 FIND (APM01_DEV,KEY=N0$+V$,DOM=1100)IOL=APM01A
1180 LET END_VEND$=V$
1185 PRINT @(40,5),FNF$(END_VEND$,M9$)," ",X1$(1,MIN(79-V1-M9-1,30)),'CL',
1190 GOTO 3900
1200 REM " --- Vendor/Product Info"
1220 CALL "IVC.IB","IL","DW",7,CHANNS[ALL],PARMS$[ALL],OPTN$[ALL],V3
1230 IF V3=2 THEN GOTO 1290
1240 LET BEG_PROD$=OPTN$[3],END_PROD$=OPTN$[4],ITEM_TYPE$=OPTN$[7]
1250 LET BEG_ITEM$=OPTN$[5],END_ITEM$=OPTN$[6]
1290 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,1200,3990
3990 GOSUB 5000
4000 REM " --- Correct"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4030 ON POS(V$="YN") GOTO 4000,4090,1000
4090 RUN "IVR.IB"
4900 REM " --- AP Not Installed"
4910 CALL "IVC.SB","A",STATUS
4990 GOTO 9900
5000 REM " --- Display screen"
5010 IF BEG_VEND$="" THEN PRINT @(40,3),"First",'CL',
5020 FIND (APM01_DEV,KEY=N0$+BEG_VEND$,DOM=5040)IOL=APM01A
5030 PRINT @(40,3),FNF$(BEG_VEND$,M9$)," ",X1$(1,MIN(39-M9-1,30)),'CL',
5040 IF END_VEND$="" THEN PRINT @(40,5),"Last",'CL',
5050 FIND (APM01_DEV,KEY=N0$+END_VEND$,DOM=5070)IOL=APM01A
5060 PRINT @(40,5),FNF$(BEG_VEND$,M9$)," ",X1$(1,MIN(39-M9-1,30)),'CL',
5070 CALL "IVC.IB","DL","DW",7,CHANNS[ALL],PARMS$[ALL],OPTN$[ALL],FKEY
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "DDM - Record Layout Maintenance (Copy Record)"
0020 REM "Program DDM.BF"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 DDM01A: IOLIST X0$(1),X1$(1),X[ALL]
0230 DDM03A: IOLIST B0$(1),B1$(1),B[ALL]
0240 DDM04A: IOLIST C0$(1),C1$(1),C[ALL]
0250 DDM06A: IOLIST XREF$
0260 DDM12A: IOLIST D0$,D1$
0270 DDM13A: IOLIST D0$,D1$
0280 DDM14A: IOLIST D0$,D1$
0300 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0500 REM " --- Initializations
0510 LET NUMBER=1
0520 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
0550 LET BEGFILE$="",ENDFILE$="",BEGREC$="",ENDREC$="",I0=0
0700 REM " --- Background
0750 PRINT @(0,L0-1),'CE','SB',@(30,L0),"From File",@(30,L0+1),"Record ID",@(32,L0+2),"To File",@(30,L0+3),"Record ID",'SF',
1000 REM " --- From File
1010 FIND (SYM03_DEV,KEY=A0$,DOM=4900)IOL=SYM03A
1020 PRINT @(40,L0),A0$," ",A1$(1,30)
1030 LET BEGFILE$=A0$
1200 REM " --- From Record ID
1210 LET V0$="S",V1$="C",V2$=BEGREC$,V3$="",V0=1,V1=40,V2=L0+1,I0=0
1220 LET V4$="Enter the Record ID of the record to be copied (<F1>=All/<F3>=Lookup)"
1230 GOSUB 7000
1240 ON V3 GOTO 1300,1320,3900,1250,4900
1250 GOSUB 6100
1260 IF V$="" THEN GOTO 1200
1300 REM 
1310 IF V$<>"" THEN GOTO 1350
1320 LET BEGREC$=""
1330 PRINT @(V1,V2),"All",'CL',
1340 GOTO 3900
1350 LET B0$(1)=A0$,B0$(7)=V$
1360 FIND (DDM03_DEV,KEY=B0$,DOM=1200)IOL=DDM03A
1370 LET BEGREC$=B0$(7)
1380 PRINT @(V1,V2),B0$(7),"      ",B1$(1,30)
1390 GOTO 3900
1400 REM " --- To File
1410 LET V0$="S",V1$="C^",V2$=ENDFILE$,V3$="",V0=6,V1=40,V2=L0+2
1420 LET V4$="Enter the name of the file to copy record layouts to (<F1>=Same/<F3>=Lookup)"
1430 GOSUB 7000
1435 IF V3=1 THEN LET ENDFILE$=BEGFILE$
1440 ON V3 GOTO 1460,1460,1400,1450,4900
1450 GOSUB 6000
1460 LET A0$(1)=V$,A1$(1)=""
1465 FIND (SYM03_DEV,KEY=A0$,DOM=1470)IOL=SYM03A
1470 PRINT @(V1,V2),A0$," ",A1$(1,30)
1480 LET ENDFILE$=A0$
1490 GOTO 3900
1600 REM " --- To Record ID
1610 IF ENDREC$="" THEN LET ENDREC$=BEGREC$
1615 LET V0$="S",V1$="C^",V2$=ENDREC$,V3$="",V0=1,V1=40,V2=L0+3
1620 LET V4$="Enter the Record ID for the record to be copied to (<F3>=Lookup)"
1625 IF BEGREC$="" THEN GOTO 1720
1630 GOSUB 7000
1640 ON V3 GOTO 1700,1700,3900,1650,4900
1650 GOSUB 6100
1700 REM 
1710 IF V$="" THEN GOTO 1600
1715 GOTO 1750
1720 LET ENDREC$=""
1730 PRINT @(V1,V2),"All",'CL',
1740 GOTO 3900
1750 LET B0$(1)=A0$,B0$(7)=V$,B1$(1)=""
1760 FIND (DDM03_DEV,KEY=B0$,DOM=1770)IOL=DDM03A
1770 LET ENDREC$=B0$(7)
1780 PRINT @(V1,V2),B0$(7),"      ",B1$(1,30)
1790 GOTO 3900
3900 REM " --- Next field
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1200,1400,1600,3990
3990 LET I0=0
4000 REM " --- Correct?
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="S",V1$="CR",V2$="Y",V3$="",V0=1,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Copy SYM-03 File Header
4110 PRINT @(0,22),'CL',@(30),"Updating",
4120 LET A0$(1)=BEGFILE$,FILENAME$=A0$
4130 FIND (SYM03_DEV,KEY=A0$,DOM=4900)IOL=SYM03A
4140 LET A0$(1)=ENDFILE$,A1$(31,6)=REVISION$
4150 PRINT @(40,22),A0$,'CL',
4160 WRITE (SYM03_DEV,KEY=A0$,DOM=4170)IOL=SYM03A
4170 GOSUB 6500
4180 LET B0$(1)=BEGFILE$,B0$(7)=BEGREC$,K$=B0$,ID$=BEGREC$
4190 READ (DDM03_DEV,KEY=B0$,DOM=4200)
4195 GOTO 4240
4200 REM " --- Read next DDM-03 Record ID
4210 LET K$=KEY(DDM03_DEV,END=4800)
4220 IF K$(1,6)<>FILENAME$ THEN GOTO 4800
4230 IF ID$<>"" THEN IF K$(7,1)<>ID$ THEN GOTO 4800
4240 READ (DDM03_DEV,KEY=K$)IOL=DDM03A
4250 LET B0$(1,6)=ENDFILE$,B1$(31,6)=REVISION$
4255 IF ENDREC$<>"" THEN LET B0$(7)=ENDREC$
4260 WRITE (DDM03_DEV,KEY=B0$)IOL=DDM03A
4270 READ (DDM03_DEV,KEY=K$)
4280 LET C0$(1)=K$,D0$(1)=K$
4290 READ (DDM04_DEV,KEY=C0$,DOM=4300)
4300 REM " --- Read next DDM-04 Field Record
4310 LET K$=KEY(DDM04_DEV,END=4400)
4320 IF K$(1,6)<>FILENAME$ THEN GOTO 4400
4325 IF ID$<>"" THEN IF K$(7,1)<>ID$ THEN GOTO 4400
4330 READ (DDM04_DEV,KEY=K$)IOL=DDM04A
4340 LET C0$(1,6)=ENDFILE$,C1$(47,6)=REVISION$
4345 IF ENDREC$<>"" THEN LET C0$(7,1)=ENDREC$
4350 PRINT @(49,22),C0$(8),
4360 WRITE (DDM04_DEV,KEY=C0$)IOL=DDM04A
4370 GOSUB 6300
4380 READ (DDM04_DEV,KEY=K$)
4390 GOTO 4300
4400 REM " --- Copy Record Comments
4410 READ (DDM13_DEV,KEY=D0$,DOM=4420)
4420 LET K$=KEY(DDM13_DEV,END=4500)
4430 IF K$(1,6)<>FILENAME$ THEN GOTO 4500
4435 IF ID$<>"" THEN IF K$(7,1)<>ID$ THEN GOTO 4500
4440 READ (DDM13_DEV,KEY=K$)IOL=DDM13A
4450 LET D0$(1,6)=ENDFILE$
4455 IF ENDREC$<>"" THEN LET D0$(7,1)=ENDREC$
4460 PRINT @(49,22),D0$(8),
4470 WRITE (DDM13_DEV,KEY=D0$)IOL=DDM13A
4480 READ (DDM13_DEV,KEY=K$)
4490 GOTO 4420
4500 REM " --- Copy Data Field Comments
4510 READ (DDM14_DEV,KEY=D0$,DOM=4520)
4520 LET K$=KEY(DDM14_DEV,END=4600)
4530 IF K$(1,6)<>FILENAME$ THEN GOTO 4600
4535 IF ID$<>"" THEN IF K$(7,1)<>ID$ THEN GOTO 4600
4540 READ (DDM14_DEV,KEY=K$)IOL=DDM14A
4550 LET D0$(1,6)=ENDFILE$
4555 IF ENDREC$<>"" THEN LET D0$(7,1)=ENDREC$
4560 PRINT @(49,22),D0$(8),
4570 WRITE (DDM14_DEV,KEY=D0$)IOL=DDM14A
4580 READ (DDM14_DEV,KEY=K$)
4590 GOTO 4520
4600 REM " --- Loop back for next DDM-03 Record ID
4620 GOTO 4200
4800 REM " --- Update revision dates
4810 LET C0$(1,6)=ENDFILE$
4850 GOSUB 6800
4900 REM " --- Run detail overlay
4910 PRINT @(0,L0-1),'CE',
4990 RUN "SYM.FA"
6000 REM " --- File Name Lookup
6010 LET CHANNEL=SYM03_DEV,RECORD$="",TITLE$="ADD+ON System Files"
6020 LET DESCRIPTION$[0]="File",V$="NO COMPANY"
6030 LET FIELD[0]=1,POSITION[0]=1,LENGTH[0]=6
6090 GOTO 6200
6100 REM " --- Record ID Lookup
6110 LET CHANNEL=DDM03_DEV,RECORD$=A0$,TITLE$=A0$+" Record ID's"
6120 LET DESCRIPTION$[0]="ID",V$="NO COMPANY & SKIP"
6130 LET FIELD[0]=1,POSITION[0]=7,LENGTH[0]=1
6200 REM " --- Call windowed lookup
6210 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,ROW=3
6220 LET DESCRIPTION$[1]="Description"
6230 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6280 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6290 RETURN 
6300 REM " --- Write new field cross-reference
6310 LET X0$(1)=C1$(1),X1$(1)=""
6320 LET X[0]=0
6330 FIND (DDM01_DEV,KEY=X0$,DOM=6340)IOL=DDM01A
6340 LET LENGTH=X[0]
6350 IF C[1]>0 THEN LET LENGTH=LENGTH*C[1]
6355 IF FNP$(C1$(17,30))<>"" THEN LET X1$(1,30)=C1$(17,30)
6360 LET XREF$(1)=X0$(1,12)+X1$(1,30)+X1$(37,1)+STR(LENGTH:"0000")+C0$
6370 WRITE (DDM06_DEV,KEY=XREF$)IOL=DDM06A
6390 RETURN 
6500 REM " --- Copy File Comments
6510 READ (DDM12_DEV,KEY=FILENAME$,DOM=6520)
6520 LET DDM12_K$=KEY(DDM12_DEV,END=6590)
6530 IF POS(FILENAME$=DDM12_K$)<>1 THEN GOTO 6590
6540 FIND (DDM12_DEV,KEY=DDM12_K$)IOL=DDM12A
6550 LET D0$(1,6)=A0$
6560 WRITE (DDM12_DEV,KEY=D0$)IOL=DDM12A
6570 READ (DDM12_DEV,KEY=DDM12_K$)
6580 GOTO 6520
6590 RETURN 
6800 REM " --- Update Last Revision Date on Header/Record ID Records
6810 LET A0$(1)=C0$(1,6),B0$(1)=C0$(1,7)
6820 FIND (SYM03_DEV,KEY=A0$)IOL=SYM03A
6830 FIND (DDM03_DEV,KEY=B0$)IOL=DDM03A
6840 LET A1$(31,6)=REVISION$,B1$(31,6)=REVISION$
6850 WRITE (SYM03_DEV,KEY=A0$)IOL=SYM03A
6860 WRITE (DDM03_DEV,KEY=B0$)IOL=DDM03A
6890 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8090 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "APR - Purchase Journal"
0020 REM "Program APR.OB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 APM01A: IOLIST A0$(1),A1$(1)
0220 APM03A: IOLIST B0$(1),B1$(1)
0230 APT02A: IOLIST C0$(1),C[0],C[1]
0240 GLM01A: IOLIST D0$(1),D1$(1)
0250 APM10A: IOLIST H0$(1)
0280 APE05A: IOLIST R0$(1),R1$(1)
0300 GLW04A: IOLIST W0$(1),W[0]
0310 APW04A: IOLIST K$
0320 APW14A: IOLIST K$
0400 REM " --- Parameters"
0450 LET M0=LEN(M0$),M1=LEN(M1$),G5=LEN(G5$),HEADINGS=4
0460 IF P3$(1,1)<>"Y" THEN LET HEADINGS=3
0500 REM " --- Initializations"
0510 DIM A0$(8),A1$(30),B0$(14),B1$(30),C0$(34),C[1],O[6],J$(M1,"-"),J1$(40)
0520 DIM HEADINGS$[HEADINGS],W0$(2+G[4]),W[1]
0545 LET HEADINGS$[0]=N1$,HEADINGS$[2]=H3$,NF$="(Not On File)"
0550 IF SEQUENCE$="D" THEN LET HEADINGS$[1]=N3$+" By Distribution Account"
0555 IF SEQUENCE$="N" THEN LET HEADINGS$[1]=N3$+" By Vendor"
0560 IF SEQUENCE$="A" THEN LET HEADINGS$[1]=N3$+" By Alternate Sequence"
0565 LET WHEN$=F0$(7,3),CLOCK$="",PAGE=0,WIDTH=132,L9=59,L=L9+1
0570 LET VM=MAX(M0,8),GM=MAX(G5,9),DONE=0,NUMBER=0,ALTSEQ$=""
0575 LET T0$="",T1$="",T2$="",T3$="",AH$="Account",DH$="Description"
0580 IF GL$="Y" THEN GOTO 0600
0585 LET AH$="",DH$="",GM=0,G5=0,WIDTH=80
0600 REM " --- Initialize Print Positions"
0610 LET O[0]=0,O[6]=WIDTH-M1
0620 IF SEQUENCE$="N" THEN GOTO 0650
0625 IF SEQUENCE$="A" THEN GOTO 0670
0630 LET O[1]=O[0]+GM+1,O[2]=O[1]+36,O[3]=O[2]+VM,W=G5+3
0635 LET O[5]=O[6]-10,O[4]=O[5]-14,DW=MIN(30,O[4]-O[3]-1)
0640 GOTO 0700
0650 LET O[1]=O[0]+VM,O[2]=O[1]+31,O[3]=O[2]+14,W=MAX(3+M0,G5)
0655 LET O[4]=O[3]+9,O[5]=O[4]+GM+1,DW=MIN(35,O[6]-O[5]-1)
0660 GOTO 0700
0670 LET O[1]=11,O[2]=O[1]+VM+31,O[3]=O[2]+14,W=13
0680 LET O[4]=O[3]+9,O[5]=O[4]+GM+1,DW=MIN(35,O[6]-O[5]-1)
0800 REM " --- Sort if printing in distribution sequence"
0810 IF SEQUENCE$<>"N" THEN GOSUB 5500
0900 REM " --- Position File"
0910 CALL "SYC.NB","Printing",W,COLUMN
0980 READ (APW04_DEV,KEY=N0$,DOM=0981)
0985 READ (APW14_DEV,KEY=N0$,DOM=0986)
0990 READ (APT02_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Get next APW-04 key"
1010 IF SEQUENCE$="N" THEN GOTO 1100
1015 IF SEQUENCE$="D" THEN GOTO 1060
1020 LET K$=KEY(APW14_DEV,END=4000)
1030 READ (APW14_DEV)
1040 LET K$=K$(1,4)+K$(15)
1050 GOTO 1200
1060 LET K$=KEY(APW04_DEV,END=4000)
1070 READ (APW04_DEV)
1080 LET K$=K$(1,4)+K$(5+G[4],17)+K$(5,G[4])+K$(22+G[4])
1090 GOTO 1200
1100 REM " --- Get next APT-02 key"
1110 LET K$=KEY(APT02_DEV,END=4000)
1200 REM " --- Read next APT-02 record"
1210 IF POS(N0$=K$)<>1 THEN GOTO 4000
1220 IF K$(22+G[4],3)<BEGDATE$ THEN GOTO 3800
1230 IF K$(22+G[4],3)>ENDDATE$ THEN GOTO 3800
1240 READ (APT02_DEV,KEY=K$)IOL=APT02A
1300 REM " --- Level breaks"
1310 IF T0$<>C0$(3,2) THEN GOSUB 6000
1320 IF T1$<>C0$(22,G[4]) THEN GOSUB 6200
1330 IF T2$<>C0$(5,6) THEN GOSUB 6400
1340 IF T3$<>C0$(11,11) THEN GOSUB 6600
1500 REM " --- Additional reads"
1510 IF SEQUENCE$<>"D" THEN GOSUB 5400
2000 REM " --- Print initializations"
2010 IF L+1>L9 THEN GOSUB 5000
2090 IF SEQUENCE$<>"D" THEN GOTO 2200
2100 REM " --- Distribution detail line"
2110 IF ACCOUNT$="" AND VENDOR$="" THEN GOTO 2140
2120 PRINT (7)""
2130 LET L=L+1
2140 IF NAME$<>"" THEN LET NAME$=NAME$(1,DW)
2145 IF L+1>L9 THEN GOSUB 5000
2150 PRINT (7)@(O[0]),ACCOUNT$,@(O[1]),DESCRIPTION$,@(O[2]),VENDOR$,@(O[3]),NAME$,@(O[4]),INVOICE$,@(O[5]),INVDATE$,@(O[6]),C[0]:M1$
2190 GOTO 3000
2200 REM " --- Vendor detail line"
2210 IF VENDOR$="" THEN GOTO 2240
2220 PRINT (7)""
2230 LET L=L+1
2240 IF DESCRIPTION$<>"" THEN LET DESCRIPTION$=DESCRIPTION$(1,DW)
2245 IF SEQUENCE$="A" THEN LET NAME$=VENDOR$+" "+NAME$,VENDOR$=ALTSEQ$
2247 IF L+1>L9 THEN GOSUB 5000
2250 PRINT (7)@(O[0]),VENDOR$,@(O[1]),NAME$,@(O[2]),INVOICE$,@(O[3]),INVDATE$,@(O[4]),ACCOUNT$,@(O[5]),DESCRIPTION$,@(O[6]),C[0]:M1$
3000 REM " --- Generate G/L Recap"
3010 IF GL$<>"Y" THEN GOTO 3100
3020 LET ACCOUNT$=D0$(3,G[4]),AMOUNT=C[0]
3030 GOSUB 6900
3100 REM " --- Accumulate totals"
3110 LET ATOTAL=ATOTAL+C[0],GTOTAL=GTOTAL+C[0],VTOTAL=VTOTAL+C[0]
3120 LET RTOTAL=RTOTAL+C[0],L=L+1
3130 LET ALTSEQ$="",VENDOR$="",NAME$="",ACCOUNT$="",DESCRIPTION$=""
3190 GOTO 3900
3800 REM " --- Skip record"
3810 READ (APT02_DEV)
3900 REM " --- Loop back for next APT-02 record"
3990 GOTO 1000
4000 REM " --- All Done"
4020 LET DONE=1
4030 GOSUB 6000
4070 LET TOTAL$="Total For Report",TOTAL=RTOTAL
4080 GOSUB 6800
4100 REM " --- Run next overlay"
4110 IF GL$<>"Y" THEN GOTO 9900
4180 PRINT @(COLUMN,11),J1$(1,W),
4190 RUN "GLR.XB"
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 IF SEQUENCE$<>"D" THEN GOTO 5055
5045 PRINT (7)@(O[0]),AH$,@(O[1]),DH$,@(O[2]),"Vendor",@(O[3]),"Name",@(O[4]),"Invoice/Check",@(O[5]+2),"Date",@(O[6]+M1-7),"Amount"; LET L=L+1
5050 GOTO 5090
5055 IF SEQUENCE$<>"N" THEN GOTO 5080
5060 PRINT (7)@(O[0]),"Vendor",@(O[1]),"Name",@(O[2]),"Invoice/Check",@(O[3]+2),"Date",@(O[4]),AH$,@(O[5]),DH$,@(O[6]+M1-7),"Amount"; LET L=L+1
5070 GOTO 5090
5080 PRINT (7)@(O[0]),"Alt Seq",@(O[1]),"Vendor",@(O[2]),"Invoice/Check",@(O[3]+2),"Date",@(O[4]),AH$,@(O[5]),DH$,@(O[6]+M1-7),"Amount"; LET L=L+1
5090 LET L=L+1
5095 RETURN 
5400 REM " --- Retrieve G/L Account"
5405 IF GL$<>"Y" THEN GOTO 5460
5410 DIM D0$(2+G[4]),D1$(35),DESCRIPTION$(35)
5420 LET D1$(1)=NF$,ACCOUNT$=FNF$(C0$(22,G[3]),G5$)
5430 FIND (GLM01_DEV,KEY=N0$+C0$(22,G[4]),DOM=5440)IOL=GLM01A
5440 LET DESCRIPTION$(1)=D1$
5450 GOTO 5490
5460 LET ACCOUNT$="",DESCRIPTION$=""
5490 RETURN 
5500 REM " --- Sort APT-02 by distribution account"
5510 CALL "SYC.NB","Sorting",3+M0,COLUMN
5590 READ (APT02_DEV,KEY=N0$,DOM=5600)
5600 REM " --- Read next APT-02 record"
5605 DIM VENDOR$(M0)
5610 LET K$=KEY(APT02_DEV,END=5900)
5620 IF POS(N0$=K$)<>1 THEN GOTO 5900
5630 READ (APT02_DEV)
5640 IF K$(22+G[4])<BEGDATE$ THEN GOTO 5600
5650 IF K$(22+G[4])>ENDDATE$ THEN GOTO 5600
5660 LET X$="",VENDOR$(1)=K$(5,6)
5665 IF P3$(1,1)="Y" THEN LET X$=C0$(3,2)+" "
5670 IF VENDOR$(1,4)<>"TEMP" THEN LET VENDOR$(1)=FNF$(VENDOR$(1,P[0]),M0$)
5675 PRINT @(COLUMN,11),X$,VENDOR$,
5700 REM " --- By distribution account"
5710 IF SEQUENCE$="A" THEN GOTO 5750
5720 LET K$=K$(1,4)+K$(22,G[4])+K$(5,17)+K$(22+G[4])
5730 WRITE (APW04_DEV,KEY=K$)IOL=APW04A
5740 GOTO 5600
5750 REM " --- By alternate sequence"
5760 DIM A0$(8),A1$(196)
5765 LET A0$(1)=K$(1,2)+K$(5,6)
5770 FIND (APM01_DEV,KEY=A0$,DOM=5780)IOL=APM01A
5780 LET K$=K$(1,4)+A1$(146,10)+K$(5)
5785 WRITE (APW14_DEV,KEY=K$)IOL=APW14A
5790 GOTO 5600
5900 REM " --- Sort completed"
5910 PRINT @(0,10),'CL',@(0,11),'CL',@(0,12),'CL',
5920 LET VENDOR$="",ALTSEQ$=""
5990 RETURN 
6000 REM " --- AP Type Break"
6010 IF T0$="" THEN GOTO 6100
6020 IF SEQUENCE$="D" THEN GOSUB 6200 ELSE GOSUB 6400
6040 IF P3$(1,1)<>"Y" THEN GOTO 6190
6050 LET TOTAL$="Total For "+H4$,TOTAL=ATOTAL
6060 GOSUB 6800
6100 REM 
6105 IF P3$(1,1)<>"Y" THEN GOTO 6190
6110 IF DONE>0 THEN GOTO 6190
6120 DIM H0$(32)
6130 LET ATOTAL=0,L=L9+1,T0$=C0$(3,2),H0$(6)=NF$
6140 FIND (APM10_DEV,KEY=N0$+"A"+T0$,DOM=6150)IOL=APM10A
6150 LET H4$="AP Type "+T0$+" "+FNP$(H0$(6,20)),HEADINGS$[3]=H4$
6190 RETURN 
6200 REM " --- G/L Account Break"
6210 IF SEQUENCE$<>"D" THEN GOTO 6390
6220 IF T1$="" THEN GOTO 6300
6240 GOSUB 6400
6250 LET TOTAL$="Total For Account "+FNF$(T1$(1,G[3]),G5$),TOTAL=GTOTAL
6260 GOSUB 6800
6300 REM 
6310 IF DONE>0 THEN GOTO 6390
6320 LET T1$=C0$(22,G[4]),GTOTAL=0,X$=""
6330 IF P3$(1,1)="Y" THEN LET X$=C0$(3,2)+" "
6340 GOSUB 5400
6350 PRINT @(COLUMN,11),X$,FNF$(T1$(1,G[3]),G5$),
6390 RETURN 
6400 REM " --- Vendor Break"
6410 IF T2$="" THEN GOTO 6500
6420 GOSUB 6600
6430 LET Z$=T2$(1,4)
6435 IF Z$<>"TEMP" THEN LET Z$=FNF$(T2$(1,P[0]),M0$)
6440 LET TOTAL$="Total For Vendor "+Z$,TOTAL=VTOTAL
6450 GOSUB 6800
6500 REM 
6510 IF DONE>0 THEN GOTO 6590
6520 LET T2$=C0$(5,6),X$="",VTOTAL=0
6530 IF P3$(1,1)="Y" THEN LET X$=C0$(3,2)
6540 IF T2$(1,4)="TEMP" THEN GOTO 6580
6550 DIM A0$(8),A1$(196),NAME$(30)
6560 LET VENDOR$="",A0$(1)=N0$+T2$,A1$(1)=NF$,B1$(1)=NF$
6565 FIND (APM01_DEV,KEY=A0$,DOM=6570)IOL=APM01A
6570 LET VENDOR$=FNF$(T2$(1,P[0]),M0$),NAME$(1)=A1$(1,30),ALTSEQ$=A1$(146,10)
6580 IF SEQUENCE$="N" THEN PRINT @(COLUMN,11),X$,VENDOR$,
6585 IF SEQUENCE$="A" THEN PRINT @(COLUMN,11),X$,ALTSEQ$,
6590 RETURN 
6600 REM " --- Invoice/Check Number Break"
6610 IF DONE>0 THEN GOTO 6790
6615 LET INVOICE$=C0$(12,10),INVDATE$=FNB$(C0$(22+G[4],3)),T3$=C0$(11,11)
6620 IF T3$(1,1)="I" THEN GOTO 6700
6630 LET INVOICE$="MC "+C0$(12,7)
6635 IF C0$(5,4)<>"TEMP" THEN GOTO 6790
6640 DIM R0$(33),R1$(30),NAME$(30),ALTSEQ$(10)
6645 LET Z$=C0$(1,4)+C0$(12,7)+C0$(5,6),R1$(1)=NF$
6650 READ (APE05_DEV,KEY=Z$,DOM=6660)
6660 LET W$=KEY(APE05_DEV,END=6680)
6665 IF POS(W$(1,17)=Z$)<>1 THEN GOTO 6680
6670 READ (APE05_DEV)IOL=APE05A
6680 LET VENDOR$=C0$(5,6),NAME$(1)=R1$
6690 GOTO 6790
6700 REM 
6710 IF C0$(5,4)<>"TEMP" THEN GOTO 6790
6720 DIM B0$(14),B1$(30),NAME$(30),ALTSEQ$(10)
6730 LET B0$(1)=N0$+C0$(3,2)+C0$(12,10),B1$(1)=NF$
6740 FIND (APM03_DEV,KEY=B0$,DOM=6750)IOL=APM03A
6750 LET VENDOR$=C0$(5,6),NAME$(1)=B1$
6790 RETURN 
6800 REM " --- Print subtotals/totals"
6810 IF L+2>L9 THEN GOSUB 5000
6820 PRINT (7)@(O[6]),J$
6830 PRINT (7)@(O[6]-LEN(TOTAL$)),TOTAL$,@(O[6]),TOTAL:M1$
6840 LET L=L+2
6890 RETURN 
6900 REM " --- Update G/L Summary"
6910 IF GL$<>"Y" THEN GOTO 6990
6920 IF AMOUNT=0 THEN GOTO 6990
6930 DIM W0$(2+G[4]),W[1]
6950 LET W0$(1)=N0$+ACCOUNT$
6960 FIND (GLW04_DEV,KEY=W0$,DOM=6970)IOL=GLW04A
6970 LET W[0]=W[0]+AMOUNT
6980 WRITE (GLW04_DEV,KEY=W0$)IOL=GLW04A
6990 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return To Menu"
9950 RUN "SYS.AA"
9999 END

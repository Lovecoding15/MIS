0010 REM BLK.10 - AUTHORIZATION/ACTIVATION FORM 
0020 LET SYS01T=UNT; OPEN (SYS01T)"SYS-01"
0030 LET TERMINAL$="T"+FID(0)
0040 READ (SYS01T,KEY=TERMINAL$)*,F0$; CLOSE (SYS01T)
0050 call "templates.pgm::ARE03"
0060 call "templates.pgm::ARE13"
0070 call "templates.pgm::ARE73"
0080 call "templates.pgm::ARE83"
0090 call "templates.pgm::CSM03"
0100 call "templates.pgm::CSM04"
0110 call "templates.pgm::SNM01"
0115 call "templates.pgm::SNT01"
0120 LET TMPDIR$=STBL("AON")+"tmp/"
0130 LET ARE83CHAN=UNT; OPEN (ARE83CHAN)"ARE-83"
0140 LET ARE73CHAN=UNT; OPEN (ARE73CHAN)"ARE-73"
0150 LET ARE13CHAN=UNT; OPEN (ARE13CHAN)"ARE-13"
0160 LET ARE03CHAN=UNT; OPEN (ARE03CHAN)"ARE-03"
0170 LET SNM01CHAN=UNT; OPEN (SNM01CHAN)"SNM-01"
0175 LET SNT01CHAN=UNT; OPEN (SNT01CHAN)"SNT-01"
0180 LET CSM01CHAN=UNT; OPEN (CSM01CHAN)"CSM-01"
0190 LET CSM02CHAN=UNT; OPEN (CSM02CHAN)"CSM-02"
0200 LET CSM03CHAN=UNT; OPEN (CSM03CHAN)"CSM-03"
0210 LET CSM04CHAN=UNT; OPEN (CSM04CHAN)"CSM-04"
0220 GOSUB BUILD_PRINT_SORT
0230 IF RECORD_COUNT=0 THEN GOTO END_SORT
0240 GOSUB CREATE_OUTFILE
0250 LET TODAY$=DATE(0:"%Mz/%Dz/%Y")
0260 READ (E1,KEY="",DOM=READ_SORT)
0270 READ_SORT: 
0280 item$=fill(15),users=0
0290 LET K$=KEY(E1,END=END_SORT)
0300 READ RECORD (E1,KEY=K$)SORT_REC$
0310 LET ARE83KEY$=SORT_REC$(8,26)
0320 READ RECORD (ARE83CHAN,KEY=ARE83KEY$)ARE83$
0330 REM if serial# history has been updated, get info from SNM01
0340 IF ARE83.SNHIST_FLAG$="Y" or are83.action$<>"A" THEN GOTO FIND_SNM01
0350 LET PK$=ARE83$(1,20)
0360 READ (ARE03CHAN,KEY=PK$(1,17)+"000")ARE03$
0370 READ RECORD (ARE13CHAN,KEY=PK$)ARE13$
0380 READ RECORD (ARE73CHAN,KEY=PK$)ARE73$
0390 LET USERS=ARE73.USERS+are73.users2
0400 let equals_total_users=(cvs(are73.available1$,4)="T")
0410 LET SLTYPE$=ARE73.TYPE_OF_SALE$
0420 IF SLTYPE$<>"ADD" THEN LET ITEM$=ARE13.ITEM_NUMBER$
0425 if len(cvs(item$,3))=15 then GOTO ITM_OK
0430 REM ARE13 for addusers doesn't have item# we need-get info from previous line or SNM-01
0440 TRY_PREVLN: REM if adduser was for item created/updated on prev line, it either isn't in SNM-01 yet, or doesn't have the new rev
0450 LET K13$=PK$
0460 READ (ARE13CHAN,KEY=K13$(1,17),DOM=NEXT_ARE13)
0470 NEXT_ARE13: 
0480 LET K13$=KEY(ARE13CHAN,END=FIND_SNM01)
0490 IF K13$(1,17)<>PK$(1,17) OR K13$>=PK$ THEN GOTO FIND_SNM01
0500 READ RECORD (ARE13CHAN,KEY=K13$)ARE13$
0510 READ (ARE83CHAN,KEY=K13$,DOM=CHKSN)
0520 CHKSN: 
0530 READ (ARE83CHAN,END=NEXT_ARE13)K83$,ACTION$,SN83$
0540 IF POS(K13$=K83$)<>1 THEN GOTO NEXT_ARE13
0550 IF SN83$<>ARE83.SERIAL_NBR$ OR ACTION$(1,1)="D" THEN GOTO CHKSN
0560 READ RECORD (ARE73CHAN,KEY=K13$)ARE73$
0570 IF POS("SM"=ARE73.TYPE_OF_SALE$)=1 THEN GOTO CHKSN
0580 if cvs(item$,3)="" or are73.type_of_sale$<>"ADD" then if equals_total_users=0 then LET USERS=USERS+ARE73.USERS+are73.users2
0590 IF  len(cvs(ARE13.ITEM_NUMBER$,3))=15 and len(cvs(item$,3))<>15 then ITEM$=ARE13.ITEM_NUMBER$
0600 GOTO NEXT_ARE13
0610 FIND_SNM01: 
0620 READ RECORD (SNM01CHAN,KEY=ARE83.SERIAL_NBR$,dom=*next)SNM01$
0630 if len(cvs(item$,3))<>15 then LET ITEM$=SNM01.PRODUCT$+SNM01.PLATFORM$+SNM01.OS_LEVEL$+SNM01.PRODUCT_REV$+SNM01.LICENSE_TYPE$
0640 IF ARE83.SNHIST_FLAG$="Y" THEN users=snm01.users else if equals_total_users=0 then users=users+snm01.users 
0650 ITM_OK: 
0655 CSM04.DESCRIPTION$=" "
0660 READ RECORD (CSM04CHAN,KEY=F0$(16,2)+ITEM$(10,3),dom=*next)CSM04$
0670 LET RV$=CVS(CSM04.DESCRIPTION$,3),P=POS(" "=RV$)
0680 IF P=0 THEN LET P=LEN(RV$)
0690 LET PROD_TYPE$=ITEM$(13,3),REV_LEVEL$=CVS(RV$(1,P),2),REV_DESC$=RV$(P+1)
0695 CSM03.PORT_ID$=""
0700 READ RECORD (CSM03CHAN,KEY=F0$(16,2)+ITEM$(7,3),dom=*next)CSM03$
0710 LET PORT$=CSM03.PORT_ID$
0720 IF POS(ITEM$(1,3)="P4E DS4") THEN LET PORT$(5,1)="4"
0730 LET PORT$=CVS(PORT$,3),P=POS("."=REV_LEVEL$),TEMP$=REV_LEVEL$
0740 IF P THEN LET TEMP$=REV_LEVEL$(1,P-1)+REV_LEVEL$(P+1)
0750 LET PORT_REV$=CVS(PORT$+"."+TEMP$,2),prddesc$="",pltdesc$=""
0760 READ (CSM01CHAN,KEY=F0$(16,2)+ITEM$(1,3),dom=*next)*,PRDDESC$
0770 READ (CSM02CHAN,KEY=F0$(16,2)+ITEM$(4,3),dom=*next)*,PLTDESC$
0780 LET PRINT_DESC$=CVS(PRDDESC$,2)
0790 IF CSM04.key_or_licen$="L" THEN LET OSLINE$=CVS(REV_DESC$,2)+" Port ID "+PORT_REV$+"  "+cvs(pltdesc$,2) ELSE LET OSLINE$=CVS(pltdesc$,2),ITEM$=CVS(ITEM$,2)+"  "+PORT$
0800 IF SLTYPE$="ADD" THEN LET PRINT_DESC$="Add users - S/N "+CVS(ARE83.SERIAL_NBR$,2)
0810 IF LEN(PRINT_DESC$)>30 THEN LET PRINT_DESC$=PRINT_DESC$(1,30); if len(CVS(PRDDESC$,2))>20 then LET PRINT_DESC$=CVS(PRDDESC$,2)
0820 IF LEN(OSLINE$)>48 THEN LET OSLINE$=OSLINE$(1,48)
0830 PRINT 'SB',@(20,20),"SALES ORDER:           SERIAL NO: ",'SF',
0840 PRINT @(33,20),ARE83.ORDER_NUMBER$,@(54,20),ARE83.SERIAL_NBR$,
0850 LET LICTEXT$=""
0860 rem IF CSM04.key_or_licen$<>"L" THEN GOTO NOT_FLEX
0870 rem LET TMPSN$=CVS(ARE83.SERIAL_NBR$,2)
0880 dl$=",";rem call "temp_license.pgm",tmpsn$,lictext$
0885 lictext$=dl$+dl$+dl$+rev_level$+dl$+dl$+dl$+dl$+dl$+cvs(SN$,3)+dl$+fill(1)
0890 NOT_FLEX: 
0900 GOSUB WRITE_OUTREC
0910 GOTO READ_SORT
0920 END_SORT: 
0930 LET ENDCHAN=UNT
0940 IF E1 THEN CLOSE (E1); ERASE E1$,ERR=THE_END
0950 THE_END: 
0960 FOR ANYCHAN=1 TO ENDCHAN; CLOSE (ANYCHAN,ERR=NXTCHAN)
0970 NXTCHAN: NEXT ANYCHAN
0980 IF RECORD_COUNT=0 THEN GOTO NO_RECORDS
0990 IF CHOICE$="P" THEN RUN "BLK.12"
1000 IF CHOICE$="E" OR CHOICE$="F" THEN RUN "BLK.16"
1010 NO_RECORDS: 
1020 PRINT 'WINDOW'(5,5,70,8,"Key/License Form Printing"),'CS',"No serial numbers selected."; WAIT 4; PRINT 'POP'
1030 RUN "SYS.AA"
1040 BUILD_PRINT_SORT: 
1050 LET RECORD_COUNT=0
1060 LET E1$=TMPDIR$+"BLK"+FID(0)+".TMP"; ERASE E1$,ERR=MAKE_SORT
1070 MAKE_SORT: MKEYED E1$,[1:33],0,60
1080 LET E1=UNT; OPEN (E1)E1$
1090 LET PREVKEY$="",PREVSN$="",snlist$=""
1100 PRINT @(20,20),"SORTING SERIAL NUMBERS TO PRINT:",
1110 LET ARE03KEY$=""; READ (ARE03CHAN,KEY=F0$(16,2),DOM=READ_ARE03)
1120 READ_ARE03: 
1130 READ RECORD (ARE03CHAN,END=END_ARE03)ARE03$; IF ARE03.FIRM_ID$<>F0$(16,2) THEN GOTO END_ARE03
1140 IF START_ONUM$<>"" THEN LET SORTBY$=ARE03.ORDER_NUMBER$; IF ARE03.ORDER_NUMBER$<START_ONUM$ THEN GOTO READ_ARE03
1150 IF END_ONUM$<>"" AND ARE03.ORDER_NUMBER$>END_ONUM$ THEN GOTO READ_ARE03
1160 IF START_INUM$<>"" THEN LET SORTBY$=ARE03.AR_INV_NBR$; IF ARE03.AR_INV_NBR$<START_INUM$ THEN GOTO READ_ARE03
1170 IF END_INUM$<>"" AND ARE03.AR_INV_NBR$>END_INUM$ THEN GOTO READ_ARE03
1180 READ (ARE83CHAN,KEY=ARE03$(1,17),DOM=READ_ARE83)
1190 READ_ARE83: 
1200 READ RECORD (ARE83CHAN,END=READ_ARE03)ARE83$; IF ARE83$(1,17)<>ARE03$(1,17) THEN GOTO READ_ARE03
1210 IF ARE83.ACTION$="D" THEN GOTO READ_ARE83
1220 IF START_SNUM$<>"" AND CVS(ARE83.SERIAL_NBR$,2)<START_SNUM$ THEN GOTO READ_ARE83
1230 IF END_SNUM$<>"" AND CVS(ARE83.SERIAL_NBR$,2)>END_SNUM$ THEN GOTO READ_ARE83
1235 p=pos(are83.serial_nbr$=snlist$)
1240 if p then 
1245   prevkey$=snlist$(p+len(are83.serial_nbr$)),p1=pos($0a$=prevkey$)
1250   if p1 then prevkey$=prevkey$(1,p1-1)
1255   if cvs(are83.auth_code$,3)<>"" THEN REMOVE (E1,KEY=PREVKEY$) else goto read_are83
1260   snlist$(p,p1-1)=" "
1270 fi
1275 REM Avoid printing more than 1 form when more than 1 record for the same SN 
1280 LET PREVKEY$=SORTBY$+ARE83$(1,26),PREVSN$=ARE83.SERIAL_NBR$
1285 snlist$=snlist$+prevsn$+prevkey$+$0a$
1290 LET SORT_REC$=PREVKEY$
1300 WRITE RECORD (E1)SORT_REC$; LET RECORD_COUNT=RECORD_COUNT+1
1310 PRINT @(54,20),ARE83.SERIAL_NBR$,
1320 goto READ_ARE83
1330 END_ARE03: RETURN
1340 CREATE_OUTFILE: 
1350 LET OUTCHAN=UNT,OUTFILE$=TMPDIR$+FID(0)+"BLK10.txt"
1360 ERASE OUTFILE$,ERR=MAKE_OUTFILE
1370 MAKE_OUTFILE: STRING OUTFILE$
1380 OPEN (OUTCHAN)OUTFILE$
1390 LET DSBCHAN=UNT,DSBFILE$=TMPDIR$+FID(0)+"BLK10dsb.txt"
1400 ERASE DSBFILE$,ERR=MAKE_DSBFILE
1410 MAKE_DSBFILE: STRING DSBFILE$
1420 OPEN (DSBCHAN)DSBFILE$
1430 RETURN
1440 WRITE_OUTREC: 
1450 REM Add purchase order # and cust #
1460 LET PO$=CVS(ARE03$(49,10),3)
1470 LET COMMA_POS=POS(","=PO$); IF COMMA_POS THEN LET PO$(COMMA_POS,1)=" "; GOTO 1470
1480 LET LICTEXT$=LICTEXT$+","+CVS(PO$,32)+","+ARE83.CUSTOMER_NBR$
1490 LET COMMA_POS=POS(","=PRINT_DESC$); IF COMMA_POS THEN LET PRINT_DESC$(COMMA_POS,1)=" "; GOTO 1490
1500 LET COMMA_POS=POS(","=OSLINE$); IF COMMA_POS THEN LET OSLINE$(COMMA_POS,1)=" "; GOTO 1500
1510 if cvs(are83.auth_code$,3)="" then gosub get_last_auth
1520 LET OUTREC$=CVS(ARE83.SERIAL_NBR$,3)+","+CVS(ARE83.AUTH_CODE$,3)+","+STR(USERS)+","+ARE83.ORDER_NUMBER$+","+TODAY$+","+CVS(ITEM$,3)+","+CVS(PRINT_DESC$,35)+","+CVS(REV_LEVEL$,3)+","+CVS(OSLINE$,35)+LICTEXT$
1530 PRINT (OUTCHAN)OUTREC$
1540 PRINT (DSBCHAN)OUTREC$
1550 RETURN
1600 get_last_auth:
1610 read(snt01chan,key=are83.serial_nbr$,dom=*next)
1620 while 1
1630   readrecord(snt01chan,end=*break)snt01$
1640   if are83.serial_nbr$<>snt01.serial_nbr$ then break
1650   if snt01.action$="D" then continue
1660   if cvs(snt01.auth_code$,3)<>"" then are83.auth_code$=snt01.auth_code$
1670 wend
1690 return

0010 REM "APR - Cash Requirements Report (Report Overlay)"
0020 REM "Program APR.BC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "15May99 WGH: Report end date as Last instead of 99/99/99 (845-850)"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0210 APT01A: IOLIST A0$,A[ALL]
0220 APM01A: IOLIST B0$(1),B1$(1)
0230 APM03A: IOLIST C0$(1),C1$(1)
0240 APM10A: IOLIST D0$(1)
0310 APT11A: IOLIST W0$,W1$,W[ALL]
0500 REM " --- Initializations"
0510 DIM A[2],W[2],HEADINGS$[8],O[12]
0520 DIM T[4],Q[4],U[4],R[4],X[4],S[4],Y[4]
0530 LET L9=59,L=L9+1,WHEN$=F0$(7,3),CLOCK$="",WIDTH=132,PAGE=0
0535 LET NF$="(Not On File)",M5$="###-###-####",DONE=0
0540 LET H6$="All Activity Based On "
0545 IF OPTION$="I" THEN LET H6$=H6$+"Invoice Date"
0550 IF OPTION$="D" THEN LET H6$=H6$+"Due Date"
0555 IF OPTION$="A" THEN LET H6$=H6$+"Accounting Date"
0560 IF DETAIL$="D" THEN LET H2$="Detail "+N3$
0565 IF DETAIL$="S" THEN LET H2$="Summary "+N3$
0570 LET HEADINGS$[0]=N1$,HEADINGS$[2]=H6$,HEADINGS=6,O=1
0575 IF P3$(1,1)="Y" THEN LET HEADINGS=7,O=2
0580 CALL "SYC.RM",M1$,M1,9,13
0585 IF SEQUENCE$="N" THEN LET HEADINGS$[1]=H2$+" By Vendor Number"
0590 IF SEQUENCE$="A" THEN LET HEADINGS$[1]=H2$+" By Alternate Sequence"
0600 REM " --- Print positions"
0610 LET O[12]=WIDTH-M1,O[0]=0,O[1]=M0
0620 FOR X=11 TO 7 STEP -1
0630 LET O[X]=O[X+1]-M1
0640 NEXT X
0650 LET O[6]=O[7],O[5]=O[7],O[4]=O[7],O[3]=O[7],O[2]=O[7]
0660 IF DETAIL$<>"D" THEN GOTO 0700
0670 LET O[6]=O[7]-1,O[5]=O[6]-3,O[4]=O[5]-9,O[3]=O[4]-11,O[2]=O[3]-11
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",M0+3,COLUMN
0800 REM " --- Headings"
0810 DIM H1$(WIDTH),H2$(WIDTH),H3$(WIDTH),J1$(6*M1,"-")
0820 FOR X=0 TO 3
0830 LET P=O[8+X]+1+M1-9,H1$(P,8)=FNB$(H4$(3*X+1,3))
0840 LET H2$(P,7)="Through",H3$(P,8)=FNB$(H5$(X*3+1,3))
0845 NEXT X
0850 LET H3$(P,8)="Last"
0855 LET H3$(1,6)="Vendor",H3$(O[7]+1+M1-9,8)="Past Due"
0860 LET H3$(O[12]+1+M1-6,5)="Total"
0865 IF DETAIL$<>"D" THEN GOTO 0890
0870 LET H2$(O[4]+3,5)="Aging",H3$(O[2]+1,9)="Reference"
0880 LET H3$(O[3]+1,7)="Invoice",H3$(O[4]+3,4)="Date"
0885 LET H3$(O[5]+1,2)="PG",H3$(O[6]+1,1)="H"
0890 LET HEADINGS$[3+O]=H1$,HEADINGS$[4+O]=H2$,HEADINGS$[5+O]=H3$
0895 IF DETAIL$="S" THEN LET HEADINGS=HEADINGS+1
0900 REM " --- Position file"
0980 READ (APW02_DEV,KEY=N0$,DOM=0990)
0990 READ (APT01_DEV,KEY=N0$,DOM=0995)
0995 READ (APT11_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Get next APW-02 key"
1010 IF SEQUENCE$<>"A" THEN GOTO 1100
1020 DIM DISPLAY$(10)
1030 LET K$=KEY(APW02_DEV,END=4000)
1040 IF POS(N0$=K$)<>1 THEN GOTO 4000
1050 READ (APW02_DEV)
1060 LET DISPLAY$(1)=K$(5,10),K$=K$(1,4)+K$(15)
1090 GOTO 1200
1100 REM " --- Get next APT-01 key"
1110 DIM DISPLAY$(M0)
1120 LET K$=KEY(APT01_DEV,END=4000)
1130 IF POS(N0$=K$)<>1 THEN GOTO 4000
1140 LET DISPLAY$(1)=K$(5,6)
1150 IF K$(5,4)<>"TEMP" THEN LET DISPLAY$(1)=FNF$(K$(5,P[0]),M0$)
1200 REM " --- Read next APT-01 header"
1210 READ (APT01_DEV,KEY=K$)IOL=APT01A
1250 LET X$=""
1260 IF P3$(1,1)="Y" THEN LET X$=K$(3,2)+" "
1270 PRINT @(COLUMN,11),X$,DISPLAY$,
1300 REM " --- Level breaks"
1310 IF K$(3,2)<>T0$ THEN GOSUB 6000
1320 IF K$(5,6)<>T1$ THEN GOSUB 6200
1500 REM " --- Calculate invoice amount"
1510 READ (APT11_DEV,KEY=A0$(1,20),DOM=2000)
2000 REM " --- Next detail"
2010 LET K$=KEY(APT11_DEV,END=3000)
2020 IF POS(K$(1,20)=A0$)<>1 THEN GOTO 3000
2030 READ (APT11_DEV)IOL=APT11A
2100 REM " --- Accumulate invoice totals"
2120 IF W1$(1,1)=" " THEN GOTO 2190
2130 LET A[0]=A[0]+W[0],A[1]=A[1]+W[1],A[2]=A[2]+W[2]
2900 REM " --- Loop back for next detail"
2990 GOTO 2000
3000 REM " --- Zero?"
3010 IF A[0]=0 THEN GOTO 3900
3300 REM " --- Which aging date?"
3310 IF OPTION$="I" THEN LET X3$=A0$(29,3)
3320 IF OPTION$="A" THEN LET X3$=A0$(32,3)
3330 IF OPTION$="D" THEN LET X3$=A0$(35,3)
3340 IF OPTION$="D" THEN IF A0$(38,3)>=BEGDATE$ AND A0$(38,3)<>A0$(29,3) THEN LET X3$=A0$(38,3),A[0]=A[0]-A[1]
3400 REM " --- Age it"
3410 LET X0=POS(X3$<H4$,3)
3420 IF X0=0 THEN GOTO 3450
3430 LET X0=INT(X0/3)
3440 GOTO 3500
3450 LET X0=4
3500 REM " --- Print detail"
3510 IF DETAIL$<>"D" THEN GOTO 3800
3520 IF VENDOR$="" THEN GOTO 3600
3530 IF L+3>L9 THEN GOSUB 5000
3540 PRINT (7)""
3550 PRINT (7)@(O[0]),VENDOR$
3560 LET L=L+2,VENDOR$=""
3600 REM 
3610 IF L+1>L9 THEN GOSUB 5000
3620 PRINT (7)@(O[0]+2),PHONE$,@(O[2]),A0$(41,10),@(O[3]),A0$(11,10),@(O[4]),FNB$(X3$),@(O[5]),A0$(25,2),@(O[6]),A0$(67,1),@(O[7+X0]),A[0]:M1$,@(O[12]),A[0]:M1$
3630 LET PHONE$="",L=L+1
3800 REM " --- Accumulate totals"
3810 LET T[X0]=T[X0]+A[0],U[X0]=U[X0]+A[0],X[X0]=X[X0]+A[0]
3820 LET R[X0]=R[X0]+A[2],Q[X0]=Q[X0]+A[2],L1=L1+1
3900 REM " --- Next invoice header"
3990 GOTO 1000
4000 REM " --- All done"
4010 LET DONE=1
4020 GOSUB 6000
4030 FOR X=0 TO 4
4040 LET S[X]=U[X],Y[X]=Q[X]
4050 NEXT X
4060 LET TOTAL$="Total For Report",RETENTION$=P3$(3,1)
4070 GOSUB 6800
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5090 RETURN 
6000 REM " --- AP Type Break"
6010 IF P3$(1,1)<>"Y" THEN GOTO 6190
6020 IF T0$="" THEN GOTO 6100
6030 GOSUB 6200
6040 FOR X=0 TO 4
6050 LET S[X]=T[X],Y[X]=Q[X]
6060 NEXT X
6070 LET TOTAL$="Total For "+H0$,RETENTION$=P3$(3,1)
6080 GOSUB 6800
6100 REM 
6110 IF DONE>0 THEN GOTO 6190
6120 DIM T[4],Q[4],D0$(32)
6130 LET T0$=A0$(3,2),L=L9+1,D0$(6)=NF$
6140 FIND (APM10_DEV,KEY=N0$+"A"+T0$,DOM=6150)IOL=APM10A
6150 LET H0$="AP Type "+T0$+" "+FNP$(D0$(6,20)),HEADINGS$[3]=H0$
6190 RETURN 
6200 REM " --- Vendor Break"
6210 IF T1$="" THEN GOTO 6400
6220 IF L1=0 THEN GOTO 6400
6230 IF DETAIL$="S" THEN GOTO 6300
6240 FOR X=0 TO 4
6250 LET S[X]=X[X]
6260 NEXT X
6270 LET TOTAL$="Total For Vendor",RETENTION$="N"
6280 GOSUB 6800
6290 GOTO 6400
6300 REM 
6310 IF L+1>L9 THEN GOSUB 5000
6320 LET L=L+1,S=X[0]+X[1]+X[2]+X[3]+X[4]
6340 PRINT (7)VENDOR$,@(O[7]),X[0]:M1$,@(O[8]),X[1]:M1$,@(O[9]),X[2]:M1$,@(O[10]),X[3]:M1$,@(O[11]),X[4]:M1$,@(O[12]),S:M1$
6400 REM 
6410 IF DONE>0 THEN GOTO 6590
6420 DIM VENDOR$(O[7]),B0$(8),B1$(198),C0$(14),C1$(30),X[4]
6430 IF A0$(5,4)="TEMP" THEN GOTO 6500
6440 LET T1$=A0$(5,6),B0$(1)=N0$+A0$(5,6),B1$(1)=NF$
6450 FIND (APM01_DEV,KEY=N0$+A0$(5,6),DOM=6460)IOL=APM01A
6460 LET VENDOR$(1)=FNF$(A0$(5,P[0]),M0$)+" "+B1$(1,30),L1=0
6470 IF SEQUENCE$="A" THEN LET VENDOR$(1)=B1$(146,10)+" "+VENDOR$
6480 LET PHONE$=FNF$(B1$(112,10),M5$)
6490 GOTO 6590
6500 REM 
6510 LET T1$="TEMP VENDOR",C0$(1)=N0$+A0$(3,2)+A0$(11,10),C1$(1)=NF$
6520 FIND (APM03_DEV,KEY=C0$,DOM=6530)IOL=APM03A
6530 LET VENDOR$(1)="TEMP",VENDOR$(M0+2)=C1$,PHONE$="",L1=0
6540 IF SEQUENCE$="A" THEN LET VENDOR$(12)=VENDOR$,VENDOR$(1,11)=""
6590 RETURN 
6800 REM " --- Print totals"
6810 LET N=2
6820 IF RETENTION$="Y" THEN LET N=3
6830 IF L+N>L9 THEN GOSUB 5000
6840 LET L=L+N,S=S[0]+S[1]+S[2]+S[3]+S[4],Y=Y[0]+Y[1]+Y[2]+Y[3]+Y[4]
6850 PRINT (7)@(O[7]),J1$
6860 PRINT (7)@(O[7]-LEN(TOTAL$)),TOTAL$,@(O[7]),S[0]:M1$,@(O[8]),S[1]:M1$,@(O[9]),S[2]:M1$,@(O[10]),S[3]:M1$,@(O[11]),S[4]:M1$,@(O[12]),S:M1$
6870 IF RETENTION$="Y" THEN PRINT (7)@(O[7]-9),"Retention",@(O[7]),Y[0]:M1$,@(O[8]),Y[1]:M1$,@(O[9]),Y[2]:M1$,@(O[10]),Y[3]:M1$,@(O[11]),Y[4]:M1$,@(O[12]),Y:M1$
6890 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

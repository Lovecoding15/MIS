0010 REM "DDR - Print Record Layout (Input)"
0020 REM "Program DDR.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=10
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="DDM-01",FILES$[2]="DDM-03",FILES$[3]="DDM-04"
0130 LET FILES$[4]="DDM-12",FILES$[5]="DDM-13",FILES$[6]="DDM-14"
0135 LET FILES$[7]="DDW-01"
0140 LET FILES$[8]="SYM-03",FILES$[9]="SYM-04",FILES$[10]="SYS-01"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET DDM01_DEV=CHANNELS[1],DDM03_DEV=CHANNELS[2],DDM04_DEV=CHANNELS[3]
0180 LET DDM12_DEV=CHANNELS[4],DDM13_DEV=CHANNELS[5],DDM14_DEV=CHANNELS[6]
0185 LET DDW01_DEV=CHANNELS[7]
0190 LET SYM03_DEV=CHANNELS[8],SYM04_DEV=CHANNELS[9],SYS01_DEV=CHANNELS[10]
0200 REM " --- IOLists
0260 SYM04A: IOLIST S0$(1),S1$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="DD",N4$=F5$,NUMBER=1
0500 REM " --- Initializations
0510 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
0550 LET SYSTEM$="",BEGFILE$="",ENDFILE$="",ID$="",INDEX$="N",ADDOPTS$="N",PASSWRD$="N"
0590 LET PAGE=1,REVISION$="",DATA_REV$="",TOC$="N"
0700 REM " --- Background
0750 PRINT 'SB',@(33,3),"System",@(20,4),"Beginning File Name",@(23,5),"Ending File Name",@(30,6),"Record ID",@(24,7),"Generate Index?",@(12,8),"Generate Table Of Contents?",@(19,9),"Starting Page Number",@(22,10),"File Last Revised",@(22,11),"Data Last Revised",@(12,12),"Include Additional Options?",@(23,13),"Print Passwords?",'SF',
0790 GOSUB 5000
1000 REM " --- System
1010 LET V0$="S",V1$="C",V2$=SYSTEM$,V3$="",V0=2,V1=40,V2=3,I0=0
1020 LET V4$="Enter (Optional) System ID (<F1>=All/<F3>=Lookup)"
1030 GOSUB 7000
1040 ON V3 GOTO 1100,1120,1000,1050,4000
1050 GOSUB 6000
1100 REM 
1110 IF V$<>"" THEN GOTO 1150
1120 LET SYSTEM$=""
1130 GOTO 1180
1150 LET SYSTEM$=FNP$(V$)
1180 GOSUB 5100
1190 GOTO 3900
1400 REM " --- Beginning File Name
1410 LET V0$="S",V1$="C",V2$=BEGFILE$,V3$="",V0=6,V1=40,V2=4
1420 LET V4$="Enter Beginning File Name (<F1>=First/<F3>=Lookup)"
1430 GOSUB 7000
1440 ON V3 GOTO 1500,1520,3900,1450,4000
1450 GOSUB 6100
1500 REM 
1510 IF V$<>"" THEN GOTO 1550
1520 LET BEGFILE$=""
1530 PRINT @(V1,V2),'CL',"First"
1540 GOTO 1590
1550 LET BEGFILE$=V$
1560 PRINT @(V1,V2),'CL',BEGFILE$
1590 GOTO 3900
1600 REM " --- Ending File Name
1605 IF BEGFILE$<>"" THEN IF ENDFILE$="" THEN LET ENDFILE$=BEGFILE$
1610 LET V0$="S",V1$="C",V2$=ENDFILE$,V3$="",V0=6,V1=40,V2=5
1620 LET V4$="Enter Ending File Name (<F1>=Last/<F3>=Lookup)"
1630 GOSUB 7000
1640 ON V3 GOTO 1700,1720,3900,1650,4000
1650 GOSUB 6100
1700 REM 
1710 IF V$<>"" THEN GOTO 1750
1720 LET ENDFILE$=""
1730 PRINT @(V1,V2),'CL',"Last"
1740 GOTO 1790
1750 LET ENDFILE$=V$
1760 PRINT @(V1,V2),'CL',ENDFILE$
1790 GOTO 3900
1800 REM " --- Record ID
1810 LET V0$="S",V1$="C",V2$=ID$,V3$="",V0=1,V1=40,V2=6
1815 LET V4$="Enter Record ID's to include (<F1>=All"
1820 IF BEGFILE$=ENDFILE$ THEN LET V4$=V4$+"/<F3>=Lookup)"
1825 LET V4$=V4$+")"
1830 GOSUB 7000
1840 ON V3 GOTO 1900,1920,3900,1850,4000
1850 IF BEGFILE$<>ENDFILE$ THEN GOTO 1800
1860 GOSUB 6200
1870 IF V$="" THEN GOTO 1800
1900 REM 
1910 IF V$<>"" THEN GOTO 1950
1920 LET ID$=""
1930 PRINT @(V1,V2),'CL',"All"
1940 GOTO 1990
1950 LET ID$=V$
1960 PRINT @(V1,V2),'CL',ID$
1990 GOTO 3900
2000 REM " --- Generate Index?
2010 LET V0$="Y",V1$="C",V2$=INDEX$,V3$="",V0=1,V1=40,V2=7
2020 LET V4$="Generate file index while printing record layouts (Y/N)?"
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 IF POS(V$="YN")=0 THEN GOTO 2000
2070 LET INDEX$=V$
2080 PRINT @(V1,V2),INDEX$
2090 GOTO 3900
2200 REM " --- Generate Table of Contents?
2210 LET V0$="Y",V1$="C",V2$=TOC$,V3$="",V0=1,V1=40,V2=8
2220 LET V4$="Generate table of contents while printing record layouts (Y/N)?"
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 IF POS(V$="YN")=0 THEN GOTO 2200
2270 LET TOC$=V$
2280 PRINT @(V1,V2),TOC$
2290 GOTO 3900
2400 REM " --- Starting Page Number
2410 LET V0$="N",V1$="C",V2$=STR(PAGE),V3$="###",V0=3,V1=40,V2=9
2420 LET V4$="Enter An (Optional) Starting Page Number To Use"
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 IF V<1 THEN GOTO 2400
2460 LET PAGE=V
2470 PRINT @(V1,V2),'CL',STR(PAGE)
2490 GOTO 3900
2600 REM " --- Last Revision
2610 LET V0$="D",V1$="C6N",V2$=REVISION$,V3$="",V0=8,V1=40,V2=10
2620 LET V4$="Enter the first File Revision Date to include (<F1>=First)"
2630 GOSUB 7000
2640 ON V3 GOTO 2700,2720,3900,2600,4000
2700 REM 
2710 IF V$<>"" THEN GOTO 2750
2720 LET REVISION$=""
2730 PRINT @(V1,V2),'CL',"First"
2740 GOTO 2790
2750 LET REVISION$=V$
2760 PRINT @(V1,V2),'CL',FNB6$(REVISION$)
2790 GOTO 3900
2800 REM " --- Last Revision
2810 LET V0$="D",V1$="C6N",V2$=DATA_REV$,V3$="",V0=8,V1=40,V2=11
2820 LET V4$="Enter the first Data Revision Date to include (<F1>=First)"
2830 GOSUB 7000
2840 ON V3 GOTO 2900,2920,3900,2800,4000
2900 REM 
2910 IF V$<>"" THEN GOTO 2950
2920 LET DATA_REV$=""
2930 PRINT @(V1,V2),'CL',"First"
2940 GOTO 2990
2950 LET DATA_REV$=V$
2960 PRINT @(V1,V2),'CL',FNB6$(DATA_REV$)
2990 GOTO 3900
3000 REM " --- Include Additional Options?"
3010 LET V0$="Y",V1$="C^",V2$=ADDOPTS$,V3$="",V0=1,V1=40,V2=12
3020 LET V4$="Print the Additional Options with the record layouts (Y/N)?"
3030 GOSUB 7000
3040 ON V3 GOTO 3050,3000,3900,3000,3900
3070 LET ADDOPTS$=V$
3090 GOTO 3900
3200 REM " --- Include Additional Options?"
3210 LET V0$="Y",V1$="C^",V2$=PASSWRD$,V3$="",V0=1,V1=40,V2=13
3220 LET V4$="Print passwords on the record layouts (Y/N)?"
3230 GOSUB 7000
3240 ON V3 GOTO 3250,3200,3900,3200,3900
3270 LET PASSWRD$=V$
3290 GOTO 3900
3900 REM " --- Next field
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1400,1600,1800,2000,2200,2400,2600,2800,3000,3200,3990
3990 LET I0=0
4000 REM " --- Correct?
4005 GOSUB 5000
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="S",V1$="CR",V2$="Y",V3$="",V0=1,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4090 ON POS(V$="YN") GOTO 4000,4900,1000
4900 REM " --- Run report overlay
4990 RUN "DDR.CB"
5000 REM " --- Display"
5005 GOSUB 5100
5010 IF BEGFILE$="" THEN PRINT @(40,4),'CL',"First", ELSE PRINT @(40,4),'CL',BEGFILE$,
5015 IF ENDFILE$="" THEN PRINT @(40,5),'CL',"Last", ELSE PRINT @(40,5),'CL',ENDFILE$,
5020 IF ID$="" THEN PRINT @(40,6),'CL',"All", ELSE PRINT @(40,6),'CL',ID$,
5030 PRINT @(40,7),'CL',INDEX$,
5040 PRINT @(40,8),'CL',TOC$,
5050 PRINT @(40,9),'CL',STR(PAGE),
5060 IF REVISION$="" THEN PRINT @(40,10),'CL',"First", ELSE PRINT @(40,10),'CL',FNB6$(REVISION$),
5070 IF DATA_REV$="" THEN PRINT @(40,11),'CL',"First", ELSE PRINT @(40,11),'CL',FNB6$(DATA_REV$),
5080 PRINT @(40,12),'CL',ADDOPTS$,
5085 PRINT @(40,13),'CL',PASSWRD$,
5090 RETURN 
5100 REM " --- Display System Description"
5110 IF SYSTEM$<>"" THEN GOTO 5140
5120 PRINT @(40,3),'CL',"All",
5130 GOTO 5190
5140 DIM S0$(2),S1$(35)
5150 LET S0$(1)=SYSTEM$
5160 FIND (SYM04_DEV,KEY=S0$,DOM=5170)IOL=SYM04A
5170 IF SYSTEM$="!" THEN LET S1$(1)="Global Variables"
5180 PRINT @(V1,V2),'CL',S0$(1,2)," ",S1$,
5190 RETURN 
6000 REM " --- System Lookup
6010 LET CHANNEL=SYM04_DEV,RECORD$="",TITLE$="ADD+ON Systems"
6020 LET DESCRIPTION$[0]="System",FIELD[0]=1,POSITION[0]=1,LENGTH[0]=2
6090 GOTO 6500
6100 REM " --- File Name Lookup
6110 LET CHANNEL=SYM03_DEV,RECORD$="",TITLE$="ADD+ON System Files"
6120 LET DESCRIPTION$[0]="File",FIELD[0]=1,POSITION[0]=1,LENGTH[0]=6
6190 GOTO 6500
6200 REM " --- Record ID Lookup
6210 LET CHANNEL=DDM03_DEV,RECORD$=BEGFILE$,TITLE$=BEGFILE$+" Record ID's"
6220 LET DESCRIPTION$[0]="ID",FIELD[0]=1,POSITION[0]=7,LENGTH[0]=1
6500 REM " --- Call Windowed Lookup
6510 LET V$="NO COMPANY",ROW=V2+2,DESCRIPTION$[1]="Description"
6520 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6530 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6540 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

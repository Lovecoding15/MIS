0010 REM "PRU - Daily Entry Update"
0020 REM "Program PRU.FA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "12Nov96 JJD: Allow "through date" transaction qualification for updates (1150). (Requires corresponding SCN to PRR.UA)"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRE-01",FILES$[2]="PRE-02",FILES$[3]="PRE-11"
0130 LET FILES$[4]="PRM-01",FILES$[5]="PRM-10",FILES$[6]="SYS-01"
0160 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRE01_DEV=CHANNELS[1],PRE02_DEV=CHANNELS[2],PRE11_DEV=CHANNELS[3]
0185 LET PRM01_DEV=CHANNELS[4],PRM10_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0200 REM " --- IOLists"
0210 PRE01A: IOLIST X0$
0220 PRE02A: IOLIST A0$,A1$,A[ALL]
0230 PRE11A: IOLIST B0$,B[0],B[1],B[2],B1$,B[3]
0240 PRM01A: IOLIST C0$,C1$
0250 PRM10A: IOLIST X0$,X0,X1
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0480 DIM P[3]
0490 LET P[0]=NUM(P2$(1,2)),P[3]=NUM(P3$(7,1))
0500 REM " --- Init Data"
0510 PRECISION P[3]
0520 DIM J1$(40," ")
0530 DIM A[2],B[3]
0700 REM " --- Background"
0720 PRINT @(0,10),'CE',
0800 REM " --- Options"
0810 LET V4$="Are You Ready To Update The Daily Entry Register"
0815 CALL "SYC.YN",0,V4$,0,V$,V3
0820 IF V$="NO" THEN GOTO 9900
0830 IF V$<>"YES" THEN GOTO 0800
0850 CALL "SYC.NB","Updating",LEN(M0$),COLUMN
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
1000 REM " --- Init Read"
1020 READ (PRE02_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Main Read"
1120 READ (PRE02_DEV,END=4000)IOL=PRE02A
1140 IF A0$(1,2)<>N0$ THEN GOTO 4000
1150 IF THROUGH_DATE$<>"" THEN IF A0$(15,3)>THROUGH_DATE$ THEN GOTO 1120
1200 REM " --- Breaks"
1220 IF A0$(1,11)<>T0$ THEN GOSUB 6000
1280 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$),
2000 REM " --- Create Detail"
2020 LET B0$=A0$(1,11)+"C01"+J1$(1,7)+"A"+A0$(13,2)+A0$(18,6)+J1$(1,4),B1$=A1$
2030 LET B[0]=A[0],B[1]=A[1],B[2]=A[2],B[3]=B[0]
2035 FIND (PRM10_DEV,KEY=N0$+"A"+A0$(18,2),DOM=2040)IOL=PRM10A
2038 IF X1<>0 THEN LET B[3]=B[0]/X1; REM "Regular portion of OT pay
2040 WRITE (PRE11_DEV,KEY=B0$(1,24),DOM=2100)IOL=PRE11A
2090 GOTO 2900
2100 REM " --- Increment"
2120 LET X0=NUM(B0$(23,2))+1
2140 IF X0>99 THEN LET X0=1
2150 LET B0$(23,2)=STR(X0:"00")
2190 GOTO 2040
2900 REM " --- Remove"
2920 REMOVE (PRE02_DEV,KEY=A0$(1,14))
2990 GOTO 1100
4000 REM " --- Done"
4020 GOTO 9900
6000 REM " --- Employee Break"
6010 DIM C1$(220)
6020 LET T0$=A0$(1,11)
6030 FIND (PRM01_DEV,KEY=T0$,DOM=6040)IOL=PRM01A
6040 LET X0$=A0$(1,11)+"C01"+J1$(1,10)+C1$(220,1)+J1$(1,7)
6050 WRITE (PRE01_DEV,KEY=X0$(1,21),DOM=6060)IOL=PRE01A
6090 RETURN 
8000 REM " --- Functions"
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

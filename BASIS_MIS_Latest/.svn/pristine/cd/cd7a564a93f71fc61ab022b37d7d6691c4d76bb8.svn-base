0010 REM "APR - Payment Selection Report 2"
0020 REM "Program APR.DB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLISTs"
0210 APE04A: IOLIST A0$,A[ALL]
0220 APM01A: IOLIST B0$,B1$
0230 APM03A: IOLIST B0$,B1$
0240 APM10A: IOLIST H3$
0500 REM " --- Init Read"
0520 LET H0=132,L=L9+1
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",17,COLUMN
1000 REM " --- Init Read"
1020 READ (APE04_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Main Read"
1120 LET K$=KEY(APE04_DEV,END=4000)
1130 IF K$(1,2)<>N0$ THEN GOTO 4000
1140 READ (APE04_DEV)IOL=APE04A
1160 PRINT @(COLUMN,11),A0$(5,6)," ",A0$(11,10),
1200 REM " --- Breaks"
1220 IF K$(3,2)<>T0$ THEN GOSUB 6000
1240 IF K$(5,6)<>T1$ THEN GOSUB 6400
1250 IF L+1>L9 THEN GOSUB 5000
1260 IF L1>0 THEN PRINT (7)'LF',; LET L=L+1
2500 REM " --- Print"
2510 IF L+1>L9 THEN GOSUB 5000
2530 IF P3$(3,1)="Y" THEN PRINT (7)@(69+M0+3*M1),A[2]:M1$,
2540 PRINT (7)@(32+M0),A0$(11,10)," ",FNB$(A0$(21,3))," ",FNB$(A0$(24,3))," ",FNB$(A0$(27,3)),A[0]:M1$,A[1]:M1$,A[0]-A[1]-A[2]:M1$,
2560 LET L1=L1+1
2600 REM " --- Accumulate"
2620 FOR X=0 TO 2
2630 LET T[X]=T[X]+A[X],U[X]=U[X]+A[X],X[X]=X[X]+A[X]
2640 NEXT X
2900 GOTO 1100
4000 REM " --- Totals"
4020 LET T9$="E"
4040 GOSUB 6000
4045 IF H=0 OR L+5>L9 THEN GOSUB 5000
4050 PRINT (7)'LF',
4060 IF P3$(3,1)="Y" THEN PRINT (7)@(69+M0+3*M1),U[2]:M1$,
4070 PRINT (7)@(10),"Grand Total:",@(69+M0),U[0]:M1$,U[1]:M1$,U[0]-U[1]-U[2]:M1$
4075 IF O3=0 THEN GOTO 4200
4077 IF L+3>L9 THEN GOSUB 5000
4080 PRINT (7)'LF','LF',@(10),"*** Warning ***  There Are Negative Checks On This Report"
4090 LET MESSAGE$="There Are Negative Checks On This Report"
4120 CALL "SYC.XA",2,MESSAGE$,1,-1,-1,V$,V3
4200 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS<>0 THEN EXITTO 9900
5050 IF P3$(1,1)="Y" THEN PRINT (7)@(INT(H0/2-LEN(H3$)/2)),H3$,'LF'; LET L=L+2
5100 PRINT (7)@(0),"Vendor",@(32+M0),"Invoice",@(43+M0),"Invoice",@(54+M0),"Due",@(61+M0),"Discount"
5110 IF P3$(3,1)="Y" THEN PRINT (7)@(58+M0+4*M1),"Retention",
5120 PRINT (7)@(0),"No",@(M0+1),"Name",@(32+M0),"Number",@(45+M0),"Date",@(54+M0),"Date",@(63+M0),"Date",@(60+M0+M1),"Invoice",@(59+M0+2*M1),"Discount",@(60+M0+3*M1),"Payment"
5130 LET L=L+2
5150 IF VBREAK$="N" THEN GOTO 5890 ELSE LET VBREAK$="N"
5800 REM " --- Vendor"
5810 IF L+2>L9 THEN GOSUB 5000
5820 PRINT (7)'LF',@(0),X1$," ",X0$,
5830 LET L=L+1
5890 LET TBREAK$="N"
5895 RETURN 
6000 REM " --- AP Type Break"
6010 IF T0$="" THEN GOTO 6100
6020 LET TBREAK$="Y"
6030 GOSUB 6400
6040 IF P3$(1,1)<>"Y" THEN GOTO 6100
6045 IF L+2>L9 THEN GOSUB 5000
6050 PRINT (7)'LF',
6060 IF P3$(3,1)="Y" THEN PRINT (7)@(69+M0+3*M1),T[2]:M1$,
6070 PRINT (7)@(10),"AP Type Total:",@(69+M0),T[0]:M1$,T[1]:M1$,T[0]-T[2]-T[1]:M1$
6090 LET T1$=""
6100 IF T9$="E" THEN RETURN 
6120 DIM T[2]
6140 LET T0$=A0$(3,2),L=L9+1,T9$=""
6150 IF P3$(1,1)<>"Y" THEN RETURN 
6160 LET H3$=""
6170 FIND (APM10_DEV,KEY=N0$+"A"+T0$,DOM=6290)IOL=APM10A
6195 LET H3$="AP Type "+T0$+" "+FNP$(H3$(6,20))
6290 RETURN 
6400 REM " --- Vendor Break"
6405 LET VBREAK$="Y"
6410 IF T1$="" THEN GOTO 6500
6420 IF X[0]-X[2]-X[1]>=0 AND L1=1 THEN GOTO 6490
6422 IF L+3>L9 THEN GOSUB 5000
6425 PRINT (7)'LF',
6430 PRINT (7)@(69+M0),J$(1,3*M1+POS("Y"=P3$(3,1))*M1)
6440 LET L=L+2
6470 IF P3$(3,1)="Y" THEN PRINT (7)@(69+M0+3*M1),X[2]:M1$,
6475 IF X[0]-X[2]-X[1]<0 THEN LET O3=1; PRINT (7)@(10),"*** Warning, Negative Total ***",
6480 PRINT (7)@(69+M0),X[0]:M1$,X[1]:M1$,X[0]-X[2]-X[1]:M1$,
6495 IF X[0]-X[2]-X[1]<0 THEN PRINT (7)"    *** Void ***" ELSE PRINT (7)'LF',
6497 LET L=L+1
6500 IF T9$<>"" THEN RETURN 
6510 DIM X1$(M0)
6520 LET X0$="Not On File",X2$=""
6530 IF A0$(5,4)="TEMP" THEN GOTO 6600
6550 FIND (APM01_DEV,KEY=N0$+A0$(5,6),DOM=6570)IOL=APM01A
6560 LET X0$=B1$(1,30)
6570 LET X1$(1)=FNF$(A0$(5,P[0]),M0$),T1$=A0$(5,6)
6590 GOTO 6700
6600 REM " --- Temp Name"
6610 LET X1$(1)=A0$(5,4),T1$=" "
6620 FIND (APM03_DEV,KEY=A0$(1,4)+A0$(11,10),DOM=6640)IOL=APM03A
6630 LET X0$=B1$(1,30)
6700 LET L1=0
6710 DIM X[4]
6720 IF L+2<=L9 AND TBREAK$<>"Y" THEN GOSUB 5800
6730 IF L+2>L9 THEN GOSUB 5000
6790 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

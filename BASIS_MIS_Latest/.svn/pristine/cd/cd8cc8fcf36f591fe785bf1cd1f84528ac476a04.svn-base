0010 REM "OPR - INVOICE HISTORY REPORT II"
0020 REM "Program OPR.KB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "26Sep96 WGH: Printing only first detail line of each invoice when more than 50 Product Types (2310,6140-6160)"
0040 REM 
0090 SETERR 9000
0095 SETESC 9000
0200 REM " --- IOLISTS"
0210 ART03A: IOLIST A0$(1),A[ALL]
0220 ARM01A: IOLIST B0$,B1$
0230 ARM10E: IOLIST Y0$(1)
0240 ARM02A: IOLIST C0$,C1$,C[ALL]
0250 IVM01A: IOLIST D0$,D1$,D2$,D3$,D4$,D5$,D6$,D[ALL]
0260 ARM10A: IOLIST W$(1)
0270 IVM10A: IOLIST X0$
0280 ART23A: IOLIST R0$,R1$,R2$,R0,R1,R2
0310 ART13A: IOLIST W0$,W1$(1),W[ALL]
0400 REM " --- Parameters "
0500 REM " --- Initialize Data "
0520 LET ORD=0,SHIP=0,BO=0,L9=59,L=L9+1,HEADINGS=2
0530 DIM J$(130,"-"),J1$(30," "),J2$(130,"="),J3$(131,"_"),R9$(0)
0540 DIM A[10],W[14],D[10],S[2],T[4],U[3],HEADINGS$[2],Y0$(32),X0$(32)
0545 LET M9$="###.0%",M8$="####.00-",CLOCK$="",WHEN$=F0$(7,3)
0550 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),M8=LEN(M8$),M9=LEN(M9$)
0555 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$,H0=132,H=0,P6=H0-M8
0560 LET P1=H0-M1*3-M3*2,P2=H0-M1*3-M3,P3=H0-M1,P4=H0-M1*2,P5=H0-M1*3
0565 DIM A0$(101),W1$(64)
0570 LET T9$="",MAX_PCODES=50; DIM T9[MAX_PCODES,3]; REM "TOTALS BY PRODUCT
0575 LET R9$="SPN"
0580 DIM R[3,4]; REM " 0-3 = STANDARD,PROMOTIONAL,NON-STOCK,OTHER"
0585 REM "             0-4 = LIST,COST,QTY ORDERED,QTY B/O,QTY SHIPPED
0590 IF I3$(17,1)="S" THEN LET X2$="Ser#" ELSE IF I3$(17,1)="L" THEN LET X2$="Lot#"
0600 CALL "SYC.GA",7,1,"","",STATUS
0605 IF STATUS>0 THEN GOTO 9900
0620 IF S0$="" THEN LET H4$="First"
0630 IF S1$="" THEN LET H5$="Last"
0640 IF S2$="" THEN LET H6$="First"
0650 IF S3$="" THEN LET H7$="Last"
0660 IF S4$="" THEN LET H8$="All"
0670 IF S5$="" THEN LET H9$="All"
0700 REM " --- Background "
0800 REM " --- Options "
0920 PRECISION NUM(I2$(5,1))
1000 REM " --- Initial Read "
1020 READ (ART03_DEV,KEY=N0$+"  "+S0$,DOM=1030)
1100 REM " --- Main Read "
1120 LET K1$=KEY(ART03_DEV,END=4000)
1125 IF POS(N0$=K1$)<>1 THEN GOTO 4000
1130 IF S1$<>"" AND K1$(1,10)>N0$+"  "+S1$ THEN GOTO 4000
1140 PRINT @(COLUMN,11),FNF$(K1$(5,6),M0$)," ",K1$(11,7)
1160 READ (ART03_DEV,KEY=K1$)IOL=ART03A
1170 IF S2$<>"" AND A0$(24,3)<S2$ THEN GOTO 1100
1180 IF S3$<>"" AND A0$(24,3)>S3$ THEN GOTO 1100
1190 IF S4$<>"" AND A0$(59,3)<>S4$ THEN GOTO 1100
1400 REM " --- Heading "
1410 IF A0$(21,1)<>"V" THEN GOTO 1600
1420 REM " --- Void "
1430 IF L+2>L9 THEN GOSUB 5000
1435 GOSUB 6000
1470 PRINT (7)'LF',@(0),FNF$(A0$(5,P[0]),M0$)," ",B1$(1,30)," ",A0$(42,7)," ",A0$(11,7)," ",FNB$(A0$(24,3))," ",A0$(96,3)," ",A0$(59,3)," *** VOID ***"
1480 LET L=L+2
1490 GOTO 1100
1600 REM " --- Print "
1620 GOSUB 6300
1630 IF L$<>"PRINT" THEN GOTO 1100
1640 GOSUB 6000
1650 GOSUB 5200
1800 REM " --- Detail "
1810 IF L2=0 THEN GOTO 3000
1820 FOR I=1 TO L2
1830 DIM W1$(40)
1840 LET W0$=L0$[I,0],W1$(1)=L0$[I,1]
1850 FOR J=0 TO 7; LET W[J]=L0[I,J]; NEXT J
1860 GOSUB 2000
1870 NEXT I
1890 GOTO 3000
2000 REM " --- Detail "
2070 LET W0=W[0],DESC$=W1$(1,40)
2080 IF W0$(21,1)<>X0$(4,1) THEN DIM X0$(32)
2090 FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=2100)IOL=ARM10E
2120 IF POS(Y0$(25,1)="SP")=0 THEN GOTO 2190
2130 DIM D[10]; FIND (IVM01_DEV,KEY=N0$+W0$(33,20),DOM=2190)IOL=IVM01A
2140 IF POS(" "<>DESC$)=0 THEN LET DESC$=D1$
2150 LET W[8]=0,W[8]=NUM(W0$(26,5),ERR=2151)
2160 LET W[9]=W[8]*W[1]*W[4]/100
2170 LET W[6]=W[1]*W[4]-W[9]
2190 GOSUB 5700
2200 LET S[0]=S[0]+W[6],S[1]=S[1]+W[0]*W[4],S[2]=S[2]+W[9],S0=S0+1
2220 LET U[0]=U[0]+W[6],U[1]=U[1]+W[0]*W[4],U[2]=U[2]+W[9]
2230 LET ORD=ORD+W[2],BO=BO+W[3],SHIP=SHIP+W[4]
2240 LET R9=0,R9=POS(Y0$(25,1)=R9$)
2250 IF R9<>0 THEN LET R[R9-1,0]=R[R9-1,0]+W[6],R[R9-1,1]=R[R9-1,1]+W[0]*W[4],R[R9-1,2]=R[R9-1,2]+W[2],R[R9-1,3]=R[R9-1,3]+W[3],R[R9-1,4]=R[R9-1,4]+W[4]
2260 IF Y0$(25,1)="O" THEN LET R[3,0]=R[3,0]+W[6]
2270 IF Y0$(32,1)="N" THEN GOTO 2900
2300 IF POS(W0$(23,3)=T9$,3)=0 THEN LET T9$=T9$+W0$(23,3)
2310 IF LEN(T9$)/3>MAX_PCODES THEN GOSUB 6100
2320 LET X=POS(W0$(23,3)=T9$,3),X=INT(X/3)
2340 LET T9[X,0]=T9[X,0]+W[6],T9[X,1]=T9[X,1]+W[0]*W[4],T9[X,2]=T9[X,2]+W[9],T9[X,3]=T9[X,3]+W[4]
2900 RETURN 
3000 REM " --- Invoice Totals "
3020 GOSUB 5400
3100 DIM S[2]; LET S0=0
3120 LET ORD=0,BO=0,SHIP=0
3900 GOTO 1100
4000 REM " --- Totals "
4005 IF H=0 THEN GOSUB 5000; GOTO 9900
4010 PRECISION 2
4015 LET E9$="E"
4020 IF L+4>L9 THEN GOSUB 5000
4030 PRINT (7)'LF',@(P2-M8),J2$(1,3*M1+M3+M8)
4040 LET TMP$=STR(FND(U[0]-U[1],U[0]):M8$)
4045 IF LEN(TMP$)>M8 THEN DIM TMP$(M8,"*")
4050 PRINT (7)@(24),"****Report Totals: ",@(P1-M8-4),"Sales Total: ",T[0],@(P2-M8),U[3]:M1$,@(P4-M8),U[1]:M1$,@(P3-M8),U[0]-U[1]:M1$,@(P6),TMP$
4055 PRINT (7)@(P1-M8-1),"Discount:",@(P2-M8),-T[2]:M1$
4060 PRINT (7)@(P1-M8),"Freight:",@(P2-M8),T[1]:M1$
4065 PRINT (7)@(P1-M8-2),"Sales Tax:",@(P2-M8),T[4]:M1$
4070 PRINT (7)@(P2-M8),J$(1,M1)
4075 PRINT (7)@(P1-M8-8),"*  Report Total:",@(P2-M8),U[3]+T[1]-T[2]+T[4]:M1$
4080 PRINT (7)@(22),"Total # Of Invoices: ",U2:"###0-"
4090 LET L=L+9
4100 REM " --- Totals By Product "
4120 DIM T[3]
4140 IF INT(LEN(T9$)/3)+7+L>L9 THEN GOSUB 5000
4150 PRINT (7)'LF',"***Product Type Totals*** ",'LF'
4160 PRINT (7)"Product",@(30+M2-6),"Units",@(30+M2+M1-6),"Sales",@(30+M2+M1*2-5),"Cost",@(30+M2+M1*3-6),"G.P.",@(30+M2+M1*3+M8-7),"G.P. %",'LF',
4170 IF LEN(T9$)=0 THEN GOTO 4400
4200 FOR X=1 TO LEN(T9$) STEP 3
4210 LET X0=INT(X/3)
4220 DIM X0$(30); LET X0$(7,20)="***Not On File***"
4240 FIND (IVM10_DEV,KEY=N0$+"A"+T9$(X,3),DOM=4260)IOL=IVM10A
4260 IF T9[X0,0]=0 AND T9[X0,1]=0 THEN GOTO 4390
4300 PRINT (7)T9$(X,3)," ",X0$(7,20),@(30),T9[X0,3]:M2$,T9[X0,0]:M1$,T9[X0,1]:M1$,T9[X0,0]-T9[X0,1]:M1$,FND(T9[X0,0]-T9[X0,1],T9[X0,0]):M8$
4320 LET T[0]=T[0]+T9[X0,0],T[1]=T[1]+T9[X0,1],T[2]=T[2]+T9[X0,2],T[3]=T[3]+T9[X0,3]
4330 LET L=L+1
4390 NEXT X
4400 PRINT (7)@(30),J$(1,M2+M1*3+M8),'LF',"Report Totals: ",
4420 PRINT (7)@(30),T[3]:M2$,T[0]:M1$,T[1]:M1$,T[0]-T[1]:M1$,FND(T[0]-T[1],T[0]):M8$
4430 LET L=L+2
4500 REM " --- Total By Line Type "
4505 IF L+10>L9 THEN GOSUB 5000
4510 DIM T[1]
4520 PRINT (7)'LF','LF','LF',@(0),"*** Line Type Total ***",'LF','LF',@(0),"Description",@(30),"Ord",@(40),"B/O",@(49),"Ship",@(62),"Sales",@(75),"Cost",'LF'
4530 IF R[0,0]<>0 OR R[0,1]<>0 OR R[0,2]<>0 OR R[0,3]<>0 OR R[0,4]<>0 THEN PRINT (7)@(0),"Standard Items",@(33-M2),R[0,2]:M2$,@(43-M2),R[0,3]:M2$,@(53-M2),R[0,4]:M2$,@(67-M3),R[0,0]:M3$,@(80-M3),R[0,1]:M3$
4540 IF R[1,0]<>0 OR R[1,1]<>0 OR R[1,2]<>0 OR R[1,3]<>0 OR R[1,4]<>0 THEN PRINT (7)@(0),"Promotional Items",@(33-M2),R[1,2]:M2$,@(43-M2),R[1,3]:M2$,@(53-M2),R[1,4]:M2$,@(67-M3),R[1,0]:M3$,@(80-M3),R[1,1]:M3$
4550 IF R[2,1]<>0 OR R[2,0]<>0 OR R[2,2]<>0 OR R[2,3]<>0 OR R[2,4]<>0 THEN PRINT (7)@(0),"Non-Stock Items",@(33-M2),R[2,2]:M2$,@(43-M2),R[2,3]:M2$,@(53-M2),R[2,4]:M2$,@(67-M3),R[2,0]:M3$,@(80-M3),R[2,1]:M3$
4560 IF R[3,0]<>0 THEN PRINT (7)@(0),"Other Charges",@(67-M3),R[3,0]:M3$
4570 FOR X=0 TO 3
4580 LET T[0]=T[0]+R[X,0]
4590 LET T[1]=T[1]+R[X,1]
4600 NEXT X
4610 PRINT (7)@(80-M3*2-M2),J$(1,M2+M3*2),'LF',"Report Totals: ",
4620 PRINT (7)@(67-M3),T[0]:M3$,@(80-M3),T[1]:M3$
4630 LET L=L+2
4990 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5050 PRINT (7)@(0),"Beginning Customer: ",H4$,@(56),"Salesperson: ",H8$,@(98),"Beginning Invoice Date: ",H6$
5060 PRINT (7)@(3),"Ending Customer: ",H5$,@(54),"Selected Item: ",H9$,@(101),"Ending Invoice Date: ",H7$
5070 PRINT (7)@(56),"Minimum GP%: ",STR(S6:M9$),'LF'
5080 LET L=L+4
5090 IF E9$="E" THEN GOTO 5190
5100 PRINT (7)@(2),"-- Customer --",@(41),"Order",@(48),"Invoice",@(57),"Invoice",@(75),"P.O.",@(97),"Dist",@(120),"Tax"
5110 PRINT (7)@(0),"Number",@(8),"Name",@(40),"Number",@(48),"Number",@(58),"Date",@(66),"By",@(70),"Sls",@(74),"Number",@(85),"Ship Via",@(97),"Code",@(103),"Terms",@(120),"Code"
5180 LET L=L+2
5190 RETURN 
5200 REM " --- Order Header "
5210 IF L+7>L9 THEN GOSUB 5000
5215 GOSUB 6900
5220 PRINT (7)'LF',@(0),FNF$(A0$(5,P[0]),M0$),@(8),B1$(1,30),@(40),A0$(42,7),@(48),A0$(11,7),@(57),FNB$(A0$(24,3)),@(66),A0$(96,3),@(70),A0$(59,3),@(74),A0$(49,10),@(85),A0$(32,10),@(98),A0$(66,2),@(103),W$,@(120),A0$(80,2),'LF'
5230 PRINT (7)@(P4-M3*2-4),"Gross",@(P6-M1-9),"Extended"
5235 PRINT (7)@(1),"Ln Cd",@(7),"Prd Whs Item",
5240 PRINT (7)@(P5-M2*2-M3*2-M8-M9-6),"Ord",@(P5-M2-M3*2-M8-M9-5),"B/O",@(P1-M8-M9-5),"Ship",@(P1-M8-4),"Disc",@(P2-M8-6),"Price",@(P4-M3-M8-7),"Sales",@(P4-M8-5),"Cost",@(P6-M1-6),"Cost",@(P6-7),"Profit",@(H0-5),"GP %"
5280 LET L=L+5
5290 RETURN 
5400 REM " --- Sales Total Freight Ssles Tax/Invoice Total "
5405 PRECISION 2
5410 IF L+7>L9 THEN GOSUB 5200
5415 PRINT (7)@(P2-M8),J$(1,M1*3+M3+M8),'LF',@(P1-M8-4),"Sales Total: ",
5420 LET TMP$=STR(FND(S[0]-S[1],S[0]):M8$)
5425 IF LEN(TMP$)>M8 THEN DIM TMP$(M8,"*")
5430 PRINT (7)@(P2-M8),S[0]:M1$,@(P4-M8),S[1]:M1$,@(P3-M8),S[0]-S[1]:M1$,@(P6),TMP$
5440 PRINT (7)@(P1-M8-1),"Discount:",@(P2-M8),-A[2]:M1$
5445 PRINT (7)@(P1-M8),"Freight:",@(P2-M8),A[1]:M1$
5450 PRINT (7)@(P1-M8-2),"Sales Tax:",@(P2-M8),A[0]:M1$
5460 PRINT (7)@(P2-M8),J$(1,M1)
5470 PRINT (7)@(P1-M8-9),"*  Invoice Total:",@(P2-M8),S[0]+A[1]-A[2]+A[0]:M1$
5480 LET U[3]=U[3]+S[0],T2=T2+1,U2=U2+1,L=L+7,ORDT=ORDT+ORD,BOT=BOT+BO,SHIPT=SHIPT+SHIP,T[1]=T[1]+A[1],T[2]=T[2]+A[2],T[4]=T[4]+A[0]
5485 PRECISION NUM(I2$(5,1))
5490 RETURN 
5500 REM " --- Serialized/Lotted Inventory "
5520 READ (ART23_DEV,KEY=L0$[I,0](1,20),DOM=5530)
5530 LET K2$=KEY(ART23_DEV,END=5590)
5540 IF K2$(1,20)<>L0$[I,0](1,20) THEN GOTO 5590
5550 READ (ART23_DEV,KEY=K2$)IOL=ART23A
5555 IF L+1>L9 THEN GOSUB 5200
5560 PRINT (7)@(5),X2$," ",K2$(21,3)," ",R1$,
5570 PRINT (7)@(P1-M2*3-M8-M9-3),R0:M2$,@(P1-M2*2-M8-M9-2),R1:M2$,@(P4-M3-M8),R2:M3$
5575 LET L=L+1
5580 GOTO 5530
5590 RETURN 
5700 REM " --- Print Detail "
5720 IF L+2>L9 THEN GOSUB 5000; GOSUB 5200
5730 PRINT (7)@(1),W0$(18,3)," ",W0$(21,1),@(11),W0$(31,2),
5732 IF POS(Y0$(25,1)="MO")>0 THEN PRINT (7)@(15),DESC$ ELSE PRINT (7)@(7),W0$(23,3),@(15),W0$(33,I[0])," ",DESC$
5734 LET L=L+1
5740 IF POS(Y0$(25,1)="M")>0 THEN GOTO 5900
5760 IF POS(Y0$(25,1)="O")>0 THEN GOTO 5820
5810 PRINT (7)@(P1-M2*3-M8-M9-3),W[2]:M2$," ",W[3]:M2$," ",W[4]:M2$,
5820 LET TMP$=STR(FND(W[6]-W[0]*W[4],W[6]):M8$)
5825 IF LEN(TMP$)>M8 THEN DIM TMP$(M8,"*")
5830 PRINT (7)@(P1-M8-M9),W[8]:M9$,@(P1-M8),W[1]:M3$,@(P2-M8),W[6]:M1$,@(P4-M3-M8),W[0]:M3$,@(P4-M8),W[0]*W[4]:M1$,@(P3-M8),W[6]-W[0]*W[4]:M1$,@(P6),TMP$
5880 LET L=L+1
5930 IF POS(Y0$(25,1)="SP")<>0 AND POS(I3$(17,1)="SL")<>0 AND D2$(19,1)="Y" THEN GOSUB 5500
5990 RETURN 
6000 REM " --- Find Customer "
6020 DIM B1$(30),C[6]
6040 LET B1$(1)="Not On File"
6060 FIND (ARM01_DEV,KEY=N0$+A0$(5,6),DOM=6070)IOL=ARM01A
6070 FIND (ARM02_DEV,KEY=N0$+A0$(5,6)+"  ",DOM=6080)IOL=ARM02A
6090 RETURN 
6100 REM " --- Too Many Pcodes For T9[] Array"
6110 LET TMP[ALL]=T9[ALL]
6120 LET MAX_PCODES=MAX_PCODES+20
6130 DIM T9[MAX_PCODES,3]
6140 FOR K=0 TO MAX_PCODES-20
6150 LET T9[K,0]=TMP[K,0],T9[K,1]=TMP[K,1],T9[K,2]=TMP[K,2],T9[K,3]=TMP[K,3]
6160 NEXT K
6190 RETURN 
6300 REM " --- Check For Selected Item and Minimum GP% "
6305 LET L2=0,L$=""
6310 READ (ART13_DEV,KEY=A0$(1,17)+$FF$,DOM=6315)
6315 LET M=1,K$=KEYP(ART13_DEV,END=6500)
6320 IF POS(A0$(1,17)=K$)<>1 THEN GOTO 6500
6325 LET M=NUM(K$(18,3))
6330 DIM L0$[M,1],L0[M,7],L1[1]
6335 READ (ART13_DEV,KEY=A0$(1,17)+"000",DOM=6340)
6340 LET K$=KEY(ART13_DEV,END=6550)
6350 IF K$(1,17)<>A0$(1,17) THEN GOTO 6550
6360 FIND (ART13_DEV,KEY=K$)IOL=ART13A
6380 LET L2=L2+1,L0$[L2,0]=W0$,L0$[L2,1]=FNP$(W1$(1,40))
6390 FOR I=0 TO 7; LET L0[L2,I]=W[I]; NEXT I
6400 IF W0$(21,1)<>Y0$(4,1) THEN DIM Y0$(32); FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=6401)IOL=ARM10E
6410 IF POS(Y0$(25,1)="SNPO")=0 THEN GOTO 6340
6420 IF S5$="" THEN LET L$="PRINT"; GOTO 6450
6425 IF Y0$(25,1)="O" THEN GOTO 6450
6430 IF Y0$(25,1)<>"N" AND S5$=W0$(33,I[0]) THEN LET L$="PRINT"
6440 IF Y0$(25,1)="N" AND POS(S5$=W1$(1,40))=1 THEN LET L$="PRINT"
6450 IF S6=100 THEN GOTO 6340
6460 IF Y0$(25,1)="O" THEN LET L1[0]=L1[0]+W[6]; GOTO 6340
6470 LET W9=0,W9=NUM(W0$(26,5),ERR=6471)
6480 LET W9=W[4]*W[1]*W9/100,L1[0]=L1[0]+W[4]*W[1]-W9,L1[1]=L1[1]+W[0]*W[4]
6490 GOTO 6340
6500 REM " --- No Invoice Detail History"
6510 IF S5$<>"" THEN GOTO 6590
6520 LET S[0]=A[6],S[1]=A[7]
6530 IF S[0]<>0 THEN LET W8=(S[0]-S[1])*100/S[0] ELSE LET W8=0
6540 GOTO 6580
6550 REM " --- End Of Invoice "
6560 IF S6=100 OR S5$<>"" AND L$="" THEN GOTO 6590
6570 IF L1[0]<>0 THEN LET W8=(L1[0]-L1[1])*100/L1[0] ELSE LET W8=0
6580 IF W8>S6 THEN LET L$="" ELSE LET L$="PRINT"
6590 RETURN 
6900 REM " --- Terms Code "
6910 DIM W$(30)
6920 LET W$(1)="* Not On File *"
6930 FIND (ARM10_DEV,KEY=N0$+"A"+A0$(62,2),DOM=6940)IOL=ARM10A
6940 LET W$=W$(6,15)
6950 RETURN 
8000 REM " --- Functions "
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FND(Q1,Q2)=SGN(ABS(Q2))*Q1*100/(Q2+SGN(ABS(Q2))-1)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "OPE - Order/Invoice Entry (Lot/Serial Number Input)"
0020 REM "Program OPE.DH"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 REM "S$=General Flag String
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0210 ARE23A: IOLIST T0$(1),T1$(1),T2$,T[ALL]
0240 IVM07A: IOLIST H0$(1),H1$(1),H[ALL]
0500 REM " --- Initialize Data"
0510 PRECISION I[2]
0520 DIM H0$(44),H1$(95),H[12],T0$(23),T1$(20),T[2]
0530 LET T0$(1)=W0$(1,20),L9=19
0540 LET P9$=I3$(17,1)
0550 IF P9$="S" THEN LET P8$="Serial Number" ELSE IF P9$="L" THEN LET P8$="Lot Number"
0560 LET O7=0
0700 REM " --- Background"
0710 IF L>L1+2 THEN PRINT @(0,L9+1),'CE',@(0,L1),'LD','LD'; LET L=L-2; GOTO 0710
0740 LET ROW=L-1
0750 PRINT 'SB',@(72,ROW),"Cost",'SF',
0900 IF S$(4,1)="1" THEN GOTO 1000
0910 GOSUB 5100
0920 LET O7=0
0930 READ (ARE23_DEV,KEY=W0$(1,20),DOM=0940)
0940 LET K$=KEY(ARE23_DEV,END=0990)
0950 IF K$(1,20)<>W0$(1,20) THEN GOTO 0990
0960 READ (ARE23_DEV)IOL=ARE23A
0970 LET O7=O7+T[0]
0980 GOTO 0940
0990 IF O7=W[2] THEN GOTO 4000
1000 REM " --- Overflow"
1020 IF L>L9 THEN PRINT @(0,20),'CE',@(0,L1+2),'LD'; LET L=L-1; GOTO 1040
1030 CALL "SYC.WC",1,0,80,0,0,20,0
1040 IF V3=4 THEN GOTO 4000
1100 REM " --- Next Key
1120 LET K$=KEY(ARE23_DEV,END=1400)
1130 IF LEN(K$)<20 THEN GOTO 1400
1140 IF K$(1,20)<>W0$(1,20) THEN GOTO 1400
1150 READ (ARE23_DEV)IOL=ARE23A
1160 GOTO 1490
1400 REM " --- Seq"
1410 IF O7<W[4] THEN LET T0=NUM(T0$(21,3))+1 ELSE LET T0=0
1420 LET V0$="Z",V1$="KCER",V2$=STR(T0:"000"),V3$="",V0=3,V1=4,V2=L,I0=0
1425 LET V4$="Enter A Serialized/Lotted Sequence Number (<Enter>=Next/<F4>=End)"
1430 GOSUB 7000
1435 IF V3=2 THEN GOTO 1420
1440 IF V3=4 THEN GOTO 4000
1450 IF NUM(V$)=0 THEN GOTO 1400
1460 LET T0$(21,3)=V$
1480 FIND (ARE23_DEV,KEY=T0$,DOM=1620)IOL=ARE23A
1485 FIND (IVM07_DEV,KEY=N0$+W0$(31,2)+W0$(33,20)+T1$,DOM=1490)IOL=IVM07A
1490 LET O9$=T1$,O9=T[0],H[1]=H[1]-O9
1500 GOSUB 5000
1530 GOTO 3300
1600 REM " --- Setup"
1610 IF O7=W[4] THEN GOTO 4000
1620 DIM T1$(20),T[2]
1630 IF P9$="S" THEN LET T[0]=1,T[1]=1 ELSE LET T[0]=W[2]-O7,T[1]=W[2]-O7
1635 IF P9$="S" AND W[4]<0 THEN LET T[0]=-1,T[1]=-1
1640 LET O9$="",O9=0
2000 REM " --- Enter Lot/Serial Number"
2020 LET V0$="S",V1$="CE",V2$=T1$(1,I[3]),V3$="",V4$="Enter a "+P8$,V0=I[3],V1=9,V2=L,I0=1
2030 IF Y0$(27,1)<>"Y" AND D2$(20,1)="Y" THEN LET V4$="Enter a valid "+P8$+" (<F3>=Lookup)"
2040 GOSUB 7000
2045 IF V3=2 THEN GOTO 3200
2050 IF V3=4 THEN GOTO 4000
2060 IF Y0$(27,1)="Y" OR D2$(20,1)<>"Y" THEN IF POS(" "<>V$)=0 THEN GOTO 2000
2080 IF Y0$(27,1)="Y" OR D2$(20,1)<>"Y" THEN LET T1$(1)=V$,T[2]=W[0]; GOTO 2290
2100 IF V3<>3 THEN GOTO 2210
2130 LET V8$="Z",H0$(1)=N0$+W0$(31,2)+W0$(33,20),H1$(1)=""; DIM H[12]
2135 IF W[2]<0 THEN IF P9$="S" THEN LET V8$="C" ELSE LET V8$=""
2140 CALL "SYC.LL",V8$,H0$,H1$,H[ALL]
2160 IF POS(" "<>H0$)=0 THEN GOTO 2000
2170 PRINT @(0,L+1),'CE'
2180 GOTO 2240
2200 REM " --- Find The Lots"
2210 LET T1$(1)=V$
2220 FIND (IVM07_DEV,KEY=N0$+W0$(31,2)+W0$(33,20)+T1$,DOM=2000)IOL=IVM07A
2225 IF O9$=T1$ THEN LET H[1]=H[1]-O9
2230 IF T[1]<0 THEN IF P9$="L" THEN GOTO 2275
2232 IF T[1]<0 AND H[0]-H[1]>0 THEN GOTO 2234 ELSE GOTO 2240
2234 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Warning!! Serial # is already in inventory (<Enter>=Retry)",V0=1,V1=FNV(V4$),V2=22
2236 GOSUB 7000
2238 GOTO 2000
2240 IF T[1]<0 OR H[0]-H[1]>0 THEN GOTO 2260
2245 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Quantity Not Available (<R>=Retry/<Enter>=Select)",V0=2,V1=FNV(V4$),V2=22
2250 GOSUB 7000
2255 IF V$="R" THEN GOTO 2000
2256 IF V$<>"" THEN GOTO 2245
2260 LET T1$(1)=H0$(25,I[3])
2270 IF H[0]-H[1]<T[0] THEN LET T[0]=H[0]-H[1],T[1]=T[0]
2275 LET T[2]=H[3]
2280 IF P9$="L" THEN PRINT @(9,L),T1$,@(Q[4],L),T[0]:M2$,@(Q[1],L),T[1]:M2$,@(77-M3,L),T[2]:M3$
2290 IF P9$="S" THEN PRINT @(9,L),T1$,@(77-M3),T[2]:M3$
2295 GOTO 3200
2400 REM " --- Order/Ship Qty"
2410 IF P9$="S" THEN GOTO 3200
2420 LET V0$="N",V1$="C",V2$=STR(T[0]),V3$=M2$,V4$="Enter Qty Ordered On This Line",V0=7,V1=Q[4],V2=L
2440 GOSUB 7000
2450 IF V3=2 OR V3=4 THEN GOTO 3200
2460 LET T[0]=V
2465 IF Y0$(27,1)<>"Y" AND D2$(20,1)="Y" AND T[0]<0 THEN GOTO 2480
2470 IF Y0$(27,1)<>"Y" AND D2$(20,1)="Y" AND T[0]>H[0]-H[1] THEN GOTO 2472 ELSE GOTO 2480
2472 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Quantity Not Available (<R>=Retry/<Enter>=Select)",V0=2,V1=FNV(V4$),V2=22
2474 GOSUB 7000
2476 IF V$="R" THEN GOTO 2400
2477 IF V$<>"" THEN GOTO 2472
2480 LET T[1]=T[0]; PRINT @(Q[1],L),T[1]:M2$; LET I0=3
2490 GOTO 3200
2600 REM " --- Ship Qty"
2610 IF P9$="S" THEN GOTO 3200
2620 LET V0$="N",V1$="C",V2$=STR(T[1]),V3$=M2$,V4$="Enter Qty Shipped On This Line",V0=7,V1=Q[1],V2=L
2640 GOSUB 7000
2650 IF V3=2 OR V3=4 THEN GOTO 3200
2660 LET T[1]=V
2665 IF Y0$(27,1)<>"Y" AND D2$(20,1)="Y" AND T[1]<0 THEN GOTO 2680
2670 IF Y0$(27,1)<>"Y" AND D2$(20,1)="Y" AND T[1]>H[0]-H[1] THEN GOTO 2672 ELSE GOTO 2680
2672 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Quantity Not Available (<R>=Retry/<Enter>=Select)",V0=2,V1=FNV(V4$),V2=22
2674 GOSUB 7000
2676 IF V$="R" THEN GOTO 2600
2677 IF V$<>"" THEN GOTO 2672
2690 GOTO 3200
3200 REM " --- Seq Control"
3210 IF V3=4 THEN IF I0>1 THEN LET I0=4; GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,2000,2400,2600,3300
3300 REM " --- Correct"
3320 LET V0$="",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=T0$,V4$="<Enter>=OK     <D>=Delete"
3330 GOSUB 7000
3340 IF V3=2 THEN LET I0=4; GOTO 3200
3360 IF V$<>"" AND V$<>"D" THEN GOTO 3300
3370 IF V$="D" THEN GOTO 3500
3400 REM " --- Write"
3410 IF O9$=T1$ AND O9=T[0] THEN GOTO 3460
3415 IF O9$="" THEN GOTO 3440
3420 LET O$=O9$,O8=O9,I=-1
3430 GOSUB 3600
3440 LET O$=T1$,O8=T[0],I=1
3450 GOSUB 3600
3455 LET O9$=T1$,O9=T[0]
3460 LET L=L+1; PRINT @(0,21),'LD','LD'
3480 WRITE (ARE23_DEV,KEY=T0$)IOL=ARE23A
3485 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3500 REM " --- Delete"
3510 REMOVE (ARE23_DEV,KEY=T0$,DOM=3520)
3520 PRINT @(0,L),'CL',@(0,21),'LD','LD',
3540 IF O9$="" THEN GOTO 3590
3550 LET O$=O9$,O8=O9,I=-1
3570 GOSUB 3600
3590 GOTO 1000
3600 REM " --- Commit / Decommit / Total"
3610 IF Y0$(27,1)="Y" OR D2$(20,1)<>"Y" THEN GOTO 3680
3620 LET IV_INFO$[1]=W0$(31,2),IV_INFO$[2]=W0$(33,20),IV_INFO$[3]="",IV_REFS[0]=O8
3630 IF I>0 THEN LET IV_ACTION$="UC" ELSE LET IV_ACTION$="OE"
3640 CALL "IVC.UA",IV_ACTION$,IV_FILES[ALL],IV_INFO[ALL],IV_PARAMS$[ALL],IV_INFO$[ALL],IV_REFS$[ALL],IV_REFS[ALL],IV_STATUS; REM "Undo previous warehouse action
3650 LET IV_INFO$[3]=O$
3660 IF I>0 THEN LET IV_ACTION$="OE" ELSE LET IV_ACTION$="UC"
3670 CALL "IVC.UA",IV_ACTION$,IV_FILES[ALL],IV_INFO[ALL],IV_PARAMS$[ALL],IV_INFO$[ALL],IV_REFS$[ALL],IV_REFS[ALL],IV_STATUS; REM "Do warehouse and serial/lot action
3680 LET O7=O7+O8*I
3690 RETURN 
4000 REM " --- Options"
4002 PRINT @(0,L),'CE',
4003 CALL "SYC.WC",1,0,80,0,0,20,0
4004 IF O7=W[4] THEN GOTO 4010
4005 LET V4$="WARNING: "+STR(O7)+" "+P8$+"'s Assigned. Total ordered = "+STR(W[4])+" (<Enter> To Continue.)"
4006 LET V0$="S",V1$="C",V2$="",V3$="",V0=2,V1=FNV(V4$),V2=22
4007 GOSUB 7000
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="Is The "+P8$+" Information Correct (Y/N/Review/List/Delete)?",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4050 IF V$="L" OR V$="LIST" THEN GOTO 4400
4060 IF V$="DELETE" THEN GOTO 4200
4070 IF V$="" THEN LET V$="Y"
4080 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- OK Here"
4110 PRINT @(72,ROW),'CL',@(0,L1+2),'CE',; LET L=L1+2
4120 LET L=L1+2,O1=1,K9$=W0$(1,20),L9=18,V$="",W0=NUM(W0$(18,3)),V3=0,T0=0,T1=0
4125 REM " --- Calc Unit Cost/Qty Shipped"
4130 READ (ARE23_DEV,KEY=W0$(1,20),DOM=4140)
4140 LET K$=KEY(ARE23_DEV,END=4180)
4150 IF K$(1,20)<>W0$(1,20) THEN GOTO 4180
4160 READ (ARE23_DEV)IOL=ARE23A
4170 LET T0=T0+T[1],T1=T1+T[1]*T[2]
4175 GOTO 4140
4180 IF O7<>W[4] THEN LET O1=2,I0=10,L=L1
4183 LET W[4]=T0; IF W[4]<>0 THEN LET W[0]=T1/W[4]
4188 LET O$=W0$(21,1)+Y0$(25,1)+Y0$(27,1)+W0$(31,2)+W0$(33,20),O[0]=W[2],O[1]=W[6],O[2]=W[7],O1$=""
4190 RUN PROGRAM$,ERR=9900
4200 REM " --- Delete All Assignments Here"
4210 READ (ARE23_DEV,KEY=W0$(1,20),DOM=4220)
4220 LET K$=KEY(ARE23_DEV,END=4290)
4230 IF K$(1,20)<>W0$(1,20) THEN GOTO 4290
4240 READ (ARE23_DEV)IOL=ARE23A
4250 LET O$=T1$,O8=T[0],I=-1
4260 GOSUB 3600
4270 REMOVE (ARE23_DEV,KEY=K$)
4280 GOTO 4220
4290 GOTO 4100
4400 REM " --- List"
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4620 READ (ARE23_DEV,KEY=W0$(1,20),DOM=4630)
4640 GOSUB 5400
4650 LET V3=0
4690 GOTO 1000
5000 REM " --- Display"
5010 IF T0$<=K9$ THEN GOSUB 5400
5040 IF P9$="L" THEN PRINT @(4,L),T0$(21,3),@(9,L),T1$,@(Q[4],L),T[0]:M2$,@(Q[1],L),T[1]:M2$,@(77-M3,L),T[2]:M3$
5060 IF P9$="S" THEN PRINT @(4,L),T0$(21,3),@(9,L),T1$,@(77-M3,L),T[2]:M3$
5080 LET K9$=T0$
5090 RETURN 
5100 REM " --- List"
5105 LET Q5=0
5110 GOSUB 5400
5120 READ (ARE23_DEV,KEY=W0$(1,20),DOM=5130)
5130 LET K$=KEY(ARE23_DEV,END=5290)
5135 IF LEN(K$)<20 THEN GOTO 5290
5140 IF K$(1,20)<>W0$(1,20) THEN GOTO 5290
5150 READ (ARE23_DEV)IOL=ARE23A
5160 LET Q5=1
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Press <Enter> To Continue",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5225 IF V3=4 THEN GOTO 5290
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+1
5280 GOTO 5130
5290 RETURN 
5400 REM " --- Clear Screen
5410 FOR X=L1+2 TO L9+1
5420 PRINT @(0,X),'CL',
5430 NEXT X
5440 LET L=L1+2
5490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0001 SETERR ERRMSG
0010 REM <extract_log.bbx>


0020 REM MKEYED "logextract.dat",[1:13],0,297
0030 LET FLEXHOME$=STBL("FLEXHOME"),AON$=STBL("AON")

0040 LET TMPFILE$=FLEXHOME$+"log/messages.0"

0050 DIM SNT01$:"SERIAL_NBR:C(20),TRANS_DATE:C(3),SN_DET_SEQ:C(5*),CUSTOMER_NBR:C(6),INV_NBR:C(7),SEQUENCE_NBR:C(3),TYPE_OF_SALE:C(3),PRODUCT_REV:C(3),AUTH_CODE:C(15),SERVER_NBR:C(20*),FIRM_ID:C(2),ACTION:C(1),FIXED_FLOAT:C(1),DEALER_STR:C(36*),USERS:N(5*),EXT_PRICE:N(7*),LICENSE_CNT:N(7*),NUM1:N(1*),NUM2:N(1*),NUM3:N(1*)"
0060 DIM ARE83$:"FIRM_ID:C(2),TYPE:C(2),CUSTOMER_NBR:C(6),ORDER_NUMBER:C(7),LINE_NUMBER:C(3),LICENSE_SEQ:C(3),SEQUENCE_NBR:C(3*),ACTION:C(1),FIXED_FLOAT:C(1),AUTH_CODE:C(15),ACT_KEY:C(15),DEALER_STR:C(8*),SERIAL_NBR:C(20*),LICENSE_CNT:N(7*),DEALER_NUM_1:N(1*),DEALER_NUM_2:N(1*)"

0070 LET ADATA$=stbl("DATA_SERVER")+STBL("AON")+"ADATA/",SNT01CHAN=UNT
0075 OPEN (SNT01CHAN)ADATA$+"SNT-01"
0080 LET ARE83CHAN=UNT; OPEN (ARE83CHAN)ADATA$+"ARE-83"

0090 LET CO$="01"
0100 LET LOGX=UNT; OPEN (LOGX)ADATA$+"logextract.dat"

0110 GOSUB CLEAR_DATA

0120 LET MO$="JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC",Y1$=DATE(0:"%Yd"),D2=JUL(0,0,0)-1,Y2$=DATE(D2:"%Yd"),D1$=DATE(0:"%Dz"),D2$=" "+DATE(D2:"%D")+" "

0130 LOGFILE: 
0140 LET TMPCHN=UNT; OPEN (TMPCHN,ERR=ERRMSG)TMPFILE$

0150 READ_TMP: 
0160 IF CVS(LOGEX.SERIAL_NUM$,2)<>"" AND CVS(LOGEX.AUTH_NUM$,2)<>"" THEN GOSUB VERIFY

0170 READ (TMPCHN,END=TMP_END)TMPREC$
0180 rem ' IF POS("DATE: "=CVS(TMPREC$,7))=0 AND POS("FROM LICENSE@"=CVS(TMPREC$,39))=0 THEN GOTO FPOS
0185 IF POS("run @"  = TMPREC$) = 0 THEN GOTO FPOS
0190 LET D$=CVS(TMPREC$,39)
0200 FOR M=1 TO 12; LET MP=POS(MO$((M-1)*3+1,3)=D$); IF MP THEN LET MN=M,M=13
0210 NEXT M; IF MP=0 THEN GOTO FPOS

0220 IF POS(D2$=D$) THEN LET DA$ = str(num(cvs(D2$,3)):"00") ELSE LET DA$=D1$
0230 IF POS(Y2$=D$(MP)) THEN LET YR$=Y2$ ELSE LET YR$=Y1$
0240 LET LOGEX.DATE$=YR$+STR(MN:"00")+DA$,D$=D$(MP),T=POS(":"=D$)
0250 IF T>2 AND POS(":"=D$(T+1))=3 THEN LET LOGEX.TIME$=D$(T-2,5)
0260 GOTO READ_TMP

0270 FPOS: 
0280 LET F=POS("FROM: "=CVS(TMPREC$,7)); IF F THEN LET LOGEX.FROM$=TMPREC$(F+6); LET F=POS("<"=TMPREC$); IF F THEN LET LOGEX.FROM$=TMPREC$(F)
0290 LET C=POS(":"=TMPREC$); IF C<2 OR LEN(TMPREC$)<C+1 THEN GOTO READ_TMP
0300 LET TMPCOL$=CVS(TMPREC$(1,C-1),7),TMPINFO$=CVS(TMPREC$(C+1),3)
0310 LET SW=POS(TMPCOL$="COMPANY            :LAST NAME          :FIRST NAME         :PHONE NUMBER       :FAX NUMBER         :EMAIL ADDRESS      :HOST NAME          :HOST ID            :SERIAL NUMBER      :LICENSE AUTH NUMBER:")
0320 IF SW=0 THEN GOTO READ_TMP
0330 LET PRT$=PRT$+TMPREC$+$0A$
0340 SWITCH INT(SW/20)+1
0350 CASE 1; LET LOGEX.COMPANY$=TMPINFO$; BREAK
0360 CASE 2; LET LOGEX.LASTNAME$=TMPINFO$; BREAK
0370 CASE 3; LET LOGEX.FIRSTNAME$=TMPINFO$; BREAK
0380 CASE 4; LET LOGEX.PHONE$=TMPINFO$; BREAK
0390 CASE 5; LET LOGEX.FAX$=TMPINFO$; BREAK
0400 CASE 6; LET LOGEX.EMAIL$=TMPINFO$; BREAK
0410 CASE 7; LET LOGEX.HOSTNAME$=TMPINFO$; BREAK
0420 CASE 8; LET LOGEX.HOSTID$=TMPINFO$; BREAK
0430 CASE 9; LET T$=TMPINFO$+FILL(12),LOGEX.SERIAL_NUM$=T$(1,12); BREAK
0440 CASE 10; IF CVS(LOGEX.SERIAL_NUM$,2)<>"" THEN LET LOGEX.AUTH_NUM$=TMPINFO$ FI ; BREAK
0450 SWEND
0460 GOTO READ_TMP
0470 TMP_END: 
0480 GOTO EXIT

0490 CLEAR_DATA: 
0500 DIM LOGEX$:"SERIAL_NUM:C(12),SEQ_NUM:C(1),GEN_COUNT:C(1),DATE:C(8),TIME:C(5),FROM:C(40),HOSTID:C(40),COMPANY:C(40),LASTNAME:C(20),FIRSTNAME:C(20),PHONE:C(20),FAX:C(20),EMAIL:C(40),AUTH_NUM:C(10),HOSTNAME:C(20)"
0510 LET PRT$=""
0520 RETURN

0530 VERIFY: 
0540 LET TESTSN$="BOD107754   PRO560079   PRO560080   PRO560081   PRO560082   PRO560083   "
0550 IF POS(LOGEX.SERIAL_NUM$=TESTSN$) THEN GOTO VERIFY_RETURN
0560 REM Check to see if a license has been generated for this SN
0570 LET LICFILE$=FLEXHOME$+"lic/"+CVS(LOGEX.SERIAL_NUM$,2)+".lic"
0580 LET LFCHAN=UNT; OPEN (LFCHAN,ERR=VERIFY_RETURN)LICFILE$
0590 CLOSE (LFCHAN)
0600 LET FOUND_SN=0; READ (ARE83CHAN,KEY=CO$,DOM=READ_ARE83)
0610 READ_ARE83: 
0620 READ RECORD (ARE83CHAN,END=END_ARE83)ARE83$
0630 IF CVS(LOGEX.SERIAL_NUM$,2)<>CVS(ARE83.SERIAL_NBR$,2) THEN GOTO READ_ARE83
0640 IF LOGEX.AUTH_NUM$<>CVS(ARE83.AUTH_CODE$,2) THEN GOTO READ_ARE83
0645 IF ARE83.ACTION$="D" THEN GOTO READ_ARE83
0650 LET FOUND_SN=1,LOGEX.GEN_COUNT$=STR(ARE83.LICENSE_CNT:"0")
0660 END_ARE83: 
0670 IF FOUND_SN THEN GOTO WRITE_REC
0680 DIM HOLD_DETAIL$:FATTR(SNT01$)
0690 READ (SNT01CHAN,KEY=CVS(LOGEX.SERIAL_NUM$,2),ERR=READ_SNT01)
0700 READ_SNT01: 
0710 LET SNT01KEY$=KEY(SNT01CHAN,END=SNT01END)
0720 IF POS(CVS(LOGEX.SERIAL_NUM$,2)=SNT01KEY$)<>1 THEN GOTO SNT01END
0730 READ RECORD (SNT01CHAN,KEY=SNT01KEY$)SNT01$
0740 IF SNT01.ACTION$="D" THEN GOTO READ_SNT01
0745 IF CVS(SNT01.AUTH_CODE$,3)="" THEN GOTO READ_SNT01
0750 LET HOLD_DETAIL$=FIELD(SNT01$)
0760 GOTO READ_SNT01
0770 SNT01END: 
0780 IF LOGEX.AUTH_NUM$<>CVS(HOLD_DETAIL.AUTH_CODE$,3) THEN GOTO VERIFY_RETURN
0790 LET LOGEX.GEN_COUNT$=STR(HOLD_DETAIL.LICENSE_CNT:"0")
0800 WRITE_REC: 
0810 IF CVS(LOGEX.HOSTID$,7)="TEMPORARY*" THEN GOTO VERIFY_RETURN
0820 LET SEQ$=""; READ (LOGX,KEY=LOGEX.SERIAL_NUM$,DOM=READ_LOGX)
0830 READ_LOGX: 
0840 READ RECORD (LOGX,END=WRITEIT)L$; IF L$(1,12)<>LOGEX.SERIAL_NUM$ THEN GOTO WRITEIT
0850 IF CVS(L$(14),4)=CVS(LOGEX$(14),4) THEN GOTO VERIFY_RETURN; REM already recorded this request
0860 REM same request sent more than once - write to original record with latest date&time
0870 IF L$(28)=LOGEX$(28) THEN LET LOGEX.SEQ_NUM$=L$(13,1); WRITE RECORD (LOGX)LOGEX$; GOTO VERIFY_RETURN
0880 LET SEQ$=L$(13,1); GOTO READ_LOGX
0890 WRITEIT: 
0892 FOR I=1 TO LEN(LOGEX$); LET A=ASC(LOGEX$(I,1))
0894 IF A<32 OR A>254 THEN LET LOGEX$(I,1)=" "
0896 NEXT I
0898 if cvs(logex.date$,3) = "" then logex.date$ = date(jul(0,0,0)-1:"%Y%Mz%Dz")
0900 REM only allow seq 0-9, then overwrite 9th rec- if seq$="" (s=0) then a record for the SN doesn't already exist
0910 LET S=POS(SEQ$="0123456789"); IF S<10 THEN LET SEQ$=STR(S:"0")
0920 LET LOGEX.SEQ_NUM$=SEQ$; WRITE RECORD (LOGX)LOGEX$
0930 LET MSG$=DATE(0:"%Mz/%Dz/%Yd ")+LOGEX.SERIAL_NUM$+"seq "+SEQ$+" extracted from "+TMPFILE$
0940 rem LET A=SCALL(AON$+"email_alert mis@basis.cloud "+$22$+MSG$+$22$+" "+AON$+"tmp/emailalert.txt")
0950 VERIFY_RETURN: 
0960 GOSUB CLEAR_DATA
0970 RETURN
0980 ERRMSG: 
0990 LET MSG$=DATE(0:"%Mz/%Dz/%Yd")+" Err "+STR(ERR)+" in "+STR(TCB(5))+" "+PGM(-2)
1000 LET A=SCALL(AON$+"email_alert kurt.e.williams@comcast.net "+$22$+MSG$+$22$+" "+AON$+"tmp/emailalert.txt")
1010 EXIT: 
1020 SETERR EXIT2
1030 IF TMPCHN THEN CLOSE (TMPCHN)
1040 IF POS(".0"=TMPFILE$)=LEN(TMPFILE$)-1 THEN LET TMPFILE$=TMPFILE$(1,LEN(TMPFILE$)-2); GOTO LOGFILE
1050 IF ARE83CHAN THEN CLOSE (ARE83CHAN)
1060 IF SNT01CHAN THEN CLOSE (SNT01CHAN)
1070 IF LOGX THEN CLOSE (LOGX)
1080 EXIT2: 
1090 RELEASE

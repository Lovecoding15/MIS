0001 SETERR ERRMSG
0010 REM <match_sn_an.bbx>
0020 SETERR INVOKED_PGM
0030 LET CALLED_PGM=0
0040 ENTER SERIALNUM$,AUTHNUM$; LET CALLED_PGM=1
0050 LET SERIALNUM$=CVS(SERIALNUM$,7)+FILL(20),SERIALNUM$=SERIALNUM$(1,20),AUTHNUM$=CVS(AUTHNUM$,3)
0060 GOTO END_INPUT
0070 INVOKED_PGM: 
0080 LET ARGNUM=1
0090 GETARGV: 
0100 LET ARG$=ARGV(ARGNUM,ERR=END_INPUT)
0110 SWITCH ARGNUM
0120 CASE 1; LET SERIALNUM$=CVS(ARG$,7)+FILL(20),SERIALNUM$=SERIALNUM$(1,20); BREAK
0130 CASE 2; LET AUTHNUM$=CVS(ARG$,3); BREAK
0140 CASE DEFAULT; BREAK
0150 SWEND
0160 LET ARGNUM=ARGNUM+1
0170 GOTO GETARGV
0180 END_INPUT: 
0190 SETERR ERRMSG
0200 LET MSG$="",MATCHED=0
0210 IF CVS(SERIALNUM$,3)="" THEN LET MSG$="No serial number"; GOTO ERRMSG
0220 IF LEN(AUTHNUM$)<>10 THEN LET MSG$="Invalid authorization number"; GOTO ERRMSG
0230 REM Authnum$ is set to "TEMPORARY*" by an e-commerce program to generate a temporary license for the registration form
0240 IF CVS(AUTHNUM$,4)="TEMPORARY*" THEN LET MATCHED=1; GOTO EXIT
0250 DIM SNM01$:"SERIAL_NBR:C(20*),SERIAL_DESC:C(60*),PRODUCT:C(3),PLATFORM:C(3),OS_LEVEL:C(3),PRODUCT_REV:C(3),LICENSE_TYPE:C(3),MEDIA_TYPE:C(3*),ACTIVE_FLAG:C(1),ORIG_SALE:C(3),CUSTOMER_NBR:C(6),PORT_ID:C(5),FIRM_ID:C(2),DEALER_STR:C(18*),COMMENT:C(60*),USERS:N(5*),DEALER_NUM_1:N(1*),DEALER_NUM_2:N(1*)"
0260 DIM SNT01$:"SERIAL_NBR:C(20),TRANS_DATE:C(3),SN_DET_SEQ:C(5*),CUSTOMER_NBR:C(6),INV_NBR:C(7),SEQUENCE_NBR:C(3),TYPE_OF_SALE:C(3),PRODUCT_REV:C(3),AUTH_CODE:C(15),SERVER_NBR:C(20*),FIRM_ID:C(2),ACTION:C(1),FIXED_FLOAT:C(1),DEALER_STR:C(36*),USERS:N(5*),EXT_PRICE:N(7*),LICENSE_CNT:N(7*),NUM1:N(1*),NUM2:N(1*),NUM3:N(1*)"
0270 DIM ARE83$:"FIRM_ID:C(2),TYPE:C(2),CUSTOMER_NBR:C(6),ORDER_NUMBER:C(7),LINE_NUMBER:C(3),LICENSE_SEQ:C(3),SEQUENCE_NBR:C(3*),ACTION:C(1),FIXED_FLOAT:C(1),AUTH_CODE:C(15),ACT_KEY:C(15),DEALER_STR:C(8*),SERIAL_NBR:C(20*),LICENSE_CNT:N(7*),DEALER_NUM_1:N(1*),DEALER_NUM_2:N(1*)"
0280 LET AON$=STBL("AON"),ADATA$=stbl("DATA_SERVER")+AON$+"ADATA/",TDATA$=ADATA$
0290 LET SNM01CHAN=UNT; OPEN (SNM01CHAN)ADATA$+"SNM-01"
0300 LET SNT01CHAN=UNT; OPEN (SNT01CHAN)ADATA$+"SNT-01"
0310 EC_START: IF POS("ADATA"=TDATA$) THEN GOTO OPEN_ARE83
0320 IF ARE83CHAN THEN CLOSE (ARE83CHAN)
0330 OPEN_ARE83: 
0340 LET FOUND_SN=0,ARE83CHAN=UNT; OPEN (ARE83CHAN)TDATA$+"ARE-83"
0350 LET ARE83KEY$=""; READ (ARE83CHAN,KEY="",DOM=READ_ARE83)
0360 READ_ARE83: 
0370 LET K$=KEY(ARE83CHAN,END=END_ARE83)
0380 READ RECORD (ARE83CHAN,KEY=K$)ARE83$
0390 IF CVS(SERIALNUM$,2)<>CVS(ARE83.SERIAL_NBR$,2) THEN GOTO READ_ARE83
0395 IF CVS(ARE83.AUTH_CODE$,3)="" THEN GOTO READ_ARE83
0400 IF AUTHNUM$=CVS(ARE83.AUTH_CODE$,3) THEN LET ARE83KEY$=K$
0410 LET FOUND_SN=1; GOTO READ_ARE83
0420 END_ARE83: 
0430 REM If serial# not in ADATA, check ecdata (skip for now-7/10/00)
0440 REM IF POS("ADATA"=TDATA$) AND ARE83KEY$="" THEN LET TDATA$="/basisaon/aon/ecdata/"; GOTO EC_START
0450 IF FOUND_SN AND ARE83KEY$<>"" THEN LET MATCHED=1; GOTO EXIT
0470 SNM01_CHECK: 
0480 LET OK=0; READ RECORD (SNM01CHAN,KEY=SERIALNUM$,DOM=SNM01END)SNM01$; LET OK=1
0490 SNM01END: IF OK=0 THEN GOTO OEM_CHECK
0500 DIM HOLD_DETAIL$:FATTR(SNT01$)
0510 READ (SNT01CHAN,KEY=SERIALNUM$,ERR=READ_SNT01)
0520 READ_SNT01: 
0530 LET SNT01KEY$=KEY(SNT01CHAN,END=SNT01END)
0540 IF SNT01KEY$(1,20)<>SERIALNUM$ THEN GOTO SNT01END
0550 READ RECORD (SNT01CHAN,KEY=SNT01KEY$)SNT01$
0560 IF SNT01.ACTION$="D" THEN GOTO READ_SNT01
0565 IF CVS(SNT01.AUTH_CODE$,2)="" THEN GOTO READ_SNT01
0570 IF AUTHNUM$=CVS(snt01.AUTH_CODE$,2) THEN LET MATCHED=1
0580 GOTO READ_SNT01
0590 SNT01END: 
0610 GOTO EXIT
0620 OEM_CHECK: 
0630 LET OEMDIR$=ADATA$
0640 DIM OEM$:"serial_num:c(9),tran:c(4),date:c(8),item_num:c(8),rev_level:c(5),prod_type:c(1),sale_type:c(1),active_flag:c(1),temp_lic_flag:c(1),lic_auth:c(13),memo:c(20),float_flag:c(1),tran_type:c(3),available:c(26*),cur_user:n(4*),new_user:n(4*),lic_count:n(1*),num1:n(1*),num2:n(1*)"
0650 LET OEM=UNT; OPEN (OEM)OEMDIR$+"oem_sn_trans.dat"
0660 LET FOUND=0,SERIALNUM$=CVS(SERIALNUM$,2); READ (OEM,KEY=SERIALNUM$,DOM=READ_OEM)
0670 READ_OEM: READ RECORD (OEM,END=END_OEM)OEM$
0680 IF OEM.SERIAL_NUM$=SERIALNUM$ THEN LET FOUND=1,AN$=OEM.LIC_AUTH$; GOTO READ_OEM
0690 END_OEM: IF FOUND=0 THEN GOTO EXIT
0700 IF AUTHNUM$=CVS(AN$,2) THEN LET MATCHED=1
0710 GOTO EXIT
0720 ERRMSG: 
0730 IF MSG$="" THEN LET MSG$="Err "+STR(ERR)+" in "+STR(TCB(5))
0740 EXIT: 
0750 LET AUTHNUM$="",SERIALNUM$="No"; IF MATCHED THEN LET SERIALNUM$="Yes"
0760 IF CALLED_PGM THEN EXIT
0770 PRINT CVS(SERIALNUM$,2)
0780 RELEASE

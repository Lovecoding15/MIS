0010 REM "IVM - Set flags in IVM-01 based on CSM flags"
0020 REM "Program BIV.01"
0030 REM 
0040 REM "CSMKEY$    : Key of CSM file- 1,2=firm  3,3=code"
0050 REM "FLAG$	     : Value to put in IVM-01 (1 byte) at FLAGPOS"
0060 REM "ITEMPOS    : Position in IVM-01 item num to check for CSMKEY$(3,3)"
0070 REM "FLAGPOS    : Position in IVM-01 A2$ string to put FLAG$"
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER CSMKEY$,FLAG$,ITEMPOS,FLAGPOS
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="IVM-01",FILES$[2]="CSM-05"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET CHAN1=CHANNELS[1],CHAN2=CHANNELS[2]
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$(1),A6$(1),A[ALL]
0220 CSM05A: IOLIST B0$,*,B2$
0500 REM " --- Initializations"
0510 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A5$(2),A6$(90),A[12]
0550 LET K$=CSMKEY$(1,2)
1000 REM " --- Display message"
1010 PRINT @(0,22),'CL',@(10,22),"Inventory masterfile flags-"
1100 REM " --- Read IVM-01"
1110 READ (CHAN1,KEY=K$,DOM=1120)
1120 LET K$=KEY(CHAN1,END=4000); IF CSMKEY$(1,2)<>K$(1,2) THEN GOTO 4000
1130 IF POS(CSMKEY$(3,3)=K$)<>ITEMPOS+2 THEN GOTO 1100
1134 REM Check license type flag if we are settin online flag based on product- it overrides product flag
1135 IF FLAGPOS=56 AND ITEMPOS=1 THEN READ (CHAN2,KEY=K$(1,2)+A0$(13,3),DOM=1140)IOL=CSM05A; IF B2$(2,1)="N" AND FLAG$="Y" THEN GOTO 1100
1140 READ (CHAN1,KEY=K$)IOL=IVM01A
1150 IF A2$(FLAGPOS,1)=FLAG$ THEN GOTO 1100
1160 IF FLAGPOS<>56 OR A2$(FLAGPOS,1)=" " THEN GOTO 1180
1165 PRINT @(0,23),'CL',@(10,23),"Change ",A2$(FLAGPOS,1)," to ",FLAG$," for ",CVS(K$(3),2),"? <Enter=Yes, N=No> "; INPUT @(70,23),X$,
1170 PRINT @(0,23),'CL',; IF CVS(X$,4)="N" THEN GOTO 1100
1180 EXTRACT (CHAN1,KEY=K$)IOL=IVM01A; LET A2$(FLAGPOS,1)=FLAG$; WRITE (CHAN1,KEY=K$)IOL=IVM01A
1190 GOTO 1100
4000 REM " --- All done"
4010 PRINT @(0,22),'CL',
4090 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9910 CLOSE (CHAN1,ERR=9911)
9920 CLOSE (CHAN2,ERR=9921)
9950 EXIT 
9999 END

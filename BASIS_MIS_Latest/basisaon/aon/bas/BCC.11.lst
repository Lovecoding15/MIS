0010 REM " Program BCC.11 - Credit Card encrypt/decrypt 
0020 REM "To encrypt, UNENCRYPTED$=plain data, ENCRYPTED$=""
0030 REM "To decrypt, ENCRYPTED$=encrypted data, UNENCRYPTED$=""
0040 SETERR EXIT
0050 SETESC EXIT
0060 ENTER UNENCRYPTED$,ENCRYPTED$
0070 LET M=MAX(LEN(UNENCRYPTED$),LEN(ENCRYPTED$))
0080 LET N=MAX(LEN(CVS(UNENCRYPTED$,2)),LEN(CVS(ENCRYPTED$,2)))
0090 LET XX$=""
0100 FOR I=34 TO 126 STEP 2; LET XX$=XX$+CHR(I); NEXT I
0110 FOR I=125 TO 33 STEP -2; LET XX$=XX$+CHR(I); NEXT I
0120 LET A$=UNENCRYPTED$+FILL(M),B$=ENCRYPTED$+FILL(M),I=2,L=MIN(N,30)
0130 WHILE I<=L
0140 LET P=POS(A$(I,1)=XX$)
0150 IF P THEN LET P=P+I+L; IF P>LEN(XX$) THEN LET P=P-LEN(XX$) FI; LET A$(I,1)=XX$(P,1)
0160 LET P=POS(B$(I,1)=XX$)
0170 IF P THEN LET P=P-I-L; IF P<1 THEN LET P=LEN(XX$)+P FI; LET B$(I,1)=XX$(P,1)
0180 LET I=I+1
0190 WEND
0200 EXIT: 
0210 IF M AND CVS(UNENCRYPTED$,3)="" THEN LET UNENCRYPTED$=B$(1,M)
0220 IF M AND CVS(ENCRYPTED$,3)="" THEN LET ENCRYPTED$=A$(1,M)
0230 EXIT 

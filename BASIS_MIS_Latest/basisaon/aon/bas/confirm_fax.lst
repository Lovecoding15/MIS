0010 REM confirm_fax.bbx
0020 REM called by DAILY_I.RM and CONFIRM
0030 SETERR EXIT
0040 ENTER FAXTEXT$
0050 IF CVS(FAXTEXT$,2)="" THEN GOTO EXIT
0060 LET PAGEBREAK$="~~pagebreak~~"+$0A$
0070 LET CNUM$=FAXTEXT$(1,6),FAXTEXT$(1,6)=FILL(7)
0080 LET C=POS("I N V O I C E"=FAXTEXT$); IF C THEN LET SUBJ$="Invoice confirmation- "+CNUM$ ELSE LET SUBJ$="Order confirmation- "+CNUM$
0090 LET C=POS("TO:"=FAXTEXT$),CNAME$=CNUM$; IF C THEN LET CNAME$=CNAME$+" "+FAXTEXT$(C+4,30)
0100 LET FAXFILE$=STBL("M90_DATA")+CNUM$+SUBJ$(1,1)+".fax"
0110 ERASE FAXFILE$,ERR=OPEN_FAX
0120 OPEN_FAX: 
0130 LET FAXNUM$="",SPEED$="",SPEEDENTRY=0
0140 SPEEDDIAL: 
0150 LET E1=UNT; OPEN (E1,ERR=END_SPEEDDIAL)"/u/docfiles/cs/SPEEDDIAL.txt"
0160 READ_SPEEDDIAL: 
0170 READ (E1,END=END_SPEEDDIAL)S$
0180 LET S=POS("0"+CNUM$=S$); IF S<>1 OR SPEEDENTRY THEN GOTO 0210
0190 IF POS("-"=S$)=8 THEN LET SPEED$="#"+S$(9)
0200 IF POS("@"=S$) THEN LET FAXNUM$=S$(9); GOTO END_SPEEDDIAL
0210 LET S=POS(SPEED$=S$); IF S AND SPEED$<>"" THEN LET FAXNUM$=S$(LEN(SPEED$)+2)
0220 GOTO READ_SPEEDDIAL
0230 END_SPEEDDIAL: 
0240 IF E1 THEN CLOSE (E1)
0250 EDIT_FAXNUM: 
0260 LET F$=""; IF LEN(FAXNUM$)<7 OR POS("@"=FAXNUM$) THEN GOTO WINDOW
0270 FOR A=1 TO LEN(FAXNUM$); IF POS(FAXNUM$(A,1)="0123456789") THEN LET F$=F$+FAXNUM$(A,1) FI; NEXT A; LET FAXNUM$=F$
0280 IF FAXNUM$(1,1)="1" OR FAXNUM$(1,3)="011" OR LEN(FAXNUM$)=7 THEN GOTO WINDOW
0290 IF LEN(FAXNUM$)<>10 THEN LET FAXNUM$="011"+FAXNUM$
0300 IF LEN(FAXNUM$)=10 THEN IF FAXNUM$(1,3)="525" THEN LET FAXNUM$="011"+FAXNUM$ ELSE LET FAXNUM$="1"+FAXNUM$
0310 WINDOW: 
0320 PRINT 'WINDOW'(3,8,72,6,""),
0330 PRINT 'CS',@(2,0),CNAME$,@(2,1),FAXNUM$,
0340 PRINT @(2,3),"OK? (CR/N)",; INPUT @(14,3),I$,; IF CVS(I$,4)<>"N" THEN GOTO POP
0350 PRINT 'CS',@(2,0),CNAME$,@(2,1),FAXNUM$,
0360 PRINT @(2,2),"Enter fax#/speeddial#/email addr/spaces to skip.",
0370 INPUTE 2,1,65,"_",FAXNUM$; LET I$="edit"
0380 POP: 
0390 PRINT 'POP',
0400 IF POS("#"=FAXNUM$)=1 THEN LET SPEED$=FAXNUM$,FAXNUM$="",SPEEDENTRY=1; GOTO SPEEDDIAL
0410 IF CVS(FAXNUM$,2)="" THEN GOTO EXIT
0420 IF I$<>"edit" THEN GOTO MAKEFILE
0430 IF POS("@"=FAXNUM$) THEN GOTO WINDOW ELSE GOTO EDIT_FAXNUM
0440 MAKEFILE: 
0450 STRING FAXFILE$
0460 LET FAXCHAN=UNT; OPEN (FAXCHAN)FAXFILE$
0470 IF POS("@"=FAXNUM$) THEN LET FAX$="To: "+FAXNUM$ ELSE LET FAX$="To: fax@basis.cloud",SUBJ$=FAXNUM$+"; "+SUBJ$
0480 REM IF POS("@"=FAXNUM$) THEN LET FAX$="To: "+FAXNUM$ ELSE LET FAX$="To: "+FAXNUM$+" <fax@basis.cloud>"
0490 LET FAX$=FAX$+$0A$+"From: customer-service@basis.cloud"+$0A$+"Subject: "+SUBJ$+$0A$+$0A$
0500 LET C=POS(CNUM$=FAXTEXT$)
0510 IF C THEN LET FAX$=FAX$+FAXTEXT$(1,C-1)+PAGEBREAK$,FAXTEXT$=FILL(LEN(CNUM$))+FAXTEXT$(C+LEN(CNUM$)); GOTO 0500 ELSE LET FAX$=FAX$+FAXTEXT$
0520 WRITE RECORD(FAXCHAN)FAX$
0530 CLOSE (FAXCHAN,ERR=EXIT)
0535 rem ' this program, confirm_fax.bbx is no longer used
0536 rem ' unix send mail is no longer used
0537 cause error on recompile
0540 rem ' LET A=SCALL("/usr/lib/send mail -t < "+FAXFILE$)
0550 EXIT: 
0560 LET FAXTEXT$=""
0570 EXIT 

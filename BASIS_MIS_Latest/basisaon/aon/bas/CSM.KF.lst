0010 REM "CSM - Product Pricing Listing"
0020 REM "Program CSM.KF - Canadian Product Pricing Listing
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "MAS * DCF * 4/28/99 * WA# 204 * BASIS"
0040 REM 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files "
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS>0 THEN GOTO 9900
0180 LET PRTR_DEV=7
0200 REM " --- IOLISTS "
0210 CSM10A: IOLIST A0$,A1$,A[ALL]
0400 REM " --- Parameters
0500 REM " --- Initialize Data "
0510 DIM HEADINGS$[2],A[29]
0520 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$+" Listing",CLOCK$="",L9=55
0525 LET WHEN$=F0$(7,3),L=L9+1,H0=132,H=0,T0=0
0530 LET NF$="(Not On File)"
0700 REM " --- Background "
0703 PRINT @(0,3),'CE',
0705 FOR X=4 TO 12 STEP 2
0710 CALL "SYC.WC",1,0,80,0,0,X,0
0715 NEXT X
0720 PRINT 'SB',@(18,3),"Beginning Product ID: ",@(21,5),"Ending Product ID: ",
0730 PRINT @(16,7),"Beginning Type of Sale: ",@(19,9),"Ending Type of Sale: ",
0790 PRINT 'SF'
1000 REM " --- Beginning Product ID"
1030 LET V0$="S",V1$="KCE^",V3$=BEGID$,V4$="",V0=3,V1=40,V2=3,O0=1
1040 LET V4$="Enter A Valid Product ID (<F1>=First, <F3>=Lookup)"
1045 GOSUB 7000
1050 ON V3 GOTO 1070,1052,1000,1055,9900
1052 LET V$=""; PRINT @(V1,V2),"First"; GOTO 1090
1055 CALL "CSC.LK",FILES$[3],V1,V2,V$,V0,30,"Product IDs"
1060 IF V$="" THEN GOTO 1000
1070 IF V$="" THEN GOTO 1000
1075 LET BEGID$=V$
1080 FIND (CSM01_DEV,KEY=N0$+BEGID$,DOM=1000)*,DESC$
1085 PRINT @(V1,V2),V$,@(V1+5,V2),DESC$
1100 REM " --- Ending Product ID"
1130 LET V0$="S",V1$="KCE^",V3$=ENDID$,V4$="",V0=3,V1=40,V2=5
1140 LET V4$="Enter A Valid Product ID (<F1>=Last, <F3>=Lookup)"
1145 GOSUB 7000
1150 ON V3 GOTO 1170,1152,1000,1155,9900
1152 LET V$=""; PRINT @(V1,V2),"Last"; GOTO 1190
1155 CALL "CSC.LK",FILES$[3],V1,V2,V$,V0,30,"Product IDs"
1160 IF V$="" THEN GOTO 1100
1170 IF V$="" THEN GOTO 1100
1175 LET ENDID$=V$
1180 FIND (CSM01_DEV,KEY=N0$+ENDID$,DOM=1100)*,DESC$
1185 PRINT @(V1,V2),V$,@(V1+5,V2),DESC$
1200 REM " --- Beginning Type of Sale"
1230 LET V0$="S",V1$="KCE^",V3$=BEGTYPE$,V4$="",V0=3,V1=40,V2=7
1240 LET V4$="Enter A Valid Type of Sale (<F1>=First, <F3>=Lookup)"
1245 GOSUB 7000
1250 ON V3 GOTO 1270,1252,1200,1255,9900
1252 LET V$=""; PRINT @(V1,V2),"First"; GOTO 1290
1255 CALL "CSC.LK",FILES$[4],V1,V2,V$,V0,30,"Types of Sale"
1260 IF V$="" THEN GOTO 1200
1270 IF V$="" THEN GOTO 1200
1275 LET BEGTYPE$=V$
1280 FIND (CSM01_DEV,KEY=N0$+BEGID$,DOM=1200)*,DESC$
1285 PRINT @(V1,V2),V$,@(V1+5,V2),DESC$
1300 REM " --- Ending Type of Sale"
1330 LET V0$="S",V1$="KCE^",V3$=ENDTYPE$,V4$="",V0=3,V1=40,V2=9
1340 LET V4$="Enter A Valid Type of Sale (<F1>=Last, <F3>=Lookup)"
1345 GOSUB 7000
1350 ON V3 GOTO 1370,1352,1000,1355,9900
1352 LET V$=""; PRINT @(V1,V2),"Last"; GOTO 1390
1355 CALL "CSC.LK",FILES$[4],V1,V2,V$,V0,30,"Types of Sale"
1360 IF V$="" THEN GOTO 1300
1370 IF V$="" THEN GOTO 1300
1375 LET ENDTYPE$=V$
1380 FIND (CSM01_DEV,KEY=N0$+ENDID$,DOM=1300)*,DESC$
1385 PRINT @(V1,V2),V$,@(V1+5,V2),DESC$
1900 REM " --- Correct?"
1920 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
1930 ON V3 GOTO 1940,1900,1900,1900,9900
1990 ON POS(V$="YN") GOTO 1900,2000,1000
2000 REM " --- Initial Read "
2005 LET COLUMN=40
2010 CALL "SYC.NB","Printing",11,40
2020 READ (CSM10_DEV,KEY=N0$+BEGID$,DOM=2021)
2100 REM " --- Main Read "
2110 READ (CSM10_DEV,END=4000)IOL=CSM10A
2120 IF POS(N0$=A0$)<>1 THEN GOTO 4000
2130 IF ENDID$<>"" AND A0$(3,3)>ENDID$ THEN GOTO 4000
2140 IF BEGTYPE$<>"" AND A0$(6,3)<BEGTYPE$ THEN GOTO 2110
2150 IF ENDTYPE$<>"" AND A0$(6,3)>ENDTYPE$ THEN GOTO 2110
2160 LET PROD_DESC$=NF$,TYPE_DESC$=NF$
2170 FIND (CSM01_DEV,KEY=N0$+A0$(3,3),DOM=2171)*,PROD_DESC$
2180 FIND (CSM07_DEV,KEY=N0$+A0$(6,3),DOM=2181)*,TYPE_DESC$
2190 PRINT @(COLUMN,11),A0$,
2200 REM " --- Print general info"
2210 IF L+6>L9 THEN GOSUB 5000
2220 PRINT (7)'LF',@(13),"Product: ",@(22),A0$(3,3)," ",PROD_DESC$
2230 PRINT (7)@(8),"Type of Sale: ",@(22),A0$(6,3)," ",TYPE_DESC$
2235 PRINT (7)@(8),"Discountable: ",@(22),A1$(1,1),"  Distribution Code: ",A1$(2,2)
2240 PRINT (7)'LF',@(6),"Minimum Users",@(20),"Maximum Users",@(54),"Price per User",'LF'
2250 LET L=L+6
2260 FOR X=0 TO 9
2265 IF L>L9 THEN GOSUB 5000
2270 PRINT (7)@(9),A[X]:MQ$,@(25),A[10+X]:MQ$,
2280 IF A[20+X]<>0 THEN PRINT (7)@(54),A[20+X]:M4$,
2285 PRINT (7)""
2287 LET L=L+1
2290 NEXT X
2300 PRINT (7)J$
2310 LET L=L+1,T0=T0+1
3900 REM 
3990 GOTO 2100
4000 REM " --- All done"
4010 IF L>L9 THEN GOSUB 5000
4020 PRINT (PRTR_DEV)""
4030 PRINT (PRTR_DEV)@(0),J$,'LF',T0," Product Pricing Tables"
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=0
5020 CALL "SYC.HA",7,HEADINGS$[ALL],2,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)""
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNQTY(Q1,Q2,Q3,Q4,Q5)=10000*Q1*Q2/(Q3*Q4*(100-Q5))
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

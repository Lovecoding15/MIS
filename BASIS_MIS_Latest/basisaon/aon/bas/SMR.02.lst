0010 REM "SMC - SAM Report"
0020 REM "Program SMC.25"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| BASIS Program - Developed using ADD-ON Interface          |"
0026 REM "| July 2006                                                 |"
0030 REM "+-----------------------------------------------------------+"
0050 BEGIN 
0060 SETESC err_rtn
0070 SETERR err_rtn
0100 REM " --- Open/Lock Files"
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="ARM-01",FILES$[3]="SMC-01",FILES$[4]="SNM-01",files$[5]="SNM-02",files$[6]="ARM-10",files$[7]="ARM-02"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01=CHANNELS[1],ARM01=CHANNELS[2],SMC01=CHANNELS[3],SNM01=CHANNELS[4],snm02=channels[5],ARM10=CHANNELS[6],arm02=CHANNELS[7]
0200 REM " --- IOLIST's"
0210 call "templates.pgm::ARM01"
0220 call "templates.pgm::SMC01"
0230 call "templates.pgm::SNM01"
0240 call "templates.pgm::SNM02"
0250 call "templates.pgm::ARM02"
0280 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET firm$=F0$(16,2),N1$=F4$
0500 REM " --- Initialize Data"
0510 LET L9=58,L=L9+1,HEADINGS=3,WIDTH=80,PAGE=0,start_dt$=fndate$(jul(0,0,0)),WHEN$=F0$(7,3),clock$=""
0520 DIM HEADINGS$[HEADINGS]
0530 LET HEADINGS$[0]=N1$
0600 REM " --- Background"
0610 CALL "SYC.WC",1,0,80,0,0,6,0
0620 PRINT 'SB',@(34,3),"Beginning:",@(37,5),"Ending:",'sf'
0630 print @(45,3),start_dt$
0700 REM " --- Start date"
0710 LET V0$="M",V1$="",V2$=start_dt$,v3$="00000000",V0=8,V1=45,V2=3
0720 LET V4$="Enter the start date (YYYYMMDD)"
0730 GOSUB 7000
0740 ON V3 GOTO 750,700,700,700,850
0750 LET start_dt$=v$,start_jul=fnjul(start_dt$)
0760 end_dt$=fndate$(start_jul+30)
0800 REM " --- End date"
0805 LET V0$="M",V1$="",V2$=end_dt$,V3$="00000000",V4$="Enter the end date (YYYYMMDD)",V0=8,V1=45,V2=5
0810 GOSUB 7000
0815 ON V3 GOTO 820,820,700,800,820
0820 LET end_dt$=v$
0825 end_jul=fnjul(end_dt$)
0830 if end_jul<start_jul then goto 800
0850 REM " --- Correct?"
0860 LET V0$="Y",V1$="C^",V2$="Y",V3$="",V4$="Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
0870 GOSUB 7000
0875 IF V3=4 THEN GOTO 9900
0880 ON POS(V$="YN") GOTO 850,890,700
0900 CALL "SYC.GA",7,1,"",PRTR$,STATUS
0920 IF STATUS THEN GOTO 9900
0950 HEADINGS$[1]="SAM Expiring Between "+start_dt$+" and "+end_dt$
1000 REM " --- Initial Read"
1010 contract_list$="",cust$=""
1020 read record(smc01,key=firm$,knum=1,dom=*next)
1030 while 1 
1040 read record(smc01,end=*break)smc01$
1050   if smc01.firm_id$<>firm$ then break
1060   if smc01.annual_dt$>end_dt$ or smc01.annual_dt$<start_dt$ then continue
1070   if pos("SM"=smc01.contr_type$)=0 or smc01.sam_active$="N" then continue
1080   if smc01.renew_flag$="N" then continue
1100   if pos(smc01.customer_nbr$="001081,007808") then continue
1130   if cust$<>smc01.customer_nbr$ then gosub print_cust
1160   contract_list$=contract_list$+smc01.annual_dt$+smc01.contract$
1170   cust$=smc01.customer_nbr$
1180 wend
1190 gosub print_cust
1500 goto report_finish
2000 print_cust:
2010 if contract_list$="" then goto print_cust_done
2020 gosub find_customer
2030 contract_list$=ssort(contract_list$,14)
2035 first=1
2040 while 1
2050   if len(contract_list$)=0 then break
2060   temp$=contract_list$(1,14),contract_list$=contract_list$(15) 
2070   contract$=temp$(9)
2080   gosub find_serial
2090   if serial_list$="" then continue
2100   while 2
2110     p=pos($0a$=serial_list$)
2120     if p=0 then break
2130     k$=serial_list$(1,p-1),serial_list$=serial_list$(p+1)
2140     gosub get_price
2150     IF L>L9 THEN GOSUB do_headings
2160     if first then PRINT (7)cust$," ",cust_name$,
2170     print(7)@(37),temp$(1,8),"     ",k$,@(65),snm01.users:"####0",samprice:"#######.00"
2180     L=L+1
2190     first=0
2200   wend
2210 wend
2220 print(7)"";l=l+1
2230 print_cust_done:
2240 contract_list$=""
2290 return
3000 get_price:
3010 readrecord(snm01,key=k$,knum=0)snm01$
3020 dim snm02$:fattr(snm02$)
3030 readrecord(snm02,key=k$,knum=0,dom=*next)snm02$
3040 dim users[1],prop$[1]
3050 trans_type$="SM1",lic_type$=snm01.license_type$,old_rev$=snm01.product_rev$,sam_type$="SM1",on_sam=1,samprice=0
3060 prop$[0]=snm02.lic_property1$
3070 prop$[1]=snm02.lic_property2$
3080 users[0]=snm02.users1
3090 users[1]=snm02.users2
3100 if cvs(prop$[0]+prop$[1],3)="" then prop$[0]=snm01.product$
3110 if users[0]+users[1]=0 then users[0]=snm01.users
3130 call "price.pgm",firm$,trans_type$,lic_type$,users[all],users[all],prop$[all],prop$[all],old_rev$,sam_type$,on_sam,disc_pct,splevel,price,samprice
3190 return
4000 report_finish:
4020 run "SYS.AA"
5000 do_headings:
5005 REM " --- Heading"
5010 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5020 LET L=HEADINGS+1
5040 PRINT (7)""
5050 PRINT (7)"Customer",@(37),"Annual Date  Serial No      Users     Price",'lf'
5080 L=L+4,first=1
5090 RETURN 
6000 find_customer:
6010 dim cust_name$(30),arm02$:fattr(arm02$)
6015 LET cust_name$(1)="Not On File"
6020 find record(ARM01,KEY=firm$+cust$,DOM=*next)arm01$
6030 IF cvs(arm01.cust_name$,3)<>"" then cust_name$(1)=arm01.cust_name$
6040 FIND RECORD(ARM02,KEY=firm$+CUST$+"  ",DOM=*next)arm02$
6050 disc_pct=0,splevel=arm02.pricing_level
6060 FIND (ARM10,KEY=firm$+"I"+arm02.disc_code$,ERR=*next)*,disc_pct
6070 if pos(arm02.cust_type$(1,1)="APM") then disc_pct=disc_pct+5
6080 customer_done:
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8100 def fndate$(julian)=date(julian:"%Y%Mz%Dz")
8120 def fnjul(q8$)=jul(num(q8$(1,4)),num(q8$(5,2)),num(q8$(7,2)))
9000 err_rtn: REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR err_rtn
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
10000 find_serial:
10100 serial_list$=""
10110 read(snm01,key=contract$,knum=3,dom=*next)
10120 while 1
10130   read record(snm01,end=*break)snm01$
10135   if snm01.contract$<>contract$ then break
10140   if snm01.active_flag$="N" or snm01.sam_active$="N" then continue
10150   serial_list$=serial_list$+snm01.serial_nbr$+$0a$
10160 wend          
10190 return

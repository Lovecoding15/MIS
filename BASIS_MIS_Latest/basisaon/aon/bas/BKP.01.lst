0010 REM "GL Backup to BACK(n) directories
0020 REM "Program BKP.01"
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=1
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="SYS-01"
0170 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0175 IF STATUS>0 THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1]
0200 REM " --- IOLists
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2)
0500 REM " --- Initializations"
0530 LET L0=6,l1=1,msg$="Last Backup Date: "
0540 today$=date(0:"%Mz/%Dz/%Y"),last_date$=today$
0700 REM " --- Background
0710 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0720 PRINT 'SB',@(l1,l0-2),msg$,@(0,L0),'CE','SF',
0730 backfile$="/basisaon/aon/BACK1/backup_date.txt"
0740 bk=unt,ok=0
0750 open(bk,err=*next)backfile$;ok=1
0760 if ok then read(bk)last_date$;close(bk)
0770 y=num(last_date$(7,4)),m=num(last_date$(1,2)),d=num(last_date$(4,2))
0780 last_jul=jul(y,m,d)
0790 today_jul=jul(0,0,0)
0800 days=today_jul-last_jul
1000 REM 
1010 PRINT 'CF',@(l1+len(msg$),l0-2),last_date$
1020 LET V2$="",v0$="Y",v1$="^",v3$=""
1050 V0=1,V2=22,V4$="Backup Now? <Y/N>",v1=42+INT(LEN(v4$)/2)
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 9900
1090 IF pos(V$="YN")=0 THEN GOTO 1000
1110 PRINT @(v1,v2),V$
1120 if v$="N" then goto 9900
1200 REM 
1210 if days<28 then
1220 Print @(l1,l0+2),'ce',"WARNING - LAST BACKUP WAS ONLY "+str(days)+" DAYS AGO!"
1230 v0$="Y",v1$="^",V4$="ARE YOU SURE? <Y/N>",v1=42+INT(LEN(v4$)/2)
1240 GOSUB 7000
1250 IF V3=4 or v$<>"Y" THEN GOTO 9900
1260 fi
1300 REM 'backup
1320 Print @(l1,l0+2),'ce',"Copying BACK2 to BACK3";wait 1
1330 a=scall("cp /basisaon/aon/BACK2/* /basisaon/aon/BACK3 2>/dev/null")
1340 rem if a then 
1350 Print @(l1,l0+3),'ce',"Copying BACK1 to BACK2";wait 1
1360 a=scall("cp /basisaon/aon/BACK1/* /basisaon/aon/BACK2 2>/dev/null")
1370 rem if a then 
1380 Print @(l1,l0+4),'ce',"Copying selected ADATA files to BACK1";wait 1
1390 a=scall("/basisaon/aon/BACK1/copy_data.cmd")
1400 rem if a then 
1420 bk=unt
1430 open(bk)backfile$
1440 print(bk)today$
1450 close(bk)
1500 Print @(l1,l0+6),"Backup Complete";wait 3
4080 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

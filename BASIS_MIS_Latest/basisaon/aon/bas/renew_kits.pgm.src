seterr errmsg
enter

open$=""

smc01=unt
open(smc01)"SMC-01"
open$=open$+str(smc01:"000")
snm01=unt
open(snm01)"SNM-01"
open$=open$+str(snm01:"000")
sn_license=unt
open(sn_license)"sn_license.dat"
open$=open$+str(sn_license:"000")
smc01w=unt
open(smc01w)"SMC-01"
open$=open$+str(smc01w:"000")
snm01w=unt
open(snm01w)"SNM-01"
open$=open$+str(snm01w:"000")
snm04=unt
open(snm04)"SNM-04"
open$=open$+str(snm04:"000")
are13=unt
open(are13)"ARE-13"
open$=open$+str(are13:"000")
are83=unt
open(are83)"ARE-83"
open$=open$+str(are83:"000")

call "templates.pgm::SMC01"
call "templates.pgm::SNM01"
call "templates.pgm::SNM04"
call "templates.pgm::sn_license"
call "templates.pgm::ARE13"
call "templates.pgm::ARE83"
deact$=""
renew$=""

firm$="01"
cust$="007808"
today=jul(0,0,0)
yy=num(date(today:"%Yl"))
yy1=yy
mm=num(date(today:"%Mz"))
nextq=4
thisq=1
while mm>=nextq
  nextq=nextq+3
  thisq=thisq+3
wend
check_dt=jul(yy,thisq,1)-1
while 1
  rem ' dvk expire dates will be 01/31/yyyy, 04/30/yyyy, 07/31/yyyy, 10/31/yyyy
  if nextq>=12 then nextq=1,yy=yy+1
  new_expire=jul(yy,nextq+1,1)-1
  if new_expire-today>45 then break
  nextq=nextq+3 
wend

monthlist$="-jan-feb-mar-apr-may-jun-jul-aug-sep-oct-nov-dec-"

are83cont$=""
read (are13,key=firm$+"  "+cust$,DOM=*next)
while 1
  read record(are13,end=*break)ARE13$
  IF pos(firm$+"  "+cust$=are13$)<>1 THEN break
  if pos("KIT"=are13.item_number$)<>13 then continue
  READ(ARE83,key=are13$(1,20),dom=*next)
  while 2
    read record(are83,end=*break)ARE83$
    if pos(are13$(1,20)=are83$)<>1 then break
    if are83.action$="D" then continue
    temp$=are83.contract$+$0a$
    if pos(temp$=are83cont$)=0 then are83cont$=are83cont$+temp$
  wend
wend

read (smc01,key=firm$+cust$,knum=1,dom=*next)
while 1
  readrecord(smc01,end=*break)smc01$
  if smc01.firm_id$+smc01.customer_nbr$<>firm$+cust$ then break
  if pos("DV"=smc01.contr_type$)<>1 then continue
  expire=jul(num(smc01.expire_on_dt$(1,4)),num(smc01.expire_on_dt$(5,2)),num(smc01.expire_on_dt$(7,2)))
  if expire>=new_expire or expire<check_dt then continue
  
  snlist$=""
  read(snm01,key=smc01.contract$,knum=3,dom=*next)
  while 2
    readrecord(snm01,end=*break)snm01$
    if snm01.contract$<>smc01.contract$ then break
    if snm01.active_flag$<>"Y" then continue
    if snm01.firm_id$+snm01.customer_nbr$<>firm$+cust$ then continue
    if snm01.license_type$<>"KIT" then snlist$="";break
    snlist$=snlist$+snm01.serial_nbr$+$0a$
  wend

  order_stat=pos(smc01.contract$=are83cont$)
  
  if snlist$="" and order_stat=0 then continue 
  temp$=snlist$
  snlist$=""

  while len(temp$)
    p=pos($0a$=temp$)
    if p=0 then break
    sn$=temp$(1,p-1)
    temp$=temp$(p+1)
    licensed=0

    rem 'was this licensed with an expiration date in the current quarter?
    read(sn_license,key=sn$(1,12),dom=*next)
    last_expire$="never licensed"

    while 3
      readrecord(sn_license,end=*break)sn_license$
      if pos(sn_license.serial_num$=sn$)=0 then break
      p=pos("-"=sn_license.expire$)
      if p=0 then continue
      x$=sn_license.expire$(p,4)
      pm=pos(x$=monthlist$)
      if pm=0 then continue
      m=int(pm/4)+1
      y=num(sn_license.expire$(p+5),err=*continue)
      d=num(sn_license.expire$(1,p-1),err=*continue)
      last_expire$="expired "+date(jul(y,m,d))
      if jul(y,m,d)>=check_dt then licensed=1;break
    wend
    
    if licensed then snlist$=snlist$+sn$+$0a$;continue

    rem 'deactivate kit if no current license found
    extract record(snm01w,key=sn$,err=*continue)snm01$
    snm01.active_flag$="N"
    snm01$=field(snm01$)
    writerecord(snm01w,key=sn$)snm01$
    deact$=deact$+sn$+snm01.contract$+$0a$
    seq=1
    dim com$(48)
    com$(1)="Deactivated "+DATE(0)+", "+last_expire$
    READ (snm04,KEY=sn$,DOM=*NEXT)
    WHILE seq<100
      READ RECORD (snm04,END=*break)snm04$
      IF snm04.serial_nbr$=sn$ THEN seq=NUM(snm04.comments_seq$)+1; CONTINUE
      snm04.serial_nbr$=sn$
      snm04.comments_seq$=STR(seq:"00")
      snm04.cmt_line$=com$
      snm04$=FIELD(snm04$)
      WRITE RECORD (snm04,KEY=snm04$(1,22))snm04$
      break
    WEND
  wend

  if snlist$="" and order_stat=0 then continue 

  extract record(smc01w,key=smc01.contract$)smc01$
  smc01.annual_dt$=date(new_expire:"%Yl%Mz%Dz")
  smc01.expire_on_dt$=date(new_expire:"%Yl%Mz%Dz")
  smc01$=field(smc01$)
  write record(smc01w,key=smc01.contract$)smc01$
  renew$=renew$+smc01.contract$+" annual: "+date(new_expire)+" exp: "+date(new_expire)+$0a$
wend

pgm_exit:
while len(open$)>1
  f=num(open$(1,3))
  open$=open$(4)
  close(f,err=*next)
wend

from$="customer-service@basis.cloud"
to$="mis@basis.cloud"
subj$="Automatic Dev Kit renewal"
cc$=""
bcc$=""
msg$ = msg$ + "Deactivate:" + $0a$ + deact$ + "Renew:" + $0a$ + renew$

call "sendEmail.src", from$, to$, cc$, bcc$, subj$, msg$, ""

seterr 0
exit

errmsg:
msg$="Renew kits: error " + str(err) + " in " + str(tcb(5)) + " of " + pgm(-2) + $0a$ + errmes(-1) + $0a$
goto pgm_exit

END

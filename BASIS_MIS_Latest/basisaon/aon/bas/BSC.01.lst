0010 REM "Shipping Charge Maintenance
0020 REM "Program BSC.01
0079 REM 
0080 BEGIN 
0090 SETERR 9000
0100 REM "OPEN/LOCK FILES"
0110 OPEN (1)"shipping.dat"
0160 OPEN (6)"SYS-01"
0200 REM "IOLISTS"
0210 IOLIST A0$,A0
0270 IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0405 FIND (6,KEY="T"+FID(0),DOM=9800)IOL=0270
0500 REM "INIT DATA"
0520 DIM A0$(7)
0540 LET A0$(1,7)="  "
0560 LET M1$="#,##0.00"
0700 REM "BACKGROUND"
0720 PRINT 'SB',@(14,3),"Zone:",@(14,4),"Type:",@(6,5),"Weight (lbs):",@(3,6),"Shipping Charge:",
0790 PRINT 'SF',
0900 REM "INIT FILE"
0920 READ (1,KEY=A0$,DOM=0930)
1000 REM "KEY"
1010 PRINT 'CF',
1020 LET A0$=KEY(1,END=1030)
1030 LET V2$=A0$(1,3),V0$="Z",V1$="",V3$="000",V0=3,V1=22,V2=3,V4$="Enter Shipping Zone (1=U.S.)"
1040 GOSUB 7000; IF V3=4 OR V$="END" THEN GOTO 4500
1045 IF V3=3 THEN GOSUB 6000
1050 LET A0$(1,3)=V$
1090 PRINT @(V1,V2),A0$(1,3); IF V$="" THEN GOTO 1000
1100 LET V2$=A0$(4,1),V0$="S",V1$="",V3$="",V0=1,V1=22,V2=4,V4$="1 Day, 2 Day, 3 Day (1,2,3) "
1110 IF A0$(1,3)<>"001" THEN LET V$=" "; GOTO 1140
1120 GOSUB 7000; IF V3=4 OR V$="END" THEN GOTO 4500
1130 IF POS(V$="123")=0 THEN GOTO 1100
1190 LET A0$(4,1)=V$; PRINT @(V1,V2),A0$(4,1)
1200 LET V2$=A0$(5,3),V0$="Z",V1$="",V3$="000",V0=3,V1=22,V2=5,V4$="Enter Weight in pounds"
1240 GOSUB 7000; IF V3=4 OR V$="END" THEN GOTO 4500
1250 LET A0$(5,3)=V$
1280 IF V3=3 THEN GOSUB 6000
1290 PRINT @(V1,V2),A0$(5,3); IF V$="" THEN GOTO 1000
1500 REM "FIND KEY"
1510 LET O9=0
1520 FIND (1,KEY=A0$,DOM=1700)IOL=0210
1600 REM "DISPLAY"
1620 GOSUB 5000
1640 REM GOTO 4000
1642 GOTO 2000
1700 REM "NEW RECORD"
1710 LET V0$="Y",V1$="C^",V2$="",V3$="",V4$="Is This A New Record? <Y/N>: ",V0=1,V1=55,V2=22
1720 REM GOSUB 7000
1740 REM ON POS(V$="YN") GOTO 1700,1800,1000
1800 REM "SET-UP"
1810 LET A0=0
2000 REM "Shipping charge 
2006 GOSUB 5000
2010 LET O9=1
2020 LET V0$="N",V1$="",V3$=M1$,V2$=STR(A0:V3$),V0=1,V1=20,V2=6,I0=0,V4$="Shipping charge for this zone, type and weight"
2040 GOSUB 7000
2060 LET A0=V
2090 GOTO 3900
3900 REM "NEXT FIELD"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,3990
3990 REM 
4000 REM "OPTIONS"
4010 LET V0$="S",V1$="C^",V2$="Y",V3$="",V4$="Is The Above Information Correct? <Y/N/Delete>: ",V0=6,V1=64,V2=22
4020 GOSUB 7000; IF V3=4 THEN GOTO 4500
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF V$="" THEN IF O9=0 THEN GOTO 1000
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM "WRITE"
4120 WRITE (1,KEY=A0$)IOL=0210
4190 GOTO 1000
4200 REM "DELETE"
4220 REMOVE (1,KEY=A0$,DOM=4230)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4900,9900
4900 REM " --- Report Initializations"
4905 LET PROGRAM$="",NUMBER=3,HEADINGS=3,WIDTH=80,CHANNEL=1
4915 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4920 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4925 DIM HEADINGS$[HEADINGS]
4930 LET FIELD$[0]="Zone",FIELD[0]=1,POSITION[0]=1,LENGTH[0]=3,TYPE$[0]="S",MASK$[0]=""
4940 LET FIELD$[1]="Shipping Type",FIELD[1]=1,POSITION[1]=4,LENGTH[1]=1,TYPE$[1]="S",MASK$[1]=""
4950 LET FIELD$[2]="Weight",FIELD[2]=1,POSITION[2]=5,LENGTH[2]=3,TYPE$[2]="S",MASK$[2]=""
4960 LET FIELD$[3]="Shipping Charge",FIELD[3]=2,POSITION[3]=1,LENGTH[3]=3,TYPE$[3]="N",MASK$[3]=M1$,TOTAL$="Classes"
4980 LET HEADINGS$[0]="",HEADINGS$[1]="Shipping charges",RECORD$="",N0$=""
4990 RUN "SYR.AA"
5000 REM "DISPLAY"
5020 PRINT @(20,6),A0:M1$,
5090 RETURN 
6000 REM " --- Zone Lookup"
6005 LET TITLE$="Shipping Charges"
6010 LET CHANNEL=1,NUMBER=2
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Zone",DESCRIPTION$[1]="Type",DESCRIPTION$[2]="Weight"
6040 LET FIELD[0]=1,POSITION[0]=1,LENGTH[0]=3
6050 LET FIELD[1]=1,POSITION[1]=4,LENGTH[1]=1
6055 LET FIELD[2]=1,POSITION[2]=5,LENGTH[2]=3
6060 LET COLUMN=79-LENGTH[0]-LENGTH[1]-LENGTH[2]-2-NUMBER*4
6070 LET V$="",ROW=V2+2
6080 CALL "BTM.10",CHANNEL,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
65000 END

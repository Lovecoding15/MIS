0010 REM "OPR - Single Invoice Printing"
0020 REM "Program OPR.01"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES=14
0115 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="ARE-03",FILES$[2]="ARE-04",FILES$[3]="ARE-13"
0125 LET FILES$[4]="ARE-20",FILES$[5]="ARE-23",FILES$[6]="ARE-33"
0130 LET FILES$[7]="ARM-01",FILES$[8]="ARM-03",FILES$[9]="ARM-09"
0135 LET FILES$[10]="ARM-10",FILES$[11]="IVM-01",FILES$[12]="SYS-01"
0140 LET FILES$[13]="ARW-43",OPTIONS$[13]="CL"
0145 LET FILES$[14]="ARM-02"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS; REM WA# 206
0155 IF STATUS>0 THEN GOTO 9900
0160 LET ARE03_DEV=CHANNELS[1],ARE04_DEV=CHANNELS[2],ARE13_DEV=CHANNELS[3]
0165 LET ARE20_DEV=CHANNELS[4],ARE23_DEV=CHANNELS[5],ARE33_DEV=CHANNELS[6]
0170 LET ARM01_DEV=CHANNELS[7],ARM03_DEV=CHANNELS[8],ARM09_DEV=CHANNELS[9]
0175 LET ARM10_DEV=CHANNELS[10],IVM01_DEV=CHANNELS[11],SYS01_DEV=CHANNELS[12]
0180 LET ARW43_DEV=CHANNELS[13],arm02_dev=channels[14]
0200 REM " --- IOLists"
0210 ARM01A: IOLIST *,X0$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 IVS01A: IOLIST X$,X$,I2$,I3$,X$,X$,M1$,M2$,M3$,M4$
0290 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0300 ARW43A: IOLIST ARW43_K$
0310 ARE03A: IOLIST A0$(1),A[ALL]
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0460 DIM P[2],I[6]
0465 FOR X=0 TO 6
0470 LET I[X]=NUM(I2$(X*2+1,2))
0475 NEXT X
0480 LET DESCLEN=I[4]+I[5]+I[6]
0485 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0490 LET M0=LEN(M0$),M2=LEN(M2$),M3=LEN(M3$),M4=LEN(M4$)
0495 CALL "SYC.RM",M1$,M1,8,12
0500 REM " --- Initialize Data"
0510 LET P5$=""
0520 DIM A0$(117),A[10]
0700 REM " --- Background"
0710 FOR X=4 TO 4 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(29,3),"Invoice Number:",'SF',

0759 rem ' now both firms are using the Jasper Invoice, collect an email address
0760 print 'SB',@(5,5),"Email Address:",'SF',

1000 REM "
1010 PRINT 'CF',
1020 LET V0$="Z",V1$="K",V2$="",V3$="",V0=7,V1=38,V2=3
1030 LET V4$="Enter Invoice Number"
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1060 LET P5$=V$
1070 PRINT @(V1,V2),P5$

1100 rem ' now both firms are using the Jasper Invoice, collect an email address
1110 rem ' if N0$ = "02" then goto skip_email
1120 LET V0$="S",V1$="",V2$="",V3$="",V0=40,V1=21,V2=5
1130 LET V4$="Enter Email Address <Enter> = 'customer-service@basis.cloud'"
1140 gosub 7000
1150 if v3 = 4 then goto 9900
1160 if v$ = "" then let email$ = "customer-service@basis.cloud" else email$ = v$
1170 print @(v1,v2),email$
1200 rem ' skip_email:

4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct"
4020 CALL "SYC.YN",1,V4$,2,V$,V3
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000

4099 rem now both firms are using the jasper invoice
4100 goto bas_print

4105 REM " --- Open Printer"
4110 CALL "SYC.GA",7,1,"","",STATUS
4120 IF STATUS THEN GOTO 9900
4130 GOSUB 10000
4140 if found=0 then msg$="Invoice "+p5$+" not found.";goto 4200
4150 REM " --- Run Overlay"
4160 LET O9$="SYS.AA",O9=2,PRTR_DEV=7,msg$="Invoice print program "+P3$(65,6)+" not found."
4170 r1$=fill(18);rem 'bypass 4500 subroutine in print program
4190 RUN P3$(65,6),ERR=4200
4200 REM " --- Missing Invoice Print Program"
4210 LET NUMBER=1
4220 DIM MESSAGE$[NUMBER]
4230 LET MESSAGE$[0]=msg$
4240 LET MESSAGE$[1]="         <Enter>=Continue"
4250 CALL "SYC.XA",2,MESSAGE$[ALL],NUMBER,-1,-1,V$,V3
4290 GOTO 9900

4400 bas_print:
4410 gosub 10000
4420 if found=0 then msg$="Invoice "+p5$+" not found.";goto 4200

4430 if A0$(1,2) = "01" then
4431      call "renderInvoice.src",A0$(1,2), A0$(5,6), A0$(42,7), jasperInvoice$
4432 else
4433       call "renderInvoiceBSG.src",A0$(1,2), A0$(5,6), A0$(42,7), jasperInvoice$
4434 fi

4440 rem ' email it
4450 from$ = "customer-service@basis.cloud"
4460 to$ = email$
4470 cc$ = "", bcc$ = "kw5121151@gmail.com"
4480 subject$ = "Invoice " + A0$(42,7) + " print"
4490 msgtxt$ = "Invoice " + A0$(42,7) + " attached."
4500 call "sendEmailHtml.src", from$, to$, cc$, bcc$, subject$, msgtxt$, jasperInvoice$
4510 erase jasperInvoice$, err = 9900
4520 goto 9900

7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
10000 REM " --- Find Invoice
10010 found=0
10020 READ (ARE03_DEV,KEY=N0$,DOM=*next)
10030 READ (ARE03_DEV,end=10090)IOL=ARE03A
10040 if n0$<>a0$(1,2) then goto 10090
10050 IF A0$(42,7)<>P5$ THEN GOTO 10030
10060 found=1
10090 RETURN 
46500 END

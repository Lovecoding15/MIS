0010 REM "Invoice Print for BSG - German language
0020 REM "Program BSG.02 - based on OPR.CA
0050 REM "O9$=Return program
0052 REM "O9=1 - Batch print from OPR.GA
0054 REM "O9=2 - On demand print from OPE.CE
0056 REM "O9=3 - Historical invoice print from ARM.ML
0058 REM "       ARE03_DEV=ART03_DEV
0060 REM "       ARE13_DEV=ART13_DEV
0064 REM "       ARE33_DEV=ART33_DEV
0066 REM "       ARE73_DEV=ART73_DEV
0068 REM "       ARE83_DEV=ART83_DEV
0070 REM "O9=4 - Email invoice from BIN.10
0071 REM "O9=9 - Email invoice from GuiAddon (invhist_print.bbj)
0075 seterr enter_err
0080 enter
0085 enter_err:
0090 SETESC 9000
0095 SETERR 9000
0100 if o9>3 then goto iolists
0105 GOSUB 17000; IF V$="E" THEN RUN "OPR.CA"
0110 REM " --- Open/lock files"
0115 LET FILES1=6
0120 DIM FILES1$[FILES1],OPTIONS1$[FILES1],CHANNELS1[FILES1]
0130 LET FILES1$[1]="TMM-01",FILES1$[2]="ARE-73"
0140 LET FILES1$[3]="ARE-83",FILES1$[4]="CSM-03"
0150 IF ARM02_DEV=0 THEN LET FILES1$[5]="ARM-02"
0155 LET FILES1$[6]="SMC-01"
0157 if o9=3 then files1$[2]="ART-73",files1$[3]="ART-83"
0160 CALL "SYC.DA",1,1,FILES1,FILES1$[ALL],OPTIONS1$[ALL],CHANNELS1[ALL],BATCH,STATUS
0170 IF STATUS<>0 THEN GOTO 4100
0180 LET TMM01_DEV=CHANNELS1[1],ARE73_DEV=CHANNELS1[2]
0190 LET ARE83_DEV=CHANNELS1[3],CSM03_DEV=CHANNELS1[4],SMC01_DEV=CHANNELS1[6]
0195 IF ARM02_DEV=0 THEN LET ARM02_DEV=CHANNELS1[5]
0200 IOLists:
0210 ARE03A: IOLIST A0$(1),A[ALL]
0220 ARE13A: IOLIST W0$,W1$(1),W[ALL]
0230 ARE20A: IOLIST Z0$,Z1$
0250 ARE33A: IOLIST D0$,D1$
0260 ARM01A1: IOLIST B0$,B1$
0265 ARM01A2: IOLIST A0$,A1$
0270 ARM02A: IOLIST B0$,B1$,B[ALL]
0280 ARM03A: IOLIST C0$,C1$
0290 ARM09A: IOLIST X$,X8$(1)
0300 ARM10A: IOLIST X1$
0310 ARM10C: IOLIST Y7$,Y9$
0320 ARM10E: IOLIST Y0$(1)
0330 ARM10I: IOLIST *,DISCOUNT_PERCENT
0340 ARM10G: IOLIST *,X4$
0350 IVM01A: IOLIST *,X2$,X9$(1),*,*,X5$(1); REM "WA:200
0370 CSM03A: IOLIST CS0$,CS1$,CS2$
0380 ARE73A: IOLIST WX0$,WX1$,WX[ALL]; REM WA# 207
0390 ARE83A: IOLIST WY0$,WY1$,WY2$,WY[ALL]; REM WA# 207
0400 ARW43A: IOLIST ARW43_K$
0410 call "templates.pgm::SMC01"
0420 call "templates.pgm::TMM01"
0500 REM " --- Initialize Data"
0505 DIM WX[4],WY[2],X5$(2)
0510 PRECISION I[2]
0520 LET L9=26
0530 DIM H9$(50,$0A$)
0540 DIM W1$(64),W[14]
0550 LET PRICE_MASK$=M4$,EXT_MASK$=M1$,HZ$=FILL(77,CHR(196)),KOPIE=1,KOPIES=4
0560 if o9>1 then HZ$=FILL(77,"_"),KOPIES=1
0570 LET EUROSIGN$=CHR(238),lf$=$0a$
0700 REM " --- Background"
0710 IF O9=1 THEN DIM A0$(117),A[10]
0800 LET PAGECOUNT=0,CLOSEPRTR=0
0810 tmp=unt,file_text$="",sz=-1
0820 open(tmp)"bsg_invoice.txt"
0830 f$=fin(tmp),sz=dec(f$(1,4))
0840 if sz then readrecord(tmp,siz=sz)file_text$
0850 close(tmp,err=*next)
0900 IF O9>1 and o9<4 THEN PRINT @(0,21),'CL',@(30,21),"Printing:",; LET COLUMN=40,ROW=11
0910 IF O9=1 THEN CALL "SYC.NB","Printing",LEN(M0$)+8,COLUMN; LET ROW=11
0920 IF O9>1 THEN READ (ARE03_DEV,KEY=A0$(1,20),DOM=4100)IOL=ARE03A; GOTO 1145
1000 REM " --- Initial Read"
1005 REM "P5$=Starting customer number on restart"
1010 READ (ARW43_DEV,KEY=N0$,DOM=1011)
1020 READ (ARE04_DEV,KEY=N0$+"I  "+P5$,DOM=1030)
1100 REM " --- Main Read"
1110 IF O9>1 THEN GOTO 4100
1115 LET ARW43_K$=KEY(ARW43_DEV,END=4000)
1116 READ (ARW43_DEV)
1117 LET K1$=ARW43_K$(1,2)+"I"+ARW43_K$(3,2)+ARW43_K$(12,13)
1118 READ (ARE04_DEV,KEY=K1$,DOM=1115)
1130 IF K1$(1,3)<>N0$+"I" THEN GOTO 4000
1140 EXTRACT (ARE03_DEV,KEY=K1$(1,2)+K1$(4)+"000",DOM=1200)IOL=ARE03A
1145 IF A0$(99,1)="N" THEN GOTO 1170
1150 DIM Y7$(30),Y9$(21),Z0$(56),Z1$(30); LET Y7$(7)="Invalid Receipt Code",Y9$(21,1)="C"
1155 IF ARE20_DEV=0 THEN GOTO 1170
1160 FIND (ARE20_DEV,KEY=A0$(1,17),DOM=1170,ERR=1170)IOL=ARE20A
1165 FIND (ARM10_DEV,KEY=N0$+"C"+Z0$(21,3),DOM=1170)IOL=ARM10C
1170 IF A0$(21,1)="V" THEN GOTO 1100
1172 IF O9>1 THEN GOTO 1400
1175 IF A0$(22,1)<>"I" THEN GOTO 1200
1180 IF A0$(68,1)="Y" THEN GOTO 1100
1185 IF A0$(68,1)<>"B" AND A0$(69,1)="Y" AND KOPIE=1 THEN GOTO 1100
1190 LET A0$(69,1)="Y"; WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
1195 GOTO 1400
1200 REMOVE (ARE04_DEV,KEY=K1$)
1210 GOTO 1100
1400 REM " --- Heading"
1405 sam_renew=(CVS(A0$(49,10),7)="SAM RENEW")
1410 if o9<4 then PRINT @(COLUMN,ROW),FNF$(A0$(5,P[0]),M0$)," ",A0$(42,7),
1420 DIM B$(150),B1$(325)
1440 LET B1$(1)="",CONTACT$=FILL(30),ust_id$=""
1460 READ (ARM01_DEV,KEY=N0$+A0$(5,6),DOM=1490)IOL=ARM01A1
1465 LET CONTACT$=B1$(228,20)+FILL(10),UIDNR$=B1$(289,15)
1470 LET B$=B1$(31,72)+B1$(179,48)+B1$(103,9)
1480 rem CALL "SYC.AA",B$,24,5,P[2],30
1490 LET B$=B1$(1,30)+B$
1495 GOSUB 10000
1500 REM " --- Ship-To"
1510 LET C$=B$
1520 IF FNP$(A0$(102,6))="" THEN GOTO 1600
1530 DIM C1$(190)
1540 IF A0$(102,6)<>"000099" THEN GOTO 1600
1545 LET SHIPTO$=""
1550 FIND (ARE33_DEV,KEY=N0$+A0$(5,13),DOM=1690)IOL=ARE33A
1555 LET C1$=D1$(1,90)+FILL(30)+D1$(91)
1560 GOTO 1630
1600 LET SHIPTO$=""
1610 FIND (ARM03_DEV,KEY=N0$+A0$(5,6)+A0$(102,6),DOM=1690)IOL=ARM03A
1620 LET SHIPTO$=A0$(102,6)
1630 LET C$=C1$(31,90)+FILL(30)+C1$(121,25)+" "+C1$(146,2)+"  "+C1$(157,15)+FILL(15," ")+C1$(148,9)
1640 CALL "SYC.AX",C$,30,6,P[2],30
1650 LET C$=C1$(1,30)+C$
1690 IF LEN(C$)<LEN(B$) THEN LET C$=C$+FILL(40); GOTO 1690
1700 REM " --- Terms"
1710 DIM X1$(20)
1720 FIND (ARM10_DEV,KEY=N0$+"A"+A0$(62,2),DOM=1740)IOL=ARM10A
1730 LET X1$=X1$(6,20)
1740 REM " --- Discount percent
1745 LET X3$=""
1750 FIND (ARM10_DEV,KEY=N0$+"I"+A0$(64,2),DOM=1800)IOL=ARM10I
1760 if discount_percent>0 then X3$=STR(DISCOUNT_PERCENT)+"%"
1800 REM " --- Job Name"
1810 DIM X8$(30)
1820 IF ARM09_DEV=0 THEN GOTO 1880
1830 FIND (ARM09_DEV,KEY=N0$+A0$(5,6)+A0$(86,10),DOM=1880)IOL=ARM09A
1840 LET X8$(1)=X8$
1850 GOTO 1900
1880 LET X8$(1)=A0$(86,10)
1900 REM " --- Print Heading"
1910 LET P=0
1920 GOSUB 15000
1940 LET T0=0,X6$=""
1990 READ (ARE13_DEV,KEY=A0$(1,17),DOM=2000)
2000 REM " --- Detail"
2020 LET K$=KEY(ARE13_DEV,END=3000)
2040 IF K$(1,17)<>A0$(1,17) THEN GOTO 3000
2060 READ (ARE13_DEV)IOL=ARE13A
2100 REM " --- Type"
2120 DIM Y0$(30)
2140 FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=2160)IOL=ARM10E
2150 LET X2$=W0$(23)
2160 IF POS(Y0$(25,1)=" SP")=0 THEN GOTO 2200
2170 DIM X9$(62),X2$(60)
2180 firm$=n0$,ivm01_itm$=w0$(33,20);gosub ivm01_itm
2190 LET X2$=CVS(X2$,35)
2200 IF Y0$(25,1)="M" AND POS(Y0$(28,1)="BI ")=0 THEN GOTO 2300
2210 GOSUB 12000; REM "WA:200
2220 GOSUB 5200
2250 REM " --- Print configurator/serial number info - WA# 207
2260 GOSUB 15400
2300 REM " --- Total"
2320 LET T0=T0+W[6]
2900 GOTO 2000
3000 REM " --- Total"
3020 GOSUB 5600
3080 IF O9=1 THEN LET A0$(68,1)="B"; WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A; GOTO 1100
4000 REM " --- Totals"
4005 IF O9>2 THEN CLOSE (PRTR_DEV,ERR=4100);goto 4100
4010 IF KOPIE<KOPIES THEN LET KOPIE=KOPIE+1; GOTO 0990
4015 CLOSE (PRTR_DEV,ERR=4016)
4016 IF O9=2 THEN GOTO 4085
4020 PRINT @(0,9),'CE',
4025 LET V4$="Are You Ready To Update The Invoice Print File"
4030 CALL "SYC.YN",0,V4$,0,V$,V3
4035 IF V$<>"YES" THEN GOTO 9900
4040 CALL "SYC.NB","Updating",LEN(M0$)+8,COLUMN
4050 READ (ARE04_DEV,KEY=N0$+"I",DOM=4060)
4060 LET K1$=KEY(ARE04_DEV,END=4100); READ (ARE04_DEV)
4065 IF K1$(1,3)<>N0$+"I" THEN GOTO 4100
4070 PRINT @(COLUMN,11),FNF$(A0$(5,P[0]),M0$)," ",A0$(42,7),
4075 EXTRACT (ARE03_DEV,KEY=K1$(1,2)+K1$(4)+"000",DOM=4060)IOL=ARE03A
4080 IF A0$(68,1)<>"B" THEN GOTO 4060
4085 LET A0$(68,2)="YN"
4090 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
4095 IF O9=1 THEN GOTO 4060
4100 REM " --- Close Files"
4110 IF O9<>3 THEN GOTO 4180
4120 READ (ARE03_DEV,KEY=A0$(1,10),DOM=4130)
4130 READ (ARM01_DEV,KEY=A0$(1,2)+A0$(5,6))IOL=ARM01A2
4140 READ (ARM02_DEV,KEY=A0$+"  ")IOL=ARM02A
4150 for f=1 to files1;close(channels1[f],err=*next)
4160 next f
4190 if o9=4 then exit
4195 RUN O9$
5200 REM " --- Item Detail"
5210 IF L>L9-1 THEN GOSUB 5800
5250 IF POS(Y0$(25,1)="MO")<>0 THEN GOTO 5280
5260 PRINT (PRTR_DEV)@(2),W[2],
5280 IF POS(Y0$(25,1)="MNO")<>0 THEN PRINT (PRTR_DEV)@(19),W1$(1,40),
5300 IF POS(Y0$(25,1)=" SRDP")<>0 THEN PRINT (PRTR_DEV)@(21),W0$(33),
5330 PRINT (PRTR_DEV)@(52),PORT$,
5340 IF KOPIE<4 AND POS(Y0$(25,1)=" SRDNP")<>0 THEN PRINT (PRTR_DEV)@(57),FNDM$(STR(W[1]:PRICE_MASK$)),
5360 IF KOPIE<4 AND POS(Y0$(25,1)="M")=0 THEN PRINT (PRTR_DEV)@(67),FNDM$(STR(W[6]:EXT_MASK$)),
5380 PRINT (PRTR_DEV)LF$,
5382 LET L=L+1
5387 IF POS(Y0$(25,1)="SP")<>0 AND L>L9-1 THEN GOSUB 5800
5390 IF POS(Y0$(25,1)="SP")<>0 THEN PRINT (PRTR_DEV)@(19),X2$,LF$,
5394 IF POS(Y0$(25,1)="SP")<>0 THEN LET L=L+1
5396 RETURN
5600 REM " --- Total"
5610 PRINT (PRTR_DEV)H9$(1,L9-L)
5620 GOSUB 6900
5630 PRINT (PRTR_DEV)@(1),HZ$,LF$,@(1),ust_msg$[0],
5632 IF KOPIE<4 THEN PRINT (PRTR_DEV)@(63-len(tr$[10])),tr$[10],@(67),FNDM$(STR(T0:EXT_MASK$)),
5640 PRINT (PRTR_DEV)LF$,@(1),ust_msg$[1],
5645 IF KOPIE<4 THEN PRINT (PRTR_DEV)@(63-len(tr$[11])),tr$[11],@(67),FNDM$(STR(A[1]:EXT_MASK$)),
5650 PRINT (PRTR_DEV)LF$,@(1),ust_msg$[2],
5655 IF KOPIE<4 THEN PRINT (PRTR_DEV)@(63-len(f3$)),f3$,@(67),FNDM$(STR(A[0]:EXT_MASK$)),LF$,@(1),HZ$
5660 LET ITOT=T0+A[0]-A[2]+A[1]
5662 IF KOPIE<4 THEN PRINT (PRTR_DEV)@(63-len(tr$[12])),tr$[12],@(66),FNDM$(STR(ITOT:EXT_MASK$)),EUROSIGN$
5665 PRINT (PRTR_DEV)@(1),HZ$
5670 IF KOPIE>1 THEN PRINT (PRTR_DEV)@(1),tr$[14],STR(KOPIE),
5680 PRINT (PRTR_DEV)'FF',
5690 RETURN
5800 REM " --- Continued"
5820 PRINT (PRTR_DEV)H9$(1,L9-L)
5840 PRINT (PRTR_DEV)lf$,@(65),tr$[13]
5850 IF KOPIE>1 THEN PRINT (PRTR_DEV)H9$(1,4),@(1),tr$[14],STR(KOPIE),
5860 PRINT (PRTR_DEV)'FF',
5870 GOSUB 15000
5890 RETURN
6200 REM " --- Messages"
6220 FOR X=1 TO 2
6240 FIND (ARM10_DEV,KEY=N0$+"G"+A0$(70,2)+STR(X),DOM=6280)IOL=ARM10G
6250 FOR Y=1 TO 5
6255 IF POS(" "<>X4$(Y*40-39,40))=0 THEN GOTO 6270
6256 IF L>L9-2 THEN GOSUB 5800
6257 IF X=1 AND Y=1 THEN PRINT (PRTR_DEV)LF$,; LET L=L+1
6260 PRINT (PRTR_DEV)@(27),X4$(Y*40-39,40)
6265 LET L=L+1
6270 NEXT Y
6280 NEXT X
6290 RETURN
6900 REM " --- Print UST ID stuff"
6910 dim ust_msg$[2]
6920 if ust_id$="" then return
6930 ust_msg$[0]="PER DOWNLOAD"
6940 rem 'ust_msg$[1]="UST ID No. Customer: "+ust_id$
6945 ust_msg$[1]="Your VAT ID: "+ust_id$
6950 rem 'ust_msg$[2]="UST ID No. BASIS Europe Distribution GmbH: DE 194403119"
6955 ust_msg$[2]="Our VAT ID: DE 194403119"
6960 if lang$="FR" then 
6962 ust_msg$[0]="Il s'agit d'une livraison par t"+chr(130)+"l"+chr(130)+"chargement" 
6964 ust_msg$[1]="NO. TVA CLIENT: "+ust_id$
6966 ust_msg$[2]="NO. TVA BASIS Europe Distribution GmbH: DE 194403119"
6970 fi
6990 RETURN
7000 REM 7000 " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7140
7030 IF V3=127 THEN GOTO 7100
7040 RETURN
7100 REM 7100 " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7140,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,LF$
7140 REM " --- Error During Input"
7150 ESCAPE
7160 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
8300 DEF FNDM$(Q1$)
8310 LET Q2$=".,."; REM period,comma,period
8320 FOR Q=1 TO LEN(Q1$); LET Q0=POS(Q1$(Q,1)=Q2$); IF Q0 THEN LET Q1$(Q,1)=Q2$(Q0+1,1) FI ; NEXT Q
8330 RETURN Q1$
8340 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,LF$
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,LF$
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
10000 REM " --- Get 30 character address from Telemaster"
10005 dim tmm01$:fattr(tmm01$)
10010 READ record(TMM01_DEV,KNUM=0,KEY=A0$(1,2)+A0$(5,6)+"000000",dom=*next)tmm01$
10020 CUST_TYPE$=TMM01.cust_type$,ust_id$=cvs(tmm01.ust_id$,3)
10030 B$=TMM01$(142,60)+TMM01$(389,60)+TMM01.city$+" "+TMM01.state$+"  "+TMM01.country_code$+FILL(28," ")+TMM01.zip_code$
10040 CALL "SYC.AX",B$,30,6,P[2],30
10050 B$=B1$(1,30)+CONTACT$+B$
10060 tmp$=cvs(tmm01.lang_code$,7)
10070 if tmp$="" tmp$=cvs(tmm01.country_code$,7)
10080 if o9<>4 or cvs(lang$,3)="" then lang$=tmp$ 
10090 tmp=unt,exists=0
10100 open(tmp,err=*next)"BSG02_"+tmm01.country_code$+".txt";exists=1
10110 close(tmp)
10120 if exists then 
10130   bsg02_txt$="<"+tmm01.country_code$+">"
10140 else
10150   bsg02_txt$="<EN>"
10160 fi
10200 translate:
10210 dim tr$[19]
10220 tr$[1]="    INVOICE NUMBER:",tr$[2]="I N V O I C E"
10230 tr$[3]="      INVOICE DATE:",tr$[4]="              PAGE:"
10240 tr$[5]="      ORDER NUMBER:",tr$[9]="        ORDER DATE:"
10250 tr$[6]="       CUSTOMER NO:"
10260 tr$[7]="  CUSTOMER P.O.  |   SHIP VIA    |   DISCOUNT            |   TERMS   "
10270 tr$[8]="  ORDER          | PRODUCT"+fill(31)+"|  PRICE   |  TOTAL"
10280 tax$="19";if a[4] and a[0] then tax$=str(round(a[0]/a[4],2)*100) 
10290 f3$=tax$+"% UST.:"
10300 tr$[10]="TOTAL NET:",tr$[11]="           FREIGHT:",tr$[12]="INVOICE TOTAL:"
10310 tr$[13]="    Continued",tr$[14]="Copy  ",tr$[15]=" Coverage through: "
10320 tr$[16]="Number of users: "
10330 tr$[17]="Serial number"
10340 tr$[18]="New "
10350 tr$[19]="Deactivated serial number"
10400 for loop=1 to 19
10410 srch$=str(loop:"00")
10420 gosub get_text
10430 if text$<>"" then tr$[loop]=text$
10440 next loop
10600 sndesc$=tr$[17],usrdesc$=tr$[16]
10610 newsn$=tr$[18]+sndesc$,newusr$=tr$[18]+usrdesc$,oldsn$=tr$[19]
10620 if lang$="DE" and pos("Neue"=newusr$) then newusr$="Neu "+usrdesc$
10690 return
11000 get_text:
11010 srch$=cvs(srch$+"_"+lang$+":",4),text$=""
11020 p0=pos(srch$=file_text$)
11030 if p0=0 then return
11040 tmp$=file_text$(p0+len(srch$))
11050 p0=pos("{"=tmp$),p1=pos("}"=tmp$)
11060 if p0=0 or p1=0 or p1<p0 then return
11070 text$=tmp$(p0+1,p1-p0-1)
11080 return
12000 REM " --- Find Port Id
12010 LET PORT$=""
12030 IF Y0$(25,1)<>"S" THEN GOTO 12090
12040 FIND (CSM03_DEV,KEY=N0$+W0$(39,3),DOM=12090)IOL=CSM03A; REM "WA:200
12050 LET PORT$=CS2$(1,5); IF W0$(33,2)="P4" THEN LET PORT$(5,1)="4"
12090 RETURN
14500 REM Date with 4 digit year - German version dd.mm.yyyy
14505 DEF FNA4$(Q$,Q2$)=STR(((ASC(Q$)-32)+1900)*POS(" "<>Q2$(2,1)):"0000")
14510 DEF FNB4$(Q1$)=FNA$(Q1$(3),Q1$)+"."+FNA$(Q1$(2),Q1$)+"."+FNA4$(Q1$(1),Q1$)
15000 REM " --- Heading"
15002 IF PAGECOUNT>20 and o9<4 THEN LET CLOSEPRTR=1,PAGECOUNT=0
15005 LET L=0,P=P+1,PAGECOUNT=PAGECOUNT+1
15010 IF O9=3 or o9=9 THEN LET ORDER$=A0$(42,7),INVOICE$=A0$(11,7) ELSE LET ORDER$=A0$(11,7),INVOICE$=A0$(42,7)
15020 IF O9=3 THEN PRINT (PRTR_DEV)@(60),"DUPLICATE" ELSE PRINT (PRTR_DEV)""
15030 PRINT (PRTR_DEV)LF$,LF$,; FOR I=1 TO 6; PRINT (PRTR_DEV)@(7),B$(I*30-29,30); NEXT I
15035 IF CVS(UIDNR$,2)<>"" THEN PRINT (PRTR_DEV)@(7),"UID NR. ",UIDNR$,
15040 temp$=tr$[2]
15042 if kopie=4 then temp$="L I E F E R S C H E I N";if lang$="IT" then temp$="B o l l a  d i  C o n s e g n a" else if lang$="FR" then temp$="B o n  d e  L i v r a i s o n"
15045 PRINT (PRTR_DEV)LF$,HZ$,LF$,@(0),tr$[1],@(21),INVOICE$,@(70-len(temp$)),'BB',temp$,'EB',LF$,@(0),tr$[3],@(21),FNB4$(A0$(24,3)),LF$,@(0),tr$[9],@(21),FNB4$(A0$(24,3)),LF$,@(0),tr$[4],@(21),P,"  ",bsg02_txt$
15050 PRINT (PRTR_DEV)@(0),tr$[5],@(21),ORDER$,LF$,
15052 IF KOPIE<4 THEN PRINT (PRTR_DEV)@(0),tr$[6],@(21),FNF$(A0$(5,P[0]),M0$),
15054 PRINT (PRTR_DEV)LF$,@(1),HZ$; IF KOPIE>3 THEN LET X3$=""; REM Disc% not printed on picklist copy
15060 PRINT (PRTR_DEV)@(0),tr$[7]
15070 PRINT (PRTR_DEV)@(3),A0$(49,10),@(17),"| ",A0$(32,10),@(33),"| ",X3$,@(57),"| ",X1$,LF$,@(1),HZ$
15080 PRINT (PRTR_DEV)@(0),tr$[8],LF$,@(1),HZ$
15090 RETURN
15400 REM " --- configuration detail
15402 IF X5$(1,1)<>"Y" THEN GOTO 15900
15405 FIND (ARE73_DEV,KEY=W0$(1,20),DOM=15900)IOL=ARE73A
15410 IF L+1>L9 THEN GOSUB 5800
15415 PRINT (PRTR_DEV)@(21),"Type: ",WX1$(4,3),
15420 IF WX1$(4,1)="S" or wx1$(16,3)="RN1" THEN READ RECORD (SMC01_DEV,KEY=WX1$(31,6),ERR=*NEXT)SMC01$; PRINT (PRTR_DEV)tr$[15],SMC01.ANNUAL_DT$(7,2),".",SMC01.ANNUAL_DT$(5,2),".",str(num(SMC01.ANNUAL_DT$(1,4))+sam_renew),
15425 IF WX[1] THEN PRINT (PRTR_DEV)" Media: ",WX1$(1,3)," Sets: ",WX[1]:"#0",
15430 PRINT (PRTR_DEV)LF$,
15435 LET L=L+1
15440 IF L+1>L9 THEN GOSUB 5800
15445 u$=usrdesc$;if wx1$(4,3)="ADD" then u$=newusr$
15450 u$=u$+str(wx[2]:"###0- ")+wx1$(10,2)+" "+str(WX[3]:"#### ")+wx1$(12,2)
15455 PRINT (PRTR_DEV)@(21),cvs(u$,35),
15460 PRINT (PRTR_DEV)LF$,
15465 LET L=L+1
15500 REM " --- serial number detail"
15510 LET ACT$="",DEACT$="",MOD$=""
15520 READ (ARE83_DEV,KEY=W0$(1,20),DOM=15521)
15530 LET ARE83_K$=KEY(ARE83_DEV,END=15600)
15540 IF POS(W0$(1,20)=ARE83_K$)<>1 THEN GOTO 15600
15550 READ (ARE83_DEV,KEY=ARE83_K$)IOL=ARE83A
15560 IF WY1$(1,1)="M" THEN LET MOD$=MOD$+WY2$
15570 IF WY1$(1,1)="A" THEN LET ACT$=ACT$+WY2$
15580 IF WY1$(1,1)="D" THEN LET DEACT$=DEACT$+WY2$
15590 GOTO 15530
15600 REM " --- print them
15610 LET STR$=MOD$,TITLE$=sndesc$+": "
15620 GOSUB 15700
15630 LET STR$=ACT$,TITLE$=newsn$+": "
15640 GOSUB 15700
15650 LET STR$=DEACT$,TITLE$=oldsn$+": "
15660 GOSUB 15700
15665 PRINT (PRTR_DEV)LF$,; LET L=L+1; IF L>L9 THEN GOSUB 5800
15670 GOTO 15900
15700 REM " --- print them
15705 IF STR$="" THEN GOTO 15790
15715 LET SNUMBS=INT(LEN(STR$)/20)
15724 IF L+2>L9 THEN GOSUB 5800
15730 PRINT (PRTR_DEV)@(21),TITLE$; LET L=L+1
15740 PRINT (PRTR_DEV)@(23),
15750 FOR SL=1 TO SNUMBS
15760 PRINT (PRTR_DEV)STR$((SL-1)*20+1,20),"  ",
15770 IF MOD(SL,2)=0 THEN PRINT (PRTR_DEV)LF$,@(23),; LET L=L+1
15772 IF L+1>L9 THEN GOSUB 5800; PRINT (PRTR_DEV)@(23),
15780 NEXT SL
15785 IF MOD(SNUMBS,2)<>0 THEN PRINT (PRTR_DEV)LF$,; LET L=L+1
15787 IF L+1>L9 THEN GOSUB 5800
15790 RETURN
15900 REM " --- Done"
15990 RETURN
16200 ivm01_itm:
16210 ivmok=0
16220 READ (ivm01_dev,KEY=firm$+ivm01_itm$,ERR=*next)iol=ivm01a;ivmok=1
16230 if ivmok or len(cvs(ivm01_itm$,3))<15 then return
16240 ivm01_itm$(10,3)="CUR"
16250 if ivm01_itm$(13,3)<>"STD" and ivm01_itm$(1,3)="BAS" then ivm01_itm$(13,3)="EXP"
16260 READ (ivm01_dev,KEY=firm$+ivm01_itm$,ERR=*next)iol=ivm01a;ivmok=1
16290 return
17000 REM German or English?
17010 PRINT @(0,3),'CE','SB',@(10,8),"Print German or English Invoice? (G/E):",'SF',
17020 CHOOSE: 
17030 LET V$="",V0$="S",V1$="CE^",V2$="",V3$="",V4$="",V0=1,V1=51,V2=8
17040 GOSUB 7000; PRINT @(V1,V2),V$; IF V3=4 THEN EXITTO 9900
17050 IF POS(V$="GE")=0 OR LEN(V$)<>1 THEN GOTO CHOOSE
17060 PRINT @(0,V2),'CL'; RETURN
46500 END

0010 REM "ECM - E_commerce Customer Data"
0020 REM "Program BEC.01"
0050 SETESC 9000
0060 SETERR 9000
0090 ENTER N0$,CNUM$,user_code$
0095 IF NUM(CNUM$,ERR=9900)=0 THEN GOTO 9900
0100 REM " --- Open/Lock Files
0110 LET FILES=1
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ECM-01"
0170 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0175 IF STATUS>0 THEN GOTO 9900
0180 LET ECM01_DEV=CHANNELS[1]
0200 REM " --- IOLists
0210 call "templates.pgm::ECM01"
0500 REM " --- Initializations"
0520 ecm01.firm_id$=n0$,ecm01.customer_nbr$=cnum$,ecm01.user_code$=user_code$
0600 REM "SET PARAMETERS
0610 LET WIDTH=80,HEIGHT=12,WIN_X=0,WIN_Y=6
0655 LET TITLE$="E-Commerce Setup",NAME$=TITLE$(1,8),X$=STBL("WINDOW_NAME",NAME$)
0700 REM " --- Background
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0730 PRINT @(1,1),"E-Mail Address 1:",@(1,4),"E-Mail Address 2:",@(1,7),"Password:"
0750 PRINT 'SF',
1000 REM 
1100 FIND record(ECM01_DEV,KEY=ecm01$(1,10),DOM=*next)ecm01$;goto 1300
1110 if cvs(ecm01.user_code$,1+2)="" then goto 2000
1120 rem ' if user doesn't exist get defaults 
1130 find record(ecm01_dev,key=ecm01$(1,8)+"  ",dom=2000)ecm01$
1140 ecm01.user_code$=user_code$
1300 REM " --- Display"
1320 GOSUB 5000
1340 GOTO 4000
2000 REM 
2100 REM " --- E-mail"
2120 LET V0$="S",V1$="C",V2$=ecm01.e_mail$,V3$="",V0=76,V1=1,V2=2,I0=0
2130 LET V4$="Enter customer's primary e-mail address"
2140 GOSUB 7000
2160 LET ecm01.e_mail$=V$
2190 GOTO 3900
2200 REM " --- Password"
2220 LET V0$="S",V1$="C",V2$=ecm01.password$,V0=20,V1=11,V2=7,V4$=""
2225 IF CVS(V2$,2)="" THEN LET V4$="Assign password (NEW to generate random password)"
2230 GOSUB 7000; IF CVS(V$,4)="NEW" THEN GOSUB PASSWORD; LET V$=P$
2280 LET ecm01.password$=V$; PRINT @(V1,V2),V$
2290 GOTO 3900
2300 REM " --- E-mail 2"
2320 LET V0$="S",V1$="C",V2$=ecm01.e_mail2$,V0=76,V1=1,V2=5
2330 LET V4$="Optional 2nd e-mail address"
2340 GOSUB 7000
2380 LET ecm01.e_mail2$=V$
2390 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 IF ABS(I0)>1000 THEN GOTO 3990
3960 ON I0 GOTO 2100,2300,2200,3990
4000 REM " --- Options"
4010 LET V0$="S",V1$="KC^",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 PRINT @(0,22),'CL',
4040 IF V$="DELETE" THEN GOTO 4200
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4110 ecm01$=field(ecm01$)
4120 WRITE record(ECM01_DEV,KEY=ecm01$(1,10))ECM01$
4130 if ecm01.user_code$="  " then call "cust_web_updt.pgm",err=*next,ecm01$(1,8)
4180 GOTO 9900
4200 REM " --- Delete
4220 REMOVE (ECM01_DEV,KEY=ecm01$(1,10),DOM=*next)
4590 GOTO 9900
5000 REM " --- Display"
5020 PRINT @(1,2),ecm01.e_mail$,@(1,5),ecm01.e_mail2$,@(11,7),ecm01.password$
5090 RETURN 
6010 PASSWORD: 
6020 LET N$="0123456789",A$="abcdefghijkmnopqrstuvwxyz"+N$
6030 LET C=7,L=7,P$="",I=123,N=2
6040 RNDI: LET X=RND(I),T$=CHR(X)
6050 IF POS(T$=A$)=0 THEN GOTO RNDI
6060 LET N1=POS(T$=N$); IF N1 AND C=L THEN GOTO RNDI
6070 IF N1 THEN IF N<1 THEN GOTO RNDI ELSE LET N=N-1
6080 LET P$=P$+T$,C=C-1; IF C THEN GOTO RNDI
6090 IF N>1 THEN LET X=RND(10),Y=RND(L); IF Y=0 THEN LET Y=Y+1 FI; LET P$(Y+1,1)=N$(X+1,1)
6100 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8100 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM "Exit"
9910 CLOSE (ECM01_DEV,ERR=9911)
9915 SETERR 9998
9920 PRINT 'POP',
9998 EXIT 
9999 END

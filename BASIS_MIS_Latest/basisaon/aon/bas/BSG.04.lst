0010 REM BSG.04 - BSG License/ActKey forms printing
0020 LET SYS01T=UNT; OPEN (SYS01T)"SYS-01"
0030 LET TERMINAL$="T"+FID(0); READ (SYS01T,KEY=TERMINAL$)*,F0$
0040 CLOSE (SYS01T)
0050 REM Printer opened in BLK.12, channel is 'printer'
0060 LET BASDIR$="/basisaon/aon/bas/",PERLDIR$="/local/mas90/SO",CURRENTDIR$=DIR("")
0070 LET INFILE$=BASDIR$+FID(0)+"BLK10.txt"
0080 LET INFILECHAN=UNT; OPEN (INFILECHAN,ERR=END_PROGRAM)INFILE$
0090 LET DSBFILE$=BASDIR$+FID(0)+"BLK10dsb.txt"
0100 TEMPLATES: 
0110 LET KEY_TMP$="number:c(10*=44),auth_key:c(13*=44),users:c(3*=44),order_no:c(7*=44),order_date:c(10*=44),part_num:c(22*=44),desc1:c(30*=44),rev_lvl:c(10*=44),system:c(48*=44),po:c(5*=44),cust:c(6)"
0120 LET LIC_TMP$="number:c(10*=44),auth_key:c(10*=44),users:c(3*=44),order_no:c(7*=44),order_date:c(10*=44),part_num:c(22*=44),desc1:c(30*=44),rev_lvl:c(10*=44),system:c(48*=44),flex_feature:c(7*=44),flex_key:c(12*=44),flex_rev_lvl:c(10*=44),flex_host_id:c(10*=44),flex_expiry_date:c(11*=44),flex_checksum:c(2*=44),flex_users:c(10*=44),flex_number:c(10*=44),platform:c(1*=44),po:c(5*=44),cust:c(6)"
0130 LET HOLDSN$="",CO$="BAS"; IF F0$(16,2)="02" THEN LET CO$="BSG"
0140 MAIN_LOOP: 
0150 READ (INFILECHAN,END=END_OF_FILE)READ_REC$
0160 IF CVS(READ_REC$,3)=$$ THEN GOTO MAIN_LOOP
0170 REM ' Parse activation key or authorization number into k$
0180 LET K$="",C1=POS(","=READ_REC$)
0190 IF C1 THEN LET C2=POS(","=READ_REC$(C1+1)); IF C2 THEN LET K$=READ_REC$(C1+1,C2-1)
0200 LET K_SIZE=LEN(CVS(K$,3)); REM Keys are 13 characters, authorization numbers are 10
0210 IF K_SIZE=10 THEN DIM REC$:LIC_TMP$; LET FORMTYPE$="win" ELSE DIM REC$:KEY_TMP$; LET FORMTYPE$="act"
0220 LET REC$=READ_REC$,NEWFILE$="y"
0230 GOSUB SETUP_FILE
0240 LET HOST_ID$=CVS(REC.AUTH_KEY$,2)
0250 LET U$=REC.USERS$,U=NUM(U$,ERR=0260); LET U$=U$+" user"; IF U>1 THEN LET U$=U$+"s"
0260 IF FORMTYPE$="act" THEN GOTO SCALL_PERL
0270 LET FEATURE$=CVS(REC.FLEX_FEATURE$,2)
0280 LET ENC_CODE$=CVS(REC.FLEX_KEY$,2)
0290 LET LIC_REV$=CVS(REC.FLEX_REV_LVL$,2)
0300 LET HOST_ID$=CVS(REC.FLEX_HOST_ID$,2)
0310 LET EXPIRE$=CVS(REC.FLEX_EXPIRY_DATE$,2)
0320 LET CHECKSUM$=CVS(REC.FLEX_CHECKSUM$,2)
0330 LET NUM_USERS$=CVS(REC.FLEX_USERS$,2)
0340 SCALL_PERL: 
0350 LET PERL$="perl emailform.pl "+NEWFILE$+" "+$22$+FORMTYPE$+$22$+" "+$22$+OUTFILE$+$22$+" "+$22$+CO$+$22$+" "+$22$+CNUM$+$22$+" "+$22$+S$+$22$+" "+$22$+T$+$22$+" "
0360 LET PERL$=PERL$+$22$+CVS(REC.PART_NUM$,2)+$22$+" "+$22$+REC.ORDER_DATE$+$22$+" "+$22$+CVS(REC.REV_LVL$,2)+$22$+" "
0370 LET PERL$=PERL$+$22$+CVS(REC.SYSTEM$,2)+$22$+" "+$22$+REC.ORDER_NO$+$22$+" "+$22$+CVS(U$,2)+$22$+" "+$22$+REC.CUST$+$22$+" "+$22$+CVS(REC.NUMBER$,2)+$22$+" "
0380 LET PERL$=PERL$+$22$+CVS(REC.AUTH_KEY$,2)+$22$+" "+$22$+FEATURE$+$22$+" "+$22$+ENC_CODE$+$22$+" "+$22$+LIC_REV$+$22$+" "+$22$+HOST_ID$+$22$+" "
0390 LET PERL$=PERL$+$22$+EXPIRE$+$22$+" "+$22$+CHECKSUM$+$22$+" "+$22$+NUM_USERS$+$22$+" "+$22$+CVS(REC.DESC1$,2)+$22$
0400 CHDIR PERLDIR$
0410 LET A=SCALL(PERL$)
0420 CHDIR CURRENTDIR$
0430 GOTO MAIN_LOOP
0440 END_OF_FILE: 
0450 GOSUB PRINT_FILE
0460 CLOSE (INFILECHAN)
0470 ERASE INFILE$,ERR=ERASE_DSB
0480 ERASE_DSB: ERASE DSBFILE$,ERR=END_PROGRAM
0490 SETERR END_PROGRAM; PRINT 'POP'
0500 GOTO END_PROGRAM
0510 SETUP_FILE: 
0520 IF HOLDSN$<>"" THEN GOSUB PRINT_FILE
0530 LET HOLDSN$=REC.NUMBER$,OUTFILE$=BASDIR$+REC.NUMBER$+".txt"
0540 ERASE OUTFILE$,ERR=SETUP_RETURN
0550 SETUP_RETURN: RETURN 
0560 PRINT_FILE: 
0570 PRINT 'CS','CR',"  Printing ",HOLDSN$; WAIT 1
0580 LET OUTCHAN=UNT,PRINTING=0; OPEN (OUTCHAN)OUTFILE$
0590 LET F$=FIN(OUTCHAN),SZ=DEC(F$(1,4))
0600 READ RECORD(OUTCHAN,SIZ=SZ)OUT$
0610 CLOSE (OUTCHAN)
0620 LET OUTREC$=""
0630 POS_LF: LET P=POS($0A$=OUT$); IF P>79 THEN LET OUT$=OUT$(1,78)+$0A$+OUT$(79),P=79
0640 REM Look for the serial number in the text file created by perl.
0650 REM Don't print until you pass it - this strips email headers.
0660 IF P AND PRINTING=0 AND POS(HOLDSN$+".txt"=OUT$(1,P)) THEN LET PRINTING=1,OUT$=OUT$(P+1); GOTO POS_LF
0670 IF P AND PRINTING THEN LET OUTREC$=OUTREC$+OUT$(1,P)
0680 IF P THEN LET OUT$=OUT$(P+1); GOTO POS_LF
0690 PRINT (PRINTER)OUTREC$,'FF',
0700 ERASE OUTFILE$,ERR=PRINT_FILE_END
0710 PRINT_FILE_END: RETURN 
0720 END_PROGRAM: 
0730 RUN "SYS.AA"

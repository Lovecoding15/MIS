0010 REM " Program BEM.01 - Email address maintenance
0020 SETERR NOT_CALLED
0030 SETESC 9000
0040 LET CALLED_PROGRAM=0
0050 ENTER FIRM$,CUST$,tm_number$
0060 LET CALLED_PROGRAM=1,user_code$=fill(2),mainkey$=cust$+tm_number$
0070 SETERR 9000
0080 GOTO OPEN_FILES
0090 NOT_CALLED: 
0100 BEGIN
0110 SETESC 9000
0120 SETERR 9000
0130 LET SYS01T=UNT; OPEN (SYS01T)"SYS-01"
0140 LET TERMINAL$="T"+FID(0)
0150 READ (SYS01T,KEY=TERMINAL$)*,F0$; CLOSE (SYS01T)
0160 LET FIRM$=F0$(16,2),SECURITY_LEVEL=NUM(F0$(15,1))
0170 OPEN_FILES: 
0180 LET FILES=4,F=0; DIM FILES[FILES]
0190 LET ARM01=UNT; OPEN (ARM01)"ARM-01"; LET FILES[F]=ARM01,F=F+1
0200 LET TMM01=UNT; OPEN (TMM01)"TMM-01"; LET FILES[F]=TMM01,F=F+1
0210 LET ECM01=UNT; OPEN (ECM01)"ECM-01"; LET FILES[F]=ECM01,F=F+1
0220 LET TMM03=UNT; OPEN (TMM03)"TMM-03"; LET FILES[F]=TMM03,F=F+1
0230 LET TMM05=UNT; OPEN (TMM05)"TMM-05"; LET FILES[F]=TMM05,F=F+1
0240 call "templates.pgm::TMM01"
0250 call "templates.pgm::ARM01"
0260 REM templates below do not match data dictionary - redefined for convenience in this program
0270 DIM TMM03$:"firm_id:c(2),main_sort:c(12),sequence_nbr:c(3),contact_name:c(30),skip1:c(205),email:c(80)"
0280 DIM TMM05$:"firm_id:c(2),main_sort:c(12),email:c(80),available:c(34)"
0290 DIM ECM01$:"firm_id:c(2),customer_nbr:c(6),user_code:c(2),email:c(80),skip:c(20),email2:c(80),available:c(80)"
0300 IF CALLED_PROGRAM THEN GOTO BUILD_EMLIST
0310 LET L0=6
0320 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0330 PRINT 'SB',@(2,3),"Customer No:",@(25,3),"User Code:",@(9,4),"Name:",@(0,6),'CE','SF',
0340 PRINT @(0,L0),'CE',
0350 CUST_INPUT: PRINT 'CF',
0370 LET V2$="",P1$="000000"
0380 LET V0$="M",V1$="KCE",V3$=P1$,V0=6,V1=15,V2=3,V4$="<F3>=Customer Lookup"
0390 GOSUB 7000
0400 IF V3=4 THEN GOTO END_PROGRAM
0410 IF V3=3 THEN CALL "SYC.LC",V$
0420 IF V$="" THEN GOTO CUST_INPUT
0430 PRINT @(15,3),V$,
0440 READ RECORD (ARM01,KEY=FIRM$+V$,DOM=CUST_INPUT)ARM01$
0450 PRINT @(15,4),ARM01.CUST_NAME$,
0460 READ (TMM01,KEY=FIRM$+V$,KNUM=0,DOM=READ_TMM01)
0470 READ_TMM01: 
0480 READ RECORD (TMM01,END=CUST_INPUT)TMM01$
0490 IF TMM01.FIRM_ID$+TMM01.CUSTOMER_NBR$<>FIRM$+V$ THEN GOTO CUST_INPUT
0500 LET CUST$=ARM01.CUSTOMER_NBR$,MAINKEY$=TMM01.customer_nbr$+tmm01.tm_number$
0600 rem " user code for muli-use of b-Commerce
0610 LET V2$=""
0620 LET V0$="S",V1$="CE^",V3$="",V0=2,V1=36,V2=3,V4$="Enter User Code (<Enter>=Default)"
0630 GOSUB 7000
0640 IF V3=4 THEN GOTO END_PROGRAM
0650 if v3=2 then goto cust_input
0660 if v$="" then v$=fill(2)
0670 LET user_code$=V$
0680 PRINT @(36,3),user_code$,
0710 BUILD_EMLIST: 
0720 LET EMLIST$="",EM_COUNT=0,EM_CHAN=TMM05,K$=FIRM$+MAINKEY$,K=0
0730 DIM TMPL$:FATTR(TMM05$)
0740 GOSUB EMLIST
0750 IF RECORD_EXISTS THEN GOTO ECOMM
0760 LET TMM05.FIRM_ID$=FIRM$,TMM05.MAIN_SORT$=MAINKEY$
0770 WRITE RECORD (TMM05,KEY=K$)TMM05$
0780 GOSUB EMLIST
0790 ECOMM: 
0800 LET EM_CHAN=ECM01,K$=FIRM$+CUST$+user_code$,K=0
0810 DIM TMPL$:FATTR(ECM01$)
0820 GOSUB EMLIST
0830 LET EM_CHAN=TMM03,K$=FIRM$+MAINKEY$,K=0
0840 DIM TMPL$:FATTR(TMM03$)
0850 GOSUB EMLIST
0860 GOSUB DISPLAY_LIST
0870 IF CALLED_PROGRAM THEN GOTO END_PROGRAM ELSE GOTO CUST_INPUT
0880 EMLIST: 
0890 LET RECORD_EXISTS=0
0900 READ RECORD (EM_CHAN,KEY=K$,KNUM=K,DOM=MAIN_READ)TMPL$
0910 LET K1$=K$; GOTO KEY_FOUND
0920 MAIN_READ: LET K1$=KEY(EM_CHAN,KNUM=K,END=EMLIST_END)
0930 IF POS(K$=K1$)<>1 THEN GOTO EMLIST_END
0940 READ RECORD (EM_CHAN,KNUM=K,KEY=K1$)TMPL$
0950 KEY_FOUND: DIM DESC$(30); LET RECORD_EXISTS=1
0960 LET DESC$(1)="Main Company Address:"
0970 IF EM_CHAN=TMM03 THEN LET DESC$(1)=CVS(TMPL.CONTACT_NAME$,2)+":"
0980 IF EM_CHAN=ECM01 THEN LET DESC$(1)="E-Commerce 1:"
0990 LET EMLIST$=EMLIST$+DESC$+TMPL.EMAIL$+STR(EM_CHAN:"000")+K1$+$0A$,EM_COUNT=EM_COUNT+1
1000 IF EM_CHAN=ECM01 THEN LET DESC$(1)="E-Commerce 2:",EMLIST$=EMLIST$+DESC$+TMPL.EMAIL2$+STR(EM_CHAN:"000")+K1$+$0A$,EM_COUNT=EM_COUNT+1
1010 GOTO MAIN_READ
1020 EMLIST_END: 
1030 RETURN
1040 DISPLAY_LIST: 
1050 LET WIDTH=78,HEIGHT=19,WIN_X=1,WIN_Y=5
1060 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
1070 LET HEADING$(2)="EMail Addresses for "+CVS(CUST$,3)+" "+tmm01.name_sort$
1080 LET FOOTING$(2)="Enter=Edit  F1=Add Contact  F4=End  PgUp  PgDn"
1090 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
1100 LET PAGES=INT(EM_COUNT*2/HEIGHT)+2,MAX_ROW=HEIGHT-6
1110 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,2]
1120 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-4),FOOTING$,'ER','SF',
1130 LET PAGE=1,L=1,X1=1,SELECTION$="",XMODE=5
1140 IF EM_COUNT=0 THEN LET LLIST$[PAGE,X1,1]=" None"
1150 NEXT_EM: LET P=POS($0A$=EMLIST$)
1160 IF P<115 THEN LET SELECTION$="|EOF"; GOSUB DISPLAY_PAGE; GOTO DISPLAY_LIST_END
1170 LET X$=EMLIST$(1,P-1),EMLIST$=EMLIST$(P+1)
1180 IF L>=MAX_ROW THEN GOSUB DISPLAY_PAGE
1190 LET LINE$=" "+X$(1,30)
1200 LET LLIST$[PAGE,X1,1]=LINE$,LLIST$[PAGE,X1,2]=X$(111),X1=X1+1,L=L+1
1210 LET LINE$="<"+CVS(X$(31,74),3)+">"; IF LINE$="<>" THEN LET LINE$="<"+FILL(74)+">"
1220 LET LLIST$[PAGE,X1,1]=LINE$,LLIST$[PAGE,X1,2]=X$(111),X1=X1+1,L=L+1
1230 GOTO NEXT_EM
1240 DISPLAY_LIST_END: 
1250 CALL "SYC.WD",NAME$
1260 RETURN
1270 DISPLAY_PAGE: 
1280 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
1290 IF FKEY=1 THEN GOSUB NEXT_TMM03; GOSUB EDIT_INFO; EXITTO BUILD_EMLIST
1300 IF FKEY=4 THEN EXITTO DISPLAY_LIST_END
1310 IF FKEY=-16 THEN LET PAGE=PAGE+1
1320 IF FKEY=0 THEN GOSUB EDIT_INFO; EXITTO BUILD_EMLIST
1330 LET L=1,X1=1,XMODE=5
1340 RETURN
1350 EDIT_INFO: 
1360 IF LEN(SELECTION$)<11 THEN GOTO EDIT_INFO_END
1370 LET EM_CHAN=NUM(SELECTION$(1,3),ERR=EDIT_INFO_END),K$=SELECTION$(4)
1380 IF EM_CHAN=ECM01 THEN DIM TMPL$:FATTR(ECM01$); LET F1$="E-Commerce:"
1390 IF EM_CHAN=TMM03 THEN DIM TMPL$:FATTR(TMM03$); LET F1$="Contact: "
1400 IF EM_CHAN=TMM05 THEN DIM TMPL$:FATTR(TMM05$); LET F1$="Main Company Address:"
1410 READ RECORD (EM_CHAN,KEY=K$,DOM=NEW_TMM03_REC)TMPL$; GOTO SETUP_EDIT
1420 NEW_TMM03_REC: 
1430 IF EM_CHAN=TMM03 THEN LET TMPL$=TMM03$ ELSE GOTO EDIT_INFO_END
1440 SETUP_EDIT: 
1450 IF EM_CHAN=TMM03 THEN LET F2$=TMPL.CONTACT_NAME$ ELSE LET F2$=""
1460 IF EM_CHAN=ECM01 THEN LET EM2$=TMPL.EMAIL2$ ELSE LET EM2$=""
1470 LET EM1$=TMPL.EMAIL$
1480 LET WIDTH=80,HEIGHT=7,WIN_X=0,WIN_Y=10,TITLE$=""
1490 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,ANAME$
1500 PRINT 'WRAP'("on"),'SB',@(0,0),F1$,
1510 PRINT 'SF',@(9,0),F2$,@(0,1),EM1$,@(0,2),EM2$
1520 BEGIN_EDIT: 
1530 IF EM_CHAN<>TMM03 THEN GOTO ENTER_EMAIL
1540 LET V0$="S",V1$="C",V2$=F2$,V3$="",V0=20,V1=9,V2=0
1550 LET V4$="Enter contact name"
1560 GOSUB 7000; LET F2$=V$
1570 ENTER_EMAIL: 
1580 IF EM_CHAN=TMM05 AND SECURITY_LEVEL<8 THEN GOTO EDIT_INFO_END
1590 LET V0$="S",V1$="C",V2$=EM1$,V3$="",V0=76,V1=1,V2=1
1600 LET V4$="Enter e-mail address"
1610 GOSUB 7000; LET EM1$=V$
1620 IF EM_CHAN<>ECM01 THEN GOTO END_EDIT
1630 LET V0$="S",V1$="C",V2$=EM2$,V3$="",V0=76,V1=1,V2=2
1640 LET V4$="Enter e-mail address"
1650 GOSUB 7000; LET EM2$=V$
1660 END_EDIT: 
1670 LET V0$="S",V1$="C^",V2$="Y",V3$="",V4$="Is the Above Correct (Y/N)? <F4>=Cancel",V0=1,V1=62,V2=4
1680 GOSUB 7000; IF V3=4 THEN GOTO EDIT_INFO_END
1690 PRINT @(0,V2),'CL',
1700 ON POS(V$="YN") GOTO END_EDIT,UPDATE_FILE,BEGIN_EDIT
1710 UPDATE_FILE: 
1720 LET TMPL.EMAIL$=EM1$; IF EM_CHAN=ECM01 THEN LET TMPL.EMAIL2$=EM2$
1730 IF EM_CHAN=TMM03 THEN LET TMPL.CONTACT_NAME$=F2$
1740 WRITE RECORD (EM_CHAN,KEY=K$)TMPL$
1750 EDIT_INFO_END: 
1760 CALL "SYC.WD",ANAME$
1770 CALL "SYC.WD",NAME$
1780 RETURN
1790 NEXT_TMM03: 
1800 LET SEQ=0,SELECTION$=""
1810 NEXT_SEQ: LET SEQ=SEQ+10; IF SEQ>990 THEN RETURN
1820 LET SEQ$=STR(SEQ:"000"),K$=FIRM$+MAINKEY$+SEQ$
1830 READ (TMM03,KEY=K$,DOM=TMM03_SETUP); GOTO NEXT_SEQ
1840 TMM03_SETUP: LET SELECTION$=STR(TMM03:"000")+K$
1850 LET TMM03.FIRM_ID$=FIRM$,TMM03.MAIN_SORT$=MAINKEY$,TMM03.SEQUENCE_NBR$=SEQ$,TMM03.CONTACT_NAME$=FILL(30),TMM03.SKIP1$=FILL(205),TMM03.EMAIL$=FILL(80)
1860 RETURN
7000 REM 7000 " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7090
7030 IF V3=127 THEN GOTO 7050
7040 RETURN
7050 REM " --- Escape During Input"
7060 CALL "SYC.ES",ERR=7090,PGM(-2),TCB(8),E$,E2,V3
7070 IF V3<>127 THEN GOTO 7000
7080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7090 REM " --- Error During Input"
7100 ESCAPE
7110 GOTO 7000
9000 REM 9000 " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9060,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9070
9040 IF E1=3 THEN GOTO 9090
9050 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9060 ESCAPE
9070 REM " --- Retry"
9080 RETRY
9090 REM " --- Return"
9100 SETERR 9000
9110 GOTO 9900
9300 REM 9300 " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9340,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9350
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9340 ESCAPE
9350 RETURN
9800 REM 9800 " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM 9900
9910 END_PROGRAM: CALL "SYC.WD",NAME$
9920 CALL "SYC.WD",ANAME$
9930 FOR F=0 TO FILES
9940 CLOSE (FILE[F],ERR=NEXT_FILECLOSE)
9950 NEXT_FILECLOSE: NEXT F
9960 IF CALLED_PROGRAM THEN EXIT ELSE RUN "SYS.AA"
9970 END

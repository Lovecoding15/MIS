0010 REM BLK.14 - Email License/ActKey forms
0020 BEGIN 
0030 LET SYS01T=UNT; OPEN (SYS01T)"SYS-01"
0040 LET TERMINAL$="T"+FID(0); READ (SYS01T,KEY=TERMINAL$)*,F0$
0050 CLOSE (SYS01T)
0060 LET ARM02CHAN=UNT; OPEN (ARM02CHAN)"ARM-02"
0070 LET BASDIR$="./aon/bas/",CURRENTDIR$=DIR("")
0080 LET INFILE$=BASDIR$+FID(0)+"BLK10.txt"
0090 LET INFILECHAN=UNT; OPEN (INFILECHAN,ERR=END_PROGRAM)INFILE$
0100 LET DSBEXIST=0,DSBFILE$=BASDIR$+FID(0)+"BLK10dsb.txt"
0110 LET DSBCHAN=UNT; OPEN (DSBCHAN,ERR=TEMPLATES)DSBFILE$; LET DSBEXIST=1
0120 TEMPLATES: 
0130 LET KEY_TMP$="number:c(10*=44),auth_key:c(13*=44),users:c(3*=44),order_no:c(7*=44),order_date:c(10*=44),part_num:c(22*=44),desc1:c(30*=44),rev_lvl:c(10*=44),system:c(48*=44),po:c(5*=44),cust:c(6)"
0140 LET LIC_TMP$="number:c(10*=44),auth_key:c(10*=44),users:c(3*=44),order_no:c(7*=44),order_date:c(10*=44),part_num:c(22*=44),desc1:c(30*=44),rev_lvl:c(10*=44),system:c(48*=44),flex_feature:c(7*=44),flex_key:c(12*=44),flex_rev_lvl:c(10*=44),flex_host_id:c(10*=44),flex_expiry_date:c(11*=44),flex_checksum:c(2*=44),flex_users:c(10*=44),flex_number:c(10*=44),platform:c(1*=44),po:c(5*=44),cust:c(6)"
0150 LET HOLD$="",CO$="BAS"; IF F0$(16,2)="02" THEN LET CO$="BSG"
0160 LET HOLDCUST$=""
0170 MAIN_LOOP: 
0180 READ (INFILECHAN,END=END_OF_FILE)READ_REC$
0190 IF DSBEXIST THEN READ (DSBCHAN)DSB$
0200 IF CVS(READ_REC$,3)=$$ THEN GOTO MAIN_LOOP
0210 REM ' Parse activation key or authorization number into k$
0220 LET K$="",C1=POS(","=READ_REC$)
0230 IF C1 THEN LET C2=POS(","=READ_REC$(C1+1)); IF C2 THEN LET K$=READ_REC$(C1+1,C2-1)
0240 LET K_SIZE=LEN(CVS(K$,3)); REM Keys are 13 characters, authorization numbers are 10
0250 IF K_SIZE=10 THEN DIM REC$:LIC_TMP$; LET FORMTYPE$="win" ELSE DIM REC$:KEY_TMP$; LET FORMTYPE$="act"
0260 LET REC$=READ_REC$,NEWFILE$="n"
0270 IF HOLD$<>REC.ORDER_NO$ THEN GOSUB SETUP_FILE
0280 IF HEAD$="-S" THEN GOTO MAIN_LOOP
0290 IF HEAD$="-Q" THEN GOTO END_OF_FILE
0300 IF DSBEXIST THEN IF DSB$(LEN(CVS(DSB$,2))-5,6)=REC.CUST$ THEN LET DSBINFO$=DSBINFO$+DSB$+$0A$
0310 LET HOST_ID$=CVS(REC.AUTH_KEY$,2)
0320 LET U$=REC.USERS$,U=NUM(U$,ERR=0330); LET U$=U$+" user"; IF U>1 THEN LET U$=U$+"s"
0330 IF FORMTYPE$="act" THEN GOTO SCALL_PERL
0340 LET FEATURE$=CVS(REC.FLEX_FEATURE$,2)
0350 LET ENC_CODE$=CVS(REC.FLEX_KEY$,2)
0360 LET LIC_REV$=CVS(REC.FLEX_REV_LVL$,2)
0370 LET HOST_ID$=CVS(REC.FLEX_HOST_ID$,2)
0380 LET EXPIRE$=CVS(REC.FLEX_EXPIRY_DATE$,2)
0390 LET CHECKSUM$=CVS(REC.FLEX_CHECKSUM$,2)
0400 LET NUM_USERS$=CVS(REC.FLEX_USERS$,2)
0410 SCALL_PERL: 
0420 LET PERL$="perl emailform.pl "+NEWFILE$+" "+$22$+FORMTYPE$+$22$+" "+$22$+OUTFILE$+$22$+" "+$22$+CO$+$22$+" "+$22$+CNUM$+$22$+" "+$22$+S$+$22$+" "+$22$+T$+$22$+" "
0430 LET PERL$=PERL$+$22$+CVS(REC.PART_NUM$,2)+$22$+" "+$22$+REC.ORDER_DATE$+$22$+" "+$22$+CVS(REC.REV_LVL$,2)+$22$+" "
0440 LET PERL$=PERL$+$22$+CVS(REC.SYSTEM$,2)+$22$+" "+$22$+REC.ORDER_NO$+$22$+" "+$22$+CVS(U$,2)+$22$+" "+$22$+REC.CUST$+$22$+" "+$22$+CVS(REC.NUMBER$,2)+$22$+" "
0450 LET PERL$=PERL$+$22$+CVS(REC.AUTH_KEY$,2)+$22$+" "+$22$+FEATURE$+$22$+" "+$22$+ENC_CODE$+$22$+" "+$22$+LIC_REV$+$22$+" "+$22$+HOST_ID$+$22$+" "
0460 LET PERL$=PERL$+$22$+EXPIRE$+$22$+" "+$22$+CHECKSUM$+$22$+" "+$22$+NUM_USERS$+$22$+" "+$22$+CVS(REC.DESC1$,2)+$22$
0470 CHDIR BASDIR$
0480 LET A=SCALL(PERL$)
0490 CHDIR CURRENTDIR$
0500 IF K_SIZE=10 THEN CALL "BAL.01",CVS(REC.NUMBER$,3),CVS(REC.AUTH_KEY$,3),CNUM$,"","",LICENSE$
0510 GOTO MAIN_LOOP
0520 END_OF_FILE: 
0530 GOSUB SEND_FILE
0540 CLOSE (INFILECHAN)
0550 IF DSBEXIST THEN CLOSE (DSBCHAN)
0560 CLOSE (ARM02CHAN)
0570 ERASE INFILE$,ERR=ERASE_DSB
0580 ERASE_DSB: ERASE DSBFILE$,ERR=END_PROGRAM
0590 GOTO END_PROGRAM
0600 SETUP_FILE: 
0610 IF HOLD$<>"" THEN GOSUB SEND_FILE
0620 LET DSBINFO$=""
0630 LET HOLD$=REC.ORDER_NO$,OUTFILE$="/mas90/"+REC.ORDER_NO$+"email.txt"
0640 ERASE OUTFILE$,ERR=EMAIL_WINDOW
0650 EMAIL_WINDOW: 
0660 LET NEWFILE$="y"
0670 GOSUB SPEEDDIAL
0680 PRINT 'WINDOW'(3,8,72,8,""),'CURSOR'("ON"),
0690 LET HEAD$=FILL(80),T$="",S$=""; IF POS("@"=CNUM$)=0 THEN LET CNUM$=""
0700 PRINT_STUFF: PRINT 'CS',@(2,0),"ORDER # ",HOLD$,@(4,1),"EMAIL ",@(1,3),"To:",@(5,3),CNUM$,@(1,4),"Subj:",@(7,4),S$,
0710 INPUTE 5,3,64,"_",CNUM$
0720 INPUTE 7,4,50,"_",S$; LET S$=S$+FILL(40),HEAD$(41,40)=S$(1,40),S$=CVS(S$,2)
0730 PRINT @(2,5),"OK? CR/N/S(skip this)/Q(quit emailing)",; INPUT @(40,5),I$,; LET I$=CVS(I$,4); IF I$="N" THEN GOTO PRINT_STUFF
0740 PRINT 'POP',
0750 IF POS(I$="SQ") THEN LET HEAD$="-"+I$; GOTO SETUP_RETURN
0760 IF CVS(CNUM$,3)="" OR POS("@"=CNUM$)=0 THEN GOTO EMAIL_WINDOW
0770 SETUP_RETURN: RETURN 
0780 SEND_FILE: 
0790 IF DSBEXIST=0 THEN GOTO SCALL_SENDMAIL
0800 READ (ARM02CHAN,KEY=F0$(16,2)+REC.CUST$+"  ")*,C$
0810 IF C$(34,3)<>"DSI" AND F0$(16,2)="01" THEN GOTO SCALL_SENDMAIL
0820 LET EMCHAN=UNT; OPEN (EMCHAN,ERR=SEND_FILE_END)OUTFILE$; LET F$=FIN(EMCHAN),S=DEC(F$(1,4))
0830 LET DSBID$=HOLD$(LEN(HOLD$)-4,5)+"dsb.txt"
0840 READ RECORD(EMCHAN,SIZ=S)EMAIL$
0850 LET AT$=$0A$+"--==========_BASIS"+$0A$+"Content-Disposition: attachment"+$0A$+"Content-Transfer-Encoding: 7bit"+$0A$+"Content-Type: text/plain;"+$0A$+" charset="+$22$+"us-ascii"+$22$+"; name="+$22$+DSBID$+$22$+$0A$+$0A$+DSBINFO$
0860 WRITE RECORD(EMCHAN)AT$; CLOSE (EMCHAN)
0870 SCALL_SENDMAIL: 
0880 IF HEAD$<>"-S" THEN LET A=SCALL("/usr/lib/sendmail -t < "+OUTFILE$)
0890 ERASE OUTFILE$,ERR=SEND_FILE_END
0900 SEND_FILE_END: RETURN 
0910 SPEEDDIAL: LET CNUM$="0"+REC.CUST$
0920 IF F0$(16,2)="02" THEN LET CNUM$="0007688"
0930 LET E1=UNT; OPEN (E1,ERR=END_SPEEDDIAL)"/u/docfiles/cs/SPEEDDIAL.txt"
0940 READ_SPEEDDIAL: 
0950 READ (E1,END=END_SPEEDDIAL)S$
0960 LET S=POS(CNUM$=S$); IF S<>1 THEN GOTO READ_SPEEDDIAL
0970 IF POS("@"=S$) THEN LET CNUM$=S$(9); GOTO END_SPEEDDIAL
0980 GOTO READ_SPEEDDIAL
0990 END_SPEEDDIAL: 
1000 IF E1 THEN CLOSE (E1)
1010 RETURN 
1020 END_PROGRAM: 
1030 RUN "SYS.AA"

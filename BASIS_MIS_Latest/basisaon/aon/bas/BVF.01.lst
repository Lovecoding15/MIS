0010 REM " Program BVF.01 - Vendor Feature Maintenance
0020 SETERR NOT_CALLED
0030 SETESC 9000
0040 LET CALLED_PROGRAM=0
0050 ENTER FIRM$,CUST$
0060 SETERR 9000
0070 LET CALLED_PROGRAM=1
0080 GOTO OPEN_FILES
0090 NOT_CALLED: 
0100 BEGIN 
0110 SETESC 9000
0120 SETERR 9000
0130 LET SYS01T=UNT; OPEN (SYS01T)"SYS-01"
0140 LET TERMINAL$="T"+FID(0)
0150 READ (SYS01T,KEY=TERMINAL$)*,F0$; CLOSE (SYS01T)
0160 LET FIRM$=F0$(16,2)
0170 OPEN_FILES: 
0180 LET FILES=1,F=0; DIM FILES[FILES]
0190 LET ARM01=UNT; OPEN (ARM01)"ARM-01"; LET FILES[F]=ARM01,F=F+1
0200 LET ARM40=UNT; OPEN (ARM40)"ARM-40"; LET FILES[F]=ARM40,F=F+1
0210 DIM ARM01$:"FIRM_ID:C(2),CUSTOMER_NBR:C(6*=10),CUST_NAME:C(30),ADDR_LINE_1:C(24),ADDR_LINE_2:C(24),ADDR_LINE_3:C(24),ZIP_CODE:C(9),PHONE_NUMBER:C(10),PHONE_EXTEN:C(4),RESALE_NBR:C(20),ALT_SEQUENCE:C(10),DATE_OPENED:C(3),AR_SHIP_VIA:C(10),FAX_NUMBER:C(10),ADDR_LINE_4:C(24),ADDR_LINE_5:C(24),RETAIN_CUST:C(1),CONTACT_NAME:C(20),D_B_NUMBER:C(9),SIC_CODE:C(8),COUNTRY:C(24),FOB:C(15),OP_FRT_TERMS:C(2*=10)"
0220 call "templates.pgm::ARM40"
0230 LET SNAME$=""
0240 IF CALLED_PROGRAM THEN FIND (ARM01,KEY=FIRM$+CUST$,DOM=END_PROGRAM); GOTO BUILD_LIST
0250 DIM SLIST$[2,3,5],SMORE$[2,3,2]
0260 LET METHOD$="Customer# Search/Feature Name Search/Add New Feature/",X1=1
0270 METHOD: LET P=POS("/"=METHOD$); IF P=0 THEN GOTO SELECT_SEARCH
0280 LET SLIST$[1,X1,1]=" "+METHOD$(1,P-1),SLIST$[1,X1,2]=STR(X1),X1=X1+1,METHOD$=METHOD$(P+1)
0290 GOTO METHOD
0300 SELECT_SEARCH: 
0310 LET CUST$="000000",FEATURE$=""
0320 LET WIDTH=35,HEIGHT=7,WIN_X=7,WIN_Y=5,TITLE$="Customer Features"
0330 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,SNAME$
0340 CALL "SYC.SA",0,SLIST$[ALL],SMORE$[ALL],SELECTION$,1,4,SNAME$,HEIGHT,WIDTH,FKEY
0350 IF FKEY=4 THEN GOTO END_PROGRAM
0360 LET X=NUM(SELECTION$,ERR=END_PROGRAM)
0370 ON X GOTO END_PROGRAM,CUSTOMER,FEATURE,ADD_RECORD,END_PROGRAM
0380 CUSTOMER: 
0390 LET V0$="M",V1$="CE",V2$="",V3$="000000",V4$="",V0=6,V1=2,V2=7
0400 GOSUB 7000; IF V3=4 THEN GOTO SELECT_SEARCH
0410 LET CUST$=V$; rem IF CUST$=V3$ THEN LET CUST$=""; GOTO BUILD_LIST
0420 rem FIND (ARM01,KEY=FIRM$+CUST$,DOM=CUSTOMER)
0425 FIND (ARM01,KEY=FIRM$+CUST$,DOM=*next)
0430 GOTO BUILD_LIST
0440 FEATURE: 
0450 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="",V0=30,V1=2,V2=7
0460 GOSUB 7000; IF V3=4 THEN GOTO SELECT_SEARCH
0470 LET V$=V$+FILL(30),FEATURE$=V$(1,30)
0480 GOTO BUILD_LIST
0490 ADD_RECORD: 
0500 LET SELECTION$="",FKEY=1,K=0; IF CALLED_PROGRAM=0 THEN LET CUST$=FILL(6,"0")
0510 CALL "SYC.WD",SNAME$
0520 GOSUB EDIT_INFO
0530 BUILD_LIST: 
0540 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
0550 LET LIST$="",COUNT=0,K$=FIRM$+CUST$,K=0
0560 IF CALLED_PROGRAM=0 AND FEATURE$<>"" THEN LET K$=CVS(FEATURE$,3),K=1
0570 READ RECORD(ARM40,KEY=K$,KNUM=K,DOM=MAIN_READ)ARM40$
0580 MAIN_READ: LET K1$=KEY(ARM40,KNUM=K,END=BUILD_LIST_END)
0590 IF POS(K$=K1$)<>1 THEN GOTO BUILD_LIST_END
0600 READ RECORD(ARM40,KNUM=K,KEY=K1$)ARM40$
0610 LET LIST$=LIST$+K1$+$0A$,COUNT=COUNT+1
0620 GOTO MAIN_READ
0630 BUILD_LIST_END: 
0640 GOSUB DISPLAY_LIST
0650 IF CALLED_PROGRAM THEN GOTO END_PROGRAM ELSE GOTO SELECT_SEARCH
0660 DISPLAY_LIST: 
0670 IF CALLED_PROGRAM=0 THEN PRINT @(1,3),'CL'
0680 IF K<>1 AND CALLED_PROGRAM=0 THEN READ RECORD(ARM01,KEY=FIRM$+CUST$,DOM=DISPLAY_SETUP)ARM01$; PRINT @(1,3),"Customer: ",CUST$," ",ARM01.CUST_NAME$
0690 DISPLAY_SETUP: 
0700 LET WIDTH=60,HEIGHT=20,WIN_X=4,WIN_Y=5,TITLE$=""
0710 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0720 LET HEADING$(2)="Cust#  Feature                    Revision  Product Code"
0730 IF CALLED_PROGRAM THEN LET HEADING$(2,5)=FILL(5)
0740 LET FOOTING$(2)="Enter=Edit  F1=Add Feature  F4=End  PgUp  PgDn"
0750 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0760 LET PAGES=INT(COUNT*2/HEIGHT)+2,MAX_ROW=HEIGHT-6
0770 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,2]
0780 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-4),FOOTING$,'ER','SF',
0790 LET PAGE=1,L=1,X1=1,SELECTION$="",XMODE=5
0800 NEXT_FEATURE: LET P=POS($0A$=LIST$)
0810 IF P<6 THEN LET SELECTION$="|EOF"; GOSUB DISPLAY_PAGE; GOTO DISPLAY_LIST_END
0820 LET K$=LIST$(1,P-1),LIST$=LIST$(P+1)
0830 IF L>=MAX_ROW THEN GOSUB DISPLAY_PAGE
0840 READ RECORD(ARM40,KEY=K$)ARM40$; LET C$=ARM40.CUSTOMER_NBR$; IF CALLED_PROGRAM THEN LET C$=FILL(6)
0850 LET LINE$=" "+C$+" "+ARM40.FEATURE$+" "+ARM40.feature_rev$+"  "+ARM40.prod_code$
0860 LET LLIST$[PAGE,X1,1]=LINE$,LLIST$[PAGE,X1,2]=K$,X1=X1+1,L=L+1
0870 GOTO NEXT_FEATURE
0880 DISPLAY_LIST_END: 
0890 CALL "SYC.WD",NAME$
0900 RETURN 
0910 DISPLAY_PAGE: 
0920 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
0930 IF FKEY=0 OR FKEY=1 THEN GOSUB EDIT_INFO; EXITTO BUILD_LIST
0940 IF FKEY=4 THEN EXITTO DISPLAY_LIST_END
0950 IF FKEY=-16 THEN LET PAGE=PAGE+1
0960 LET L=1,X1=1,XMODE=5
0970 RETURN 
0980 EDIT_INFO: 
0990 IF FKEY=1 THEN GOTO NEW_FEATURE
1000 LET K$=SELECTION$
1010 EXTRACT RECORD(ARM40,KEY=K$,KNUM=K,DOM=NEW_FEATURE)ARM40$
1020 LET CUST$=ARM40.CUSTOMER_NBR$; GOTO SETUP_EDIT
1030 NEW_FEATURE: 
1040 LET ARM40.FIRM_ID$=FIRM$,ARM40.CUSTOMER_NBR$=FILL(6),ARM40.FEATURE$=FILL(30),ARM40.prod_code$=fill(3),arm40.feature_rev$=fill(5),ARM40.vendor_string$=fill(3),ARM40.AVAILABLE$=FILL(29),ARM40.num_1=0,ARM40.num_2=0,ARM40.num_3=0
1050 IF CALLED_PROGRAM OR K=0 THEN LET ARM40.CUSTOMER_NBR$=CUST$
1060 SETUP_EDIT: 
1070 CALL "SYC.WD",NAME$
1080 LET K$=ARM40.FIRM_ID$+ARM40.CUSTOMER_NBR$+ARM40.FEATURE$
1090 LET WIDTH=65,HEIGHT=9,WIN_X=6,WIN_Y=10,TITLE$=""
1100 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,ANAME$
1110 PRINT 'CS','SB',@(6,0),"Customer:",@(7,1),"Feature:",@(6,2),"Revision:",@(2,3),"Product Code:",@(1,4),"Vendor String:",
1120 IF FKEY=1 AND CALLED_PROGRAM=0 THEN PRINT 'SF',
1130 LET INX=17; PRINT @(INX,0),ARM40.CUSTOMER_NBR$,
1140 PRINT 'SF',@(INX,1),ARM40.FEATURE$,@(INX,2),ARM40.feature_rev$,@(INX,3),ARM40.prod_code$,@(INX,4),arm40.vendor_string$,
1150 BEGIN_EDIT: 
1160 IF FKEY<>1 OR CALLED_PROGRAM THEN GOTO ENTER_FEATURE
1170 ENTER_CUST: 
1180 LET V0$="M",V1$="CE",V2$=ARM40.CUSTOMER_NBR$,V3$="000000",V0=6,V1=INX,V2=0
1190 LET V4$="Enter Customer Number"
1200 GOSUB 7000; IF V3=4 THEN GOTO EDIT_INFO_END ELSE LET CUST$=V$,ARM40.CUSTOMER_NBR$=CUST$
1210 ENTER_FEATURE: 
1215 arm01.cust_name$="BASIS"
1220 if cust$<>"000000" then READ RECORD(ARM01,KEY=FIRM$+CUST$,DOM=ENTER_CUST)ARM01$
1230 PRINT 'SB',@(INX+7,0),ARM01.CUST_NAME$,'SF'
1240 LET V0$="S",V1$="C",V2$=ARM40.FEATURE$,V3$="",V0=30,V1=INX,V2=1
1250 LET V4$="Enter the feature name"
1260 GOSUB 7000; IF V3=2 THEN GOTO BEGIN_EDIT
1270 IF V3=4 THEN GOTO END_EDIT ELSE LET ARM40.FEATURE$=V$
1280 ENTER_rev: 
1290 LET V0$="S",V1$="C",V2$=ARM40.feature_rev$,V3$="",V0=5,V1=INX,V2=2
1300 LET V4$="Enter the revision level for this feature"
1310 GOSUB 7000; IF V3=2 THEN GOTO ENTER_FEATURE
1320 IF V3=4 THEN GOTO END_EDIT ELSE LET ARM40.feature_rev$=cvs(V$,7)
1330 ENTER_code: 
1340 LET V0$="S",V1$="C",V2$=ARM40.prod_code$,V3$="",V0=3,V1=INX,V2=3
1350 LET V4$="Enter the product code for pricing"
1360 GOSUB 7000; IF V3=2 THEN GOTO ENTER_Rev
1370 IF V3=4 THEN GOTO END_EDIT ELSE LET ARM40.prod_code$=cvs(V$,7)
1372 ENTER_vndrstr: 
1373 LET V0$="S",V1$="C",V2$=ARM40.vendor_string$,V3$="",V0=3,V1=INX,V2=4
1374 LET V4$="Enter the vendor string for licensing"
1375 GOSUB 7000; IF V3=2 THEN GOTO ENTER_code
1376 IF V3=4 THEN GOTO END_EDIT ELSE LET ARM40.vendor_string$=cvs(V$,7)
1380 END_EDIT: 
1390 LET V0$="S",V1$="C^",V2$="Y",V3$="",V4$="Is the Above Correct (Y/N/Delete)? <F4>=Cancel",V0=6,V1=58,V2=6
1400 GOSUB 7000; IF V3=4 THEN GOTO EDIT_INFO_END
1410 PRINT @(0,V2),'CL',
1420 LET OPTION$=V$; IF V$="DELETE" THEN GOTO REMOVE_ARM40
1430 ON POS(V$="YN") GOTO END_EDIT,UPDATE_FILE,BEGIN_EDIT
1440 UPDATE_FILE: 
1450 LET ARM40$=FIELD(ARM40$),K1$=ARM40.FIRM_ID$+ARM40.CUSTOMER_NBR$+ARM40.FEATURE$
1460 IF FKEY=0 THEN GOTO WRITE_ARM40
1470 FIND (ARM40,KEY=K1$,DOM=WRITE_ARM40)
1480 LET V0$="S",V1$="C^",V2$="N",V3$="",V4$="Record already exist. Overwrite it (Y/N)?",V0=1,V1=58,V2=5
1490 GOSUB 7000; IF V$<>"Y" THEN GOTO EDIT_INFO_END
1500 WRITE_ARM40: 
1510 IF OPTION$="Y" THEN WRITE RECORD(ARM40,KEY=K1$)ARM40$
1520 IF FKEY=1 OR K=0 THEN LET FEATURE$="" ELSE LET FEATURE$=ARM40.FEATURE$
1530 REMOVE_ARM40: 
1540 IF K$<>K1$ OR OPTION$="DELETE" THEN REMOVE (ARM40,KEY=K$,DOM=EDIT_INFO_END)
1550 EDIT_INFO_END: 
1560 CALL "SYC.WD",ANAME$
1570 RETURN 
7000 REM 7000 " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7090
7030 IF V3=127 THEN GOTO 7050
7040 RETURN 
7050 REM " --- Escape During Input"
7060 CALL "SYC.ES",ERR=7090,PGM(-2),TCB(8),E$,E2,V3
7070 IF V3<>127 THEN GOTO 7000
7080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7090 REM " --- Error During Input"
7100 ESCAPE
7110 GOTO 7000
9000 REM 9000 " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9060,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9070
9040 IF E1=3 THEN GOTO 9090
9050 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9060 ESCAPE
9070 REM " --- Retry"
9080 RETRY
9090 REM " --- Return"
9100 SETERR 9000
9110 GOTO 9900
9300 REM 9300 " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9340,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9350
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9340 ESCAPE
9350 RETURN 
9800 REM 9800 " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM 9900
9910 END_PROGRAM: 
9920 CALL "SYC.WD",NAME$
9930 CALL "SYC.WD",ANAME$
9940 CALL "SYC.WD",SNAME$
9950 FOR F=0 TO FILES
9960 CLOSE (FILE[F],ERR=*NEXT)
9970 NEXT F
9980 IF CALLED_PROGRAM THEN EXIT ELSE RUN "SYS.AA"
9990 END

0010 REM "E-commerce masterfile maintenance - customer lookup"
0020 REM "Program BEC.10"
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ARM-02",FILES$[3]="SYS-01"
0170 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0175 IF STATUS>0 THEN GOTO 9900
0180 LET ARM01_DEV=CHANNELS[1],ARM02_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLists
0210 ARM01A: IOLIST A0$(1),A1$(1)
0220 ARM02A: IOLIST B0$,B1$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2)
0500 REM " --- Initializations"
0510 DIM J0$(20,"0"),J$(80,"-")
0520 DIM A0$(8),A1$(305),B0$(10),B1$(64),B[10],E[5],Y[4]
0530 LET K0=1,L0=6,A0$(1,2)=N0$
0700 REM " --- Background
0710 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0720 PRINT 'SB',@(2,3),"Customer No:",@(25,3),"User Code:",@(9,4),"Name:",@(0,6),'CE','SF',
0800 REM " --- Clear After Delete"
0820 PRINT @(0,L0),'CE',
0890 GOTO 1000
0900 REM " --- Init File"
0920 READ (ARM01_DEV,KEY=A0$(1,2),DOM=0930)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",P1$="000000"
1050 LET V0$="M",V1$="KCE",V3$=P1$,V0=6,V1=15,V2=3,V4$="<F3>=Customer Lookup"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 9900
1080 IF V3=3 THEN CALL "SYC.LC",V$
1090 IF V$="" THEN GOTO 1000
1100 LET A0$(3)=V$
1110 PRINT @(15,3),A0$(3,6),@(36,3),user_code$,
1200 REM " --- Find Key"
1220 FIND (ARM01_DEV,KEY=A0$(1,8),DOM=1000)IOL=ARM01A
1230 FIND (ARM02_DEV,KEY=A0$(1,8)+"  ",DOM=1240)IOL=ARM02A
1300 REM " --- Display"
1320 GOSUB 5000
1400 rem " user code for muli-use of b-Commerce
1410 LET V2$=""
1420 LET V0$="S",V1$="CE^",V3$="",V0=2,V1=36,V2=3,V4$="Enter User Code (<Enter>=Default)"
1430 GOSUB 7000
1440 IF V3=4 THEN GOTO 9900
1450 if v3=2 then goto 1000
1460 LET user_code$=V$
4000 REM " --- Options"
4070 CALL "BEC.01",N0$,A0$(3,6),user_code$
4075 PRINT @(0,L0),'CE',
4080 GOTO 1000
5000 REM " --- Display"
5020 PRINT @(15,3),A0$(3,6),@(15,4),A1$(1,30),
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

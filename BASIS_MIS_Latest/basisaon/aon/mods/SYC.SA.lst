0010 REM "SYC - Windowed Lightbar Display/Selection Routine"
0020 REM "Program SYC.SA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "10Jun96 JAL: Lightbar should remain on the same line when moving fro
0031:m right to left during PF display (560,2115,9920)"
0040 REM 
0042 REM "ACTION             : Mode of operation (input)"
0043 REM "                      0=No header/footer, no blank line at top"
0044 REM "                      1=No header/footer, 1 blank line at top"
0045 REM "                      2=No header/footer, don't flag selection"
0046 REM "                      3=Same as 5, special PF character handling"
0047 REM "                      5=Header/footer with page count (standard)"
0048 REM "                      6=Same as 5, no lightbar or selection"
0049 REM "                     15=Same as 5, no redisplay on entry"
0050 REM "                     26=Same as 6, allow blank lines"
0051 REM "                     3x=Same as 5 with x header lines/1 footer"
0052 REM "                     4x=Same as 5, use CF (not CS) to clear detail"
0053 REM "LLIST$[page,line,n]: Standard display list (input)"
0054 REM "                     [page,line,1]=Text to display in window"
0055 REM "                     [page,line,2]=Value to return when selected"
0056 REM "                     [page,line,5]=Detail window name"
0057 REM "MORE$[page,line,n] : Additional information (input)"
0058 REM "                     [page,line,0]=Number of lines to display
0059 REM "                     [page,line,n]=Text to display in detail window"
0060 REM "SELECTION$         : User selection (returned)"
0061 REM "PAGE               : Current page number (input)"
0062 REM "MAXROW             : Maximum number of lines to display (input)"
0063 REM "NAME$              : Name of display window (input)"
0064 REM "HEIGHT             : Window height (input)"
0065 REM "WIDTH              : Window width (input)"
0066 REM "FKEY               : Function key value (returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER ACTION,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAXROW,NAME$,HEIGHT,W
0090:IDTH,FKEY
0500 REM " --- Initializations"
0510 DIM DISPLAY$(WIDTH-2)
0550 LET FKEY=0,HEADER=0,ROLLUP=0,NOROLL=1,HIGHLIGHT=1,CS$='CS',CHOICES$=""
0560 LET DISPLAY=1,BLANKS=0,PAGENUMBER=0,EOF=0,MARK=1,PAGES=PAGE,PF=0,PFLINE=0
0570 IF ACTION<>3 THEN GOTO 0600
0580 LET P=POS("|"=SELECTION$),PF$=SELECTION$(1,P-1)
0590 LET SELECTION$=SELECTION$(P+1)
0600 REM " --- Parse any special information out of SELECTION$"
0610 LET P=POS("|"=SELECTION$)
0620 IF P=0 THEN GOTO 0800
0625 IF SELECTION$(P+1,3)="EOF" THEN LET EOF=1
0630 IF P=1 THEN GOTO 0645
0635 LET SELECTION$=SELECTION$(1,P-1)+SELECTION$(P+4)
0640 GOTO 0650
0645 LET SELECTION$=SELECTION$(5)
0650 LET P=POS("|"=SELECTION$)
0655 IF P=0 THEN GOTO 0800
0660 LET PAGES=NUM(SELECTION$(P+1,3))
0665 IF P=1 THEN GOTO 0690
0670 LET SELECTION$=SELECTION$(1,P-1)
0680 GOTO 0800
0690 LET SELECTION$=""
0800 REM " --- Options based on XMODE"
0810 IF ACTION>1 THEN LET NOROLL=0
0815 IF ACTION=3 THEN LET PF=1,BLANKS=1,ACTION=2,CS$='CF'
0820 IF ACTION>40 THEN LET ACTION=ACTION-40,CS$='CF'
0825 IF ACTION>30 THEN LET HEADER=ACTION-30,ACTION=5
0830 IF ACTION>20 THEN LET ACTION=ACTION-20,BLANKS=1
0840 IF ACTION>10 THEN LET ACTION=ACTION-10,DISPLAY=0
0850 IF ACTION=6 THEN LET ACTION=5,HIGHLIGHT=0
0860 IF ACTION=5 THEN LET ACTION=1,PAGENUMBER=1
0870 IF ACTION<>0 THEN GOTO 0900
0880 LET FIRST=0,XMODE=1
0890 GOTO 1000
0900 REM 
0910 IF ACTION=1 THEN LET FIRST=2,XMODE=-1
0920 IF ACTION=2 THEN LET FIRST=1,XMODE=0,MARK=0
1000 REM " --- Initializations for page display"
1010 IF PAGE<1 THEN LET PAGE=1
1020 IF DISPLAY=0 THEN GOTO 1100
1030 IF XMODE<0 THEN GOSUB 6500
1040 IF PF=0 THEN GOTO 1100
1050 DIM DISPLAY$(WIDTH-1)
1060 GOSUB 6500
1100 REM " --- Display each line and store first character"
1110 LET CHARS$=""
1120 FOR X=1 TO MAXROW-1
1125 IF BLANKS THEN LET X$=" "; GOTO 1170
1130 IF FNP$(LLIST$[PAGE,X,1])<>"" THEN GOTO 1160
1140 LET LAST=X
1150 EXITTO 1500
1160 LET X$=FNS$(LLIST$[PAGE,X,1])
1170 LET CHARS$=CHARS$+X$(1,1)
1180 IF DISPLAY THEN PRINT @(0,X+FIRST+HEADER-1),LLIST$[PAGE,X,1],
1190 NEXT X
1200 REM " --- Full window"
1210 LET CHARS$=CHARS$+".",LAST=MAXROW
1500 REM " --- Determine current line for lightbar"
1510 IF XMODE=1 THEN LET LAST=LAST-2
1520 IF PF THEN LET LAST=LAST-1
1530 IF LAST<0 THEN GOTO 9900
1540 LET CURRENT=FIRST,PREVIOUS=CURRENT
1600 REM " --- Position lightbar on any preselection"
1610 IF LEN(SELECTION$)=0 THEN GOTO 2000
1620 FOR X=1 TO LAST
1630 IF LLIST$[PAGE,X,2]<>SELECTION$ THEN GOTO 1650
1640 LET CURRENT=X-XMODE,PREVIOUS=CURRENT
1650 NEXT X
2000 REM " --- Range test current line and reverse any previous highlight"
2010 IF ROLLUP>0 THEN LET CURRENT=LAST
2020 IF CURRENT<FIRST THEN LET CURRENT=LAST
2030 IF CURRENT>LAST THEN LET CURRENT=FIRST
2040 IF PREVIOUS<=FIRST OR PREVIOUS>LAST THEN LET PREVIOUS=FIRST
2050 IF PREVIOUS=CURRENT THEN GOTO 2100
2060 PRINT @(0,PREVIOUS+HEADER),LLIST$[PAGE,PREVIOUS+XMODE,1],
2100 REM " --- Highlight current line and display additional information"
2110 LET DISPLAY$(1)=LLIST$[PAGE,CURRENT+XMODE,1],V2=CURRENT+HEADER,ROLLUP=0
2115 LET PFLINE=CURRENT+XMODE
2120 IF HIGHLIGHT THEN PRINT @(0,V2),'BR',DISPLAY$,'ER',
2130 GOSUB 6000
2140 IF PF=0 THEN GOTO 2200
2150 LET V1=NUM(MORE$[PAGE,1,0],ERR=2200)
2160 LET X=NUM(MORE$[PAGE,2,0],ERR=2200),X$=MORE$[PAGE,3,0]
2170 IF X$<>"" THEN PRINT @(V1,X),'SB','BR',X$,'ER','SF',
2200 REM " --- Accept user input"
2210 LET V0$="S",V1$="Cal",V2$="",V3$="",V4$="",V0=1,V1=0
2220 GOSUB 7000
2230 IF HIGHLIGHT THEN PRINT @(0,V2),DISPLAY$,
2240 LET PREVIOUS=CURRENT
2260 IF V3=1 THEN GOTO 3000
2270 IF V3=2 THEN GOTO 2400
2280 IF V3=3 THEN GOTO 3000
2290 IF V3=4 THEN GOTO 2450
2300 IF V3=5 THEN LET V$="STARTOVER"; GOTO 9900
2310 IF V3=6 THEN LET V$="EXIT"; GOTO 9900
2320 IF V3=-1 THEN GOTO 2500
2330 IF V3=-2 THEN GOTO 2550
2340 IF V3=-3 THEN GOTO 2600
2350 IF V3=-16 THEN GOTO 2650
2360 IF V3=-17 THEN GOTO 2700
2380 IF V3<>0 THEN GOTO 2100
2390 GOTO 3000
2400 REM " --- Up Arrow"
2410 IF HIGHLIGHT=0 THEN GOTO 2440
2415 LET CURRENT=CURRENT-1
2420 IF CURRENT>=FIRST THEN GOTO 2000
2425 IF NOROLL THEN GOTO 2000
2430 IF PAGE<2 THEN GOTO 2000
2435 LET ROLLUP=1
2440 GOTO 2700
2450 REM " --- <F4>"
2460 LET V$="END"
2490 GOTO 9900
2500 REM " --- Right Arrow"
2510 LET V$=""
2520 IF PF=0 THEN LET V3=0
2540 GOTO 4000
2550 REM " --- Left Arrow"
2560 LET V$="PRIOR"
2590 GOTO 9900
2600 REM " --- Down arrow"
2610 IF HIGHLIGHT=0 THEN GOTO 2000
2615 LET CURRENT=CURRENT+1
2620 IF CURRENT<=LAST THEN GOTO 2000
2630 IF NOROLL THEN GOTO 2000
2635 IF EOF THEN IF PAGE=PAGES THEN GOTO 2000
2640 LET CURRENT=LAST
2650 REM " --- Page down"
2655 LET V3=-16
2660 IF EOF THEN IF PAGE=PAGES THEN PRINT 'RB'; GOTO 2000
2670 IF FNP$(LLIST$[PAGE+1,1,1])="" THEN GOTO 4100
2680 LET PAGE=PAGE+1
2690 GOTO 1000
2700 REM " --- Page up"
2710 LET PAGE=PAGE-1,V3=-17
2740 GOTO 1000
3000 REM " --- Process input"
3010 IF V$="" THEN GOTO 4000
3020 LET OLDCL=CURRENT,V$=FNU$(FNP$(V$))
3030 IF PF THEN IF POS(V$=PF$) THEN GOTO 4100
3040 IF CHOICES$="" THEN LET CHOICES$=CHARS$
3100 REM " --- Does entered character match any line?"
3110 LET X=POS(V$=CHOICES$)
3120 IF X=0 THEN GOTO 3200
3130 LET CHOICES$(X,1)="*",CURRENT=X-XMODE
3140 IF CURRENT=OLDCL THEN IF POS(V$=CHOICES$) THEN GOTO 3100
3190 GOTO 2000
3200 REM " --- Entered a non-matching character"
3210 IF CHOICES$=CHARS$ THEN GOTO 2000
3220 LET CHOICES$=CHARS$
3290 GOTO 3100
4000 REM " --- Store selection"
4010 IF HIGHLIGHT THEN LET V$=LLIST$[PAGE,CURRENT+XMODE,2]
4100 REM " --- Next page"
4110 IF MARK THEN PRINT @(0,CURRENT+HEADER),"*",
4190 GOTO 9900
6000 REM " --- Display additional information in the detail window"
6010 IF FNP$(MORE$[PAGE,CURRENT+XMODE,0])="" THEN GOTO 6390
6020 LET LINES=NUM(MORE$[PAGE,CURRENT+XMODE,0],ERR=6390)
6030 LET DETAIL$=LLIST$[PAGE,CURRENT+XMODE,5]
6040 IF DETAIL$="" THEN GOTO 6390
6050 CALL "SYC.WB",DETAIL$,0
6100 REM " --- Replace {} with SB/SF and display detail line"
6110 PRINT CS$,
6120 FOR X=1 TO LINES
6130 LET X$=MORE$[PAGE,CURRENT+XMODE,X]
6140 IF X$="" THEN GOTO 6290
6150 WHILE POS("{"=X$)>0
6160 LET P=POS("{"=X$),X$=X$(1,P-1)+'SB'+" "+X$(P+1)
6170 WEND
6180 WHILE POS("}"=X$)>0
6190 LET P=POS("}"=X$),X$=X$(1,P-1)+'SF'+" "+X$(P+1)
6200 WEND
6210 PRINT @(0,X-1),'SF',X$,'SF',
6290 NEXT X
6300 REM " --- Switch back to main window"
6310 CALL "SYC.WB",NAME$,0
6390 RETURN
6500 REM " --- Clear main window"
6510 FOR X=FIRST+HEADER TO MAXROW+HEADER
6520 PRINT @(0,X),'CL',
6530 NEXT X
6540 REM IF PAGENUMBER THEN PRINT @(WIDTH-7,HEIGHT-3),'SB','BR',"Pg",MAX(PAGE,
6540:1)," ",'ER','SF',
6541 IF PAGENUMBER THEN PRINT @(WIDTH-7,HEIGHT-3),'SB','BR',"Pg",PAGE," ",'ER'
6541:,'SF',
6590 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNS$(Q$)=CVS(Q$,1)
8090 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 REM " --- Return to caller"
9910 LET SELECTION$=V$,FKEY=V3
9920 IF PF THEN LET SELECTION$=SELECTION$+STR(PFLINE)
9950 EXIT
9999 END
0010 REM "CSM - License Type Maintenance (Description)"
0020 REM "Program CSM.EB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "MAS * DCF * WA# 204 * BASIS"
0040 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 PRECISION P[1]
0500 REM " --- Initializations"
1300 REM " --- Display record"
1310 GOSUB 5000
1320 LET HOLD$=""
1390 ON O0 GOTO 4000,4000,4000,2000
2000 REM " --- Description"
2020 LET V0$="S",V1$="CE",V2$=csm05.description$,V3$="",V0=30,V1=22,V2=4
2030 LET V4$="Enter the Description"
2040 GOSUB 7000;v$=v$+fill(30)
2080 LET csm05.description$=V$(1,30)
2090 GOTO 3900
2100 REM " --- OK to use"
2110 IF csm05.active_flag$=" " THEN LET csm05.active_flag$="Y"
2120 LET V0$="S",V1$="CE^",V2$=csm05.active_flag$,V0=1,V1=22,V2=5
2130 LET V4$="Can this be used to configure new items? (Y/N)"
2140 GOSUB 7000; IF POS(V$="YN")=0 THEN GOTO 2100
2180 LET csm05.active_flag$=V$; IF V$="N" THEN LET csm05.online_flag$="N"; PRINT @(22,6),csm05.online_flag$
2190 GOTO 3900
2200 REM " --- OK to sell online"
2210 LET HOLD$=csm05.online_flag$; IF csm05.online_flag$=" " THEN LET csm05.online_flag$="Y"
2220 LET V0$="S",V1$="CE^",V2$=csm05.online_flag$,V3$="",V0=1,V1=40,V2=5
2230 LET V4$="OK to use this in the e_commerce web site? (Y/N)"
2240 GOSUB 7000; IF POS(V$="YN")=0 THEN GOTO 2200
2250 LET csm05.online_flag$=V$
2290 GOTO 3900
2300 REM " --- Expires YN"
2310 IF csm05.expires$=" " THEN LET csm05.expires$="N"
2320 LET V0$="S",V1$="CE^",V2$=csm05.expires$,V3$="",V0=1,V1=22,V2=6
2330 LET V4$="Does this license type expire? (Y/N)"
2340 GOSUB 7000; IF POS(V$="YN")=0 THEN GOTO 2300
2350 LET csm05.expires$=V$
2390 GOTO 3900
2400 REM " --- Expire days"
2410 IF csm05.expires$<>"Y" THEN LET csm05.expire_days=0;print @(v1,v2),fill(4);goto 3900
2420 LET V0$="N",V1$="C",v3$="####",V2$=str(csm05.expire_days:V3$),V0=4,V1=40,V2=6

2430 LET V4$="Standard # days to expiration"
2440 GOSUB 7000
2450 LET csm05.expire_days=V
2490 GOTO 3900
2500 REM " --- Distribution Code"
2510 V0$="S",V1$="^",V3$="",V2$=csm05.dist_code$,V0=2,V1=65,V2=6
2520 V4$="Enter A Valid Distribution Code (<F3>=Lookup)"
2530 GOSUB 7000
2540 ON V3 GOTO 2560,2560,2580,2550,2580
2550 GOSUB 6000
2560 dim D$(5);FIND(ARM10_DEV,KEY=n0$+"D"+v$,DOM=*next)d$(1)
2570 v$=D$(4,2),csm05.dist_code$=v$
2580 PRINT @(v1,v2),V$
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,2300,2400,2500,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 GOSUB 5000
4020 LET V4$="Is The Above Information Correct (Y/N/DELETE)?"
4030 LET V0$="S",V1$="KC^",V2$="",V3$="",V0=6,V1=FNV(V4$),V2=22
4040 GOSUB 7000
4070 ON V3 GOTO 4080,4000,4000,4000,4900
4080 IF V$="DELETE" THEN GOTO 4500
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 IF HOLD$<>"" AND HOLD$<>csm05.online_flag$ THEN CALL "BIV.01",csm05$(1,5),csm05.online_flag$,13,56; REM Set flags in IVM-01
4130 LET ffl$=FATTR(csm05$,"dealer_str"),ffl=DEC(ffl$(10,2)),csm05.dealer_str$=FILL(ffl)
4150 csm05$=field(csm05$);WRITE record(CSM05_DEV,KEY=csm05$(1,5))CSM05$
4190 GOTO 4900
4500 REM " --- Delete"
4510 DIM CHAN[5]
4520 LET CHAN[1]=SYS01_DEV,CHAN[3]=CSM12_DEV,CHAN[4]=CSM13_DEV
4530 LET CHAN[2]=CSM05_DEV
4540 REM LET CHAN[5]=CSM14_DEV
4550 DIM PARAMS$[5]
4560 LET PARAMS$[0]=N0$,PARAMS$[1]=csm05.license_type$,PARAMS$[2]="E"
4570 LET PARAMS$[3]="Y",PARAMS$[4]="N",PARAMS$[5]="N"
4580 CALL "CSC.DA",CHAN[ALL],PARAMS$[ALL],STATUS
4590 GOTO 4900
4900 REM " --- Run menu overlay"
4990 RUN "CSM.EA"
5000 REM " --- Display"
5010 PRINT @(22,4),csm05.description$
5090 RETURN
6000 REM " --- Code Lookup"
6010 LET NUMBER=1
6015 RECORD$="D",TITLE$="AR Distribution Codes"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6050 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6060 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,V$="SKIP",ROW=V2+2
6080 CALL "SYC.LK",ARM10_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8050 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

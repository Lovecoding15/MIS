0010 REM "OPR - Order Status Report By Customer
0020 REM "Program OPR.QA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ARE-03",FILES$[2]="ARE-13",FILES$[3]="ARM-01"
0140 LET FILES$[4]="ARM-10",FILES$[5]="SYS-01"
0150 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET ARE03_DEV=CHANNELS[1],ARE13_DEV=CHANNELS[2],ARM01_DEV=CHANNELS[3]
0180 LET ARM10_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 ARE03A: IOLIST A0$(1),A[ALL]
0215 ARE13A: IOLIST W0$,W1$(1),W[ALL]
0220 ARM01A: IOLIST B0$,B1$(1)
0230 ARM10A: IOLIST X0$(1)
0240 ARM10E: IOLIST Y0$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0290 IVS01A: IOLIST X$,X$,I2$,X$,X$,I5$,M1$,M2$,M3$
0400 REM " --- Parameters"
0405 DIM P[2]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0490 LET P[0]=NUM(P2$(1,2))
0500 REM " --- Initializations"
0505 LET HEADINGS=3
0510 DIM A0$(101),A[10],W1$(64),W[14],X0$(32),HEADINGS$[HEADINGS]
0520 LET OPTN$="OQBC",P8$=""
0540 LET T0=0,T1=0,T0$="",DONE=0,M8$="(###)-###-####"
0550 LET M0=LEN(M0$),M1=LEN(M1$),L9=59,L=L9+1,H0=132,H=0
0560 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,CLOCK$="",WHEN$=F0$(7,3)
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0750 PRINT 'SB',@(32,3),"Option:",'SF',
0800 REM " --- Option"
0810 LET V0$="S",V1$="^C",V2$=P8$,V3$="",V4$="O=Open Orders, Q=Quotes, B=Backorders, C=Credit Held Orders, <F1>=All Orders",V0=4,V1=40,V2=3,X$="",H3$=""
0820 PRINT @(V1,V2),'CL',
0830 GOSUB 7000
0835 ON V3 GOTO 0840,0840,0800,0800,9900
0840 IF V$="" OR V3=1 THEN LET V$=OPTN$; GOTO 0870
0842 FOR X=1 TO LEN(OPTN$)
0844 IF POS(OPTN$(X,1)=V$)>0 THEN LET X$=X$+OPTN$(X,1)
0846 NEXT X
0848 IF LEN(X$)>0 THEN LET V$=X$ ELSE GOTO 0800
0850 FOR X=1 TO LEN(V$)
0855 IF X=LEN(V$) AND X<>1 THEN LET H3$=H3$+" and " ELSE IF H3$<>"" THEN LET H3$=H3$+", "
0860 IF V$(X,1)="O" THEN LET H3$=H3$+"Open" ELSE IF V$(X,1)="Q" THEN LET H3$=H3$+"Quoted" ELSE IF V$(X,1)="B" THEN LET H3$=H3$+"Backordered" ELSE IF V$(X,1)="C" THEN LET H3$=H3$+"Credit Held" ELSE EXITTO 0800
0865 NEXT X
0870 LET P8$=V$
0875 IF LEN(P8$)<4 THEN LET H3$=H3$+" Orders Only" ELSE LET H3$="All Orders"
0880 IF LEN(H3$)>80-V1-LEN(P8$)-1 THEN LET X$=H3$(1,80-V1-LEN(P8$)-1) ELSE LET X$=H3$
0890 PRINT @(V1,V2),'CL',P8$," ",X$,
0900 REM " --- Correct?"
0905 LET V4$="Is The Above Information Correct"
0920 CALL "SYC.YN",1,V4$,2,V$,V3
0930 IF V3=4 THEN GOTO 9900
0940 ON POS(V$="YN") GOTO 0900,0950,0800
0950 REM " --- File Initializations"
0960 CALL "SYC.GA",7,1,"","",STATUS
0965 IF STATUS>0 THEN GOTO 9900
0967 CALL "sdofc_e.bb","newbook",RESP$,ERR$
0968 CALL "sdofc_e.bb","show",RESP$,ERR$
0970 CALL "SYC.NB","Printing",M0+8,COLUMN
0980 LET HEADINGS$[2]=H3$
0990 READ (ARE03_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Next key"
1110 DIM K$(20)
1120 LET K$(1)=KEY(ARE03_DEV,END=4000)
1130 IF K$(1,2)<>N0$ THEN GOTO 4000
1140 IF K$(18,3)<>"000" THEN GOTO 3900
1200 REM " --- Next header"
1210 READ (ARE03_DEV,END=4000)IOL=ARE03A
1220 IF A0$(21,1)="V" OR A0$(22,1)="I" THEN GOTO 3900
1240 IF POS("O"=P8$)>0 AND A0$(23,1)<>"B" AND A0$(101,1)<>"C" AND A0$(21,1)<>"P" THEN GOTO 1300
1250 IF POS("B"=P8$)>0 AND A0$(23,1)="B" THEN GOTO 1300
1260 IF POS("C"=P8$)>0 AND A0$(101,1)="C" THEN GOTO 1300
1270 IF POS("Q"=P8$)>0 AND A0$(21,1)="P" THEN GOTO 1300
1290 GOTO 3900
1300 REM " --- Got One To Print"
1310 PRINT @(COLUMN,11),FNF$(A0$(5,P[0]),M0$)," ",A0$(11,7),
1400 REM " --- Customer break?"
1410 IF A0$(5,6)<>T0$ THEN GOSUB 6000
1600 REM " --- Retrieve terms"
1610 IF A0$(62,2)=X0$(4,2) THEN GOTO 1700
1620 DIM X0$(32)
1640 FIND (ARM10_DEV,KEY=N0$+"A"+A0$(62,2),DOM=1700)IOL=ARM10A
1660 LET TERMS$=X0$(6,20)
1700 REM " --- Type?"
1710 LET X9$=A0$(22,1),TYPE$="Open Order"
1720 IF X9$<>"O" THEN LET TYPE$=X9$; GOTO 1790
1730 IF A0$(101,1)="C" THEN LET X9$="C",TYPE$="Credit Hold"
1740 IF A0$(101,1)="R" THEN LET X9$="R",TYPE$="Released"
1750 IF A0$(23,1)="B" THEN LET X9$="B",TYPE$="Backorder"
1760 IF A0$(21,1)="P" THEN LET X9$="Q",TYPE$="Quote"
1900 REM " --- Add Up Order Amount"
1905 LET AMNT=0
1907 READ (ARE13_DEV,KEY=NO$+A0$(1,17)+"000",DOM=1910)
1910 LET KB$=KEY(ARE13_DEV,END=2000)
1920 IF LEN(KB$)<17 THEN GOTO 2000
1930 IF KB$(1,17)<>A0$(1,17) THEN GOTO 2000
1940 READ (ARE13_DEV)IOL=ARE13A
1950 LET PRICE=W[1]*W[2]
1960 DIM Y0$(32); FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=1970)IOL=ARM10E
1970 IF Y0$(25,1)="O" THEN IF W1$(44,1)="Y" THEN LET PRICE=W[6] ELSE LET PRICE=W[1]
1980 LET AMNT=AMNT+PRICE
1990 GOTO 1910
2000 REM " --- Print Status Line"
2010 IF L+1>L9 THEN GOSUB 5000
2020 PRINT (7)CUSTOMER$,@(M0+1),NAME$,@(32+M0),A0$(11,7),@(40+M0),FNB$(A0$(77,3)),@(49+M0),FNB$(A0$(27,3)),@(58+M0),A0$(49,10),@(69+M0),A0$(32,10),@(80+M0),A0$(62,2)," ",TERMS$,@(101+M0)," ",X9$,@(H0-M1),AMNT:M1$
2025 CALL "sdofc_e.bb","writerow '"+A0$(11,7)+"\t"+FNB$(A0$(77,3))+"\t"+FNB$(A0$(27,3))+"\t'"+CVS(A0$(49,10),2)+"\t"+CVS(A0$(32,10),2)+"\t"+CVS(TERMS$,2)+"\t"+CVS(TYPE$,2)+"\t"+STR(AMNT),RESP$,ERR$
2027 LET CUST_TOTAL=CUST_TOTAL+AMNT
2030 LET CUSTOMER$="",NAME$=PHONE$,PHONE$=""
2040 LET L=L+1,NUMBER=NUMBER+1,T0=T0+1,T1=T1+AMNT
3900 REM " --- Get Next Order "
3990 GOTO 1000
4000 REM " --- End "
4010 LET DONE=1
4020 GOSUB 6000
4030 IF L+2>L9 THEN GOSUB 5000
4040 PRINT (7)""
4050 PRINT (7)T0," Total Orders ",@(H0-M1-13),"Total Amount",@(H0-M1),T1:M1$
4060 CALL "sdofc_e.bb","leaveopen",RESP$,ERR$
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],3,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)@(32+M0),"Order",@(41+M0),"Order",@(51+M0),"Ship",@(101+M0),"Order"
5050 PRINT (7)"Customer",@(32+M0),"Number",@(42+M0),"Date",@(51+M0),"Date",@(58+M0),"PO Number",@(69+M0),"Ship-Via",@(80+M0),"Terms",@(101+M0),"Type",@(H0-13),"Order Amount"
5060 LET L=L+2
5190 RETURN 
6000 REM " --- Customer break"
6010 IF T0$="" THEN GOTO 6100
6011 CALL "sdofc_e.bb","getrow",NEXT$,ERR$
6012 CALL "sdofc_e.bb","writerow \nTotal\t\t\t\t\t\t\t"+STR(CUST_TOTAL),RESP$,ERR$
6013 CALL "sdofc_e.bb","format row="+STR(NUM(NEXT$)+1)+",bold",RESP$,ERR$
6014 CALL "sdofc_e.bb","format col=8,numberformat="+$22$+"_(* $#,##0.00_);_(* $(#,##0.00);$0.00 ;_(@_)",RESP$,ERR$
6015 CALL "sdofc_e.bb","format autofit",RESP$,ERR$
6020 IF NUMBER>1 THEN GOTO 6100
6025 IF L+1>L9 THEN GOSUB 5000
6030 PRINT (7)@(M0+1),NAME$
6040 LET L=L+1
6100 IF DONE>0 THEN GOTO 6190
6110 DIM B1$(282)
6120 LET T0$=A0$(5,6),B1$(1)="(Not On File)",NUMBER=0
6130 FIND (ARM01_DEV,KEY=A0$(1,2)+A0$(5,6),DOM=6140)IOL=ARM01A
6140 LET CUSTOMER$=FNF$(A0$(5,P[0]),M0$),NAME$=B1$(1,30)
6150 LET PHONE$=FNF$(B1$(112,10),M8$)
6155 LET CUST_TOTAL=0
6160 IF L+2>L9 THEN GOSUB 5000
6170 PRINT (7)""
6171 CALL "sdofc_e.bb","newsheet "+CVS(NAME$,2),RESP$,ERR$
6172 CALL "sdofc_e.bb","writerow "+CVS(NAME$,2),RESP$,ERR$
6173 CALL "sdofc_e.bb","format range=a1,bold,color=blue,center",RESP$,ERR$
6174 CALL "sdofc_e.bb","mergecells range=a1:h1",RESP$,ERR$
6175 CALL "sdofc_e.bb","writerow "+PHONE$+"\n\n",RESP$,ERR$
6176 CALL "sdofc_e.bb","format range=a2,center",RESP$,ERR$
6177 CALL "sdofc_e.bb","mergecells range=a2:h2",RESP$,ERR$
6178 CALL "sdofc_e.bb","writerow Order #\tOrder Date\tShip Date\tPO Number\tShip Via\tTerms\tOrder Type\tOrder Amount\n\n",RESP$,ERR$
6180 LET L=L+1
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "SNR - Serial Number Detail Listing (Input Overlay)"
0020 REM "Program SNR.RA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "MAS * DCF * WA# 208
0040 REM 
0045 REM " ** BASIS Modified to print SAM/Developer Contract Info  
0050 SETERR 9000
0055 SETESC 9000
0060 ON O0 GOTO 0070,0500
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=49
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="SYS-01",FILES$[2]="SNM-01",FILES$[3]="SNM-02"
0120 LET FILES$[4]="SNM-03",FILES$[5]="SNM-04",FILES$[6]="SNT-01"
0125 LET FILES$[7]="SNT-02",FILES$[8]="SNT-03"
0130 LET FILES$[9]="CSM-01",FILES$[10]="CSM-02",FILES$[11]="CSM-03"
0140 LET FILES$[12]="CSM-04",FILES$[13]="CSM-05",FILES$[14]="CSM-06"
0145 LET FILES$[15]="TMM-01",FILES$[16]="ARM-01",FILES$[17]="SMC-01"
0150 CALL "SYC.DA",1,1,17,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SNM01_DEV=CHANNELS[2],SNM02_DEV=CHANNELS[3]
0165 LET SNM03_DEV=CHANNELS[4],SNM04_DEV=CHANNELS[5],SNT01_DEV=CHANNELS[6]
0170 LET SNT02_DEV=CHANNELS[7],SNT03_DEV=CHANNELS[8]
0175 LET CSM01_DEV=CHANNELS[9],CSM02_DEV=CHANNELS[10],CSM03_DEV=CHANNELS[11]
0180 LET CSM04_DEV=CHANNELS[12],CSM05_DEV=CHANNELS[13],CSM06_DEV=CHANNELS[14]
0190 LET TMM01_DEV=CHANNELS[15],ARM01_DEV=CHANNELS[16],SMC01_DEV=CHANNELS[17]
0200 REM " --- IOLIST's"
0210 SNM01A: IOLIST A0$,A1$,A2$,A3$,A4$,A[ALL]
0220 SNM02A: IOLIST B0$,B1$,B2$,B[ALL]
0230 SNM03A: IOLIST C0$,C1$,C2$,C3$,C[ALL]
0240 SNT01A: IOLIST W0$,W1$,W2$,W[ALL]
0250 SNT03A: IOLIST Y0$,Y1$,Y2$,Y[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0275 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$
0280 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$
0285 GLS01A: IOLIST G0$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0290 IVS10D: IOLIST I5$,I6$(1),I7$(1)
0295 IVS10N: IOLIST Y0$,Y[ALL]
0400 REM " --- Parameters"
0405 DIM P[8],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="IV",LF$="N",LS$="N",MF$="N",MAXROW=15
0420 LET AR$="N",AP$="N",BM$="N",GL$="N",OP$="N",PO$="N",WO$="N",DI$="N"
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 LET G[0]=NUM(G2$(1,2)),G[3]=NUM(G2$(7,2)),G[4]=NUM(G2$(9,2))
0435 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0437 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1)),P[2]=NUM(I2$(7,2))
0440 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0443 LET P[6]=NUM(I4$(1,2)),P[7]=FNYY_YEAR(I4$(3,2)),P[8]=NUM(I2$(3,2))
0445 IF POS("Y"=I2$(51,3))>0 THEN LET MF$="Y"
0450 IF POS(I3$(3,1)="LF")>0 THEN LET LF$="Y"
0455 IF POS(I3$(17,1)="LS")>0 THEN LET LS$="Y"
0460 GOSUB 6000
0465 IF AR$="N" THEN GOTO 0480
0470 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0475 LET CUSTLEN=NUM(P2$(1,2)),CUSTMASK$=M0$,DI$=P3$(11,1)
0480 IF GL$="N" THEN LET DI$="N"
0500 REM " --- Initializations"
0505 DIM A[2],B[1],C[1],W[5],Y[1],HEADINGS$[4]
0510 DIM DATEP[8],DATEW[8]
0540 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),M4=LEN(M4$),M5=LEN(M5$)
0545 LET G5=LEN(G5$),MONTHS$=G3$(235,G[0]*3),M6$="###0.00-",ITEM$=""
0550 LET NF$="(Not On File)",M9$="##0-"
0555 LET DATEP[1]=29,DATEP[2]=32,DATEP[3]=35,DATEP[4]=38,H1$=F5$
0560 LET DATEP[5]=41,DATEP[6]=50,DATEP[7]=44,DATEP[8]=47
0565 LET DATEW[1]=28,DATEW[2]=31,DATEW[3]=34,DATEW[4]=37
0570 LET DATEW[5]=43,DATEW[6]=46,DATEW[7]=40,DATEW[8]=49
0575 IF O0$="" THEN LET O0$="SYS.AA"
0580 ON O0 GOTO 0600,0900
0600 REM " --- Other file opens"
0680 REM " --- Input initializations"
0685 DIM PARAMS$[3],OPTIONS$[7]
0695 LET PARAMS$[0]=N0$,PARAMS$[1]=I1$,PARAMS$[2]=I2$,PARAMS$[3]=I3$
0700 REM " --- Background"
0710 PRINT 'SB',@(22,3),"Include Comments:",'SF',
0720 CALL "SYC.WC",1,0,80,0,0,4,0
0770 REM CALL "IVC.IB","BL","DW",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0780 REM PRINT @(40,3),SELECTION$,
0790 REM CALL "IVC.IB","DL","DW",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0800 REM " --- Sequence"
0805 LET V4$="Include Comments For Each Serial Number (Y/N)?"
0810 LET V0$="Y",V1$="CE",V2$=INCLUDE$,V3$="",V0=1,V1=40,V2=3
0815 GOSUB 7000
0820 ON V3 GOTO 0825,0800,0800,0800,0865
0825 LET INCLUDE$=V$
0850 REM " --- Process input"
0855 REM CALL "IVC.IB","IL","DW",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0860 REM ON V3 GOTO 0865,0850,0800,0850,0865
0865 PRINT @(40,3),INCLUDE$,
0870 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
0875 ON V3 GOTO 0880,0870,0870,0870,9900
0880 ON POS(V$="YN") GOTO 0870,0885,0800
0885 REM LET BEGPROD$=OPTIONS$[3],ENDPROD$=OPTIONS$[4],BEGITEM$=OPTIONS$[5]
0890 REM LET ENDITEM$=OPTIONS$[6],TYPE$=OPTIONS$[7]
0900 REM " --- Report Initializations"
0905 PRECISION P[1]
0910 DIM J0$(20,"0"),J$(80,"-"),J1$(80," "),DP[3]
0915 DIM A0$(20),A1$(60),A2$(24),A3$(34),A4$(60),A[2],Y[1]
0920 LET WIDTH=80,PAGE=0,L9=59,L=L9+1,HEADINGS=2,PRODUCT$="",CLASS$=""
0925 LET HEADINGS$[0]=F4$,HEADINGS$[1]=H1$
0930 LET WHEN$=F0$(7,3),CLOCK$="",O=WIDTH-MAX(M2,MAX(M3,M4))-3
0935 CALL "SYC.GA",7,1,"","",STATUS
0940 IF STATUS THEN GOTO 4000
0945 CALL "SYC.NB","Printing",P[0],COLUMN
0950 REM " --- Position files"
0970 LET K$=BEGSER$
0980 READ (SNM01_DEV,KEY=K$,DOM=1000)
0990 GOTO 1030
1000 REM " --- Next serial number"
1010 LET K$=KEY(SNM01_DEV,END=4000)
1030 IF ENDSER$<>"" THEN IF K$>ENDSER$ THEN GOTO 4000
1040 PRINT @(COLUMN,11),K$(3),
1050 READ (SNM01_DEV,KEY=K$)IOL=SNM01A
1080 LET HEADINGS$[3]=" Serial Nbr: "+A0$+"  Current Users: "+STR(A[0]:"###0"),L=L9+1
1090 LET HEADINGS$[4]="Description: "+A1$
1100 REM " --- IVM-01 Master Info"
1110 LET X$="Serial Number Information"
1120 GOSUB 5100
1125 GOSUB GET_CONTR_INFO
1130 PRINT (7)@(9),"Product: ",@(18),A2$(1,3),@(22),PROD_DESC$,@(61),"Active?: ",@(70),A3$(1,1)
1140 PRINT (7)@(8),"Platform: ",@(18),A2$(4,3),@(22),PLAT_DESC$,@(52),"Orig. Sales Date: ",@(70),FNB$(A3$(2,3))
1150 PRINT (7)@(8),"OS Level: ",@(18),A2$(7,3),@(22),OSLVL_DESC$,@(61),"Port ID: ",@(70),A3$(11,5)
1160 PRINT (7)@(0),"Product Revision: ",@(18),A2$(10,3),@(22),PRREV_DESC$
1170 PRINT (7)@(4),"License Type: ",@(18),A2$(13,3),@(22),LTYPE_DESC$,@(52),TYPE$," Contract Nbr: ",@(70),CONTRACT$
1180 PRINT (7)@(6),"Media Type: ",@(18),A2$(16,3),@(22),MEDIA_DESC$,@(52),"Next Annual Date: ",ANNUAL$
1184 PRINT (7)@(53),"Expiration Date: ",EXPIRE$
1190 PRINT (7)@(1),"Purchasing Customer: ",@(22),FNF$(A3$(5,CUSTLEN),CUSTMASK$)
1195 DIM X1$(325)
1196 FIND (ARM01_DEV,KEY=A3$(16,2)+A3$(5,6),DOM=1197)*,X1$
1200 LET WIDTH_IN=24,LINES_IN=3,ZIP_LEN=9,WIDTH_OUT=24,ADDRESS$=X1$(31,WIDTH_IN*LINES_IN+ZIP_LEN)
1210 CALL "SYC.AA",ADDRESS$,WIDTH_IN,LINES_IN,ZIP_LEN,WIDTH_OUT
1220 PRINT (7)@(5),X1$(1,30),'LF',@(5),ADDRESS$(1,WIDTH_OUT),'LF',@(5),ADDRESS$(1+WIDTH_OUT,WIDTH_OUT),'LF',@(5),ADDRESS$(1+WIDTH_OUT*2,WIDTH_OUT)+" ",FNP$(ADDRESS$(1+WIDTH_OUT*3))
1300 PRINT (7)'LF',@(2),"Comment: ",@(11),A4$
1310 LET L=L+16
1400 REM " --- Serial Number Detail
1410 LET X$="Serial Number Detail"
1420 GOSUB 5100
1430 PRINT (7)@(1),"Invoice",@(11),"Date",@(17),"Type of Sale",@(30),"Prod Rev",@(41),"Users: ",@(48),"Price",@(60),"Lic Count",'LF'
1440 LET L=L+2
1450 READ (SNT01_DEV,KEY=A0$,DOM=1451)
1460 LET K$=KEY(SNT01_DEV,END=1600)
1470 IF POS(A0$=K$)<>1 THEN GOTO 1600
1490 READ (SNT01_DEV)IOL=SNT01A
1500 IF L>L9 THEN GOSUB 5100
1510 PRINT (7)@(1),W1$(7,7),@(9),FNB$(W0$(21,3)),@(22),W1$(17,3),@(33),W1$(20,3),@(42),STR(W[0]:"###0"),@(48),W[1]:M1$,@(61),W[2]:"#0"
1520 PRINT (7)@(17),"Authorization: ",W1$(22,15),@(48),"Server #: ",W1$(37,20)
1530 LET L=L+2
1540 GOTO 1460
1600 REM " --- Deactivated/Activated Numbers"
1610 LET X$="Deactivated/Activated Numbers"
1620 GOSUB 5100
1630 PRINT (7)@(4),"Serial Number",@(30),"Action",@(46),"Date",@(57),"Invoice",'LF'
1640 LET L=L+2
1650 READ (SNT03_DEV,KEY=A0$,DOM=1651)
1660 LET K$=KEY(SNT03_DEV,END=1800)
1670 IF POS(A0$=K$)<>1 THEN GOTO 1800
1690 READ (SNT03_DEV)IOL=SNT03A
1700 IF L>L9 THEN GOSUB 5100
1705 IF Y1$(31,1)="A" THEN LET ACTION$="Activated"
1710 IF Y1$(31,1)="D" THEN LET ACTION$="Deactivated"
1720 PRINT (7)@(4),Y1$(1,20),@(30),ACTION$,@(44),FNB$(Y1$(21,3)),@(57),Y1$(24,7)
1730 LET L=L+2
1740 GOTO 1660
1800 REM " --- Registration Information"
1810 LET X$="Registration Information"
1815 GOSUB 5100
1820 FIND (SNM02_DEV,KEY=A0$,DOM=2000)IOL=SNM02A
1824 DIM TMM01$(400)
1825 FIND (TMM01_DEV,KEY=B1$(1,22),DOM=1826)TMM01$
1830 PRINT (7)@(1),"Registered Contact: ",@(21),B1$(3,20)
1840 PRINT (7)@(4),TMM01$(112,30)
1850 PRINT (7)@(4),TMM01$(142,30)
1860 PRINT (7)@(4),TMM01$(172,30)
1870 PRINT (7)@(4),CVS(TMM01$(202,25),2),", ",TMM01$(227,2)
1880 PRINT (7)@(4),TMM01$(229,9); REM FORMAT ZIP?
1900 PRINT (7)@(2),"Contact: ",@(11),TMM01$(52,30),@(46),"Registration Date: ",@(65),FNB$(B1$(23,3))
1910 PRINT (7)@(4),"Phone: ",@(11),TMM01$(38,10),@(30),"Ext: ",@(35),TM_EXT$,@(46),"Domestic or Int'l: ",@(65),B1$(26,1); REM FORMAT PHONE?
1920 PRINT (7)@(6),"Fax: ",@(11),TMM01$(379,10),@(45),"Registered Contact: ",@(65),B1$(27,1); REM FORMAT FAX?
1930 PRINT (7)'LF'
1940 LET L=L+10
2000 REM " --- Marketing Profile
2010 LET X$="Marketing Profile"
2015 GOSUB 5100
2020 FIND (SNM03_DEV,KEY=A0$,DOM=2200)IOL=SNM03A
2030 PRINT (7)@(7),"Computer Make: ",@(22),C1$(1,20)
2040 PRINT (7)@(6),"Computer Model: ",@(22),C1$(21,20)
2050 PRINT (7)@(4),"Operating System: ",@(22),C1$(41,20)
2060 PRINT (7)@(8),"Install Date: ",@(22),FNB$(C2$(1,3))
2070 PRINT (7)@(5),"Purchasing Cust: ",@(22),FNF$(C2$(4,CUSTLEN),CUSTMASK$)
2080 PRINT (7)@(1),"BBX Sold w/Package?: ",@(22),C2$(10,1)
2090 PRINT (7)@(8),"Package Name: ",@(22),C2$(11,20)
2100 PRINT (7)@(11),"Lead Code: ",@(22),C2$(31,1)
2110 LET L=L+8
2700 REM " --- Position comments file"
2720 IF INCLUDE$="N" THEN GOTO 2900
2730 DIM C0$(22),C1$(48)
2740 LET X$="Comments"
2750 GOSUB 5100
2760 LET FIRST$=A0$,X$=""
2790 READ (SNM04_DEV,KEY=FIRST$,DOM=2800)
2800 REM " --- Next comment record"
2810 LET K$=KEY(SNM04_DEV,END=2900)
2820 IF POS(FIRST$=K$)<>1 THEN GOTO 2900
2830 READ (SNM04_DEV)C0$,C1$
2840 IF L+1<L9 THEN GOTO 2870
2845 GOSUB 5000
2850 LET X$="Comments"
2855 GOSUB 5100
2870 PRINT (7)C0$(23)," ",C1$
2880 LET L=L+1
2890 GOTO 2800
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 ON O0 GOTO 9900,4020
4020 CLOSE (7,ERR=4030)
4030 READ (SNM01_DEV,KEY=SERIAL$,DOM=4040)IOL=SNM01A
4040 PRINT @(0,L0),'CE',
4090 RUN O0$,ERR=9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+3
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)HEADINGS$[3]
5050 PRINT (7)HEADINGS$[4]
5090 RETURN 
5100 REM " --- Section Heading"
5110 DIM J$(80,"-")
5120 IF X$<>"" THEN LET J$(9,LEN(X$)+2)=" "+X$+" "
5130 IF L+2>L9 THEN GOSUB 5000
5140 PRINT (7)""
5150 PRINT (7)J$
5160 LET L=L+2
5190 RETURN 
6000 REM " --- What other applications are installed?"
6010 CALL "SYC.VA","AR",INFO$[ALL]
6020 LET AR$=INFO$[20]
6030 CALL "SYC.VA","AP",INFO$[ALL]
6040 LET AP$=INFO$[20]
6050 CALL "SYC.VA","BM",INFO$[ALL]
6060 LET BM$=INFO$[20]
6070 CALL "SYC.VA","GL",INFO$[ALL]
6080 LET GL$=INFO$[20]
6090 CALL "SYC.VA","OP",INFO$[ALL]
6100 LET OP$=INFO$[20]
6110 CALL "SYC.VA","PO",INFO$[ALL]
6120 LET PO$=INFO$[20]
6130 CALL "SYC.VA","WO",INFO$[ALL]
6140 LET WO$=INFO$[20]
6150 CALL "SYC.VA","SA",INFO$[ALL]
6160 LET SA$=INFO$[20]
6170 IF GL$<>"Y" THEN GOTO 6190
6180 CALL "SYC.VA",N2$,INFO$[ALL]
6185 LET GL$=INFO$[9]
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8100 REM " --- FNYEAR_YY$ Un-Convert 21st Century Numeric Year to 2-Char Year"
8105 DEF FNYEAR_YY$(Q)=STR(MOD(Q,100):"00")
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
11000 GET_CONTR_INFO: 
11010 LET CONTRACT$=CVS(A3$(18,6),1+2)
11020 call "templates.pgm::smc01"
11030 DIM ANNUAL$(8),EXPIRE$(8),TYPE$(3)
11040 FIND RECORD(SMC01_DEV,KEY=CONTRACT$,DOM=CONTR_DONE)smc01$
11050 LET ANNUAL$(1)=smc01.ANNUAL_DT$(5,2)+"/"+smc01.ANNUAL_DT$(7,2)+"/"+smc01.ANNUAL_DT$(3,2)
11060 LET EXPIRE$(1)=smc01.EXPIRE_ON_DT$(5,2)+"/"+smc01.EXPIRE_ON_DT$(7,2)+"/"+smc01.EXPIRE_ON_DT$(3,2)
11070 LET TYPE$=smc01.CONTR_TYPE$
11090 CONTR_DONE: 
11100 RETURN 

0010 REM "TMC - Called Routine: Create Contact from Customer Master
0020 REM "Program TMC.CA
0030 REM "ADD+ON SOFTWARE VERSION 5.1
0035 REM "12/16/91 by Jerry Koch
0040 REM "Copyright(C)1991 MicroAccounting Systems, Inc. ALL RIGHTS RESERVED
0050 SETERR 9000
0090 ENTER A0$,A1$,A2$,A3$,A4$,CHAN[ALL],V3
0100 REM "OPEN FILES
0130 LET SYS01_DEV=UNT; OPEN (SYS01_DEV,ERR=9900)"SYS-01"
0140 LET X$=STBL("SYS01_DEV",STR(SYS01_DEV))
0250 IOLIST P0$,P1$,P2$
0260 IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$
0270 IOLIST X$,F0$,X$,X$,F4$,F5$
0400 REM "PARAMETERS
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=0270
0420 LET N0$=F0$(16,2),M0$="000000"
0445 FIND (SYS01_DEV,KEY=N0$+"TM"+"00",DOM=9800)IOL=0250
0450 DIM P[2]
0455 LET P[0]=6
0460 FIND (SYS01_DEV,KEY=N0$+"AR"+"00",DOM=0491)IOL=0260
0490 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(3,2)),P[2]=NUM(I3$(4,1))
0495 CLOSE (SYS01_DEV); LET X$=STBL("!CLEAR","SYS01_DEV",ERR=0496)
0500 REM --INIT DATA--
0510 LET A0$(3)="",A1$(1)="",A2$(1)="",A3$(1)="",A4$(1)=""
0520 LET PAGE=1
0530 DIM B1$(80),DESC$[100](60)
0560 LET L0=1,L9=12,X1=1,X0=0,M8$="(###)-###-####"
0570 IF P[2]=9 THEN LET M7$="00000-0000" ELSE LET M7$="0000"
0590 LET STATES$="ALAKASAZARCACCCOCTDEDCFLGAGUHIIDILINIAKSKYLAMEMDMAMIMNMSMOMTNENVNHNJNMNYNCNDOHOKORPAPRRISCSDTNTXUTVTVAVIWAWVWIWY"
0600 REM "SET PARAMETERS
0610 LET WIDTH=47,HEIGHT=16,WIN_X=5,WIN_Y=6
0655 LET TITLE$="Create Contact from Customer",NAME$=CVS(TITLE$(1,8),2),X$=STBL("WINDOW_NAME",NAME$)
0680 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2); LET HEADING$(4)="Date",HEADING$(11)="Comment Information",FOOTING$(1)="                      F4=End"
0700 REM --BACKGROUND--
0720 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0750 PRINT 'SB',@(2,1),"Customer:",@(6,2),"Firm:",@(5,3),"Addr1:",@(5,4),"Addr2:",@(3,5),"(Addr3):",@(6,6),"City:",@(5,7),"State:",@(15),"Zip:",@(6,8),"Tele:",@(27),"Ext:",@(3,9),"Contact:",
0760 IF P0$(16,1)="Y" THEN PRINT @(6,10),"Firm:", ELSE PRINT @(6,10),"Name:",
0780 REM " PRINT @(0,0),'SB','BR',HEADING$,'ER','SF',
0790 PRINT 'SF',
1000 REM "--- Customer
1010 PRINT 'CF',@(1,22),"Enter A Valid Customer Number, <F3>=Look-Up",
1020 LET V0$="M",V1$="C",V2$=CUST$,V3$=M0$,V4$="",V0=P[0],V1=12,V2=1
1030 GOSUB 7000
1040 IF V3=9 THEN GOTO 9900
1050 IF V3<>3 THEN GOTO 1090
1060 LET V0$=M0$,V4=3,V5=4,V6=5,V$=""
1070 CALL "SYC.LC",V$
1080 IF V$="" THEN GOTO 1000
1090 IF V3=4 THEN GOTO 9900
1100 DIM X0$(305)
1110 FIND (CHAN[3],KEY=N0$+V$,DOM=1000)*,X0$(1)
1120 LET CUST$=V$
1130 PRINT @(V1,V2),FNF$(CUST$(1,P[0]),M0$),
1140 DIM X1$(65); FIND (CHAN[4],KEY=N0$+V$+"  ",DOM=1141)*,X1$
1200 REM "--- Display Customer Info
1220 PRINT @(12,2),X0$(1,30),@(12,3),X0$(31,24),@(12,4),X0$(55,24),@(12,5),X0$(79,24),
1250 LET CITY$=X0$(203,22)
1270 LET STATE$=CVS(X0$(225,2),2); IF POS(STATE$=STATES$,2)=0 THEN LET STATE$=""
1310 PRINT @(12,6),CITY$,@(12,7),STATE$,@(20),FNZ$(X0$(103,P[2]))
1320 PRINT @(12,8),FNF$(X0$(112,10),M8$),@(32),X0$(122,4),@(12,9),X0$(228,20),
1330 GOTO 4000
4000 REM "--- Correct
4010 IF P0$(16,1)="Y" THEN LET KEY$=X0$(1,20),V4$="Contact Firm To Create" ELSE LET KEY$=X0$(228,20),V4$="Contact Name To Create"
4020 LET V4$=V4$+", <F2>=Cust, <F4>=Exit"
4030 PRINT @(INT(WIDTH/2)-INT(LEN(V4$)/2),22),V4$,
4040 LET V0$="S",V1$="CR^",V2$=KEY$,V3$="",V4$="",V0=20,V1=12,V2=10
4050 GOSUB 7000
4070 IF V3=4 OR V3=9 THEN GOTO 9900
4080 IF V3=2 THEN GOTO 1000
4090 LET CONTACT$=V$
4100 REM "--- Create Contact
4110 FIND (CHAN[1],KEY=N0$+Cust$+m0$,DOM=4200)A0$,A1$(1),A2$(1),A3$(1),A4$(1)
4120 PRINT @(2,12),"This Is Already A Contact! ",
4130 LET V0$="Y",V1$="R^",V2$="",V3$="",V4$="",V0=1,V1=38,V2=13
4140 GOSUB 7000;goto 9900
4150 PRINT @(0,12),'CL',@(0,13),'CL',
4160 IF V$="N" THEN LET A0$(3,20)="",A1$(1)="",A2$(1)="",A3$(1)="",A4$(1)=""; GOTO 4000
4190 GOTO 4201
4200 LET A0$(360,1)="Y"
4220 LET A0$(3,20)=CONTACT$
4230 LET A0$(23,15)=CVS(X0$(228,15),4)
4240 LET A0$(38,34)=X0$(112,14)+X0$(228,20),A0$(112,30)=X0$(1,30)
4250 LET A0$(142,30)=X0$(31,24)
4260 LET A0$(172,30)=X0$(55,24)
4270 IF STATE$<>"" THEN LET A0$(227,2)=STATE$
4280 LET A0$(202,25)=CITY$,A0$(229,9)=X0$(103,9),A0$(238,15)=X0$(265,15)
4290 LET A0$(253,6)=CUST$,a0$(269,6)=m0$,A0$(296,3)=X1$(1,3)
4295 LET A0$(375,14)=X0$(169,10)+FILL(4),A0$(285,3)=X1$(21,3),A0$(267,2)=X1$(4,2),A0$(260,3)=X1$(34,3),A0$(264,2)=X1$(6,2)
4300 LET A0$(375,14)=FNUSPH$(A0$(375,14)),A0$(38,14)=FNUSPH$(A0$(38,14))
4390 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8005 REM "-- Special Date functions for YYYYMMDD Dates: FNU=Unpack (trims Century), FNP=Pack (add Century), FNB=Display YYYYMMDD as MM/DD/YY
8010 DEF FNA$(Q$,Q2$)=STR((ASC(Q$)-32)*POS(" "<>Q2$(2,1)):"00")
8020 DEF FNB$(Q1$)=Q1$(5,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(7,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(3,2)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(NUM(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8080 DEF FNH(Q$)=ASC(Q$)-32
8090 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8094 DEF FNP$(Q1$)=STR(FNY(Q1$+"  ")*(LEN(Q1$)>2):"####")+Q1$(3*(LEN(Q1$)>2)+(LEN(Q1$)=0))
8096 DEF FNU$(Q1$)=Q1$(5,4)+Q1$(3,2)
8098 DEF FNY(Q$)=(ASC(Q$(1,1))-(65*(Q$(1,1)>="A"))-48*(Q$(1,1)<"A"))*10+NUM(Q$(2,1))+1900+(100*(Q$(1,1)>="A"))
8100 DEF FNUSPH$(TMPPH$)
8110 LET TMP$="",TLEN=LEN(TMPPH$); IF TLEN THEN FOR I=1 TO TLEN; IF POS(TMPPH$(I,1)="0123456789") THEN LET TMP$=TMP$+TMPPH$(I,1) FI; NEXT I
8120 IF LEN(TMP$)=10 AND CVS(A0$(238,15),2)="" THEN LET TMPPH$=TMP$(1,3)+"."+TMP$(4,3)+"."+TMP$(7)
8130 RETURN TMPPH$
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM "END
9905 REM LET NAME$=CGV("WINDOW_NAME",ERR=09100)
9920 LET X$=STBL("!CLEAR","WINDOW_NAME",ERR=9921)
9930 CALL "SYC.WD",NAME$
9998 EXIT 
9999 END

0010 REM "ARM - Customer Maintenance (Ship-To Addresses)"
0020 REM "Program ARM.MF"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists" 
0230 ARM03A: IOLIST C0$,C1$(1)
0500 REM " --- Initializations"
0520 DIM C0$(14),C1$(174)
0540 LET C0$(1)=A0$
0560 DIM SLS$(30),TER$(30),TAX$(30)
0600 IF ST_DEV=0 THEN GOTO OPEN_STATE ELSE LET A$=FID(ST_DEV,ERR=OPEN_STATE)
0610 GOTO CTRY_DEV
0620 OPEN_STATE: LET ST_DEV=UNT; OPEN (ST_DEV)"states.dat"
0630 CTRY_DEV: IF CTRY_DEV=0 THEN GOTO OPEN_CTRY ELSE LET A$=FID(CTRY_DEV,ERR=OPEN_CTRY)
0640 GOTO 0700
0650 OPEN_CTRY: LET CTRY_DEV=UNT; OPEN (CTRY_DEV)"country.dat"
0700 REM " --- Background"
0710 GOSUB 6000
0720 PRINT 'SB',@(15,5),"Ship-To Address Maintenance",
0740 PRINT @(3,6),"Ship-To No:",@(1,7),"Ship-To Name:",@(6,8),"Address:",@(9,11),"City:",@(8,12),"State:",@(3,13),"Zip/Postal:",@(6,14),"Country:"
0790 PRINT 'SF',
0800 REM " --- Options"
0810 IF V$="Restore" THEN LET V$="R"
0820 IF V$="R" THEN GOTO 1200
0900 REM " --- Init File"
0920 READ (ARM03_DEV,KEY=A0$,DOM=0930)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(ARM03_DEV,END=1050)
1030 IF V2$(1,8)<>A0$ THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(9,6)
1050 LET V0$="Z",V1$="KE",V0=6,V1=15,V2=6,V4$="<F3>=Lookup"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 9200
1080 IF V3=3 THEN CALL "SYC.LT","S",C0$(3,6),V$
1090 IF V$="" THEN GOTO 0900
1100 IF NUM(V$)=99 THEN GOTO 1000; REM "Skip Manual Ship-To number
1110 LET C0$(9)=V$
1200 REM " --- Find Key"
1210 LET O9=0
1220 FIND (ARM03_DEV,KEY=C0$,DOM=1400)IOL=ARM03A
1300 REM " --- Display"
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-up"
1510 LET C1$(1)=""
1520 LET C1$(146,3)=B1$(1,3),C1$(149,3)=B1$(21,3),C1$(152,2)=B1$(32,2)
2000 REM " --- Name"
2005 EXTRACT (ARM03_DEV,KEY=C0$,DOM=2006)
2010 LET O9=1
2020 LET V0$="S",V1$="",V2$=C1$(1,30),V3$="",V0=30,V1=15,V2=7,I0=0
2040 GOSUB 7000
2060 LET C1$(1,30)=V$
2090 GOTO 3900
2100 REM " --- Address"
2110 LET X=I0-1
2120 LET V0$="S",V1$="",V2$=C1$(X*30+31,30),V0=30,V1=15,V2=X+8
2130 GOSUB 7000
2150 IF V3=4 OR V3=2 THEN GOTO 3900
2180 LET C1$(X*30+31,30)=V$
2190 GOTO 3900
2300 REM " --- Zip Code"
2320 LET V0$="S",V1$="",V2$=C1$(148,P[2]),V3$="",V0=P[2],V1=15,V2=13
2340 GOSUB 7000
2360 LET C1$(148,9)=V$
2370 PRINT @(V1,V2),FNZ9$(C1$(148,9))
2390 GOTO 3900
2400 REM " --- City"
2420 LET V0$="S",V1$="",V2$=C1$(121,25),V3$="",V0=25,V1=15,V2=11
2440 GOSUB 7000
2460 LET C1$(121,25)=V$
2490 GOTO 3900
2500 REM " --- State"
2510 LET V0$="S",V1$="^",V2$=C1$(146,2),V0=2,V1=15,V2=12,V4$="State  (<F3>=Lookup)"
2520 GOSUB 7000
2525 IF V3<>3 THEN GOTO STATE_VERIFY
2530 LET TITLE$="State Codes",N=2,V$="SKIP",ROW=V2+2
2535 DIM DESCRIPTION$[N],FIELD[N],POSITION[N],LENGTH[N]
2540 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Name",FIELD[0]=1,POSITION[0]=1,LENGTH[0]=2,FIELD[1]=1,POSITION[1]=3,LENGTH[1]=30,COLUMN=79-LENGTH[0]-LENGTH[1]-2-N*4
2545 CALL "BTM.10",ST_DEV,N,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
2550 IF V$="" THEN GOTO 2500
2555 STATE_VERIFY: LET V$=V$+"  ",V$=V$(1,2); IF V$<>"  " THEN FIND (ST_DEV,KEY=V$,DOM=2500)
2560 LET C1$(146,2)=V$; PRINT @(V1,V2),V$
2590 GOTO 3900
2700 REM " --- Country"
2710 LET V0$="S",V1$="C^",V2$=C1$(157,15),V3$="",V4$="Country if not USA  (<F3>=Lookup)",V0=15,V1=15,V2=14
2720 GOSUB 7000
2725 IF V3<>3 THEN GOTO COUNTRY_VERIFY
2730 LET TITLE$="Countries",N=1,V$="",ROW=V2+2
2735 DIM DESCRIPTION$[N],FIELD[N],POSITION[N],LENGTH[N]
2740 LET DESCRIPTION$[0]="Country",DESCRIPTION$[1]="",FIELD[0]=1,POSITION[0]=1,LENGTH[0]=15,FIELD[1]=0,POSITION[1]=0,LENGTH[1]=0,COLUMN=79-LENGTH[0]-LENGTH[1]-2-N*4
2745 CALL "BTM.10",CTRY_DEV,N,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
2750 IF V$="" THEN GOTO 2700
2755 COUNTRY_VERIFY: LET V$=V$+FILL(30),V$=V$(1,30); IF V$<>FILL(30) THEN FIND (CTRY_DEV,KEY=V$,knum=1,DOM=2700)
2770 LET C1$(157,15)=V$; PRINT @(V1,V2),C1$(157,15)
2790 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 IF ABS(I0)>99 THEN GOTO 3990
3960 ON I0 GOTO 2000,2100,2100,2100,2400,2500,2300,2700,3990
4000 REM " --- Options"
4010 LET V0$="S",V1$="KC^",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N/##/Restore/Delete)?",V0=7,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4024 LET I=NUM(V$,ERR=4030)
4026 IF I>0 THEN GOTO 4800
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF O9=0 THEN IF V$="" OR V$="Y" THEN GOTO 1000
4045 IF V$="R" OR V$="RESTORE" THEN GOTO 0700
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 WRITE (ARM03_DEV,KEY=C0$)IOL=ARM03A
4190 GOTO 1000
4200 REM " --- Delete"
4220 REMOVE (ARM03_DEV,KEY=C0$,DOM=4230)
4290 GOTO 1000
4800 REM " --- ##"
4820 LET I0=I-2,O9=1
4830 EXTRACT (ARM03_DEV,KEY=C0$,DOM=4840)
4890 GOTO 3900
5000 REM " --- Display"
5020 PRINT @(15,6),C0$(9,6),@(15,7),C1$(1,30),@(15,8),C1$(31,30),@(15,9),C1$(61,30),@(15,10),C1$(91,30),@(15,11),C1$(121,25),@(15,12),C1$(146,2),@(15,13),FNZ9$(C1$(148,9)),@(15,14),C1$(157,15),
5090 RETURN 
6000 REM " --- Clear Screen"
6020 PRINT @(0,L0),'CE',
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8100 DEF FNZ9$(Q$)
8105 LET Q=NUM(Q$,ERR=8110); IF LEN(CVS(Q$,2))=9 THEN LET Q$=Q$(1,5)+"-"+Q$(6,4)
8110 RETURN Q$
8115 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9212 IF ST_DEV THEN CLOSE (ST_DEV)
9214 IF CTRY_DEV THEN CLOSE (CTRY_DEV)
9220 RUN "ARM.MA"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "OPE - Invoice Entry/Ribbon"
0020 REM "Program OPE.CC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "S$=General Flag String"
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0210 ARE03A: IOLIST A0$(1),A[ALL]
0220 ARM09A: IOLIST D0$(1),D1$(1),D[ALL]
0230 ARM10A: IOLIST TR_CD$(1)
0240 ARM10B: IOLIST TX_CD$(1)
0250 ARM10D: IOLIST DS_CD$(1)
0260 ARM10F: IOLIST SLS_PSN$(1),A[3]
0270 ARM10G: IOLIST MS_CD$(1)
0280 ARM10H: IOLIST TER_CD$(1)
0290 ARM10I: IOLIST SD_CD$(1)
0500 REM " --- Init Data"
0510 IF A0$(21,1)="V" THEN LET A0$(21,1)="S"
0520 DIM D0$(40),D1$(145),D[1]
0700 REM " --- Background"
0720 PRINT @(0,13),'CE',
0740 CALL "OPC.PR",N0$,A0$,ARM09_DEV,2; REM "Display ribbon data
0790 GOSUB 5000
1000 REM " --- Check Last GL Close Date for Cash Customers"
1020 IF S$(1,3)<>"111" OR S$(7,1)<>"0" THEN GOTO 1100
1040 GOSUB 6800
1050 IF STATUS>99 THEN GOTO 2100
1090 GOTO 4000
1100 REM " --- Done: For Invoice Complete If Last GL Close Date Is Okay"
1120 IF I_CMPLT$<>"YES" THEN GOTO 2000
1140 GOSUB 6800
1150 IF STATUS>99 THEN GOTO 2100
1190 GOTO 4100
2000 REM " --- Type"
2005 LET I0=0,V3=0
2090 GOTO 3900
2100 REM " --- Invoice Date"
2120 LET V0$="D",V1$="R",V2$=A0$(24,3),V3$="",V4$="",V0=8,V1=5,V2=11,I0=1
2130 GOSUB 7000
2135 ON V3 GOTO 2150,2100,2180,2100,2180
2150 LET A0$(24,3)=V$
2180 GOSUB 6800
2185 IF STATUS>99 THEN GOTO 2100
2190 GOTO 3900
2200 REM " --- Ship Date"
2220 LET V0$="D",V2$=A0$(27,3),V3$="",V4$="",V0=8,V1=16,V2=11,V1$="R"
2230 GOSUB 7000
2235 ON V3 GOTO 2260,2200,2290,2200,2290
2260 LET A0$(27,3)=V$
2290 GOTO 3900
2300 REM " --- PO Number"
2320 LET V0$="S",V2$=A0$(49,10),V3$="",V4$="",V0=10,V1=30,V2=11
2330 GOSUB 7000
2335 ON V3 GOTO 2360,2300,2390,2300,2390
2360 LET A0$(49,10)=V$
2390 GOTO 3900
2400 REM " --- Sales Discount"
2410 GOTO 3900
2420 LET V0$="S",V1$="KCR",V2$=A0$(64,2),V3$="",V0=2,V1=52,V2=11,V4$="Enter A Valid Sales Discount Code (<F3>=Lookup)"
2430 GOSUB 7000
2435 ON V3 GOTO 2450,2400,2490,2440,2490
2440 GOSUB LOOKUP_SD_CD
2450 LET X$=V$+FILL(2),X$=X$(1,2)
2460 GOSUB DISPLAY_SD_CD
2470 IF BAD_SD_CD THEN GOTO 2400
2480 LET A0$(64,2)=V$
2490 GOTO 3900
2500 REM " --- Terms Code"
2510 GOTO 3900
2520 LET V0$="S",V1$="KCR",V2$=A0$(62,2),V3$="",V0=2,V1=55,V2=11,V4$="Enter A Valid Terms Code (<F3>=Lookup)"
2530 GOSUB 7000
2535 ON V3 GOTO 2550,2500,2590,2540,2590
2540 GOSUB LOOKUP_TR_CD
2550 LET X$=V$+FILL(2),X$=X$(1,2)
2560 GOSUB DISPLAY_TR_CD
2570 IF BAD_TR_CD THEN GOTO 2500
2580 LET A0$(62,2)=V$
2590 GOTO 3900
2600 REM " --- Distribution Code"
2610 GOTO 3900
2620 LET V0$="S",V1$="KCR",V2$=A0$(66,2),V3$="",V0=2,V1=58,V2=11,V4$="Enter A Valid Distribution Code (<F3>=Lookup)"
2630 GOSUB 7000
2635 ON V3 GOTO 2650,2600,2690,2640,2690
2640 GOSUB LOOKUP_DS_CD
2650 LET X$=V$+FILL(2),X$=X$(1,2)
2660 GOSUB DISPLAY_DS_CD
2670 IF BAD_DS_CD THEN GOTO 2500
2680 LET A0$(66,2)=V$
2690 GOTO 3900
2700 REM " --- Ship Via"
2720 LET V0$="S",V1$="",V2$=A0$(32,10),V3$="",V4$="",V0=10,V1=41,V2=11
2725 IF SHIP$="Y" THEN LET V1$="C",V4$="<F3>=Lookup"
2730 GOSUB 7000
2735 ON V3 GOTO 2760,2700,2790,2740,2790
2740 IF SHIP$<>"Y" THEN GOTO 2700
2750 CALL "SHC.LK",V1,V2,"A",0,V$
2760 LET A0$(32,10)=V$
2780 PRINT @(V1,V2),A0$(32,10)
2790 GOTO 3900
2800 REM " --- Salesperson Code"
2810 GOTO 3900
2820 LET V0$="S",V1$="KCR",V2$=A0$(59,3),V3$="",V0=3,V1=61,V2=11,V4$="Enter A Valid Salesperson Code (<F3>=Lookup)"
2830 GOSUB 7000
2835 ON V3 GOTO 2850,2800,2890,2840,2890
2840 GOSUB LOOKUP_SLS_PSN
2850 LET X$=V$+FILL(3),X$=X$(1,3)
2860 GOSUB DISPLAY_SLS_PSN
2870 IF BAD_SLS_PSN THEN GOTO 2800
2880 LET A0$(59,3)=V$
2890 GOTO 3900
3000 REM " --- Tax Code"
3010 GOTO 3900
3020 LET V0$="S",V1$="KC",V2$=A0$(80,2),V3$="",V0=2,V1=72,V2=11,V4$="Enter A Valid Tax Code (<F3>=Lookup)"
3030 GOSUB 7000
3035 ON V3 GOTO 3050,3000,3090,3040,3090
3040 GOSUB LOOKUP_TX_CD
3050 LET X$=V$+FILL(2),X$=X$(1,2)
3060 GOSUB DISPLAY_TX_CD
3070 IF BAD_TX_CD THEN GOTO 3000
3080 LET A0$(80,2)=V$
3090 GOTO 3900
3100 REM " --- Message Code"
3110 GOTO 3900
3120 LET V0$="S",V1$="KC",V2$=A0$(70,2),V3$="",V0=2,V1=65,V2=11,V4$="Enter A Valid Message Code (<F3>=Lookup)"
3130 GOSUB 7000
3135 ON V3 GOTO 3150,3100,3190,3140,3190
3140 CALL "ARC.LM",V$
3150 IF FNP$(V$)="" THEN GOTO 3190
3160 LET X$=V$+FILL(2),X$=X$(1,2)
3170 GOSUB DISPLAY_MS_CD
3175 IF BAD_MS_CD THEN GOTO 3100
3180 LET A0$(70,2)=V$
3190 GOTO 3900
3200 REM " --- Territory Code"
3210 GOTO 3900
3220 LET V0$="S",V1$="KC",V2$=A0$(72,3),V3$="",V0=3,V1=68,V2=11,V4$="Enter A Valid Territory Code (<F3>=Lookup)"
3230 GOSUB 7000
3235 ON V3 GOTO 3250,3200,3290,3240,3290
3240 GOSUB LOOKUP_TER_CD
3250 LET X$=V$+FILL(3),X$=X$(1,3)
3260 GOSUB DISPLAY_TER_CD
3270 IF BAD_TER_CD THEN GOTO 3200
3280 LET A0$(72,3)=V$
3290 GOTO 3900
3300 REM " --- Price Code"
3310 GOTO 3900
3320 LET V0$="S",V1$="KC",V2$=A0$(75,2),V3$="",V4$="Enter List Price Code Or Leave Blank For Current Price",V0=2,V1=75,V2=11
3330 GOSUB 7000
3335 ON V3 GOTO 3360,3300,3390,3300,3390
3360 LET A0$(75,2)=V$
3370 GOSUB DISPLAY_LP_CD
3390 GOTO 3900
3400 REM " --- Job Number"
3410 IF P3$(12,1)<>"Y" THEN GOTO 3490
3420 LET V0$="S",V1$="KC",V0=10,V2$=A0$(86,10),V1=11,V2=9,V4$="<F1>=Enter New Job Number     <F3>=Job Number Lookup"
3430 GOSUB 7000
3435 ON V3 GOTO 3450,3500,3900,3440,3900
3440 GOSUB LOOKUP_JOB_NMBR
3450 IF FNP$(V$)="" THEN GOTO 3900
3455 IF V$="S" THEN LET A0$(86,10)="STOCK"; GOTO 3480
3460 LET V$=V$+FILL(10),V$=V$(1,10)
3470 FIND (ARM09_DEV,KEY=N0$+A0$(5,6)+V$,DOM=3400)IOL=ARM09A
3475 LET A0$(86,10)=V$
3480 PRINT @(11,9),FILL(40),@(11,9),A0$(86,10),@(22,9),D1$(1,27),
3490 GOTO 3900
3500 REM " --- Enter New Job Number"
3510 LET V0$="S",V1$="KC",V0=10,V2$=D0$(9,10),V1=11,V2=9
3520 LET V4$="Enter New Job Number"
3530 GOSUB 7000
3535 ON V3 GOTO 3540,3500,3400,3500,3400
3540 LET V$=V$+FILL(10),V$=V$(1,10)
3550 FIND (ARM09_DEV,KEY=N0$+A0$(5,6)+V$,DOM=3580)
3560 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Job Already Exists (<Enter>=Continue)"
3565 LET V0=1,V1=FNV(V4$),V2=22
3570 GOSUB 7000
3575 GOTO 3500
3580 LET D0$(1)=N0$+A0$(5,6)+V$
3590 PRINT @(V1,V2),D0$(9,10),
3600 REM " --- Job Description"
3620 LET V0$="S",V1$="C",V2$=D1$(1,27),V0=27,V1=22,V2=9,V4$="Enter Job Description (<F2>=Job Number)"
3630 GOSUB 7000
3635 ON V3 GOTO 3650,3600,3500,3600,3500
3650 LET D1$(1,27)=V$
3660 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="Is This Job Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
3665 GOSUB 7000
3670 ON POS(V$="YN") GOTO 3660,3680,3500
3680 WRITE (ARM09_DEV,KEY=D0$(1,18))IOL=ARM09A
3685 LET V$=D0$(9,10)
3690 GOTO 3450
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,3400,2300,2700,2400,2500,2600,2800,3100,3200,3000,3300,4000
4000 REM " --- Correct?"
4005 GOSUB CONFIRM_SALESPERSON
4010 IF BAD_SALESPERSON THEN GOTO 2800
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="Is The Ribbon Information Correct (Y/N/##/F2=Header Edit)?",V0=2,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4035 ON V3 GOTO 4050,4000,4200,4000,4090
4050 LET I0=INT(NUM(V$,ERR=4090))
4060 IF I0>0 THEN LET I0=I0-1
4070 IF ABS(I0)>1000 THEN GOTO 4000
4080 GOTO 3900
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Run Overlay?"
4120 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
4140 GOSUB 6900
4150 RUN "OPE.CD"
4200 REM " --- <F2> Back To Header"
4220 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
4240 LET O0=3
4250 RUN "OPE.CB"
5000 REM " --- Display"
5005 IF A0$(21,1)="P" THEN LET X7$="Q" ELSE LET X7$="S"
5010 PRINT @(1,11),X7$,@(5,11),FNB$(A0$(24,3)),@(16,11),FNB$(A0$(27,3)),@(30),A0$(49,10)," ",A0$(32,10),
5020 IF P3$(12,1)<>"Y" THEN GOTO 5040
5025 PRINT @(11,9),A0$(86,10),
5030 FIND (ARM09_DEV,KEY=N0$+A0$(5,6)+A0$(86,10),DOM=5040)IOL=ARM09A
5035 PRINT @(22,9),D1$(1,27),
5040 LET X$=A0$(64,2); GOSUB DISPLAY_SD_CD
5045 LET X$=A0$(62,2); GOSUB DISPLAY_TR_CD
5050 LET X$=A0$(66,2); GOSUB DISPLAY_DS_CD
5055 LET X$=A0$(59,3); GOSUB DISPLAY_SLS_PSN
5060 LET X$=A0$(70,2); GOSUB DISPLAY_MS_CD
5065 LET X$=A0$(72,3); GOSUB DISPLAY_TER_CD
5070 LET X$=A0$(80,2); GOSUB DISPLAY_TX_CD
5075 GOSUB DISPLAY_LP_CD
5090 RETURN 
5100 REM " --- Display Sales Discount Code"
5105 DISPLAY_SD_CD: 
5110 LET BAD_SD_CD=1
5120 PRINT @(29,13),'SB',"  Sales Discount Code:",'SF','CL',
5125 DIM SD_CD$(30); LET SD_CD$(6)="Not On File"
5130 FIND (ARM10_DEV,KEY=N0$+"I"+X$,DOM=5140)IOL=ARM10I
5135 LET BAD_SD_CD=0
5140 PRINT @(52,11),SD_CD$(4,2),@(52,13),SD_CD$(6,20),
5145 RETURN 
5150 REM " --- Display Terms Code"
5155 DISPLAY_TR_CD: 
5160 LET BAD_TR_CD=1
5170 PRINT @(29,14),'SB',"           Terms Code:",'SF','CL',
5175 DIM TR_CD$(30); LET TR_CD$(6)="Not On File"
5180 FIND (ARM10_DEV,KEY=N0$+"A"+X$,DOM=5190)IOL=ARM10A
5185 LET BAD_TR_CD=0
5190 PRINT @(55,11),TR_CD$(4,2),@(52,14),TR_CD$(6,20),
5195 RETURN 
5200 REM " --- Display Distribution Code"
5205 DISPLAY_DS_CD: 
5210 LET BAD_DS_CD=1
5220 PRINT @(29,15),'SB',"    Distribution Code:",'SF','CL',
5225 DIM DS_CD$(30); LET DS_CD$(6)="Not On File"
5230 FIND (ARM10_DEV,KEY=N0$+"D"+X$,DOM=5240)IOL=ARM10D
5235 LET BAD_DS_CD=0
5240 PRINT @(58,11),DS_CD$(4,2),@(52,15),DS_CD$(6,20),
5245 RETURN 
5250 REM " --- Display Salesperson Code"
5255 DISPLAY_SLS_PSN: 
5260 LET BAD_SLS_PSN=1
5270 PRINT @(29,16),'SB',"     Salesperson Code:",'SF','CL',
5275 DIM SLS_PSN$(31); LET SLS_PSN$(7)="Not On File"
5280 FIND (ARM10_DEV,KEY=N0$+"F"+X$,DOM=5290)IOL=ARM10F
5285 LET BAD_SLS_PSN=0
5290 PRINT @(61,11),SLS_PSN$(4,3),@(52,16),SLS_PSN$(7,20),
5295 RETURN 
5300 REM " --- Display Standard Message Code"
5305 DISPLAY_MS_CD: 
5310 LET BAD_MS_CD=1
5320 PRINT @(29,17),'SB',"Standard Message Code:",'SF','CL',
5325 DIM MS_CD$(30); LET MS_CD$(7)="Not On File"
5330 FIND (ARM10_DEV,KEY=N0$+"G"+X$+"1",DOM=5340)IOL=ARM10G
5335 LET BAD_MS_CD=0
5340 PRINT @(65,11),MS_CD$(4,2),@(52,17),MS_CD$(7,20),
5345 RETURN 
5350 REM " --- Display Territory Code"
5355 DISPLAY_TER_CD: 
5360 LET BAD_TER_CD=1
5365 DIM TER_CD$(30); LET TER_CD$(7)="Not On File"
5370 PRINT @(29,18),'SB',"       Territory Code:",'SF','CL',
5380 FIND (ARM10_DEV,KEY=N0$+"H"+X$,DOM=5390)IOL=ARM10H
5385 LET BAD_TER_CD=0
5390 PRINT @(68,11),TER_CD$(4,3),@(52,18),TER_CD$(7,20),
5395 RETURN 
5400 REM " --- Display Sales Tax Code"
5405 DISPLAY_TX_CD: 
5410 LET BAD_TX_CD=1
5420 PRINT @(29,19),'SB',"       Sales Tax Code:",'SF','CL',
5425 DIM TX_CD$(60); LET TX_CD$(6)="Not On File"
5430 FIND (ARM10_DEV,KEY=N0$+"B"+X$,DOM=5440)IOL=ARM10B
5435 LET BAD_TX_CD=0
5440 PRINT @(72,11),TX_CD$(4,2),@(52,19),TX_CD$(6,20),
5445 RETURN 
5450 REM " --- Display List Price Code"
5455 DISPLAY_LP_CD: 
5470 PRINT @(29,20),'SB',"      List Price Code:",'SF','CL',
5480 IF A0$(75,2)<>"  " THEN LET X$="List Price Code "+A0$(75,2) ELSE LET X$="Current List Price"
5490 PRINT @(75,11),A0$(75,2),@(52,20),X$,
5495 RETURN 
6000 REM " --- Confirm Valid Salesperson"
6005 CONFIRM_SALESPERSON: 
6010 LET BAD_SALESPERSON=0
6020 FIND (ARM10_DEV,KEY=N0$+"F"+A0$(59,3),DOM=6050)IOL=ARM10F
6030 GOTO 6090
6050 DIM MESSAGE$[1]
6060 LET MESSAGE$[0]="No Salesperson Assigned To This Order (<Enter>=Continue)"
6070 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
6080 LET BAD_SALESPERSON=1
6090 RETURN 
6100 REM " --- Lookup Sales Discount Code"
6105 LOOKUP_SD_CD: 
6110 LET NMBR=1
6115 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6120 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6125 LET DESCRIPT$[1]="Description",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6130 CALL "SYC.LK",ARM10_DEV,"I",NMBR,"Sales Discount Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],1,3,V$
6140 RETURN 
6150 REM " --- Lookup Terms Code"
6155 LOOKUP_TR_CD: 
6160 LET NMBR=1
6165 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6170 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6175 LET DESCRIPT$[1]="Description",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6180 CALL "SYC.LK",ARM10_DEV,"A",NMBR,"Terms Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],1,3,V$
6190 RETURN 
6200 REM " --- Lookup Distribution Code"
6205 LOOKUP_DS_CD: 
6210 LET NMBR=1
6215 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6220 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6225 LET DESCRIPT$[1]="Description",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6230 CALL "SYC.LK",ARM10_DEV,"D",NMBR,"Distribution Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],1,3,V$
6240 RETURN 
6250 REM " --- Lookup Salesperson Code"
6255 LOOKUP_SLS_PSN: 
6260 LET NMBR=1
6265 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6270 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3
6275 LET DESCRIPT$[1]="Salesperson",FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
6280 CALL "SYC.LK",ARM10_DEV,"F",NMBR,"Salesperson Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],1,3,V$
6290 RETURN 
6300 REM " --- Lookup Territory Code"
6305 LOOKUP_TER_CD: 
6310 LET NMBR=1
6315 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6320 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3
6325 LET DESCRIPT$[1]="Description",FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
6330 CALL "SYC.LK",ARM10_DEV,"H",NMBR,"Territory Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],1,3,V$
6340 RETURN 
6350 REM " --- Lookup Sales Tax Code"
6355 LOOKUP_TX_CD: 
6360 LET NMBR=1
6365 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6370 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6375 LET DESCRIPT$[1]="Sales Tax",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6380 CALL "SYC.LK",ARM10_DEV,"B",NMBR,"Sales Tax Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],1,3,V$
6390 RETURN 
6400 REM " --- Lookup Job Number"
6405 LOOKUP_JOB_NMBR: 
6410 LET NMBR=1
6415 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6420 LET DESCRIPT$[0]="Job Number",FIELD[0]=1,POSITION[0]=9,LENGTH[0]=10
6425 LET DESCRIPT$[1]="Description",FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6430 CALL "SYC.LK",ARM09_DEV,A0$(5,6),NMBR,"Job Number Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],1,3,V$
6440 RETURN 
6800 REM " --- Check Invoice Date"
6810 LET STATUS=0
6820 IF GL$="Y" THEN CALL "GLC.EA",A0$(24,3),"Y",PERIOD$,YEAR$,STATUS
6890 RETURN 
6900 REM " --- Clear G/L stuff"
6910 CALL "GLC.DA",ERR=6990,STATUS
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8060 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

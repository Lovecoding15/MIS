0010 REM "TMC - Called Routine: Comments Maintenance
0020 REM "Program TMC.MA
0030 REM "ADD+ON SOFTWARE VERSION 5
0031 REM "12/04/91 by Jerry Koch
0032 REM "MAS * JCM * 01/14/96 * Racing Strollers (From Baseline) * WA:205
0040 REM "Copyright(C)1991 MicroAccounting Systems, Inc. ALL RIGHTS RESERVED
0041 REM 
0042 REM "06/05/96 KJS WA: #211 Use CHAN[], <F10> is new hotkey"
0043 REM "06/07/95 KJS WA: #211 <F9> is now quick exit"
0079 REM 
0080 SETERR 9000
0090 ENTER key0$,FLAG$,CUST$,V3,CHAN[ALL]; REM "WA:205
0100 REM "OPEN FILES
0130 LET SYS01_DEV=UNT; OPEN (SYS01_DEV,ERR=9900)"SYS-01"
0140 LET X$=STBL("SYS01_DEV",STR(SYS01_DEV))
0220 IOLIST B0$,B1$(1)
0270 IOLIST X$,F0$,X$,X$,F4$,F5$
0400 REM "PARAMETERS
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=0270
0420 LET N0$=F0$(16,2)
0430 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)*,*,*,P3$; REM "WA:205
0495 CLOSE (SYS01_DEV); LET X$=STBL("!CLEAR","SYS01_DEV",ERR=0496)
0500 REM --INIT DATA--
0520 LET PAGE=1
0530 DIM B1$(80),DESC$[100](60),J0$(20,"0"); REM "WA:205
0560 LET L0=1,L9=12,X1=1,X0=0
0600 REM "SET PARAMETERS
0610 LET WIDTH=80,HEIGHT=17,WIN_X=0,WIN_Y=6
0655 LET TITLE$="Contact Comments Maintenance",TITLE$="COMMENTS",NAME$=TITLE$(1,8),X$=STBL("WINDOW_NAME",NAME$)
0680 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2); LET HEADING$(4)="Date",HEADING$(11)="Who",HEADING$(15)="Comment Information",FOOTING$(1)="                      F4=End"
0700 REM --BACKGROUND--
0720 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0790 PRINT @(0,0),'SB','BR',HEADING$,'ER','SF',
1000 REM "--- Init Read
1020 READ (CHAN[2],KEY=key0$+DATE$,DOM=1021)
1030 PRINT 'CF',
1080 LET L=L0,DT_BRK$=""
1100 REM "--- Main Read
1120 LET K$=KEY(CHAN[2],END=4000)
1130 IF POS(key0$=K$)<>1 THEN GOTO 4000
1140 READ (CHAN[2],KEY=K$)IOL=0220
1190 LET MORE$=""
1200 REM "--- Page Break?
1220 IF L>L9 THEN LET MORE$="Y"; GOTO 4000
1300 REM "--- Date Break
1320 IF B0$(15,8)<>DT_BRK$ THEN LET DT_BRK$=B0$(15,8); PRINT @(1,L),FNB$(STR(99999999-NUM(B0$(15,8)))),@(10),B1$(66,3),; REM "WA:205
1500 REM "--- Display Comment
1520 PRINT @(14,L),B1$(1,65),; REM "WA:205
1580 LET L=L+1
1990 GOTO 1100
2000 REM "--- Edit Comments
2010 LET B0$=key0$+DATE$+"000"
2020 GOSUB 6000
2040 LET MODE=0,LINES=99,L_WIDTH=65,WIDTH=65,HEIGHT=12,XPOS=10,YPOS=8,TITLE$="Contact Notes "+FNB$(STR(99999999-NUM(DATE$))),CNAME$=""
2050 REM CALL "SYCWTA",MODE,DESC$[ALL],LINES,L_WIDTH,WIDTH,HEIGHT,XPOS,YPOS,TITLE$,CNAME$
2060 CALL "ADC.07",MODE,DESC$[ALL],LINES,L_WIDTH,WIDTH,HEIGHT,XPOS,YPOS,TITLE$,CNAME$
2100 REM "--- Write Records
2120 GOSUB 6200
2140 GOSUB 6100
2190 GOTO 1000
4000 REM "--- Correct
4010 LET L=L0,DT_BRK$="",FOOTING$(1)="",LINE$="<F1>=Today <F2>=Restart <F3>=AR Comments <F4>=End"; IF MORE$="Y" THEN LET LINE$="<CR>=More "+LINE$; REM "WA:205
4020 LET V0$="D",V1$="C6",V2$="",V3$="",V0=6,V1=47,V2=13,FOOTING$(39-INT(LEN(LINE$)/2))=LINE$
4030 PRINT @(30,13),"Enter Note Date:",@(0,14),'BR',FOOTING$,'ER',
4040 GOSUB 7000
4045 PRINT @(0,14),'CL',
4050 IF V3=4 OR V3=9 THEN GOTO 9900
4051 IF V3=3 THEN GOTO 5500; REM "WA:205
4055 IF V3=2 THEN LET DATE$=""; GOTO 1000
4060 IF V3=1 THEN LET V$=F0$(18,6)
4070 IF POS(" "<>V$)=0 THEN IF MORE$="Y" THEN PRINT 'CF',; GOTO 1300 ELSE GOTO 4000
4080 LET DATE$=STR(99999999-NUM(FNP$(V$)))
4090 GOTO 2000
5000 REM "--- Page Break
5020 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="<Cr> To Continue Or End, <F9>=Quit",V0=1,V1=41+INT(LEN(V4$)/2),V2=16
5040 GOSUB 7000
5050 IF V$="END" THEN EXITTO 4000
5060 IF V3=9 THEN EXITTO 9900
5070 LET L=L0,DT_BRK$=""
5080 PRINT 'CF',
5090 RETURN 
5500 REM "AR COMMENTS
5510 LET ARM05=UNT; OPEN (ARM05)"ARM-05"
5715 LET X9$=STR(NUM(P3$(24,2))-1); IF LEN(X9$)=1 THEN LET X9$=J0$(1,1)+X9$
5720 LET L2=0
5725 CALL "SYC.WA",0,52,13,10,8,"AR Comments",CNAME$
5730 READ (ARM05,KEY=N0$+CUST$+X9$,DOM=5740)
5740 LET K5$=KEY(ARM05,END=5790)
5750 READ (ARM05)X0$,X1$
5755 IF K5$(9,2)>P3$(26,2) THEN GOTO 5790
5760 IF K5$(1,8)<>N0$+CUST$ THEN GOTO 5790
5765 PRINT @(1,L2),X1$,
5770 LET L2=L2+1
5776 IF K5$(9,2)=P3$(26,2) THEN GOTO 5790
5780 IF L2<10 THEN GOTO 5787
5782 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="<CR> for more Comments, <F4>=End, <F9>=Quit",V0=1,V1=40+INT(LEN(V4$)/2),V2=22
5783 GOSUB 7000
5784 DIM JSPC$(48); FOR I=0 TO 10; PRINT @(1,I),JSPC$; NEXT I; LET L2=0
5785 IF V3=4 OR V3=9 THEN GOTO 5795
5787 GOTO 5740
5790 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="",V0=1,V1=46,V2=12; PRINT @(10,12),"End of Comments.... <CR>",; GOSUB 7000
5795 CLOSE (ARM05)
5797 CALL "SYC.WD",CNAME$
5799 IF V3=9 THEN GOTO 9900 ELSE GOTO 4000
6000 REM "--- Read Records into Array
6005 READ (CHAN[2],KEY=key0$+DATE$,DOM=6006)
6010 DIM DESC$[100](68); LET J=1
6020 LET K1$=KEY(CHAN[2],END=6090)
6040 IF POS(key0$+DATE$=K1$)<>1 THEN GOTO 6090
6060 READ (CHAN[2],KEY=K1$)IOL=0220
6070 LET DESC$[J](1)=B1$(1,68),J=J+1
6080 GOTO 6020
6090 RETURN 
6100 REM "--- Write Array to File Records
6105 IF LINES=0 THEN GOTO 6190
6110 FOR J=1 TO LINES
6130 LET K1$=B0$(1,22)+STR(J:"000"),B1$(1,68)=DESC$[J]; REM "WA:205
6131 rem IF B1$(66,3)="   " THEN LET B1$(66,3)=F0$(4,3); REM "WA:205
6140 WRITE (CHAN[2],KEY=K1$)K1$,B1$
6150 NEXT J
6160 LET FLAG$="1"
6190 RETURN 
6200 REM "--- Remove Existing Records before Writing New
6220 READ (CHAN[2],KEY=key0$+DATE$,DOM=6221)
6240 LET K1$=KEY(CHAN[2],END=6290)
6250 IF POS(key0$+DATE$=K1$)<>1 THEN GOTO 6290
6270 REMOVE (CHAN[2],KEY=K1$)
6280 GOTO 6240
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8005 REM "-- Special Date functions for YYYYMMDD Dates: FNU=Unpack (trims Century), FNP=Pack (add Century), FNB=Display YYYYMMDD as MM/DD/YY
8010 DEF FNA$(Q$,Q2$)=STR((ASC(Q$)-32)*POS(" "<>Q2$(2,1)):"00")
8020 DEF FNB$(Q1$)=Q1$(5,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(7,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(3,2)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(NUM(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8080 DEF FNH(Q$)=ASC(Q$)-32
8090 DEF FNI$(Q)=CHR(Q+32)
8094 DEF FNP$(Q1$)=STR(FNY(Q1$+"  ")*(LEN(Q1$)>2):"####")+Q1$(3*(LEN(Q1$)>2)+(LEN(Q1$)=0))
8096 DEF FNU$(Q1$)=Q1$(5,4)+Q1$(3,2)
8098 DEF FNY(Q$)=(ASC(Q$(1,1))-(65*(Q$(1,1)>="A"))-48*(Q$(1,1)<"A"))*10+NUM(Q$(2,1))+1900+(100*(Q$(1,1)>="A"))
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM "END
9902 CLOSE (SYS01_DEV,ERR=9903)
9905 REM LET NAME$=CGV("WINDOW_NAME",ERR=09100)
9915 CLOSE (PRM01_DEV,ERR=9916); LET X$=STBL("!CLEAR","PRM01_DEV",ERR=9916)
9920 LET X$=STBL("!CLEAR","WINDOW_NAME",ERR=9921)
9930 CALL "SYC.WD",NAME$
9998 EXIT 
9999 END

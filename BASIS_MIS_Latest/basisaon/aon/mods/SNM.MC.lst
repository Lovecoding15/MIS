0010 REM "SNM - Serial Number Maintenance (Detail)"
0020 REM "Program SNM.MC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "MAS * DCF * WA# 208 * BASIS"
0040 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 PRECISION P[1]
0200 REM " --- IOLIST's"
0210 SNT01A: IOLIST W0$,W1$(1),W2$,W[ALL]
0220 SNT02A: IOLIST X0$,X1$
0500 REM " --- Initializations"
0510 DIM W0$(28),W1$(57),W2$(40),W[5]
0520 LET L0=8,L=L0,L9=20,L2=8
0700 REM " --- Background"
0705 PRINT @(0,6),'CE',
0720 PRINT 'SB',@(1,6),"Invoice",@(11),"Date",@(17),"Type of Sale",@(30),"Prod Rev",@(41),"Users",@(48),"Price",@(60),"Lic Count",'SF',
0900 REM " --- Display any existing lines"
0920 GOSUB 5100
0990 GOTO 4000
1000 REM " --- Overflow"
1010 IF L>L9 THEN PRINT @(0,21),'CL',@(0,22),'CL',@(0,L2),'LD','LD',
1020 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1110 LET K$=KEY(SNT01_DEV,END=4000)
1120 IF POS(A0$=K$)<>1 THEN GOTO 4000
1130 READ (SNT01_DEV)IOL=SNT01A
1170 GOSUB 5000
1180 LET I0=3
1190 GOTO 3300
2000 REM " --- Lic Count"
2010 IF F0$(15,1)<"9" THEN GOTO 3200
2015 EXTRACT (SNT01_DEV,KEY=W0$)IOL=SNT01A; LET EXTRACTED=1
2017 GOSUB 5000
2020 LET V0$="N",V1$="CE",V2$=STR(W[2]),V3$="#0",V4$="Enter the License Count",V0=2,V1=61,V2=L
2030 GOSUB 7000
2040 LET W[2]=V
2090 GOTO 3200
3200 REM " --- Sequence control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 2000,3300
3300 REM " --- Line Correct?"
3310 LET V4$="<Enter>=Continue     <F1>=List Clients     <F2>=Change"
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=B0$
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3500,2000,3300,3350
3350 IF V$="" THEN GOTO 3400
3390 GOTO 3300
3400 REM " --- Write"
3410 IF EXTRACTED THEN WRITE (SNT01_DEV,KEY=W0$)IOL=SNT01A
3450 LET L=L+2,EXTRACTED=0
3460 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3500 REM " --- Show server linked clients"
3530 CALL "SYC.WA",0,40,10,25,9,"",NAME$
3600 REM " --- List"
3605 LET Q1=0,LX=1,LX9=8,LX1=1
3610 GOSUB 5400
3620 READ (SNT02_DEV,KEY=W0$(1,20),DOM=3621)
3630 LET K$=KEY(SNT02_DEV,END=3800)
3640 IF POS(W0$(1,20)=K$)<>1 THEN GOTO 3800
3660 READ (SNT02_DEV)IOL=SNT02A
3690 IF LX<=LX9 THEN GOTO 3750
3700 REM " --- Continue"
3710 LET V4$="Press <Enter> To Continue:"
3715 LET V0$="S",V1$="CE",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
3720 GOSUB 7000
3730 IF V3=4 THEN GOTO 5800
3740 PRINT @(0,0),'CE',
3750 PRINT @(1,LX),X1$
3760 LET LX=LX+1,K9$=X0$
3790 GOTO 3630
3800 REM END
3810 LET V4$="<F1>=Restart, <F4>=Done"
3820 LET V0$="S",V1$="CE",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
3830 GOSUB 7000
3840 IF V3=1 THEN GOTO 3600
3980 CALL "SYC.WD",NAME$
3990 GOTO 3300
4000 REM " --- Options"
4005 LET EXTRACTED=0
4010 LET V4$="Is The Above Information Correct (Y/List/Review)?"
4020 LET V0$="S",V1$="C^",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4100
4050 IF V$="L" OR V$="LIST" THEN GOTO 4500
4070 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4090 ON POS(V$="YN") GOTO 4000,4100,4000
4100 REM " --- Return to header"
4110 GOSUB 5400
4190 GOTO 4900
4500 REM " --- List"
4510 LET W0$(21)=""
4520 GOSUB 5100
4590 GOTO 4000
4600 REM " --- Review"
4610 LET W0$(21)=""
4650 REM " --- Restart"
4660 READ (SNT01_DEV,KEY=A0$,DOM=4670)
4670 GOSUB 5400
4690 GOTO 1000
4900 REM " --- Run menu overlay"
4990 RUN "SNM.MA"
5000 REM " --- Display"
5005 CALL "BPR.01",W1$(20,3),RV$; IF RV$="" THEN LET RV$=W1$(20,3)
5010 PRINT @(1,L),W1$(7,7),@(9),FNB4$(W0$(21,3)),@(22),W1$(17,3),@(33),RV$,@(41),STR(W[0]:"-###0"),@(48),W[1]:M1$,@(61),W[2]:"#0"
5020 PRINT @(5),"Item: ",w2$(12,15),"  Auth: ",W1$(23,15),"Server: ",W1$(38,20)
5090 RETURN 
5100 REM " --- List"
5105 LET Q1=0
5110 GOSUB 5400
5120 READ (SNT01_DEV,KEY=A0$,DOM=5130)
5130 LET K$=KEY(SNT01_DEV,END=5300)
5140 IF POS(A0$=K$)<>1 THEN GOTO 5300
5160 READ (SNT01_DEV)IOL=SNT01A
5190 IF L<=L9 THEN GOTO 5250
5200 REM " --- Continue"
5210 LET V4$="Press <Enter> To Continue:"
5215 LET V0$="S",V1$="CE",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5230 IF V3=4 THEN GOTO 5300
5240 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+2,K9$=W0$
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5420 PRINT @(0,L2),'CE',
5440 LET L=L2
5490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END
14500 REM Date with 4 digit year
14505 DEF FNA4$(Q$,Q2$)=STR(((ASC(Q$)-32)+1900)*POS(" "<>Q2$(2,1)):"0000")
14510 DEF FNB4$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA4$(Q1$(1),Q1$)

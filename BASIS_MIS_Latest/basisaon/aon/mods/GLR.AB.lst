0010 REM "GLR - Standard Report Initializations"
0020 REM "Program GLR.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0085 SETESC 9000
0500 REM " --- Initializations"
0510 DIM WORK$[7]
0550 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),H1$=N1$,H2$=N4$
0555 LET L9=59,L=L9+1,H=0,H0=132,WHEN$=F0$(7,3),CLOCK$=""
0560 LET HEADINGS=1,WORK$[0]=H1$,WORK$[1]=H2$,X$=""
0565 LET H0$="",H1$="",H2$="",H3$="",H4$="",H5$=""
0570 IF POS("D"=ACTION$)=0 THEN GOTO 0600
0575 IF SEQUENCE$="D" THEN LET WORK$[1]=WORK$[1]+" By Date"
0580 IF SEQUENCE$="A" THEN LET WORK$[1]=WORK$[1]+" By Audit Number"
0800 REM " --- Run report?"
0810 IF POS("P"=ACTION$)=0 THEN GOTO 0900
0820 LET PROMPT$="Do You Want To Print The "+WORK$[1]
0830 CALL "SYC.YN",1,PROMPT$,2,V$,V3
0840 IF V$<>"Y" THEN GOTO 4900
0900 REM " --- Open Printer"
0910 IF PRINTER$="N" THEN GOTO 1000
0915 LET PRTR$=""; IF pos(PROGRAM$="GLR.DB,GLR.PB,GLE.AB") THEN CALL "BPS.01",3,PRTR$,STATUS; IF STATUS THEN GOTO 9900
0920 CALL "SYC.GA",7,1,"",PRTR$,STATUS
0930 IF STATUS THEN GOTO 9900
1000 REM " --- Set !GLCONTROL For Allocation Entry Register"
1005 IF PROGRAM$<>"GLR.QB" THEN GOTO 1100
1010 CALL "GLC.BA",ERR=9900,SOURCE$,N2$,GLW11$,GL$,STATUS
1020 IF STATUS<>0 THEN GOTO 9900
1060 LET FILES$[1]="GLM-01",FILES$[2]=GLW11$,OPTIONS$[2]="C"
1070 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
1080 IF STATUS>0 THEN GOTO 9900
1090 LET GLM01_DEV=CHANNELS[1],GLW11_DEV=CHANNELS[2]
1100 REM " --- Build standard report headings"
1110 IF ACTION$="" THEN GOTO 3000
1200 REM " --- Account"
1210 IF POS("2"=ACTION$)=0 THEN GOTO 1300
1220 LET X$="From ",Y$=""
1230 IF BEGACCT$="" THEN LET Y$="First",X$=X$+Y$+" Account" ELSE LET Y$=FNF$(BEGACCT$(1,P[3]),M0$),X$=X$+"Account "+Y$
1235 IF POS("D"=ACTION$)>0 THEN LET H0$="Beginning Account: "+Y$
1240 LET X$=X$+" Through ",Y$=""
1250 IF ENDACCT$="" THEN LET Y$="Last",X$=X$+Y$+" Account" ELSE LET Y$=FNF$(ENDACCT$(1,P[3]),M0$),X$=X$+"Account "+Y$
1255 IF POS("D"=ACTION$)>0 THEN LET H1$="   Ending Account: "+Y$
1260 IF WILDCARD$<>"" THEN LET Y$="Wildcard "+$22$+WILDCARD$+$22$,X$=X$+" ("+Y$+")"
1270 IF POS("D"=ACTION$)>0 THEN IF WILDCARD$<>"" THEN LET H3$=Y$
1290 LET HEADINGS=HEADINGS+1,WORK$[HEADINGS]=X$
1300 REM " --- Beginning/Ending Period/Year"
1310 IF POS("1"=ACTION$)=0 THEN GOTO 1400
1320 LET X$="From ",X=NUM(BEGPER$)
1330 IF BEGPER$="" THEN LET X$=X$+"First Period " ELSE LET X$=X$+FNP$(P3$((X-1)*16+27,16))+" (Period "+BEGPER$+") "
1340 IF BEGYEAR$="" THEN LET X$=X$+"First Year" ELSE LET X$=X$+FNYY21_YY$(BEGYEAR$)
1350 LET X$=X$+" Through ",X=NUM(ENDPER$)
1360 IF ENDPER$="" THEN LET X$=X$+"Last Period " ELSE LET X$=X$+FNP$(P3$((X-1)*16+27,16))+" (Period "+ENDPER$+") "
1370 IF ENDYEAR$="" THEN LET X$=X$+"Last Year" ELSE LET X$=X$+FNYY21_YY$(ENDYEAR$)
1390 LET HEADINGS=HEADINGS+1,WORK$[HEADINGS]=X$
1400 REM " --- For Period/Year"
1410 IF POS("7"=ACTION$)=0 THEN GOTO 1500
1420 LET X=NUM(BEGPER$),Y=FNYY_YEAR(BEGYEAR$)
1430 CALL "SYC.PA",SYS01_DEV,X,Y,X$,Y$,STATUS
1440 LET HEADINGS=HEADINGS+1,WORK$[HEADINGS]="For "+FNP$(P3$((X-1)*16+27,16))+" "+FNYY21_YY$(BEGYEAR$)+" (Period "+STR(X)+")"
1490 LET HEADINGS=HEADINGS+1,WORK$[HEADINGS]=FNB$(X$)+" Through "+FNB$(Y$)
1500 REM " --- Detail/Summary"
1510 LET T$=""
1520 IF POS("5"=ACTION$)=0 THEN GOTO 1600
1530 IF DETAIL$="D" THEN LET T$="Detail Report" ELSE LET T$="Summary Report"
1540 IF POS("3"=ACTION$)>0 THEN GOTO 1600
1580 LET HEADINGS=HEADINGS+1,WORK$[HEADINGS]=T$
1590 GOTO 1700
1600 REM " --- Journal"
1610 IF POS("3"=ACTION$)=0 THEN GOTO 1700
1620 LET X$=" For "
1630 IF T$<>"" THEN LET X$=T$+X$
1640 IF JOURNAL$="" THEN LET X$=X$+"All Journals" ELSE LET X$=X$+"Journal "+JOURNAL$+" "+FNP$(C0$(5,20))
1650 IF POS("D"=ACTION$)>0 THEN LET H2$=X$
1690 LET HEADINGS=HEADINGS+1,WORK$[HEADINGS]=FNS$(X$)
1700 REM " --- Audit/Date"
1710 IF POS("4"=ACTION$)=0 THEN GOTO 1800
1715 IF SEQUENCE$<>"A" THEN GOTO 1750
1720 LET X$="By Audit Number From "
1723 IF POS("D"=ACTION$)>0 THEN LET H4$="Beginning Audit Number: ",H5$="   Ending Audit Number: "
1725 IF BEGVAL$="" THEN LET X$=X$+"First Through",H4$=H4$+" First " ELSE LET X$=X$+BEGVAL$,H4$=H4$+BEGVAL$
1730 IF ENDVAL$="" THEN LET X$=X$+" Last",H5$=H5$+" Last" ELSE LET X$=X$+" "+ENDVAL$,H5$=H5$+ENDVAL$
1735 GOTO 1790
1750 LET X$="By Date From "
1755 IF POS("D"=ACTION$)>0 THEN LET H4$="Beginning Date: ",H5$="   Ending Date: "
1760 IF BEGVAL$="" THEN LET X$=X$+"First Through",H4$=H4$+"First" ELSE LET X$=X$+FNB2$(BEGVAL$),H4$=H4$+FNB2$(BEGVAL$)
1765 IF ENDVAL$="" THEN LET X$=X$+" Last",H5$=H5$+"Last" ELSE LET X$=X$+" "+FNB2$(ENDVAL$),H5$=H5$+FNB2$(ENDVAL$)
1790 LET HEADINGS=HEADINGS+1,WORK$[HEADINGS]=X$
1800 REM " --- Budget Heading"
1810 IF POS("H"=ACTION$)=0 THEN GOTO 1900
1820 IF HEADINGS>6 THEN GOTO 1900
1830 LET HEADINGS=HEADINGS+1,WORK$[HEADINGS]=BUDGET$
1900 REM " --- Adjust headings for Daily Detail Register"
1910 IF POS("D"=ACTION$)=0 THEN GOTO 2000
1920 IF HEADINGS<4 THEN GOTO 2000
1930 DIM X$(H0)
1935 LET X=H0-MAX(LEN(H4$),LEN(H5$))+1
1940 LET X$(1)=H0$,X$(FNC(H2$,H0),LEN(H2$))=H2$,X$(X)=H4$
1945 LET WORK$[HEADINGS-2]="",WORK$[HEADINGS-1]=X$,X$(1)=H1$
1950 IF LEN(H3$)>0 THEN LET X$(FNC(H3$,H0),LEN(H3$))=H3$
1960 LET X$(X)=H5$,WORK$[HEADINGS]=X$
3000 REM " --- Xfer WORK$[*] to HEADINGS$[*] for report overlay"
3010 DIM HEADINGS$[HEADINGS]
3020 FOR X=0 TO HEADINGS
3030 LET HEADINGS$[X]=WORK$[X]
3040 NEXT X
4000 REM " --- Run Report Overlay"
4090 RUN PROGRAM$,ERR=4900
4900 REM " --- Run Menu Overlay"
4990 RUN MENU$,ERR=9900
8000 REM " --- Function Definitions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNB2$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8030 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8075 DEF FNS$(Q$)=Q$(" "<>FNP$(Q$))
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

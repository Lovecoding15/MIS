0010 REM "IVM - Item Maintenance (Item Information)"
0020 REM "Program IVM.MB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "11Oct96 JWK: Update IVM-03 C (Whse/Prod), F (Buyer/Vendor), J (Class) and T (Type) x-ref records (330-350,1230,4100-4195,6000-6290,6600-6690)
0032 REM "23Aug96 Caj: Misspelled variable names (4215,4255)
0033 REM "26Aug96 WGH: Do NOT write blank item synonyms (4235-4240,4275-4280)"
0034 REM "04Mar97 WGH: Re-set Inventoried? to N when Lotted or Serialized Item? is not Y (2840)"
0035 REM "21Jan98 JWK: Wasn't writing UPC Xref Records (4180)
0036 REM "MAS * DCF * 8/19/99 * WA# 205"
0040 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 PRECISION P[1]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$(1),A6$(1),A[ALL]; REM WA# 205
0215 IVM01B: IOLIST S0$(1),S1$(1)
0220 IVM02A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0300 IVM10A: IOLIST X0$
0320 IVM13A: IOLIST X0$,X1$
0325 IVM15A: IOLIST X0$,X1$
0330 IVM03: IOLIST X0$(1)
0380 IVM14A: IOLIST X0$(1)
0390 IVM24A: IOLIST X0$(1)
0500 REM " --- Initializations"
0505 DIM A5$(2); REM WA# 205
0510 DIM PRODUCT$(20),CLASS$(20),DATEV4$[8],DATEP[8],DATEV1[4]
0520 DIM FILES[14],PARAMS[3],ALTSUP$(79-(14+P[0]+1)),TYPE$(20)
0545 LET PARAMS[0]=P[0],PARAMS[1]=P[3],PARAMS[2]=P[4],PARAMS[3]=P[5]
0550 LET EV$="Enter A Valid ",TI$=" This Item",M6$="###0.00-",M6=LEN(M6$)
0555 LET DATEV4$[1]="Receipt",FILES[1]=IVM01_DEV,FILES[14]=IVM14_DEV
0560 LET DATEV4$[2]="Date Built",DATEV4$[3]="Sale",DATEV4$[4]="Issue"
0565 LET DATEV4$[5]="Adjustment",DATEV4$[6]="Return",DATEV4$[7]="Transfer In"
0570 LET DATEV4$[8]="Transfer Out",DATEP[1]=29,DATEP[2]=32,DATEP[3]=35
0575 LET DATEP[4]=38,DATEP[5]=41,DATEP[6]=50,DATEP[7]=44,DATEP[8]=47
0580 LET DATEV1[1]=9,DATEV1[2]=26,DATEV1[3]=47,DATEV1[4]=70
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,11,0
0720 CALL "SYC.WC",1,0,80,0,0,13,0
0730 CALL "SYC.WC",1,0,80,0,0,16,0
0740 PRINT 'SB',@(0,6),"Product Type:",@(44),"Sale UM:",@(61),"Sales Analysis:",@(2,7),"Item Class:",@(40),"Purchase UM:",@(59),"Configured Item?:",@(3,8),"Item Type:",@(45),"Weight:",@(64),"Commission?:",@(3,9),"MSR Price:",@(34),"Conversion Factor:",@(68),"Taxable:",@(4,10),"UPC Code:",@(43),"Bar Code:",@(8,11)," Alternate/Superceded/Not Used ",@(0,12),"Code:",@(8),"Item:",@(8,13)," Last Transaction Dates ",@(0,14),"Receipt:",@(20),"Sold:",@(35),"Adjustment:",@(57),"Transfer In:",@(2,15),"Built:",@(18),"Issued:",@(37),"Returned:",@(56),"Transfer Out:",'SF'; REM WA# 205
1200 REM " --- Retrieve record"
1210 FIND (IVM01_DEV,KEY=A0$,DOM=4900)IOL=IVM01A
1220 LET OLDPRODUCT$=A2$(1,3),OLDUPCCODE$=A3$(1,20),OLDBARCODE$=A3$(21,20)
1230 LET OLDCLASS$=A2$(21,3),OLDTYPE$=A2$(53,3)
1240 PRINT 'SB',@(0,17),"Sell Online?:",; IF A2$(57,1)="B" THEN PRINT @(33),"Client Item Number:",@(31,18),"Multiuser Available?:"
1242 PRINT 'SF'
1300 REM " --- Display record"
1310 GOSUB 6500
1320 GOSUB 5000
1390 ON O0 GOTO 4000,4000,1600
1600 REM " --- Product type"
1610 LET V4$=EV$+"Product Type (<F3>=Lookup)",I0=0
1620 LET V0$="S",V1$="C^",V2$=A2$(1,3),V3$="",V0=3,V1=14,V2=6
1630 GOSUB 7000
1640 ON V3 GOTO 1660,1600,3900,1650,3900
1650 CALL "IVC.LK",1,V1,V2,V$
1660 LET A2$(1,3)=V$
1670 FIND (IVM10_DEV,KEY=N0$+"A"+A2$(1,3),DOM=1600)IOL=IVM10A
1675 PRINT @(V1,V2),A2$(1,3)," ",X0$(7,20),
1680 IF A2$(18,1)=" " THEN LET A2$(18,1)=X0$(27,1)
1690 GOTO 3900
1700 REM " --- Item Class"
1710 LET V4$=EV$+"Item Class (<F3>=Lookup)"
1720 LET V0$="S",V1$="C",V2$=A2$(21,3),V3$="",V0=3,V1=14,V2=7
1730 GOSUB 7000
1740 ON V3 GOTO 1760,1700,3900,1750,3900
1750 CALL "IVC.LK",5,V1,V2,V$
1760 LET A2$(21,3)=V$
1770 FIND (IVM13_DEV,KEY=N0$+A2$(21,3),DOM=1700)IOL=IVM13A
1780 PRINT @(V1,V2),A2$(21,3)," ",X1$(1,20),
1790 GOTO 3900
1800 REM " --- Weight"
1810 LET V4$="Enter The Weight Of"+TI$
1820 LET V0$="N",V1$="C",V2$=STR(A[1]),V3$=M6$,V0=M6,V1=53,V2=8
1830 GOSUB 7000
1840 ON V3 GOTO 1850,1800,3900,1800,3900
1850 IF V<0 OR V>9999.99 THEN GOTO 1800
1860 LET A[1]=V
1890 GOTO 3900
1900 REM " --- MSR Price"
1902 GOTO 3900
1910 LET V4$="Enter The Manufacturer's Suggested Retail Price For"+TI$
1920 LET V0$="N",V1$="C",V2$=STR(A[2]),V3$=M4$,V0=M4,V1=14,V2=9
1930 GOSUB 7000
1940 ON V3 GOTO 1950,1900,3900,1900,3900
1950 IF V<0 THEN GOTO 1900
1960 LET A[2]=V
1990 GOTO 3900
2000 REM " --- UPC Code"
2002 GOTO 3900
2010 LET V4$="Enter An Optional UPC Code For"+TI$
2020 LET V0$="S",V1$="C",V2$=A3$(1,20),V3$="",V0=20,V1=14,V2=10
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 LET A3$(1,20)=V$
2090 GOTO 3900
2100 REM " --- Sale UM"
2102 GOTO 3900
2110 LET V4$=EV$+"Sale Unit Of Measure"
2120 LET V0$="S",V1$="C",V2$=A2$(4,2),V3$="",V0=2,V1=53,V2=6
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2150 LET A2$(4,2)=V$
2190 GOTO 3900
2200 REM " --- Purchase UM"
2202 GOTO 3900
2210 LET V4$=EV$+"Purchase Unit Of Measure"
2220 LET V0$="S",V1$="C",V2$=A2$(6,2),V3$="",V0=2,V1=53,V2=7
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 LET A2$(6,2)=V$
2290 GOTO 3900
2300 REM " --- Conversion Factor"
2302 GOTO 3900
2310 LET V4$="Enter The Conversion Factor To Be Used With This Purchase UM"
2320 LET V0$="N",V1$="C",V2$=STR(A[0]),V3$=M5$,V0=M5,V1=53,V2=9
2330 GOSUB 7000
2340 ON V3 GOTO 2350,2300,3900,2300,3900
2350 IF V<0 THEN GOTO 2300
2360 LET A[0]=V
2390 GOTO 3900
2400 REM " --- Taxable?"
2410 LET V4$="Is This A Taxable Item (Y/N)?"
2420 LET V0$="S",V1$="C^",V2$=A2$(8,1),V3$="YN ",V0=1,V1=77,V2=9
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 LET A2$(8,1)=V$
2490 GOTO 3900
2500 REM " --- Bar Code"
2502 GOTO 3900
2510 LET V4$="Enter An Optional Bar Code For"+TI$
2520 LET V0$="S",V1$="C",V2$=A3$(21,20),V3$="",V0=20,V1=53,V2=10
2530 GOSUB 7000
2540 ON V3 GOTO 2550,2500,3900,2500,3900
2550 LET A3$(21,20)=V$
2590 GOTO 3900
2600 REM " --- Sales Analysis Level"
2610 LET V4$="P=Product Level     I=Product+Item Level     N=No Sales Analysis"
2620 LET V0$="S",V1$="C^",V2$=A2$(18,1),V3$="PIN",V0=1,V1=77,V2=6,V$="N"
2625 IF SA$<>"Y" THEN GOTO 2650
2630 GOSUB 7000
2640 ON V3 GOTO 2650,2600,3900,2600,3900
2650 LET A2$(18,1)=V$
2660 PRINT @(V1,V2),A2$(18,1),
2690 GOTO 3900
2700 REM " --- Lotted/Serialized"
2710 LET V4$="Is This A Lotted/Serialized Item (Y/N)?"
2720 LET V0$="Y",V1$="C",V2$=A2$(19,1),V3$="",V0=1,V1=77,V2=7
2725 IF LS$<>"Y" THEN GOTO 2780
2730 GOSUB 7000
2740 ON V3 GOTO 2750,2700,3900,2700,3900
2750 LET A2$(19,1)=V$
2760 PRINT @(V1,V2),A2$(19,1)
2770 GOTO 3900
2780 PRINT @(V1,V2),"n/a",
2790 GOTO 3900
2800 REM " --- Inventoried?"
2810 LET V4$="Maintain This Item In Inventory By Lot/Serial Number (Y=Yes/N=Enter When Sold)?"
2820 LET V0$="S",V1$="C",V2$=A2$(20,1),V3$="",V$="N",V0=2,V1=77,V2=8
2830 IF LS$<>"Y" THEN GOTO 2880
2840 IF A2$(19,1)<>"Y" THEN LET A2$(20,1)="N"; GOTO 2880
2845 GOSUB 7000
2850 IF POS(V$="YN")=0 THEN GOTO 2800
2855 ON V3 GOTO 2860,2800,3900,2800,3900
2860 LET A2$(20,1)=V$
2865 PRINT @(V1,V2),A2$(20,1)
2870 GOTO 3900
2880 PRINT @(V1,V2),"n/a",
2890 GOTO 3900
2900 REM " --- Item Type"
2910 LET V4$="Enter An Item Type For"+TI$+" (<F3>=Lookup)"
2920 LET V0$="S",V1$="C",V2$=A2$(53,3),V3$="",V0=3,V1=14,V2=8
2930 GOSUB 7000
2940 ON V3 GOTO 2960,2900,3900,2950,3900
2950 CALL "IVC.LK",6,V1,V2,V$
2960 LET V$=V$+"   ",V$=V$(1,3),TYPE$(1)=""
2970 FIND (IVM15_DEV,KEY=N0$+V$,DOM=2900)IOL=IVM15A
2980 LET A2$(53,3)=V$,TYPE$(1)=X1$(1,20)
2985 PRINT @(V1,V2),A2$(53,3)," ",TYPE$,
2990 GOTO 3900
3000 REM " --- Alternate/Superceded"
3002 GOTO 3900
3010 LET V4$="Is There An Alternate Item Or Has This Item Been Superceded (A/S/N=Neither)?"
3020 LET V0$="S",V1$="C^",V2$=A4$(1,1),V3$="ASN",V0=1,V1=6,V2=12
3030 GOSUB 7000
3040 ON V3 GOTO 3050,3000,3900,3000,3900
3050 LET A4$(1,1)=V$
3060 IF POS(A4$(1,1)="AS")>0 THEN GOTO 3900
3070 LET A4$(2)="",ALTSUP$(1)=""
3080 PRINT @(14,12),A4$(2,P[0]),@(14+P[0]+1),ALTSUP$,'CL',
3090 GOTO 3900
3100 REM " --- Alternate/Superceded Item"
3105 IF POS(A4$(1,1)="AS")=0 THEN GOTO 3900
3110 DIM ITEM$[6],ITEM[12]
3120 LET V1=14,V2=12,ACTION$=""
3150 CALL "IVC.IA",ACTION$,V1,V2,FILES[ALL],N0$,A4$(2),V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
3160 ON V3 GOTO 3170,3100,3900,3100,3900
3170 LET A4$(2)=ITEM$[0],ALTSUP$(1)=ITEM$[1]
3180 PRINT @(14,12),A4$(2,P[0]),@(14+P[0]+1),ALTSUP$,'CL',
3190 GOTO 3900
3200 REM " --- Dates"
3205 IF O0<2 THEN GOTO 3900
3210 LET X=I0-15,V4$=DATEV4$[X]
3215 IF X<>2 THEN LET V4$=V4$+" Date"
3220 LET V0=3,V2=14,V4$="Enter The Last "+V4$+" For"+TI$
3225 LET V0$="D",V1$="C",V2$=A2$(DATEP[X],3),V3$="",V1=DATEV1[INT((X+1)/2)]
3230 IF MOD(X,2)=0 THEN LET V2=V2+1
3235 GOSUB 7000
3240 ON V3 GOTO 3250,3200,3900,3200,3900
3250 LET A2$(DATEP[X],3)=V$
3290 GOTO 3900
3300 REM " --- Sell online"
3310 LET V4$="OK to sell this on the e_commerce web site? (Y/N)"
3320 LET V0$="S",V1$="C^",V2$=A2$(56,1),V3$="YN",V0=1,V1=14,V2=17
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3300,3900,3300,3900
3350 LET A2$(56,1)=V$
3390 GOTO 3900
3400 REM " --- Client item"
3405 IF A2$(57,1)<>"B" THEN GOTO 3900
3410 LET V4$="Enter the item number of the client for this data server bundle (<F3>=Lookup)"
3420 LET V0$="S",V1$="C^",V2$=A3$(1,20),V3$="",V0=20,V1=53,V2=17
3430 GOSUB 7000
3440 ON V3 GOTO 3450,3400,3900,3445,3900
3445 CALL "SYC.LI",V$; PRINT @(V1,V2),V$
3447 IF V$="" THEN GOTO 3400
3450 LET V$=V$+FILL(20); IF CVS(V$,2)="" THEN LET A3$(1,20)=V$,A2$(58,1)=" "; GOTO 3900
3460 FIND (IVM01_DEV,KEY=N0$+V$(1,20),DOM=3400)*,*,X$
3465 IF X$(57,1)<>"C" THEN LET V2$=V$,V4$="Item is not a client- Press Enter to redo"; GOSUB 7000; GOTO 3400
3470 LET A3$(1,20)=V$(1,20)
3490 REM GOTO 3900
3500 REM " --- Multiuser Client?"
3505 IF A2$(57,1)<>"B" THEN GOTO 3900
3510 LET V4$="Can this client be sold as multi-user? (Y/N)"
3520 LET V0$="S",V1$="C^",V2$=A2$(58,1),V3$="YN",V0=1,V1=53,V2=18
3530 GOSUB 7000
3550 LET A2$(58,1)=V$
3590 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1600,1700,2900,1900,2000,2100,2200,1800,2300,2500,2600,12700,12800,2400,3000,3100,3200,3200,3200,3200,3200,3200,3200,3200,3300,3400,3990; REM WA# 205
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete/#/Restore)?"
4020 LET V0$="S",V1$="KC^",V2$="Y",V3$="",V0=7,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4050 IF V$="DELETE" THEN GOTO 4400
4060 IF V$="R" OR V$="RESTORE" THEN GOTO 1200
4065 LET I0=NUM(V$,ERR=4090)-1
4070 IF V$="" THEN GOTO 4090
4075 IF I0<0 OR I0>23 THEN GOTO 4000
4080 LET I0=I0-1
4085 GOTO 3900
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write xrefs"
4105 REM " --- IVM03A: Prod/Item
4110 LET REC_SIZE=26,OLD_REC$=A0$(1,2)+"A"+OLDPRODUCT$+A0$(3),NEW_REC$=A0$(1,2)+"A"+A2$(1,3)+A0$(3)
4115 GOSUB 6600
4120 REM " --- IVM03I: BarCode/Item
4125 LET REC_SIZE=43,OLD_REC$=A0$(1,2)+"I"+OLDBARCODE$+A0$(3),NEW_REC$=A0$(1,2)+"I"+A3$(21)+A0$(3)
4130 IF FNP$(A3$(21))="" THEN LET NEW_REC$=""
4135 GOSUB 6600
4140 REM " --- IVM03J: Class/Item
4145 LET REC_SIZE=26,OLD_REC$=A0$(1,2)+"J"+OLDCLASS$+A0$(3),NEW_REC$=A0$(1,2)+"J"+A2$(21,3)+A0$(3)
4150 GOSUB 6600
4155 REM " --- IVM03T: Type/Item
4160 LET REC_SIZE=26,OLD_REC$=A0$(1,2)+"T"+OLDTYPE$+A0$(3),NEW_REC$=A0$(1,2)+"T"+A2$(53,3)+A0$(3)
4165 GOSUB 6600
4170 REM " --- IVM03U: UPC/Item
4175 LET REC_SIZE=43,OLD_REC$=A0$(1,2)+"U"+OLDUPCCODE$+A0$(3),NEW_REC$=A0$(1,2)+"U"+A3$(1,20)+A0$(3)
4180 IF FNP$(A3$(1,20))="" THEN LET NEW_REC$=""
4185 GOSUB 6600
4190 REM " --- Other xrefs
4195 GOSUB 6000
4210 DIM X0$(42)
4215 LET X0$(1)=N0$+A0$(3)+OLDUPCCODE$
4220 REMOVE (IVM24_DEV,KEY=X0$,DOM=4225)
4225 LET X0$(1)=N0$+OLDUPCCODE$+A0$(3)
4230 REMOVE (IVM14_DEV,KEY=X0$,DOM=4235)
4235 IF FNP$(A3$(1,20))="" THEN GOTO 4255
4237 LET X0$(1)=N0$+A0$(3)+A3$(1,20)
4240 WRITE (IVM24_DEV,KEY=X0$)IOL=IVM24A
4245 LET X0$(1)=N0$+A3$(1,20)+A0$(3)
4250 WRITE (IVM14_DEV,KEY=X0$)IOL=IVM14A
4255 LET X0$(1)=N0$+A0$(3)+OLDBARCODE$
4260 REMOVE (IVM24_DEV,KEY=X0$,DOM=4265)
4265 LET X0$(1)=N0$+OLDBARCODE$+A0$(3)
4270 REMOVE (IVM14_DEV,KEY=X0$,DOM=4275)
4275 IF FNP$(A3$(21))="" THEN GOTO 4295
4277 LET X0$(1)=N0$+A0$(3)+A3$(21)
4280 WRITE (IVM24_DEV,KEY=X0$)IOL=IVM24A
4285 LET X0$(1)=N0$+A3$(21)+A0$(3)
4290 WRITE (IVM14_DEV,KEY=X0$)IOL=IVM14A
4300 REM " --- Write item"
4310 WRITE (IVM01_DEV,KEY=A0$)IOL=IVM01A
4390 GOTO 4900
4400 REM " --- Delete item and associated records"
4410 DIM PARAMS$[10],PARAMS[10]
4420 LET ACTION$="I",PARAMS$[0]=N0$,PARAMS$[1]="",PARAMS$[2]=A0$(3)
4430 LET PARAMS$[3]=OP$,PARAMS$[4]=PO$,PARAMS$[5]=WO$,PARAMS$[6]=BM$
4440 LET PARAMS$[7]=AP$,PARAMS[0]=G[0],PARAMS[1]=P[6],PARAMS[2]=P[7]
4450 CALL "IVC.DA",ACTION$,CHANNELS[ALL],PARAMS$[ALL],PARAMS[ALL],STATUS
4460 IF STATUS<>0 THEN GOTO 4000
4470 CALL "SYC.WC",1,0,80,0,0,L0-1,0
4480 PRINT @(0,L0),'CE',
4490 LET O0=2
4900 REM " --- Run menu overlay"
4990 RUN "IVM.MA"
5000 REM " --- Display record"
5010 DIM DATES$[8](8)
5015 LET PRODUCT$(1)="",CLASS$(1)="",TYPE$(1)=""
5020 FIND (IVM10_DEV,KEY=N0$+"A"+A2$(1,3),DOM=5030)IOL=IVM10A
5025 LET PRODUCT$(1)=X0$(7,20)
5030 FIND (IVM13_DEV,KEY=N0$+A2$(21,3),DOM=5040)IOL=IVM13A
5035 LET CLASS$(1)=X1$(1,20)
5040 FIND (IVM15_DEV,KEY=N0$+A2$(53,3),DOM=5050)IOL=IVM15A
5045 LET TYPE$(1)=X1$(1,20)
5050 LET LOTSER$=A2$(19,1),INVENT$=A2$(20,1)
5060 IF LS$<>"Y" THEN LET LOTSER$="n/a",INVENT$="n/a"
5065 IF LS$="Y" AND A2$(19,1)="N" THEN LET INVENT$="n/a"
5070 FOR X=1 TO 8
5075 IF FNP$(A2$(DATEP[X],3))<>"" THEN LET DATES$[X](1)=FNB$(A2$(DATEP[X],3))
5080 NEXT X
5085 REM PRINT @(14,6),A2$(1,3)," ",PRODUCT$,@(53),A2$(4,2),@(77),A2$(18,1),@(14,7),A2$(21,3)," ",CLASS$,@(53),A2$(6,2),@(77),A5$(1,1),@(14,8),A2$(53,3)," ",TYPE$,@(53),A[1]:M6$,@(77),INVENT$,@(14,9),A[2]:M4$,@(53),A[0]:M5$,@(77),A2$(8,1),@(14,10),A3$(1,20),@(53),A3$(21,20),@(6,12),A4$(1,1),@(14,12),A4$(2,P[0]),@(14+P[0]+1),ALTSUP$,'CL',@(DATEV1[1],14),DATES$[1],@(DATEV1[2]),DATES$[3],@(DATEV1[3]),DATES$[5],@(DATEV1[4]),DATES$[7],@(DATEV1[1],15),DATES$[2],@(DATEV1[2]),DATES$[4],@(DATEV1[3]),DATES$[6],@(DATEV1[4]),DATES$[8],; REM WA# 205
5086 PRINT @(14,6),A2$(1,3)," ",PRODUCT$,@(53),A2$(4,2),@(77),A2$(18,1),@(14,7),A2$(21,3)," ",CLASS$,@(53),A2$(6,2),@(77),A5$(1,1),@(14,8),A2$(53,3)," ",TYPE$,@(53),A[1]:M6$,@(77),a5$(2,1),@(14,9),A[2]:M4$,@(53),A[0]:M5$,@(77),A2$(8,1),@(14,10),"",@(53),A3$(21,20),@(6,12),A4$(1,1),@(14,12),A4$(2,P[0]),@(14+P[0]+1),ALTSUP$,'CL',@(DATEV1[1],14),DATES$[1],@(DATEV1[2]),DATES$[3],@(DATEV1[3]),DATES$[5],@(DATEV1[4]),DATES$[7],@(DATEV1[1],15),DATES$[2],@(DATEV1[2]),DATES$[4],@(DATEV1[3]),DATES$[6],@(DATEV1[4]),DATES$[8],; REM WA# 205
5087 PRINT @(14,17),A2$(56,1),@(53),A3$(1,20),@(53,18),A2$(58,1),
5090 RETURN 
6000 REM " --- Update Warehouse xrefs
6010 READ (IVM03_DEV,KEY=N0$+"G"+A0$(3),DOM=6011)
6050 LET K_G$=KEY(IVM03_DEV,END=6290)
6060 IF POS(N0$+"G"+A0$(3)=K_G$)<>1 THEN GOTO 6290
6065 LET WHS$=K_G$(24,2)
6070 REM " --- IVM03C: Whse/Prod/Item 
6080 LET REC_SIZE=28,OLD_REC$=A0$(1,2)+"C"+WHS$+OLDPRODUCT$+A0$(3),NEW_REC$=A0$(1,2)+"C"+WHS$+A2$(1,3)+A0$(3)
6090 GOSUB 6600
6100 REM " --- Buyer/Vendor xrefs
6110 IF A2$(24,1)="I" THEN LET BUYER$=A2$(9,3),WHS$=FILL(2); GOTO 6150
6120 DIM B0$(24),B1$(64),B[20]
6130 READ (IVM02_DEV,KEY=N0$+WHS$+A0$(3))IOL=IVM02A
6140 LET BUYER$=B1$(19,3)
6150 READ (IVM03_DEV,KEY=N0$+"H"+A0$(3),DOM=6151)
6160 LET K_H$=KEY(IVM03_DEV,END=6270)
6170 IF POS(N0$+"H"+A0$(3)=K_H$)<>1 THEN GOTO 6270
6200 REM " --- IVM03F: Buyer/Vend/Prod/Item/Whse
6210 LET REC_SIZE=37,OLD_REC$=A0$(1,2)+"F"+BUYER$+K_H$(24,6)+OLDPRODUCT$+A0$(3)+WHS$,NEW_REC$=A0$(1,2)+"F"+BUYER$+K_H$(24,6)+A2$(1,3)+A0$(3)+WHS$
6220 GOSUB 6600
6250 READ (IVM03_DEV,KEY=K_H$)
6260 GOTO 6160
6270 READ (IVM03_DEV,KEY=K_G$)
6280 GOTO 6050
6290 RETURN 
6500 REM " --- Retrieve alternate/superceded item description"
6510 DIM S0$(22),S1$(60)
6520 LET ALTSUP$(1)="",S0$(1)=N0$+A4$(2)
6530 IF POS(A4$(1,1)="AS")=0 THEN GOTO 6590
6540 FIND (IVM01_DEV,KEY=S0$,DOM=6590)IOL=IVM01B
6550 LET ALTSUP$(1)=FNITEM$(S1$,P[3],P[4],P[5])
6590 RETURN 
6600 REM " --- Update IVM-03 Record
6620 DIM X0$(REC_SIZE)
6630 LET X0$(1)=OLD_REC$
6640 REMOVE (IVM03_DEV,KEY=X0$,DOM=6641)
6660 LET X0$(1)=NEW_REC$
6670 IF NEW_REC$<>"" THEN WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END
12700 REM " --- Configured Item?" - WA# 205
12710 LET V4$="Is This A Configured Item (Y/N)?"
12720 LET V0$="Y",V1$="C^",V2$=A5$(1,1),V3$="",V0=1,V1=77,V2=7
12730 GOSUB 7000
12740 ON V3 GOTO 12750,12700,3900,12700,3900
12750 LET A5$(1,1)=V$
12760 PRINT @(V1,V2),A5$(1,1)
12790 GOTO 3900
12800 REM " --- Commissionable Item?"
12810 LET V4$="Is This A Commissionable Item (Y/N)?"
12820 LET V0$="Y",V1$="C^",V2$=A5$(2,1),V3$="",V0=1,V1=77,V2=8
12830 GOSUB 7000
12840 ON V3 GOTO 12850,12800,3900,12800,3900
12850 LET A5$(2,1)=V$
12860 PRINT @(V1,V2),A5$(2,1)
12890 GOTO 3900

0010 REM "SYC - Display Range Of Customer Or Vendor Comments"
0020 REM "Program SYC.CM"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "ACTION$: Display type B=Box/W=Window (input)"
0050 REM "N0$    : Firm ID (input)"
0052 REM "KEYVAL$: Customer or vendor number (input)"
0054 REM "FIRST$ : Starting comment line number to display (input)"
0056 REM "LAST$  : Ending comment line number to display (input)"
0058 REM "CHANNEL: Comment file (ARM-05 or ARM-09) channel (input)"
0060 REM "WIDTH  : Comment box width (input)"
0062 REM "HEIGHT : Comment box height"
0064 REM "COLUMN : Comment box upper left column position"
0066 REM "ROW    : Comment box upper left row position"
0070 REM 
0080 SETESC 9000
0085 SETERR 9000
0090 ENTER ACTION$,N0$,KEYVAL$,FIRST$,LAST$,CHANNEL,WIDTH,HEIGHT,COLUMN,ROW
0200 REM " --- IOLists"
0210 COMMENTS: IOLIST X0$,X1$
0500 REM " --- Initialize Data"
0540 LET TOP=ROW+1,BOTTOM=ROW+HEIGHT-2,LINE=TOP,COUNT=0,MARGIN=1
0550 LET NAME$="",BEGKEY$=N0$+KEYVAL$,ENDKEY$=N0$+KEYVAL$+LAST$
0560 IF NUM(LAST$,ERR=0570)=0 THEN LET ENDKEY$=""
0570 IF POS("B"=ACTION$) THEN GOTO 0700
0580 LET WIN_X=COLUMN,WIN_Y=ROW,COLUMN=0,ROW=0,WIDTH=WIDTH+2
0590 LET TOP=0,BOTTOM=HEIGHT-3,LINE=TOP,MARGIN=0
0700 REM " --- Display Background"
0710 IF POS("W"=ACTION$) THEN CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,"",NAME$
0720 IF POS("B"=ACTION$) THEN CALL "SYC.WC",0,0,WIDTH,HEIGHT,COLUMN,ROW,0
0740 GOSUB 5000
0900 REM " --- Position For First Comment"
0920 READ (CHANNEL,KEY=BEGKEY$+FIRST$,DOM=1020)IOL=COMMENTS
0940 GOTO 1040
1000 REM " --- Get Comment"
1020 READ (CHANNEL,END=4000)IOL=COMMENTS
1040 IF POS(BEGKEY$=X0$)<>1 THEN GOTO 4000
1060 IF FNP$(ENDKEY$)<>"" AND X0$>ENDKEY$ THEN GOTO 4000
1100 REM " --- Overflow"
1110 IF LINE<BOTTOM THEN GOTO 1200
1120 GOSUB 6000
1170 IF V3=4 THEN GOTO 4000
1180 GOSUB 5000
1190 LET LINE=TOP
1200 REM " --- Display Comment"
1220 IF LEN(X1$)>WIDTH-2 THEN LET X1$=X1$(1,WIDTH-2)
1221 PRINT @(COLUMN+MARGIN,LINE),X1$,
1240 LET LINE=LINE+1,COUNT=COUNT+1
1300 REM " --- Next Comment"
1390 GOTO 1000
4000 REM " --- Done (Box)"
4010 IF POS("W"=ACTION$) THEN GOTO 4100
4020 IF COUNT>0 THEN GOTO 4090
4040 LET X$="(No Comments)"
4060 PRINT @(COLUMN+FNC2(X$,WIDTH),INT(TOP+(BOTTOM-TOP)/2)),X$,
4090 GOTO 4900
4100 REM " --- Done (Window)"
4110 IF COUNT>0 THEN GOSUB 6000
4900 REM " --- All done"
4990 GOTO 9900
5000 REM " --- Clear Comment Box"
5020 FOR I=TOP TO BOTTOM
5040 PRINT @(COLUMN+MARGIN,I),FILL(WIDTH-2),
5060 NEXT I
5090 RETURN 
6000 REM " --- Continue?"
6010 LET V4$="Press <Enter> To Continue",V1=COLUMN+FNC2(V4$,WIDTH),V2=BOTTOM
6020 PRINT @(V1,V2),V4$,
6030 LET V0=1,V1=V1+LEN(V4$)+1,V0$="S",V1$="E",V2$="",V3$="",V4$=""
6040 GOSUB 7000
6050 PRINT @(COLUMN+MARGIN,BOTTOM),FILL(WIDTH-2),
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNC2(Q$,Q)=INT((Q-2-LEN(Q$))/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Exit to host"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Caller"
9910 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9950 EXIT 
9999 END

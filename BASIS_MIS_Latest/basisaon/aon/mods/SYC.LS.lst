0010 REM "SYC - Serial Number Lookup - BASIS"
0020 REM "Program SYC.LS"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "MAS * DCF * WA# 208 * BASIS
0040 REM 
0045 REM "SELECTION$: Serial Number (returned)"
0050 REM 
0060 REM " ** BASIS Modified to display SAM/Developer Contract Info   
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER SELECTION$
0100 REM " --- Open Files"
0110 LET FILES=8
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SNM-01",FILES$[3]="ARM-04"
0135 LET FILES$[4]="SNM-01",FILES$[5]="SYM-06",FILES$[6]="ARM-01"
0140 LET FILES$[7]="SNT-01",FILES$[8]="SMC-01"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1],SNM01_DEV=CHANNELS[2],ARM04_DEV=CHANNELS[3]
0185 LET SNM01_DEV2=CHANNELS[4],SYM06_DEV=CHANNELS[5],ARM01_DEV=CHANNELS[6]
0190 LET SNT01_DEV=CHANNELS[7],SMC01_DEV=CHANNELS[8]
0200 REM " --- IOLIST's"
0210 SNM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A[ALL]
0230 SYM06A: IOLIST X$,COMPANY$
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$,M4$,M5$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 SNT01A: IOLIST W0$,W1$,W2$,W[ALL]
0290 call "templates.pgm::smc01"
0400 REM " --- Parameters"
0405 DIM P[5],I[2],INFO$[20],SPEED$(128)
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0425 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),SPEEDSEARCH$="N",AP$="N"
0435 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=0445)
0440 LET N0$=SPEED$(1,2),SPEEDSEARCH$="Y"
0445 FIND (SYM06_DEV,KEY=N0$,DOM=9800)IOL=SYM06A
0450 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0455 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[3]=NUM(P2$(7,2))
0460 LET P[2]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2)),P[5]=NUM(P2$(13,2))
0500 REM " --- Initializations"
0505 PRECISION NUM(P2$(5,1))
0510 DIM A0$(20),A1$(60),A2$(18),A3$(35),A4$(60),A[2],S0$(42),W[5]
0540 LET WIDTH=73,HEIGHT=12,WIN_X=1,WIN_Y=3,MAX_ROW=HEIGHT-5,M9$="###0.00-"
0550 LET SWIDTH=73,SHEIGHT=6,SWIN_X=WIN_X,SWIN_Y=WIN_Y+HEIGHT,M9=LEN(M9$)
0560 LET PGM$=PGM(-2),OPTIONS=0,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3
0570 LET STITLE$="Serial Number Detail",W1=15+MAX(M4,M9),W2=20
0600 REM " --- Window arrays"
0610 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0620 LET HEADING$(2)="Number",HEADING$(25)="Active",HEADING$(35)="Customer"
0630 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0640 LET TITLE$="Serial Number Lookup"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Which search option?"
0810 DIM STRINGS$[10],NUMBERS[10]
0815 LET STRINGS$[0]=PGM$,STRINGS$[1]="",NUMBERS[0]=MAXROW
0820 LET NUMBERS[1]=OWIN_X,NUMBERS[2]=OWIN_Y,NUMBERS[3]=P[0]
0825 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
0830 CALL "SYC.SM",STRINGS$[ALL],NUMBERS[ALL],FKEY
0835 IF FKEY THEN GOTO 9900
0840 LET PREFIX$=STRINGS$[2],START$=FNU$(STRINGS$[3]),METHOD$=STRINGS$[4]
0845 LET V3$=STRINGS$[5],V0=NUMBERS[4],W=INT(WIDTH/2); REM WIDTH-2-(P[0]+3)
0900 REM " --- Detail Window"
0910 CALL "SYC.WA",0,SWIDTH,SHEIGHT,SWIN_X,SWIN_Y,STITLE$,SNAME$
0915 CALL "SYC.WB",NAME$,0
0920 REM LET HEADING$(W)=METHOD$
0925 REM PRINT @(0,0),'CF','SB','BR',HEADING$,'ER','SF',
0930 IF SPEEDSEARCH$<>"Y" THEN GOTO 1000
0950 REM " --- Display SpeedSearch Company"
0955 DIM X$(WIDTH+SWIDTH)
0960 LET X$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
0965 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
0970 CALL "SYC.WA",0,SWIDTH,3,SWIN_X,SWIN_Y+SHEIGHT,"",CNAME$
0975 PRINT @(0,0),'BR',X$,'ER',
0980 CALL "SYC.WB",NAME$,0
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,SHEIGHT-2]
1020 LET PAGE=1,L=1,X1=1,X0=0,CUST$="",SELECTION$=""
1030 IF PREFIX$="" THEN GOTO 1200
1040 IF PREFIX$="*" THEN GOTO 1300
1050 IF PREFIX$<>"C" THEN GOTO 1200
1060 REM " --- Customer Name"
1065 LET V9=V0,V0=6
1070 READ (SNM01_DEV2,KNUM=1,KEY=N0$,DOM=1080)
1080 READ (ARM04_DEV,KEY=N0$+"A"+START$,DOM=1600)
1090 GOTO 1650
1200 REM " --- Serial Number"
1210 READ (SNM01_DEV,KEY=START$,DOM=1400)IOL=SNM01A
1220 LET K$=A0$,X$=A0$
1290 GOTO 1900
1300 REM " --- Keyword"
1310 REM LET START$=FNU$(START$)
1320 READ (SNM01_DEV,KEY="",DOM=1400)
1340 GOTO 1400
1400 REM " --- Read next record"
1410 IF PREFIX$="" THEN GOTO 1800
1420 IF PREFIX$="*" THEN GOTO 1800
1500 REM " --- Next SNM-01(2) sort record"
1510 LET K$=KEY(SNM01_DEV2,END=1600)
1540 IF POS(N0$+CUST$=K$)<>1 THEN GOTO 1600
1550 READ (SNM01_DEV2,KEY=K$)
1580 LET K$=K$(9)
1590 GOTO 1900
1600 REM " --- Next ARM-04 customer name"
1610 LET K$=KEY(ARM04_DEV,END=3000)
1620 IF POS(N0$+"A"=K$)<>1 THEN GOTO 3000
1650 READ (ARM04_DEV,KEY=K$)
1660 LET CUST$=K$(4+V9),X$=K$(4,V9)
1680 READ (SNM01_DEV2,KEY=N0$+CUST$,DOM=1400)
1690 GOTO 1400
1800 REM " --- Next SNM-01 key"
1810 LET K$=KEY(SNM01_DEV,END=3000)
1830 LET X$=K$
1840 GOTO 1900
1900 REM " --- Read inventory master record"
1905 LET LOCKED=0
1910 READ (SNM01_DEV,KEY=K$,ERR=1950)IOL=SNM01A
1915 IF PREFIX$<>"*" THEN GOTO 2000
1917 IF START$="" THEN GOTO 1935
1920 IF POS(START$=FNU$(A1$))>0 THEN GOTO 1935
1922 IF POS(START$=FNU$(A2$))>0 THEN GOTO 1935
1924 IF POS(START$=FNU$(A3$))>0 THEN GOTO 1935
1926 IF POS(START$=FNU$(A4$))>0 THEN GOTO 1935
1930 GOTO 2900
1935 LET X$=START$
1940 GOTO 2000
1950 REM " --- Error reading inventory master record"
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1980 LET LOCKED=1
2000 REM " --- Display page?"
2010 IF L<MAX_ROW THEN GOTO 2100
2020 LET XMODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 0800
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2400 REM " --- Lookup window record"
2410 DIM G1$(WIDTH-2)
2420 IF LOCKED THEN LET G1$(2)=K$(3)+" Record In Use"; GOTO 2490
2425 DIM CUST_NAME$(30); LET CUST_NAME$(1)="** Not on File **"
2430 FIND (ARM01_DEV,KEY=N0$+A3$(5,6),DOM=2431)*,CUST_NAME$
2480 LET G1$(2)=A0$,G1$(25)=A3$(1,1),G1$(35)=CUST$+" "+CUST_NAME$(1,30)
2490 LET G1$(WIDTH-2)="",LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$
2500 REM " --- Format detail window fields"
2505 IF LOCKED THEN GOTO 2680
2600 REM " --- Detail window record"
2610 DIM G2$(SWIDTH-2)
2615 GOSUB 3600
2617 GOSUB GET_CONTR_INFO
2620 LET G2$(1)="   {Product ID}"+A2$(1,3),G2$(W2)="          {Users}"+STR(A[0])
2623 LET G2$(52)="{Server}"+SERVER$
2625 LET MORE$[PAGE,X1,1]=G2$
2630 LET G2$(1)="  {Platform ID}"+A2$(4,3),G2$(W2)="   {License Type}"+A2$(13,3)
2632 LET G2$(46)="{"+TYPE$+" Contract}"+CONTRACT$
2635 LET MORE$[PAGE,X1,2]=G2$
2640 LET G2$(1)="     {OS Level}"+A2$(7,3),G2$(W2)="{Orig Sales Date}"+FNB$(A3$(2,3))
2642 LET G2$(47)="{Annual Date}"+ANNUAL$
2645 LET MORE$[PAGE,X1,3]=G2$
2650 LET G2$(1)="{Prod Revision}"+A2$(10,3),G2$(W2)="        {Port ID}"+A3$(11,5)
2652 LET G2$(48)="{Expiration}"+EXPIRE$
2655 LET MORE$[PAGE,X1,4]=G2$
2680 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),LLIST$[PAGE,X1,5]=SNAME$
2690 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2910 IF LOCKED=0 THEN GOTO 1400
2920 LET LOCKED=0
2930 IF PREFIX$<>"" AND PREFIX$<>"*" THEN GOTO 1400
2940 LET K$=K$(1,LEN(K$)-1)+CHR(ASC(K$(LEN(K$),1))+1)
2950 READ (SNM01_DEV,KEY=K$,DOM=1400,END=3000)IOL=SNM01A
2990 GOTO 1820
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3035 LET MESSAGE$[0]="No Serial Numbers Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page"
3510 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
3590 GOTO 4000
3600 REM " --- Look for data server
3610 LET DSDATE$="",SERVER$=""
3620 READ (SNT01_DEV,KEY=A0$,DOM=3621)
3630 LET K1$=KEY(SNT01_DEV,END=3690)
3640 IF K1$(1,20)<>A0$ THEN GOTO 3690
3650 READ (SNT01_DEV)IOL=SNT01A
3660 IF POS(" "<>W1$(38,20))=0 THEN GOTO 3630
3670 IF W0$(21,3)>DSDATE$ THEN LET DSDATE$=W0$(21,3),SERVER$=W1$(38,20)
3680 GOTO 3630
3690 RETURN 
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
6500 REM " --- Error detected during processing of selection"
6510 DIM MESSAGE$[1]
6520 IF REASON=1 THEN LET MESSAGE$[0]="Lot/Serial Inventory"
6530 IF REASON=2 THEN LET MESSAGE$[0]="Accounts Payable"
6540 LET MESSAGE$[0]=MESSAGE$[0]+" Is Required For This"
6550 LET MESSAGE$[1]="Selection (<Enter>=Continue)"
6560 CALL "SYC.XA",2,MESSAGE$[ALL],1,-1,-1,V$,V3
6590 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNS$(Q$)=CVS(Q$,1)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 FOR X=1 TO FILES
9915 CLOSE (CHANNELS[X],ERR=9916)
9920 NEXT X
9930 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9935 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
9940 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9945 IF SELECTION$="END" THEN LET SELECTION$=""
9950 EXIT 
9999 END
11000 GET_CONTR_INFO: 
11010 LET CONTRACT$=CVS(A3$(18,6),1+2)
11020 DIM smc01$:fattr(smc01$)
11030 DIM ANNUAL$(8),EXPIRE$(8),TYPE$(3)
11040 FIND RECORD(SMC01_DEV,KEY=CONTRACT$,DOM=CONTR_DONE)smc01$
11050 LET ANNUAL$(1)=smc01.ANNUAL_DT$(5,2)+"/"+smc01.ANNUAL_DT$(7,2)+"/"+smc01.ANNUAL_DT$(3,2)
11060 LET EXPIRE$(1)=smc01.EXPIRE_ON_DT$(5,2)+"/"+smc01.EXPIRE_ON_DT$(7,2)+"/"+smc01.EXPIRE_ON_DT$(3,2)
11070 LET TYPE$=smc01.CONTR_TYPE$
11090 CONTR_DONE: 
11100 RETURN 

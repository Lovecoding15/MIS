0010 REM "TMC - Called Routine: Contact Address Maintenance
0020 REM "Program TMC.LA
0030 REM "ADD+ON SOFTWARE VERSION 5
0031 REM "12/04/91 by Jerry Koch
0032 REM "MAS * JCM * 01/14/96 * Racing Strollers (From Baseline) * WA:205
0040 REM "Copyright(C)1991 MicroAccounting Systems, Inc. ALL RIGHTS RESERVED
0041 REM 
0042 REM "06/05/96 KJS WA: #211 Use CHAN[], <F10> is new hotkey"
0043 REM "06/07/95 KJS WA: #211 <F9> is now quick exit"
0079 REM 
0080 SETERR 9000
0090 ENTER B0$,V3,CHAN[ALL]; REM "WA:205
0100 REM "OPEN FILES
0130 LET SYS01_DEV=UNT; OPEN (SYS01_DEV,ERR=9900)"SYS-01"
0140 LET X$=STBL("SYS01_DEV",STR(SYS01_DEV))
0220 IOLIST B0$,B1$(1),B2$(1)
0270 IOLIST X$,F0$,X$,X$,F4$,F5$
0400 REM "PARAMETERS
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=0270
0420 LET N0$=F0$(16,2)
0430 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)*,*,*,P3$; REM "WA:205
0495 CLOSE (SYS01_DEV); LET X$=STBL("!CLEAR","SYS01_DEV",ERR=0496)
0500 REM --INIT DATA--
0520 LET PAGE=1
0530 DIM B1$(151),B2$(80),DESC$[100](60),J0$(20,"0"); REM "WA:205
0540 LET M7$="00000-0000",M8$="(###)-###-####"
0560 LET L0=1,L9=8,X1=1,X0=0
0600 REM "SET PARAMETERS
0610 LET WIDTH=60,HEIGHT=11,WIN_X=10,WIN_Y=10
0655 LET TITLE$="Contact Address",NAME$=TITLE$(1,8),X$=STBL("WINDOW_NAME",NAME$)
0680 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2); LET HEADING$(4)=B0$(26,20),HEADING$(30)=B0$(66,14),FOOTING$(1)="Is the Above Information Correct? <Y/N>: "
0700 REM --BACKGROUND--
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,'ER','SF',
0725 PRINT @(0,10),'SB','BR',FOOTING$,'ER','SF',
0730 LET L=L0
0740 PRINT 'SB',@(3,L),"Address:",@(6,L+2),"City:",@(5,L+3),"State:",@(27),"ZIP/Postal Code:",
0750 PRINT @(7,L+4),"    ",@(4,L+5),"E-Mail:",@(2,L+6),"Announcements:",
0760 PRINT 'SF',
1000 REM "--- Init Read
1030 PRINT 'CF',
1100 REM "--- Main Read
1140 READ (CHAN[12],KEY=B0$(1,25),DOM=4000)IOL=0220
1150 GOSUB 5000
1990 GOTO 4000
2000 REM Address 1
2010 LET V0$="S",V1$="C",V2$=B1$(1,30),V3$="",V4$="",V0=30,V1=12,V2=L
2020 LET I0=1
2030 GOSUB 7000
2080 LET B1$(1,30)=V$
2090 GOTO 2900
2100 REM Address 2
2110 LET V0$="S",V1$="C",V2$=B1$(31,30),V3$="",V4$="",V0=30,V1=12,V2=L+1
2120 GOSUB 7000
2180 LET B1$(31,30)=V$
2190 GOTO 2900
2200 REM City
2220 LET V0$="S",V1$="C",V2$=B1$(61,25),V3$="",V4$="",V0=25,V1=12,V2=L+2
2230 GOSUB 7000
2280 LET B1$(61,25)=V$
2290 GOTO 2900
2300 REM State
2320 LET V0$="S",V1$="C^",V2$=B1$(86,2),V3$="",V4$="",V0=2,V1=12,V2=L+3
2330 GOSUB 7000
2380 LET B1$(86,2)=V$
2390 GOTO 2900
2400 REM Zip Code
2410 LET V0$="S",V1$="C",V2$=B1$(88,9),V3$="",V4$="",V0=10,V1=44,V2=L+3
2430 GOSUB 7000
2440 LET B1$(88,9)=V$
2490 GOTO 2900
2500 REM Fax
2505 GOTO 2900; REM Let's not use another fax number right now-12/13/99
2510 LET V0$="H",V1$="C",V2$=B1$(122,10),V3$=M8$,V4$="",V0=10,V1=12,V2=L+4
2530 GOSUB 7000
2550 LET B1$(122,10)=V$
2590 GOTO 2900
2600 REM "E-Mail
2620 LET V0$="S",V1$="C",V2$=B2$(1,45),V3$="",V4$="",V0=45,V1=12,V2=L+5
2630 GOSUB 7000
2650 LET B2$(1)=V$
2690 GOTO 2900
2700 REM Annoucements
2720 LET V0$="Y",V1$="C^",V2$=B1$(121,1),V3$="",V4$="",V0=1,V1=17,V2=L+6
2730 GOSUB 7000
2750 LET B1$(121,1)=V$
2790 GOTO 2900
2900 REM SEQ CONTROL
2905 IF V3=9 THEN GOTO 9900
2910 IF V3=4 THEN IF I0>1 THEN LET I0=4; GOTO 4000
2920 IF V3<>2 THEN GOTO 2950
2930 IF I0>0 THEN LET I0=I0-1
2940 GOTO 2960
2950 LET I0=I0+1
2960 ON I0 GOTO 2000,2000,2100,2200,2300,2400,2500,2600,2700,4000
4000 REM "--- Correct
4010 LET L=L0,FOOTING$(1)="",LINE$="Is The Above Information Correct? <Y/N> "
4020 LET V0$="Y",V1$="C^",V2$="Y",V3$="",V0=1,V1=50,V2=14,FOOTING$(1)=LINE$
4030 PRINT @(0,V2),'BR',FOOTING$,'ER',
4035 LET V4$=LINE$
4040 GOSUB 7000
4045 PRINT @(0,V2),'CL',
4050 IF V3=4 OR V3=9 THEN GOTO 9900
4060 IF V3=2 THEN GOTO 9900
4070 IF V$="Y" OR V$="" THEN GOTO 4100
4090 GOTO 2000
4100 REM Write
4110 WRITE (CHAN[12],KEY=B0$(1,25))IOL=0220
4190 GOTO 9900
5000 REM "--- Display
5010 LET L=L0
5020 PRINT @(12,L),B1$(1,30),@(12,L+1),B1$(31,30),@(12,L+2),B1$(61,25),@(12,L+3),B1$(86,2),@(44),B1$(88,9),@(12,L+4),; REM FNF$(B1$(122,10),M8$)
5030 PRINT @(12,L+5),B2$,@(17,L+6),B1$(121,1)
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8005 REM "-- Special Date functions for YYYYMMDD Dates: FNU=Unpack (trims Century), FNP=Pack (add Century), FNB=Display YYYYMMDD as MM/DD/YY
8010 DEF FNA$(Q$,Q2$)=STR((ASC(Q$)-32)*POS(" "<>Q2$(2,1)):"00")
8020 DEF FNB$(Q1$)=Q1$(5,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(7,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(3,2)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(NUM(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8080 DEF FNH(Q$)=ASC(Q$)-32
8090 DEF FNI$(Q)=CHR(Q+32)
8094 DEF FNP$(Q1$)="19"+Q1$
8096 DEF FNU$(Q1$)=Q1$(5,4)+Q1$(3,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM "END
9902 CLOSE (SYS01_DEV,ERR=9903)
9905 REM LET NAME$=CGV("WINDOW_NAME",ERR=09100)
9915 CLOSE (PRM01_DEV,ERR=9916); LET X$=STBL("!CLEAR","PRM01_DEV",ERR=9916)
9920 LET X$=STBL("!CLEAR","WINDOW_NAME",ERR=9921)
9930 CALL "SYC.WD",NAME$
9998 EXIT 
9999 END

0010 REM "OPC - Order Processing SpeedSearch (Invoice History Line Items)"
0020 REM "Program OPC.QB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0060 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open/Lock Files"
0110 LET FILES=6; REM "WA:200
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ARM-01",FILES$[2]="ART-03",FILES$[3]="ART-13"
0140 LET FILES$[4]="ARM-10",FILES$[5]="IVM-01"
0145 LET FILES$[6]="ART-83"; REM "WA:200
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0170 LET ARM01_DEV=CHANNELS[1],ART03_DEV=CHANNELS[2],ART13_DEV=CHANNELS[3]
0180 LET ARM10_DEV=CHANNELS[4],IVM01_DEV=CHANNELS[5]
0190 LET ART83_DEV=CHANNELS[6]; REM "WA:200
0200 REM " --- IOLIST's"
0210 ARM01A: IOLIST A0$(1),A1$(1)
0220 ART03A: IOLIST B0$(1),B[ALL]
0230 ART13A: IOLIST C0$(1),C1$(1),C[ALL]
0240 IVM01A: IOLIST D0$(1),D1$(1)
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,I5$,M1$,M2$,M3$
0310 ARM10E: IOLIST Y0$(1)
0350 ART73A: IOLIST WX0$,WX1$,WX[ALL]; REM "WA:200
0360 ART83A: IOLIST WY0$,WY1$,WY2$,WY[ALL]; REM :WA:200
0400 REM " --- Parameters"
0405 DIM P[2],I[5]
0410 LET X$=STBL("!SPEEDSEARCH"),N0$=X$(1,2),MAXPAGE=NUM(X$(66,2))
0415 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0420 LET SNAME$=FNP$(X$(19,8)),SWIDTH=NUM(X$(43,2)),SHEIGHT=NUM(X$(45,2))
0425 LET MAXOPT=NUM(X$(57,2)),SYS01_DEV=NUM(STBL("!SYS01")),ONAME$=FNP$(X$(27,8))
0430 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0435 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0440 LET PAGES=INT((DSZ-5000)/4000),TNAME$=FNP$(X$(3,8))
0445 IF PAGES<3 THEN GOTO 9900
0450 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0455 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9900)IOL=ARS01A
0460 LET P[0]=NUM(P2$(1,2))
0465 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9900)IOL=IVS01A
0470 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1))
0475 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0500 REM " --- Initializations"
0505 PRECISION I[1]
0510 DIM A0$(8),A1$(30),B0$(101),B[10],C0$(52),C1$(64),C[14],Y0$(32)
0520 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),OPTLIST$[2,MAXOPT,6]
0530 DIM D0$(22),D1$(60)
0550 LET D_STATUS=0,MAXROW=HEIGHT-5,PAGE=1,PGM$=PGM(-2)
0560 LET A0$(1)=N0$+OPTION$[3],M1=LEN(M1$),M2=LEN(M2$),W=WIDTH-14-I[0]
0570 LET B0$(1)=N0$+"  "+OPTION$[3],B0$(18,3)="000",M=WIDTH-14-M1,V=M1
0590 CALL "SYC.RM",M3$,M3,M1,V
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,ONAME$
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0720 LET HEADING$(2)="Seq C WH Item",HEADING$(12+I[0])="Description"
0730 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0740 PRINT @(0,0),'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",SNAME$,0
0920 PRINT 'CF',
0930 CALL "SYC.WB",TNAME$,0
0940 PRINT @(0,1),'CL',@(0,2),'CL',@(0,3),'CL',
1000 REM " --- Process key field"
1010 GOSUB 6000
1020 FIND (ARM01_DEV,KEY=A0$,DOM=1030)IOL=ARM01A
1030 FIND (ART03_DEV,KEY=B0$(1,20),DOM=1040)IOL=ART03A
1200 REM " --- Retrieve records for display"
1210 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,SHEIGHT-2],X$(WIDTH-2)
1220 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,SELECTION$=""
1230 LET X$(1)='SB'+" Customer: "+'SF'+FNF$(A0$(3,P[0]),M0$)+" "+A1$(1,30)
1240 LET X1$=A0$(3,6),X2$=PARAM$
1250 CALL "SYC.QB",X$,X1$,X2$
1260 LET X$(1)='SB'+"  Invoice: "+'SF'+B0$(11,7)
1270 LET X1$=B0$(11,7),X2$=SUBPARAM$
1280 CALL "SYC.QB",X$,X1$,X2$
1290 READ (ART13_DEV,KEY=B0$(1,20),DOM=1400)
1400 REM " --- Read next line item"
1410 LET K$=KEY(ART13_DEV,END=1900)
1420 IF POS(B0$(1,17)=K$)<>1 THEN GOTO 1900
1430 READ (ART13_DEV)IOL=ART13A
1440 LET Y0$(1)=N0$+"E"+C0$(21,1),D0$(1)=N0$+C0$(33),D1$(1)="(Not On File)"
1450 FIND (ARM10_DEV,KEY=Y0$(1,4),DOM=1800)IOL=ARM10E
1500 REM " --- Display page?"
1510 IF L<MAXROW THEN GOTO 1600
1520 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
1530 GOSUB 5000
1540 IF DONE THEN GOTO 4000
1570 IF ANSWER$<>"" THEN GOTO 3000
1590 LET L=1,X1=1
1600 REM " --- Middle window record"
1610 DIM G1$(WIDTH-2),DESCRIPTION$(W)
1620 LET G1$(2)=C0$(18,3),G1$(6,1)=C0$(21,1),G1$(8,2)=C0$(31,2)
1630 IF POS(Y0$(25,1)="SP")>0 THEN GOTO 1660
1640 LET G1$(11)=C1$(1,40)
1650 GOTO 1690
1660 FIND (IVM01_DEV,KEY=D0$,DOM=1670)IOL=IVM01A
1670 LET DESCRIPTION$(1)=FNITEM$(D1$,I[3],I[4],I[5])
1680 LET G1$(11)=D0$(3,I[0]),G1$(12+I[0])=DESCRIPTION$
1690 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=D0$(3)
1700 REM " --- Bottom window record"
1710 DIM G2$(SWIDTH-2)
1720 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2)
1730 LET G2$(1)="    {Order Qty}"+STR(C[2]:M2$)
1740 LET G2$(M)="      {Price}"+STR(C[1]:M3$),MORE$[PAGE,X1,1]=G2$
1750 LET G2$(1)="{Backorder Qty}"+STR(C[3]:M2$)
1760 LET G2$(M)="  {Extension}"+STR(C[6]:M1$),MORE$[PAGE,X1,2]=G2$
1770 LET G2$(1)="     {Ship Qty}"+STR(C[4]:M2$)
1780 LET G2$(M)="{Order Total}"+STR(B[6]:M1$),MORE$[PAGE,X1,3]=G2$
1790 LET LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
1800 REM " --- Loop back for next order"
1810 GOSUB 15500
1890 GOTO 1400
1900 REM " --- No more orders"
1910 IF X0>0 THEN GOTO 2000
1920 DIM MESSAGE$[1]
1930 LET MESSAGE$[0]="No Line Items Found (<Enter>=Continue)"
1940 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
1960 IF V3=5 THEN GOTO 4000
1970 IF V3=6 THEN GOTO 4000
1990 GOTO 3000
2000 REM " --- Display page?"
2010 IF L<MAXROW THEN GOTO 2100
2020 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2030 GOSUB 5000
2040 IF DONE THEN GOTO 4000
2050 IF ANSWER$<>"" THEN GOTO 3000
2090 LET L=1,X1=1
2100 REM " --- Middle window record for discount, tax and freight"
2110 DIM G1$(WIDTH-2),DESCRIPTION$(W)
2120 LET G1$(11)="Discount, Tax, And Freight Amounts"
2190 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=""
2200 REM " --- Bottom window record for discount, tax and freight"
2210 DIM G2$(SWIDTH-2)
2220 LET G2$(1)="  {Discount}"+STR(-B[2]:M1$),MORE$[PAGE,X1,1]=G2$
2230 LET G2$(1)="{Tax Amount}"+STR(B[0]:M1$),MORE$[PAGE,X1,2]=G2$
2240 LET G2$(1)="   {Freight}"+STR(B[1]:M1$),V=(B[6]-B[2]+B[0]+B[1])*1
2245 LET G2$(M)="   {Net Sale}"+STR(V:M1$),MORE$[PAGE,X1,3]=G2$
2280 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),LLIST$[PAGE,X1,5]=SNAME$
2290 LET X1=X1+1,L=L+1
2500 REM " --- Display last page"
2510 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2520 GOSUB 5000
2530 IF DONE THEN GOTO 4000
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$=B0$(5,13),CHOICE2$="",D0$(3)=ANSWER$
3030 GOSUB 5000
3040 IF CONTROL=1 THEN GOTO 1000
3050 IF FNP$(ANSWER$)<>FNP$(CHOICE1$) THEN IF DONE=0 THEN LET OPTION$[3]=D0$(3)
3090 GOTO 4000
3800 REM " --- Exit"
3810 LET OPTION$[1]="EXIT"
3890 GOTO 4000
3900 REM " --- Startover"
3910 LET OPTION$[1]="STARTOVER"
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
5190 RETURN 
5200 REM " --- Display Customer Address"
5205 DISPLAY_ADDRESS: 
5210 LET WIDTH_IN=24,LINES_IN=3,ZIP_LEN=9,WIDTH_OUT=24,ADDRESS$=X1$(31,WIDTH_IN*LINES_IN+ZIP_LEN)
5220 CALL "SYC.AA",ADDRESS$,WIDTH_IN,LINES_IN,ZIP_LEN,WIDTH_OUT
5230 PRINT @(5,15),X1$(1,30),@(5,16),ADDRESS$(1,WIDTH_OUT),@(5,17),ADDRESS$(1+WIDTH_OUT,WIDTH_OUT),@(5,18),ADDRESS$(1+WIDTH_OUT*2,WIDTH_OUT)+" ",FNP$(ADDRESS$(1+WIDTH_OUT*3))
5240 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CS',
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9950 EXIT 
9999 END
10000 REM " --- Look for serial numbers
10005 RETURN 
15400 REM " --- configuration detail
15405 FIND (ART73_DEV,KEY=C0$(1,20),DOM=15900)IOL=ART73A
15410 IF L+1>L9 THEN GOSUB 5800
15415 PRINT (PRTR_DEV)@(H0+20),"Type: ",WX1$(4,3),
15420 IF WX[1]<>0 THEN PRINT (PRTR_DEV)@(H0+30),"Media: ",WX1$(1,3),
15430 IF WX[1]<>0 THEN PRINT (PRTR_DEV)"  Sets: ",WX[1]:"#0",
15435 PRINT (PRTR_DEV)'LF',
15440 LET L=L+1
15445 IF L+1>L9 THEN GOSUB 5800
15455 PRINT (PRTR_DEV)@(H0+20),"Number of users: ",WX[2],
15460 PRINT (PRTR_DEV)'LF',
15465 LET L=L+1
15500 REM " --- serial number detail"
15505 RETURN 
15510 LET ACT$="",DEACT$="",MOD$=""
15520 READ (ART83_DEV,KEY=C0$(1,20),DOM=15521)
15530 LET ART83_K$=KEY(ART83_DEV,END=15600)
15540 IF POS(C0$(1,20)=ART83_K$)<>1 THEN GOTO 15600
15550 READ (ART83_DEV,KEY=ART83_K$)IOL=ART83A
15560 IF WY1$(1,1)="M" THEN LET MOD$=MOD$+WY2$
15570 IF WY1$(1,1)="A" THEN LET ACT$=ACT$+WY2$
15580 IF WY1$(1,1)="D" THEN LET DEACT$=DEACT$+WY2$
15590 GOTO 15530
15600 REM " --- print them
15610 LET STR$=MOD$,TITLE$="Serial Numbers: "
15620 GOSUB 15700
15630 LET STR$=ACT$,TITLE$="New Serial Numbers: "
15640 GOSUB 15700
15650 LET STR$=DEACT$,TITLE$="Deactivated Serial Numbers: "
15660 GOSUB 15700
15670 GOTO 15900
15700 REM " --- print them
15705 IF STR$="" THEN GOTO 15790
15710 LET SLINES=INT(LEN(STR$)/40)+1
15715 LET SNUMBS=INT(LEN(STR$)/20)
15720 IF L+SLINES+1>L9 THEN GOSUB 5800
15730 PRINT (PRTR_DEV)@(H0+17),TITLE$; LET L=L+1
15740 PRINT (PRTR_DEV)@(H0+20),
15750 FOR SL=1 TO SNUMBS
15760 PRINT (PRTR_DEV)STR$((SL-1)*20+1,20),"  ",
15770 IF MOD(SL,2)=0 THEN PRINT (PRTR_DEV)'LF',@(H0+20),; LET L=L+1
15780 NEXT SL
15785 IF MOD(SNUMBS,2)<>0 THEN PRINT (PRTR_DEV)'LF',; LET L=L+1
15790 RETURN 
15900 REM " --- Done"
15990 RETURN 
46500 END

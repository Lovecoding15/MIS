0010 REM "IVM - Item Maintenance (Menu Processing)"
0020 REM "Program IVM.MA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "26Aug96 WGH: For new items, default Barcode to Item Number (1540)"
0032 REM "08Oct96 WGH: GL Accounts incorrectly initialized for new items (1520,1550-1565)"
0033 REM "11Dec96 Caj: Error 36 @ 6670 (6670)
0034 REM "MAS * DCF * WA# 205
0040 REM 
0050 SETERR 9000
0055 SETESC 9000
0060 ON O0 GOTO 0070,2000,1000,2000,2000
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=49
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0120 LET FILES$[4]="IVM-03",FILES$[5]="IVM-09",FILES$[6]="IVM-10"
0125 LET FILES$[7]="IVM-12",FILES$[8]="IVM-13",FILES$[9]="IVM-14"
0130 LET FILES$[10]="IVM-15",FILES$[11]="IVM-24",FILES$[12]="IVS-10"
0140 LET FILES$[13]="IVT-04"
0150 CALL "SYC.DA",1,1,13,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0165 LET IVM03_DEV=CHANNELS[4],IVM09_DEV=CHANNELS[5],IVM10_DEV=CHANNELS[6]
0170 LET IVM12_DEV=CHANNELS[7],IVM13_DEV=CHANNELS[8],IVM14_DEV=CHANNELS[9]
0175 LET IVM15_DEV=CHANNELS[10],IVM24_DEV=CHANNELS[11],IVS10_DEV=CHANNELS[12]
0180 LET IVT04_DEV=CHANNELS[13]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$(1),A6$(1),A[ALL]; REM WA# 205
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0275 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$
0280 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$
0285 GLS01A: IOLIST G0$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0290 IVS10D: IOLIST I5$,I6$(1),I7$(1)
0295 IVS10N: IOLIST Y0$,Y[ALL]
0300 IVM10A: IOLIST X0$(1)
0310 WOS01A: IOLIST WO0$,WO1$,WO2$,WO3$,WO4$,WO5$,WOM1$,WOM2$,WOM3$
0400 REM " --- Parameters"
0405 DIM P[8],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="IV",LF$="N",LS$="N",MF$="N",MAXROW=15
0420 LET AR$="N",AP$="N",BM$="N",GL$="N",OP$="N",PO$="N",WO$="N",DI$="N"
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 LET G[0]=NUM(G2$(1,2)),G[3]=NUM(G2$(7,2)),G[4]=NUM(G2$(9,2))
0435 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0437 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1)),P[2]=NUM(I2$(7,2))
0440 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0443 LET P[6]=NUM(I4$(1,2)),P[7]=FNYY_YEAR(I4$(3,2)),P[8]=NUM(I2$(3,2))
0445 IF POS("Y"=I2$(51,3))>0 THEN LET MF$="Y"
0450 IF POS(I3$(3,1)="LF")>0 THEN LET LF$="Y"
0455 IF POS(I3$(17,1)="LS")>0 THEN LET LS$="Y"
0460 GOSUB 6000
0465 IF AR$="N" THEN GOTO 0480
0470 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0475 LET CUSTLEN=NUM(P2$(1,2)),CUSTMASK$=M0$,DI$=P3$(11,1)
0480 IF GL$="N" THEN LET DI$="N"
0483 IF AP$="N" THEN GOTO 0493
0485 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0490 LET VENDLEN=NUM(P2$(1,2)),VENDMASK$=M0$
0493 IF WO$="N" THEN GOTO 0500
0495 FIND (SYS01_DEV,KEY=N0$+"WO00",DOM=9800)IOL=WOS01A
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(80," "),DP[3]
0515 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A6$(9*G[4]),A[12],Y[1]
0516 DIM A5$(2); REM WA# 205
0520 DIM OPTLIST$[2,MAXROW,6],PARAM$[2,MAXROW,3]
0530 LET L0=6,M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),M4=LEN(M4$),M5=LEN(M5$)
0540 LET WOM1=LEN(WOM1$),WOM2=LEN(WOM2$),WOM3=LEN(WOM3$)
0545 LET A0$(1,2)=N0$,G5=LEN(G5$),DP[1]=14,MONTHS$=G3$(235,G[0]*3)
0550 IF P[4]>0 THEN LET DP[2]=DP[1]+P[3]+1
0555 IF P[5]>0 THEN LET DP[3]=DP[1]+P[3]+1+P[4]+1
0560 IF LF$="Y" THEN LET FILES$[14]="IVM-04"
0565 IF LS$="N" THEN GOTO 0580
0570 LET FILES$[15]="IVM-07",FILES$[16]="IVM-08",FILES$[17]="IVM-17"
0575 LET FILES$[18]="IVT-01",FILES$[19]="IVT-02",FILES$[20]="IVT-03"
0580 IF MF$="N" THEN GOTO 0590
0583 IF I2$(51,1)="Y" THEN LET FILES$[21]="IVM-11"
0585 IF I2$(52,1)="Y" THEN LET FILES$[22]="IVM-21"
0587 IF I2$(53,1)="Y" THEN LET FILES$[23]="IVM-31"
0590 IF AR$="Y" THEN LET FILES$[24]="ARM-01",FILES$[25]="ARM-10"
0595 IF AP$="Y" THEN LET FILES$[26]="IVM-05",FILES$[27]="APM-01"
0600 IF BM$="N" THEN GOTO 0610
0605 LET FILES$[28]="BMM-01",FILES$[29]="BMM-02",FILES$[30]="BMM-04"
0610 IF GL$="Y" THEN LET FILES$[31]="GLM-01"
0615 IF OP$="N" THEN GOTO 0625
0620 LET FILES$[32]="ARE-03",FILES$[33]="ARE-07",FILES$[34]="ARE-13"
0625 IF PO$="N" THEN GOTO 0645
0630 LET FILES$[35]="POE-01",FILES$[36]="POE-02",FILES$[37]="POE-11"
0633 LET FILES$[38]="POE-12",FILES$[39]="POE-41",FILES$[40]="POE-42"
0635 LET FILES$[41]="POM-02",FILES$[42]="POT-04",FILES$[43]="POT-14"
0640 LET FILES$[44]="POT-24",FILES$[45]="POT-34"
0645 IF WO$="N" THEN GOTO 0660
0650 LET FILES$[46]="WOE-01",FILES$[47]="WOE-03"
0655 LET FILES$[48]="WOE-22",FILES$[49]="WOT-02"
0680 REM " --- Other file opens"
0685 CALL "SYC.DA",1,14,49,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0690 IF STATUS>0 THEN GOTO 9900
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0720 PRINT 'SB',@(1,3),"Item Number:",@(1,4),"Description:",'SF',@(0,L0),'CE',
0800 REM " --- Assign remaining channel numbers"
0805 LET IVM04_DEV=CHANNELS[14],IVM07_DEV=CHANNELS[15],IVM08_DEV=CHANNELS[16]
0810 LET IVM17_DEV=CHANNELS[17],IVT01_DEV=CHANNELS[18],IVT02_DEV=CHANNELS[19]
0815 LET IVT03_DEV=CHANNELS[20],IVM11_DEV=CHANNELS[21],IVM21_DEV=CHANNELS[22]
0820 LET IVM31_DEV=CHANNELS[23],ARM01_DEV=CHANNELS[24],ARM10_DEV=CHANNELS[25]
0825 LET IVM05_DEV=CHANNELS[26],APM01_DEV=CHANNELS[27],BMM01_DEV=CHANNELS[28]
0830 LET BMM02_DEV=CHANNELS[29],BMM04_DEV=CHANNELS[30],GLM01_DEV=CHANNELS[31]
0835 LET ARE03_DEV=CHANNELS[32],ARE07_DEV=CHANNELS[33],ARE13_DEV=CHANNELS[34]
0840 LET POE01_DEV=CHANNELS[35],POE02_DEV=CHANNELS[36],POE11_DEV=CHANNELS[37]
0845 LET POE12_DEV=CHANNELS[38],POE41_DEV=CHANNELS[39],POE42_DEV=CHANNELS[40]
0850 LET POM02_DEV=CHANNELS[41],POT04_DEV=CHANNELS[42],POT14_DEV=CHANNELS[43]
0855 LET POT24_DEV=CHANNELS[44],POT34_DEV=CHANNELS[45],WOE01_DEV=CHANNELS[46]
0860 LET WOE03_DEV=CHANNELS[47],WOE22_DEV=CHANNELS[48],WOT02_DEV=CHANNELS[49]
0900 REM " --- Position File"
0990 READ (IVM01_DEV,KEY=A0$,DOM=1000)
1000 REM " --- Item number"
1010 PRINT 'CF',@(14,4),'CL'
1015 LET V2$="",V2$=KEY(IVM01_DEV,END=1030)
1020 IF POS(A0$(1,2)=V2$)<>1 THEN LET V2$=""
1025 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1030 LET V0$="S",V1$="KCE",V3$="",V4$="",V0=P[0],V1=14,V2=3,O0=1
1035 IF I3$(9,1)<>"N" THEN LET V4$="<F1>=Auto Number/"
1040 LET V4$="Enter A Valid Item Number ("+V4$+"<F3>=Lookup)"
1045 GOSUB 7000
1050 ON V3 GOTO 1080,1150,1000,1055,9900
1055 CALL "SYC.LI",V$
1060 IF V$="" THEN GOTO 1000
1080 IF V$="" THEN GOTO 0900
1090 GOTO 1200
1150 REM " --- Auto Generate Item Number"
1160 IF I3$(9,1)="N" THEN GOTO 1000
1170 GOSUB 6500
1180 IF STATUS>0 THEN GOTO 1000
1190 PRINT @(V1,V2),V$,
1200 REM " --- Find record"
1210 LET A0$(3)=V$
1220 READ (IVM01_DEV,KEY=A0$,DOM=1400)IOL=IVM01A
1300 REM " --- Display record"
1320 GOSUB 5000
1340 GOTO 2000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,1000
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Initializations for new record"
1510 DIM A1$(60),A2$(64),A3$(40),A4$(21),A6$(9*G[4]),A[12]
1515 DIM A5$(2); REM WA# 205
1516 LET A5$(1,1)="N",a5$(2,1)="Y"; REM WA# 205
1520 DIM I6$(32),I7$(6*G[4]),X0$(32)
1530 LET A2$(4,5)="EAEAY",A4$(1)="N",A[0]=1,A2$(24,5)="IADDD",A2$(19,2)="NN"
1540 LET A3$(21)=A0$(3),A[0]=1,O0=4
1550 FIND (IVS10_DEV,KEY=N0$+"D",DOM=1555)IOL=IVS10D
1555 LET A2$(1,11)=I6$(1,11),A2$(19,2)=I6$(12,2),A2$(21,3)=I6$(14,3)
1560 LET A2$(24,5)=I6$(17,5),A2$(53,3)=I6$(25,3)
1565 LET A6$(1,10)=I7$(1,10),A6$(11,10)=I7$(11,10),A6$(21,10)=I7$(51,10),A6$(31,10)=I7$(41,10),A6$(41,10)=I7$(21,10),A6$(51,10)=I7$(31,10)
1570 FIND (IVM10_DEV,KEY=N0$+"A"+A2$(1,3),DOM=1580)IOL=IVM10A
1575 LET A2$(18,1)=X0$(27,1)
1580 GOSUB 5000
2000 REM " --- Branch to appropriate submenu or subtask"
2080 ON O0 GOTO 0070,2090,1000,2200,2100
2090 ON MENU GOTO 4000,4200,4300,4000
2100 REM " --- New Item: Load submenu"
2110 LET X$="IVMNEW",V$="",V3$="",V0=0,V1=0,V2=0,SUBTASKS=0,SUBTASK=0,O0=3
2150 CALL "SYC.OL",X$,SUBTASKS,V0,OPTLIST$[ALL],PARAM$[ALL],V$,V1,V3$,V2
2200 REM " --- New item: Run next subtask"
2210 LET SUBTASK=SUBTASK+1
2220 IF SUBTASK>SUBTASKS THEN GOTO 4000
2230 LET DESCRIPTION$=FNS$(OPTLIST$[1,SUBTASK,1])
2240 LET SELECTION$=OPTLIST$[1,SUBTASK,2]
2250 GOSUB 5400
2290 RUN SELECTION$,ERR=2200
4000 REM " --- Main submenu"
4010 DIM MENUS$[5]
4015 LET MENUS=1,MENUS$[MENUS]="IVM.MA",WIN_X=40,WIN_Y=L0+1,MENU=0
4020 IF AP$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="IVM.MF"
4025 IF DI$="N" THEN IF GL$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="IVM.MY"
4030 LET MENUS=MENUS+1,MENUS$[MENUS]="IVMSTA"
4040 LET MENUS=MENUS+1,MENUS$[MENUS]="IVMHIS"
4050 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4055 IF SELECTION$="S" THEN GOTO 4200
4060 IF SELECTION$="H" THEN GOTO 4300
4070 GOSUB 5400
4075 IF SELECTION$="" THEN GOTO 1000
4080 IF SELECTION$="SYC.KA" THEN GOTO 4100
4090 RUN SELECTION$,ERR=4000
4100 REM " --- Comments"
4110 DIM INFO$[5],INFO[5]
4120 LET X$="Item "+A0$(3,P[0])+" "+FNITEM$(A1$,P[3],P[4],P[5])
4130 LET INFO$[0]="IVM-09",INFO$[1]=A0$,INFO$[2]=X$,ACTION=0,INFO[1]=L0
4150 CALL "SYC.KA",ACTION,INFO$[ALL],INFO[ALL]
4190 GOTO 4000
4200 REM " --- Status inquiries submenu"
4210 DIM MENUS$[9]
4215 LET MENUS=1,MENUS$[MENUS]="IVM.MD",WIN_X=40,WIN_Y=L0+1,MENU=1
4220 IF LF$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="IVM.MT"
4225 IF LS$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="IVM.MN"
4230 IF OP$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="IVM.MM"
4235 IF PO$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="IVM.MG"
4240 IF WO$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="IVM.MJ"
4243 IF BM$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="IVM.MW"
4245 LET MENUS=MENUS+1,MENUS$[MENUS]="IVMMNT"
4250 LET MENUS=MENUS+1,MENUS$[MENUS]="IVMHIS"
4255 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4260 IF SELECTION$="M" THEN GOTO 4000
4265 IF SELECTION$="H" THEN GOTO 4300
4270 GOSUB 5400
4280 IF SELECTION$="" THEN GOTO 1000
4290 RUN SELECTION$,ERR=4200
4300 REM " --- Status inquiries submenu"
4310 DIM MENUS$[6]
4315 LET MENUS=1,MENUS$[MENUS]="IVM.MU",WIN_X=40,WIN_Y=L0+1,MENU=2
4320 IF LS$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="IVM.MO"
4325 IF PO$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="IVM.MI"
4330 IF WO$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="IVM.MK"
4335 LET MENUS=MENUS+1,MENUS$[MENUS]="IVMMNT"
4340 LET MENUS=MENUS+1,MENUS$[MENUS]="IVMSTA"
4355 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4360 IF SELECTION$="M" THEN GOTO 4000
4365 IF SELECTION$="S" THEN GOTO 4200
4370 GOSUB 5400
4380 IF SELECTION$="" THEN GOTO 1000
4390 RUN SELECTION$,ERR=4300
5000 REM " --- Display"
5010 PRINT 'SB',@(14,3),A0$(3,P[0]),'SF',
5015 LET V=1
5020 FOR X=3 TO 5
5030 IF P[X]=0 THEN GOTO 5080
5040 PRINT 'SB',@(DP[X-2],4),A1$(V,P[X]),'SF',
5050 LET V=V+P[X]
5080 NEXT X
5090 RETURN 
5400 REM " --- Clear bottom"
5410 CALL "SYC.WC",1,0,80,0,0,L0-1,0
5420 PRINT @(0,L0),'CE',
5430 IF SELECTION$<>"" THEN PRINT 'SB',@(8,L0-1)," ",DESCRIPTION$," ",'SF',
5500 REM " --- Window initializations for overlays"
5510 LET WIN_X=0,WIN_Y=5,HEIGHT=19,WIDTH=80,PAGES=MIN(INT((DSZ-5000)/4000),20)
5520 LET TITLE$=DESCRIPTION$,MAXROW=HEIGHT-5,H0=WIDTH-1
5530 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
5590 RETURN 
6000 REM " --- What other applications are installed?"
6010 CALL "SYC.VA","AR",INFO$[ALL]
6020 LET AR$=INFO$[20]
6030 CALL "SYC.VA","AP",INFO$[ALL]
6040 LET AP$=INFO$[20]
6050 CALL "SYC.VA","BM",INFO$[ALL]
6060 LET BM$=INFO$[20]
6070 CALL "SYC.VA","GL",INFO$[ALL]
6080 LET GL$=INFO$[20]
6090 CALL "SYC.VA","OP",INFO$[ALL]
6100 LET OP$=INFO$[20]
6110 CALL "SYC.VA","PO",INFO$[ALL]
6120 LET PO$=INFO$[20]
6130 CALL "SYC.VA","WO",INFO$[ALL]
6140 LET WO$=INFO$[20]
6150 CALL "SYC.VA","SA",INFO$[ALL]
6160 LET SA$=INFO$[20]
6170 IF GL$<>"Y" THEN GOTO 6190
6180 CALL "SYC.VA",N2$,INFO$[ALL]
6185 LET GL$=INFO$[9]
6190 RETURN 
6500 REM " -- Generate next item number"
6505 LET STATUS=1
6510 CALL "SYC.YN",1,"Do You Wish To Generate A New Item Number",2,V$,V3
6520 IF V$<>"Y" THEN GOTO 6690
6530 LET Y[0]=1,X=P[0],X$=""
6535 IF I3$(9,1)="C" THEN LET X=P[0]-1
6540 EXTRACT (IVS10_DEV,KEY=N0$+"N",DOM=6550)IOL=IVS10N
6550 DIM J9$(MIN(X,10),"9")
6560 IF Y[0]>NUM(J9$) THEN GOTO 6650
6570 LET NUMBER=Y[0],Y[0]=Y[0]+1
6580 WRITE (IVS10_DEV,KEY=Y0$)IOL=IVS10N
6590 IF I3$(9,1)<>"C" THEN GOTO 6635
6600 PRECISION 4
6605 LET X$=STR(TIM*10000),X$=X$(LEN(X$),1)
6630 PRECISION P[1]
6635 DIM V$(P[0])
6640 LET V$(1)=STR(NUMBER)+X$,STATUS=0
6645 GOTO 6690
6650 DIM MESSAGE$[1]
6660 LET MESSAGE$[0]="No More Automatic Item Numbers Are Available (<Enter>=Continue)"
6670 CALL "SYC.XA",2,MESSAGE$[ALL],0,-1,-1,V$,V3
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNS$(Q$)=CVS(Q$,3)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "GLR - Financial Statements"
0020 REM "Program GLR.FA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.6 - 15Aug99 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "02Aug99 WGH: Display 21st century dates correctly (2210)
0040 REM 
0080 SETESC 9000
0085 SETERR 9000
0090 ON O0 GOTO 0100,1000
0100 REM " --- Open/Lock Files"
0110 IF GLE05_DEV THEN LET GLE04_DEV=GLE05_DEV; REM "13 Period Financial Reporting
0160 LET PRTR$=""; CALL "BPS.01",3,PRTR$,STATUS; IF STATUS THEN GOTO 9900
0170 CALL "SYC.GA",7,1,"",PRTR$,STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLIST's"
0250 GLE04A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0260 GLS01C: IOLIST X0$(1)
0280 GLM12A: IOLIST A0$(1),M1$,M2$,M3$,M4$
0290 GLM32A: IOLIST R0$,R1$(1)
0310 GLM13A: IOLIST H0$(1),H1$(1)
0400 REM " --- Parameters"
0410 DIM X0$(12)
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"02",DOM=9800)IOL=GLS01C
0430 LET PERIOD=NUM(X0$(7,2)),YEAR=FNYY_YEAR(BEGYEAR$),SEQUENCE$=X0$(FLAGS,1)
0440 LET X=NUM(X0$(FLAGS+2,1))+1
0450 IF X>9 THEN LET X=0
0460 LET X0$(FLAGS+2,1)=STR(X),PRINTFLAG$=X0$(FLAGS+2,1)
0470 WRITE (SYS01_DEV,KEY=X0$(1,6))IOL=GLS01C
0500 REM " --- Initializations"
0510 LET HW=132,HEADMAX=5,HEADINGS=0,COLMAX=8
0520 DIM A0$(30),B0$(4),B1$(16),B[2],H0$(7),H1$(HW),R1$(7)
0550 LET L9=59,L=L9+1,H0=132,CLOCK$="",WHEN$=F0$(7,3)
0560 LET S_HEAD$="|"; REM "sdOffice"
0700 REM " --- Background"
0750 CALL "SYC.NB","Printing",28,COLUMN
0800 REM " --- Determine starting/ending dates for report period"
0810 CALL "SYC.PA",SYS01_DEV,PERIOD,YEAR,BEGDATE$,ENDDATE$,STATUS
0900 REM " --- Position File"
0990 READ (GLE04_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next GLE-04 record"
1010 READ (GLE04_DEV,END=9900)IOL=GLE04A
1100 REM " --- Retrieve GLM-12 Report Master"
1110 DIM A0$(30)
1120 LET A0$(1,4)=B0$(1,4),A0$(5,4)="0000"
1130 FIND (GLM12_DEV,KEY=A0$(1,8),DOM=1000)IOL=GLM12A
1140 IF A0$(1,2)<>N0$ THEN GOTO 9900
1150 PRINT @(COLUMN,11),A0$(3,2)," ",A0$(9,20)," ",A0$(5,4),
1200 REM " --- Retrieve GLM-13 Heading Info"
1210 DIM H0$(7),H1$(HW),RPT_HEAD$(HW*HEADMAX)
1220 LET H0$(1)=A0$(1,2)+A0$(29,2)
1230 READ (GLM13_DEV,KEY=H0$,DOM=1240)
1240 LET K13$=KEY(GLM13_DEV,END=1500)
1250 IF K13$(1,4)<>A0$(1,2)+A0$(29,2) THEN GOTO 1500
1260 IF NUM(H0$(5,3),ERR=1290)>=HEADMAX THEN GOTO 1500
1270 READ (GLM13_DEV,KEY=K13$)IOL=GLM13A
1280 LET RPT_HEAD$(NUM(H0$(5,3))*HW+1,HW)=H1$
1290 GOTO 1240
1500 REM " --- Standard heading elements"
1510 LET CURYEAR$=STR(1900+YEAR),PRIYEAR$=STR(1900+YEAR-1)
1520 LET PRIBEG$=FNB$(BEGDATE$),PRIBEG$(7,2)=PRIYEAR$(3)
1530 LET PRIEND$=FNB$(ENDDATE$),PRIEND$(7,2)=PRIYEAR$(3)
1540 LET PERIOD$=FNP$(P3$((PERIOD-1)*16+27,16)),QUARTER$="Fourth"
1550 IF PERIOD<10 THEN LET QUARTER$="Third"
1560 IF PERIOD<7 THEN LET QUARTER$="Second"
1570 IF PERIOD<4 THEN LET QUARTER$="First"
2000 REM " --- Replace tokens in heading with standard elements"
2010 LET TOKEN$="[C-YEAR]",ELEMENT$=CURYEAR$,TYPE=0
2020 GOSUB 6400
2030 LET TOKEN$="[P-YEAR]",ELEMENT$=PRIYEAR$,TYPE=0
2040 GOSUB 6400
2050 LET TOKEN$="[MONTH]",ELEMENT$=PERIOD$,TYPE=1
2060 GOSUB 6400
2070 LET TOKEN$="[QUARTER]",ELEMENT$=QUARTER$,TYPE=1
2080 GOSUB 6400
2090 LET TOKEN$="[C-PERIOD]",ELEMENT$="Period "+STR(PERIOD)+" From "+FNB$(BEGDATE$)+" To "+FNB$(ENDDATE$),TYPE=1
2100 GOSUB 6400
2110 LET TOKEN$="[P-PERIOD]",ELEMENT$="Period "+STR(PERIOD)+" From "+PRIBEG$+" To "+PRIEND$,TYPE=1
2120 GOSUB 6400
2130 LET TOKEN$="[C-END]",ELEMENT$="Period Ending "+FNB$(ENDDATE$),TYPE=1
2140 GOSUB 6400
2150 LET TOKEN$="[P-END]",ELEMENT$="Period Ending "+PRIEND$,TYPE=1
2160 GOSUB 6400
2170 LET TOKEN$="[FIRM]",ELEMENT$=N1$,TYPE=1,S_FIRM$=ELEMENT$; REM "sdOffice"
2180 GOSUB 6400
2190 LET TOKEN$="[R-DATE]",ELEMENT$="Run Date "+FNB$(F0$(7,3)),TYPE=0
2200 GOSUB 6400
2210 LET TOKEN$="[Y-END]",V$=FNB$(ENDDATE$),ELEMENT$=PERIOD$+" "+V$(4,2)+", "+STR(1900+FNYY_YEAR(V$(7,2))),TYPE=1,S_DATE$="Period Ending "+ELEMENT$; REM "sdOffice"
2220 GOSUB 6400
2900 REM " --- Store heading information"
2910 DIM HEADINGS$[HEADMAX-1]
2920 FOR X=0 TO HEADMAX-1
2930 LET HEADINGS$[X]=RPT_HEAD$(X*HW+1,HW)
2940 IF FNP$(HEADINGS$[X])<>"" THEN LET HEADINGS=X
2950 NEXT X
3000 REM " --- Extract column positions"
3010 DIM A[COLMAX-1,3]
3020 LET X=0
3030 READ (GLM32_DEV,KEY=B0$,DOM=3040)
3040 LET K32$=KEY(GLM32_DEV,END=3800)
3050 IF K32$(1,4)<>B0$ OR X>=COLMAX THEN GOTO 3800
3060 READ (GLM32_DEV,KEY=K32$)IOL=GLM32A
3100 LET W0$=R1$
3120 LET W0=POS(W0$(1,1)="PQYABC")
3140 IF W0<>0 THEN GOTO 3170
3150 LET A[X,0]=9
3160 GOTO 3290
3170 LET A[X,0]=W0-1
3180 LET W0=POS(W0$(2,1)="ABV")
3190 IF W0=0 THEN LET W0=1
3200 LET A[X,1]=W0-1
3220 LET W0=POS(W0$(3,1)="AU")
3230 IF W0=0 THEN LET W0=1
3240 LET A[X,2]=W0-1
3260 LET A[X,3]=NUM(W0$(5,3))
3290 LET X=X+1
3300 GOTO 3040
4000 REM " --- Run report overlay"
4010 LET L=L9+1
4090 RUN REPORT$,ERR=9900
6400 REM " --- Replace heading token with standard element"
6410 LET P=POS(TOKEN$=RPT_HEAD$),LT=LEN(TOKEN$),LE=LEN(ELEMENT$)
6420 IF P=0 THEN GOTO 6590
6430 IF TYPE>0 THEN GOTO 6500
6440 LET RPT_HEAD$(P,LT)="",RPT_HEAD$(P,LE)=ELEMENT$
6490 GOTO 6400
6500 REM 
6510 IF P+INT((LT-LE)/2)<0 THEN GOTO 6440
6520 LET RPT_HEAD$(P,LT)="",L=LE-LT
6530 IF L>0 THEN LET P=MAX(P-INT(L/2),1)
6540 IF L<0 THEN LET P=P+INT(ABS(L)/2)
6570 LET RPT_HEAD$(P,LE)=ELEMENT$
6580 GOTO 6400
6590 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- End"
9910 CALL "sdofc_e.bb","leaveopen",RESP$,ERR$; REM "sdOffice"
9990 RUN "SYS.AA"
9999 END

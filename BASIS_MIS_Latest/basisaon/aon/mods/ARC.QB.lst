0010 REM "ARC - Customer SpeedSearch (Aging & Sales)"
0020 REM "Program ARC.QB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ARM-01",FILES$[2]="ARM-02"
0140 LET FILES$[3]="ARM-06",FILES$[4]="ARM-10"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET ARM01_DEV=CHANNELS[1],ARM02_DEV=CHANNELS[2]
0180 LET ARM06_DEV=CHANNELS[3],ARM10_DEV=CHANNELS[4]
0200 REM " --- IOLIST's"
0210 ARM01A: IOLIST A0$(1),A1$(1)
0220 ARM02A: IOLIST B0$(1),B1$(1),B[ALL]
0230 ARM06A: IOLIST C0$,C1$,C[ALL]
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0310 ARM10A: IOLIST XA$(1)
0320 ARM10B: IOLIST XB$(1)
0330 ARM10D: IOLIST XD$(1)
0340 ARM10F: IOLIST XF$(1)
0350 ARM10H: IOLIST XH$(1)
0360 ARM10I: IOLIST XM$(1)
0400 REM " --- Parameters"
0410 DIM P[1],X$(128)
0420 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2)
0425 LET TNAME$=FNP$(X$(3,8)),TWIDTH=NUM(X$(35,2)),THEIGHT=NUM(X$(37,2))
0430 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0435 LET SNAME$=FNP$(X$(19,8)),SWIDTH=NUM(X$(43,2)),SHEIGHT=NUM(X$(45,2))
0440 LET ONAME$=FNP$(X$(27,8)),OWIDTH=NUM(X$(83,2)),OHEIGHT=NUM(X$(85,2))
0445 LET MAXOPT=NUM(X$(57,2)),SYS01_DEV=NUM(STBL("!SYS01"))
0450 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0455 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0480 FIND (SYS01_DEV,KEY=N0$+"AR"+"00",DOM=9900)IOL=ARS01A
0490 LET P[0]=NUM(P2$(1,2))
0500 REM " --- Initializations"
0510 DIM A0$(8),OPTLIST$[2,MAXOPT,6],HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0540 LET D_STATUS=0,MAX_ROW=HEIGHT-4,PAGE=1,PAGES=2,M1=LEN(M1$),P2=18
0550 LET A0$(1)=N0$+OPTION$[3],P1=WIDTH-2-M1,W=(P1-9)-P2,NF$="(Not On File)"
0560 LET M9$="(###)-###-####",PGM$=PGM(-2),OPTION=0
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,ONAME$
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0730 LET HEADING$(FNC(TITLE$,WIDTH-2))=TITLE$
0740 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-10)="F4=End"
0750 PRINT 'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER',@(1,1),"Salesperson",@(P1-7),"Future",@(3,2),"Territory",@(P1-8),"Current",@(2,3),"Terms Code",@(P1-8),"30 Days",@(4,4),"Tax Code",@(P1-8),"60 Days",@(0,5),"Distribution",@(P1-8),"90 Days",@(0,6),"    Discount",@(P1-9),"120 Days",@(0,7),"Credit Limit",@(P1-8),"Balance",@(0,8),"Last Invoice",@(P1-10),"MTD Sales",@(0,9),"Last Payment",@(P1-10),"YTD Sales",'SF',
0780 CALL "SYC.WB",SNAME$,0
0790 PRINT 'SB',@(1,0),"Contact",@(WIDTH-18),"Opened",@(3,1),"Phone",@(WIDTH-32),"SIC",@(WIDTH-16),"Type",@(5,2),"Fax",@(WIDTH-23),"Credit Hold",'SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",SNAME$,0
0920 PRINT 'CF',
0930 CALL "SYC.WB",TNAME$,0
0940 PRINT @(0,1),'CL',@(0,2),'CL',@(0,3),'CL',
0990 GOTO 1100
1000 REM " --- Process key field"
1005 LET C$='CF'
1010 GOSUB 6000
1015 CALL "SYC.WB",TNAME$,0
1030 PRINT @(1,0),'SB',"Customer:",'SF',@(0,1),'CL',@(0,2),'CL',
1035 LET V2$="",V2$=KEY(ARM01_DEV,END=1050)
1040 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1045 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1050 LET V0$="M",V1$="KE",V3$=M0$,V0=6,V1=11,V2=0,V4$=""
1055 PRINT @(V1+P[0]+1,V2),'CL',
1060 GOSUB 7000
1065 IF V3=5 THEN LET OPTION$[1]="STARTOVER"; GOTO 4000
1070 IF V3=4 OR V3=6 THEN LET OPTION$[1]="EXIT"; GOTO 4000
1080 IF V3=3 THEN CALL "SYC.LC",V$
1090 IF V$="" THEN GOTO 0900
1095 LET A0$(3)=V$
1100 REM " --- Retrieve customer"
1110 DIM X$(WIDTH-2),A1$(320)
1120 READ (ARM01_DEV,KEY=A0$,DOM=1000)IOL=ARM01A
1130 LET X$(1)='SB'+" Customer: "+'SF'+FNF$(A0$(3,P[0]),M0$)+" "+A1$(1,30)
1140 LET X1$=A0$(3,6),X2$=PARAM$
1150 CALL "SYC.QB",X$,X1$,X2$
1200 REM " --- Retrieve records for display"
1210 DIM B0$(10),B1$(64),B[10],C0$(8),C1$(16),C[21]
1220 DIM XA$(32),XB$(32),XD$(32),XF$(32),XH$(32),XM$(32)
1230 LET XA$(6)=NF$,XB$(6)=NF$,XD$(6)=NF$,XF$(7)=NF$
1240 LET XH$(7)=NF$,XM$(6)=NF$,B0$(1)=A0$,C0$(1)=A0$,HOLD$="No"
1250 FIND (ARM02_DEV,KEY=B0$,DOM=1260)IOL=ARM02A
1260 FIND (ARM06_DEV,KEY=C0$,DOM=1270)IOL=ARM06A
1280 FIND (ARM10_DEV,KEY=N0$+"A"+B1$(4,2),DOM=1290)IOL=ARM10A
1290 FIND (ARM10_DEV,KEY=N0$+"B"+B1$(32,2),DOM=1300)IOL=ARM10B
1300 FIND (ARM10_DEV,KEY=N0$+"D"+B1$(8,2),DOM=1310)IOL=ARM10D
1310 FIND (ARM10_DEV,KEY=N0$+"F"+B1$(1,3),DOM=1320)IOL=ARM10F
1320 FIND (ARM10_DEV,KEY=N0$+"H"+B1$(21,3),DOM=1330)IOL=ARM10H
1330 FIND (ARM10_DEV,KEY=N0$+"I"+B1$(6,2),DOM=1340)IOL=ARM10I
1500 REM " --- Display middle window"
1510 CALL "SYC.WB",NAME$,0
1520 LET BALANCE=B[0]+B[1]+B[2]+B[3]+B[4]+B[5],SALE$="",CASH$="",OPENED$=""
1530 IF FNP$(C1$(1,3))<>"" THEN LET SALE$=FNB$(C1$(1,3))
1540 IF FNP$(C1$(4,3))<>"" THEN LET CASH$=FNB$(C1$(4,3))
1550 IF FNP$(A1$(156,3))<>"" THEN LET OPENED$=FNB$(A1$(156,3))
1560 IF B1$(39,1)="Y" THEN LET HOLD$="Yes"
1570 PRINT @(13,1),B1$(1,3),@(P2),XF$(7,W),@(13,2),B1$(21,3),@(P2),XH$(7,W),@(13,3),B1$(4,2),@(P2),XA$(6,W),@(13,4),B1$(32,2),@(P2),XB$(6,W),@(13,5),B1$(8,2),@(P2),XD$(6,W),@(13,6),XM$(6,W-1),@(13,7),B[6]:M1$,@(13,8),SALE$,@(13,9),CASH$,@(P1,1),B[0]:M1$,@(P1,2),B[1]:M1$,@(P1,3),B[2]:M1$,@(P1,4),B[3]:M1$,@(P1,5),B[4]:M1$,@(P1,6),B[5]:M1$,@(P1,7),BALANCE:M1$,@(P1,8),C[0]:M1$,@(P1,9),C[1]:M1$,
1600 REM " --- Display bottom window"
1610 CALL "SYC.WB",SNAME$,0
1620 PRINT 'CF',@(9,0),A1$(228,20),@(9,1),FNF$(A1$(112,10),M9$),@(9,2),FNF$(A1$(169,10),M9$),@(WIDTH-11,0),OPENED$,@(WIDTH-28,1),A1$(257,8),@(WIDTH-11),B1$(34,3),@(WIDTH-11,2),HOLD$,
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$=A0$(3),CHOICE2$=""
3030 GOSUB 5000
3040 IF CONTROL=1 THEN GOTO 1000
4000 REM " --- All done"
4010 LET C$='CS'
4020 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT C$,
6030 CALL "SYC.WB",NAME$,0
6040 PRINT C$,
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT C$,
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9921)
9930 NEXT X
9950 EXIT 
9999 END

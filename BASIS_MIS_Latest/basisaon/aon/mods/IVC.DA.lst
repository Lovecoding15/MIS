0010 REM "IVC - Delete Item and/or Warehouse"
0020 REM "Program IVC.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "15Jun98 JWK: Remove all "F", "J" and "T" Xref Records (3180-3185,3455-3465,3770-3785,6200-6290)
0040 REM 
0043 REM "ACTION$     : Action Code (input)"
0045 REM "              I=Delete Item and Warehouse(s)"
0047 REM "              W=Delete Warehouse Only"
0050 REM "CHANNELS[n] : Channel references - See IVM.MA (input)"
0052 REM "PARAMS$[n]  : Parameter information (input)"
0054 REM "            : [0]=Firm ID             [4]=PO Installed (Y/N)"
0056 REM "            : [1]=Warehouse ID        [5]=WO Installed (Y/N)"
0058 REM "            : [2]=Item Number         [6]=BM Installed (Y/N)"
0060 REM "            : [3]=OP Installed (Y/N)  [7]=AP Installed (Y/N)"
0062 REM "PARAMS[n]   : Parameter information (input)"
0064 REM "            : [0]=# of Periods [1]=Current Period [2]=Current Year"
0070 REM "STATUS      : 0=No Error (returned)"
0075 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER ACTION$,CHANNELS[ALL],PARAMS$[ALL],PARAMS[ALL],STATUS
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$,A6$,A[ALL]
0220 IVM02A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0230 IVM12A: IOLIST C0$(1),C1$,C2$,C[ALL]
0400 REM " --- Parameters"
0410 LET N0$=PARAMS$[0],WHSE$=PARAMS$[1],ITEM$=PARAMS$[2],OP$=PARAMS$[3]
0420 LET PO$=PARAMS$[4],WO$=PARAMS$[5],BM$=PARAMS$[6],AP$=PARAMS$[7]
0500 REM " --- Initializations"
0510 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A[12]
0550 LET A0$(1)=N0$+ITEM$,M1$=STR(PARAMS[1]:"00"),Y1$=FNYEAR_YY21$(PARAMS[2])
0555 LET M2=PARAMS[1]+1,Y2=PARAMS[2]
0560 IF M2>PARAMS[0] THEN LET M2=1,Y2=Y2+1
0563 LET M2$=STR(M2:"00"),Y2$=FNYEAR_YY21$(Y2)
0565 LET IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3],IVM03_DEV=CHANNELS[4]
0570 LET IVM09_DEV=CHANNELS[5],IVM12_DEV=CHANNELS[7],IVM14_DEV=CHANNELS[9]
0575 LET IVM24_DEV=CHANNELS[11],IVM05_DEV=CHANNELS[26],BMM01_DEV=CHANNELS[28]
0580 LET BMM04_DEV=CHANNELS[30],ARE07_DEV=CHANNELS[33],POE41_DEV=CHANNELS[39]
0585 LET WOE03_DEV=CHANNELS[47],STATUS=0
0900 REM " --- Position warehouse xref"
0910 LET STATUS=0,FIRST$=N0$+"G"+ITEM$+WHSE$,K$=FIRST$
0970 FIND (IVM01_DEV,KEY=A0$,DOM=4000)IOL=IVM01A
0980 READ (IVM03_DEV,KEY=K$,DOM=1000)
0990 GOTO 1020
1000 REM " --- Quantity on hand, on order or committed?"
1010 LET K$=KEY(IVM03_DEV,END=1600)
1020 IF POS(FIRST$=K$)<>1 THEN GOTO 1600
1030 READ (IVM03_DEV)
1040 DIM B0$(24),B1$(64),B[20]
1050 LET B0$(1)=N0$+K$(24,2)+ITEM$
1060 FIND (IVM02_DEV,KEY=B0$,DOM=1070)IOL=IVM02A
1070 IF B1$(15,1)="Y" THEN LET STATUS=1
1080 REM IF B[0]<>0 OR B[1]<>0 OR B[2]<>0 THEN LET STATUS=2
1090 IF STATUS<>0 THEN GOTO 2000
1100 REM " --- Open Orders?"
1110 IF OP$<>"Y" THEN GOTO 1200
1120 LET CHANNEL=ARE07_DEV,X$=B0$,X=3
1130 GOSUB 6000
1140 IF STATUS<>0 THEN GOTO 2000
1200 REM " --- Requisitions?"
1210 IF PO$<>"Y" THEN GOTO 1300
1220 LET CHANNEL=POE41_DEV,X$=B0$,X=4
1230 GOSUB 6000
1240 IF STATUS<>0 THEN GOTO 2000
1300 REM " --- Work Orders?"
1310 IF WO$<>"Y" THEN GOTO 1400
1320 LET CHANNEL=WOE03_DEV,X$=B0$(1,2)+"C"+B0$(3),X=5
1330 GOSUB 6000
1340 IF STATUS<>0 THEN GOTO 2000
1350 LET X$=B0$(1,2)+"E"+"  "+B0$(3),X=6
1360 GOSUB 6000
1370 IF STATUS<>0 THEN GOTO 2000
1400 REM " --- Current or next period activity?"
1410 LET Y$=Y1$,M$=M1$,X=9
1420 REM GOSUB 6100
1430 IF STATUS<>0 THEN GOTO 2000
1440 LET Y$=Y2$,M$=M2$,X=10
1450 REM GOSUB 6100
1460 IF STATUS<>0 THEN GOTO 2000
1500 REM " --- Loop back for next warehouse"
1590 GOTO 1000
1600 REM " --- Check Bill of Materials"
1610 IF ACTION$="W" THEN GOTO 2000
1620 IF BM$<>"Y" THEN GOTO 2000
1630 LET K$=N0$+ITEM$
1640 FIND (BMM01_DEV,KEY=K$,DOM=1670)
1650 LET STATUS=7
1660 GOTO 2000
1670 LET CHANNEL=BMM04_DEV,X$=K$,X=8
1680 GOSUB 6000
2000 REM " --- Set message when STATUS<>0"
2010 IF STATUS=0 THEN GOTO 2900
2020 DIM MESSAGE$[2]
2030 IF STATUS=1 THEN LET MESSAGE$[0]="This Item Has Been Selected For Physical Inventory"
2040 IF STATUS=2 THEN LET MESSAGE$[0]="Warehouse Quantities Exist For This Inventory Item"
2050 IF STATUS=3 THEN LET MESSAGE$[0]="Sales Orders For This Inventory Item Current Exist"
2060 IF STATUS=4 THEN LET MESSAGE$[0]="Purchase Requisitions Exist For This Inventory Item"
2070 IF STATUS=5 THEN LET MESSAGE$[0]="Work Orders For This Inventory Item Currently Exist"
2080 IF STATUS=6 THEN LET MESSAGE$[0]="This Item Is Required For An Existing Work Order"
2090 IF STATUS=7 THEN LET MESSAGE$[0]="This Item Is Also A Bill Of Materials Master Bill"
2100 IF STATUS=8 THEN LET MESSAGE$[0]="This Item Is Also A Bill Of Materials Component"
2110 IF STATUS=9 THEN LET MESSAGE$[0]="Warehouse Activity Exists For The Current Period"
2120 IF STATUS=10 THEN LET MESSAGE$[0]="Warehouse Activity Exists For The Next Period"
2190 LET MESSAGE$[1]="It Can Not Be Deleted (<Enter>=Continue)"
2200 REM " --- Display message"
2210 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
2220 IF V$<>"SPECIAL" THEN GOTO 4000
2900 REM " --- Position warehouse xref"
2910 LET K$=FIRST$
2980 READ (IVM03_DEV,KEY=K$,DOM=3000)
2990 GOTO 3020
3000 REM " --- Next warehouse xref"
3010 LET K$=KEY(IVM03_DEV,END=3300)
3020 IF POS(FIRST$=K$)<>1 THEN GOTO 3300
3030 REMOVE (IVM03_DEV,KEY=K$,DOM=3040)
3040 DIM B0$(24),B1$(64),B[20]
3050 LET B0$(1)=N0$+K$(24,2)+ITEM$,X$=K$
3060 FIND (IVM02_DEV,KEY=B0$,DOM=3070)IOL=IVM02A
3070 REMOVE (IVM02_DEV,KEY=B0$,DOM=3080)
3100 REM " --- Remove IVM-03 Xrefs"
3110 DIM X0$(37)
3120 LET X0$(1)=B0$(1,2)+"B"+B0$(3,2)+B1$(11,2)+B1$(1,10)+B0$(5)
3130 REMOVE (IVM03_DEV,KEY=X0$,DOM=3140)
3140 DIM X0$(28)
3150 LET X0$(1)=B0$(1,2)+"C"+B0$(3,2)+A2$(1,3)+B0$(5)
3160 REMOVE (IVM03_DEV,KEY=X0$,DOM=3170)
3170 DIM X0$(37)
3180 GOSUB 6200
3190 READ (IVM12_DEV,KEY=B0$,DOM=3200)
3200 REM " --- Remove IVM-12 Activity"
3210 LET K$=KEY(IVM12_DEV,END=3250)
3220 IF POS(B0$=K$)<>1 THEN GOTO 3250
3230 REMOVE (IVM12_DEV,KEY=K$,DOM=3200)
3240 GOTO 3200
3250 REM " --- Loop back for next warehouse"
3260 READ (IVM03_DEV,KEY=X$,DOM=3000)
3290 GOTO 3000
3300 REM " --- Position vendor xref"
3310 IF ACTION$="W" THEN GOTO 4000
3320 LET FIRST$=N0$+"H"+ITEM$
3390 READ (IVM03_DEV,KEY=FIRST$,DOM=3400)
3400 REM " --- Read next IVM-05 vendor record"
3410 LET K$=KEY(IVM03_DEV,END=3500)
3420 IF POS(FIRST$=K$)<>1 THEN GOTO 3500
3430 LET X$=K$(1,2)+K$(24)+K$(4,20)
3440 IF AP$="Y" THEN REMOVE (IVM05_DEV,KEY=X$,DOM=3450)
3450 LET X$=X$(1,2)+"D"+X$(3)
3455 REMOVE (IVM03_DEV,KEY=X$,DOM=3456)
3460 LET X$=K$(1,2)+"F"+A2$(9,3)+K$(24,6)+A2$(1,3)+A0$(3)+"  "
3465 REMOVE (IVM03_DEV,KEY=X$,DOM=3466)
3470 REMOVE (IVM03_DEV,KEY=K$,DOM=3480)
3490 GOTO 3400
3500 REM " --- Position synonyms file"
3590 READ (IVM24_DEV,KEY=A0$,DOM=3600)
3600 REM " --- Read next synonym"
3610 LET K$=KEY(IVM24_DEV,END=3700)
3620 IF POS(A0$=K$)<>1 THEN GOTO 3700
3630 REMOVE (IVM24_DEV,KEY=K$,DOM=3640)
3640 LET K$=K$(1,2)+K$(23)+K$(3,20)
3650 REMOVE (IVM14_DEV,KEY=K$,DOM=3660)
3690 GOTO 3600
3700 REM " --- Remove IVM-03 Item Xrefs"
3705 DIM X0$(26)
3710 LET X0$(1)=N0$+"A"+A2$(1,3)+A0$(3)
3715 REMOVE (IVM03_DEV,KEY=X0$,DOM=3720)
3720 DIM X0$(43)
3725 LET X0$(1)=N0$+"E"+A1$,X0$(24)=A0$(3)
3730 REMOVE (IVM03_DEV,KEY=X0$,DOM=3735)
3735 LET X0$(1)=N0$+"I"+A3$(21,20)+A0$(3)
3740 REMOVE (IVM03_DEV,KEY=X0$,DOM=3745)
3745 LET X0$(1)=N0$+"U"+A3$(1,20)+A0$(3)
3750 REMOVE (IVM03_DEV,KEY=X0$,DOM=3755)
3755 DIM X0$(37)
3760 LET X0$(1)=N0$+"F"+A2$(9,3)+A2$(12,6)+A2$(1,3)+A0$(3)
3765 REMOVE (IVM03_DEV,KEY=X0$,DOM=3770)
3770 LET X0$=N0$+"J"+A2$(21,3)+A0$(3)
3775 REMOVE (IVM03_DEV,KEY=X0$,DOM=3780)
3780 LET X0$=N0$+"T"+A2$(53,3)+A0$(3)
3785 REMOVE (IVM03_DEV,KEY=X0$,DOM=3790)
3790 READ (IVM09_DEV,KEY=A0$,DOM=3800)
3800 REM " --- Remove IVM-09 commments"
3810 LET K$=KEY(IVM09_DEV,END=3900)
3820 IF POS(A0$=K$)<>1 THEN GOTO 3900
3830 REMOVE (IVM09_DEV,KEY=K$,DOM=3840)
3890 GOTO 3800
3900 REM " --- Remove IVM-01 item master"
3910 REMOVE (IVM01_DEV,KEY=A0$,DOM=4000)
4000 REM " --- All done"
4090 GOTO 9900
6000 REM " --- Does Firm/Item/Warehouse exist in file?"
6010 READ (CHANNEL,KEY=X$,DOM=6020)
6020 LET K$=KEY(CHANNEL,END=6090)
6030 IF POS(X$=K$)=1 THEN LET STATUS=X
6090 RETURN 
6100 REM " --- Current or Next Period Activity?"
6110 DIM C0$(28),C[12]
6120 LET C0$(1)=B0$+Y$+M$
6130 FIND (IVM12_DEV,KEY=C0$,DOM=6190)IOL=IVM12A
6140 FOR P=0 TO 8
6150 IF C[P]<>0 THEN LET STATUS=X
6160 NEXT P
6190 RETURN 
6200 REM " --- Remove IVM03F Records for all Vendors (Stocking level W)
6210 READ (IVM03_DEV,KEY=N0$+"H"+ITEM$,DOM=6211)
6230 LET H_K$=KEY(IVM03_DEV,END=6290)
6240 IF POS(N0$+"H"+ITEM$=H_K$)<>1 THEN GOTO 6290
6250 LET X0$(1)=N0$+"F"+B1$(19,3)+H_K$(24,6)+A2$(1,3)+B0$(5)+B0$(3,2)
6260 REMOVE (IVM03_DEV,KEY=X0$,DOM=6261)
6270 READ (IVM03_DEV,KEY=H_K$,DOM=6271)
6280 GOTO 6230
6290 RETURN 
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 LET STATUS=999
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9950 EXIT 
9999 END

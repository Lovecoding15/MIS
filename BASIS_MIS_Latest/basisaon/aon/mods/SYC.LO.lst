0010 REM "SYC - Order/Invoice Lookup"
0020 REM "Program SYC.LO"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "03Dec96 JWK: Displaying blank item description (4530)
0032 REM "16Apr97 WGH: Display non-stock item description (4420,4450,4540)"
0033 REM "26Jan98 WGH: Not finding orders for alpha Customers shorter than 6-char (550,1080,2030,3220,5620)"
0034 REM "13Apr98 JWK: No Historical Invoices found if <CR> by Order # (2430)
0040 REM 
0045 REM "SELECTION$: Customer number (input)"
0050 REM "            Order or Invoice number (returned)"
0060 REM "SOURCE$   : Lookup source (input)/Selection source (returned)"
0070 REM "            O=Order (ARE-03)/I=Invoice (ART-03)
0075 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER SELECTION$,SOURCE$
0100 REM " --- Open/Lock Files"
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ARM-01",FILES$[2]="ARE-03",FILES$[3]="ARE-13"
0140 LET FILES$[4]="ART-03",FILES$[5]="ART-13",FILES$[6]="ARM-10"
0145 LET FILES$[7]="IVM-01",FILES$[8]="SYS-01",FILES$[9]="SYM-06"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0170 LET ARM01_DEV=CHANNELS[1],ARE03_DEV=CHANNELS[2],ARE13_DEV=CHANNELS[3]
0180 LET ART03_DEV=CHANNELS[4],ART13_DEV=CHANNELS[5],ARM10_DEV=CHANNELS[6]
0190 LET IVM01_DEV=CHANNELS[7],SYS01_DEV=CHANNELS[8],SYM06_DEV=CHANNELS[9]
0200 REM " --- IOLIST's"
0210 ARE03A: IOLIST A0$(1),A[ALL]
0220 ART13A: IOLIST W0$(1),W1$(1),W[ALL]
0230 IVM01A: IOLIST D0$(1),D1$(1)
0240 ARM10E: IOLIST Y0$(1)
0250 SYM06A: IOLIST X$,COMPANY$
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,X$,M1$,M2$,M3$
0400 REM " --- Parameters"
0405 DIM P[2],I[5]
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0425 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),SPEEDSEARCH$="N"
0440 LET X$=STBL("!SPEEDSEARCH",ERR=450),N0$=X$(1,2),SPEEDSEARCH$="Y"
0450 FIND (SYM06_DEV,KEY=N0$,DOM=9800)IOL=SYM06A
0460 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0465 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0470 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0475 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1))
0480 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0485 CALL "SYC.RM",M1$,M1,8,11
0490 CALL "SYC.RM",M2$,M2,6,9
0495 CALL "SYC.RM",M3$,M3,6,9
0500 REM " --- Initializations"
0505 PRECISION I[1]
0510 DIM A0$(101),A[10],W0$(52),W1$(64),W[14],D0$(22),D1$(60)
0515 DIM OLIST$[2,MAXROW,6],OPARAM$[2,MAXROW,3],MLIST$[2,MAXROW,6]
0520 DIM ILIST$[2,MAXROW,6],IPARAM$[2,MAXROW,3],MPARAM$[2,MAXROW,3]
0525 DIM SLIST$[2,MAXROW,6]
0540 LET SOURCE$=SOURCE$+FILL(10),SOURCE$=SOURCE$(1,10)
0545 IF POS(SOURCE$(1,1)="OI")=0 THEN LET SOURCE$(1,1)="O"
0550 LET M7$="(###)-###-####",CUSTOMER$=FILL(6),CUSTOMER$(1)=SELECTION$,SELECTION$=""
0555 LET WIDTH=80,HEIGHT=12,WIN_X=0,WIN_Y=6,MAX_ROW=HEIGHT-5
0560 LET OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+2,START$="",INVOICES=0
0565 LET DWIN_X=OWIN_X+5,DWIN_Y=OWIN_Y+3,DTITLE$="Display"
0570 LET TITLE$="Open Order/Invoice History Lookup",OTITLE$="Method"
0600 REM " --- Retrieve list of search options and calculate window size"
0610 LET KEYLEN=7,MASK$=FILL(KEYLEN,"0"),OPGM$=PGM(-2),IPGM$="SYC.LY"
0620 CALL "SYC.OL",OPGM$,ORDOPTS,ORDWIDTH,OLIST$[ALL],OPARAM$[ALL],X$,MAXROW,MASK$,KEYLEN
0630 CALL "SYC.OL",IPGM$,INVOPTS,INVWIDTH,ILIST$[ALL],IPARAM$[ALL],X$,MAXROW,MASK$,KEYLEN
0640 LET ORDHEIGHT=ORDOPTS+4,ORDWIDTH=ORDWIDTH+4
0650 LET INVHEIGHT=INVOPTS+4,INVWIDTH=INVWIDTH+4
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0800 REM " --- Search options when method is Order Status"
0810 LET SLIST$[1,1,1]=" Orders",SLIST$[1,1,2]="O"
0820 LET SLIST$[1,2,1]=" Backorders",SLIST$[1,2,2]="B"
0830 LET SLIST$[1,3,1]=" Credit Hold",SLIST$[1,3,2]="C"
0840 LET SLIST$[1,4,1]=" Quotes",SLIST$[1,4,2]="Q"
0850 LET SLIST$[1,5,1]=" Invoices",SLIST$[1,5,2]="I"
0860 LET SLIST$[1,6,1]=" All Orders",SLIST$[1,6,2]="A"
1000 REM " --- Order headings/footings"
1010 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
1020 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
1030 IF INVOICES THEN GOTO 1100
1040 LET HEADING$(2)="Order # Typ Ord Date Shp Date Sls"
1050 LET HEADING$(36)="PO Number",FOOTING$(2)="Open Orders"
1060 LET HEADING$(47)="Ship Via",HEADING$(59)="Invoice"
1070 LET HEADING$(WIDTH-7)="Total",X$="F3=Invoice History  "
1080 IF FNP$(CUSTOMER$)="" THEN LET X$=""
1090 GOTO 1200
1100 REM " --- Invoice headings/footings"
1120 LET HEADING$(2)="Invoice Typ Ord Date Inv Date Sls"
1125 LET HEADING$(36)="PO Number",FOOTING$(2)="Invoice History"
1130 LET HEADING$(47)="Ship Via",HEADING$(59)="Order #"
1145 LET HEADING$(WIDTH-7)="Total",X$="F3=Open Orders  "
1200 REM " --- Select method for order lookup"
1210 GOSUB 5000
1220 IF INVOICES THEN GOTO 1600
1230 LET MLIST$[ALL]=OLIST$[ALL],OPTIONS=ORDOPTS
1240 LET OHEIGHT=ORDHEIGHT,OWIDTH=ORDWIDTH
1250 GOSUB 5400
1260 IF FKEY=4 OR OPTION$="END" OR OPTION$="PRIOR" THEN GOTO 9900
1270 IF FNP$(OPTION$)="I" AND INVOICES=0 THEN LET INVOICES=1; GOTO 1000
1280 IF METHOD=0 THEN GOTO 9900
1300 REM " --- Enter starting value"
1310 LET V0$=OPARAM$[1,METHOD,1],V1$="KCE",V2$="",V4$="",V1=1
1320 LET V3$=OPARAM$[1,METHOD,3],V0=NUM(OPARAM$[1,METHOD,2],ERR=1000)
1330 LET V2=OHEIGHT-2,SMETHOD=METHOD,STATUS$="",PREFIX$=FNP$(OPTION$)
1340 IF PREFIX$="S" THEN GOTO 1400
1350 GOSUB 7000
1360 IF V3=4 THEN GOTO 1000
1370 IF V3=5 OR V3=6 THEN GOTO 9900
1380 LET START$=V$
1390 GOTO 1800
1400 REM " --- Select order statuses to display"
1410 GOSUB 5200
1420 IF DNAME$<>"" THEN CALL "SYC.WD",DNAME$
1430 IF FKEY=4 OR OPTION$="END" OR OPTION$="PRIOR" OR METHOD=0 THEN GOTO 9900
1440 LET STATUS$=FNP$(OPTION$),METHOD=SMETHOD
1450 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
1490 GOTO 1800
1600 REM " --- Select method for invoice lookup"
1610 LET MLIST$[ALL]=ILIST$[ALL],OPTIONS=INVOPTS
1620 LET OHEIGHT=INVHEIGHT,OWIDTH=INVWIDTH
1630 GOSUB 5400
1640 IF FKEY=4 OR OPTION$="END" OR OPTION$="PRIOR" THEN GOTO 9900
1650 IF FNP$(OPTION$)="I" AND INVOICES THEN LET INVOICES=0; GOTO 1000
1660 IF METHOD=0 THEN GOTO 9900
1700 REM " --- Enter starting value"
1710 LET V0$=IPARAM$[1,METHOD,1],V1$="KCE",V2$="",V4$="",V1=1
1720 LET V3$=IPARAM$[1,METHOD,3],V0=NUM(IPARAM$[1,METHOD,2],ERR=1000)
1730 LET V2=OHEIGHT-2,SMETHOD=METHOD,STATUS$="",PREFIX$=FNP$(OPTION$)
1740 GOSUB 7000
1750 IF V3=4 THEN GOTO 1000
1760 IF V3=5 OR V3=6 THEN GOTO 9900
1770 LET START$=V$
1800 REM " --- Display SpeedSearch Company"
1810 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
1820 IF DNAME$<>"" THEN CALL "SYC.WD",DNAME$
1830 IF SPEEDSEARCH$<>"Y" THEN GOTO 1900
1840 DIM X$(WIDTH)
1850 LET X$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
1860 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
1870 CALL "SYC.WA",0,WIDTH+SWIDTH,3,WIN_X,WIN_Y+HEIGHT,"",CNAME$
1880 PRINT @(0,0),'BR',X$,'ER',
1890 CALL "SYC.WB",NAME$,0
1900 REM " --- Position File"
1910 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,5]
1920 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$=""
1930 IF PREFIX$="*" THEN LET START$=FNU$(START$)
1970 LET CHANNEL=ARE03_DEV,START1=0,START1=NUM(START$,ERR=1980)
1980 IF INVOICES THEN LET CHANNEL=ART03_DEV
1990 READ (CHANNEL,KEY=N0$+"  "+CUSTOMER$,DOM=2000)
2000 REM " --- Read next record"
2010 LET K$=KEY(CHANNEL,END=3500)
2020 IF POS(N0$=K$)<>1 THEN GOTO 3500
2030 IF FNP$(CUSTOMER$)<>"" THEN IF K$(5,6)<>CUSTOMER$ THEN GOTO 3500
2040 LET LOCKED=0
2050 READ (CHANNEL,ERR=1550)IOL=ARE03A
2060 IF PREFIX$<>"N" THEN GOTO 2200
2070 IF START$="" THEN GOTO 2200
2080 IF A0$(11,7)<START$ THEN GOTO 3400
2090 GOTO 2200
2100 REM " --- Record locked?"
2110 IF ERR<>0 THEN GOTO 9000
2120 LET LOCKED=1
2190 READ (CHANNEL,KEY=K$(1,LEN(K$)-1)+$FF$,DOM=2600)
2200 REM " --- Additional validity tests"
2210 IF INVOICES THEN GOTO 2300
2220 IF A0$(21,1)="S" AND A0$(22,1)<>"O" AND SOURCE$(1,1)<>"I" THEN GOTO 3400
2230 IF A0$(21,1)="P" AND A0$(22,1)<>"O" THEN GOTO 3400
2240 IF A0$(21,1)="P" AND A0$(22,1)="O" THEN LET A0$(22,1)="Q"
2250 IF A0$(101,1)="C" AND A0$(22,1)="O" THEN LET A0$(22,1)="C"
2260 IF A0$(23,1)="B" AND A0$(22,1)="O" THEN LET A0$(22,1)="B"
2290 IF PREFIX$="N" THEN GOTO 2600
2300 REM " --- Check for keyword match"
2310 IF PREFIX$<>"*" THEN GOTO 2400
2320 IF START$<>"" THEN IF POS(START$=FNU$(A0$))=0 THEN GOTO 3400
2390 GOTO 2600
2400 REM " --- Check for order number match"
2410 IF PREFIX$<>"#" THEN GOTO 2500
2420 LET X=NUM(A0$(42,7),ERR=3400)
2430 IF START1<>0 THEN IF X<>START1 THEN GOTO 3400
2490 GOTO 2600
2500 REM " --- Check for order status match"
2510 IF PREFIX$<>"S" THEN GOTO 2600
2520 IF STATUS$="A" THEN GOTO 2600
2530 IF A0$(22,1)<>STATUS$ THEN GOTO 3400
2600 REM " --- Display page?"
2610 IF L<MAX_ROW THEN GOTO 3000
2620 LET XMODE=5,SL=L,NOPAGES=PAGE
2630 GOSUB 5600
2640 IF FKEY=1 OR FKEY=3 THEN GOTO 1000
2650 IF FKEY=4 THEN GOTO 9900
2660 IF SELECTION$<>"" THEN GOTO 4000
3000 REM " --- Lookup window record"
3010 DIM G$(WIDTH-2)
3020 LET G$(2)=K$(11,7),G$(10)="Record Locked by Another User"
3030 IF LOCKED THEN GOTO 3200
3040 LET G$(1)=" "+A0$(11,7),G$(10)=A0$(22,1),G$(14)=FNB$(A0$(77,3))
3050 LET G$(23)=FNB$(A0$(27,3)),G$(32)=A0$(59,3),G$(36)=A0$(49,10)
3060 IF INVOICES THEN LET G$(23,8)=FNB$(A0$(24,3))
3070 LET G$(47)=A0$(32,10),G$(59)=A0$(42,7),G$(WIDTH-1-M1)=STR(A[6]:M1$)
3090 IF INVOICES THEN GOTO 3200
3100 REM " --- Set remaining order status flags"
3110 IF A0$(68,1)="Y" THEN LET G$(11,1)="P"
3120 IF A0$(69,1)="Y" THEN LET G$(12,1)="L" ELSE IF A0$(69,1)<>"N" THEN LET G$(12,1)=A0$(69,1)
3130 IF A0$(21,1)="V" THEN LET G$(12,1)="V"
3200 REM " --- Store display line"
3210 LET LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=A0$(11,7)
3220 IF FNP$(CUSTOMER$)="" THEN LET LLIST$[PAGE,X1,2]=A0$(5,13)
3290 LET X0=X0+1,X1=X1+1,L=L+1
3400 REM " --- Loop back for next record"
3490 GOTO 2000
3500 REM " --- End of file"
3510 IF X0>0 THEN GOTO 3900
3520 LET V4$="Open Orders"
3530 IF INVOICES THEN LET V4$="Historical Invoices"
3540 GOSUB 6900
3550 IF V3=1 THEN GOTO 1000
3560 IF V3=5 OR V3=6 THEN GOTO 9900
3600 REM " --- If displaying invoices, switch display back to orders"
3610 IF INVOICES=0 THEN GOTO 3700
3620 IF V3=4 THEN GOTO 4000
3630 LET INVOICES=0
3690 GOTO 1000
3700 REM " --- If displaying orders and <F3> was pressed, switch to invoices"
3710 IF V3<>3 THEN GOTO 4000
3720 LET INVOICES=1
3790 GOTO 1000
3900 REM " --- Display last page of orders or invoices"
3910 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3920 GOSUB 5600
3930 IF FKEY=1 OR FKEY=3 THEN GOTO 1000
4000 REM " --- Display line items if INVOICES=1"
4010 LET SOURCE$(1,1)="O"
4020 IF INVOICES=0 THEN GOTO 4900
4100 REM " --- Position detail file"
4110 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,5]
4120 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,W4=WIDTH-1-M1,W3=W4-M2,W2=W3-M2,W1=W2-M3
4130 LET XMODE=5,DETAIL=1,W=W1-2-(12+I[0]),FOOTING$(2)="Line Item Detail",X$=""
4140 LET HEADING$(2)="C WH Item",HEADING$(8+I[0])="Description"
4150 LET HEADING$(W1+M3-6)="Price",HEADING$(W2+M2-6)="Order"
4160 LET HEADING$(W3+M2-5)="Ship",HEADING$(W4+M1-10)="Extension"
4170 GOSUB 5000
4180 LET A0$(1)=N0$+"  "+CUSTOMER$+SELECTION$,A0$(18,3)="000"
4185 FIND (ART03_DEV,KEY=A0$(1,20),DOM=4900)IOL=ARE03A
4190 READ (ART13_DEV,KEY=A0$(1,20),DOM=4200)
4200 REM " --- Read next line item"
4210 LET K$=KEY(ART13_DEV,END=4700)
4220 IF POS(A0$(1,17)=K$)<>1 THEN GOTO 4700
4230 READ (ART13_DEV)IOL=ART13A
4300 REM " --- Display page?"
4310 IF L<MAX_ROW THEN GOTO 4400
4320 LET XMODE=5
4330 GOSUB 5800
4340 IF FKEY=1 THEN GOTO 1000
4350 IF FKEY=4 THEN GOTO 4850
4360 IF ANSWER$<>"" THEN GOTO 4850
4390 LET L=1,X1=1
4400 REM " --- Build display line"
4410 DIM G$(WIDTH-2),DESCRIPTION$(W),Y0$(32)
4420 LET G$(2)=W0$(21,1),G$(4,2)=W0$(31,2),Y0$(1)=N0$+"E"+W0$(21,1)
4430 FIND (ARM10_DEV,KEY=Y0$(1,4),DOM=4440)IOL=ARM10E
4440 IF POS(Y0$(25,1)="SP") THEN GOTO 4500
4450 LET G$(7)=W1$(1,40)
4460 IF POS(Y0$(25,1)="NO") THEN GOTO 4550
4490 GOTO 4600
4500 REM " --- Retrieve inventory item"
4510 LET D1$(1)="(Not On File)"
4520 FIND (IVM01_DEV,KEY=W0$(1,2)+W0$(33,20),DOM=4530)IOL=IVM01A
4530 LET DESCRIPTION$(1)=FNITEM$(D1$,I[3],I[4],I[5])
4540 LET G$(7)=W0$(33,I[0]),G$(8+I[0])=DESCRIPTION$
4550 LET G$(W1)=STR(W[1]:M3$),G$(W2)=STR(W[2]:M2$)
4560 LET G$(W3)=STR(W[4]:M2$),G$(W4)=STR(W[6]:M1$)
4600 REM " --- Store display line"
4610 LET LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=A0$(11,7)
4620 LET X0=X0+1,X1=X1+1,L=L+1
4690 GOTO 4200
4700 REM " --- No more line items"
4710 IF X0>0 THEN GOTO 4800
4720 LET V4$="Line Items"
4730 GOSUB 6900
4740 IF V3=1 THEN GOTO 1000
4790 GOTO 4900
4800 REM " --- Display last page and exit"
4810 LET XMODE=5
4820 GOSUB 5800
4830 IF FKEY=1 THEN GOTO 1000
4850 LET SOURCE$(1,1)="I"
4900 REM " --- All done"
4990 GOTO 9900
5000 REM " --- Display header/footer"
5010 LET X$="F1=Restart  "+X$+"F4=End  PgUp  PgDn"
5020 LET FOOTING$(WIDTH-7-LEN(X$))=X$
5050 PRINT @(0,0),'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
5090 RETURN
5200 REM " --- Order Status Selection"
5210 LET OWIDTH=16,OHEIGHT=8,OMAX_ROW=OHEIGHT-1,PAGE=1,XMODE=0,OPTIONS=OHEIGHT-2
5220 DIM MORE$[2,OPTIONS,1]
5230 CALL "SYC.WA",0,OWIDTH,OHEIGHT,DWIN_X,DWIN_Y,DTITLE$,DNAME$
5300 REM " --- Lightbar selection of order statuses to display"
5310 CALL "SYC.SA",XMODE,SLIST$[ALL],MORE$[ALL],OPTION$,PAGE,OMAX_ROW,DNAME$,OHEIGHT,OWIDTH,FKEY
5330 IF FKEY=4 OR OPTION$="END" OR OPTION$="PRIOR" THEN GOTO 5390
5340 LET METHOD$="",METHOD=0
5360 FOR X=1 TO OPTIONS
5370 IF OPTION$=SLIST$[1,X,2] THEN LET METHOD$=FNP$(SLIST$[1,X,1]),METHOD=X
5380 NEXT X
5390 RETURN
5400 REM " --- Order/Invoice Search Method"
5410 DIM MORE$[2,MAXROW,1]
5420 CALL "SYC.WA",0,OWIDTH,OHEIGHT,OWIN_X,OWIN_Y,OTITLE$,ONAME$
5430 CALL "SYC.WC",1,0,OWIDTH-2,0,0,OHEIGHT-4,1
5440 LET OMAX_ROW=OHEIGHT-2,PAGE=1,XMODE=0
5500 REM " --- Lightbar selection of search method"
5510 CALL "SYC.SA",XMODE,MLIST$[ALL],MORE$[ALL],OPTION$,PAGE,OMAX_ROW,ONAME$,OHEIGHT,OWIDTH,FKEY
5520 IF FKEY=4 OR OPTION$="END" OR OPTION$="PRIOR" THEN GOTO 5590
5530 LET METHOD$="",METHOD=0
5540 FOR X=1 TO OPTIONS
5550 IF OPTION$=MLIST$[1,X,2] THEN LET METHOD$=FNP$(MLIST$[1,X,1]),METHOD=X
5560 NEXT X
5590 RETURN
5600 REM " --- Lightbar selection of order/invoice"
5610 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
5620 IF FKEY=3 AND FNP$(CUSTOMER$)="" THEN LET FKEY=0
5630 IF FKEY=3 AND INVOICES=0 THEN LET INVOICES=1; GOTO 5690
5640 IF FKEY=3 AND INVOICES THEN LET INVOICES=0; GOTO 5690
5650 IF FKEY=-16 THEN LET PAGE=PAGE+1
5660 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,5
5670 LET L=1,X1=1
5690 RETURN
5800 REM " --- Lightbar selection of line items"
5810 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],ANSWER$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
5820 IF FKEY=-16 THEN LET PAGE=PAGE+1
5830 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
5840 LET L=1,X1=1
5890 RETURN
6900 REM " --- No records found"
6910 DIM MESSAGE$[1]
6920 LET MESSAGE$[0]="No "+V4$+" Found (<Enter>=Continue)"
6930 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=INT((WIDTH-(LEN(MESSAGE$[0])+9))/2)
6940 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
6990 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 REM " --- Return to caller"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 FOR X=1 TO FILES
9915 CLOSE (CHANNELS[X],ERR=9920)
9920 NEXT X
9925 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9930 IF DNAME$<>"" THEN CALL "SYC.WD",DNAME$
9935 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
9940 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9945 IF SELECTION$="END" THEN LET SELECTION$=""
9950 EXIT
9999 END

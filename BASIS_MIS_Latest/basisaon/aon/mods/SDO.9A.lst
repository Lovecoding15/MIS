0010 REM "SDO - Sales Analysis to Excel Chart"
0020 REM "Program: SDO.9A"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="SAM-01",FILES$[3]="SYS-01"
0130 LET FILES$[4]="SAW-99"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET ARM01_DEV=CHANNELS[1],SAM01_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0185 LET SAW99_DEV=CHANNELS[4]
0200 REM " --- IOLists
0210 ARM01A: IOLIST A0$(1),A1$(1)
0220 SAM01A: IOLIST SAM01_0$,SAM01_X[ALL],SAM01_Y[ALL],SAM01_Z[ALL]
0230 SAW99A: IOLIST SAW99_0$,SAW99[ALL]
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST G0$,G1$,G2$,G3$,G4$,G5$,G6$,G7$,G8$
0400 REM " --- Parameters
0405 DIM P[2]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$,WHEN$=F0$(7,3)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM G[2]
0460 LET G[0]=NUM(G2$(1,2))
0470 DIM PERS$[G[0]]
0480 FOR X=1 TO G[0]; LET PERS$[X]=G3$((X-1)*3+235,3); NEXT X
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0500 REM " --- Init Data
0510 DIM A0$(8),A1$(230),SAM01_X[12],SAM01_Y[12],SAM01_Z[12],SAW99[12]
0530 LET H=0,H1$=N1$,H2$=N3$
0540 LET M0=LEN(M0$)
0550 LET M8$="(###)-###-####",M7$="00000"
0570 IF P[2]=9 THEN LET M7$="00000-0000"
0580 LET LETTERS$="bcdefghijklmn",COLUMN$=LETTERS$(G[0],1)
0600 REM " --- Setup Current Year"
0610 LET CURR_YEAR$=FNYY21_YY$(P4$(3,2))
0620 IF CURR_YEAR$<"50" THEN LET CURR_YEAR$="20"+CURR_YEAR$ ELSE LET CURR_YEAR$="19"+CURR_YEAR$
0700 REM " --- Background
0710 PRINT 'CF'
0720 FOR X=4 TO 10 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0750 PRINT 'SB',@(25,3),"From Customer:",@(22,5),"Through Customer:",@(26,7),"Current Year:",@(23,9),"Number of Years:",'SF',
1000 REM " --- Options
1005 PRINT 'CF',
1010 LET V0$="M",V1$="CE",V2$=FROM_CUST$,V3$=P1$,V4$="Enter Beginning Customer Number (<F1>=First/<F3>=Lookup)",V0=6,V1=40,V2=3,I0=0
1020 GOSUB 7000
1025 ON V3 GOTO 1050,1030,1010,1040,1090
1030 LET FROM_CUST$=""; PRINT @(V1,V2),"First",'CL',; GOTO 1090
1040 CALL "SYC.LC",V$
1050 IF V$="" THEN GOTO 1030
1060 FIND (ARM01_DEV,KEY=N0$+V$,DOM=1010)IOL=ARM01A
1070 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$)," ",A1$(1,30),'CL',
1080 LET FROM_CUST$=V$
1090 GOTO 3900
1100 REM " --- Through Customer"
1110 LET V0$="M",V1$="CE",V2$=THRU_CUST$,V3$=P1$,V4$="Enter Ending Customer Number (<F1>=Last/<F3>=Lookup)",V0=6,V1=40,V2=5
1120 GOSUB 7000
1125 ON V3 GOTO 1150,1130,1190,1140,1190
1130 LET THRU_CUST$=""; PRINT @(V1,V2),"Last",'CL',; GOTO 1190
1140 CALL "SYC.LC",V$
1150 IF V$="" THEN GOTO 1130
1155 IF V$<FROM_CUST$ THEN GOTO 1100
1160 FIND (ARM01_DEV,KEY=N0$+V$,DOM=1100)IOL=ARM01A
1170 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$)," ",A1$(1,30),'CL',
1180 LET THRU_CUST$=V$
1190 GOTO 3900
1200 REM " --- Current Year"
1210 LET V0$="Z",V1$="CER",V2$=CURR_YEAR$,V3$="",V4$="Enter Latest Year To Report",V0=4,V1=40,V2=7
1220 GOSUB 7000
1230 ON V3 GOTO 1240,1240,1290,1240,1290
1240 IF LEN(V$)=2 THEN IF V$<"50" THEN LET CURR_YEAR$="20"+V$ ELSE LET CURR_YEAR$="19"+V$
1250 IF LEN(V$)<>4 THEN GOTO 1200
1260 REM IF FNYY_YY21$(CURR_YEAR$(3,2))>P4$(3,2) THEN GOTO 1200
1270 LET CURR_YEAR$=V$
1280 PRINT @(V1,V2),CURR_YEAR$,'CL',
1290 GOTO 3900
1300 REM " --- Number of Years"
1310 LET V0$="N",V1$="CER",V2$=YEARS$,V3$="",V4$="Enter Number Of Years To Report (Maximum 5)",V0=1,V1=40,V2=9
1320 GOSUB 7000
1330 ON V3 GOTO 1340,1340,1390,1340,1390
1340 IF V>5 OR V<1 THEN GOTO 1300
1350 LET YEARS$=STR(V)
1380 PRINT @(V1,V2),YEARS$,'CL',
1390 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 IF ABS(I0)>1000 THEN GOTO 3990
3960 ON I0 GOTO 1000,1100,1200,1300,3990
4000 REM " --- Done?
4010 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V3=4 THEN GOTO 9900
4040 IF V$="N" THEN GOTO 1000
4050 IF V$<>"Y" THEN GOTO 4000
4060 CALL "SYC.FA",SAW99_DEV
4070 CALL "SYC.NB","Building:",MAX(M0,10),COLUMN
4075 DIM YRS$[NUM(YEARS$)]
4080 FOR X=NUM(YEARS$) TO 1 STEP -1
4085 LET TEMP=NUM(CURR_YEAR$)-X+1
4090 LET YRS$[X]=FNYY_YY21$(STR(TEMP:"00"))
4095 NEXT X
4100 REM " --- Build Work File"
4110 FOR X=1 TO NUM(YEARS$)
4120 READ (SAM01_DEV,KEY=N0$+FNYY_YY21$(YRS$[X](3,2))+FROM_CUST$,DOM=4121)
4130 LET SAM01_KEY$=KEY(SAM01_DEV,END=4290)
4140 IF POS(N0$+FNYY_YY21$(YRS$[X](3,2))=SAM01_KEY$)<>1 THEN GOTO 4290
4150 IF THRU_CUST$<>"" THEN IF SAM01_KEY$(5,6)>THRU_CUST$ THEN GOTO 4290
4160 READ (SAM01_DEV,KEY=SAM01_KEY$)IOL=SAM01A
4170 PRINT @(COLUMN,11),SAM01_KEY$(3,2)," ",SAM01_KEY$(5,6),
4180 DIM SAW99[12]
4200 LET SAW99_0$=N0$+SAM01_KEY$(5,6)+YRS$[X]
4210 READ (SAW99_DEV,KEY=SAW99_0$,DOM=4211)IOL=SAW99A
4220 FOR Y=0 TO 12
4230 LET SAW99[Y]=SAW99[Y]+SAM01_Z[Y]
4240 NEXT Y
4250 WRITE (SAW99_DEV,KEY=SAW99_0$)IOL=SAW99A
4260 GOTO 4130
4290 NEXT X
4990 GOTO 6000
5000 REM " --- Customer Break"
5005 IF T0$="" THEN GOTO 5100
5010 DIM ARM01_1$(30); FIND (ARM01_DEV,KEY=N0$+T0$(3,6))IOL=ARM01A
5015 CALL "sdofc_e.bb","newbook",RESP$,ERR$
5020 CALL "sdofc_e.bb","show",RESP$,ERR$
5025 CALL "sdofc_e.bb","newsheet Periods",RESP$,ERR$
5030 CALL "sdofc_e.bb","writerow "+CVS(A1$(1,30),2)+"\n\n",RESP$,ERR$
5035 CALL "sdofc_e.bb","format row=1,bold,center",RESP$,ERR$
5040 CALL "sdofc_e.bb","mergecells range=a1:"+COLUMN$+"1",RESP$,ERR$
5045 LET HEAD$=""; FOR HEAD=1 TO G[0]; LET HEAD$=HEAD$+"\t"+PERS$[HEAD]; NEXT HEAD
5050 CALL "sdofc_e.bb","writerow "+HEAD$,RESP$,ERR$
5055 FOR OUT=NUM(YEARS$) TO 1 STEP -1
5060 LET DET$=""; FOR DET=1 TO G[0]+1; LET DET$=DET$+OUT$[OUT,DET]+"\t"; NEXT DET
5065 CALL "sdofc_e.bb","writerow "+DET$,RESP$,ERR$
5070 NEXT OUT
5080 CALL "sdofc_e.bb","format range=$b$4:$"+COLUMN$+"$"+STR(NUM(YEARS$)+3)+",numberformat="+$22$+"$* #,##0_);[Red]$* (#,##0);$* 0_);_(@_)"+$22$,RESP$,ERR$
5090 CALL "sdofc_e.bb","addchart x=0,y=2,w=9,h=4,range=$a$3:$"+COLUMN$+"$"+STR(NUM(YEARS$)+3)+",title=Period Totals",RESP$,ERR$
5092 CALL "sdofc_e.bb","editchart serieslabels=1,catlabels=1",RESP$,ERR$
5095 GOSUB 10100
5097 CALL "sdofc_e.bb","leaveopen",RESP$,ERR$
5110 DIM OUT$[NUM(YEARS$),14](1,"0")
5120 FOR X=1 TO NUM(YEARS$)
5130 LET OUT$[X,1]=YRS$[X]
5140 NEXT X
5150 LET T0$=SAW99_0$(1,8)
5190 RETURN 
6000 REM " --- Setup for Export"
6010 DIM OUT$[NUM(YEARS$),14](1,"0")
6020 FOR X=1 TO NUM(YEARS$)
6030 LET OUT$[X,1]=YRS$[X]
6040 NEXT X
6050 READ (SAW99_DEV,KEY="",DOM=6051)
6100 REM " --- Build Output String"
6120 READ (SAW99_DEV,END=6900)IOL=SAW99A
6130 IF T0$<>SAW99_0$(1,8) THEN GOSUB 5000
6140 FOR X=1 TO NUM(YEARS$)
6150 IF SAW99_0$(9,4)<>YRS$[X] THEN GOTO 6190
6160 FOR Y=0 TO 12
6170 LET OUT$[X,Y+2]=STR(NUM(OUT$[X,Y+2])+SAW99[Y])
6180 NEXT Y
6190 NEXT X
6200 GOTO 6100
6970 GOSUB 5000
6980 CALL "sdofc_e.bb","leaveopen",RESP$,ERR$
6990 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8075 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
10100 REM " --- Build Summary Page"
10110 CALL "sdofc_e.bb","newsheet Summary",RESP$,ERR$
10120 CALL "sdofc_e.bb","writerow "+CVS(A1$(1,30),2)+"\n\n",RESP$,ERR$
10125 CALL "sdofc_e.bb","format row=1,bold,center",RESP$,ERR$
10130 CALL "sdofc_e.bb","mergecells range=a1:"+LETTERS$(NUM(YEARS$),1)+"1",RESP$,ERR$
10135 LET SUMM$="\t"; FOR YEARS=4 TO 3+NUM(YEARS$); LET SUMM$=SUMM$+"=Periods!a"+STR(YEARS)+"\t"; NEXT YEARS
10140 CALL "sdofc_e.bb","writerow "+SUMM$,RESP$,ERR$
10150 LET SUMM$="Sales\t"; FOR YEARS=4 TO 3+NUM(YEARS$); LET SUMM$=SUMM$+"=sum(Periods!b"+STR(YEARS)+":Periods!"+COLUMN$+STR(YEARS)+")\t"; NEXT YEARS
10160 CALL "sdofc_e.bb","writerow "+SUMM$,RESP$,ERR$
10170 CALL "sdofc_e.bb","format range=$b$4:$"+LETTERS$(NUM(YEARS$),1)+"$4,numberformat="+$22$+"$* #,##0_);[Red]$* (#,##0);$* 0_);_(@_)"+$22$,RESP$,ERR$
10210 CALL "sdofc_e.bb","addchart x=0,y=1,w=5,h=2,range=$a$3:$"+LETTERS$(NUM(YEARS$),1)+"$4,title=Sales",RESP$,ERR$
10220 CALL "sdofc_e.bb","editchart legend=no,catlabels=1",RESP$,ERR$
10300 REM " --- Remove all extraneous worksheets"                              
10310 CALL "sdofc_e.bb","getsheets",RESP$,ERR$
10315 LET RESP$=RESP$+$0D0A$
10320 IF LEN(RESP$)=0 THEN GOTO 10390
10330 LET RMSHEET$=RESP$(1,POS($0D0A$=RESP$)-1)
10340 LET RESP$=RESP$(POS($0D0A$=RESP$)+2)
10350 IF RMSHEET$="Periods" OR RMSHEET$="Summary" THEN GOTO 10320
10360 CALL "sdofc_e.bb","activatesheet "+RMSHEET$,RES$,ERR$
10370 CALL "sdofc_e.bb","delsheet",RES$,ERR$
10380 GOTO 10320
10390 RETURN 

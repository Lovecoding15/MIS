0010 REM "IVM - Item Maintenance (Description)"
0020 REM "Program IVM.MH"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 PRECISION P[1]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$,A6$(1),A[ALL]
0220 IVM11A: IOLIST B0$(1),B1$
0310 IVM03E: IOLIST X0$(1)
0500 REM " --- Initializations"
0510 DIM B0$(22),N[3],O[3],DESC_DEV[3]
0550 LET I0=0,N[1]=15,N[2]=27,N[3]=39,O[1]=1,O[2]=P[3]+1,O[3]=O[2]+P[4]
0570 LET DESC_DEV[1]=IVM11_DEV,DESC_DEV[2]=IVM21_DEV,DESC_DEV[3]=IVM31_DEV
0580 LET OLDDESC$=A1$
1300 REM " --- Display record"
1310 GOSUB 5000
1390 ON O0 GOTO 4000,4000,2000
2000 REM " --- Description segment"
2005 LET X=I0+1,X$=FNP$(I2$(N[X],12))
2010 IF P[X+2]=0 THEN GOTO 3900
2015 IF X$="" THEN LET X$="Description"
2020 LET V4$="Enter The "+X$+" For This Item"
2025 IF I2$(50+X,1)="Y" THEN LET V4$=V4$+" (<F3>=Lookup)"
2030 LET V0$="S",V1$="C",V2$=A1$(O[X],P[X+2]),V3$="",V0=P[X+2],V1=DP[X],V2=4
2035 GOSUB 7000
2040 ON V3 GOTO 2100,2000,3900,2050,3900
2050 IF I2$(50+X,1)<>"Y" THEN GOTO 2000
2060 GOSUB 6000
2090 IF V$="" THEN GOTO 2000
2100 REM " --- Retrieve/store description"
2110 IF I2$(50+X,1)<>"Y" THEN GOTO 2150
2115 IF FNP$(V$)="" THEN GOTO 2150
2120 LET B0$(1)=N0$+V$
2130 FIND (DESC_DEV[X],KEY=B0$,DOM=2000)IOL=IVM11A
2150 LET A1$(O[X],P[X+2])=V$
2190 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2000,2000,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 GOSUB 5000
4020 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4030 ON V3 GOTO 4040,4000,4000,4000,4900
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 DIM X0$(43)
4125 LET X0$(1)=A0$(1,2)+"E"+OLDDESC$,X0$(24)=A0$(3)
4130 REMOVE (IVM03_DEV,KEY=X0$,DOM=4135)
4135 LET X0$(1)=A0$(1,2)+"E"+A1$,X0$(24)=A0$(3)
4140 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03E
4150 WRITE (IVM01_DEV,KEY=A0$)IOL=IVM01A
4190 GOTO 4900
4900 REM " --- Run menu overlay"
4990 RUN "IVM.MA"
5000 REM " --- Display"
5010 LET V=1
5020 FOR X=3 TO 5
5030 IF P[X]=0 THEN GOTO 5080
5040 PRINT 'SB',@(DP[X-2],4),A1$(V,P[X]),'SF',
5050 LET V=V+P[X]
5080 NEXT X
5090 RETURN 
6000 REM " --- Description Lookup"
6010 LET RECORD$="",NUMBER=1,TITLE$="Description Lookup"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6025 LET CHANNEL=DESC_DEV[X],DESCRIPTION$[0]=X$,DESCRIPTION$[1]="Description"
6030 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=20
6040 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6050 LET ROW=3,COLUMN=79-LENGTH[0]-LENGTH[1]-NUMBER*4,V$=""
6070 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

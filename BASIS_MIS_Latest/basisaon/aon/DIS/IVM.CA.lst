0010 REM "IVM - Warehouse Maintenance"
0020 REM "Program IVM.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="IVM-10"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],IVM10_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 IVM10C: IOLIST A0$(1)
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$,M4$,M5$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0500 REM " --- Initializations"
0510 DIM A0$(165)
0550 LET K9=5,M9$="(###)-###-####",R$="C",A0$(1,3)=N0$+R$
0700 REM " --- Background"
0750 PRINT 'SB',@(1,4),"Warehouse:",@(6,5),"Name:",@(3,6),"Address:",@(5,10),"State:",@(2,11),"Zip Code:",@(5,12),"Phone:",'SF',
0900 REM " --- Position file"
0990 READ (IVM10_DEV,KEY=N0$+R$,DOM=1000)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",I0=0,V2$=KEY(IVM10_DEV,END=1050)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4)
1050 LET V4$="Enter A Valid Warehouse ID (<F3>=Lookup)"
1055 LET V0$="S",V1$="KCE",V3$="",V0=2,V1=12,V2=4,I0=0
1060 GOSUB 7000
1065 ON V3 GOTO 1080,1000,1000,1070,4500
1070 CALL "IVC.LK",3,V1,V2,V$
1080 IF V$="" THEN GOTO 0900
1085 PRINT @(V1,V2),V$,
1090 LET A0$(4,2)=V$
1200 REM " --- Find record"
1210 FIND (IVM10_DEV,KEY=A0$(1,K9),DOM=1400)IOL=IVM10C
1300 REM " --- Display"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record"
1510 LET A0$(6)=""
2000 REM "--- Name"
2010 LET V0$="S",V1$="CER",V2$=A0$(6,24),V3$="",V0=24,V1=12,V2=5
2020 LET V4$="Enter warehouse description",I0=1
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 LET A0$(6,24)=V$
2060 GOTO 3900
2200 REM "--- Address"
2210 LET X=I0-2,V0$="S",V1$="CE",V3$="",V0=24,V1=12,V2=6+X
2215 IF X=3 THEN LET V0=22
2220 LET V2$=A0$(30+X*24,V0),V4$="Enter address line "+STR(X+1)
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 LET A0$(30+X*24,V0)=V$
2260 GOTO 3900
2300 REM " --- State"
2310 LET V4$="Enter The State Or Province Code For This Warehouse"
2320 LET V0$="S",V1$="C",V2$=A0$(124,2),V3$="",V0=2,V1=12,V2=10
2330 GOSUB 7000
2340 ON V3 GOTO 2350,2300,3900,2300,3900
2350 LET A0$(124,2)=V$
2390 GOTO 3900
2400 REM " --- Zip Code"
2410 LET V0$="S",V1$="CE",V2$=A0$(136,9),V3$="",V0=9,V1=12,V2=11
2420 LET V4$="Enter The Zip/Postal Code For This Warehouse"
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 LET A0$(136,9)=V$
2460 PRINT @(12,11),FNZ$(A0$(136)),
2490 GOTO 3900
2500 REM "--- Phone"
2510 LET V0$="H",V1$="CE",V2$=A0$(126,10),V3$=M9$,V0=10,V1=12,V2=12
2520 LET V4$="Enter optional telephone number"
2530 GOSUB 7000
2540 ON V3 GOTO 2550,2500,3900,2500,3900
2550 LET A0$(126,10)=V$
2560 GOTO 3900
3900 REM " --- Next field"
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,2000,2200,2200,2200,2200,2300,2400,2500,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CR",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record"
4120 WRITE (IVM10_DEV,KEY=A0$(1,K9))IOL=IVM10C
4190 GOTO 1000
4200 REM " --- Delete record"
4220 REMOVE (IVM10_DEV,KEY=A0$(1,K9),DOM=4230)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4550 ON POS(V$="YN") GOTO 4500,4560,9900
4560 GOSUB 6000
4590 GOTO 9900
5000 REM " --- Display"
5010 PRINT @(12,5),A0$(6,24),@(12,6),A0$(30,24),@(12,7),A0$(54,24),@(12,8),A0$(78,24),@(12,9),A0$(102,22),@(12,10),A0$(124,2),@(12,11),FNZ$(A0$(136)),@(12,12),FNF$(A0$(126,10),M9$),
5090 RETURN 
6000 REM " --- Listing"
6010 DIM HEADINGS$[2]
6020 CALL "SYC.GA",7,1,"","",STATUS
6030 IF STATUS THEN GOTO 9900
6040 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$+" Listing",HEADINGS=2
6045 LET WHEN$=F0$(7,3),CLOCK$="",PAGE=0,WIDTH=80,L9=55,L=L9+1
6080 CALL "SYC.NB","Printing",2,COLUMN
6090 READ (IVM10_DEV,KEY=N0$+R$,DOM=6100)
6100 REM " --- Print next record"
6110 LET K$=KEY(IVM10_DEV,END=6200)
6115 IF POS(N0$+R$=K$)<>1 THEN GOTO 6200
6120 READ (IVM10_DEV)IOL=IVM10C
6125 PRINT @(COLUMN,11),A0$(4,2),
6130 LET L=L+2,N=N+1
6135 IF L+6>L9 THEN GOSUB 6300
6140 PRINT (7)""
6145 PRINT (7)A0$(4,2),@(6),A0$(6,24),@(38),FNF$(A0$(126,10),M9$)
6150 LET A0$(102,24)=FNP$(A0$(102,22)+A0$(124,2)),W$=A0$(30,96)+A0$(136,9)
6155 CALL "SYC.AA",W$,24,4,9,30
6160 FOR X=1 TO 150 STEP 30
6165 IF FNP$(W$(X,30))="" THEN GOTO 6180
6170 PRINT (7)@(6),W$(X,30)
6175 LET L=L+1
6180 NEXT X
6190 GOTO 6100
6200 REM " --- Listing completed"
6210 PRINT (7)""
6220 PRINT (7)N:"###0-","Warehouse(s)"
6290 RETURN 
6300 REM " --- Heading"
6310 LET L=HEADINGS+3
6320 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
6330 IF STATUS>0 THEN EXITTO 9900
6370 PRINT (7)"Whse  Name/Address",@(38),"Telephone"
6380 PRINT (7)""
6390 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((H0-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

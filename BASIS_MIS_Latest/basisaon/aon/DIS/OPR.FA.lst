0010 REM "OPR - Customer/Job Report"
0020 REM "Program OPR.FA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " ---Open/Lock Files "
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ARM-09",FILES$[2]="ARM-01",FILES$[3]="IVM-01"
0135 LET FILES$[4]="SYS-01"
0140 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET ARM09_DEV=CHANNELS[1],ARM01_DEV=CHANNELS[2],IVM01_DEV=CHANNELS[3]
0170 LET SYS01_DEV=CHANNELS[4]
0200 REM " ---IOLISTS "
0230 ARM01A: IOLIST *,X0$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters "
0405 DIM P[2]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0435 IF P3$(12,1)<>"Y" THEN GOTO 4900
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0500 REM " --- Initialize Data "
0510 DIM J0$(20,"0")
0550 LET M0=LEN(M0$),M1=LEN(M1$),P9$="",P8$=""
0700 REM " --- Background "
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,6,0
0750 PRINT 'SB',@(20,3),"Beginning Customer:",@(23,5),"Ending Customer:",'SF',
1000 REM " --- From Customer "
1010 PRINT 'CF'
1020 LET V0$="M",V1$="KC",V2$=P9$,V3$=P1$,V0=6,V1=40,V2=3
1025 LET V4$="Enter The Beginning Customer (<F1>=First/<F3>=Lookup)"
1030 GOSUB 7000
1040 IF V3=4 THEN GOTO 9900
1042 IF V3<>3 THEN GOTO 1050
1044 CALL "SYC.LC",V$
1046 IF V$="" THEN GOTO 1020
1050 IF V3=1 THEN LET V$=""
1060 IF V$=J0$(1,LEN(V$)) THEN LET V$=""
1065 LET P9$=V$
1070 IF P9$="" THEN GOTO 1100
1080 FIND (ARM01_DEV,KEY=N0$+P9$,DOM=1000)IOL=ARM01A
1090 PRINT @(V1,V2),FNF$(P9$,M0$),@(V1+M0+1,V2),X0$(1,30),
1095 GOTO 3900
1100 PRINT @(V1,V2),"First",'CL',
1190 GOTO 3900
1200 REM " --- Through Customer "
1220 LET V0$="M",V1$="KC",V2$=P8$,V3$=P1$,V0=6,V1=40,V2=5
1225 LET V4$="Enter The Ending Customer (<F1>=Last/<F3>=Lookup)"
1230 GOSUB 7000
1232 IF V3<>3 THEN GOTO 1240
1234 CALL "SYC.LC",V$
1236 IF V$="" THEN GOTO 1220
1250 IF V3=1 THEN LET V$=""
1260 IF V$=J0$(1,LEN(V$)) THEN LET V$=""
1265 LET P8$=V$
1270 IF P8$="" THEN GOTO 1300
1280 FIND (ARM01_DEV,KEY=N0$+P8$,DOM=1200)IOL=ARM01A
1290 PRINT @(V1,V2),FNF$(P8$,M0$),@(V1+M0+1,V2),X0$(1,30),
1295 GOTO 3900
1300 PRINT @(V1,V2),"Last",'CL',
1390 GOTO 3900
3900 REM " --- Next Field "
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,4000
4000 REM " --- Correct? "
4010 LET I0=0,V4$="Is The Above Information Correct (Y/N)?"
4020 LET V1=FNV(V4$),V2=22
4025 CALL "SYC.YN",1,V4$,2,V$,V3
4030 IF V3=4 THEN GOTO 9900
4080 ON POS(V$="YN") GOTO 4000,4090,1000
4090 RUN "OPR.FB"
4900 REM " --- Job Numbers not in use"
4910 DIM MESSAGE$[2]
4920 LET MESSAGE$[0]="Order Processing Parameters Indicate That Job Numbers Are"
4930 LET MESSAGE$[1]="Not Being Used. This Task May Not Be Used Unless Job Numbers"
4940 LET MESSAGE$[2]="Are Being Used (<Enter>=Continue)"
4950 CALL "SYC.XA",3,MESSAGE$[ALL],2,-1,-1,V$,V3
4990 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions "
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

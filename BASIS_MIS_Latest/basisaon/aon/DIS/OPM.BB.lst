0010 REM "OPM - Contract Pricing Table Listing"
0020 REM "Program OPM.BB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files "
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS>0 THEN GOTO 9900
0200 REM " --- IOLISTS "
0210 IVM06A: IOLIST A0$,A1$,A[ALL]
0220 IVM01A: IOLIST *,X1$(1)
0230 ARM01A: IOLIST *,X0$(1)
0500 REM " --- Initialize Data "
0510 DIM HEADINGS$[2],X0$(30),X1$(I[1]+I[2]+I[3]),O[5]
0550 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$+" Listing",CLOCK$="",L9=55
0560 LET WHEN$=F0$(7,3),L=L9+1,H0=132,H=0,T0=0,M4$="####0",M4=LEN(M4$)
0570 LET NF$="(Not On File)",O[5]=H0-M3-1,O[4]=O[5]-M2,O[3]=O[4]-M4
0580 LET O[2]=O[3]-22,O[1]=O[2]-10,O[0]=O[1]-10
0700 REM " --- Background "
0710 CALL "SYC.NB","Printing",M0+1+I[0],COLUMN
1000 REM " --- Initial Read "
1020 READ (IVM06_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Main Read "
1110 READ (IVM06_DEV,END=4000)IOL=IVM06A
1120 IF POS(N0$=A0$)<>1 THEN GOTO 4000
1130 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$)," ",A0$(9,I[0]),
1400 REM " --- Additional Reads"
1410 LET X0$(1)=NF$,X1$(1)=NF$
1420 FIND (IVM01_DEV,KEY=N0$+A0$(9,20),DOM=1430)IOL=IVM01A
1430 FIND (ARM01_DEV,KEY=A0$(1,8),DOM=1440)IOL=ARM01A
1500 REM " --- How many levels?
1510 LET LEVELS=0
1520 FOR X=1 TO 9
1530 IF A[X]=0 AND A[X+10]=0 THEN GOTO 1590
1540 LET LEVELS=X
1590 NEXT X
2000 REM " --- Print contract"
2010 LET NEEDED=3
2020 IF LEVELS>1 THEN LET NEEDED=NEEDED+(LEVELS-1)
2030 LET L=L+NEEDED,X=0,T0=T0+1
2040 IF L>L9 THEN GOSUB 5000
2050 PRINT (7)""
2060 PRINT (7)FNF$(A0$(3,P[0]),M0$),@(M0+1),X0$,@(O[0]),FNB$(A1$(21,3)),@(O[1]),FNB$(A1$(24,3)),@(O[2]),A1$(1,20),@(O[3]),X+1:M4$,@(O[4]),A[X]:M2$,@(O[5]),A[X+10]:M3$
2070 PRINT (7)@(2),A0$(9,I[0])," ",X1$,
2080 IF LEVELS>0 THEN GOTO 2200
2085 PRINT (7)""
2090 GOTO 3900
2200 REM " --- Print additional contract levels"
2210 FOR X=1 TO LEVELS
2220 PRINT (7)@(O[3]),X+1:M4$,@(O[4]),A[X]:M2$,@(O[5]),A[X+10]:M3$
2290 NEXT X
3900 REM 
3990 GOTO 1100
4000 REM " --- All done"
4010 IF L>L9 THEN GOSUB 5000
4020 PRINT (7)""
4030 PRINT (7)T0," Contract Prices"
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=0
5020 CALL "SYC.HA",7,HEADINGS$[ALL],2,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)@(O[0]+2),"Start",@(O[1]+2),"Stop"
5050 PRINT (7)"Customer/Item",@(O[0]+2),"Date",@(O[1]+2),"Date",@(O[2]),"Comment",@(O[3]),"Level",@(O[4]+M2-4),"Qty",@(O[5]+M3-6),"Price"
5090 RETURN 
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "OPC - Order Processing SpeedSearch (Open Orders By Inventory Item)"
0020 REM "Program OPC.QF"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0060 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open/Lock Files"
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ARM-01",FILES$[2]="ARE-03",FILES$[3]="ARE-13"
0140 LET FILES$[4]="ARE-07",FILES$[5]="IVM-01",FILES$[6]="IVM-10"
0145 LET FILES$[7]="IVM-14"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0170 LET ARM01_DEV=CHANNELS[1],ARE03_DEV=CHANNELS[2],ARE13_DEV=CHANNELS[3]
0180 LET ARE07_DEV=CHANNELS[4],IVM01_DEV=CHANNELS[5],IVM10_DEV=CHANNELS[6]
0190 LET IVM14_DEV=CHANNELS[7]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1)
0220 ARE03A: IOLIST B0$(1),B[ALL]
0230 ARM01A: IOLIST C0$(1),C1$(1)
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,I5$,M1$,M2$,M3$
0310 IVM10C: IOLIST X0$(1)
0320 ARE13A: IOLIST W0$(1),W1$(1),W[ALL]
0400 REM " --- Parameters"
0405 DIM P[2],I[5]
0410 LET X$=STBL("!SPEEDSEARCH"),N0$=X$(1,2),MAXPAGE=NUM(X$(66,2))
0415 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0420 LET SNAME$=FNP$(X$(19,8)),SWIDTH=NUM(X$(43,2)),SHEIGHT=NUM(X$(45,2))
0425 LET MAXOPT=NUM(X$(57,2)),SYS01_DEV=NUM(STBL("!SYS01")),ONAME$=FNP$(X$(27,8))
0430 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0435 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0440 LET PAGES=INT((DSZ-5000)/4000),TNAME$=FNP$(X$(3,8))
0445 IF PAGES<3 THEN GOTO 9900
0450 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0455 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9900)IOL=ARS01A
0460 LET P[0]=NUM(P2$(1,2))
0465 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9900)IOL=IVS01A
0470 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1))
0475 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0480 CALL "SYC.RM",M1$,M1,7,10
0500 REM " --- Initializations"
0505 PRECISION I[1]
0510 DIM A0$(22),A1$(60),A2$(64),B0$(101),B[10],C0$(8),C1$(30),PARAMS[3]
0520 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),OPTLIST$[2,MAXOPT,6]
0530 DIM X0$(32),W0$(52),W1$(64),W[14],FILES[14],ITEM$[6],ITEM[12]
0550 LET D_STATUS=0,MAXROW=HEIGHT-5,PAGE=1,PGM$=PGM(-2),H0=WIDTH-2
0560 LET A0$(1)=N0$+OPTION$[3],M1=LEN(M1$),M2=LEN(M2$),W=WIDTH-14-I[0]
0570 LET B0$(1)=N0$+"  "+OPTION$[3],B0$(18,3)="000",M=WIDTH-14-M1,V=M1
0580 LET FILES[1]=IVM01_DEV,FILES[14]=IVM14_DEV,PARAMS[0]=I[0],M=WIDTH-M1-14
0590 LET PARAMS[1]=I[3],PARAMS[2]=I[4],PARAMS[3]=I[5],WHSE$=I3$(6,2)
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,ONAME$
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0720 LET HEADING$(2)="Order   Seq Sts Customer"
0730 LET HEADING$(H0-19)=" Ord Date Ship Date"
0740 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0750 PRINT @(0,0),'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",SNAME$,0
0920 PRINT 'CF',
0930 CALL "SYC.WB",TNAME$,0
0940 PRINT 'CS',
0990 GOTO 1100
1000 REM " --- Process key field"
1010 GOSUB 6000
1015 CALL "SYC.WB",TNAME$,0
1020 PRINT @(0,0),'CS',@(1,0),'SB',"Item:",'SF',
1025 LET V2$="",V2$=KEY(IVM01_DEV,END=1040)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1035 IF V2$<>"" THEN LET V2$=V2$(3,I[0])
1040 LET ACTION$="V",V1=7,V2=0,V4$=""
1045 CALL "IVC.IA",ACTION$,V1,V2,FILES[ALL],N0$,V2$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1050 ON V3 GOTO 1070,1000,1000,1000,3800,3900,3800
1070 LET V$=FNP$(ITEM$[0])
1080 IF V$="" THEN GOTO 1000
1090 LET A0$(1)=N0$+V$
1100 REM " --- Retrieve record for display"
1110 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,SHEIGHT-2],X$(WIDTH-2)
1120 DIM DESCRIPTION$(WIDTH-I[0]-10)
1130 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,SELECTION$="",BEGDATE$="",ENDDATE$=""
1140 READ (IVM01_DEV,KEY=A0$,DOM=1000)IOL=IVM01A
1150 LET DESCRIPTION$(1)=FNITEM$(A1$,I[3],I[4],I[5])
1160 PRINT @(1,0),'CL','SB',"Item: ",'SF',A0$(3,P[0])," ",DESCRIPTION$
1170 LET X$="",X1$=A0$(3,P[0]),X2$=PARAM$
1180 CALL "SYC.QB",X$,X1$,X2$
1190 IF LEN(WHSE$)>2 THEN LET WHSE$=WHSE$(1,2)
1200 REM " --- Warehouse ID"
1210 PRINT @(1,1),'CL','SB',"Whse:",'SF',
1220 LET V0$="S",V1$="KE",V2$=WHSE$,V3$="",V4$="",V0=2,V1=7,V2=1
1230 GOSUB 7000
1240 ON V3 GOTO 1260,1200,1000,1250,3800,3900,3800
1250 CALL "IVC.LK",3,V1,V2,V$
1260 LET X0$(1)=N0$+"C"+V$,WHSE$=X0$(4,2)
1270 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=1200)IOL=IVM10C
1280 PRINT @(V1,V2),WHSE$," ",X0$(6,20),
1300 REM " --- Beginning Date"
1310 PRINT @(1,2),'CL','SB',"Beginning Date:",@(28),"Ending Date:",'SF',
1320 LET V0$="D",V1$="E",V2$=BEGDATE$,V3$="",V4$="",V0=3,V1=17,V2=2
1330 GOSUB 7000
1340 ON V3 GOTO 1350,1300,1200,1300,3800,3900,3800
1350 IF V$="" THEN PRINT @(V1,V2),"First   ",
1360 LET BEGDATE$=V$
1400 REM " --- Ending Date"
1410 LET V0$="D",V1$="E",V2$=ENDDATE$,V3$="",V4$="",V0=3,V1=41,V2=2
1420 GOSUB 7000
1430 ON V3 GOTO 1440,1400,1300,1400,3800,3900,3800
1440 IF V$="" THEN PRINT @(V1,V2),"Last    ",
1450 LET ENDDATE$=V$
1900 REM " --- Position file"
1910 DIM X$(25)
1980 LET X$(1)=A0$(1,2)+WHSE$+A0$(3)
1990 READ (ARE07_DEV,KEY=X$,DOM=2000)
2000 REM " --- Read next ARE-07 record"
2010 LET K$=KEY(ARE07_DEV,END=3000)
2020 IF POS(K$(1,LEN(X$))=X$)<>1 THEN GOTO 3000
2030 READ (ARE07_DEV)
2040 LET B0$(1)=K$(1,2)+K$(25,2)+K$(37,6)+K$(27,7)+"000"
2050 LET W0$(1)=B0$(1,17)+K$(34,3)
2060 FIND (ARE03_DEV,KEY=B0$(1,20),DOM=2900)IOL=ARE03A
2070 IF BEGDATE$<>"" THEN IF B0$(77,3)<BEGDATE$ THEN GOTO 2900
2080 IF ENDDATE$<>"" THEN IF B0$(77,3)>ENDDATE$ THEN GOTO 2900
2090 FIND (ARE13_DEV,KEY=W0$(1,20),DOM=2900)IOL=ARE13A
2500 REM " --- Display page?"
2510 IF L<MAXROW THEN GOTO 2600
2520 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2530 GOSUB 5000
2540 IF DONE THEN GOTO 4000
2570 IF ANSWER$<>"" THEN GOTO 3500
2590 LET L=1,X1=1
2700 REM " --- Middle window record"
2710 DIM G$(WIDTH-2),C1$(30),STATUS$(3)
2715 IF B0$(101,1)="C" THEN IF B0$(22,1)="O" THEN LET B0$(22,1)="C"
2720 LET STATUS$(1,1)=B0$(22,1),SHIPDATE$=B0$(27,3)
2725 IF B0$(68,1)="Y" THEN LET STATUS$(2,1)="P"
2730 IF B0$(69,1)="Y" THEN LET STATUS$(3,1)="L"
2735 IF POS(B0$(69,1)="YN")>0 THEN LET STATUS$(3,1)=B0$(69,1)
2740 IF FNP$(W1$(41,3))<>"" THEN LET SHIPDATE$=W1$(41,3)
2760 LET C0$(1)=B0$(1,2)+B0$(5,6),C1$(1)="Unknown Customer"
2765 FIND (ARM01_DEV,KEY=C0$,DOM=2770)IOL=ARM01A
2770 LET CUSTOMER$=FNF$(C0$(3,P[0]),M0$)+" "+C1$
2775 LET G$(2)=W0$(11,7)+" "+W0$(18,3)+" "+STATUS$+" "+CUSTOMER$
2780 LET G$(H0-19)=" "+FNB$(B0$(77,3))+" "+FNB$(SHIPDATE$)
2790 LET LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=A0$(3)
2800 REM " --- Bottom window record"
2810 LET G$(1)="",COMMITTED$="Committed"
2820 IF W1$(44,1)="N" THEN LET COMMITTED$="Uncommitted"
2825 LET G$(1)="    {Order Qty}"+STR(W[2]:M2$)+" "+COMMITTED$
2830 LET G$(M+6)="{Price}"+STR(W[1]:M1$),MORE$[PAGE,X1,1]=G$
2835 LET G$(1)="{Backorder Qty}"+STR(W[3]:M2$)
2840 LET G$(M)="  {Extension}"+STR(W[6]:M1$),MORE$[PAGE,X1,2]=G$
2845 LET G$(1)="  {Shipped Qty}"+STR(W[4]:M2$)
2850 LET G$(M)="{Order Total}"+STR(B[6]:M1$),MORE$[PAGE,X1,3]=G$
2880 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),LLIST$[PAGE,X1,5]=SNAME$
2890 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2990 GOTO 2000
3000 REM " --- No more records"
3010 IF X0>0 THEN GOTO 3200
3020 DIM MESSAGE$[1]
3030 LET MESSAGE$[0]="No Open Orders Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 LET SELECTION$="PRIOR"
3090 GOTO 3500
3200 REM " --- Display last page"
3210 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
3220 GOSUB 5000
3230 IF DONE THEN GOTO 4000
3500 REM " --- Process option window"
3510 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3520 LET ACTION$="O",CHOICE1$=A0$(3),CHOICE2$=""
3530 GOSUB 5000
3540 IF CONTROL=1 THEN GOTO 1000
3590 GOTO 4000
3800 REM " --- Exit"
3810 LET OPTION$[1]="EXIT"
3890 GOTO 4000
3900 REM " --- Startover"
3910 LET OPTION$[1]="STARTOVER"
4000 REM " --- All done"
4010 GOSUB 6000
4020 CALL "SYC.WB",TNAME$,0
4030 PRINT @(0,0),'CL',@(0,1),'CL',@(0,2),'CL',
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CS',
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9950 EXIT 
9999 END

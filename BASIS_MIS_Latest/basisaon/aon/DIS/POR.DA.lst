0010 REM "POR - Purchase Order Receipt Register (Receipts by Vendor Overlay)"
0020 REM "Program POR.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 BEGIN 
0085 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=27
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="SYS-01",FILES$[2]="POE-04",FILES$[3]="POE-14"
0120 LET FILES$[4]="POE-24",FILES$[5]="POM-02",FILES$[6]="POW-03"
0125 LET FILES$[7]="POW-04",FILES$[8]="POW-05",FILES$[9]="POW-08"
0130 LET FILES$[10]="POW-09",FILES$[11]="APM-01",FILES$[12]="IVM-01"
0135 LET FILES$[13]="IVM-02",FILES$[14]="IVM-03",FILES$[15]="IVM-10"
0140 LET OPTIONS$[2]="L",OPTIONS$[6]="C",OPTIONS$[7]="C"
0145 LET OPTIONS$[8]="C",OPTIONS$[9]="C",OPTIONS$[10]="C"
0150 CALL "SYC.DA",1,1,15,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],POE04_DEV=CHANNELS[2],POE14_DEV=CHANNELS[3]
0165 LET POE24_DEV=CHANNELS[4],POM02_DEV=CHANNELS[5],POW03_DEV=CHANNELS[6]
0170 LET POW04_DEV=CHANNELS[7],POW05_DEV=CHANNELS[8],POW08_DEV=CHANNELS[9]
0175 LET POW09_DEV=CHANNELS[10],APM01_DEV=CHANNELS[11],IVM01_DEV=CHANNELS[12]
0180 LET IVM02_DEV=CHANNELS[13],IVM03_DEV=CHANNELS[14],IVM10_DEV=CHANNELS[15]
0200 REM " --- IOLIST's"
0210 POE04A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0220 APM01A: IOLIST B0$(1),B1$(1)
0230 IVM01A: IOLIST C0$(1),C1$(1),C2$(1),C3$,C4$,C5$,C6$(1)
0240 IVM02A: IOLIST D0$(1),D1$(1),D2$,D[ALL]
0250 WOE01A: IOLIST S0$(1),S1$(1),S[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,I5$,I6$,I7$,I8$,I9$,I10$
0280 POS01A: IOLIST P0$,P1$,P2$,P3$,P4$
0285 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0290 APS01A: IOLIST X$,U1$,U2$,U3$,X$,M0$
0300 ARS01A: IOLIST X$,P5$,P6$,P7$,X$,P8$
0310 POE14A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0315 POE24A: IOLIST L0$(1),L1$(1),L2$,L[ALL]
0320 POM02A: IOLIST Y0$(1),Y1$(1),Y2$(1)
0330 POW03A: IOLIST R0$(1),R[ALL]
0340 POW04A: IOLIST X4$(1)
0350 POW05A: IOLIST X5$(1)
0360 POW08A: IOLIST X8$(1)
0370 POW09A: IOLIST X9$(1)
0380 ARM10D: IOLIST Z0$(1),Z1$(1)
0390 WOM10A: IOLIST U0$(1),U1$(1)
0400 REM " --- Parameters"
0405 DIM I[7],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO",AP$="N",AR$="N",SF$="N",LS$="N",LF$="N"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=POS01A
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0435 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0440 LET VENDLEN=NUM(U2$(1,2)),I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1))
0445 LET I[2]=NUM(I2$(7,2)),I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2))
0450 LET I[5]=NUM(I2$(13,2)),I[6]=NUM(I2$(3,2)),I[7]=FNYY_YEAR(I4$(3,2))
0455 LET G[0]=NUM(G2$(1,2)),G[1]=NUM(G2$(3,2)),G[2]=NUM(G2$(5,2))
0460 LET G[3]=NUM(G2$(7,2)),G[4]=NUM(G2$(9,2)),REPLENISHMENT$=U3$(21,1)
0463 CALL "SYC.VA","AR",INFO$[ALL]
0465 LET AR$=INFO$[20],OP$="N",DISTCODE$="N"
0467 IF AR$<>"Y" THEN GOTO 0480
0470 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0473 CALL "SYC.VA","OP",INFO$[ALL]
0475 LET CUSTLEN=NUM(P6$(1,2)),OP$=INFO$[20],DISTCODE$=P7$(11,1)
0480 CALL "SYC.VA","WO",INFO$[ALL]
0485 LET SF$=INFO$[20],GL$="N",STATUS=0,SOURCE$=PGM(-2)
0490 CALL "GLC.BA",ERR=0495,SOURCE$,N2$,GLW11$,GL$,STATUS
0495 IF STATUS THEN GOTO 9900
0500 REM " --- Initializations"
0505 PRECISION I[1]
0510 DIM A0$(22),A1$(160),A[8],B0$(8),B1$(195),O[13],X4$(53),X5$(93)
0515 DIM C0$(22),C1$(60),C2$(64),C6$(G[4]*6),D0$(24),D1$(64),D[20]
0520 DIM W0$(25),W1$(48),W2$(32),W3$(22),W4$(40),W[12],DESCRIPTION$(40)
0525 DIM HEADINGS$[3],TOTALS[5],RECEIPT[5],VENDOR[5],REPORT[5],X8$(37),X9$(43)
0530 DIM Y0$(4),Y1$(32),Y2$(G[4]),Z0$(32),Z1$(11*G[4]),R0$(24),R[1]
0535 DIM L0$(28),L1$(20),L[1],DIF$[99],LINEQTY[99],LSQTY[99],FACTOR[99]
0540 DIM GLFILES[20],GLPARAMS$[20],GLPARAMS[20],W$[10]
0550 IF POS(I3$(17,1)="LS")>0 THEN LET LS$="Y"
0555 IF POS(I3$(3,1)="LF")>0 THEN LET LF$="Y"
0560 LET M0=LEN(M0$),M1$=I6$,M2$=I7$,M3$=I8$,WIDTH=132,PAGE=0,L9=59
0565 LET L=L9+1,CLOCK$="",WHEN$=F0$(7,3),HEADINGS=2,COST$="Avg",DONE=0,LS=0
0570 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$,NUMBER=0,NF$="(Not On File)"
0575 IF I3$(8,1)="S" THEN LET COST$="Std"
0580 IF I3$(8,1)="R" THEN LET COST$="Rep"
0585 LET BW=MAX(M0+16,32),DH1$="Dist",DH2$="Code",LSDESC$="Serial Nbr"
0590 IF I3$(17,1)="L" THEN LET LSDESC$="Lot Nbr"
0595 LET XREF_DEV=0,LOTSER=0,ERRORS=0,DIF$="",BREAK$="N"
0600 REM " --- Additional File Opens"
0610 IF AR$="N" THEN GOTO 0630
0615 LET FILES$[16]="ARM-01",FILES$[17]="ARM-10"
0620 IF OP$="N" THEN GOTO 0630
0625 LET FILES$[18]="ARE-03",FILES$[19]="ARE-07",FILES$[20]="ARE-13"
0630 IF LF$="N" THEN GOTO 0640
0635 LET FILES$[21]="IVM-04",FILES$[22]="IVW-04",OPTIONS$[22]="CL"
0640 IF GL$="N" THEN GOTO 0650
0645 LET FILES$[23]="GLM-01",FILES$[24]=GLW11$,OPTIONS$[24]="CL"
0650 IF SF$="N" THEN GOTO 0660
0655 LET FILES$[25]="WOE-01",FILES$[26]="WOE-04",FILES$[27]="WOM-10"
0660 CALL "SYC.DA",1,16,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0665 IF STATUS THEN GOTO 9900
0670 LET ARM01_DEV=CHANNELS[16],ARM10_DEV=CHANNELS[17],ARE03_DEV=CHANNELS[18]
0675 LET ARE07_DEV=CHANNELS[19],ARE13_DEV=CHANNELS[20],IVM04_DEV=CHANNELS[21]
0680 LET IVW04_DEV=CHANNELS[22],GLM01_DEV=CHANNELS[23],GLW11_DEV=CHANNELS[24]
0685 LET WOE01_DEV=CHANNELS[25],WOE04_DEV=CHANNELS[26],WOM10_DEV=CHANNELS[27]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,6,0
0730 PRINT 'SB',@(17,3),"Page Break By Receipt:",@(33,5),"Phase:",'SF',
0800 REM " --- Page break?
0810 LET V4$="Should Each Receipt Be Printed On A New Page (Y/N)?"
0815 LET V0$="Y",V1$="C",V2$=BREAK$,V3$="",V0=1,V1=40,V2=3
0820 GOSUB 7000
0830 ON V3 GOTO 0840,0800,0800,0800,0850
0840 IF POS(V$="YN")=0 THEN GOTO 0800
0845 LET BREAK$=V$
0850 REM " --- Correct?"
0860 LET V4$="Is The Above Information Correct"
0870 CALL "SYC.YN",1,V4$,2,V$,V3
0880 IF V3=4 THEN GOTO 9900
0890 IF V$<>"Y" THEN GOTO 9900
0900 REM " --- Resize masks and set print positions"
0910 CALL "SYC.RM",M1$,M1,10,15
0915 CALL "SYC.RM",M2$,M2,9,12
0920 CALL "SYC.RM",M3$,M3,6,12
0930 LET O[12]=WIDTH-10,O[11]=O[12]-M1,O[10]=O[11]-M3,O[9]=O[10]-M1
0935 LET O[8]=O[9]-M3,O[7]=O[8]-M2,O[6]=O[7]-M2,O[5]=O[6]-3,O[4]=O[5]-5
0940 LET O[0]=3,O[1]=O[0]+4,O[2]=O[1]+3,O[3]=O[2]+3
0950 REM " --- Position file"
0960 CALL "SYC.GA",7,1,"","",STATUS
0970 IF STATUS THEN GOTO 9900
0975 PRINT @(40,5),"Purchase Order Receipt Register",
0980 CALL "SYC.NB","Printing",BW,COLUMN
0990 READ (POE04_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next header"
1010 LET K$=KEY(POE04_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 READ (POE04_DEV)IOL=POE04A
1040 PRINT @(COLUMN,11),FNF$(A0$(3,VENDLEN),M0$)," ",A0$(9,7)," ",A0$(16,7),
1050 IF BREAK$="Y" THEN LET L=L9+1
1200 REM " --- Header fields"
1210 DIM RECEIPT[5]
1220 LET COMPLETE$="(Backorder)",PACKLIST$="",NUMBER=NUMBER+1
1230 IF A1$(134,1)="Y" THEN LET COMPLETE$="(Complete)"
1240 IF FNP$(A1$(119,15))<>"" THEN LET PACKLIST$=A1$(119,15)
1300 REM " --- Level break?"
1310 IF T0$<>A0$(3,6) THEN GOSUB 6000
1400 REM " --- Print header information"
1410 IF L+6>L9 THEN GOSUB 5000 ELSE GOSUB 5200
1490 READ (POE14_DEV,KEY=A0$,DOM=1500)
1500 REM " --- Read next detail record"
1510 LET K$=KEY(POE14_DEV,END=3900)
1520 IF POS(A0$=K$)<>1 THEN GOTO 3900
1530 READ (POE14_DEV)IOL=POE14A
1540 IF Y0$(1,4)=N0$+W1$(1,2) THEN GOTO 1600
1550 LET Y0$(1,4)=N0$+W1$(1,2),Y1$(1)=""
1560 FIND (POM02_DEV,KEY=Y0$,DOM=3800)IOL=POM02A
1600 REM " --- Retrieve inventory item for S lines"
1610 LET C0$(1)=N0$+W3$(3),C1$(1)="",C2$(1)="",DESCRIPTION$(1)=NF$
1620 LET D0$(1)=N0$+W3$,D1$(1)="",D[11]=0,CODE$=""
1630 LET BEGSORT$=N0$+W3$(1,2)+A1$(29,6),ENDSORT$=W0$(3)
1640 IF Y1$(21,1)<>"S" THEN GOTO 1700
1650 FIND (IVM01_DEV,KEY=C0$,DOM=3800)IOL=IVM01A
1660 FIND (IVM02_DEV,KEY=D0$,DOM=3800)IOL=IVM02A
1670 LET DESCRIPTION$(1)=FNITEM$(C1$,I[3],I[4],I[5])
1680 IF DISTCODE$="Y" THEN LET CODE$=D1$(13,2)
1700 REM " --- Calculate line extensions"
1710 LET QTY=W[7],IV_EXTENSION=0
1720 IF Y1$(21,1)="O" THEN LET QTY=1
1730 PRECISION 2
1740 LET PO_EXTENSION=W[1]*QTY
1750 IF Y1$(21,1)="S" THEN LET IV_EXTENSION=D[11]*QTY
1760 PRECISION I[1]
1800 REM " --- Branch based on line code"
1890 ON POS(Y1$(21,1)="SNVMO") GOTO 3800,1900,2000,2100,2200,2300
1900 REM " --- Standard line"
1910 IF L+2>L9 THEN GOSUB 5000
1920 PRINT (7)@(O[0]),W0$(23,3),@(O[1]),W1$(1,2),@(O[2]),W3$(1,2),@(O[3]),W3$(3,I[0]),@(O[4]),CODE$,@(O[5]),W1$(22,2),@(O[6]),W[3]:M2$,@(O[7]),W[7]:M2$,@(O[8]),D[11]:M3$,@(O[9]),IV_EXTENSION:M1$,@(O[10]),W[1]:M3$,@(O[11]),PO_EXTENSION:M1$,@(O[12]),W1$(24,10)
1930 PRINT (7)@(O[3]),DESCRIPTION$
1940 LET L=L+2
1990 GOTO 2400
2000 REM " --- Non-stock line"
2010 IF L+2>L9 THEN GOSUB 5000
2020 PRINT (7)@(O[0]),W0$(23,3),@(O[1]),W1$(1,2),@(O[2]),W3$(1,2),@(O[3]),W3$(3,I[0]),@(O[4]),CODE$,@(O[5]),W1$(22,2),@(O[6]),W[3]:M2$,@(O[7]),W[7]:M2$,@(O[10]),W[1]:M3$,@(O[11]),PO_EXTENSION:M1$,@(O[12]),W1$(24,10)
2030 PRINT (7)@(O[3]),W4$
2040 LET L=L+2
2090 GOTO 2400
2100 REM " --- Vendor part number line"
2110 IF L+1>L9 THEN GOSUB 5000
2120 PRINT (7)@(O[0]),W0$(23,3),@(O[1]),W1$(1,2),@(O[2]),W3$(1,2),@(O[3]),W3$(3)
2140 LET L=L+1
2190 GOTO 2400
2200 REM " --- Message line"
2210 IF L+1>L9 THEN GOSUB 5000
2220 PRINT (7)@(O[0]),W0$(23,3),@(O[1]),W1$(1,2),@(O[2]),W3$(1,2),@(O[3]),W4$
2240 LET L=L+1
2290 GOTO 2400
2300 REM " --- Other type line"
2310 IF L+2>L9 THEN GOSUB 5000
2320 PRINT (7)@(O[0]),W0$(23,3),@(O[1]),W1$(1,2),@(O[2]),W3$(1,2),@(O[10]),W[1]:M3$,@(O[11]),PO_EXTENSION:M1$,@(O[12]),W1$(24,10)
2330 PRINT (7)@(O[3]),W4$
2340 LET L=L+2
2400 REM " --- Lotted/Serialized?"
2410 IF LS$="N" THEN GOTO 3000
2420 IF Y1$(21,1)<>"S" THEN GOTO 3000
2430 IF C2$(19,2)<>"YY" THEN GOTO 3000
2440 LET TOTAL=0,LASTSEQ=0,NEEDED=W[0]*W[7]
2490 READ (POE24_DEV,KEY=W0$,DOM=2500)
2500 REM " --- Read next lot/serial detail record"
2510 LET K$=KEY(POE24_DEV,END=2800)
2520 IF POS(W0$=K$)<>1 THEN GOTO 2800
2530 READ (POE24_DEV)IOL=POE24A
2540 PRECISION 2
2550 LET EXTENSION=L[0]*L[1]
2560 PRECISION I[1]
2600 REM " --- Print detail line"
2610 IF L+1>L9 THEN GOSUB 5000
2620 PRINT (7)@(O[3]),LSDESC$," ",L0$(26,3)," ",L1$(1,I[2]),@(O[7]),L[0]:M2$,@(O[10]),L[1]:M3$,@(O[11]),EXTENSION:M1$
2630 LET L=L+1,TOTAL=TOTAL+L[0],LASTSEQ=NUM(L0$(26))
2690 GOTO 2500
2800 REM " --- Enough lot/serial numbers?"
2810 IF NEEDED=TOTAL THEN GOTO 3000
2820 LET LOTSER=LOTSER+1
2830 IF LOTSER>99 THEN GOTO 2900
2840 LET LINEQTY[LOTSER]=W[7],LSQTY[LOTSER]=TOTAL
2850 LET FACTOR[LOTSER]=W[0],DIF$[LOTSER]=W0$
2900 REM " --- Print lines for missing lot/serial numbers"
2910 IF TOTAL>NEEDED THEN GOTO 3000
2920 DIM X$(I[2],"_")
2930 IF I3$(17,1)="L" THEN LET NEEDED=TOTAL+1
2940 FOR X=TOTAL+1 TO NEEDED
2950 IF L+1>L9 THEN GOSUB 5000
2960 LET L=L+1,LASTSEQ=LASTSEQ+1
2970 PRINT (7)@(O[3]),LSDESC$," ",LASTSEQ:"000-",X$
2990 NEXT X
3000 REM " --- Generate GL Recap Postings"
3010 IF GL$="N" THEN GOTO 3500
3020 LET GLFILES[1]=ARM10_DEV,GLFILES[2]=WOE01_DEV,GLFILES[3]=WOM10_DEV
3030 LET GLFILES[4]=GLM01_DEV,GLFILES[5]=GLW11_DEV,GLFILES[6]=XREF_DEV
3040 LET W$[0]=W0$,W$[1]=W1$,W$[2]=W2$,W$[3]=W3$,W$[4]=W4$
3050 LET GLPARAMS$[0]=GL$,GLPARAMS$[1]=A1$(29,6),GLPARAMS$[2]=B0$(3,VENDLEN)
3060 LET GLPARAMS$[3]=M0$,GLPARAMS$[4]=G4$,GLPARAMS$[5]=DESCRIPTION$
3070 LET GLPARAMS$[6]=Y1$,GLPARAMS$[7]=C6$,GLPARAMS$[8]=DISTCODE$
3075 LET GLPARAMS$[9]=CODE$,GLPARAMS$[10]=N0$,GLPARAMS$[11]=SF$
3080 LET GLPARAMS[0]=PO_EXTENSION,GLPARAMS[1]=G[4],GLPARAMS[2]=I[0]
3090 CALL "POC.GA",GLFILES[ALL],W$[ALL],W[ALL],GLPARAMS$[ALL],GLPARAMS[ALL],STATUS
3100 IF STATUS THEN GOTO 9900
3500 REM " --- Write sort records"
3510 LET X4$(1)=BEGSORT$+W3$(3)+ENDSORT$,X5$(1)=BEGSORT$+W3$(3)+W4$+ENDSORT$
3520 LET X8$(1)=N0$+W3$(1,2)+W2$(1,10)+ENDSORT$,R0$(1)=N0$+W3$,R[0]=0,R[1]=0
3530 LET X9$(1)=N0$+W3$(1,2)+W2$(11,16)+ENDSORT$
3540 IF POS(Y1$(21,1)="NO") THEN WRITE (POW05_DEV,KEY=X5$)IOL=POW05A
3545 IF FNP$(W2$(1,10))<>"" THEN WRITE (POW08_DEV,KEY=X8$)IOL=POW08A
3550 IF FNP$(W2$(11,16))<>"" THEN WRITE (POW09_DEV,KEY=X9$)IOL=POW09A
3555 IF Y1$(21,1)<>"S" THEN GOTO 3600
3560 FIND (POW03_DEV,KEY=R0$,DOM=3570)IOL=POW03A
3570 LET R[0]=R[0]+(W[7]*W[0])
3580 WRITE (POW03_DEV,KEY=R0$)IOL=POW03A
3590 WRITE (POW04_DEV,KEY=X4$)IOL=POW04A
3600 REM " --- Accumulate totals"
3610 LET RECEIPT[0]=RECEIPT[0]+W[3],RECEIPT[1]=RECEIPT[1]+W[7]
3620 LET RECEIPT[3]=RECEIPT[3]+IV_EXTENSION,RECEIPT[5]=RECEIPT[5]+PO_EXTENSION
3630 LET VENDOR[0]=VENDOR[0]+W[3],VENDOR[1]=VENDOR[1]+W[7]
3640 LET VENDOR[3]=VENDOR[3]+IV_EXTENSION,VENDOR[5]=VENDOR[5]+PO_EXTENSION
3650 LET REPORT[0]=REPORT[0]+W[3],REPORT[1]=REPORT[1]+W[7]
3660 LET REPORT[3]=REPORT[3]+IV_EXTENSION,REPORT[5]=REPORT[5]+PO_EXTENSION
3800 REM " --- Loop back for next detail record"
3890 GOTO 1500
3900 REM " --- Loop back for next header record"
3910 LET TOTALS$="Total For PO # "+A0$(16,7),TOTALS[ALL]=RECEIPT[ALL]
3920 GOSUB 6800
3990 GOTO 1000
4000 REM " --- All done"
4010 LET DONE=1
4020 GOSUB 6000
4030 LET TOTALS$="Total For Register",TOTALS[ALL]=REPORT[ALL]
4040 GOSUB 6800
4500 REM " --- Any missing lot/serial numbers?"
4510 IF LS$="N" THEN GOTO 4900
4520 IF LOTSER=0 THEN GOTO 4900
4530 LET LS=1,HEADINGS=HEADINGS-1,M2$=I7$,M5$=I10$
4540 CALL "SYC.RM",M2$,M2,13,13
4550 CALL "SYC.RM",M5$,M5,10,12
4560 LET O[0]=0,O[1]=O[0]+M0+31,O[2]=O[1]+10,O[3]=O[2]+12
4570 LET O[4]=O[3]+M2,O[5]=O[4]+M5,O[6]=O[5]+M2,O[7]=O[6]+M2
4590 IF L+6>L9 THEN GOSUB 5000 ELSE GOSUB 5400
4600 REM " --- Print list of lines with lot/serial number differences"
4610 FOR X=1 TO LOTSER
4620 LET K$=DIF$[X],B0$(1)=N0$+K$(3,6),B1$(1)=NF$
4630 FIND (APM01_DEV,KEY=B0$,DOM=4640)IOL=APM01A
4640 IF L+1>L9 THEN GOSUB 5000
4650 LET STOCK=LINEQTY[X]*FACTOR[X],DIF=STOCK-LSQTY[X]
4660 PRINT (7)@(O[0]),FNF$(B0$(3,VENDLEN),M0$)," ",B1$(1,30),@(O[1]),K$(9,7),@(O[2]),K$(16,7),"-",K$(23,3),@(O[3]),LINEQTY[X]:M2$,@(O[4]),FACTOR[X]:M5$,@(O[5]),STOCK:M2$,@(O[6]),LSQTY[X]:M2$,@(O[7]),DIF:M2$
4670 LET L=L+1
4690 NEXT X
4900 REM " --- Run next overlay"
4910 IF NUMBER=0 THEN GOTO 9900
4990 RUN "POR.DB"
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+4
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS THEN EXITTO 9900
5040 IF LS THEN GOTO 5400
5050 PRINT (7)@(O[4]),DH1$,@(O[6]+M2-9),"Quantity",@(O[7]+M2-9),"Quantity",@(O[8]+M3-5),COST$,@(O[9]+M1-10),COST$,@(O[10]+M3-5),"PO",@(O[11]+M1-10),"PO",@(O[12]),"Receiving"
5060 PRINT (7)@(O[0]),"Seq",@(O[1]),"Cd",@(O[2]),"Wh",@(O[3]),"Item",@(O[4]),DH2$,@(O[5]),"UM",@(O[6]+M2-9),"Ordered",@(O[7]+M2-9),"Received",@(O[8]+M3-5),"Cost",@(O[9]+M1-10),"Extension",@(O[10]+M3-5),"Cost",@(O[11]+M1-10),"Extension",@(O[12]),"Location"
5070 IF DONE=0 THEN GOSUB 5200
5090 RETURN 
5200 REM " --- Receipt subheading"
5210 PRINT (7)""
5220 PRINT (7)"Vendor: ",VENDOR$,@(63),"Rec #: ",A0$(9,7),@(97),"Receipt Date: ",FNB6$(A1$(29,6))," ",COMPLETE$
5230 PRINT (7)@(63)," PO #: ",A0$(16,7),@(97),PACKLIST$
5240 PRINT (7)""
5250 LET L=L+4
5260 CALL "GLC.EA",A1$(29,6),"N",PERIOD$,YEAR$,GLSTATUS
5270 IF GLSTATUS THEN CALL "GLC.GA",7,A1$(29,6),GLSTATUS,L,ERRORS,STATUS
5290 RETURN 
5400 REM " --- Subheading for lot/serial number errors"
5410 PRINT (7)""
5420 PRINT (7)@(51),"Lot/Serial Number Differences"
5430 PRINT (7)""
5440 PRINT (7)@(O[3]+M2-10),"Line Item",@(O[4]+M5-7),"Conv",@(O[5]+M2-10),"Stocking",@(O[6]+M2-11),"Lot/Serial"
5450 PRINT (7)@(O[0]),"Vendor",@(O[1]),"Receiver",@(O[2]),"PO #    Seq",@(O[3]+M2-10),"Qty Rec'd",@(O[4]+M5-7),"Factor",@(O[5]+M2-10),"Qty Rec'd",@(O[6]+M2-11),"Qty Rec'd",@(O[7]+M2-11),"Difference"
5480 LET L=L+5
5490 RETURN 
6000 REM " --- Vendor break"
6010 IF T0$="" THEN GOTO 6100
6020 LET TOTALS$="Total For "+VENDOR$,TOTALS[ALL]=VENDOR[ALL]
6030 GOSUB 6800
6100 IF DONE THEN GOTO 6190
6110 DIM VENDOR[5]
6120 LET T0$=A0$(3,6),B0$(1)=N0$+T0$,B1$(1)=NF$
6130 FIND (APM01_DEV,KEY=B0$,DOM=6140)IOL=APM01A
6140 LET VENDOR$=FNF$(B0$(3,VENDLEN),M0$)+" "+FNP$(B1$(1,30))
6190 RETURN 
6800 REM " --- Print totals"
6810 IF L+1>L9 THEN GOSUB 5000
6820 IF LEN(TOTALS$)>O[6] THEN LET TOTALS$=TOTALS$(1,O[6])
6830 PRINT (7)@(O[6]-LEN(TOTALS$)),TOTALS$,@(O[6]),TOTALS[0]:M2$,@(O[7]),TOTALS[1]:M2$,@(O[9]),TOTALS[3]:M1$,@(O[11]),TOTALS[5]:M1$
6840 LET L=L+1
6890 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

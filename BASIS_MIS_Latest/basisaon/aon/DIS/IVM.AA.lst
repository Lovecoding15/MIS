0010 REM "IVM - Product Type Maintenance"
0020 REM "Program IVM.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="IVM-10",FILES$[3]="IVM-03"
0140 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],IVM10_DEV=CHANNELS[2],IVM03_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 IVM10A: IOLIST A0$(1)
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 CALL "SYC.VA","SA",INFO$[ALL]
0490 LET SA$=INFO$[20],P0=NUM(P2$(1,2))
0500 REM " --- Initializations"
0510 DIM A0$(32)
0590 LET RECORD$="A",K9=6,A0$(1,3)=N0$+RECORD$
0700 REM " --- Background"
0720 PRINT 'SB',@(10,3),"Product Type:",@(11,4),"Description:",@(0,5),"Default Sales Analysis:",'SF',
0900 REM " --- Position File"
0990 READ (IVM10_DEV,KEY=A0$(1,3),DOM=1000)
1000 REM " --- Product Type"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(IVM10_DEV,END=1050)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4)
1050 LET V0$="S",V1$="KCE",V3$="",V0=3,V1=24,V2=3
1055 LET V4$="Enter Product Type (<F3>=Lookup)"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN CALL "IVC.LK",1,V1,V2,V$
1080 IF V$="" THEN GOTO 0900
1085 PRINT @(V1,V2),'CL',V$
1090 LET A0$(4)=V$
1200 REM " --- Retrieve Record"
1210 FIND (IVM10_DEV,KEY=A0$(1,K9),DOM=1400)IOL=IVM10A
1300 REM " --- Display"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Initialize new record"
1510 LET A0$(7)="",A0$(27,1)="I"
1520 IF SA$<>"Y" THEN LET A0$(27,1)="N"
2000 REM " --- Description"
2020 LET V0$="S",V1$="",V2$=A0$(7,20),V3$="",V0=20,V1=24,V2=4,I0=0
2030 LET V4$="Enter the description of this product code"
2040 GOSUB 7000
2050 ON V3 GOTO 2060,2000,3900,2000,3900
2060 LET A0$(7,20)=V$
2090 GOTO 3900
2100 REM " --- Default Sales Analysis"
2110 IF A0$(27,1)=" " THEN LET A0$(27,1)="I"
2115 LET V4$="P=Product Level     I=Product+Item Level     N=No Sales Analysis"
2120 LET V0$="S",V1$="RC",V2$=A0$(27,1),V3$="PIN",V0=1,V1=24,V2=5
2130 IF SA$="N" THEN LET V$="N"; GOTO 2200
2140 GOSUB 7000
2150 ON V3 GOTO 2200,2100,3900,2100,3900
2200 REM 
2210 LET A0$(27,1)=V$
2220 PRINT @(V1,V2),A0$(27,1),
2290 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record"
4110 WRITE (IVM10_DEV,KEY=A0$(1,K9))IOL=IVM10A
4190 GOTO 1000
4200 REM " --- Remove record"
4210 READ (IVM03_DEV,KEY=A0$(1,K9),DOM=4220)
4220 LET K$=KEY(IVM03_DEV,END=4280)
4230 IF POS(K$(1,K9)=A0$(1,K9))=1 THEN GOTO 4300
4280 REMOVE (IVM10_DEV,KEY=A0$(1,K9),DOM=4290)
4290 GOTO 1000
4300 REM " --- Product type in use"
4310 DIM MESSAGE$[1]
4320 LET MESSAGE$[0]="This Product Type Cannot Be Deleted. It Is Used"
4330 LET MESSAGE$[1]="By Item "+FNP$(K$(7,P0))+" (<Enter>=Continue)"
4340 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
4390 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4900,9900
4900 REM " --- Report Initializations"
4910 LET PROGRAM$="",NUMBER=2,CHANNEL=IVM10_DEV,HEADINGS=3,WIDTH=80
4920 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4930 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4940 DIM HEADINGS$[HEADINGS]
4950 LET FIELD$[0]="Type",FIELD[0]=1,POSITION[0]=4
4955 LET LENGTH[0]=3,TYPE$[0]="S",MASK$[0]=""
4960 LET FIELD$[1]="Description",FIELD[1]=1,POSITION[1]=7
4965 LET LENGTH[1]=20,TYPE$[1]="S",MASK$[1]=""
4970 LET FIELD$[2]="Sales Analysis",FIELD[2]=1,POSITION[2]=27
4975 LET LENGTH[2]=1,TYPE$[2]="S",MASK$[2]="",TOTAL$="Product Types"
4980 LET HEADINGS$[0]=N1$,HEADINGS$[1]=F5$+" Listing"
4990 RUN "SYR.AA"
5000 REM " --- Display record"
5010 PRINT @(24,4),A0$(7,20),@(24,5),A0$(27,1),
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

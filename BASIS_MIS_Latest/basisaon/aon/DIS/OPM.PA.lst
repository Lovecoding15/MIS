0010 REM "OPM - Price Inquiry"
0020 REM "Program OPM.PA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES=10
0115 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="ARM-01",FILES$[3]="ARM-02"
0125 LET FILES$[4]="ARM-10",FILES$[5]="IVM-01",FILES$[6]="IVM-02"
0130 LET FILES$[7]="IVM-03",FILES$[8]="IVM-06",FILES$[9]="IVM-10"
0133 LET FILES$[10]="IVM-14"
0135 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0140 IF STATUS>0 THEN GOTO 9900
0145 LET SYS01_DEV=CHANNELS[1],ARM01_DEV=CHANNELS[2],ARM02_DEV=CHANNELS[3]
0150 LET ARM10_DEV=CHANNELS[4],IVM01_DEV=CHANNELS[5],IVM02_DEV=CHANNELS[6]
0155 LET IVM03_DEV=CHANNELS[7],IVM06_DEV=CHANNELS[8],IVM10_DEV=CHANNELS[9]
0190 LET IVM14_DEV=CHANNELS[10]
0200 REM " --- IOLISTS"
0210 ARM01A: IOLIST A0$(1),A1$(1)
0220 ARM02A: IOLIST D0$(1),D1$(1)
0230 IVM10E: IOLIST C0$(1),C[ALL]
0240 IVM10C: IOLIST X0$(1)
0250 IVM02A: IOLIST W0$(1),W1$(1),W2$,W[ALL]
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,X$,X$,X$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,X$,M1$,M2$,M3$,M4$
0310 ARM10M: IOLIST Y0$(1)
0360 IVM06A: IOLIST U0$(1),U1$(1),U[ALL]
0400 REM " --- Parameters "
0405 DIM P[2],I[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="AR"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0450 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0460 LET I[0]=NUM(I2$(1,2)),I[4]=NUM(I2$(5,1))
0470 LET I[1]=NUM(I2$(9,2)),I[2]=NUM(I2$(11,2)),I[3]=NUM(I2$(13,2))
0500 REM " --- Initialize Data "
0505 PRECISION I[4]
0510 DIM A0$(8),A1$(280),C0$(40),C[19],W0$(24),W1$(64),W[16],O[5],FILES[14]
0520 DIM D0$(10),D1$(64),X0$(160),Y0$(40),U0$(28),U1$(48),U[19],B0$(I[0])
0550 LET M0=LEN(M0$),M4=LEN(M4$),M8$="##0.00%",NAME$=""
0555 LET ADDRESS=14+M0+1,PM$="(###) 000-0000",AL=52-ADDRESS,NL=50-ADDRESS
0560 LET FILES[1]=IVM01_DEV,FILES[14]=IVM14_DEV,HEIGHT=10,WIDTH=80
0570 LET TITLE$="Warehouse Availability",WIN_X=0,WIN_Y=14
0575 LET DW=80-M2-11-(16+I[0])
0600 REM " --- Print positions"
0610 CALL "SYC.RM",M2$,M2,9,12
0620 LET O[0]=3,O[1]=O[0]+M2,O[2]=O[1]+8
0630 LET O[3]=O[0]+40,O[4]=O[1]+40,O[5]=O[2]+40
0700 REM " --- Background "
0710 CALL "SYC.WC",1,0,80,0,0,9,0
0720 CALL "SYC.WC",1,0,80,0,0,14,0
0730 PRINT 'SB',@(4,3),"Customer:",@(51),"Contact:",@(53,4),"Phone:",@(55,5),"Fax:",@(2,10),"Price Code:",@(70),"Quantity",@(0,11),"Warehouse ID:",@(80-M2-9),"On Hand:",@(1,12),"Item Number:",@(80-M2-11),"Committed:",@(2,13),"List Price:",@(80-M2-11),"Available:",@(O[0]+M2-9,16),"Quantity",@(O[1]),"Percent",@(O[2]+M4-6),"Price",@(O[3]+M2-9),"Quantity",@(O[4]),"Percent",@(O[5]+M4-6),"Price",'SF',
0740 FOR X=1 TO 5
0750 PRINT 'SB',@(0,X+16),X:"#.",@(40),X+5:"##.",'SF',
0760 NEXT X
0900 REM " --- Position file"
0980 LET A0$(1,2)=N0$
0990 READ (ARM01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Key "
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(ARM01_DEV,END=1050)
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1050 LET V4$="Enter Customer Number (<F1>=Price Code/<F3>=Lookup)"
1055 LET V0$="M",V1$="KCE",V3$=P1$,V0=6,V1=14,V2=3
1060 GOSUB 7000
1065 ON V3 GOTO 1090,1200,1000,1070,9900
1070 CALL "SYC.LC",V$
1080 IF V$="" THEN GOTO 1000
1090 LET A0$(3)=V$,D0$(1)=A0$,D1$(1)=""
1100 REM " --- Read customer and price code"
1110 READ (ARM01_DEV,KEY=A0$,DOM=1000)IOL=ARM01A
1120 FIND (ARM02_DEV,KEY=D0$,DOM=1050)IOL=ARM02A
1130 LET Y0$(1)=N0$+"M"+D1$(24,4)+"(Not On File)"
1140 FIND (ARM10_DEV,KEY=Y0$(1,7),DOM=1150)IOL=ARM10M
1150 LET ADDRESS$=A1$(31,72)+A1$(179,48)+A1$(103,9),PHONE$="",FAX$=""
1155 CALL "SYC.AA",ADDRESS$,24,5,9,40
1160 IF FNP$(A1$(112,10))<>"" THEN LET PHONE$=FNF$(A1$(112,10),PM$)
1170 IF FNP$(A1$(169,10))<>"" THEN LET FAX$=FNF$(A1$(169,10),PM$)
1180 GOSUB 5000
1190 GOTO 1500
1200 REM " --- Price Code"
1210 PRINT 'CF',
1220 LET Y0$(1)="",A0$(3)=""
1230 LET V4$="Enter Price Code (<F2>=Customer/<F3>=Lookup)"
1240 LET V0$="S",V1$="CE",V2$=Y0$(4,4),V3$="",V0=4,V1=14,V2=10
1250 GOSUB 7000
1260 ON V3 GOTO 1300,1200,1000,1270,9900
1270 GOSUB 6000
1280 IF V$="" THEN GOTO 1200
1300 REM " --- Read price code"
1310 LET Y0$(1)=N0$+"M"+V$
1320 FIND (ARM10_DEV,KEY=Y0$(1,7),DOM=1200)IOL=ARM10M
1330 PRINT @(14,10),Y0$(4,4)," ",Y0$(8,20),
1500 REM " --- Warehouse ID"
1510 LET V$=I3$(6,2)
1520 IF I3$(1,1)="N" THEN GOTO 1600
1530 LET V4$="Enter A Valid Warehouse (<F3>=Lookup)"
1540 LET V0$="S",V1$="CKE",V2$=V$,V3$="",V0=2,V1=14,V2=11
1550 GOSUB 7000
1560 ON V3 GOTO 1580,1500,1000,1570,1000
1570 CALL "IVC.LK",3,V1,V2,V$
1580 IF V$="" THEN GOTO 1200
1600 REM " --- Retrieve warehouse record"
1610 LET X0$(1)=N0$+"C"+V$
1620 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=1500)IOL=IVM10C
1630 PRINT @(14,11),X0$(4,2)," ",X0$(6,24),
2000 REM " --- Item Number"
2010 DIM ITEM$[6],ITEM[12],B1$(DW),B2$(64)
2020 GOSUB 5200
2030 FOR X=11 TO 13
2040 PRINT @(80-M2,X),'CL',
2050 NEXT X
2055 PRINT @(14,13),FILL(M4),
2060 LET ACTION$="",V1=14,V2=12,V2$=B0$
2070 CALL "IVC.IA",ACTION$,V1,V2,FILES[ALL],N0$,V2$,V4$,I[ALL],ITEM$[ALL],ITEM[ALL],V3
2080 ON V3 GOTO 2090,2000,1500,2000,1000
2090 LET B0$(1)=ITEM$[0],B1$(1)=ITEM$(1),B2$(1)=ITEM$[2]
2095 PRINT @(V1,V2),B0$(1,I[0]),
2100 REM " --- Read warehouse record for item"
2110 LET W0$(1)=N0$+X0$(4,2)+ITEM$[0],V2$=""
2120 FIND (IVM02_DEV,KEY=W0$,DOM=2200)IOL=IVM02A
2190 GOTO 2500
2200 REM " --- Item not in warehouse"
2210 LET V1$="This Item Is Not Defined In This Warehouse (<Enter>=Continue)"
2220 GOSUB 6900
2290 GOTO 2000
2500 REM " --- Display quantities for warehouse"
2510 LET AVAILABLE=W[0]-W[2],COST=W[11],LISTPRICE=W[9]
2520 PRINT @(14,13),W[9]:M4$,@(80-M2,11),W[0]:M2$,@(80-M2,12),W[2]:M2$,@(80-M2,13),AVAILABLE:M2$,
3000 REM " --- Get Price Discount"
3010 LET C0$(1)=N0$+"E"+B2$(21,3)+Y0$(4,4)
3020 FIND (IVM10_DEV,KEY=C0$(1,10),DOM=3400)IOL=IVM10E
3030 LET PRICE=LISTPRICE*(100-C[10])/100
3040 LET DESCRIPTION$="Pricing Table - "+FNP$(C0$(11,20))
3100 REM " --- Method for pricing?"
3110 LET X$=" (Unknown Pricing Method)"
3120 IF C0$(31,1)="C" THEN LET X$=" (Mark-Up Over Cost)"
3130 IF C0$(31,1)="L" THEN LET X$=" (Mark-Down From List)"
3140 IF C0$(31,1)="M" THEN LET X$=" (Margin Over Cost)"
3150 LET DESCRIPTION$=DESCRIPTION$+X$
3200 REM " --- Display pricing table"
3205 PRINT @(0,15),'CL',@(FNC(DESCRIPTION$)),DESCRIPTION$,
3210 FOR X=0 TO 4
3220 IF C[X]=0 AND C[X+10]=0 THEN GOTO 3250
3230 LET PRICE=0,PERCENT=C[X+10]
3235 GOSUB 6100
3240 PRINT @(O[0],X+17),C[X]:M2$,@(O[1]),PERCENT:M8$,@(O[2]),PRICE:M4$,
3250 IF C[X+5]=0 AND C[X+15]=0 THEN GOTO 3290
3260 LET PRICE=0,PERCENT=C[X+15]
3270 GOSUB 6100
3280 PRINT @(O[3],X+17),C[X+5]:M2$,@(O[4]),PERCENT:M8$,@(O[5]),PRICE:M4$,
3290 NEXT X
3300 REM 
3390 GOTO 3500
3400 REM " --- No price table record"
3410 LET V1$="No Pricing Table Entry Exists For This Item Class And"
3420 LET V2$="Customer Price Code (<Enter>=Continue)"
3430 GOSUB 6900
3500 REM " --- Contract Pricing "
3510 LET U0$(1)=N0$+A0$(3)+B0$
3520 FIND (IVM06_DEV,KEY=U0$,DOM=4000)IOL=IVM06A
3530 LET V4$="This Customer Has Contract Pricing For This Item. Display It"
3540 CALL "SYC.YN",1,V4$,1,V$,V3
3550 IF V$<>"Y" THEN GOTO 4000
3600 REM " --- Display Contract Pricing"
3610 GOSUB 5200
3615 LET DESCRIPTION$="Contract Pricing"
3620 IF FNP$(U1$(1,20))<>"" THEN LET DESCRIPTION$=DESCRIPTION$+" - "+FNP$(U1$(1,20))
3630 LET DESCRIPTION$=DESCRIPTION$+" (From "+FNB$(U1$(21,3))+" Through "+FNB$(U1$(24,3))+")"
3635 PRINT @(FNC(DESCRIPTION$),15),DESCRIPTION$,
3640 FOR X=0 TO 4
3650 IF U[X]<>0 OR U[X+10]<>0 THEN PRINT @(O[0],X+17),U[X]:M2$,@(O[2]),U[X+10]:M4$,
3660 IF U[X+5]<>0 OR U[X+15]<>0 THEN PRINT @(O[3],17),U[X+5]:M2$,@(O[5]),U[X+15]:M4$,
3680 NEXT X
4000 REM " --- Options "
4010 LET V4$="Press <Enter> To Continue (<F1>=Warehouse Availability/<F2>=Item):"
4020 LET V0$="S",V1$="CE",V2$="",V3$="",V0=1,V1=INT(40+LEN(V4$)/2),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 1000,4100,2000,4000,9900
4100 REM " --- Display warehouse availability"
4110 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),L[5]
4120 LET L[0]=2,L[5]=WIDTH-2-M2,L[4]=L[5]-M2,L[3]=L[4]-M2,L[2]=L[3]-M2
4125 LET L[1]=L[2]-11,HEADING$(L[0])="Warehouse",HEADING$(L[1])="Location"
4130 LET HEADING$(L[2]+M2-8)="On Hand",HEADING$(L[3]+M2-10)="Committed"
4135 LET HEADING$(L[4]+M2-10)="Available",HEADING$(L[5]+M2-9)="On Order"
4140 LET FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End",FIRST$=N0$+"G"+B0$
4150 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
4160 PRINT @(0,0),'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
4170 LET PAGES=MIN(INT((DSZ-5000)/4000),10),MAXROW=HEIGHT-5,V$=""
4175 IF PAGES<3 THEN LET PAGES=3
4180 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,5]
4185 LET PAGE=1,X1=1,L=1
4190 READ (IVM03_DEV,KEY=FIRST$,DOM=4200)
4200 REM " --- For each warehouse"
4210 LET K$=KEY(IVM03_DEV,END=4800)
4220 IF POS(FIRST$=K$)<>1 THEN GOTO 4800
4230 READ (IVM03_DEV)
4240 DIM W0$(24),W1$(64),W[20],X0$(32)
4250 LET W0$(1)=N0$+K$(24,2)+B0$,X0$(6)="(Not On File)"
4260 FIND (IVM02_DEV,KEY=W0$,DOM=4700)IOL=IVM02A
4270 FIND (IVM10_DEV,KEY=N0$+"C"+W0$(3,2),DOM=4300)IOL=IVM10C
4300 REM " --- Display page?"
4310 IF L<MAXROW THEN GOTO 4400
4320 LET XMODE=5
4330 GOSUB 6200
4340 IF FKEY=4 THEN GOTO 4900
4350 IF V$<>"" THEN GOTO 4900
4400 REM " --- Build display line"
4410 DIM G$(WIDTH-2)
4420 LET AVAILABLE=W[0]-W[2]
4430 LET G$(2)=W0$(3,2)+" "+X0$(6,24),G$(L[1]-1)=" "+W1$(1,10)
4440 LET G$(L[2])=STR(W[0]:M2$),G$(L[3])=STR(W[2]:M2$)
4450 LET G$(L[4])=STR(AVAILABLE:M2$),G$(L[5])=STR(W[1]:M2$)
4490 LET LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=W0$(3,2),X1=X1+1,L=L+1
4500 REM " --- End of warehouse loop"
4590 GOTO 4200
4800 REM " --- Display last page"
4810 LET XMODE=5,V$="|EOF"
4820 GOSUB 6200
4900 REM " --- Done with warehouse availability"
4910 CALL "SYC.WD",NAME$
4920 IF V$="END" THEN LET V$=""
4930 IF V$<>"" THEN GOTO 1600
4990 GOTO 4000
5000 REM " --- Display "
5010 PRINT @(14,3),FNF$(A0$(3,P[0]),M0$)," ",A1$(1,NL),@(60),A1$(228,20),@(ADDRESS,4),ADDRESS$(1,AL),@(60),PHONE$,@(ADDRESS,5),ADDRESS$(41,AL),@(60),FAX$,@(ADDRESS,6),ADDRESS$(81,40),@(ADDRESS,7),ADDRESS$(121,40),@(ADDRESS,8),ADDRESS$(161,40),@(14,10),Y0$(4,4)," ",Y0$(8,20),
5090 RETURN 
5200 REM " --- Clear bottom of screen"
5210 FOR X=0 TO 4
5220 PRINT @(O[0],X+17),FILL(40-O[0]),@(O[3]),'CL',
5230 NEXT X
5290 RETURN 
6000 REM " --- Code Lookup"
6010 LET NUMBER=1,CHANNEL=ARM10_DEV,RECORD$="M"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=4
6050 LET FIELD[1]=1,POSITION[1]=8,LENGTH[1]=20
6060 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,V$="SKIP",ROW=V2+2
6080 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
6100 REM " --- Determine Price "
6110 IF C0$(31,1)="C" THEN LET PRICE=COST+((COST*PERCENT)/100)
6120 IF C0$(31,1)="L" THEN LET PRICE=LISTPRICE*(100-PERCENT)/100
6130 IF C0$(31,1)="M" THEN LET PRICE=(COST*100)/(100-PERCENT)
6190 RETURN 
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],V$,PAGE,MAXROW,NAME$,HEIGHT,WIDTH,V3
6220 IF V3=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
6900 REM " --- Display message"
6910 DIM MESSAGE$[1]
6920 LET MESSAGE$[0]=V1$,V=0
6930 IF V2$<>"" THEN LET MESSAGE$[1]=V2$,V=1
6940 CALL "SYC.XA",3,MESSAGE$[ALL],V,22,-1,V$,V3
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions "
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$)=INT((80-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9910 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9950 RUN "SYS.AA"
9999 END

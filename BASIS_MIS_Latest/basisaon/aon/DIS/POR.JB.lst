0010 REM "POR - Purchase Orders By Item Report (Print Overlay)
0020 REM "Program POR.JB
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0260 IVM01A: IOLIST *,X1$(1)
0270 IVM02A: IOLIST *,*,*,*,QTYONORDER
0280 IVM10C: IOLIST X0$
0300 POE12A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1),B4$(1),B5$,B6$,B[ALL]
0310 POM02A: IOLIST S0$(1),S1$(1)
0500 REM " --- Init Data
0510 PRECISION IVPRECISION
0530 LET GRANDTOT=0,PRINTWTOT$="N"
0540 DIM ITEMDESC$(60),WHSENAME$(24),ITEMNUM$(20)
0600 REM " --- Headings and print positions
0610 LET HEADINGS=2
0620 DIM HEADINGS$[HEADINGS],O[14]
0630 LET WIDTH=132,PAGE=0,L9=59,L=L9+1
0640 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$
0760 POSITIONS: 
0770 LET O[1]=O[0]+5,O[2]=O[1]+8,O[3]=O[2]+4,O[4]=O[3]+7
0780 LET O[5]=O[4]+3,O[6]=O[5]+9,O[7]=O[6]+9,O[8]=O[7]+9
0790 LET O[9]=O[8]+M5+1,O[10]=O[9]+M2+1,O[11]=O[10]+M2+1,O[12]=O[11]+M2+1
0800 LET O[13]=O[12]+M3+1,O[14]=O[13]+M1+1
0810 IF O[14]+11<WIDTH THEN GOTO 0950
0820 LET O[14]=WIDTH-M2,O[13]=O[14]-M1-1,O[12]=O[13]-M3,O[11]=O[12]-M2
0830 LET O[10]=O[11]-M2,O[9]=O[10]-M2,O[8]=O[9]-M5
0950 REM " --- Position files
0980 PRINT @(COLUMN-9,11),'SB',"Printing",'SF',
1000 REM " --- Init Sort File
1010 IF WHSEFROM$="" THEN LET FIRSTWHSE$=FILL(2) ELSE LET FIRSTWHSE$=WHSEFROM$
1020 READ (POW15_DEV,KEY=N0$+FIRSTWHSE$+ITEMFROM$+POFROM$,DOM=READSORT)
1030 READSORT: 
1040 LET K$=KEY(POW15_DEV,END=4000)
1050 IF K$(1,2)<>N0$ THEN GOTO 4000
1060 LET SORTKEY$=N0$+K$(35,6)+K$(25,10)
1070 READ (POW15_DEV,KEY=K$)
1100 REM " --- Find corresponding detail line
1110 DIM B0$(18),B1$(48),B2$(32),B3$(22),B4$(40),B[12]
1120 FIND (POE12_DEV,KEY=SORTKEY$,DOM=TESTDETAIL)IOL=POE12A
1130 TESTDETAIL: 
1140 IF B0$<>SORTKEY$ THEN GOTO NEXTSORT
1200 REM " --- Test PO Detail ranges
1210 IF B3$<>K$(3,22) THEN GOTO NEXTSORT
1220 IF WHSEFROM$<>"" AND B3$(1,2)<WHSEFROM$ THEN GOTO NEXTSORT
1230 IF WHSETHRU$<>"" AND B3$(1,2)>WHSETHRU$ THEN GOTO NEXTSORT
1240 IF ITEMFROM$<>FILL(20) AND B3$(3,20)<ITEMFROM$ THEN GOTO NEXTSORT
1250 IF ITEMTHRU$<>FILL(20) AND B3$(3,20)>ITEMTHRU$ THEN GOTO NEXTSORT
1260 IF POFROM$<>FILL(7) AND B0$(9,7)<POFROM$ THEN GOTO NEXTSORT
1270 IF POTHRU$<>FILL(7) AND B0$(9,7)>POTHRU$ THEN GOTO NEXTSORT
1280 IF VENDFROM$<>FILL(6) AND B0$(3,6)<VENDFROM$ THEN GOTO NEXTSORT
1290 IF VENDTHRU$<>FILL(6) AND B0$(3,6)>VENDTHRU$ THEN GOTO NEXTSORT
1300 GOSUB GETTESTDATE_DETAIL
1310 IF DATEFROM$<>"" AND TESTDATED$<DATEFROM$ THEN GOTO NEXTSORT
1320 IF DATETHRU$<>"" AND TESTDATED$>DATETHRU$ THEN GOTO NEXTSORT
1330 REM " --- Get Line Type
1340 DIM S0$(4),S1$(32)
1350 LET LINETYPE$="N"
1360 FIND (POM02_DEV,KEY=B0$(1,2)+B1$(1,2),DOM=1370)IOL=POM02A
1370 LET LINETYPE$=S1$(21,1)
1380 IF LINETYPE$<>"S" THEN GOTO NEXTSORT
1400 REM " --- Test breaks; print line
1410 PRINT @(COLUMN,11),FNF$(B0$(3,VENDLEN),VENDOMASK$)," ",B0$(9,7),
1420 GOSUB TESTBREAKS
1430 GOSUB PRINTDETAIL
1600 REM " --- Accumulate totals; then get next sort file record
1605 NEXTSORT: 
1620 LET QTYORDTOT=QTYORDTOT+QTYORD,QTYRECTOT=QTYRECTOT+QTYREC
1630 LET BALTOT=BALTOT+BALANCE,ITEMTOT=ITEMTOT+EXTENSION
1640 LET WHSETOT=WHSETOT+EXTENSION,GRANDTOT=GRANDTOT+EXTENSION
1650 LET EXTENSION=0,EXTENSION$="",BALANCE=0,BALANCE$=""
1660 LET QTYORD=0,QTYORD$="",QTYREC=0,QTYREC$=""
1670 GOTO READSORT
4000 REM " --- All Done
4010 IF L+2>L9 THEN GOSUB 5000
4020 GOSUB ITEMTOTALS
4030 GOSUB WHSETOTALS
4040 PRINT (7)@(O[1]),"Total For Report",@(O[13]),GRANDTOT:M1$
4050 GOTO 9900
4100 REM " --- Print Detail Subroutine
4105 PRINTDETAIL: 
4120 REM " --- Set up variables
4130 DIM DATEREQ$(8),DATEPROM$(8),NOTB4DATE$(8)
4140 LET ITEMNUM$=B3$(3,20),UNITMEAS$=B1$(22,2)
4150 IF B1$(3,6)<>FILL(6) THEN LET DATEREQ$=FNB6$(B1$(3,6))
4160 IF B1$(9,6)<>FILL(6) THEN LET DATEPROM$=FNB6$(B1$(9,6))
4165 IF B1$(15,6)<>FILL(6) THEN LET NOTB4DATE$=FNB6$(B1$(15,6))
4170 LET CONVFACT=B[0]
4175 IF CONVFACT=0 THEN LET CONVFACT=1
4180 LET CONVFACT$=STR(CONVFACT:M5$),UNITCOST$=STR((B[1]/CONVFACT):M3$)
4185 LET QTYORD=B[3]*CONVFACT,QTYREC=B[7]*CONVFACT
4190 LET QTYORD$=STR(QTYORD:M2$),QTYREC$=STR(QTYREC:M2$)
4200 LET PONUM$=B0$(9,7),VENDOR$=FNF$(B0$(3,VENDLEN),VENDOMASK$)
4210 REM " --- Get extension
4220 GOSUB CALC_EXTENSION
4400 REM " --- Print detail line
4410 IF L+1>L9 THEN GOSUB 5000
4420 PRINT (7)@(O[1]),PONUM$,@(O[2]),B0$(16,3),@(O[3]),VENDOR$,@(O[4]),UNITMEAS$,@(O[5]),DATEREQ$,@(O[6]),DATEPROM$,@(O[7]),NOTB4DATE$,@(O[8]),CONVFACT$,@(O[9]),QTYORD$,@(O[10]),QTYREC$,@(O[11]),BALANCE$,@(O[12]),UNITCOST$,@(O[13]),EXTENSION$
4430 LET L=L+1
4440 DONE_PRINTDETAIL: 
4450 RETURN 
5000 REM " --- Heading
5010 LET L=HEADINGS+1
5020 GOSUB ASSIGNHEADS
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS>0 THEN EXITTO 9900
5110 PRINT (7)@(0),"From Vendor: ",VENDF$," ",VENDFNAME$(1,20),@(42),"From Item: ",ITEMF$,@(75),"From Purchase Order: ",POF$,@(117-LEN(DATETYPE$)),"From ",DATETYPE$,": ",DATEF$
5120 PRINT (7)@(2),"To Vendor: ",VENDT$," ",VENDTNAME$(1,20),@(44),"To Item: ",ITEMT$,@(77),"To Purchase Order: ",POT$,@(119-LEN(DATETYPE$)),"To ",DATETYPE$,": ",DATET$
5130 PRINT (7)""
5140 PRINT (7)@(O[5]),"Date",@(O[6]),"Date",@(O[7]),"Not",@(O[8]+M5-7),"Conv",@(O[9]+M2-9),"Quantity",@(O[10]+M2-9),"Quantity",@(O[12]+M3-5),"Unit",@(O[14]+M2-9),"Quantity"
5150 PRINT (7)@(O[0]),"Item",@(O[1]),"PO #",@(O[2]),"Seq",@(O[3]),"Vendor",@(O[4]),"UM",@(O[5]),"Required",@(O[6]),"Promised",@(O[7]),"Before",@(O[8]+M5-7),"Factor",@(O[9]+M2-9),"Ordered",@(O[10]+M2-9),"Received",@(O[11]+M2-8),"Balance",@(O[12]+M3-5),"Cost",@(O[13]+M1-10),"Extension",@(O[14]+M2-9),"On Order"
5160 PRINT (7)""
5180 LET L=L+6
5190 RETURN 
5300 REM " --- Assign headings variables
5305 ASSIGNHEADS: 
5310 IF VENDFROM$=FILL(6) THEN LET VENDF$="First" ELSE LET VENDF$=VENDFROM$
5320 IF VENDTHRU$=FILL(6) THEN LET VENDT$="Last" ELSE LET VENDT$=VENDTHRU$
5325 IF ITEMFROM$=FILL(20) THEN LET ITEMF$="First" ELSE LET ITEMF$=ITEMFROM$
5330 IF ITEMTHRU$=FILL(20) THEN LET ITEMT$="Last" ELSE LET ITEMT$=ITEMTHRU$
5340 IF POFROM$=FILL(7) THEN LET POF$="First" ELSE LET POF$=POFROM$
5350 IF POTHRU$=FILL(7) THEN LET POT$="Last" ELSE LET POT$=POTHRU$
5360 IF DATEFROM$="" THEN LET DATEF$="First" ELSE LET DATEF$=FNB6$(DATEFROM$)
5370 IF DATETHRU$="" THEN LET DATET$="Last" ELSE LET DATET$=FNB6$(DATETHRU$)
5390 LET HEADINGS$[1]=HEADINGS$[1](1,LEN(F5$))+" For Warehouse "+WHSE$+" "+CVS(WHSENAME$,2)
5395 RETURN 
5600 REM " --- Determine date to use when testing range in detail               
5605 GETTESTDATE_DETAIL: 
5610 ON POS(WHICHDATE$="OPRD") GOTO ORDR_D,ORDR_D,PROM_D,REQD_D,NOTB4_D
5620 ORDR_D: 
5640 GOTO 5740
5650 PROM_D: 
5660 LET TESTDATED$=B1$(9,6)
5670 GOTO 5740
5680 REQD_D: 
5690 LET TESTDATED$=B1$(3,6)
5700 GOTO 5740
5710 NOTB4_D: 
5720 LET TESTDATED$=B1$(15,6)
5730 GOTO 5740
5740 RETURN 
6000 REM " --- Test for Breaks
6005 TESTBREAKS: 
6010 IF B3$(1,2)=WHSE$ THEN GOTO TESTITEM
6020 IF PRINTITOT$="Y" THEN GOSUB ITEMTOTALS
6030 IF PRINTWTOT$="Y" THEN GOSUB WHSETOTALS
6040 GOSUB NEWWHSE
6050 GOSUB NEWITEM
6060 GOTO 6095
6070 TESTITEM: 
6080 IF B3$(3,20)=ITEMNUM$ THEN GOTO 6095
6085 GOSUB ITEMTOTALS
6090 GOSUB NEWITEM
6095 RETURN 
6100 REM " --- New Warehouse
6105 NEWWHSE: 
6120 DIM C0$(160)
6130 LET WHSETOT=0,WHSENAME$="Not On File",WHSE$=B3$(1,2)
6140 LET PRINTWTOT$="Y",PRINTITOT$="N"
6150 FIND (IVM10_DEV,KEY=N0$+"C"+WHSE$,DOM=6170)IOL=IVM10C
6160 LET WHSENAME$=C0$(6,24)
6180 GOSUB 5000
6210 RETURN 
6300 REM " --- New Item
6305 NEWITEM: 
6310 LET ITEMTOT=0,QTYORDTOT=0,QTYRECTOT=0,BALTOT=0,PRINTITOT$="Y"
6320 DIM ITEMDESC$(60),X1$(60)
6330 LET ITEMDESC$(1)="Not On File",ITEMNUM$=B3$(3,20)
6340 FIND (IVM01_DEV,KEY=N0$+B3$(3,20),DOM=6370)IOL=IVM01A
6350 LET ITEMDESC$(1)=FNITEM$(X1$,DESCLEN[1],DESCLEN[2],DESCLEN[3])
6370 LET QTYONORDER=0
6380 FIND (IVM02_DEV,KEY=N0$+B3$,DOM=6410)IOL=IVM02A
6410 IF L+3>L9 THEN GOSUB 5000
6440 PRINT (7)@(0),ITEMNUM$(1,ITEMLEN)," ",ITEMDESC$,@(O[14]),QTYONORDER:M2$
6470 LET L=L+1
6490 RETURN 
6500 REM " --- Print Warehouse total
6505 WHSETOTALS: 
6520 IF L+2>L9 THEN GOSUB 5000
6530 PRINT (7)@(O[1]),"Total For Warehouse ",WHSE$," ",WHSENAME$,@(O[13]),WHSETOT:M1$
6540 PRINT (7)""
6550 LET L=L+2
6560 RETURN 
6600 REM " --- Print Item total
6605 ITEMTOTALS: 
6620 IF L+2>L9 THEN GOSUB 5000
6630 PRINT (7)@(O[1]),"Total For Item ",ITEMNUM$(1,ITEMLEN)," ",ITEMDESC$(1,22),@(O[9]),QTYORDTOT:M2$,@(O[10]),QTYRECTOT:M2$,@(O[11]),BALTOT:M2$,@(O[13]),ITEMTOT:M1$
6640 PRINT (7)""
6650 LET L=L+2
6660 RETURN 
6900 REM " --- Calc extension and Balance
6905 CALC_EXTENSION: 
6930 LET BALANCE=QTYORD-QTYREC,BALANCE$=STR(BALANCE:M2$)
6950 PRECISION 2
6960 LET EXTENSION=B[1]/CONVFACT*BALANCE,EXTENSION$=STR(EXTENSION:M1$)
6970 PRECISION IVPRECISION
6990 RETURN 
8000 REM " --- Functions
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

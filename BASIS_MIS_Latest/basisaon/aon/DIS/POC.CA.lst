0010 REM "POC - Create Purchase Calendar"
0020 REM "Program POC.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "FILENAME$: Calendar file name POM-01/MPM-02 (input)"
0060 REM "STATUS   : 0=No error (returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER FILENAME$,STATUS
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]=FILENAME$
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],POM01_DEV=CHANNELS[2]
0200 REM " --- IOLISTs"
0210 POM01A: IOLIST A0$(1),A1$(1),A0
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0420 LET N0$=F0$(16,2)
0500 REM " --- Init Data"
0510 DIM A0$(6),A1$(31)
0550 LET R0$=F0$(7,3),R0=12,R8$="312831303130313130313031"
0560 LET R9$="SunMonTueWedThuFriSat",STATUS=0
0700 REM " --- Background"
0710 FOR X=4 TO 10 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(10,3),"Last Day Currently Scheduled:",@(19,5),"1st Day To Schedule:",@(23,7),"Number Of Weeks:",@(26,9),"Days To Omit:",'SF',
0800 REM " --- Position file"
0890 READ (POM01_DEV,KEY=N0$,DOM=0900)
0900 REM " --- Retrieve last date scheduled"
0910 CALL "POC.CE",POM01_DEV,N0$,BEGDATE$,ENDDATE$,STATUS
0920 LET X$="None"
0930 IF ENDDATE$="" THEN GOTO 0990
0950 CALL "SYC.CA",ENDDATE$,R0$,1
0960 LET X$=FNB$(ENDDATE$)
0990 PRINT @(40,3),X$,
1000 REM " --- 1st Day To Schedule"
1010 LET V4$="Enter The Beginning Date For Scheduling"
1020 LET V0$="D",V1$="CKR",V2$=R0$,V3$="",V0=3,V1=40,V2=5,I0=0
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1000,9900
1050 LET R0$=V$
1090 GOTO 3900
1100 REM " --- Number of Weeks"
1110 LET V4$="Enter Number Of Weeks To Schedule Ahead"
1120 LET V0$="N",V1$="KEC",V2$=STR(R0),V3$="##0",V0=3,V1=40,V2=7
1130 GOSUB 7000
1140 ON V3 GOTO 1150,1100,3900,1100,3900
1150 IF V<=0 THEN GOTO 1100
1180 LET R0=V
1190 GOTO 3900
1200 REM " --- Days to Omit"
1210 LET V4$="Enter Days To Omit As Work Days (1=Sun/2=Mon/3=Tue/4=Wed/5=Thu/6=Fri/7=Sat)"
1220 LET V0$="S",V1$="KEC",V2$=R1$,V3$="",V0=7,V1=40,V2=9,R2$="",R3$=""
1230 GOSUB 7000
1240 ON V3 GOTO 1250,1200,3900,1200,9900
1250 LET V$=FNP$(V$)
1260 IF V$="" THEN GOTO 1350
1270 FOR X=1 TO LEN(V$)
1280 IF POS(V$(X,1)="1234567")=0 THEN EXITTO 1200
1290 LET V=NUM(V$(X,1)),R2$=R2$+R9$((V-1)*3+1,3)+" ",R3$=R3$+R9$((V-1)*3+1,3)
1300 NEXT X
1310 FOR X=1 TO 7
1320 IF POS(STR(X:"0")=V$,1,2) THEN EXITTO 1200
1330 NEXT X
1350 LET R2$=FNP$(R2$),R1$=V$
1360 PRINT @(V1,V2),'CL',V$," ",R2$,
1390 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,1200,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4030 ON POS(V$="YN") GOTO 4000,4050,1000
4100 REM " --- Write"
4105 CALL "SYC.NB","Creating",8,COLUMN
4110 LET X$=FNYY_YY21$(FNM$(FNC$(R0$))),OLD_DATE$=X$
4120 GOSUB 6100
4210 FOR X=1 TO R0
4220 FOR Y=1 TO 7
4230 CALL "SYC.CB",X$,X0$,X0
4240 IF X$(1,4)<>OLD_DATE$(1,4) THEN GOSUB 6000
4250 IF POS(X0$=R3$,3)>0 THEN LET A1$(NUM(X$(5,2)),1)="C"; GOTO 4270
4260 LET A1$(NUM(X$(5,2)),1)="W"
4270 LET X9$=X$
4280 PRINT @(COLUMN,11),FNB6$(X$),
4285 LET X0$=""
4290 CALL "SYC.CA",X$,X0$,1
4295 LET X$=X0$
4300 NEXT Y
4310 NEXT X
4320 GOSUB 6000
4390 GOTO 9900
6000 REM " --- Write Record"
6010 LET A0$=N0$+OLD_DATE$(1,4)
6020 IF X9$(3,2)<>"02" THEN LET A0=NUM(R8$((NUM(X9$(3,2))-1)*2+1,2)); GOTO 6050
6030 CALL "SYC.CB",X9$(1,2)+"0229",X0$,X0
6040 IF X0<>0 THEN LET A0=29 ELSE LET A0=28
6050 WRITE (POM01_DEV,KEY=A0$)IOL=POM01A
6070 LET OLD_DATE$=X$
6080 GOSUB 6100
6090 RETURN 
6100 REM " --- Get existing record"
6110 LET A0$=N0$+X$(1,4),A1$(1)=""
6120 FIND (POM01_DEV,KEY=A0$,DOM=6190)IOL=POM01A
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 CLOSE (SYS01_DEV,ERR=9920)
9920 CLOSE (POM01_DEV,ERR=9930)
9950 EXIT 
9999 END

0010 REM "IVR - Transaction Register"
0020 REM "Program IVR.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=21
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="SYS-01",FILES$[2]="IVE-01",FILES$[3]="IVE-11"
0130 LET FILES$[4]="IVM-01",FILES$[5]="IVM-02",FILES$[6]="IVM-10"
0140 LET FILES$[7]="IVM-12",FILES$[8]="IVT-04"
0150 LET OPTIONS$[2]="L",OPTIONS$[3]="L"
0160 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1],IVE01_DEV=CHANNELS[2],IVE11_DEV=CHANNELS[3]
0185 LET IVM01_DEV=CHANNELS[4],IVM02_DEV=CHANNELS[5],IVM10_DEV=CHANNELS[6]
0190 LET IVM12_DEV=CHANNELS[7],IVT04_DEV=CHANNELS[8]
0200 REM " --- IOLists
0210 IVE01A: IOLIST C0$(1)
0215 IVE11A: IOLIST A0$,A1$,A2$,A[ALL]
0220 IVM01A: IOLIST B0$,B1$(1),B2$,B3$,B4$,B5$,Z0$(1)
0250 IVM10B: IOLIST X0$(1),X1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0285 ARS01A: IOLIST X$,X$,X$,A9$
0290 IVM02A: IOLIST D0$,D1$(1)
0300 ARM10D: IOLIST *,Z9$
0400 REM " --- Parameters"
0405 DIM P[5],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="IV",LS$="N",LF$="N",SOURCE$=PGM(-2),WHEN$=F0$(7,3)
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 LET G[0]=NUM(G2$(1,2)),G[3]=NUM(G2$(7,2)),G[4]=NUM(G2$(9,2))
0435 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(7,2)),P[2]=NUM(P2$(5,1))
0440 LET P[3]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2)),P[5]=NUM(P2$(13,2))
0442 LET DW=P[3]+P[4]+P[5],G5=LEN(G5$)
0444 LET G[0]=G[3],P9$=P3$(17,1),P8$="/Lot/Serial #",AR$="N",A9$="N"
0446 IF POS(P3$(17,1)="LS")>0 THEN LET LS$="Y" ELSE LET LS$="N",P8$=""
0448 IF POS(P3$(3,1)="LF")>0 THEN LET LF$="Y"
0450 CALL "SYC.RM",M1$,M1,11,15
0453 CALL "SYC.RM",M2$,M2,9,12
0455 CALL "SYC.RM",M3$,M3,5,12
0460 CALL "SYC.VA","AR",INFO$[ALL]
0465 LET AR$=INFO$[20]
0470 IF AR$<>"Y" THEN GOTO 0485
0475 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0480 LET A9$=A9$(11,1)
0485 LET GL$="N",STATUS=0
0490 CALL "GLC.BA",ERR=0495,SOURCE$,N2$,GLW11$,GL$,STATUS
0495 IF STATUS<>0 THEN GOTO 9900
0500 REM " --- Initializations"
0505 PRECISION P[2]
0510 DIM C0$(50),A[2],HEADINGS$[2],O[8],LF[10],ITEM$[10]
0520 LET WIDTH=132,PAGE=0,L9=59,L=L9+1,W=34,UNITS=0,ERRORS=0,GLH$="",GLH=0
0523 IF GL$="Y" THEN LET GLH$="GL Account",GLH=MAX(G5,LEN(GLH$)+2)
0525 LET HEADINGS=2,HEADINGS$[0]=F4$,HEADINGS$[1]=F5$,GLSTATUS=0,PLACES=P[2]
0530 LET O[1]=O[0]+4,O[2]=O[1]+3,O[3]=O[2]+P[0]+1,O[4]=O[3]+DW
0535 LET O[5]=O[4]+M2,O[6]=O[5]+M3,O[7]=O[6]+M1+1,O[8]=O[7]+GLH
0540 IF O[8]+3<WIDTH THEN GOTO 0555
0545 LET O[8]=WIDTH-3,O[7]=O[8]-GLH-1,O[6]=O[7]-M1-1,O[5]=O[6]-M3,O[4]=O[5]-M2
0550 LET O[1]=O[0]+5,O[2]=O[1]+3,O[3]=O[2]+P[0]+1,DW=O[4]-O[3]-1
0555 IF LS$="N" THEN GOTO 0570
0560 LET FILES$[9]="IVM-07",FILES$[10]="IVM-08",FILES$[11]="IVM-17"
0565 LET FILES$[12]="IVT-01",FILES$[13]="IVT-02",FILES$[14]="IVT-03"
0570 IF LF$="N" THEN GOTO 0580
0575 LET FILES$[15]="IVM-04",FILES$[16]="IVW-04",OPTIONS$[16]="CL"
0580 IF GL$<>"Y" THEN GOTO 0590
0585 LET FILES$[17]="GLM-01",FILES$[18]=GLW11$,OPTIONS$[18]="CL"
0590 IF AR$="Y" THEN IF A9$="Y" THEN LET FILES$[19]="ARM-10"
0600 REM " --- Additional File Opens"
0610 CALL "SYC.DA",1,9,19,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS>0 THEN GOTO 9900
0630 LET IVM07_DEV=CHANNELS[9],IVM08_DEV=CHANNELS[10],IVM17_DEV=CHANNELS[11]
0640 LET IVT01_DEV=CHANNELS[12],IVT02_DEV=CHANNELS[13],IVT03_DEV=CHANNELS[14]
0650 LET IVM04_DEV=CHANNELS[15],IVW04_DEV=CHANNELS[16],GLM01_DEV=CHANNELS[17]
0660 LET GLW11_DEV=CHANNELS[18],ARM10_DEV=CHANNELS[19]
0670 LET LF[0]=IVM04_DEV,LF[1]=IVW04_DEV,LF[2]=IVM02_DEV
0800 REM " --- Run Report?
0810 LET V4$="Do You Want To Print The "+F5$
0820 CALL "SYC.YN",1,V4$,2,V$,V3
0830 IF V3=4 THEN GOTO 9900
0840 IF V$<>"Y" THEN GOTO 9900
0900 REM " --- Position file"
0910 CALL "SYC.GA",7,1,"","",STATUS
0920 IF STATUS THEN GOTO 9900
0930 CALL "SYC.NB","Printing",11,COLUMN
0990 READ (IVE01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next IVE-01 header"
1010 LET K$=KEY(IVE01_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 READ (IVE01_DEV,END=4000)IOL=IVE01A
1090 READ (IVE11_DEV,KEY=K$(1,9),DOM=1100)
1100 REM " --- Read next IVE-11 detail"
1110 LET K$=KEY(IVE11_DEV,END=4000)
1120 IF POS(K$(1,9)=C0$)<>1 THEN GOTO 3900
1130 READ (IVE11_DEV)IOL=IVE11A
1200 REM " --- Breaks"
1210 IF A0$(3,7)<>T0$ THEN GOSUB 6000
1280 PRINT @(COLUMN,11),A0$(3,7),"-",A0$(10,3)
1300 REM " --- Additional Reads"
1310 DIM B0$(22),B1$(60),Z0$(G[4]),DESCRIPTION$(DW)
1320 LET B0$(1)=N0$+A1$(3),B1$(1)="Unknown Item"
1330 FIND (IVM01_DEV,KEY=B0$,DOM=1340)IOL=IVM01A
1340 LET DESCRIPTION$(1)=FNITEM$(B1$,P[3],P[4],P[5])
1350 IF LS$="N" THEN GOTO 1500
1360 IF B2$(19,2)<>"YY" THEN GOTO 1500
1390 GOTO 2000
1500 REM " --- LIFO/FIFO?"
1510 IF LF$="N" THEN GOTO 2000
1520 IF X0$(26,1)="C" THEN GOTO 2000
1525 LET ITEM$[0]=N0$,ITEM$[1]=A1$(1,2),ITEM$[2]=A1$(3,20),LF_DATE$=C0$(15,3)
1530 LET ACTION$=P3$(3,1)+"R",QUANTITY=A[0],COST=A[1],RET_COST=0,TYPE$="R"
1540 IF X0$(26,1)="I" THEN LET ACTION$=P3$(3,1)+"I"
1550 IF X0$(26,1)="A" THEN IF QUANTITY<0 THEN LET ACTION$=P3$(3,1)+"I"
1560 CALL "IVC.LF",ACTION$,TYPE$,LF_DATE$,LF[ALL],ITEM$[ALL],PLACES,QUANTITY,COST,RET_COST,STATUS
1600 REM " --- Cost change?"
1610 IF A[1]=RET_COST THEN GOTO 2000
1620 IF RET_COST<>0 THEN LET A[1]=RET_COST
1630 PRECISION 2
1640 LET A[2]=A[0]*A[1]
1650 PRECISION P[2]
1690 WRITE (IVE11_DEV,KEY=A0$(1,12))IOL=IVE11A
2000 REM " --- New page?"
2010 IF L+2<L9 THEN GOTO 2100
2020 GOSUB 5000
2030 GOSUB 5200
2100 REM " --- Print detail line"
2110 LET ACCOUNT$="",LOTSER$="",T0=T0+A[2],L=L+2
2120 IF GL$="Y" THEN LET ACCOUNT$=FNF$(A0$(28,G[0]),G5$)
2130 IF LS$="Y" THEN IF B2$(20,1)="Y" THEN LET LOTSER$=A2$(1,P[1])
2140 PRINT (7)@(O[0]),A0$(10,3),@(O[1]),A1$(1,2),@(O[2]),A1$(3,P[0]),@(O[3]),DESCRIPTION$,@(O[4]),A[0]:M2$,@(O[5]),A[1]:M3$,@(O[6]),A[2]:M1$,@(O[7]),ACCOUNT$,@(O[8]),A0$(38,3)
2150 PRINT (7)@(O[3]),LOTSER$
3000 REM " --- G/L Distribution"
3010 IF GL$<>"Y" THEN GOTO 3800
3020 IF X0$(26,1)="C" THEN GOTO 3800
3100 REM " --- Distribute by Item?"
3110 IF X0$(27,1)="N" THEN GOTO 3800
3120 IF A9$="N" THEN GOTO 3200
3130 DIM D1$(40)
3140 FIND (IVM02_DEV,KEY=N0$+A1$,DOM=3200)IOL=IVM02A
3150 FIND (ARM10_DEV,KEY=N0$+"D"+D1$(13,2),DOM=3200)IOL=ARM10D
3160 LET Z0$(1)=Z9$(61)
3200 REM " --- Post summary record"
3210 PRECISION 2
3220 LET ACCOUNT$=Z0$,GLDATE$=C0$(15,3),MEMO$=A1$(3)+" "+DESCRIPTION$
3225 LET REF1$="Inv Trn "+C0$(13,2),REF2$=A0$(3,7),REF3$=A0$(10,3)
3230 LET AMOUNT=A[2]
3235 IF X0$(26,1)="I" THEN LET AMOUNT=-AMOUNT
3240 GOSUB GLRECAP
3250 LET ACCOUNT$=A0$(28,G[4]),AMOUNT=-AMOUNT
3260 GOSUB GLRECAP
3290 PRECISION P[2]
3800 REM " --- Loop back for next detail"
3890 GOTO 1100
3900 REM " --- Loop back for next header"
3990 GOTO 1000
4000 REM " --- Totals"
4010 LET T9$="E",UPDATE$="IVU.AA"
4020 GOSUB 6000
4080 IF GL$="Y" THEN RUN "GLR.XA"
4090 RUN UPDATE$,ERR=9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+2
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)@(O[0]),"Seq",@(O[1]),"Wh",@(O[2]),"Item",@(O[3]),"Description",P8$,@(O[4]+M2-9),"Quantity",@(O[5]+M3-5),"Cost",@(O[6]+M1-11),"Total Cost",@(O[7]),GLH$,@(O[8]),"Who"
5090 RETURN 
5200 REM " --- Trans Heading"
5210 IF L+6>L9 THEN GOSUB 5000
5220 PRINT (7)""
5230 PRINT (7)"Reference ",C0$(3,7),@(23),"Transaction Code: ",C0$(13,2)," ",X0$(6,20),@(73),"Transaction Date: ",FNB$(C0$(15,3)),@(103),"Comment: ",C0$(18,20)
5240 IF GL$<>"Y" THEN GOTO 5270
5250 CALL "GLC.EA",C0$(15,3),"N",PERIOD$,YEAR$,GLSTATUS
5260 IF GLSTATUS>0 THEN CALL "GLC.GA",7,C0$(15,3),GLSTATUS,L,ERRORS,STATUS
5270 PRINT (7)""
5280 LET L=L+3
5290 RETURN 
6000 REM " --- Reference"
6010 IF T0$="" THEN GOTO 6100
6020 IF L+1>L9 THEN GOSUB 5000
6030 PRINT (7)@(O[6]-27),"Total For Reference ",T0$,@(O[6]),T0:M1$
6040 LET L=L+1
6100 IF T9$<>"" THEN RETURN 
6110 DIM X0$(32),X1$(G[4])
6130 LET T0$=C0$(3,7),X0$(6,20)="(Not On File)",T0=0
6140 FIND (IVM10_DEV,KEY=N0$+"B"+C0$(13,2),DOM=6150)IOL=IVM10B
6150 GOSUB 5200
6190 RETURN 
6900 REM " --- Standard G/L Recap Routine"
6910 GLRECAP: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLW11_DEV,GLT05_DEV,ACCOUNT$,GLDATE$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

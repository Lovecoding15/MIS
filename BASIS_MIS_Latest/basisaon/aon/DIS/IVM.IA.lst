0010 REM "IVM - Lot/Serial Transaction History By Customer Inquiry"
0020 REM "Program IVM.IA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=11
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-03"
0130 LET FILES$[4]="IVM-10",FILES$[5]="IVM-14",FILES$[6]="IVT-01"
0140 LET FILES$[7]="IVT-02",FILES$[8]="ARM-01",FILES$[9]="ARM-02"
0150 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM03_DEV=CHANNELS[3]
0180 LET IVM10_DEV=CHANNELS[4],IVM14_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1)
0220 ARM01A: IOLIST B0$(1),B1$(1)
0230 ARM02A: IOLIST C0$(1),C1$(1)
0250 IVM10A: IOLIST X0$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0275 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$
0280 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$
0285 GLS01A: IOLIST G0$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0405 DIM P[5],G[1],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="IV",WIDTH=80
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0425 IF POS(I3$(17,1)="LS")=0 THEN GOTO 4900
0430 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1)),P[2]=NUM(I2$(7,2))
0433 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0435 CALL "SYC.VA","AR",INFO$[ALL]
0440 IF INFO$[20]<>"Y" THEN GOTO 4800
0445 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0450 CALL "SYC.VA","GL",INFO$[ALL]
0455 LET GL$=INFO$[20],CUSTLEN=NUM(P2$(1,2)),CUSTMASK$=M0$,CUSTIN$=P1$
0460 IF GL$="N" THEN GOTO 0475
0465 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0470 LET G[1]=NUM(G2$(7,2)),FILES$[10]="GLM-01"
0475 CALL "SYC.VA","AP",INFO$[ALL]
0480 LET AP$=INFO$[20]
0485 IF AP$="N" THEN GOTO 0500
0490 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0495 LET VENDLEN=NUM(P2$(1,2)),VENDMASK$=M0$,FILES$[11]="APM-01"
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM FILES[6],PARAMS[13],PARAMS$[15],X0$(32),A0$(22),A1$(60)
0515 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),IFILES[14],IPARAMS[3]
0520 DIM B0$(8),B1$(30),C0$(10),C1$(64),D0$(44),D1$(96),D[12],T0$(61)
0535 LET WIN_X=0,WIN_Y=7,HEIGHT=24-WIN_Y,TITLE$="",NAME$=""
0545 LET H0=WIDTH-1,IFILES[1]=IVM01_DEV,IFILES[14]=IVM14_DEV
0550 LET M4=LEN(M4$),M2=LEN(M2$),M3=LEN(M3$),FILES[0]=IVM01_DEV
0560 LET FILES[1]=IVM10_DEV,PARAMS$[0]=N0$,PARAMS$[9]=M4$,PARAMS[11]=P[2]
0565 LET PARAMS$[10]=M2$,PARAMS$[11]=M3$,PARAMS[0]=P[0],PARAMS[4]=P[3]
0570 LET PARAMS[5]=P[4],PARAMS[6]=P[5],PARAMS[7]=5,PARAMS[8]=HEIGHT-5
0575 LET PARAMS[9]=WIDTH,PARAMS[10]=HEIGHT,PARAMS[11]=P[2],PARAMS[12]=P[1]
0580 LET PARAMS[1]=CUSTLEN,PARAMS$[1]=CUSTMASK$,DW=63-(14+P[0])
0590 LET IPARAMS[0]=P[0],IPARAMS[1]=P[3],IPARAMS[2]=P[4],IPARAMS[3]=P[5]
0600 REM " --- Additional File Opens"
0605 DIM DESCRIPTION$(DW)
0610 CALL "SYC.DA",1,6,11,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS>0 THEN GOTO 9900
0630 LET ARM01_DEV=CHANNELS[8],ARM02_DEV=CHANNELS[9],IVT02_DEV=CHANNELS[7]
0640 LET FILES[2]=ARM01_DEV,FILES[5]=IVT02_DEV,FILES[6]=CHANNELS[6]
0650 IF AP$="Y" THEN LET PARAMS[2]=VENDLEN,PARAMS$[2]=VENDMASK$,FILES[3]=CHANNELS[11]
0660 IF GL$="Y" THEN LET PARAMS[3]=G[1],PARAMS$[3]=G5$,FILES[4]=CHANNELS[10]
0670 LET HEADING$(2)="Prd Lot/Serial #",HEADING$(6+MAX(P[2],14))="Wh Item"
0680 LET HEADING$(H0-M4-M3-9)="Quantity",HEADING$(H0-M4-5)="Cost"
0690 LET HEADING$(H0-6)="Price",FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0700 REM " --- Background"
0710 PRINT 'SB',@(4,3),"Customer:",@(59),"Last Sale:",@(5,4),"Product:",@(56),"Last Payment:",@(0,5),"Lot/Serial #:",@(64),"From:",@(1,6),"Item Number:",@(66),"To:",'SF',
0720 CALL "SYC.WC",1,0,80,0,0,WIN_Y,0
0900 REM " --- Position file"
0990 READ (IVT02_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Customer"
1010 CALL "SYC.WB",TNAME$,0
1015 PRINT 'CF',
1020 LET V2$="",V2$=KEY(IVT02_DEV,END=1050)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3,CUSTLEN)
1050 LET V0$="M",V1$="CKE",V3$=CUSTIN$,V0=6,V1=14,V2=3,I0=0
1055 LET V4$="Enter A Valid Customer Number (<F3>=Lookup)"
1060 GOSUB 7000
1070 ON V3 GOTO 1100,1000,1000,1080,9900
1080 CALL "SYC.LC",V$
1200 REM " --- Find Key"
1210 LET B0$(1)=N0$+V$,B1$(1)="",C0$(1)=B0$,C1$(1)="",T0$(1)=B0$
1215 LET V1$="",V2$="",PRODUCT$="",LOTSER$="",ITEM$=""
1220 FIND (ARM01_DEV,KEY=B0$,DOM=1230)IOL=ARM01A
1230 FIND (ARM02_DEV,KEY=C0$,DOM=1240)IOL=ARM02A
1240 IF FNP$(C1$(14,3))<>"" THEN LET V1$=FNB$(C1$(14,3))
1250 IF FNP$(C1$(17,3))<>"" THEN LET V2$=FNB$(C1$(17,3))
1260 PRINT @(V1,V2),FNF$(B0$(3,CUSTLEN),CUSTMASK$)," ",B1$,@(70),V1$,'CL',@(70,4),V2$,'CL',
1290 READ (IVT02_DEV,KEY=T0$,DOM=1300)
1300 REM " --- Product type"
1310 LET V2$="",V2$=KEY(IVT02_DEV,END=1340)
1320 IF POS(T0$(1,8)=V2$)<>1 THEN LET V2$=""
1330 IF V2$<>"" THEN LET V2$=V2$(9,3)
1340 LET V0$="S",V1$="KE",V3$="",V4$="",V0=3,V1=14,V2=4,I0=1
1345 LET V4$="Enter A Valid Product Type (<F1>=All/<F3>=Lookup)"
1350 GOSUB 7000
1360 ON V3 GOTO 1400,1500,3900,1370,9900
1370 CALL "IVC.LK",1,V1,V2,V$
1400 REM " --- Display product"
1410 LET X0$(1)=N0$+"A"+V$,T0$(9)=V$
1420 IF V$="" THEN GOTO 1500
1430 FIND (IVM10_DEV,KEY=X0$(1,6),DOM=1300)IOL=IVM10A
1440 PRINT @(V1,V2),X0$(4,3)," ",X0$(7,20),
1450 LET PRODUCT$=X0$(4,3)
1480 READ (IVT02_DEV,KEY=T0$,DOM=3900)
1490 GOTO 3900
1500 REM " --- All Products"
1510 PRINT @(V1,V2),"All",FILL(21),
1520 LET PRODUCT$=""
1590 GOTO 3900
2000 REM " --- Lot/Serial Number"
2010 LET X=8,V2$="",V2$=KEY(IVT02_DEV,END=2040)
2015 IF PRODUCT$<>"" THEN LET X=11
2020 IF POS(T0$(1,X)=V2$)<>1 THEN LET V2$=""
2030 IF V2$<>"" THEN LET V2$=V2$(12,20)
2040 LET V0$="S",V1$="KE",V3$="",V4$="",V0=P[2],V1=14,V2=5
2045 LET V4$="Enter A Valid Lot/Serial Number (<F1>=All)"
2050 GOSUB 7000
2060 ON V3 GOTO 2100,2200,3900,2000,3900
2100 REM " --- Lot/Serial Number"
2110 IF FNP$(V$)="" THEN GOTO 2200
2120 LET LOTSER$=V$+FILL(P[2]),LOTSER$=LOTSER$(1,P[2])
2190 GOTO 3900
2200 REM " --- All lot/serial numbers"
2210 PRINT @(V1,V2),"All",FILL(P[2]),
2220 LET LOTSER$=""
2290 GOTO 3900
2400 REM " --- Item Number"
2410 DIM ITEMS$[6],ITEM[12],DESCRIPTION$(DW)
2420 LET ACTION$="A",V1=14,V2=6
2430 CALL "IVC.IA",ACTION$,V1,V2,IFILES[ALL],N0$,A0$(3),V4$,IPARAMS[ALL],ITEMS$[ALL],ITEM[ALL],V3
2440 ON V3 GOTO 2450,2500,3900,2400,3900
2450 IF FNP$(ITEMS$[0])="" THEN GOTO 2500
2460 LET A0$(1)=N0$+ITEMS$[0]
2470 FIND (IVM01_DEV,KEY=A0$,DOM=2400)IOL=IVM01A
2480 LET ITEM$=A0$(3),DESCRIPTION$(1)=FNITEM$(A1$,P[3],P[4],P[5])
2490 PRINT @(V1,V2),A0$(3,P[0])," ",DESCRIPTION$,
2495 GOTO 3900
2500 REM " --- All items"
2510 LET DESCRIPTION$(1)="",ITEM$=""
2520 PRINT @(V1,V2),"All",FILL(P[0]-2),DESCRIPTION$,
2590 GOTO 3900
2600 REM " --- Beginning Date"
2610 LET V0$="D",V1$="CE",V2$=BEGDATE$,V3$="",V4$="",V0=3,V1=70,V2=5
2615 LET V4$="Enter The Date Of The First Transaction To Display (<F1>=First)"
2620 GOSUB 7000
2630 ON V3 GOTO 2650,2640,3900,2600,3900
2640 LET V$=""
2650 IF V$="" THEN PRINT @(V1,V2),"First   ",
2660 LET BEGDATE$=V$
2690 GOTO 3900
2800 REM " --- Ending Date"
2810 LET V0$="D",V1$="CE",V2$=ENDDATE$,V3$="",V4$="",V0=3,V1=70,V2=6
2815 LET V4$="Enter The Date Of The Last Transaction To Display (<F1>=Last)"
2820 GOSUB 7000
2830 ON V3 GOTO 2850,2840,3900,2800,3900
2840 LET V$=""
2850 IF V$="" THEN PRINT @(V1,V2),"Last    ",
2860 IF V$<>"" THEN IF V$<BEGDATE$ THEN GOTO 2800
2870 LET ENDDATE$=V$
2890 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1300,2000,2400,2600,2800,4000
4000 REM " --- Call display routine"
4005 GOSUB 5000
4010 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
4020 PRINT @(0,0),'SB','BR',HEADING$,'ER','SF',
4025 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
4030 LET ACTION$="",PARAMS$[4]=D0$,PARAMS$[5]=BEGDATE$,PARAMS$[6]=ENDDATE$
4035 LET PARAMS$[7]=NAME$,PARAMS$[12]=B0$(3),PARAMS$[13]=PRODUCT$
4040 LET PARAMS$[14]=LOTSER$,PARAMS$[15]=ITEM$
4050 CALL "IVC.TB",ACTION$,FILES[ALL],PARAMS[ALL],PARAMS$[ALL],SELECTION$,FKEY
4060 CALL "SYC.WD",NAME$
4090 GOTO 1300
4800 REM " --- AR Not Installed"
4810 CALL "IVC.SB","R",STATUS
4890 GOTO 9900
4900 REM " --- Lotted/Serialized Inventory not enabled"
4910 CALL "IVC.SB","L",STATUS
4990 GOTO 9900
5000 REM " --- Display"
5010 LET V0$="All"+FILL(21),V1$="All"+FILL(P[2]),X0$(1)=N0$+"A"+PRODUCT$
5020 LET DESCRIPTION$(1)="",V2$="All"+FILL(P[0]-2)+DESCRIPTION$
5030 LET A0$(1)=N0$+ITEM$,V3$="First",V4$="Last"
5040 FIND (IVM10_DEV,KEY=X0$(1,6),DOM=5050)IOL=IVM10A
5050 IF PRODUCT$<>"" THEN LET V0$=X0$(4,3)+" "+X0$(7,20)
5060 FIND (IVM01_DEV,KEY=A0$,DOM=5070)IOL=IVM01A
5070 IF ITEM$<>"" THEN LET V2$=A0$(3,P[0])+" "+FNITEM$(A1$,P[3],P[4],P[5])
5080 IF LOTSER$<>"" THEN LET V1$=LOTSER$
5090 IF BEGDATE$<>"" THEN LET V3$=FNB$(BEGDATE$)
5100 IF ENDDATE$<>"" THEN LET V4$=FNB$(ENDDATE$)
5110 PRINT @(14,4),V0$,@(14,5),V1$,@(14,6),V2$,@(70,5),V3$,@(70,6),V4$,
5190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9910 IF TNAME$<>"" THEN CALL "SYC.WD",TNAME$
9920 IF BNAME$<>"" THEN CALL "SYC.WD",BNAME$
9950 RUN "SYS.AA"
9999 END

0010 REM "IVM - Item Maintenance (Warehouse Information)"
0020 REM "Program IVM.MC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 PRECISION P[1]
0200 REM " --- IOLIST's"
0220 IVM02A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0290 IVS10D: IOLIST X$,I5$(1)
0300 IVM10C: IOLIST W0$(1)
0310 ARM10D: IOLIST Y0$(1)
0320 IVM03B: IOLIST X0$(1)
0330 IVM03C: IOLIST X0$(1)
0340 IVM03F: IOLIST X0$(1)
0350 IVM03G: IOLIST X0$(1)
0500 REM " --- Initializations"
0510 DIM B0$(24),B1$(64),B[20],W0$(32),X0$(25),LP$[2],CP$[6],CP[6]
0550 LET O=75-MAX(M3,M4),DV$="Distribution Code:",FI$=" For This Item"
0560 LET LP$[1]="Current",LP$[2]="Prior"
0570 LET CP$[1]="Unit",CP$[2]="Landed",CP$[3]="Last Purchase Order"
0580 LET CP$[4]="Average",CP$[5]="Standard",CP$[6]="Replacement"
0585 LET CP[1]=11,CP[2]=15,CP[3]=16,CP[4]=12,CP[5]=13,CP[6]=14
0590 IF DI$<>"Y" THEN LET DV$=""
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,11,0
0720 CALL "SYC.WC",1,0,80,0,0,15,0
0750 PRINT 'SB',@(5,6),"Warehouse ID:",@(O-12),"Cycle Code:",@(9,7),"Location:",@(O-10),"Selected:",@(0,8),DV$,@(O-15),"Last Physical:",@(4,9),"Special Order:",@(8,11)," Prices ",@(5,12),"Current List:",@(O-12),"Prior List:",@(7,13),"Price Code:",@(O-12),"Price Code:",@(8,15)," Costs ",@(8,16),"Unit Cost:",@(O-14),"Average Cost:",@(6,17),"Landed Cost:",@(O-15),"Standard Cost:",@(5,18),"Last PO Cost:",@(O-18),"Replacement Cost:",'SF',
0900 REM " --- Position file"
0990 READ (IVM03_DEV,KEY=A0$(1,2)+"G"+A0$(3),DOM=1000)
1000 REM " --- Warehouse ID"
1005 PRINT 'CF',
1010 LET V2$="",V2$=KEY(IVM03_DEV,END=1040)
1020 IF POS(A0$(1,2)+"G"+A0$(3)=V2$)<>1 THEN LET V2$=""
1030 IF V2$<>"" THEN LET V2$=V2$(24)
1040 LET V0$="S",V1$="KCE",V3$="",V0=2,V1=19,V2=6
1045 LET V4$="Enter A Valid Warehouse ID (<F3>=Lookup)"
1047 IF I3$(1,1)<>"Y" THEN LET V$=I3$(6,2); GOTO 1080
1050 GOSUB 7000
1055 ON V3 GOTO 1065,1000,1000,1060,4900
1060 CALL "IVC.LK",3,V1,V2,V$
1065 IF V$="" THEN GOTO 0900
1070 LET V$=V$+"  ",V$=V$(1,2)
1080 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=1000)IOL=IVM10C
1090 LET B0$(1)=A0$(1,2)+V$+A0$(3)
1200 REM " --- Retrieve record"
1210 PRINT @(V1,V2),W0$(4,2)," ",W0$(6,24),
1220 FIND (IVM02_DEV,KEY=B0$,DOM=1400)IOL=IVM02A
1230 LET LOCATION$=B1$(1,10),CYCLE$=B1$(11,2),BUYER$=B1$(19,3),VENDOR$=B1$(22,6)
1300 REM " --- Display record"
1310 GOSUB 5000
1320 LET O1=0
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,1000
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Initializations for new record"
1510 DIM B1$(64),B[20],I5$(25)
1520 LET B1$(15,1)="N",B1$(56,5)="ADDDN",O1=1
1550 FIND (IVS10_DEV,KEY=N0$+"D",DOM=1560)IOL=IVS10D
1560 LET B1$(13,2)=I5$(23,2),B1$(19,3)=I5$(9,3),B1$(56,4)=I5$(18,4)
1570 LET LOCATION$=B1$(1,10),CYCLE$=B1$(11,2),BUYER$=B1$(19,3),VENDOR$=B1$(22,6)
1590 GOSUB 5000
2000 REM " --- Location"
2005 IF B1$(15,1)="Y" THEN GOTO 2100
2010 LET V4$="Enter The Location Of This Item In This Warehouse"
2020 LET V0$="S",V1$="C",V2$=B1$(1,10),V3$="",V0=10,V1=19,V2=7,I0=0
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 LET B1$(1,10)=V$
2090 GOTO 3900
2100 REM " --- Selected for physical"
2110 DIM MESSAGE$[1]
2120 LET MESSAGE$[0]="Location Or Cycle Code May Not Be Changed When The Item Has Been"
2130 LET MESSAGE$[1]="Selected For Physical Inventory (<Enter>=Continue)"
2140 CALL "SYC.XA",2,MESSAGE$[ALL],1,-1,-1,V$,V3
2190 GOTO 3900
2200 REM " --- Distribution Code"
2205 IF DI$<>"Y" THEN GOTO 3900
2210 LET V4$="Enter A Valid Distribution Code (<F3>=Lookup)"
2220 LET V0$="S",V1$="C",V2$=B1$(13,2),V3$="",V0=2,V1=19,V2=8
2230 GOSUB 7000
2240 ON V3 GOTO 2260,2200,3900,2250,3900
2250 GOSUB 6300
2255 IF V$="" THEN GOTO 2200
2260 LET B1$(13,2)=V$
2270 FIND (ARM10_DEV,KEY=N0$+"D"+B1$(13,2),DOM=2200)IOL=ARM10D
2275 LET DISTRIB$(1)=Y0$(6,20)
2280 PRINT @(V1,V2),B1$(13,2)," ",DISTRIB$,
2290 GOTO 3900
2300 REM " --- Special Order?"
2310 LET V4$="Is This A Special Order (Non-Stock) Item For This Warehouse (Y/N)?"
2320 LET V0$="Y",V1$="CR",V2$=B1$(60,1),V3$="",V0=1,V1=19,V2=9
2330 GOSUB 7000
2340 ON V3 GOTO 2350,2300,3900,2300,3900
2350 LET B1$(60,1)=V$
2390 GOTO 3900
2400 REM " --- Cycle Code"
2405 IF B1$(15,1)="Y" THEN GOTO 3900
2410 LET V4$="Enter A Physical Inventory Cycle Code"+FI$
2420 LET V0$="S",V1$="C",V2$=B1$(11,2),V3$="",V0=2,V1=O,V2=6
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 LET B1$(11,2)=V$
2490 GOTO 3900
2500 REM " --- Last Physical"
2505 IF O0<2 THEN GOTO 3900
2510 LET V4$="Enter The Date Of The Last Physical Inventory"+FI$
2520 LET V0$="D",V1$="C",V2$=B1$(16,3),V3$="",V0=3,V1=O,V2=8
2530 GOSUB 7000
2540 ON V3 GOTO 2550,2500,3900,2500,3900
2550 LET B1$(16,3)=V$
2590 GOTO 3900
2600 REM " --- Current/Prior Price"
2610 LET X=(I0-3)/2,V4$="Enter The "+LP$[X]+" List Price"+FI$,V1=19
2615 IF X>1 THEN LET V1=O
2620 LET V0$="N",V1$="C",V2$=STR(B[8+X]),V3$=M4$,V0=M4,V2=12
2630 GOSUB 7000
2640 ON V3 GOTO 2650,2600,3900,2600,3900
2650 IF V<0 THEN GOTO 2600
2660 LET B[8+X]=V
2690 GOTO 3900
2700 REM " --- Current/Prior List Price Code"
2710 LET X=(I0-4)/2,V4$="Enter The "+LP$[X]+" List Price Code"+FI$,V1=19
2715 IF X>1 THEN LET V1=O
2720 LET V0$="S",V1$="C",V2$=B1$(50+(X*2),2),V3$="",V0=2,V2=13
2730 GOSUB 7000
2740 ON V3 GOTO 2750,2700,3900,2700,3900
2750 LET B1$(50+(X*2),2)=V$
2790 GOTO 3900
2800 REM " --- Costs"
2805 IF O0<2 AND O1=0 THEN GOTO 3900
2810 LET X=(I0-8),V4$="Enter The "+CP$[X]+" Cost For The Warehouse/Item"
2815 LET V0$="N",V1$="C",V2$=STR(B[CP[X]]),V3$=M3$,V0=M3,V1=19,V2=I0+7
2820 IF I0>11 THEN LET V1=O,V2=(I0-3)+7
2825 GOSUB 7000
2830 ON V3 GOTO 2835,2800,3900,2800,3900
2835 LET B[CP[X]]=V
2840 PRINT @(V1,V2),V:M3$,
2845 IF I0>9 THEN GOTO 3900
2850 FOR X=2 TO 6
2855 IF B[CP[X]]<>0 THEN GOTO 2880
2860 LET B[CP[X]]=V,V1=19,V2=I0+7
2865 IF I0>11 THEN LET V1=O,V2=(I0-3)+7
2870 PRINT @(V1,V2),V:M3$,
2880 NEXT X
2890 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,2300,2400,2500,2600,2700,2600,2700,2800,2800,2800,2800,2800,2800,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="KC",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4050 IF V$="DELETE" THEN GOTO 4400
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Update IVM-03 Xrefs"
4110 DIM X0$(37)
4120 LET X0$(1)=B0$(1,2)+"B"+B0$(3,2)+CYCLE$+LOCATION$+B0$(5)
4125 REMOVE (IVM03_DEV,KEY=X0$,DOM=4130)
4130 LET X0$(1)=B0$(1,2)+"B"+B0$(3,2)+B1$(11,2)+B1$(1,10)+B0$(5)
4135 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03B
4140 DIM X0$(28)
4150 LET X0$(1)=B0$(1,2)+"C"+B0$(3,2)+A2$(1,3)+B0$(5)
4155 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03C
4160 DIM X0$(37)
4165 LET X0$(1)=B0$(1,2)+"F"+BUYER$+VENDOR$+A2$(1,3)+B0$(5)+B0$(3,2)
4170 REMOVE (IVM03_DEV,KEY=X0$,DOM=4175)
4175 LET X0$(1)=B0$(1,2)+"F"+B1$(19,3)+B1$(22,6)+A2$(1,3)+B0$(5)+B0$(3,2)
4180 IF A2$(24,1)="W" THEN WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03F
4185 DIM X0$(25)
4190 LET X0$(1)=N0$+"G"+B0$(5)+B0$(3,2)
4195 WRITE (IVM03_DEV,KEY=X0$)IOL=IVM03G
4200 REM " --- Update IVM-02 Warehouse"
4210 WRITE (IVM02_DEV,KEY=B0$)IOL=IVM02A
4280 IF I3$(1,1)<>"Y" THEN GOTO 4900
4290 GOTO 1000
4400 REM " --- Delete warehouse and associated records"
4410 DIM PARAMS$[10],PARAMS[10]
4420 LET ACTION$="W",PARAMS$[0]=N0$,PARAMS$[1]=B0$(3,2),PARAMS$[2]=A0$(3)
4430 LET PARAMS$[3]=OP$,PARAMS$[4]=PO$,PARAMS$[5]=WO$,PARAMS$[6]=BM$
4440 LET PARAMS$[7]=AP$,PARAMS[0]=G[0],PARAMS[1]=P[6],PARAMS[2]=P[7]
4450 CALL "IVC.DA",ACTION$,CHANNELS[ALL],PARAMS$[ALL],PARAMS[ALL],STATUS
4460 IF STATUS THEN GOTO 4000
4490 GOTO 1000
4900 REM " --- Run menu overlay"
4990 RUN "IVM.MA"
5000 REM " --- Display record"
5010 DIM W0$(32),Y0$(32),CODE$(2),DISTRIB$(20)
5020 LET Y0$(1)=B0$(1,2)+"D"+B1$(13,2),W0$(1)=B0$(1,2)+"C"+B0$(3,2),WHEN$=""
5030 IF FNP$(B1$(16,3))<>"" THEN LET WHEN$=FNB$(B1$(16,3))
5040 IF DI$<>"Y" THEN GOTO 5070
5050 FIND (ARM10_DEV,KEY=Y0$(1,5),DOM=5060)IOL=ARM10D
5060 LET CODE$(1)=Y0$(4,2),DISTRIB$(1)=Y0$(6,20)
5070 FIND (IVM10_DEV,KEY=W0$(1,5),DOM=5080)IOL=IVM10C
5080 PRINT @(19,6),B0$(3,2)," ",W0$(6,24),@(O),B1$(11,2),@(19,7),B1$(1,10),@(O),B1$(15,1),@(19,8),CODE$," ",DISTRIB$,@(O),WHEN$,@(19,9),B1$(60,1),@(19,12),B[9]:M4$,@(O),B[10]:M4$,@(19,13),B1$(52,2),@(O),B1$(54,2),@(19,16),B[11]:M3$,@(O),B[12]:M3$,@(19,17),B[15]:M3$,@(O),B[13]:M3$,@(19,18),B[16]:M3$,@(O),B[14]:M3$,
5090 RETURN 
6300 REM " --- Distribution Code Lookup"
6310 DIM DESCRIPTION$[1],FIELD[1],POSITION[1],LENGTH[1]
6320 LET RECORD$="D",TITLE$="Distribution Codes",NUMBER=1,CHANNEL=ARM10_DEV
6330 LET DESCRIPTION$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6340 LET DESCRIPTION$[1]="Description",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6350 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,ROW=V2+2,V$="SKIP"
6360 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6390 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "ARR - Monthly Sales Register"
0020 REM "Program OPR.ZB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files"
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STSTUS>0 THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 ART03A: IOLIST A0$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8],A[9],A[10]
0220 ART13A: IOLIST B0$,B1$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13],B[14]
0230 ARM10E: IOLIST Y0$(1),Y1$(1)
0235 ARM01A: IOLIST *,X0$(1)
0240 IVM02A: IOLIST K0$,K1$
0250 ARM10D: IOLIST *,D9$(1)
0260 ARM10B: IOLIST TAX$(1),X0,X1
0270 ARM10B2: IOLIST X0$,X0,X1
0280 ARW14A: IOLIST W0$,W0
0282 ARW14A2: IOLIST Z0$,Z0
0285 GLM01A: IOLIST *,X1$(1)
0290 GLW04A: IOLIST Z0$,Z0,Z1
0400 REM " --- Parameters"
0410 LET G5=LEN(G5$),M0=LEN(M0$),M1=LEN(M1$)
0500 REM " --- Initializations"
0510 DIM A[10],B[14]
0520 LET T0$="",T1$="",D1=0,D2=0,D3=0,C1=0,C2=0,C3=0
0530 IF R1$="" THEN LET H5$="First" ELSE IF R0$="A" THEN LET H5$=R1$ ELSE LET H5$=FNF$(R1$,M0$)
0535 IF R2$="" THEN LET H6$="Last" ELSE IF R0$="A" THEN LET H6$=R2$ ELSE LET H6$=FNF$(R2$,M0$)
0540 IF R3$="" THEN LET H7$="First" ELSE LET H7$=FNB$(R3$)
0545 IF R4$="" THEN LET H8$="Last" ELSE LET H8$=FNB$(R4$)
0550 IF R5$="S" THEN LET H9$="*** Summary ***",X8$="",X9$="" ELSE LET H9$="*** Detail ***",X8$="No.",X9$="Line"
0560 LET WHEN$=F0$(7,3),CLOCK$="",PAGE=0,WIDTH=132,L9=55,L=L9+1,HEADINGS=5
0565 DIM HEADINGS$[HEADINGS],J1$(WIDTH),J2$(WIDTH,"-"),J3$(WIDTH,"=")
0570 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,HEADINGS$[2]=""
0575 DIM X$(WIDTH); LET X$(1)="Beginning Customer: "+H5$,X$(INT((WIDTH-LEN(H9$))/2))=H9$,X$(100)="Beginning Invoice Date: "+H7$
0580 LET HEADINGS$[3]=X$
0585 DIM X$(WIDTH); LET X$(4)="Ending Customer: "+H6$,X$(103)="Ending Invoice Date: "+H8$
0590 LET HEADINGS$[4]=X$
0700 REM " --- Background"
0710 CALL "SYC.NB"," Sorting",M0+9,COLUMN
1000 REM " --- Create Work File "
1020 IF R0$="N" THEN READ (ART03_DEV,KEY=N0$+"  "+R1$,DOM=1120)
1030 READ (ARM04_DEV,KEY=N0$+"A"+R1$,DOM=1040)
1050 LET S0$=KEY(ARM04_DEV,END=3000)
1060 IF S0$(1,3)<>N0$+"A" THEN GOTO 3000
1070 IF R2$<>"" AND S0$(4,LEN(R2$))>R2$ THEN GOTO 3000
1080 READ (ARM04_DEV)
1090 IF R0$="A" THEN READ (ART03_DEV,KEY=N0$+"  "+S0$(14),DOM=1100)
1100 REM " --- Initial Read "
1120 IF R0$="N" THEN LET A0$=KEY(ART03_DEV,END=2900) ELSE IF R0$="A" THEN LET A0$=KEY(ART03_DEV,END=1050)
1130 IF A0$(1,2)<>N0$ THEN GOTO 2900
1140 IF R0$="A" THEN IF A0$(5,6)<>S0$(14,6) THEN GOTO 1050
1150 IF R0$="N" AND R2$<>"" AND A0$(5,6)>R2$ THEN GOTO 3000
1180 READ (ART03_DEV,KEY=A0$)IOL=ART03A
1220 PRINT @(COLUMN,11),FNF$(A0$(5,P[0]),M0$)+" "+FNB$(A0$(24,3)),
1240 IF R3$<>"" AND A0$(24,3)<R3$ THEN GOTO 1100
1250 IF R4$<>"" AND A0$(24,3)>R4$ THEN GOTO 1100
1420 LET D0$=A0$(66,2)
1450 GOSUB 2400
2000 REM " --- Read Detail "
2010 READ (ART13_DEV,KEY=A0$(1,17),DOM=2011)
2020 LET B0$=KEY(ART13_DEV,END=1100)
2040 IF B0$(1,17)<>A0$(1,17) THEN GOTO 1100
2070 READ (ART13_DEV,KEY=B0$)IOL=ART13A
2100 REM " --- Line Type "
2110 DIM Y0$(32),Y1$(12)
2115 LET D1$=""
2120 FIND (ARM10_DEV,KEY=B0$(1,2)+"E"+B0$(21,1),DOM=2300)IOL=ARM10E
2130 IF Y0$(25,1)="M" THEN GOTO 2020
2140 IF Y0$(25,1)="S" THEN GOTO 2200
2150 IF Y0$(25,1)<>"O" THEN LET D0$=Y1$(11,2); GOTO 2300
2180 FIND (GLM01_DEV,KEY=B0$(1,2)+Y1$(1,10),DOM=2300)
2190 LET D1$=Y1$(1,10); GOTO 2360
2200 REM " --- Standard Item "
2210 LET D0$=A0$(66,2)
2220 IF P3$(11,1)<>"Y" THEN GOTO 2300
2230 FIND (IVM02_DEV,KEY=B0$(1,2)+B0$(31),DOM=2300)IOL=IVM02A
2240 LET D0$=K1$(13,2)
2300 REM " --- Find Revenue Account "
2320 IF D1$<>"" THEN GOTO 2360
2330 DIM D9$(80)
2340 FIND (ARM10_DEV,KEY=B0$(1,2)+"D"+D0$,DOM=2020)IOL=ARM10D
2350 LET D1$=D9$(21,10)
2360 LET D0=-B[6]
2370 IF R5$="D" THEN LET Z9$=B0$(18,3) ELSE LET Z9$="   "
2380 GOSUB 6800
2390 GOTO 2020
2400 REM " --- A/R Account, Cash Account "
2420 DIM D9$(80)
2430 FIND (ARM10_DEV,KEY=N0$+"D"+A0$(66,2),DOM=2700)IOL=ARM10D
2440 IF A0$(99,1)="Y" THEN LET D1$=D9$(11,10) ELSE LET D1$=D9$(1,10)
2450 LET D0=A[6]+A[0]+A[1]-A[2]
2470 LET Z9$="   "
2480 GOSUB 6800
2500 REM " --- Discount Amount "
2510 IF A[2]=0 THEN GOTO 2550
2520 LET D1$=D9$(31,10),D0=A[2],Z9$="   "
2540 GOSUB 6800
2550 REM " --- Freight Amount "
2555 IF A[1]=0 THEN GOTO 2600
2560 LET D1$=D9$(41,10),D0=-A[1],Z9$="   "
2590 GOSUB 6800
2600 REM " --- Tax Account "
2605 PRECISION 4
2610 DIM TAX$(60),X0$(60)
2620 FIND (ARM10_DEV,KEY=N0$+"B"+A0$(80,2),DOM=2700)IOL=ARM10B
2630 LET X=X0*A[4]/100
2632 PRECISION 2
2634 LET X=X*1,Y=X
2635 IF X1<>0 AND ABS(X)>X1 THEN LET X=X1*SGN(X)
2640 LET D1$=TAX$(46,G[4]),D0=-X
2643 IF D0<>0 THEN GOSUB 6800
2645 LET X0$(1)=TAX$
2650 FOR X9=1 TO 10
2655 PRECISION 4
2660 LET X1$=TAX$(26+2*(X9-1),2)
2670 IF X1$="  " THEN GOTO 2680
2672 FIND (ARM10_DEV,KEY=N0$+"B"+X1$,DOM=2680)IOL=ARM10B2
2673 LET X=X0*A[4]/100
2674 PRECISION 2
2675 LET X=X*1,Y=Y+X
2677 IF X1<>0 AND ABS(X)>X1 THEN LET X=X1*SGN(X)
2678 LET D1$=X0$(46,G[4]),D0=-X
2679 IF D0<>0 THEN GOSUB 6800
2680 NEXT X9
2682 PRECISION 2
2684 IF A[0]=Y THEN GOTO 2689
2686 LET X=A[0]-Y
2687 LET D1$=X0$(46,G[4]),D0=-X
2688 IF D0<>0 THEN GOSUB 6800
2700 RETURN 
2900 REM " --- End Of File? "
2910 IF R0$="A" THEN GOTO 1050
3000 REM " --- Initial Read From Work File "
3005 IF R5$<>"D" THEN GOTO 4100
3010 PRINT @(COLUMN-9,11),'SB',"Printing ",'SF',J1$(1,M0+9),
3020 IF R0$<>"N" THEN GOTO 3040
3030 READ (ARW14_DEV,KEY=N0$,DOM=3100)
3040 READ (ARM04_DEV,KEY=N0$+"A"+R1$,DOM=3050)
3050 LET S0$=KEY(ARM04_DEV,END=4000)
3055 IF S0$(1,3)<>N0$+"A" THEN GOTO 4000
3060 IF R2$<>"" AND S0$(4,LEN(R2$))>R2$ THEN GOTO 4000
3065 READ (ARM04_DEV)
3070 READ (ARW14_DEV,KEY=N0$+"  "+S0$(14,6),DOM=3080)
3100 REM " --- Heading Read "
3120 IF R0$="A" THEN LET W0$=KEY(ARW14_DEV,END=3050) ELSE LET W0$=KEY(ARW14_DEV,END=4000)
3130 IF W0$(1,2)<>N0$ THEN GOTO 3900
3140 IF R0$="A" THEN IF W0$(5,6)<>S0$(14,6) THEN GOTO 3050
3200 READ (ARW14_DEV,KEY=W0$)IOL=ARW14A
3300 REM " --- Breaks "
3320 IF W0$(5,6)<>T0$ THEN GOSUB 6000
3330 IF W0$(11,7)=T1$ THEN GOTO 3700
3340 IF D3-ABS(C3)<>0 THEN GOSUB 6200
3500 REM " --- Find Invoice Date "
3510 DIM A0$(117)
3520 READ (ART03_DEV,KEY=W0$(1,17)+"000",DOM=3530)IOL=ART03A
3600 REM "PRINT INVOICE NUMBER & DATE
3610 PRINT @(COLUMN,11),FNF$(W0$(5,P[0]),M0$)+" "+FNB$(A0$(24,3)),
3620 IF L+1>L9 THEN GOSUB 5000
3640 PRINT (7)@(39),W0$(11,7),@(48),FNB$(A0$(24,3)),
3650 LET T1$=W0$(11,7),D3=0,C3=0
3700 REM " --- Find G/L Name "
3710 DIM X1$(35)
3720 LET X1$(1)="*** Not On File ***"
3760 FIND (GLM01_DEV,KEY=N0$+W0$(18,10),DOM=3780)IOL=GLM01A
3770 LET X1$=X1$(1,35)
3800 REM " --- Print "
3820 IF L+1>L9 THEN GOSUB 5000
3840 PRINT (7)@(58),W0$(28,3),@(63),FNF$(W0$(18,G[3]),G5$),@(64+G5),X1$,
3850 IF W0>0 THEN PRINT (7)@(131-2*M1+2),W0:M1$ ELSE PRINT (7)@(132-M1),ABS(W0):M1$
3860 LET L=L+1
3870 IF W0>0 THEN LET D1=D1+W0,D2=D2+W0,D3=D3+W0 ELSE LET C1=C1+W0,C2=C2+W0,C3=C3+W0
3890 GOTO 3100
3900 REM "End of file?
3910 IF R0$="A" THEN GOTO 3050
4000 REM " --- End "
4010 LET T9$="E"
4020 GOSUB 6000
4040 IF L+2>L9 THEN GOSUB 5000
4050 PRINT (7)@(132-2*M1),J3$(1,2*M1)
4070 PRINT (7)@(10),"Grand Total:",
4080 IF D2-ABS(C2)<>0 THEN PRINT (7)@(131-3*M1),"***",
4090 PRINT (7)@(131-2*M1+2),D2:M1$,@(132-M1),ABS(C2):M1$,
4100 REM " --- G/L Account Totals
4110 LET RETAIN$="YES"
4120 RUN "GLR.XB"
5000 REM " --- Report Heading"
5010 LET L=0
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)@(0),"Customer",@(39),"Invoice",@(49),"Invoice",@(58),X9$,@(65),"G/L"
5110 PRINT (7)@(0),"Number",@(7),"Name",@(39),"Number",@(51),"Date",@(58),X8$,@(65),"No.",@(64+G5),"Description",@(WIDTH-M1-5),"Debit",@(WIDTH-7),"Credit",'LF'
5180 LET L=4
5190 RETURN 
6000 REM " --- Customer Break "
6010 IF T0$="" THEN GOTO 6100
6020 IF L+3>L9 THEN GOSUB 5000
6025 IF D3-ABS(C3)<>0 THEN GOSUB 6200
6030 PRINT (7)@(132-2*M1),J2$(1,2*M1)
6040 PRINT (7)@(10),"Customer ",FNF$(T0$,M0$)," Total: ",@(131-2*M1+2),D1:M1$,@(132-M1),ABS(C1):M1$,'LF'
6050 LET L=L+3
6100 IF T9$="E" THEN GOTO 6190
6110 DIM X0$(30)
6120 LET X0$(1)="*** Not On File ***"
6130 FIND (ARM01_DEV,KEY=W0$(1,2)+W0$(5,6),DOM=6150)IOL=ARM01A
6140 LET X0$=FNF$(W0$(5,P[0]),M0$)+" "+X0$(1,30)
6150 IF L+1>L9 THEN GOSUB 5000
6160 PRINT (7)@(0),X0$,
6170 LET T0$=W0$(5,6),D1=0,C1=0,D3=0,C3=0
6190 RETURN 
6200 REM " --- Previous Invoice Out Of Balance "
6220 PRINT (7)@(131-2*M1),"**",D3:M1$,@(132-M1),ABS(C3):M1$,'LF'
6230 LET L=L+2
6290 RETURN 
6800 REM " --- Write To Work File "
6820 DIM Z0$(30)
6830 LET Z0=0
6850 LET Z0$(1)=A0$(1,17)+D1$+Z9$
6860 FIND (ARW14_DEV,KEY=Z0$,DOM=6870)IOL=ARW14A2
6870 LET Z0=Z0+D0
6880 WRITE (ARW14_DEV,KEY=Z0$)IOL=ARW14A2
6900 REM " --- Update G/L Summary"
6920 IF D0=0 THEN GOTO 6990
6950 LET Z0$=A0$(1,2)+D1$,Z0=0,Z1=0
6960 READ (GLW04_DEV,KEY=Z0$,DOM=6970)IOL=GLW04A
6970 LET Z0=Z0+D0
6980 WRITE (GLW04_DEV,KEY=Z0$)IOL=GLW04A
6990 RETURN 
8000 REM " --- Functions "
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "IVC - Inventory SpeedSearch (Price & Availability)"
0020 REM "Program IVC.QB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open Files"
0105 LET FILES=5
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="IVM-01",FILES$[2]="IVM-02",FILES$[3]="IVM-03"
0130 LET FILES$[4]="IVM-10",FILES$[5]="IVM-14"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET IVM01_DEV=CHANNELS[1],IVM02_DEV=CHANNELS[2],IVM03_DEV=CHANNELS[3]
0180 LET IVM10_DEV=CHANNELS[4],IVM14_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$,A4$,A5$,A6$,A[ALL]
0220 IVM02A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0230 IVM10A: IOLIST Y0$(1)
0240 IVM10C: IOLIST X0$(1)
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$,M4$
0400 REM " --- Parameters"
0410 DIM P[5],X$(128)
0420 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2),TNAME$=FNP$(X$(3,8))
0430 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0435 LET SNAME$=FNP$(X$(19,8)),SWIDTH=NUM(X$(43,2)),SHEIGHT=NUM(X$(45,2))
0440 LET MAXOPT=NUM(X$(57,2)),ONAME$=FNP$(X$(27,8))
0445 LET SYS01_DEV=NUM(STBL("!SYS01"))
0450 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0455 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0480 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9900)IOL=IVS01A
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(5,1))
0495 LET P[3]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2)),P[5]=NUM(P2$(13,2))
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM A0$(22),A1$(60),A2$(64),A[12],OPTLIST$[2,MAXOPT,6],FILES[14]
0520 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),PARAMS[3],ITEM$[6]
0540 LET D_STATUS=0,MAXROW=HEIGHT-5,M3=LEN(M3$),M4=LEN(M4$)
0550 LET A0$(1)=N0$+OPTION$[3],H0=WIDTH-2,PGM$=PGM(-2),OPTION=0,DONE=0
0560 LET PARAMS[0]=P[0],PARAMS[1]=P[3],PARAMS[2]=P[4],PARAMS[3]=P[5]
0570 LET FILES[1]=IVM01_DEV,FILES[14]=IVM14_DEV,COLUMN=17+MAX(M3,M4)
0590 CALL "SYC.RM",M2$,M2,10,10
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,ONAME$
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0725 LET HEADING$(2)="WH Location",HEADING$(H0-M2*3-8)="On-Hand"
0730 LET HEADING$(H0-M2*2-10)="Committed",HEADING$(H0-M2-10)="Available"
0735 LET HEADING$(H0-9)="On Order",FOOTING$(2)=TITLE$
0740 LET FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0750 PRINT @(0,0),'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",SNAME$,0
0920 PRINT 'CF',
0930 CALL "SYC.WB",TNAME$,0
0940 PRINT 'CS',
0990 GOTO 1200
1000 REM " --- Process key field"
1010 GOSUB 6000
1015 CALL "SYC.WB",TNAME$,0
1020 PRINT @(0,0),'CS',@(0,2),'SB'," Item:",'SF',
1025 LET V2$="",V2$=KEY(IVM01_DEV,END=1040)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1035 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1040 LET ACTION$="V",V1=7,V2=2,V4$=""
1045 CALL "IVC.IA",ACTION$,V1,V2,FILES[ALL],N0$,V2$,V4$,PARAMS[ALL],ITEM$[ALL],A[ALL],V3
1050 ON V3 GOTO 1080,1000,1000,1000,1060,1070,1060
1060 LET OPTION$[1]="EXIT"
1065 GOTO 4000
1070 LET OPTION$[1]="STARTOVER"
1075 GOTO 4000
1080 LET V$=FNP$(ITEM$[0])
1090 IF V$="" THEN GOTO 0900
1095 LET A0$(3)=V$
1200 REM " --- Retrieve records for display"
1210 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,SUPALT$="Alt"
1220 READ (IVM01_DEV,KEY=A0$,DOM=1000)IOL=IVM01A
1230 IF A4$(1,1)="S" THEN LET SUPALT$="Sup"
1240 LET PAGES=MIN(INT((DSZ-5000)/4000),10)
1250 IF PAGES<3 THEN LET PAGES=3
1260 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,SHEIGHT-2],X$(WIDTH-2)
1270 LET X$(1)='SB'+" Item: "+'SF'+A0$(3,P[0])+" "+FNITEM$(A1$,P[3],P[4],P[5])
1280 LET X1$=A0$(3,P[0]),X2$=PARAM$
1290 CALL "SYC.QB",X$,X1$,X2$
1300 REM " --- Position xref"
1310 DIM Y0$(32)
1320 LET Y0$(7,20)="(Not On File)"
1330 FIND (IVM10_DEV,KEY=A0$(1,2)+"A"+A2$(1,3),DOM=1340)IOL=IVM10A
1340 LET TYPE$=A2$(1,3)+" "+Y0$(7,20),FIRST$=N0$+"G"+A0$(3)
1390 READ (IVM03_DEV,KEY=FIRST$,DOM=1400)
1400 REM " --- For each warehouse in IVM-01 record"
1410 LET K$=KEY(IVM03_DEV,END=1900)
1420 IF POS(FIRST$=K$)<>1 THEN GOTO 1900
1430 READ (IVM03_DEV)
1440 DIM B0$(24),B1$(64),B[20],X0$(32)
1450 LET B0$(1)=N0$+K$(24,2)+A0$(3),X0$(6)="(Not On File)"
1460 FIND (IVM02_DEV,KEY=B0$,DOM=1470)IOL=IVM02A
1470 FIND (IVM10_DEV,KEY=N0$+"C"+B0$(3,2),DOM=1500)IOL=IVM10C
1500 REM " --- Display page?"
1510 IF L<MAXROW THEN GOTO 1600
1520 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
1530 GOSUB 5000
1540 IF DONE THEN GOTO 4000
1580 IF ANSWER$<>"" THEN GOTO 3000
1590 LET L=1,X1=1
1600 REM " --- Middle window record"
1610 DIM G1$(WIDTH-2),G2$(SWIDTH-2)
1620 LET G1$(2)=B0$(3,2)+" "+B1$(1,10),G1$(H0-M2*4)=STR(B[0]:M2$)
1630 LET G1$(H0-M2*3)=STR(B[2]:M2$),G1$(H0-M2*2)=STR(B[0]-B[2]:M2$)
1635 LET G1$(H0-M2)=STR(B[1]:M2$)
1640 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$(3)
1700 REM " --- Bottom window record"
1710 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2)
1720 LET G2$(1)=" {MSR Price:}"+STR(A[2]:M4$)
1730 LET G2$(COLUMN)="{Whse:}"+B0$(3,2)+" "+X0$(6,20),MORE$[PAGE,X1,1]=G2$
1740 LET G2$(1)="{List Price:}"+STR(B[9]:M3$),G2$(COLUMN)="{Type:}"+TYPE$
1750 LET MORE$[PAGE,X1,2]=G2$
1760 LET G2$(1)=" {Unit Cost:}"+STR(B[11]:M3$)+" "+A2$(4,2)
1770 LET G2$(COLUMN)=" {"+SUPALT$+":}"+A4$(2),MORE$[PAGE,X1,3]=G2$
1790 LET LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
1800 REM " --- End of warehouse loop"
1890 GOTO 1400
1900 REM " --- No more warehouses"
1910 IF X0>0 THEN GOTO 2000
1920 DIM MESSAGE$[1]
1930 LET MESSAGE$[0]="No Warehouse Information Found (<Enter>=Continue)"
1940 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
1960 IF V3=5 OR V3=6 THEN GOTO 4000
1990 GOTO 3000
2000 REM " --- Display last page"
2010 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2020 GOSUB 5000
2030 IF DONE THEN GOTO 4000
2060 IF ANSWER$<>"" THEN GOTO 3000
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$=A0$(3),CHOICE2$=""
3030 GOSUB 5000
3040 IF CONTROL=1 THEN GOTO 1000
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CS',
6090 RETURN 
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9950 EXIT 
9999 END

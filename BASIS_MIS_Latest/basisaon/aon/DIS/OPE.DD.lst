0010 REM "OPE - Order Entry/Detail Lines"
0020 REM "Program OPE.DD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "S$=General Flag String"
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Flow Control On Return From OPE.DH"
0102 REM "O1=0 when not/done interfacing with OPE.DH
0104 REM "  =1 when successful return from OPE.DH
0106 REM "  =2 when not enough Serial/Lot numbers returned from OPE.DH
0140 PRECISION I[2]
0150 ON O1 GOTO 0200,1000,3095
0200 REM " --- IOLists"
0210 ARE03A: IOLIST A0$,A[ALL]
0220 ARE07A: IOLIST ARE07_K$
0230 ARE13A: IOLIST W0$(1),W1$(1),W[ALL]
0240 ARE23A: IOLIST T0$,T1$(1),T2$,T[ALL]
0250 ARM10E: IOLIST Y0$
0260 ART13A: IOLIST W0$(1),W1$(1),W[ALL]
0270 IVM01A: IOLIST X$,D1$(1),D2$(1),X$,D4$(1),X$,X$,D[ALL]
0280 IVM02A: IOLIST L0$,L1$,L2$,L[ALL]
0290 IVM10A: IOLIST Y1$
0300 IVM10C: IOLIST DEFLT_WHS$
0400 REM " --- Parameters"
0410 READ (ARM10_DEV,KEY=N0$+"E"+P3$(40,1),DOM=4900)IOL=ARM10E
0420 LET DEFLT_LCODE$=Y0$
0440 FIND (IVM10_DEV,KEY=N0$+"C"+I3$(6,2),DOM=4950)IOL=IVM10C
0470 LET BLANK_WHS$="N"
0480 FIND (IVM10_DEV,KEY=N0$+"C"+"  ",DOM=0500)
0490 LET BLANK_WHS$="Y"
0500 REM " --- Initialize Data"
0510 DIM D1$(DESCLEN),D2$(62),D4$(21),D[12],L[20],O[7],T1$(20),T[2],U[2],W0$(52),W1$(64),W[14]
0520 LET W0$(1)=A0$(1,17),S$(4,2)="01"
0530 DIM ITEM_DESC$(79-9-I[0]-1)
0550 REM " --- Print/Input Locations"
0555 LET L0=9,L1=L0+1,L=L1,L9=18
0560 DIM Q[6]
0565 LET Q[0]=77-M1
0570 LET Q[1]=76-M1-M2
0575 LET Q[2]=75-M1-2*M2
0580 LET Q[3]=74-M1-2*M2-M3
0585 LET Q[4]=73-M1-3*M2-M3
0590 LET Q[5]=72-M1-3*M2-2*M3
0600 REM " --- Background"
0610 PRINT @(0,L0),'CE',
0615 IF Q[5]+M3-5>13 THEN PRINT 'SB',@(Q[5]+M3-5),"Cost",
0620 PRINT 'SB',@(0,L0),"Seq C Wh Item",@(Q[4]+M2-6),"Order",@(Q[3]+M3-6),"Price",@(Q[2]+M2-4),"B/O",@(Q[1]+M2-5),"Ship",@(Q[0]+M1-10),"Extension",
0630 GOSUB PRINT_BTM_LINE
0640 IF S$(11,1)="1" THEN GOTO 4000
0650 REM " --- Get Price code For Cash Account"
0660 IF S$(2,2)<>"11" THEN GOTO 0700
0670 GOSUB GET_PRICE_CODE
0680 IF BAD_PRICE_CODE THEN GOTO 4200
0700 REM " --- Convert Historical Invoice"
0705 IF HIST_INV$="" THEN GOTO 0800
0710 READ (ART13_DEV,KEY=A0$(1,10)+HIST_INV$,DOM=0715)
0715 LET ART13_K$=KEY(ART13_DEV,END=0900)
0720 IF POS(A0$(1,10)+HIST_INV$=ART13_K$)<>1 THEN GOTO 0900
0725 READ (ART13_DEV)IOL=ART13A
0730 DIM Y0$(32); FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=0770)IOL=ARM10E
0735 IF Y0$(25,1)="M" THEN GOTO 0770
0740 IF Y0$(25,1)="O" AND W1$(44,1)="N" THEN LET W[6]=W[1],W[1]=0
0745 IF CREDIT<0 THEN LET W[2]=-W[4],W[6]=-W[6]
0750 IF Y0$(25,1)<>"O" THEN LET W[4]=W[2],W[3]=0,W[7]=0; PRECISION 2; LET W[6]=W[1]*W[4]; PRECISION I[2]
0755 IF POS(Y0$(25,1)="SP")=0 THEN IF Y0$(26,1)="Y" THEN LET W[7]=W[6]; GOTO 0770 ELSE GOTO 0770
0760 FIND (IVM01_DEV,KEY=N0$+W0$(33,20),DOM=0770)IOL=IVM01A
0765 IF D2$(8,1)="Y" AND Y0$(26,1)="Y" THEN LET W[7]=W[6]
0770 LET W0$(11,7)=A0$(11,7),W1$(41,3)=A0$(27,3),W1$(44,1)="Y",W1$(45,1)="N"
0772 IF W1$(41,3)>COMDATE$ THEN LET W1$(44,1)="N"
0775 IF BLANK_WHS$="N" AND W0$(31,2)="  " AND Y0$(27,1)="Y" AND P3$(41,1)="N" THEN LET W0$(31,2)=I3$(6,2); REM "Reset Drop-Ship BLANK Warehouse to Inventory Standard Warehouse"
0780 WRITE (ARE13_DEV,KEY=W0$(1,20))IOL=ARE13A
0785 LET S8$=W0$(21,1)+Y0$(25,1)+Y0$(27,1)+W0$(31,2)+W0$(33,20),S7=W[2],S8=W[6],S9=W[7],I=1
0790 IF W1$(44,1)="Y" THEN GOSUB UPDATE_TOTALS
0795 GOTO 0715
0800 REM " --- Get Invoice Totals"
0810 READ (ARE13_DEV,KEY=A0$(1,17),DOM=0815)
0815 LET ARE13_K$=KEY(ARE13_DEV,END=0900)
0820 IF POS(A0$(1,17)=ARE13_K$)<>1 THEN GOTO 0900
0825 READ (ARE13_DEV)IOL=ARE13A
0830 DIM Y0$(32); FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=0870)IOL=ARM10E
0835 IF Y0$(25,1)="M" THEN GOTO 0895
0840 IF W1$(44,1)="Y" OR A0$(21,1)="P" THEN GOTO 0855
0845 IF Y0$(25,1)<>"O" THEN LET W[3]=0,W[4]=0,W[6]=0,W[7]=0; REM "Can't ship if not committed
0850 IF Y0$(25,1)="O" AND W[6]<>0 THEN LET W[1]=W[6],W[6]=0,W[7]=0; REM "Can't extend price if not committed, so save it as Unit Price
0855 IF POS(Y0$(25,1)="SP")=0 THEN IF Y0$(26,1)="Y" THEN LET W[7]=W[6]; GOTO 0870 ELSE GOTO 0870
0860 FIND (IVM01_DEV,KEY=N0$+W0$(33,20),DOM=0870)IOL=IVM01A
0865 IF D2$(8,1)="Y" AND Y0$(26,1)="Y" THEN LET W[7]=W[6]
0870 LET U[0]=U[0]+W[6],U[1]=U[1]+W[7],U[2]=U[2]+W[0]*W[2]
0880 WRITE (ARE13_DEV,KEY=W0$(1,20))IOL=ARE13A
0895 GOTO 0815
0900 REM " --- Invoice Complete?"
0910 IF HIST_INV$<>"" THEN LET HIST_INV$=""
0950 REM " --- Display Existing Detail Lines"
0960 PRINT @(Q[0],8),U[0]:M1$,
0970 GOSUB 5100
0980 IF Q5=0 THEN GOTO 1400
0990 GOTO 4000
1000 REM " --- Overflow"
1010 PRINT @(Q[0],8),U[0]:M1$,
1020 IF L>L9 THEN PRINT @(0,20),'CE',@(0,L1),'LD','LD',
1030 GOSUB PRINT_BTM_LINE
1035 LET O1=0
1040 IF L>L9 THEN LET L=L9
1050 IF V$="N" THEN GOTO 1400
1055 IF V3=4 THEN GOTO 4000
1060 IF V$="LINE" THEN GOTO 1412
1100 REM " --- Next Key"
1120 LET K$=KEY(ARE13_DEV,END=1400)
1140 IF K$(1,17)<>W0$(1,17) THEN GOTO 1400
1150 READ (ARE13_DEV,KEY=K$)IOL=ARE13A
1160 GOTO 1490
1400 REM " --- Seq"
1410 LET W0=NUM(W0$(18,3))+10
1412 PRINT @(0,L),W0:"000",
1415 IF S$(4,2)="00" THEN LET W0$(18,3)=STR(W0:"000"); GOTO 1600
1416 IF S$(5,1)="1" THEN LET W0$(18,3)=STR(W0:"000"); GOTO 1470
1420 LET V0$="Z",V1$="KER",V2$=STR(W0),V3$="",V0=3,V1=0,V2=L,I0=0
1425 IF S$(4,1)="0" THEN PRINT @(4,L),'CL',@(0,L+1),'CL',
1430 GOSUB 7000
1432 IF V<=0 OR V3=2 THEN GOTO 1420
1440 IF V3=4 THEN GOTO 4000
1450 IF NUM(V$)=0 THEN GOTO 1412
1460 LET W0$(18,3)=V$
1480 FIND (ARE13_DEV,KEY=W0$(1,20),DOM=1600)IOL=ARE13A
1485 FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=1400)IOL=ARM10E
1490 IF W0$(1,20)<=K9$ THEN GOSUB 5400
1500 GOSUB 5000
1510 LET O$=W0$(21,1)+Y0$(25,1)+Y0$(27,1)+W0$(31,2)+W0$(33,20),O[0]=W[2],O[1]=W[6],O[2]=W[7],O[6]=W[0]*W[2],I0=11
1520 IF W1$(44,1)="N" THEN LET O[0]=0
1530 LET O1$=""
1540 GOTO 3300
1600 REM " --- Setup"
1610 IF S$(12,1)="1" THEN LET S$(1,1)="1"
1620 DIM W[14],O[7]
1630 LET W0$(21,1)=P3$(40,1),W0$(22,1)="N",W0$(26,1)="P",W0$(31,2)=R2$(32,2),W0$(33,20)=""
1640 LET W1$(1)="",W1$(41,3)=A0$(27,3),W1$(44,1)="Y"
1650 LET O$="",S$(4,1)="1",Y0$=DEFLT_LCODE$,I0=0,V3=0,W6=0,W7=0
1660 IF P3$(39,1)="Y" THEN PRINT @(4,L),P3$(40,1), ELSE PRINT @(4,L),W0$(21,1),
1670 IF POS(Y0$(25,1)="SP")<>0 THEN PRINT @(6,L),W0$(31,2),
1680 IF A0$(21,1)="P" OR W1$(41,3)>COMDATE$ THEN LET W1$(44,1)="N"
1690 GOTO 3200
2000 REM " --- Type"
2010 DIM Y0$(32)
2015 IF V3<>2 THEN IF P3$(39,1)="Y" THEN GOTO 2060
2020 LET V0$="S",V1$="KRC",V2$=W0$(21,1),V0=1,V1=4,V2=L,V4$="Enter A Valid Order/Invoice Line Code (<F3>=Lookup)"
2022 GOSUB 7000
2030 IF V3<>3 THEN GOTO 2035
2034 GOSUB LOOKUP_LINE
2035 IF V$=W0$(21,1) THEN GOTO 2040
2036 PRINT @(6,L),'CL',@(0,L+1),'CL',
2038 DIM W[14]; LET W0$(22,1)="N",W0$(23,3)="",W0$(26,1)="P",W0$(31,2)=R2$(32,2),W0$(33,20)="",W1$(1)="",W1$(41,3)=A0$(27,3),W1$(44,1)="Y"
2040 IF V3=2 THEN LET S$(4,2)="00"; GOTO 1420
2045 IF V3=4 AND S$(4,1)="1" THEN GOTO 4000
2050 LET W0$(21,1)=V$
2060 FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=2020)IOL=ARM10E
2070 PRINT @(4,L),W0$(21,1),@(1,21),'CE','SB',@(1,21),"Line Code: ",'SF',Y0$(5,20),
2075 IF Y0$(27,1)="Y" THEN PRINT @(0,L+1),"*Drpshp*",
2080 IF Y0$(25,1)="O" THEN IF POS(" "<>W1$(1,40))=0 THEN LET W1$(1,40)=Y0$(5,20); PRINT @(0,L+1),'CL',
2085 IF A0$(21,1)="P" OR W1$(41,3)>COMDATE$ THEN LET W1$(44,1)="N"
2090 GOTO 3200
2100 REM " --- Warehouse"
2110 IF V3<>2 AND Y0$(27,1)="Y" THEN IF P3$(41,1)="N" THEN LET W0$(31,2)=R2$(32,2); GOTO 2150 ELSE GOTO 2120
2115 IF V3<>2 AND (POS(Y0$(25,1)="MNO")<>0 OR R2$(31,1)="Y") THEN GOTO 2150
2120 LET V0$="S",V1$="KRC",V2$=W0$(31,2),V0=2,V1=6,V2=L,I0=3,V4$="Enter A Valid Warehouse Code (<F3>=Lookup)"
2122 GOSUB 7000
2130 IF V3<>3 THEN GOTO 2136
2134 GOSUB LOOKUP_WHS
2136 IF V3=2 THEN GOTO 3200
2138 IF V3=4 AND S$(4,1)="1" THEN GOTO 4000
2140 LET V3=0
2145 LET W0$(31,2)=V$
2150 FIND (IVM10_DEV,KEY=N0$+"C"+W0$(31,2),DOM=2120)IOL=IVM10C
2160 PRINT @(1,21),'CL','SB',"Warehouse: ",'SF',DEFLT_WHS$(6,20),
2170 PRINT @(6,L),W0$(31,2),
2190 GOTO 3200
2200 REM " --- Item Number"
2205 IF POS(Y0$(25,1)="MNO")<>0 THEN GOTO 3200
2210 DIM IV_ITEM$[6],IV_ITEM[12]
2220 LET V0$="",V2$=W0$(33,I[0]),V4$="",V0=I[0],V1=9,V2=L+1
2230 CALL "IVC.IA",V0$,V1,V2,IV_FILES[ALL],N0$,V2$,V4$,IV_PARAMS[ALL],IV_ITEM$[ALL],IV_ITEM[ALL],V3
2235 ON V3 GOTO 2250,2200,2290,2200,2240
2240 IF S$(4,1)="1" THEN GOTO 4000
2250 IF IV_ITEM$[0]<>W0$(33,20) THEN DIM W[14]; PRINT @(Q[3],L),'CL',
2255 LET W0$(33,20)=IV_ITEM$[0],ITEM_DESC$(1)=IV_ITEM$[1],D2$(1)=IV_ITEM$[2],D4$(1)=IV_ITEM$[4]
2260 READ (IVM02_DEV,KEY=N0$+W0$(31,2)+W0$(33,20),DOM=2300)IOL=IVM02A
2265 LET W[0]=L[11],W[5]=L[9],W0$(23,3)=D2$(1,3)
2270 PRINT @(V1,V2),W0$(33,20),@(V1+I[0]+1,V2),ITEM_DESC$,'CL',
2275 GOSUB PRINT_BTM_LINE
2280 PRINT @(1,21),'SB',"Warehouse: ",'SF',DEFLT_WHS$(6,20),'SB',"Type: ",'SF',D2$(1,3),@(44,21),'SB'," Class: ",'SF',D2$(21,3),@(55,21),'SB',"  Avail: ",'SF',L[0]-L[2]:M2$,@(1,23),'SB',"  On-Hand: ",'SF',L[0]:M2$,@(27,23),'SB',"Committed: ",'SF',L[2]:M2$,@(54,23),'SB',"On-Order: ",'SF',L[1]:M2$,
2285 GOSUB 6500
2290 GOTO 3200
2300 REM " --- Item Not In This Warehouse"
2320 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Warehouse Record Not On File For This Item (<Enter>=Continue)",V0=1,V1=FNV(V4$),V2=22
2340 GOSUB 7000
2360 LET W0$(33,20)=""
2390 GOTO 2200
2400 REM " --- Memo"
2410 IF POS(Y0$(25,1)="MNO")=0 THEN GOTO 3200
2420 LET V0$="S",V1$="",V2$=W1$(1,40),V0=40,V1=9,V2=L+1,W0$(33,20)=""
2430 IF POS(Y0$(25,1)="NO")=0 THEN LET W[6]=0
2440 GOSUB 7000
2460 LET W1$(1,40)=V$
2470 PRINT @(0,21),'CL',
2490 GOTO 3200
2500 REM " --- P/A"
2510 IF Y0$(25,1)<>"S" THEN GOTO 3200
2515 LET W0$(26,1)="P"
2516 GOTO 3200
2520 LET V0$="S",V2$=W0$(26,1),V0=1,V1=73,V2=L,V3$="PA",V4$="P=Pull, A=Assemble"
2530 GOSUB 7000
2540 LET W0$(26,1)=V$
2590 GOTO 3200
2600 REM " --- Cost"
2610 IF POS(Y0$(25,1)="NSP")=0 THEN GOTO 3200
2612 IF Y0$(27,1)="Y" THEN IF P3$(28,1)="N" THEN GOTO 3200 ELSE GOTO 2620
2615 IF POS(Y0$(25,1)="SP")<>0 THEN IF W[0]<>0 THEN GOTO 3200
2620 LET V0$="N",V2$=STR(W[0]),V3$=M3$,V1=Q[5],V2=L
2640 GOSUB 7000
2645 IF V3=2 THEN GOTO 3200
2650 IF V<0 THEN GOTO 2600
2660 LET W[0]=V
2690 GOTO 3200
2700 REM " --- Price"
2705 IF POS(Y0$(25,1)="N")<>0 THEN GOTO 2720
2710 IF POS(Y0$(25,1)="MO")<>0 THEN GOTO 3200
2715 IF S$(4,1)="1" AND S$(6,1)="0" THEN GOTO 3200
2720 LET V0$="N",V1$="C",V2$=STR(W[1]),V3$=M3$,V9$="",V0=12,V1=Q[3],V2=L
2725 IF POS(Y0$(25,1)="SP")<>0 THEN LET V4$="Press <F1> To Reprice"
2730 GOSUB 7000
2740 IF V3<>1 THEN LET V=NUM(V$,ERR=2720); PRINT @(Q[3],L),V:M3$,
2745 IF V3=1 THEN GOSUB 6020; LET W0$(22,1)="N"; IF W[1]=0 THEN GOTO 2700 ELSE GOTO 2775
2750 IF V<>W[1] THEN LET V9$="1"
2755 IF POS(Y0$(25,1)="SP")<>0 THEN IF V9$="1" THEN LET W0$(22,1)="Y"
2760 LET W[1]=V*1
2775 GOSUB 5300
2780 IF W0$(22,1)="Y" THEN PRINT @(Q[3],L),'SB',W[1]:M3$,'SF',
2790 GOTO 3200
2800 REM " --- Order"
2805 IF POS(Y0$(25,1)="MO")<>0 THEN GOTO 3200
2810 LET V0$="N",V1$="C",V2$=STR(W[2]),V3$=M2$,V4$="",V1=Q[4],V2=L
2815 IF POS(Y0$(25,1)="PS")>0 THEN LET V4$="<F1>=Item Comments   <F3>=Availability Lookup"
2820 GOSUB 7000
2825 IF POS(Y0$(25,1)="PS")=0 THEN GOTO 2850
2830 IF V3=3 THEN CALL "OPC.PA",IV_FILES[ALL],N0$,A0$(5,6),W0$(31,2),W0$(33,20),A0$(82,4),A0$(75,2),A0$(77,3),STATUS; GOTO 2800
2835 IF V3<>1 THEN GOTO 2850
2840 DIM INFO$[5],INFO[5]
2842 LET ROW=L+2,INFO$[2]="Item "+FNP$(W0$(33,I[0]))+" Comments",INFO[2]=8
2844 IF ROW+INFO[2]+3>22 THEN LET ROW=L-1-INFO[2]
2846 LET INFO$[0]="IVM-09",INFO$[1]=N0$+W0$(33,20),INFO[1]=ROW,ACTION=1
2848 CALL "SYC.KA",ACTION,INFO$[ALL],INFO[ALL]
2849 GOTO 2800
2850 IF W[2]=V AND W[1]<>0 THEN GOTO 2885
2855 IF W[2]=V OR V3=2 THEN GOTO 2870
2860 LET W[2]=V,W[3]=0,W[4]=0
2865 IF W1$(44,1)="Y" OR A0$(21,1)="P" THEN LET W[4]=W[2]
2870 IF POS(Y0$(25,1)="N")<>0 THEN GOTO 2885
2880 GOSUB 6000
2885 GOSUB 5300
2890 GOTO 3200
2900 REM " --- BO"
2905 IF P3$(42,1)="N" OR (P3$(15,1)="Y" AND A0$(5,6)=P3$(16,6)) THEN GOTO 2990
2910 IF POS(Y0$(25,1)="MO")<>0 THEN GOTO 2990
2920 IF W1$(44,1)="N" THEN GOTO 2990
2925 IF S$(4,1)="1" THEN LET V=0; GOTO 2960
2930 LET V0$="N",V2$=STR(W[3]),V3$=M2$,V1=Q[2],V2=L
2940 GOSUB 7000
2950 IF V=0 THEN LET W[3]=V
2960 IF W[3]<>V THEN LET W[3]=V,W[4]=W[2]-W[3]; GOSUB 5300
2990 GOTO 3200
3000 REM " --- Ship"
3005 IF POS(Y0$(25,1)="MO")<>0 THEN GOTO 3090
3010 IF W1$(44,1)="N" THEN GOTO 3090
3015 IF S$(4,1)="1" THEN GOTO 3200
3020 LET V0$="N",V2$=STR(W[4]),V3$=M2$,V1=Q[1],V2=L
3040 GOSUB 7000
3045 IF V<=W[2] THEN GOTO 3060
3050 LET V0$="S",V1$="C",V4$="Ship Quantity Exceeds Order Quantity (<Enter>=Continue)",V0=1,V1=FNV(V4$),V2=22
3055 GOSUB 7000; GOTO 3000
3060 IF W[4]<>V THEN LET W[3]=W[2]-V; IF W[3]<0 THEN LET W[3]=0
3062 LET W[4]=V
3064 IF P3$(42,1)="N" OR (P3$(15,1)="Y" AND A0$(5,6)=P3$(16,6)) OR POS(Y0$(25,1)="MO")<>0 THEN LET W[3]=0
3070 GOSUB 5300
3090 GOTO 3200
3095 LET O1=0,SERIN$="X"; REM "Returned with Short Ser/lot #'s
3097 LET W[3]=W[2]-W[4]
3099 GOTO 3064
3100 REM " --- Extension"
3110 IF POS(Y0$(25,1)="M")<>0 THEN GOTO 3200
3115 IF POS(Y0$(25,1)="NSP")<>0 THEN GOTO 3165
3120 LET V0$="N",V2$=STR(W[6]),V3$=M1$,V1=Q[0],V2=L
3125 LET V0=0
3140 GOSUB 7000
3150 PRECISION 2
3160 LET W[6]=V,W[7]=0
3162 PRECISION I[2]
3166 IF BALANCE+W[6]+U[0]-O[1]>C[6] AND V3<>2 AND S$(1,1)="0" THEN GOSUB 5500
3170 IF Y0$(26,1)="N" THEN GOTO 3200
3175 IF POS(Y0$(25,1)="OMN")=0 THEN IF D2$(8,1)="N" THEN GOTO 3200
3180 LET W[7]=W[6]
3190 GOTO 3200
3200 REM " --- Seq Control"
3210 IF V3=4 THEN IF I0>1 THEN LET I0=10; GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,2000,3700,2100,2200,2400,2600,2800,2700,2900,3000,3100,2500,3300
3300 REM " --- Correct?"
3305 IF Y0$(25,1)="M" OR W[3]<>0 THEN GOTO 3315
3310 IF W1$(44,1)="Y" AND ABS(W[6])<NUM(P3$(34,5)) THEN LET V4$="Extension Amount Is Less Than "+STR(NUM(P3$(34,5)):"$#####")+", <Enter>:" ELSE GOTO 3315
3312 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
3314 GOSUB 7000
3315 LET V8$=""
3317 IF W1$(44,1)<>"N" AND POS(Y0$(25,1)="PS")>0 AND D2$(19,2)="YY" THEN IF I3$(17,1)="L" THEN LET V8$="<F3>=Lot #'s, " ELSE IF I3$(17,1)="S" THEN LET V8$="<F3>=Serial #'s, "
3320 LET V0$="",V1$="KC",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=W0$(1,20),V4$="<Enter>=OK, <F1>=Add'l Options, <F2>=Modify, "+V8$+"D=Delete"
3325 GOSUB 7000
3330 IF V3=2 THEN LET S$(4,1)="0"; GOTO 3200
3335 IF W[5]<>0 THEN LET W[8]=100-W[1]*100/W[5]; GOTO 3365
3340 IF W[8]=100 THEN GOTO 3365
3345 PRECISION I[2]+3
3350 LET FACTOR=100/(100-W[8])
3355 PRECISION I[2]
3360 LET W[5]=W[1]*FACTOR
3365 IF V3=1 THEN GOSUB 6200; GOTO 3300
3370 IF A0$(21,1)<>"P" AND W1$(44,1)="N" AND Y0$(25,1)="O" AND W[6]<>0 THEN LET W[1]=W[6],W[6]=0,W[7]=0; PRINT @(Q[0],L),W[6]:M1$,
3375 GOSUB 6400
3380 LET O1$=W0$(21,1)+Y0$(25,1)+Y0$(27,1)+W0$(31,2)+W0$(33,20),O[3]=W[2],O[4]=W[6],O[5]=W[7],O[7]=W[0]*W[2]; IF W1$(44,1)="N" THEN LET O[3]=0
3385 IF V$="D" THEN GOTO 3500
3390 IF V$<>"" THEN GOTO 3300
3400 REM " --- Write"
3410 IF Y0$(25,1)="M" THEN LET W[6]=0
3420 IF POS(Y0$(25,1)="MO")<>0 THEN LET W[2]=0,W[3]=0,W[4]=0
3430 IF POS(Y0$(25,1)="NOP")<>0 THEN IF Y0$(32,1)="D" THEN LET W0$(23,3)=Y0$(29,3) ELSE IF Y0$(32,1)="N" THEN LET W0$(23,3)=""
3440 IF O$="" THEN LET S8$=O1$,S7=O[3],S8=O[4],S9=O[5],S0=O[7],I=1; GOTO 3470
3450 IF O1$=O$ THEN LET S8$=O1$,S7=O[3]-O[0],S8=O[4]-O[1],S9=O[5]-O[2],S0=O[7]-O[6],I=1; GOTO 3470
3460 IF O$<>O1$ THEN LET S8$=O$,S7=O[0],S8=O[1],S9=O[2],S0=O[6],I=-1; GOSUB UPDATE_TOTALS; LET S8$=O1$,S7=O[3],S8=O[4],S9=O[5],S0=O[7],I=1
3465 IF O$<>O1$ THEN IF POS(Y0$(25,1)="SP")>0 THEN IF POS(I3$(17,1)="LS")>0 AND D2$(19,1)="Y" THEN GOSUB 5600; REM "Go Delete Serial/Lot's
3470 GOSUB UPDATE_TOTALS
3475 LET L=L+2; PRINT @(0,21),'CE'
3480 WRITE (ARE13_DEV,KEY=W0$(1,20))IOL=ARE13A
3482 LET S$(4,2)="01"
3485 IF V3=3 AND W1$(44,1)<>"N" THEN IF POS(Y0$(25,1)="PS")>0 AND W1$(44,1)<>"N" AND POS(I3$(17,1)="LS")>0 AND POS("Y"=D2$(19,1))<>0 THEN LET PROGRAM$="OPE.DD",SERIN$=""; RUN "OPE.DH"
3487 LET SERIN$=""
3490 GOTO 1000
3500 REM " --- Delete"
3510 REMOVE (ARE13_DEV,KEY=W0$(1,20),DOM=3520)
3520 PRINT @(0,L),'CL',@(0,L+1),'CL',@(0,21),'CE'
3540 IF O$="" THEN IF POS(Y0$(25,1)="SP")=0 THEN GOTO 3590
3545 IF Y0$(27,1)="Y" AND S$(4,1)="1" THEN GOTO 3590
3550 IF O$="" THEN GOTO 3590
3560 LET I=-1,S8$=O$,S7=O[0],S8=O[1],S9=O[2],S0=O[6]
3570 GOSUB UPDATE_TOTALS
3580 IF POS(Y0$(25,1)="SP")>0 THEN IF POS(I3$(17,1)="LS")>0 AND D2$(19,1)="Y" THEN GOSUB 5600
3590 GOTO 1000
3600 REM " --- Update Order/Invoice Totals & Commit Inventory"
3605 UPDATE_TOTALS: 
3610 LET U[0]=U[0]+S8*I,U[1]=U[1]+S9*I,U[2]=U[2]+S0*I,ARE07_K$=N0$+S8$(4)+W0$(3,2)+W0$(11,10)+W0$(5,6)
3620 IF POS(S8$(2,1)="SP")=0 THEN GOTO 3690
3630 IF S8$(3,1)="Y" OR A0$(21,1)="P" THEN GOTO 3670; REM "Drop ship or promotional item
3640 LET IV_INFO$[1]=S8$(4,2),IV_INFO$[2]=S8$(6,20),IV_INFO$[3]="",IV_REFS[0]=S7
3650 IF I>0 THEN LET IV_ACTION$="OE" ELSE LET IV_ACTION$="UC"
3660 CALL "IVC.UA",IV_ACTION$,IV_FILES[ALL],IV_INFO[ALL],IV_PARAMS$[ALL],IV_INFO$[ALL],IV_REFS$[ALL],IV_REFS[ALL],IV_STATUS
3670 IF I=1 THEN WRITE (ARE07_DEV,KEY=ARE07_K$)IOL=ARE07A
3680 IF I=-1 THEN REMOVE (ARE07_DEV,KEY=ARE07_K$,DOM=3685)
3690 RETURN 
3700 REM " --- Product Type Input - Non-Stock"
3704 IF POS(Y0$(25,1)="SM")<>0 THEN GOTO 3790
3706 IF Y0$(32,1)<>"E" THEN GOTO 3790
3710 PRINT @(1,21),'CE','SB',@(1,21),"Line Code: ",'SF',Y0$(5,20),'SB',@(35,21),"Type: ",'SF',
3720 LET V0$="S",V1$="C",V2$=W0$(23,3),V3$="",V0=3,V1=41,V2=21,V4$="<F3>=Product Type Lookup"
3730 GOSUB 7000
3731 IF V3=2 THEN GOTO 3790
3732 IF V3<>3 THEN GOTO 3740
3736 GOSUB LOOKUP_PRODUCT
3750 LET W0$(23,3)=V$
3760 FIND (IVM10_DEV,KEY=N0$+"A"+W0$(23,3),DOM=3700)IOL=IVM10A
3770 PRINT @(45,21),Y1$(7,20),
3790 GOTO 3200
4000 REM " --- Correct?"
4005 PRINT @(0,L),'CE'; GOSUB PRINT_BTM_LINE
4010 PRINT @(23,21),"Is The Above Information Correct?",
4015 LET V4$="(Y/N/Review/List/Line #/F2=Ribbon Edit/Delete)?"
4020 IF C9$(7,1)<>"Y" OR A0$(21,1)="P" THEN LET PRINT$="YES",V4$=V4$(1,16)+"/SP/Print"+V4$(17)
4025 IF A0$(21,1)="S" THEN LET V4$=V4$(1,LEN(V4$)-23)+"Invoice/"+V4$(LEN(V4$)-22)
4030 LET V0$="S",V1$="KC",V2$="Y",V3$="",V0=7,V1=FNV(V4$),V2=22
4035 GOSUB 7000
4040 PRINT @(0,21),'CL',
4045 IF V3=2 THEN GOTO 4200
4050 LET S$(4,2)="01"
4055 IF NUM(V$,ERR=4060)>0 THEN LET W0=NUM(V$),V$="LINE"; GOTO 1000
4060 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4065 IF V$="L" OR V$="LIST" THEN GOTO 4400
4070 IF V$="DELETE" THEN LET O0=4; RUN "OPE.DB"
4075 IF V$="SP" AND PRINT$="YES" THEN GOTO 4300
4080 IF (V$="P" OR V$="PRINT") AND PRINT$="YES" THEN GOTO 4100
4085 IF (V$="I" OR V$="INVOICE") AND A0$(21,1)="S" THEN GOTO 4100
4090 ON POS(V$="YN") GOTO 4000,4095,1000
4100 REM " --- Run Overlay"
4105 IF U[0]>0 AND U[0]<NUM(P3$(29,5)) THEN GOTO 4160
4110 GOSUB HEADER_TOTALS
4115 IF (V$="P" OR V$="PRINT") AND PRINT$="YES" THEN GOTO 4500
4120 IF (V$="I" OR V$="INVOICE") AND A0$(21,1)="S" THEN GOTO 4700
4130 IF C9$(7,1)="Y" AND A0$(21,1)<>"P" THEN RUN "OPE.DE"
4135 LET A0$(69,1)="N"
4140 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
4150 RUN "OPE.DB"
4160 LET V4$="Order",I9$=STR(NUM(P3$(29,5)):"$#####"),A9=2
4170 LET V4$="Total "+V4$+" Amount Is Less Than "+I9$+" - Continue"
4175 CALL "SYC.YN",0,V4$,4,V$,V3
4180 ON POS(V$="YN") GOTO 4170,4110,4000
4200 REM " --- <F2> Back To Ribbon"
4210 GOSUB HEADER_TOTALS
4290 RUN "OPE.DC"
4300 REM " --- Printer Select"
4310 DIM OPTION$[2]
4320 LET OPTION$[1]="ORDER"
4330 CALL "SYC.QP",STAT,OPTION$[ALL]
4335 IF OPTION$[1]="EXIT" THEN GOTO 4360
4340 LET R2$(15,2)=OPTION$[1]
4350 IF LEN(OPTION$[1])>2 THEN LET FORMAT$=OPTION$[1](3) ELSE LET FORMAT$=""
4360 PRINT 'SB',@(40,1),R2$(15,2),
4380 PRINT 'SF',
4390 GOTO 4000
4400 REM " --- List"
4420 GOSUB 5100
4490 GOTO 4000
4500 REM " --- Print"
4510 LET PRTR_DEV=UNT
4520 CALL "SYC.GA",PRTR_DEV,0,"Print Order "+A0$(11,7),R2$(15,2)+FORMAT$,STATUS
4530 IF STATUS THEN GOTO 4000
4550 IF A0$(68,1)="Y" THEN LET A0$(100,1)="Y"
4560 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
4580 LET O3$="",O9$="OPE.DB",O9=2
4590 RUN "OPR.PA"
4600 REM " --- Review"
4620 READ (ARE13_DEV,KEY=A0$(1,17),DOM=4630)
4640 GOSUB 5400
4690 GOTO 1000
4700 REM " --- Convert Order To Invoice"
4705 IF A0$(21,1)="P" THEN GOTO 4770
4710 LET V4$="Do You Wish To Convert This Order To An Invoice"
4715 CALL "SYC.YN",0,V4$,0,V$,V3
4720 IF V$="NO" THEN GOTO 4000
4730 IF V$<>"YES" THEN GOTO 4700
4735 IF A0$(5,6)=P3$(16,6) THEN LET S$(2,2)="11",A0$(99,1)="Y"
4740 LET S$(7,1)="1"
4745 PRINT 'SB',@(32,2),"Invoice",@(63,3),"Invoice:",'SF',
4750 PRINT @(0,L0),'CE'
4755 LET S9$=A0$,O0=5
4760 RUN "OPE.CB"
4770 DIM MESSAGE$[1]
4775 LET MESSAGE$[0]="Quotes May Not Be Invoiced (<Enter>=Continue)"
4780 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
4790 GOTO 4000
4900 REM " --- Missing Default Line Code"
4910 DIM MESSAGE$[1]
4920 LET MESSAGE$[0]="A Valid "+$22$+P3$(40,1)+$22$+" Line Code Must Be Defined (<Enter>=Continue)"
4930 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
4940 GOTO 4100
4950 REM " --- Missing Default Warehouse"
4960 DIM MESSAGE$[1]
4970 LET MESSAGE$[0]="A Default Warehouse ID Must Be Defined (<Enter>=Continue)"
4980 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
4990 GOTO 4100
5000 REM " --- Display"
5005 PRINT @(0,L),W0$(18,3),@(4,L),W0$(21,1),@(6,L),W0$(31,2),
5010 DIM Y0$(30); FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=5020)IOL=ARM10E
5020 IF POS(Y0$(25,1)="MNO") THEN GOTO 5050
5025 LET D1$(1)="Item Not On File",D2$(1)="",D4$(1)=""
5030 FIND (IVM01_DEV,KEY=N0$+W0$(33,20),DOM=5035)IOL=IVM01A
5035 LET ITEM_DESC$(1)=FNITEM$(D1$,I[4],I[5],I[6])
5040 IF POS(Y0$(25,1)="NMO")=0 THEN PRINT @(Q[4],L),W[2]:M2$,@(Q[3]),W[1]:M3$,@(Q[2]),W[3]:M2$,@(Q[1]),W[4]:M2$,@(9,L+1),W0$(33,I[0]),@(9+I[0]+1,L+1),ITEM_DESC$,
5045 GOTO 5070
5050 PRINT @(9,L+1),W1$(1,40),
5055 IF Y0$(25,1)="N" THEN PRINT @(Q[5],L),W[0]:M3$,@(Q[4]),W[2]:M2$,@(Q[3]),W[1]:M3$,@(Q[2]),W[3]:M2$,@(Q[1]),W[4]:M2$,@(9,L+1),W1$(1,40),
5060 IF Y0$(25,1)="O" AND FNP$(W1$(1,40))="" THEN PRINT @(9,L+1),Y0$(5,20),
5070 IF Y0$(25,1)<>"M" THEN PRINT @(Q[0],L),W[6]:M1$,
5080 IF W0$(22,1)="Y" THEN PRINT @(Q[3],L),'SB',W[1]:M3$,'SF',
5085 IF Y0$(27,1)="Y" THEN PRINT @(0,L+1),"*Drpshp*",
5090 RETURN 
5100 REM " --- List"
5105 LET Q5=0
5110 GOSUB 5400
5120 READ (ARE13_DEV,KEY=A0$(1,17),DOM=5130)
5130 LET ARE13_K$=KEY(ARE13_DEV,END=5290)
5140 IF POS(A0$(1,17)=ARE13_K$)<>1 THEN GOTO 5290
5150 READ (ARE13_DEV)IOL=ARE13A
5160 LET Q5=1
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Press <Enter> To Continue",V0=1,V1=FNV(V4$),V2=22
5215 GOSUB 7000
5220 IF V3=4 THEN GOTO 5290
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+2,K9$=W0$(1,20)
5280 GOTO 5130
5290 RETURN 
5300 REM " --- Display B/O, Ship & Ext"
5310 PRECISION 2
5320 LET W[6]=W[1]*W[4]
5330 PRINT @(Q[2],L),W[3]:M2$,@(Q[1],L),W[4]:M2$,@(Q[0],L),W[6]:M1$,
5340 REM " --- Check If Taxable"
5342 PRECISION I[2]
5345 LET W[7]=0
5350 IF BALANCE+W[6]+U[0]-O[1]>C[6] AND V3<>2 AND S$(1,1)="0" THEN GOSUB 5500
5355 IF Y0$(26,1)="N" THEN GOTO 5380
5360 IF POS(Y0$(25,1)="OMN")=0 THEN IF D2$(8,1)="N" THEN GOTO 5380
5365 LET W[7]=W[6]
5380 IF W0$(22,1)="Y" THEN PRINT 'SB',
5385 IF Y0$(25,1)<>"O" THEN PRINT @(Q[3],L),W[1]:M3$,'SF',
5390 RETURN 
5400 REM " --- Clear Screen"
5410 FOR X=L1 TO L9+1
5420 PRINT @(0,X),'CL',
5430 NEXT X
5440 LET L=L1
5490 RETURN 
5500 REM " --- Credit Limit Exceeded"
5505 IF C[6]=0 THEN GOTO 5580
5510 LET Q$=STR(C[6]),Q=LEN(Q$)
5520 DIM MESSAGE$[1]
5530 LET MESSAGE$[0]="Credit Limit Exceeded (<Enter>=Continue)"
5540 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
5580 LET S$(1,1)="1"
5585 PRINT @(77-M1-23,8),'SF',"[*Credit Limit*]",
5590 RETURN 
5600 REM " --- Delete Lot/Serial Number"
5610 READ (ARE23_DEV,KEY=W0$(1,20),DOM=5620)
5620 LET K9$=KEY(ARE23_DEV,END=5690)
5625 IF K9$(1,20)<>W0$(1,20) OR O$="" THEN GOTO 5690
5630 READ (ARE23_DEV)IOL=ARE23A
5640 LET IV_INFO$[1]=O$(4,2),IV_INFO$[2]=O$(6,20),IV_INFO$[3]="",IV_REFS[0]=T[0]
5650 CALL "IVC.UA","OE",IV_FILES[ALL],IV_INFO[ALL],IV_PARAMS$[ALL],IV_INFO$[ALL],IV_REFS$[ALL],IV_REFS[ALL],IV_STATUS; REM "Undo previous warehouse action
5660 LET IV_INFO$[3]=T1$
5670 CALL "IVC.UA","UC",IV_FILES[ALL],IV_INFO[ALL],IV_PARAMS$[ALL],IV_INFO$[ALL],IV_REFS$[ALL],IV_REFS[ALL],IV_STATUS; REM "Do warehouse and serial/lot action
5680 REMOVE (ARE23_DEV,KEY=K9$)
5685 GOTO 5620
5690 RETURN 
5800 REM " --- Pricing Code"
5805 GET_PRICE_CODE: 
5810 PRINT @(20,L+1),'CL',"Enter Customer Pricing Code:",
5820 LET V0$="S",V1$="C",V2$=A0$(82,4),V3$="",V4$="<F3>=Lookup",V0=4,V1=49,V2=L+1,BAD_PRICE_CODE=0
5830 GOSUB 7000
5835 ON V3 GOTO 5860,5800,5840,5850,5840
5840 LET BAD_PRICE_CODE=1; GOTO 5890
5850 GOSUB LOOKUP_PRICING
5860 LET V$=V$+"    ",V$=V$(1,4)
5865 FIND (ARM10_DEV,KEY=N0$+"M"+V$,DOM=5800)
5870 LET A0$(82,4)=V$
5880 PRINT @(0,7),A0$(82,4),@(20,L+1),'CL',
5890 RETURN 
5900 REM " --- Print Bottom Line"
5905 PRINT_BTM_LINE: 
5910 PRINT @(0,20),'CE','SB'
5920 CALL "SYC.WC",1,0,80,0,0,20,0
5930 PRINT 'SF',
5990 RETURN 
6000 REM " --- Pricing Routine"
6005 LET S$(6,1)="0"
6010 IF W[1]<>0 THEN GOTO 6190
6020 LET ORDQTY=W[2]
6030 CALL "OPC.PC",IV_FILES[ALL],N0$,W0$(31,2),W0$(33,20),A0$(75,2),A0$(5,6),A0$(77,3),A0$(82,4),ORDQTY,TYPEFLAG$,PRICE,DISC,STATUS
6040 IF PRICE<>0 THEN GOTO 6100
6045 DIM MESSAGE$[1]
6050 LET MESSAGE$[0]="Item Price Must Be Entered (<Enter>=Continue)"
6060 CALL "SYC.XA",3,MESSAGE$[ALL],0,10,-1,V$,V3
6070 LET S$(6,1)="1",W[1]=0,W[8]=DISC
6090 GOTO 6130
6100 REM " --- Price Has Been Set"
6105 IF TYPEFLAG$="N" THEN LET S$(6,1)="1"
6110 LET W[1]=PRICE,W[8]=DISC
6120 IF TYPEFLAG$="X" THEN PRINT @(50,20),"**Contract Price ",W[1]:M3$,
6130 IF W[8]=100 THEN LET W[5]=L[9]; GOTO 6180
6140 PRECISION I[2]+3
6150 LET FACTOR=100/(100-W[8])
6160 PRECISION I[2]
6170 LET W[5]=W[1]*FACTOR
6180 PRINT @(Q[3],L),W[1]:M3$
6190 RETURN 
6200 REM " --- Add'l Options Window"
6210 LET CMTB4$=W1$(44,1)
6220 CALL "OPC.DD",I[2],L,M1$,M2$,M3$,A0$(21,1),Y0$(25,1),W0$,W1$,W[ALL],MSG$
6230 IF A0$(68,1)="Y" AND MSG$="COMMIT" THEN LET WARN$="YES"; REM "Warn about printing pick ticket
6235 IF A0$(21,1)="P" THEN GOTO 6270
6240 IF W1$(44,1)="N" THEN IF Y0$(25,1)<>"O" THEN LET W[3]=0,W[4]=0,W[6]=0,W[7]=0 ELSE IF W[6]<>0 THEN LET W[1]=W[6],W[6]=0,W[7]=0; PRINT @(Q[0],L),W[6]:M1$,
6245 IF CMTB4$<>"N" OR W1$(44,1)<>"Y" THEN GOTO 6270
6250 LET W[4]=W[2]
6260 IF Y0$(25,1)="O" AND W[6]=0 AND W[1]<>0 THEN LET W[6]=W[1],W[1]=0,W[5]=0; PRINT @(Q[0],L),W[6]:M1$,; REM "Reset Extension for committed Other line types
6270 IF POS(Y0$(25,1)="MO")=0 THEN GOSUB 5300
6280 IF Y0$(25,1)="O" THEN GOSUB 5340
6290 RETURN 
6400 REM " --- Clear up windows"
6410 IF OPWIN$="" THEN GOTO 6450
6420 CALL "SYC.WB",ERR=6430,OPWIN$,0
6430 CALL "SYC.WD",ERR=6450,OPWIN$
6440 LET OPWIN$=""
6450 IF OPWIN2$="" THEN GOTO 6490
6460 CALL "SYC.WB",ERR=6460,OPWIN2$,0
6470 CALL "SYC.WD",ERR=6480,OPWIN2$
6480 LET OPWIN2$=""
6490 RETURN 
6500 REM " --- Alternate Item"
6505 IF Y0$(25,1)="N" THEN GOTO 6590
6510 ON POS(D4$(1,1)="NAS") GOTO 6590,6590,6520,6550
6520 IF W[2]<L[0]-L[2] THEN GOTO 6590
6530 PRINT @(10,20)," Alternate Item is ",FNP$(D4$(2))," ",
6540 RETURN 
6550 REM " --- Superceded Item"
6560 PRINT @(10,20)," Superceded by Item ",FNP$(D4$(2))," ",'RB',
6590 RETURN 
6600 REM " --- Lookup Line Code"
6605 LOOKUP_LINE: 
6610 LET NMBR=1
6615 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6620 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=1
6625 LET DESCRIPT$[1]="Description",FIELD[1]=1,POSITION[1]=5,LENGTH[1]=20
6630 CALL "SYC.LK",ARM10_DEV,"E",NMBR,"Line Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6640 RETURN 
6650 REM " --- Lookup Warehouse"
6655 LOOKUP_WHS: 
6660 LET NMBR=1
6665 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6670 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6675 LET DESCRIPT$[1]="Warehouse",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6680 CALL "SYC.LK",IVM10_DEV,"C",NMBR,"Warehouse Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6690 RETURN 
6700 REM " --- Lookup Product Type"
6705 LOOKUP_PRODUCT: 
6710 LET NMBR=1
6715 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6720 LET DESCRIPT$[0]="Type",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3
6725 LET DESCRIPT$[1]="Description",FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
6730 CALL "SYC.LK",IVM10_DEV,"A",NMBR,"Product Type Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6740 RETURN 
6750 REM " --- Lookup Pricing Code"
6755 LOOKUP_PRICING: 
6760 LET NMBR=1
6765 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6770 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=4
6775 LET DESCRIPT$[1]="Description",FIELD[1]=1,POSITION[1]=8,LENGTH[1]=20
6780 CALL "SYC.LK",ARM10_DEV,"M",NMBR,"Pricing Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6790 RETURN 
6900 REM " --- Update Header Totals"
6905 HEADER_TOTALS: 
6910 LET A[6]=U[0],A[7]=U[2],A[4]=U[1]-A[2]
6920 IF U[1]=0 THEN LET A[4]=0
6930 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

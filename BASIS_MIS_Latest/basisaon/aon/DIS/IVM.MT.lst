0010 REM "IVM - Item Maintenance (LIFO/FIFO Tier Inquiry)"
0020 REM "Program IVM.MT"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 PRECISION P[1]
0200 REM " --- IOLIST's"
0220 IVM04A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0230 IVM10C: IOLIST X0$(1)
0500 REM " --- Initializations"
0510 DIM B0$(30),B1$(16),B[5],O[2],TYPES$[11],X0$(32)
0555 LET HEADING$(1)="   Date   Seq Type Ref #   Line"
0560 LET HEADING$(H0-M1-M3-9)="Quantity",HEADING$(H0-M1-5)="Cost"
0565 LET HEADING$(H0-10)="Extension",FOOTING$(1)=" <F1>=Next Warehouse"
0570 LET FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End",O[2]=H0-M1
0575 LET O[1]=O[2]-M3,O[0]=O[1]-M2,SOURCE$="BBBOTAOPRCTIWOIAIIIRPH"
0580 LET TYPES$[0]="????",TYPES$[1]="Conv",TYPES$[2]="BOM ",TYPES$[3]="Tier"
0585 LET TYPES$[4]="Sale",TYPES$[5]="P.O.",TYPES$[6]="Xfer",TYPES$[7]="W.O."
0590 LET TYPES$[8]="Adj ",TYPES$[9]="Iss ",TYPES$[10]="Rec ",TYPES$[11]="Phys"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT 'CS','SB',@(1,0),"Warehouse ID:",@(48),"From:",@(65),"To:",'SF',
0730 CALL "SYC.WC",1,0,WIDTH-2,1,0,1,0
0740 PRINT @(0,2),'SB','BR',HEADING$,'ER','SF',
0750 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 IF PAGES<3 THEN GOTO 4000
0990 READ (IVM03_DEV,KEY=A0$(1,2)+"G"+A0$(3),DOM=1000)
1000 REM " --- Warehouse ID"
1005 PRINT 'CF',
1010 LET V2$="",V2$=KEY(IVM03_DEV,END=1040)
1020 IF POS(A0$(1,2)+"G"+A0$(3)=V2$)<>1 THEN LET V2$=""
1030 IF V2$<>"" THEN LET V2$=V2$(24)
1040 LET V0$="S",V1$="KE",V3$="",V4$="",V0=2,V1=15,V2=0
1050 GOSUB 7000
1055 ON V3 GOTO 1080,1000,1000,1060,4000
1060 CALL "IVC.LK",3,V1,V2,V$
1070 IF V$="" THEN GOTO 0900
1080 LET WHSE$=V$+"  ",WHSE$=WHSE$(1,2)
1200 REM " --- Retrieve record"
1210 FIND (IVM10_DEV,KEY=N0$+"C"+WHSE$,DOM=1000)IOL=IVM10C
1280 LET K$=A0$(1,2)+"G"+A0$(3)+WHSE$
1290 READ (IVM03_DEV,KEY=K$,DOM=1300)
1300 REM " --- Display record"
1310 GOSUB 5000
1400 REM " --- Beginning Date"
1410 LET V0$="D",V1$="E",V2$=BEGDATE$,V3$="",V4$="",V0=3,V1=54,V2=0
1420 GOSUB 7000
1430 ON V3 GOTO 1450,1440,1000,1400,4000
1440 LET V$=""
1450 IF V$="" THEN PRINT @(V1,V2),"First   ",
1490 LET BEGDATE$=V$
1500 REM " --- Ending Date"
1510 LET V0$="D",V1$="E",V2$=ENDDATE$,V3$="",V4$="",V0=3,V1=69,V2=0
1520 GOSUB 7000
1530 ON V3 GOTO 1550,1540,1400,1500,4000
1540 LET V$=""
1550 IF V$="" THEN PRINT @(V1,V2),"Last    ",
1560 IF V$<>"" THEN IF V$<BEGDATE$ THEN GOTO 1500
1590 LET ENDDATE$=V$
1900 REM " --- Position file"
1910 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,1]
1930 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,SELECTION$="",QTY=0,AMT=0
1940 LET B0$(1)=A0$(1,2)+WHSE$+A0$(3)+BEGDATE$
1990 READ (IVM04_DEV,KEY=B0$,DOM=2000)
2000 REM " --- Read next IVM-04 record"
2010 LET K$=KEY(IVM04_DEV,END=3000)
2020 IF POS(N0$+WHSE$+A0$(3)=K$)<>1 THEN GOTO 3000
2030 IF ENDDATE$<>"" THEN IF B0$(28,3)>ENDDATE$ THEN GOTO 3000
2040 READ (IVM04_DEV,KEY=K$)IOL=IVM04A
2400 REM " --- Display page?"
2410 IF L+NEEDED<=MAXROW THEN GOTO 2500
2420 GOSUB 5400
2430 IF FKEY=1 THEN GOTO 1000
2440 IF FKEY=4 THEN GOTO 4000
2450 IF SELECTION$<>"" THEN GOTO 1000
2490 LET L=1,X1=1
2500 REM " --- Store detail line"
2510 DIM G1$(WIDTH-2)
2520 LET AMOUNT=B[0]*B[1],QTY=QTY+B[0],AMT=AMT+AMOUNT
2530 LET TYPE$=B1$(1,2),P=INT(1+POS(B1$(1,2)=SOURCE$,2)/2)
2535 IF P>0 THEN LET TYPE$=TYPES$[P]
2540 LET G1$(2)=FNB$(B0$(25,3))+" "+B0$(28,3)
2550 LET G1$(15)=TYPE$+" "+B1$(3,7)+" "+B1$(10,3)
2570 LET G1$(O[0])=STR(B[0]:M2$),G1$(O[1])=STR(B[1]:M3$)
2580 LET G1$(O[2])=STR(AMOUNT:M1$)
2590 LET LLIST$[PAGE,X1,1]=G1$,X1=X1+1,X0=X0+1,L=L+1
2900 REM " --- Loop back for next record"
2990 GOTO 2000
3000 REM " --- No more transactions"
3010 IF X0>0 THEN GOTO 3100
3020 DIM MESSAGE$[1]
3030 LET MESSAGE$[0]="No tiers found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
3060 IF V3=4 THEN GOTO 4000
3090 GOTO 1000
3100 REM " --- Add totals to display page"
3110 FOR X=0 TO 1
3120 DIM G1$(WIDTH-2)
3130 IF X=0 THEN GOTO 3200
3200 REM " --- Display page?"
3210 IF L<MAXROW THEN GOTO 3300
3220 GOSUB 5400
3230 IF FKEY=1 THEN EXITTO 1000
3240 IF FKEY=4 THEN EXITTO 4000
3250 IF SELECTION$<>"" THEN EXITTO 1000
3290 LET L=1,X1=1
3300 REM " --- Insert bar"
3310 IF X>0 THEN GOTO 3400
3320 DIM G1$(WIDTH-2,"-")
3390 GOTO 3600
3400 REM " --- Store total"
3410 LET G1$(O[0]-6)="Totals",G1$(O[0])=STR(QTY:M2$),G1$(O[2])=STR(AMT:M1$)
3600 REM " --- Store line"
3610 LET LLIST$[PAGE,X1,1]=G1$,X1=X1+1,X0=X0+1,L=L+1
3800 REM " --- Next totals element"
3890 NEXT X
3900 REM " --- Display last page"
3910 LET SELECTION$="|EOF"
3920 GOSUB 5400
3930 IF FKEY=4 THEN GOTO 4000
3990 GOTO 1000
4000 REM " --- Run menu overlay"
4010 CALL "SYC.WD",NAME$
4090 RUN "IVM.MA"
5000 REM " --- Display record"
5010 PRINT @(15,0),X0$(4,2)," ",X0$(6,24),
5090 RETURN 
5400 REM " --- Middle window processing"
5410 LET MODE=31
5420 CALL "SYC.SA",MODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAXROW,NAME$,HEIGHT,WIDTH,FKEY
5480 IF FKEY=-16 THEN LET PAGE=PAGE+1
5485 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAXROW,5,SHEIGHT-2
5490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9910 CALL "SYC.WD",NAME$
9950 RUN "SYS.AA"
9999 END

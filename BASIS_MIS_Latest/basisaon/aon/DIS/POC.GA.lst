0010 REM "POC - Generate PO Receipt General Ledger Postings"
0020 REM "Program POC.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0042 REM "CHANNELS[n]: Channel numbers (input)"
0044 REM "             [1]=ARM-10     [3]=WOM-10     [5]=GLT-04"
0046 REM "             [2]=WOE-01     [4]=GLM-01     [6]=GLT-05"
0048 REM "W$[n]      : POE-14 strings (input)"
0050 REM "W[n]       : POE-14 numerics (input)"
0052 REM "PARAMS$[n] : String parameters (input)"
0054 REM "             [0]=GL installed?          [6]=Line code 2nd string"
0056 REM "             [1]=Posting date           [7]=Item GL accounts"
0058 REM "             [2]=Vendor number          [8]=Distribute by item?"
0060 REM "             [3]=Vendor mask            [9]=Distribution code"
0062 REM "             [4]=GL params/4th string  [10]=Firm ID"
0064 REM "             [5]=Item description"     [11]=SF installed?"
0066 REM "PARAMS[n]  : Numeric parameters (input)"
0068 REM "             [0]=Extension              [2]=Item number length"
0069 REM "             [1]=GL number length"
0070 REM "STATUS     : 0=No error (returned)"
0072 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER CHANNELS[ALL],W$[ALL],W[ALL],PARAMS$[ALL],PARAMS[ALL],STATUS
0200 REM " --- IOLIST's'
0210 ARM10D: IOLIST Z0$(1),Z1$(1)
0220 WOE01A: IOLIST S0$(1),S1$(1),S[ALL]
0230 WOM10A: IOLIST U0$(1),U1$(1)
0400 REM " --- Parameters"
0405 DIM G[4],I[1]
0410 LET ARM10_DEV=CHANNELS[1],WOE01_DEV=CHANNELS[2],WOM10_DEV=CHANNELS[3]
0420 LET GLM01_DEV=CHANNELS[4],GLT04_DEV=CHANNELS[5],GLT05_DEV=CHANNELS[6]
0430 LET W0$=W$[0],W1$=W$[1],W2$=W$[2],W3$=W$[3],W4$=W$[4]
0440 LET GL$=PARAMS$[0],WHEN$=PARAMS$[1],VENDOR$=PARAMS$[2],MASK$=PARAMS$[3]
0450 LET G4$=PARAMS$[4],DESCRIPTION$=PARAMS$[5],Y1$=PARAMS$[6]
0460 LET IVACCTS$=PARAMS$[7],DISTCODE$=PARAMS$[8],CODE$=PARAMS$[9]
0470 LET N0$=PARAMS$[10],SF$=PARAMS$[11]
0480 LET EXTENSION=PARAMS[0],G[4]=PARAMS[1],I[0]=PARAMS[2]
0500 REM " --- Initializations"
0510 DIM S0$(75),S1$(141),S[11],U0$(40),U1$(9*G[4]),Z0$(32),Z1$(11*G[4])
0550 LET MEMO$=W3$(3,I[0]),REF1$=FNF$(VENDOR$,M0$)
0900 REM " --- Branch based on line code"
0990 ON POS(Y1$(21,1)="SNVMO") GOTO 4000,1000,2000,4000,4000,4000
1000 REM " --- Account numbers for Standard Lines"
1010 LET IVACCT$=IVACCTS$(1,G[4]),POACCT$=IVACCTS$(21,G[4])
1020 LET MEMO$=MEMO$+" "+DESCRIPTION$
1030 LET REF2$="PO "+W0$(16,7),REF3$="Line "+W0$(23,3)
1200 REM " --- Retrieve distribution code record"
1210 IF DISTCODE$<>"Y" THEN GOTO 3000
1220 LET Z0$(1)=N0$+"D"+CODE$
1230 FIND (ARM10_DEV,KEY=Z0$(1,5),DOM=1240)IOL=ARM10D
1240 LET IVACCT$=Z1$(6*G[4]+1,G[4]),POACCT$=Z1$(10*G[4]+1,G[4])
1290 GOTO 3000
2000 REM " --- Account numbers for Non-Stock"
2010 IF SF$<>"Y" THEN GOTO 4000
2020 IF FNP$(W2$(1,7))="" THEN GOTO 4000
2030 LET MEMO$=MEMO$+" "+W4$,REF2$=W0$(16,7)+W0$(23,3),REF3$="WO "+W2$(1,7)
2200 REM " --- Get WIP/Purchases account for work order receipts"
2210 LET S0$(1)=N0$+"  "+W2$(1,7)
2220 FIND (WOE01_DEV,KEY=S0$(1,11),DOM=2230)IOL=WOE01A
2230 LET U0$(1)=N0$+"A"+S0$(12,2)
2240 FIND (WOM10_DEV,KEY=U0$(1,5),DOM=2250)IOL=WOM10A
2250 LET IVACCT$=U1$(1,G[4]),POACCT$=U1$(4*G[4]+1,G[4])
3000 REM " --- Generate GL Recap Postings"
3010 LET ACCOUNT$=IVACCT$,AMOUNT=EXTENSION,UNITS=0
3020 IF G4$(3,1)="Y" THEN LET UNITS=W[7]
3030 GOSUB GLPOST
3040 LET ACCOUNT$=POACCT$,AMOUNT=-AMOUNT,UNITS=-UNITS
3050 GOSUB GLPOST
4000 REM " --- All done"
4010 LET STATUS=0
4090 GOTO 9900
6900 REM " --- Standard G/L Posting Routine"
6910 GLPOST: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,WHEN$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Function Definitions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 LET STATUS=999
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9950 EXIT 
9999 END

0010 REM "POR - PO Receipt Cost Variance Report (Print Overlay)
0020 REM "Program POR.OB
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 APM01A: IOLIST C0$(1),C1$(1)
0260 IVM01A: IOLIST *,X1$(1)
0290 POT04A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0300 POT14A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1),B4$(1),B5$,B6$,B[ALL]
0310 POM02A: IOLIST S0$(1),S1$(1)
0320 POW18A: IOLIST WFKEY$(1),WFPOAMT
0500 REM " --- Init Data
0510 PRECISION IVPRECISION
0520 DIM A0$(22),A1$(160),A[8],VENDTOT[3],PREVK$(31),VENDOR$(6),PREVVEND$(6)
0530 LET PRINTVTOT$="N",PRINTPTOT$="N"
0600 REM " --- Headings and print positions
0610 LET HEADINGS=4
0620 DIM HEADINGS$[HEADINGS],O[14]
0630 LET WIDTH=132,PAGE=0,L9=59,L=L9+1
0650 GOSUB ASSIGNHEADS
0760 POSITIONS: 
0770 LET O[1]=O[0]+3,O[2]=O[1]+3,O[3]=O[2]+4,O[4]=O[3]+3
0780 LET O[5]=O[4]+3,O[6]=O[5]+21,O[7]=O[6]+9,O[8]=O[7]+9
0790 LET O[9]=O[8]+3,O[10]=O[9]+M2,O[11]=O[10]+M2,O[12]=O[11]+M1
0795 LET O[13]=O[12]+M1,O[14]=O[13]+M1
0810 IF O[14]+8<WIDTH THEN GOTO 0950
0820 LET O[14]=WIDTH-MPCNT,O[13]=O[14]-M1,O[12]=O[13]-M1,O[11]=O[12]-M1
0830 LET O[10]=O[11]-M2,O[9]=O[10]-M2
0950 REM " --- Position files
0980 CALL "SYC.NB","Printing",22,COLUMN
1500 REM " --- Init Read
1540 READ (POT44_DEV,KEY=N0$+VENDFROM$+DATEFROM$,DOM=READSORT)
1600 REM " --- Read through Sort file
1605 READSORT: 
1620 LET K$=KEY(POT44_DEV,END=4000)
1640 IF K$(1,2)<>N0$ THEN GOTO 4000
1650 READ (POT44_DEV); REM "Move ptr
1700 REM " --- Valid Vendor/Receipt Date?
1710 IF VENDTHRU$<>"" AND K$(3,6)>VENDTHRU$ THEN GOTO 4000
1720 IF DATEFROM$<>"" AND K$(9,6)<DATEFROM$ THEN GOTO 1760
1730 IF DATETHRU$<>"" AND K$(9,6)>DATETHRU$ THEN GOTO 1790
1740 LET VENDOR$=K$(3,6),PONUM$=K$(15,7),RECNUM$=K$(22,7),LINENUM$=K$(29,3)
1750 GOTO GETHEADER
1760 REM " --- Skip to first receipt date for vendor
1770 READ (POT44_DEV,KEY=K$(1,8)+DATEFROM$,DOM=1780)
1780 GOTO NEXTSORT
1790 REM " --- Skip to next vendor
1800 READ (POT44_DEV,KEY=K$(1,8)+$FF$,DOM=1810)
1810 GOTO NEXTSORT
1900 REM " --- Get Header & assign variables
1905 GETHEADER: 
1920 IF K$(1,28)=PREVK$(1,28) THEN GOTO GETDETAIL
1930 LET PREVK$(1)=K$
1940 FIND (POT04_DEV,KEY=N0$+VENDOR$+PONUM$+RECNUM$,DOM=NEXTSORT)IOL=POT04A
1950 DIM RCPTDATE$(8),ORDDATE$(8)
1960 IF A1$(29,6)<>FILL(6) THEN LET RCPTDATE$=FNB6$(A1$(29,6))
1970 IF A1$(5,6)<>FILL(6) THEN LET ORDDATE$=FNB6$(A1$(5,6))
2000 REM " --- Get detail 
2005 GETDETAIL: 
2010 DIM B0$(25),B1$(48),B2$(32),B3$(22),B4$(40),B[12]
2020 FIND (POT14_DEV,KEY=A0$+LINENUM$,DOM=NEXTSORT)IOL=POT14A
2030 LET WHSE$=B3$(1,2),ITEM$=B3$(3,20)
2150 REM " --- Valid Whse/Item?
2160 IF WHSEFOR$<>"" THEN IF WHSE$<>WHSEFOR$ THEN GOTO NEXTSORT
2170 IF ITEMFROM$<>"" THEN IF ITEM$<ITEMFROM$ THEN GOTO NEXTSORT
2180 IF ITEMTHRU$<>"" THEN IF ITEM$>ITEMTHRU$ THEN GOTO NEXTSORT
2200 REM " --- Valid line type (ie S or N)?
2210 GOSUB GETLINETYPE
2220 IF POS(LINETYPE$="SN")=0 THEN GOTO NEXTSORT
2300 REM " --- Valid, so assign vars
2310 PRINT @(COLUMN,11),FNF$(A0$(3,VENDLEN),VENDOMASK$)," ",A0$(9,7)," ",A0$(16,7)
2330 LET LINECODE$=B1$(1,2),UNITMEAS$=B1$(22,2)
2350 DIM DATEPROM$(8),REQDATE$(8)
2360 IF B1$(9,6)<>FILL(6) THEN LET DATEPROM$=FNB6$(B1$(9,6))
2370 IF B1$(3,6)<>FILL(6) THEN LET REQDATE$=FNB6$(B1$(3,6))
2400 PRECISION 2
2410 LET QTYORD=B[3],QTYREC=B[7],UNITCOST=B[1]
2420 LET ORDVAL=UNITCOST*QTYORD,RCPTVAL=UNITCOST*QTYREC
2430 LET VARIANCE=RCPTVAL-ORDVAL
2440 IF ORDVAL<>0 THEN LET VARIPCNT=RCPTVAL*100/ORDVAL ELSE LET VARIPCNT=0
2450 PRECISION IVPRECISION
2460 IF VARIPCNT<MINVARI THEN GOTO NEXTSORT
2500 REM " --- Go test breaks
2510 GOSUB TESTBREAKS
2600 REM " --- Get Item Desc
2610 DIM ITEMDESC$(60),X1$(60)
2620 IF LINETYPE$="N" THEN GOTO 2700
2630 LET ITEMDESC$(1)="(Not On File)"
2640 FIND (IVM01_DEV,KEY=N0$+B3$(3,20),DOM=2690)IOL=IVM01A
2650 LET ITEMDESC$(1)=FNITEM$(X1$,DESCLEN[1],DESCLEN[2],DESCLEN[3])
2690 GOTO 2750
2700 REM " --- Nonstock description"
2710 LET ITEMDESC$(1)=B4$
2750 REM " --- Go print the data
2760 GOSUB PRINTLINE
2900 REM " --- Accumulate totals; then get next detail
2910 LET REC_OVAL=REC_OVAL+ORDVAL,REC_RVAL=REC_RVAL+RCPTVAL
2920 LET PO_RVAL=PO_RVAL+RCPTVAL,VENDRVAL=VENDRVAL+RCPTVAL
2940 LET GRANDRVAL=GRANDRVAL+RCPTVAL
3000 REM " --- Loop up for next header record via sort files
3010 NEXTSORT: 
3060 GOTO READSORT
4000 REM " --- All Done
4010 IF L+1>L9 THEN GOSUB 5000
4020 GOSUB POTOTALS
4030 GOSUB VENDTOTALS
4040 LET GRANDVARI=GRANDRVAL-GRANDOVAL
4050 IF GRANDOVAL>0 THEN LET GRANDVPCNT=GRANDRVAL*100/GRANDOVAL
4060 PRINT (7)@(O[11]-17),"Total For Report",@(O[11]),GRANDOVAL:M1$,@(O[12]),GRANDRVAL:M1$,@(O[13]),GRANDVARI:M1$,@(O[14]),GRANDVPCNT:MPCNT$
4090 GOTO 9900
5000 REM " --- Heading
5010 LET L=HEADINGS+1
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS THEN EXITTO 9900
5140 PRINT (7)@(O[6]),"Date",@(O[7]),"Date",@(O[9]+M2-10),"Quantity",@(O[10]+M2-10),"Quantity",@(O[11]+M1-7),"Order",@(O[12]+M1-9),"Receipt",@(O[14]+MPCNT-7),"Vari"
5150 PRINT (7)@(O[2]),"Seq",@(O[3]),"Cd",@(O[4]),"Wh",@(O[5]),"Item",@(O[6]),"Required",@(O[7]),"Promised",@(O[8]),"UM",@(O[9]+M2-10),"Ordered",@(O[10]+M2-10),"Received",@(O[11]+M1-7),"Value",@(O[12]+M1-9),"Value",@(O[13]+M1-9),"Variance",@(O[14]+MPCNT-7),"Ratio"
5160 PRINT (7)""
5180 LET L=L+3
5190 RETURN 
5300 REM " --- Assign headings variables
5305 ASSIGNHEADS: 
5310 DIM TMPH3$(WIDTH),TMPH4$(WIDTH)
5320 IF VENDFROM$="" THEN LET VENDF$="First" ELSE LET VENDF$=FNF$(VENDFROM$(1,VENDLEN),VENDOMASK$)
5330 IF VENDTHRU$="" THEN LET VENDT$="Last" ELSE LET VENDT$=FNF$(VENDTHRU$(1,VENDLEN),VENDOMASK$)
5340 IF DATEFROM$="" THEN LET DATEF$="First" ELSE LET DATEF$=FNB6$(DATEFROM$)
5350 IF DATETHRU$="" THEN LET DATET$="Last" ELSE LET DATET$=FNB6$(DATETHRU$)
5360 IF ITEMFROM$="" THEN LET ITEMF$="First" ELSE LET ITEMF$=ITEMFROM$(1,ITEMLEN)
5370 IF ITEMTHRU$="" THEN LET ITEMT$="Last" ELSE LET ITEMT$=ITEMTHRU$(1,ITEMLEN)
5380 LET MIDH3$="From Item "+ITEMF$+" Through "+ITEMT$
5390 LET TMPH3$(1)="From Vendor: "+VENDF$,TMPH3$(FNC(MIDH3$,WIDTH))=MIDH3$
5400 LET TMPH3$(WIDTH-LEN(DATEF$)-18)="From Receipt Date: "+DATEF$
5410 IF WHSEFOR$="" THEN LET MIDH4$="All Warehouses" ELSE LET MIDH4$="For Warehouse "+WHSEFOR$
5420 LET TMPH4$(1)="  To Vendor: "+VENDT$,TMPH4$(FNC(MIDH4$,WIDTH))=MIDH4$
5430 LET TMPH4$(WIDTH-LEN(DATEF$)-18)="  To Receipt Date: "+DATET$
5440 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$+" For Variances Greater Than "+STR(MINVARI:MPCNT$)
5450 LET HEADINGS$[2]=TMPH3$,HEADINGS$[3]=TMPH4$
5490 RETURN 
6000 REM " --- Test breaks
6010 TESTBREAKS: 
6020 IF VENDOR$=PREVVEND$ THEN GOTO TESTPO
6030 IF PRINTPTOT$="Y" THEN GOSUB POTOTALS
6040 IF PRINTVTOT$="Y" THEN GOSUB VENDTOTALS
6050 GOSUB NEWVENDOR
6060 GOSUB NEWPO
6070 GOTO 6190
6075 TESTPO: 
6080 IF PONUM$=PREVPO$ THEN GOTO TESTREC
6090 IF PRINTRTOT$="Y" THEN GOSUB RECTOTALS
6100 IF PRINTPTOT$="Y" THEN GOSUB POTOTALS
6110 GOSUB NEWPO
6120 GOTO 6190
6130 TESTREC: 
6140 IF RECNUM$=PREVREC$ THEN GOTO 6190
6150 IF PRINTRTOT$="Y" THEN GOSUB RECTOTALS
6160 GOSUB NEWREC
6190 RETURN 
6200 REM " --- New Vendor
6205 NEWVENDOR: 
6210 DIM C0$(8),C1$(195),VENDTOT[3]
6220 LET VENDNAME$="Not On File",PREVVEND$=VENDOR$
6230 LET PRINTVTOT$="Y",VENDOVAL=0,VENDRVAL=0,VENDVARI=0,VENDVPCNT=0
6240 FIND (APM01_DEV,KEY=N0$+VENDOR$,DOM=6260)IOL=APM01A
6250 LET VENDNAME$=C1$(1,30)
6260 IF L+4>L9 THEN GOSUB 5000
6270 PRINT (7)@(0),FNF$(VENDOR$(1,VENDLEN),VENDOMASK$)," ",VENDNAME$
6280 LET L=L+1
6290 RETURN 
6300 REM " --- New Receiver
6305 NEWREC: 
6310 LET REC_OVAL=0,REC_RVAL=0,REC_VARI=0,REC_VPCNT=0
6320 LET PREVREC$=RECNUM$,PRINTRTOT$="Y"
6330 IF FROMNEWPO THEN GOTO 6390
6340 IF L+3>L9 THEN GOSUB 5000
6350 PRINT (7)@(O[1]),"PO #: ",PONUM$,@(18),"Receiver #:",RECNUM$,@(40),"Ordered: ",ORDDATE$,@(59),"Received: ",RCPTDATE$
6370 LET L=L+1
6390 RETURN 
6400 REM " --- New PO
6405 NEWPO: 
6410 LET PO_OVAL=0,PO_RVAL=0,PO_VARI=0,PO_VPCNT=0
6420 LET PREVPO$=PONUM$,PRINTPTOT$="Y"
6430 IF L+3>L9 THEN GOSUB 5000
6440 PRINT (7)@(O[1]),"PO #: ",PONUM$,@(18),"Receiver #:",RECNUM$,@(40),"Ordered: ",ORDDATE$,@(59),"Received: ",RCPTDATE$
6470 LET L=L+1,FROMNEWPO=1
6480 GOSUB NEWREC
6485 LET FROMNEWPO=0
6490 RETURN 
6500 REM " --- Print Vendor total
6505 VENDTOTALS: 
6510 IF VENDOVAL>0 THEN LET VENDVPCNT=VENDRVAL*100/VENDOVAL
6520 LET VENDVARI=VENDRVAL-VENDOVAL
6530 IF L+1>L9 THEN GOSUB 5000
6540 PRINT (7)@(O[11]-18-LEN(VENDOMASK$)),"Total For Vendor ",FNF$(PREVVEND$(1,VENDLEN),VENDOMASK$),@(O[11]),VENDOVAL:M1$,@(O[12]),VENDRVAL:M1$,@(O[13]),VENDVARI:M1$,@(O[14]),VENDVPCNT:MPCNT$
6550 LET L=L+1
6590 RETURN 
6600 REM " --- Print PO total
6605 POTOTALS: 
6610 DIM WFKEY$(18)
6620 LET START18$=N0$+PREVVEND$+PREVPO$
6630 READ (POW18_DEV,KEY=START18$,DOM=READACCUMPO)
6635 READACCUMPO: 
6640 LET K18$=KEY(POW18_DEV,END=DONEACCUMPO)
6650 IF POS(START18$=K18$)<>1 THEN GOTO DONEACCUMPO
6660 READ (POW18_DEV)IOL=POW18A
6670 PRECISION 2
6680 LET POAMT=POAMT+WFPOAMT
6690 PRECISION IVPRECISION
6700 GOTO READACCUMPO
6710 DONEACCUMPO: 
6720 LET PO_OVAL=PO_OVAL+POAMT,VENDOVAL=VENDOVAL+POAMT
6730 LET GRANDOVAL=GRANDOVAL+POAMT,POAMT=0
6740 IF PO_OVAL>0 THEN LET PO_VPCNT=PO_RVAL*100/PO_OVAL
6750 LET PO_VARI=PO_RVAL-PO_OVAL
6760 IF L+1>L9 THEN GOSUB 5000
6770 PRINT (7)@(O[11]-23),"Total For PO # ",PREVPO$,@(O[11]),PO_OVAL:M1$,@(O[12]),PO_RVAL:M1$,@(O[13]),PO_VARI:M1$,@(O[14]),PO_VPCNT:MPCNT$
6780 LET L=L+1
6790 RETURN 
6800 REM " --- Print Rec total
6805 RECTOTALS: 
6810 IF REC_OVAL>0 THEN LET REC_VPCNT=REC_RVAL*100/REC_OVAL
6830 LET REC_VARI=REC_RVAL-REC_OVAL
6840 IF L+1>L9 THEN GOSUB 5000
6850 PRINT (7)@(O[11]-25),"Total For Rcpt # ",PREVREC$,@(O[11]),REC_OVAL:M1$,@(O[12]),REC_RVAL:M1$,@(O[13]),REC_VARI:M1$,@(O[14]),REC_VPCNT:MPCNT$
6860 LET L=L+1
6890 RETURN 
6900 REM " --- Get Line Type
6905 GETLINETYPE: 
6910 DIM S0$(4),S1$(32)
6920 LET LINETYPE$="N"
6930 FIND (POM02_DEV,KEY=B0$(1,2)+B1$(1,2),DOM=6945)IOL=POM02A
6940 LET LINETYPE$=S1$(21,1)
6945 RETURN 
6950 REM " --- Print data line
6955 PRINTLINE: 
6960 IF L+2>L9 THEN GOSUB 5000
6970 PRINT (7)@(O[2]),LINENUM$,@(O[3]),LINECODE$,@(O[4]),WHSE$,@(O[5]),ITEM$(1,ITEMLEN),@(O[6]),REQDATE$,@(O[7]),DATEPROM$,@(O[8]),UNITMEAS$,@(O[9]),QTYORD:M2$,@(O[10]),QTYREC:M2$,@(O[11]),ORDVAL:M1$,@(O[12]),RCPTVAL:M1$,@(O[13]),VARIANCE:M1$,@(O[14]),VARIPCNT:MPCNT$
6980 PRINT (7)@(O[5]),ITEMDESC$
6985 LET L=L+2
6990 RETURN 
8000 REM " --- Functions
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "IVR - Lot/Serial Status Report (Input Overlay)"
0020 REM "Program IVR.YA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=9
0110 DIM FILES$[FILES],FILEOPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0130 LET FILES$[4]="IVM-03",FILES$[5]="IVM-10",FILES$[6]="IVM-14"
0140 LET FILES$[7]="IVM-15",FILES$[8]="IVM-07",FILES$[9]="IVM-08"
0150 CALL "SYC.DA",1,1,7,FILES$[ALL],FILEOPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0180 LET IVM03_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],IVM14_DEV=CHANNELS[6]
0190 LET IVM15_DEV=CHANNELS[7]
0200 REM " --- IOLIST's"
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0400 REM " --- Parameters"
0405 DIM P[5]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="IV"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 IF POS(I3$(17,1)="LS")=0 THEN GOTO 4900
0450 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1)),P[2]=NUM(I2$(7,2))
0460 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM FILES[15],PARAMS$[3],OPTIONS$[7],B[12]
0550 LET FILES[1]=IVM01_DEV,FILES[10]=IVM10_DEV,FILES[14]=IVM14_DEV
0555 LET PARAMS$[0]=N0$,PARAMS$[1]=I1$,PARAMS$[2]=I2$,PARAMS$[3]=I3$
0560 LET SEQUENCE$="I",STATUS$="O",BEGLOT$="",ENDLOT$=""
0565 LET FILES[15]=IVM15_DEV
0600 REM " --- Additional File Opens"
0610 CALL "SYC.DA",1,8,9,FILES$[ALL],FILEOPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS>0 THEN GOTO 9900
0630 LET IVM07_DEV=CHANNELS[8],IVM08_DEV=CHANNELS[9]
0700 REM " --- Background"
0710 PRINT 'SB',@(30,3),"Sequence:",@(18,4),"Open, Closed Or Both:",'SF',
0730 CALL "IVC.IB","B","D",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0740 PRINT 'SB',@(11,12),"Beginning Lot/Serial Number:",@(14,13),"Ending Lot/Serial Number:",'SF',
0900 REM " --- Display default values"
0950 GOSUB 5000
1000 REM " --- Sequence"
1010 LET V4$="List Items By Item Number Or Lot/Serial # (I=Item Number/L=Lot/Serial Number)"
1020 LET V0$="S",V1$="CE",V2$=SEQUENCE$,V3$="IL",V0=1,V1=40,V2=3,I0=0
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1000,3900
1050 LET SEQUENCE$=V$
1090 GOTO 3900
1200 REM " --- Open/Closed/Both"
1210 LET V4$="List Open Lot/Serial Numbers, Closed Or Both (O=Open/C=Closed/B=Both)?"
1220 LET V0$="S",V1$="C",V2$=STATUS$,V3$="",V0=1,V1=40,V2=4
1230 GOSUB 7000
1240 ON V3 GOTO 1250,1200,3900,1200,3900
1250 LET STATUS$=V$
1290 GOTO 3900
1400 REM " --- Process standard input"
1410 CALL "IVC.IB","I","D",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
1420 ON V3 GOTO 1430,1400,3900,1400,3900
1490 GOTO 3900
1600 REM " --- Beginning Lot/Serial Number"
1610 LET V4$="Enter The Beginning Lot/Serial Number To Include On This Report (<F1>=First)"
1620 LET V0$="S",V1$="KCE",V2$=BEGLOT$,V3$="",V0=P[2],V1=40,V2=12
1630 GOSUB 7000
1640 ON V3 GOTO 1660,1650,3900,1600,3900
1650 LET V$=""
1660 LET BEGLOT$=V$,X$="First"
1675 IF BEGLOT$<>"" THEN LET X$=BEGLOT$
1680 PRINT @(V1,V2),X$,'CL',
1690 GOTO 3900
1800 REM " --- Ending Lot/Serial Number"
1810 LET V4$="Enter The Ending Lot/Serial Number To Include On This Report (<F1>=Last)"
1820 LET V0$="S",V1$="KCE",V2$=ENDLOT$,V3$="",V0=P[2],V1=40,V2=13
1830 GOSUB 7000
1840 ON V3 GOTO 1860,1850,3900,1800,3900
1850 LET V$=""
1860 LET ENDLOT$=V$,X$="Last"
1870 IF BEGLOT$<>"" THEN IF ENDLOT$<BEGLOT$ THEN GOTO 1800
1875 IF ENDLOT$<>"" THEN LET X$=ENDLOT$
1880 PRINT @(V1,V2),X$,'CL',
1890 GOTO 3900
3900 REM " --- Next Field"
3902 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4030 ON POS(V$="YN") GOTO 4000,4050,1000
4100 REM " --- Run report overlay"
4110 LET LEVEL$=OPTIONS$[0],BEGWHSE$=OPTIONS$[1],ENDWHSE$=OPTIONS$[2]
4120 LET BEGPROD$=OPTIONS$[3],ENDPROD$=OPTIONS$[4],BEGITEM$=OPTIONS$[5]
4130 LET ENDITEM$=OPTIONS$[6],TYPE$=OPTIONS$[7]
4190 RUN "IVR.YB"
4900 REM " --- Lotted/Serialized Inventory not enabled"
4910 CALL "IVC.SB","L",STATUS
4990 GOTO 9900
5000 REM " --- Display"
5010 PRINT @(40,3),SEQUENCE$,@(40,4),STATUS$,
5020 CALL "IVC.IB","D","D",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
5030 LET X1$="First",X2$="Last"
5040 IF BEGLOT$<>"" THEN LET X1$=BEGLOT$
5050 IF ENDLOT$<>"" THEN LET X2$=ENDLOT$
5060 PRINT @(40,12),X1$,'CL',@(40,13),X2$,'CL',
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "OPR - Sales Commission Report"
0020 REM "Program OPR.MB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0095 PRECISION 4
0100 REM " --- Open/Lock Files"
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS>0 THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 ARM01A: IOLIST B0$,B1$(1)
0220 ARM10F: IOLIST H5$,C0
0230 ART01A: IOLIST W0$,W[ALL]
0240 ART05A: IOLIST A0$,A[ALL]
0250 ART07A: IOLIST D0$,D1$,D[ALL]
0260 ART11A: IOLIST C0$,C1$,C[ALL]
0500 REM " --- Initializations"
0510 DIM A[10],C[1],D[1],T[9],U[9],W[1],X[1],O[11],HEADINGS$[6]
0540 LET X0$="First Date",X1$="Last Date",H5$=""
0545 IF P8$<>"" THEN LET X0$=FNB$(P8$)
0550 IF P9$<>"" THEN LET X1$=FNB$(P9$)
0555 LET H4$="Paid Invoices",WHEN$=F0$(7,3),CLOCK$=""
0560 IF P7$="B" THEN LET H4$="Booked Invoices"
0565 IF S0$<>"" THEN LET H6$=S0$ ELSE LET H6$="First"
0570 IF S1$<>"" THEN LET H7$=S1$ ELSE LET H7$="Last"
0575 IF S2$="S" THEN LET H8$="Commission Based On Gross Sales"
0580 IF S2$="P" THEN LET H8$="Commission Based On Gross Profit"
0585 LET T0$="",T0=0,T1=0,L9=55,L=L9+1,H=0,H0=132
0590 LET X0$="Beginning Date: "+X0$,X1$="   Ending Date: "+X1$
0595 CALL "SYC.RM",M1$,M1,11,13
0600 REM " --- Headings"
0610 DIM H1$(H0),H2$(H0),J$(5*M1+2*M4,"-")
0615 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0620 LET H1$(1)="Beginning Salesperson: "+H6$,H1$(FNC(H4$,H0),LEN(H4$))=H4$
0625 LET H1$(H0-LEN(X0$))=X0$,H2$(1)="   Ending Salesperson: "+H7$
0630 LET H2$(FNC(H8$,H0),LEN(H8$))=H8$,H2$(H0-LEN(X1$))=X1$
0645 LET HEADINGS$[2]=H1$,HEADINGS$[3]=H2$
0650 REM " --- Print positions"
0660 LET O[11]=H0-M1,H1$="Commission",H2$="   Due    ",H1=O[11]+M1-11
0670 IF P7$<>"P" THEN LET O[11]=H0-1,H1=O[11],H1$="",H2$=""
0680 LET O[1]=8,O[2]=O[1]+M0+1,O[10]=O[11]-M1,O[9]=O[10]-M4
0685 LET O[8]=O[9]-M4,O[7]=O[8]-M1,O[6]=O[7]-M1,O[5]=O[6]-M1
0690 LET O[4]=O[5]-M1,O[3]=O[4]-9,H2=H1,WIDTH=MIN(30,O[3]-O[2]-1)
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",7,COLUMN
0900 REM 
1000 REM " --- Position File Pointer "
1020 READ (ART05_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Read Comission Record "                  
1110 LET K$=KEY(ART05_DEV,END=4000)
1120 READ (ART05_DEV,END=2000)IOL=ART05A
1130 IF POS(N0$=A0$)<>1 THEN GOTO 4000
1150 IF P7$="B" THEN IF P8$<>"" THEN IF A0$(22,3)<P8$ THEN GOTO 1100
1160 IF P7$="B" THEN IF P9$<>"" THEN IF A0$(22,3)>P9$ THEN GOTO 1100
1170 IF S0$<>"" THEN IF A0$(5,3)<S0$ THEN GOTO 1100
1180 IF S1$<>"" THEN IF A0$(5,3)>S1$ THEN GOTO 1100
1190 IF A0$(21,1)="V" THEN GOTO 1100
2200 REM " --- Control Breaks "
2220 IF A0$(5,3)<>T0$ THEN GOSUB 6000
2260 IF P7$="B" THEN GOTO 2400
2300 REM " --- Calculate commission due"
2310 GOSUB 6200
2320 IF PAID=0 THEN GOTO 1100
2330 LET A[9]=0
2340 IF S2$="S" THEN IF SOLD<>0 THEN LET A[9]=(-PAID)/SOLD*A[2]*A[0]/100
2350 IF S2$="P" THEN IF SOLD<>0 THEN LET A[9]=(-PAID)/SOLD*(A[2]-A[3])*A[0]/100
2400 REM " -- Get Customer Name "
2410 DIM B1$(WIDTH)
2420 LET B1$(1)="(Not On File)"
2430 FIND (ARM01_DEV,KEY=N0$+A0$(15,6),DOM=2440)IOL=ARM01A
3000 REM " --- Print "
3010 PRINT @(COLUMN,11),T0$,
3020 IF L>L9 THEN GOSUB 5000
3030 LET X0=0,A[8]=A[2]*A[0]/100,DUE$=""
3040 IF A[2]<>0 THEN LET X0=100*(A[2]-A[3])/A[2]
3045 IF S2$="P" THEN LET A[8]=(A[2]-A[3])*A[0]/100
3050 IF P7$="P" THEN LET DUE$=STR(A[9]:M1$)
3060 PRINT (7)@(O[0]),A0$(8,7),@(O[1]),FNF$(A0$(15,P[0]),M0$),@(O[2]),B1$,@(O[3]),FNB$(A0$(22,3)),@(O[4]),A[2]:M1$,@(O[5]),A[1]:M1$,@(O[6]),A[3]:M1$,@(O[7]),A[2]-A[3]:M1$,@(O[8]),X0:M4$,@(O[9]),A[0]:M4$,@(O[10]),A[8]:M1$,@(O[11]),DUE$
3100 LET L=L+1,L1=L1+1,T1=T1+1
3200 REM " --- Accumulate totals"
3210 PRECISION 2
3220 LET T[5]=T[5]+A[1],U[5]=U[5]+A[1]
3230 LET T[6]=T[6]+A[2],U[6]=U[6]+A[2]
3245 LET T[7]=T[7]+A[3],U[7]=U[7]+A[3]
3250 LET T[8]=T[8]+A[8],U[8]=U[8]+A[8]
3260 LET T[9]=T[9]+A[9],U[9]=U[9]+A[9]
3290 PRECISION 4
3900 REM " --- Next record"
3990 GOTO 1100
4000 REM " --- All done"
4010 LET T9$="E"
4020 GOSUB 6000
4030 LET X0=0,T0$="",X$="Report Totals",H5$="Report Summary"
4040 GOSUB 5000
4050 PRINT (7)T0," Total Salespersons"
4060 IF U[6]<>0 THEN LET X0=100*(U[6]-U[7])/U[6]
4065 IF P7$="P" THEN LET DUE$=STR(U[9]:M1$)
4070 PRINT (7)@(O[4]-LEN(X$)),X$,@(O[4]),U[6]:M1$,@(O[5]),U[5]:M1$,@(O[6]),U[7]:M1$,@(O[7]),U[6]-U[7]:M1$,@(O[8]),X0:M4$,@(O[10]),U[8]:M1$,@(O[11]),DUE$
4100 REM " --- Averages "
4110 IF T0=0 THEN GOTO 4900
4130 FOR X=0 TO 9
4140 LET U[X]=U[X]/T0
4150 NEXT X
4220 LET X0=0,X$="Report Averages"
4230 IF U[6]<>0 THEN LET X0=100*(U[6]-U[7])/U[6]
4240 IF P7$="P" THEN LET DUE$=STR(U[9]:M1$)
4250 LET U[3]=0
4260 IF S2$="S" AND U[6]<>0 THEN LET U[3]=100*U[8]/U[6]
4270 IF S2$="P" AND U[6]-U[7]<>0 THEN LET U[3]=100*U[8]/(U[6]-U[7])
4290 PRINT (7)@(O[4]-LEN(X$)),X$,@(O[4]),U[6]:M1$,@(O[5]),U[5]:M1$,@(O[6]),U[7]:M1$,@(O[7]),U[6]-U[7]:M1$,@(O[8]),X0:M4$,@(O[9]),U[3]:M4$,@(O[10]),U[8]:M1$,@(O[11]),DUE$
4900 REM 
4990 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=9,HEADINGS$[5]=H5$
5020 CALL "SYC.HA",7,HEADINGS$[ALL],6,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)@(O[0]),"Invoice",@(O[4]+M1-6),"Gross",@(O[7]+M1-6),"Gross",@(H1),H1$
5120 PRINT (7)@(O[0]),"Number",@(O[1]),"Customer",@(O[3]+2),"Date",@(O[4]+M1-6),"Sales",@(O[5]+M1-6),"Other",@(O[6]+M1-5),"Cost",@(O[7]+M1-7),"Profit",@(O[8]+M4-5),"GP %",@(O[9]+M4-7),"Comm %",@(O[10]+M1-11),"Commission",@(H2),H2$
5130 PRINT (7)""
5190 RETURN 
6000 REM " --- Salesperson Break "
6010 IF T0$="" THEN GOTO 6100
6020 IF L1=0 THEN GOTO 6100
6030 LET X0=0,X$="Total For "+H5$
6035 IF LEN(X$)>O[4] THEN LET X$=X$(1,O[4]-1)
6040 IF T[6]<>0 THEN LET X0=100*(T[6]-T[7])/T[6]
6050 IF P7$="P" THEN LET DUE$=STR(T[9]:M1$)
6060 PRINT (7)@(O[4]),J$
6080 PRINT (7)@(O[4]-LEN(X$)),X$,@(O[4]),T[6]:M1$,@(O[5]),T[5]:M1$,@(O[6]),T[7]:M1$,@(O[7]),T[6]-T[7]:M1$,@(O[8]),X0:M4$,@(O[10]),T[8]:M1$,@(O[11]),DUE$
6090 LET T0=T0+1
6100 IF T9$<>"" THEN RETURN 
6110 DIM H5$(32),T[9]
6120 LET T0$=K$(5,3),H5$(7)="(Not On File)",L=L9+1,L1=0
6130 FIND (ARM10_DEV,KEY=N0$+"F"+T0$,DOM=6140)IOL=ARM10F
6140 LET H5$="Salesperson "+T0$+" "+FNP$(H5$(7,20))
6190 RETURN 
6200 REM " --- Search Invoice Transactions"
6210 LET PAID=0,SOLD=0
6220 READ (ART01_DEV,KEY=A0$(1,4)+A0$(15,6)+A0$(8,7)+"00",DOM=6350)IOL=ART01A
6230 LET SOLD=W[0]
6250 READ (ART11_DEV,KEY=A0$(1,4)+A0$(15,6)+A0$(8,7),DOM=6260)IOL=ART11A
6260 LET K$=KEY(ART11_DEV,END=6390)
6270 IF K$(1,17)<>A0$(1,4)+A0$(15,6)+A0$(8,7) THEN GOTO 6390
6280 READ (ART11_DEV,KEY=K$)IOL=ART11A
6290 IF P8$<>"" THEN IF C1$(2,3)<P8$ THEN GOTO 6260
6300 IF P9$<>"" THEN IF C1$(2,3)>P9$ THEN GOTO 6260
6310 IF C1$(1,1)="C" THEN LET PAID=PAID+C[0]+C[1] ELSE LET SOLD=SOLD+C[0]+C[1]
6340 GOTO 6260
6350 REM " --- Zero Balance Cash Invoice ?"
6360 READ (ART07_DEV,KEY=A0$(1,4)+A0$(15,6)+A0$(8,7),DOM=6390)IOL=ART07A
6370 IF P8$<>"" THEN IF D0$(18,3)<P8$ THEN GOTO 6390
6375 IF P9$<>"" THEN IF D0$(18,3)>P9$ THEN GOTO 6390
6380 LET SOLD=-D[0],PAID=D[1]
6390 RETURN 
8000 REM " --- Functions "
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8090 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "IVU - Physical Inventory Update
0020 REM "Program IVU.VA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0150 CALL "SYC.DA",1,8,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET IVM04_DEV=CHANNELS[8],IVM12_DEV=CHANNELS[9],IVT04_DEV=CHANNELS[10]
0200 REM " --- IOLIST's"
0230 IVE03A: IOLIST W0$(1),W1$(1),W[ALL]
0260 IVM02A: IOLIST A0$(1),A1$(1),A2$,A[ALL]
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0290 IVM10P: IOLIST Y0$(1)
0400 REM " --- Parameters"
0410 DIM G[1]
0420 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 LET G[0]=NUM(G2$(1,2))
0500 REM " --- Initializations"
0510 PRECISION P[2]
0515 DIM IV_CHANS[44],PARAMS[10],PARAMS$[10],ITEM$[3]
0575 LET PARAMS[0]=G[0],PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3)
0580 LET PARAMS$[2]=I2$,PARAMS$[3]=I3$,PARAMS$[4]=I4$
0590 LET ACTION$="PH"
0600 REM " --- Additional File Opens"
0610 IF LS$<>"Y" THEN GOTO 0650
0620 CALL "SYC.DA",1,11,15,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS>0 THEN GOTO 9900
0640 LET IVM08_DEV=CHANNELS[11],IVM17_DEV=CHANNELS[12],IVT01_DEV=CHANNELS[13]
0645 LET IVT02_DEV=CHANNELS[14],IVT03_DEV=CHANNELS[15]
0650 REM " --- Update channels"
0660 LET IV_CHANS[0]=SYS01_DEV,IV_CHANS[1]=IVM01_DEV,IV_CHANS[2]=IVM02_DEV
0670 LET IV_CHANS[4]=IVM04_DEV,IV_CHANS[7]=IVM07_DEV,IV_CHANS[8]=IVM08_DEV
0680 LET IV_CHANS[12]=IVM12_DEV,IV_CHANS[17]=IVM17_DEV,IV_CHANS[41]=IVT01_DEV
0690 LET IV_CHANS[42]=IVT02_DEV,IV_CHANS[43]=IVT03_DEV,IV_CHANS[44]=IVT04_DEV
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0800 REM " --- Run Update?"
0810 LET V4$="Are You Ready To Update The "+F5$
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0910 CALL "SYC.NB","Updating",17+P[0],COLUMN
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
0970 REM " --- Position file"
0990 READ (IVW10_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next entry record"
1010 LET K$=KEY(IVW10_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 READ (IVW10_DEV)
1200 REM " --- Update IVM-10 cycle code record"
1210 LET Y0$(1)=N0$+"P"+WHSE$+K$(3,2)
1220 FIND (IVM10_DEV,KEY=Y0$(1,7),DOM=1900)IOL=IVM10P
1230 LET Y0$(8,1)="4"
1240 WRITE (IVM10_DEV,KEY=Y0$(1,7))IOL=IVM10P
1900 REM " --- Position physical inventory record"
1980 LET FIRST$=K$(1,2)+WHSE$+K$(3)
1990 READ (IVE03_DEV,KEY=FIRST$,DOM=2000)
2000 REM " --- Read next physical inventory record"
2010 LET K$=KEY(IVE03_DEV,END=3900)
2020 IF POS(FIRST$=K$)<>1 THEN GOTO 3900
2030 READ (IVE03_DEV)IOL=IVE03A
2040 PRINT @(COLUMN,11),W0$(3,2)," ",W0$(5,2)," ",K$(7,10)," ",K$(17,P[0]),
2050 LET FREEZEQTY=W[0],COUNTQTY=W[1],LASTPHYSDATE$=W1$(1,3)
2100 REM " --- Has Phys Count been entered?/Is Freeze same as Counted?
2110 DIM A0$(24),A1$(64),A[20]
2130 EXTRACT (IVM02_DEV,KEY=K$(1,2)+K$(3,2)+K$(17,20),DOM=NEXTIVE03)IOL=IVM02A
2140 IF A1$(15,1)<>"Y" THEN GOTO NEXTIVE03; REM "Selected for Phys Inventory
2150 IF W1$(4,1)<>"Y" THEN GOTO NEXTIVE03; REM "Phys Count not entered
2160 IF FREEZEQTY<>COUNTQTY THEN GOTO INITATAMO
2170 LET A1$(15,1)="N",A1$(16,3)=LASTPHYSDATE$,A[8]=COUNTQTY
2180 WRITE (IVM02_DEV,KEY=K$(1,2)+K$(3,2)+K$(17,P[0]))IOL=IVM02A
2190 GOTO NEXTIVE03
2300 REM " --- Set up for call to ATAMO
2305 INITATAMO: 
2310 DIM REFS$[11],REFS[5]
2320 LET QTYTOPASS=COUNTQTY-FREEZEQTY
2330 LET ITEM$[0]=W0$(1,2),ITEM$[1]=W0$(3,2),ITEM$[2]=W0$(17,20)
2340 LET ITEM$[3]=W0$(37,20)
2350 LET REFS$[0]=LASTPHYSDATE$,REFS$[4]=W0$(5,2)
2370 LET REFS[0]=QTYTOPASS,REFS[1]=A[11],REFS[5]=COUNTQTY
2400 REM " --- Call IV update routine, ATAMO
2420 CALL "IVC.UA",ACTION$,IV_CHANS[ALL],PARAMS[ALL],PARAMS$[ALL],ITEM$[ALL],REFS$[ALL],REFS[ALL],STATUS
2900 REM " --- Loop back for next physical inventory record"
2905 NEXTIVE03: 
2950 REMOVE (IVE03_DEV,KEY=K$)
2990 GOTO 2000
3900 REM " --- Loop back for next entry record"
3990 GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

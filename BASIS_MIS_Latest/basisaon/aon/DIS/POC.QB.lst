0010 REM "POC - Purchase Order SpeedSearch (Purchase Order Header)"
0020 REM "Program POC.QB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0060 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open/Lock Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APM-01",FILES$[2]="APM-10",FILES$[3]="POE-02"
0140 LET FILES$[4]="POE-12",FILES$[5]="POM-02"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0170 LET APM01_DEV=CHANNELS[1],APM10_DEV=CHANNELS[2],POE02_DEV=CHANNELS[3]
0180 LET POE12_DEV=CHANNELS[4],POM02_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 APM01A: IOLIST A0$(1),A1$(1)
0220 POE02A: IOLIST B0$(1),B1$(1),B2$,B3$,B[ALL]
0230 APM10C: IOLIST C0$(1)
0260 APS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,I5$,M1$,M2$,M3$
0310 POE12A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0320 POM02A: IOLIST Y0$(1),Y1$(1)
0400 REM " --- Parameters"
0405 DIM P[2]
0410 LET X$=STBL("!SPEEDSEARCH"),N0$=X$(1,2),MAXPAGE=NUM(X$(66,2))
0415 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0420 LET SNAME$=FNP$(X$(19,8)),SWIDTH=NUM(X$(43,2)),SHEIGHT=NUM(X$(45,2))
0425 LET MAXOPT=NUM(X$(57,2)),SYS01_DEV=NUM(STBL("!SYS01"))
0430 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0440 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0450 LET PAGES=INT((DSZ-5000)/4000),TNAME$=FNP$(X$(3,8)),ONAME$=FNP$(X$(27,8))
0460 IF PAGES<3 THEN GOTO 9900
0470 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0480 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9900)IOL=APS01A
0485 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9900)IOL=IVS01A
0490 LET P[0]=NUM(P2$(1,2))
0500 REM " --- Initializations"
0510 DIM A0$(8),A1$(30),B0$(15),B1$(160),B[8],C0$(32)
0520 DIM Y0$(4),Y1$(32),W0$(18),W1$(48),W2$(32),W3$(22),W4$(40),W[12]
0530 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),OPTLIST$[2,MAXOPT,6]
0550 CALL "SYC.RM",M1$,M1,9,15
0560 LET D_STATUS=0,MAXROW=HEIGHT-5,PAGE=1,PGM$=PGM(-2)
0570 LET A0$(1)=N0$+OPTION$[3],M1=LEN(M1$),P=WIDTH-2-M1
0580 LET B0$(1)=N0$+OPTION$[3]
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,ONAME$
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0720 LET HEADING$(2)="PO #    Wh Ordered  Required Promised Ship Via"
0730 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0740 PRINT @(0,0),'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",SNAME$,0
0920 PRINT 'CF',
0930 CALL "SYC.WB",TNAME$,0
0940 PRINT @(0,1),'CL',@(0,2),'CL',@(0,3),'CL',
0990 GOTO 1100
1000 REM " --- Process key field"
1010 GOSUB 6000
1015 CALL "SYC.WB",TNAME$,0
1030 PRINT @(1,0),'CL','SB',"Vendor:",'SF',@(0,1),'CL',@(0,2),'CL',
1035 LET V2$="",V2$=KEY(APM01_DEV,END=1050)
1040 IF POS(A0$(1,2)=V2$)<>1 THEN LET V2$=""
1045 IF V2$<>"" THEN LET V2$=V2$(3)
1050 LET V0$="M",V1$="KE",V3$=M0$,V0=6,V1=9,V2=0,V4$=""
1055 PRINT @(V0+V1+1,V2),'CL',
1060 GOSUB 7000
1065 ON V3 GOTO 1080,1000,1000,1070,3800,3900,3800
1070 CALL "SYC.LV",V$
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(3)=V$,B0$(3)=V$
1100 REM " --- Retrieve records for display"
1110 DIM X$(WIDTH-2)
1120 FIND (APM01_DEV,KEY=A0$,DOM=1000)IOL=APM01A
1130 LET X$(1)='SB'+" Vendor: "+'SF'+FNF$(A0$(3,P[0]),M0$)+" "+FNP$(A1$)
1140 LET X1$=A0$(3,6),X2$=PARAM$
1150 CALL "SYC.QB",X$,X1$,X2$
1180 READ (POE02_DEV,KEY=B0$,DOM=1200)IOL=POE02A
1190 GOTO 1300
1200 REM " --- PO Number"
1210 PRINT @(1,1),'CL','SB',"  PO #:",'SF',
1220 LET V0=7,V1=9,V2=1,V0$="Z",V1$="KE",V2$="",V3$="",V4$=""
1230 GOSUB 7000
1240 ON V3 GOTO 1260,1200,1000,1250,3800,3900,3800
1250 CALL "POC.LA","P",B0$(3,6),V$
1260 LET B0$(1)=A0$(1,2)+A0$(3)+V$
1300 REM " --- Retrieve records for display"
1310 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,SHEIGHT-2],X$(WIDTH-2)
1320 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,SELECTION$=""
1330 READ (POE02_DEV,KEY=B0$,DOM=1400)IOL=POE02A
1340 LET X$(1)='SB'+"   PO #: "+'SF'+B0$(9,7)
1350 LET X1$=B0$(9,7),X2$=SUBPARAM$
1360 CALL "SYC.QB",X$,X1$,X2$
1390 GOTO 1500
1400 REM " --- Read next purchase order"
1410 LET K$=KEY(POE02_DEV,END=1900)
1420 IF POS(A0$=K$)<>1 THEN GOTO 1900
1430 READ (POE02_DEV)IOL=POE02A
1440 LET C0$(1)=N0$+"C"+B1$(38,2)
1450 FIND (APM10_DEV,KEY=C0$(1,5),DOM=1500)IOL=APM10C
1500 REM " --- Display page?"
1510 IF L<MAXROW THEN GOTO 1600
1520 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
1530 GOSUB 5000
1540 IF DONE THEN GOTO 4000
1570 IF ANSWER$<>"" THEN GOTO 3000
1590 LET L=1,X1=1
1600 REM " --- Middle window record"
1610 DIM G$(WIDTH-2)
1620 LET G$(2)=B0$(9,7),G$(10)=B1$(1,2),TOTAL=0,Y0$(1)=""
1630 LET G$(13)=FNB6$(B1$(5,6)),G$(22)=FNB6$(B1$(23,6))
1640 IF FNP$(B1$(11,6))<>"" THEN LET G$(31)=FNB6$(B1$(11,6))
1650 LET G$(40)=B1$(59,15),LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=B0$(3)
1690 READ (POE12_DEV,KEY=B0$,DOM=1700)
1700 REM " --- Read next line item"
1710 LET K$=KEY(POE12_DEV,END=1800)
1720 IF POS(B0$=K$)<>1 THEN GOTO 1800
1730 READ (POE12_DEV)IOL=POE12A
1740 IF N0$+W1$(1,2)=Y0$ THEN GOTO 1770
1750 LET Y0$(1)=N0$+W1$(1,2)
1760 FIND (POM02_DEV,KEY=Y0$,DOM=1700)IOL=POM02A
1770 LET QTY=W[3]-W[7]
1775 IF Y1$(21,1)="O" THEN LET QTY=1
1780 LET TOTAL=(TOTAL+(QTY*W[1]))*1
1790 GOTO 1700
1800 REM " --- Bottom window record"
1810 LET G$(1)="",MORE$[PAGE,X1,0]=STR(SHEIGHT-2)
1820 LET G$(5)=" {AP Terms}"+C0$(4,2)+" "+C0$(6,20)
1830 LET G$(P-10)="{PO Total}"+STR(TOTAL:M1$),MORE$[PAGE,X1,1]=G$
1840 LET G$(1)="{Freight Terms}"+B1$(44,15)
1850 IF FNP$(B1$(17,6))<>"" THEN LET G$(P-12)="{Not Before}"+FNB6$(B1$(17,6))
1860 LET MORE$[PAGE,X1,2]=G$,G$(1)=""
1870 IF FNP$(B1$(74,20))<>"" THEN LET G$(2)="{Acknowledged}"+B1$(74,20)
1880 IF FNP$(B1$(112,7))<>"" THEN LET G$(P-7)="{Req #}"+B1$(112,7)
1885 LET MORE$[PAGE,X1,3]=G$,LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
1890 GOTO 1400
1900 REM " --- No more PO's"
1910 IF X0>0 THEN GOTO 2000
1920 DIM MESSAGE$[1]
1930 LET MESSAGE$[0]="No Purchase Orders Found (<Enter>=Continue)"
1940 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
1960 IF V3=5 THEN GOTO 4000
1970 IF V3=6 THEN GOTO 4000
1990 GOTO 3000
2000 REM " --- Display last page"
2010 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2020 GOSUB 5000
2030 IF DONE THEN GOTO 4000
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$=ANSWER$,CHOICE2$=""
3030 GOSUB 5000
3040 IF CONTROL=1 THEN GOTO 1000
3090 GOTO 4000
3800 REM " --- Exit"
3810 LET OPTION$[1]="EXIT"
3890 GOTO 4000
3900 REM " --- Startover"
3910 LET OPTION$[1]="STARTOVER"
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CS',
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9950 EXIT 
9999 END

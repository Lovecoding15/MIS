0010 REM "IVE - Transaction Entry (Detail Overlay)"
0020 REM "Program IVE.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 PRECISION I[2]
0200 REM " --- IOLIST's"
0210 IVE11A: IOLIST W0$(1),W1$(1),W2$(1),W[ALL]
0220 IVM01A: IOLIST D0$(1),D1$(1),D2$(1),D3$,D4$,D5$,D6$,D[ALL]
0230 IVM02A: IOLIST L0$(1),L1$,L2$,L[ALL]
0280 IVM07A: IOLIST R0$(1),R1$,R[ALL]
0500 REM " --- Init Data"
0510 DIM D0$(22),D1$(60),D2$(62),D[12],L0$(24),L[20],R0$(44),R[12]
0520 DIM W0$(53),W1$(22),W2$(67),W[2],DESCRIPTION$(DW),MESSAGE$[1]
0530 LET W0$(1)=A0$(1,9),L=L1,CP=77-M1-M2-M3,QP=77-M1-M2
0540 IF WHO$="" THEN LET WHO$=F0$(4,3)
0550 DIM ITEM$[6],ITEM[12]
0900 REM " --- Display existing records"
0910 GOSUB 5100
0920 IF Q5=1 THEN GOTO 4000
0990 GOTO 1200
1000 REM " --- Overflow"
1010 IF L>L9 THEN PRINT @(0,L9+2),'CE',@(0,L1),'LD','LD',
1020 IF L>L9 THEN LET L=L9
1100 REM " --- Next Key
1110 LET K$=KEY(IVE11_DEV,END=1200)
1120 IF POS(K$(1,9)=W0$)<>1 THEN GOTO 1200
1140 READ (IVE11_DEV)IOL=IVE11A
1190 GOTO 1490
1200 REM " --- Generate next sequence number"
1210 LET W0=NUM(W0$(10,3))+10
1220 IF W0<1000 THEN GOTO 1400
1230 LET MESSAGE$[0]="Next Sequence Number Exceeds 999. Please Use Another Sequence"
1240 LET MESSAGE$[1]="Number Or A Different Reference Number (<Enter>=Continue)"
1250 LET NUMBER=1,W0=0
1260 GOSUB 6900
1400 REM " --- Sequence number"
1410 LET V4$="Enter A Valid Sequence Number"
1420 LET V0$="Z",V1$="CKER",V2$=STR(W0),V3$="",V0=3,V1=0,V2=L,I0=0
1425 PRINT @(0,L),'CL',@(0,L+1),'CL',
1430 GOSUB 7000
1435 ON V3 GOTO 1450,1400,1400,1400,1440
1440 PRINT @(0,L),'CL',@(0,L+1),'CL',
1445 GOTO 4000
1450 IF V>999 THEN GOTO 1400
1460 PRINT @(0,21),'CL',@(0,22),'CL',
1470 IF V$="000" THEN GOTO 1200
1475 LET W0$(10,3)=V$
1480 FIND (IVE11_DEV,KEY=W0$(1,12),DOM=1600)IOL=IVE11A
1490 IF W0$(1,12)<=K9$ THEN GOSUB 5400
1500 REM " --- Display existing record"
1510 GOSUB 5000
1520 LET I0=9,O0$="",O1$=""
1530 IF TYPE$="I" THEN LET O0$=W1$,O1$=W2$(1,20),O[0]=W[0]
1590 GOTO 3300
1600 REM " --- New record initializations"
1610 DIM W[2],L[16],R[12]
1620 LET W0$(13)="",W0$(38,3)=WHO$,W0$(41,1)="N",W1$(3)=""
1630 LET O0$="",O1$="",O[0]=0
1660 IF POSTGL$<>"Y" THEN GOTO 3200
1670 LET W0$(28,10)=X1$
1680 IF GL$="Y" THEN PRINT @(5,L+1),FNF$(W0$(28,G[0]),G5$)
1690 GOTO 3200
2000 REM " --- Warehouse"
2010 IF P3$(1,1)<>"Y" THEN LET W1$(1,2)=P3$(6,2); GOTO 2080
2015 IF W1$(1,2)="  " THEN LET W1$(1,2)=P3$(6,2)
2020 LET V0$="S",V1$="C",V2$=W1$(1,2),V0=2,V1=5,V2=L,I0=1
2025 LET V4$="Enter A Valid Warehouse ID (<F3>=Lookup)"
2030 GOSUB 7000
2035 ON V3 GOTO 2050,2000,3200,2040,3900
2040 CALL "IVC.LK",3,V1,V2,V$
2050 LET W1$(1,2)=V$
2060 FIND (IVM10_DEV,KEY=N9$+"C"+W1$(1,2),DOM=2200)
2080 PRINT @(5,L),W1$(1,2),
2090 GOTO 3200
2200 REM " --- Item Number"
2210 PRINT @(0,21),'CL',
2220 LET V0$="",V2$=W1$(3,I[0]),V4$="",V1=10,V2=L
2230 CALL "IVC.IA",V0$,V1,V2,IV_FILES[ALL],N0$,V2$,V4$,IV_PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
2235 ON V3 GOTO 2240,2200,3200,2200,3900
2240 LET D0$(1)=N0$+ITEM$[0],D1$(1)=ITEM$[1],D2$(1)=ITEM$[2],D3$=ITEM$[3]
2245 LET W1$(3)=D0$(3),D4$=ITEM$[4],DESCRIPTION$(1)=D1$,L0$(1)=N0$+W1$
2250 PRINT @(V1,V2),W1$(3,I[0]),@(DP,L),DESCRIPTION$,
2255 FIND (IVM02_DEV,KEY=L0$,DOM=2200)IOL=IVM02A
2260 IF W1$=O0$ THEN GOTO 2290
2265 DIM W[2]
2270 LET W2$(1)=""
2275 PRINT @(0,L+1),'CL'
2280 REMOVE (IVE11_DEV,KEY=W0$(1,12),DOM=2290)
2290 LET W[1]=L[11]
2300 REM " --- Display item information"
2310 PRINT @(12+G5,L+1),D2$(1,3),@(CP),W[1]:M3$,@(0,21),'CL',
2320 IF D4$(1,1)="S" THEN GOTO 2350
2330 PRINT @(0,21),'SB',"Location ",'SF',L1$(1,10),'SB'," On Hand ",'SF',L[0]:M9$,'SB'," Committed ",'SF',L[2]:M9$,'SB'," Available ",'SF',L[0]-L[2]:M9$,
2340 GOTO 3200
2350 PRINT @(39,21),'RB','SB',"Superceded by Item ",'SF',D4$(2,I[0]),
2390 GOTO 3200
2400 REM " --- GL Account"
2405 IF GL$<>"Y" THEN GOTO 3200
2410 IF POSTGL$<>"Y" THEN GOTO 3200
2420 LET V4$="Enter A Valid G/L Account Number (<F3>=Lookup)"
2430 LET V0$="M",V1$="C",V2$=W0$(28,G[0]),V3$=G1$,V0=G[1],V1=5,V2=L+1
2440 GOSUB 7000
2445 ON V3 GOTO 2460,2400,3200,2450,3200
2450 CALL "SYC.LG",V$
2460 FIND (GLM01_DEV,KEY=N0$+V$,DOM=2400)
2470 LET W0$(28,G[0])=V$
2480 IF GL$="Y" THEN PRINT @(V1,V2),FNF$(W0$(28,G[0]),G1$)
2490 GOTO 3200
2500 REM " --- Who"
2510 LET V4$="Enter An Identification For The User Entering This Transaction"
2520 LET V0$="S",V1$="C",V2$=W0$(38,3),V3$="",V0=3,V1=7+G5,V2=L+1
2530 GOSUB 7000
2540 ON V3 GOTO 2550,2500,3200,2500,3200
2550 LET W0$(38,3)=V$,WHO$=W0$(38,3)
2590 GOTO 3200
2600 REM " --- Enter Lotted/Serialized Information"
2610 IF LS$<>"Y" THEN GOTO 3200
2620 IF D2$(19,2)<>"YY" THEN GOTO 3200
2650 GOSUB 6000
2690 GOTO 3200
2700 REM " --- Cost
2710 IF TYPE$="I" OR (TYPE$="A" AND W[0]<0) THEN GOTO 2790
2715 IF LS$="Y" AND D2$(19,2)="YY" THEN GOTO 2790
2720 LET V0$="N",V2$=STR(W[1]),V3$=M3$,V0=M3,V1=CP,V2=L+1
2725 LET V4$="Enter The Cost Of This Item"
2740 GOSUB 7000
2745 ON V3 GOTO 2750,2700,3200,2700,3200
2750 IF V<0 THEN GOTO 2700
2760 LET W[1]=V
2770 GOSUB 5900
2790 GOTO 3200
2800 REM " --- Skip quantity entry for lotted/serialized"
2810 LET V0$="N",V2$=STR(W[0]),V3$=M2$,V0=M2,V1=QP,V2=L+1
2820 IF LS$<>"Y" OR D2$(19,2)<>"YY" THEN GOTO 2900
2830 IF L9$<>"R" THEN GOTO 2900
2840 IF P3$(17,1)="S" THEN LET W[0]=1,V=W[0]
2850 IF P3$(17,1)="L" THEN LET V=W[0]
2860 PRINT @(V1,V2),W[0]:M2$
2890 GOTO 3000
2900 REM " --- Quantity"
2910 LET V4$="Enter The Quantity For This Transaction"
2920 GOSUB 7000
2930 ON V3 GOTO 2940,2900,3200,2900,3200
2940 IF TYPE$="A" THEN GOTO 3000
2950 LET TESTQUAN=V
2955 IF TYPE$="I" THEN LET TESTQUAN=-TESTQUAN
2960 IF TESTQUAN>=0 THEN GOTO 3020
2970 IF LS$="Y" AND D2$(19,2)="YY" THEN LET ON_HAND=R[0] ELSE LET ON_HAND=L[0]
2975 IF ON_HAND>=0 AND ABS(TESTQUAN)<=ON_HAND THEN GOTO 3020
2980 LET MESSAGE$[0]="Insufficient Stock (<Enter>=Continue)",NUMBER=0
2985 GOSUB 6900
2990 IF V$<>"SPECIAL" THEN GOTO 2900
3000 REM " --- Calculate extension and display"
3010 IF D2$(19,2)="YY" AND P3$(17,1)="S" AND ABS(V)>1 THEN GOTO 2800
3020 LET W[0]=V
3030 GOSUB 5900
3090 GOTO 3200
3200 REM " --- Seq Control"
3210 IF V3=4 THEN IF I0>2 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1200,2000,2200,2400,2500,2600,2700,2800,3300
3300 REM " --- Correct"
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=W0$(1,12)
3325 LET V4$="<Enter>=Correct      <F2>=Change     <D>=Delete"
3330 GOSUB 7000
3335 PRINT @(0,L9+2),'CL',
3340 IF V3=2 THEN LET I0=8; PRINT @(0,L+2),'CL'; GOTO 3200
3350 IF V$="" THEN GOTO 3400
3360 IF V$="D" THEN GOTO 3900
3370 GOTO 3300
3400 REM " --- Write"
3410 IF FNP$(W1$(3))="" AND W[0]=0 THEN LET V$="D"; GOTO 3900
3430 WRITE (IVE11_DEV,KEY=W0$(1,12))IOL=IVE11A
3450 LET L=L+2
3460 IF POS(TYPE$="IC")>0 THEN GOTO 3500
3470 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3500 REM " --- Commit any difference between old and new quantities"
3510 IF O0$=W1$ AND O1$=W2$(1,20) AND O[0]=W[0] THEN GOTO 3980
3520 IF O0$<>W1$ OR O1$<>W2$(1,20) THEN GOTO 3600
3530 LET QNTY=W[0]-O[0],ACTION$="CO"
3540 GOSUB 5500
3590 GOTO 3980
3600 REM " --- Commit new item/Uncommit old item"
3610 LET QNTY=O[0],ACTION$="UC"
3620 GOSUB 5500
3630 LET QNTY=W[0],ACTION$="CO"
3640 LET O0$=W1$,O1$=W2$(1,20)
3650 GOSUB 5500
3690 GOTO 3980
3900 REM " --- Delete"
3910 LET QNTY=O[0],ACTION$="UC"
3920 GOSUB 5500
3930 REMOVE (IVE11_DEV,KEY=W0$(1,12),DOM=3950)
3950 PRINT @(0,L),'CL',@(0,L+1),'CL',
3960 DIM W[2]
3980 IF V3=4 THEN GOTO 4000
3990 GOTO 1000
4000 REM " --- Options"
4005 PRINT @(0,21),'CL',@(0,22),'CL',
4010 LET V4$="Is The Detail Information Correct (Y/N/Review/List/Delete)?"
4015 LET V0$="S",V1$="C",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4040 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4050 IF V$="L" OR V$="LIST" THEN GOTO 4400
4060 IF V$="DELETE" THEN LET O0=2; GOTO 4190
4070 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Return to header overlay"
4110 PRINT @(0,L0),'CE',
4190 RUN "IVE.AA"
4400 REM " --- List"
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4620 READ (IVE11_DEV,KEY=A0$(1,12),DOM=4640)
4640 GOSUB 5400
4690 GOTO 1000
5000 REM " --- Display"
5010 LET D1$(1)="",D2$(1)=""
5020 FIND (IVM01_DEV,KEY=N0$+W1$(3),DOM=5030)IOL=IVM01A
5030 LET DESCRIPTION$(1)=FNITEM$(D1$,I[3],I[4],I[5]),X$="",Z$="",WHO$=W0$(38,3)
5040 IF GL$="Y" THEN IF POSTGL$="Y" THEN LET X$=FNF$(W0$(28,G[0]),G5$)
5050 IF LS$="Y" THEN LET Z$=W2$(1,I[1])
5060 PRINT @(0,L),W0$(10,3),@(5),W1$(1,2),@(10),W1$(3,I[0]),@(DP),DESCRIPTION$,@(5,L+1),X$,@(7+G5),W0$(38,3),@(12+G5),D2$(1,3),@(17+G5),Z$,@(77-M2-M1-M3),W[1]:M3$,W[0]:M2$,W[2]:M1$,
5090 RETURN 
5100 REM " --- List"
5105 LET Q5=0
5110 GOSUB 5400
5120 READ (IVE11_DEV,KEY=A0$(1,12),DOM=5130)
5130 LET K$=KEY(IVE11_DEV,END=5300)
5140 IF POS(K$(1,9)=W0$)<>1 THEN GOTO 5300
5150 READ (IVE11_DEV)IOL=IVE11A
5160 LET Q5=1
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="<Enter>=Continue",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5225 IF V3=4 THEN GOTO 5300
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+2,K9$=W0$(1,12)
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5410 FOR X=L1 TO L9+1
5420 PRINT @(0,X),'CL',
5430 NEXT X
5440 LET L=L1
5490 RETURN 
5500 REM " --- Commit Inventory"
5510 IF O0$="" THEN GOTO 5590
5520 LET IV_INFO$[1]=O0$(1,2),IV_INFO$[2]=O0$(3),IV_INFO$[3]=O1$
5530 LET IV_REFS[0]=QNTY
5550 CALL "IVC.UA",ACTION$,IV_FILES[ALL],IV_INFO[ALL],IV_PARAMS$[ALL],IV_INFO$[ALL],IV_REFS$[ALL],IV_REFS[ALL],IV_STATUS
5590 RETURN 
5900 REM " --- Calculate extension and display"
5910 PRECISION 2
5920 LET W[2]=W[0]*W[1]
5930 PRECISION I[2]
5940 PRINT @(CP,L+1),W[1]:M3$,W[0]:M2$,W[2]:M1$,
5990 RETURN 
6000 REM " --- Lotted/Serialized Item"
6010 DIM R0$(44),R[12]
6020 LET R1$="",L9$="I"
6030 IF TYPE$="R" THEN LET L9$="R"
6040 IF TYPE$="A" AND W[0]>0 THEN LET L9$="R"
6050 IF FNP$(W2$(1,20))="" THEN GOTO 6500
6060 IF L<18 THEN GOTO 6100
6070 PRINT @(0,21),'CE',@(0,L1),'LD','LD',
6080 LET L=L-2
6090 GOTO 6060
6100 REM " --- Display lot/serial information"
6110 PRINT 'SB',@(3,L+2),P8$+"/Comment",@(77-M3-4),"Qty",@(77-5),"Cost",'SF',@(3,L+3),W2$(1,I[1]),@(77-M2-M3),W[0]:M2$,@(77-M3),W[1]:M3$,@(6,L+4),W2$(28,40),@(48),'SB',"Current Location: ",'SF',W2$(21,7),
6200 REM " --- Sufficient quantity available for issue?"
6210 IF L9$<>"I" THEN GOTO 6300
6220 LET R0$(1)=N0$+W1$+W2$
6230 FIND (IVM07_DEV,KEY=R0$,DOM=6300)IOL=IVM07A
6240 PRINT 'SB',@(77-M3-M2*2-7,L+2),"On-Hnd",@(77-M3-M2-6,L+2),"Avail",'SF',@(77-M3-M2*3,L+3),R[0]:M2$,@(77-M3-M2*2),R[0]-R[1]:M2$,
6300 REM " --- OK?"
6310 LET V4$="<Enter>=Use This Lot/Serial Number  <F2>=Change  <D>=Delete And Select Another"
6320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L+2
6340 GOSUB 7000
6350 PRINT @(0,21),'CL',@(0,L+2),'LD','LD','LD',
6360 IF V$="D" THEN GOTO 6400
6370 IF V3=2 AND L9$="R" THEN GOTO 6450
6380 GOTO 6700
6400 REM " --- Delete"
6410 LET W2$(1)="",R0$="",R1$="",QNTY=0,ACTION$="UC"
6420 GOSUB 5500
6430 GOTO 6500
6450 REM " --- Initializations"
6460 DIM R0$(44),R1$(66),R[12]
6470 LET R0$(1)=N0$+W1$,R0$(25)=W2$(1,20),R1$(1)=W2$(21,7),R1$(14)=W2$(28)
6480 LET R[3]=W[1],R[0]=W[0]
6500 REM " --- Enter/Select Lot/Serial Number"
6510 IF L9$="R" THEN GOTO 6540
6520 LET V8$="Z",R0$=N0$+W1$
6525 IF TYPE$="A" THEN LET V8$="A"
6530 CALL "SYC.LL",V8$,R0$,R1$,R[ALL]
6535 GOTO 6560
6540 IF L>15 THEN PRINT @(0,L1),'LD','LD'; LET L=L-2; GOTO 6540
6545 PRINT @(0,L+2),'CE',
6550 LET V0=L+2,V1=21,V9=12,V9$=N0$+W1$,R[0]=W[0],R[3]=W[1]
6555 CALL "IVC.SA",V9$,R0$,R1$,R[ALL],V0,V1,TYPE$,IVE11_DEV,W0$
6560 IF FNP$(R0$)<>"" AND FNP$(R0$)<>"NONE" THEN GOTO 6600
6561 PRINT @(17+G5,L+1),J1$(1,I[1])
6570 LET W2$(1)=""
6575 REMOVE (IVE11_DEV,KEY=W0$(1,12),DOM=6700)
6595 GOTO 6700
6600 REM " --- Create IVE-11 from IVM-07"
6610 DIM W2$(67)
6620 LET W2$(1)=R0$(25,20)+R1$(1,7),W2$(28)=R1$(14,40),W[1]=R[3],W[2]=W[0]*W[1]
6630 IF L9$="R" THEN LET W[0]=R[0],W[1]=R[3],W[2]=R[0]*R[3]
6640 PRINT @(17+G5,L+1),W2$(1,I[1]),@(77-M2-M1-M3,L+1),W[1]:M3$,W[0]:M2$,W[2]:M1$
6700 REM 
6710 IF L9$<>"I" THEN GOTO 6800
6720 IF FNP$(R0$)="" OR R0$="NONE" THEN GOTO 6730
6725 GOTO 6800
6730 LET MESSAGE$[0]="No Lot/Serial Number Selected (<Enter>=Continue/<F4>=Skip Entry)",NUMBER=0
6740 GOSUB 6900
6750 IF V3<>4 THEN GOTO 6500
6760 LET W[0]=0,I0=7
6800 REM " --- Done with lot/serial number"
6890 RETURN 
6900 REM " --- Display warning"
6930 CALL "SYC.XA",2,MESSAGE$[ALL],NUMBER,22,-1,V$,V3
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "IVP - Parameter Maintenance (Item Defaults)"
0020 REM "Program IVP.AE"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 IVS10D: IOLIST A0$,A1$(1),A2$(1)
0220 GLM01A: IOLIST B0$(1),B1$(1)
0300 IVM10A: IOLIST X0$
0310 IVM10F: IOLIST X0$
0320 IVM13A: IOLIST X0$,X1$
0330 ARM10D: IOLIST X0$
0500 REM " --- Initializations"
0510 DIM A0$(3),A1$(32),A2$(5*G[4]),PRODUCT$(20),CLASS$(20),BUYER$(20)
0520 DIM DISTRIB$(20),DESCRIPTION$[1],FIELD[1],POSITION[1],LENGTH[1]
0530 DIM GLV4$[5]
0550 LET A0$(1)=N0$+"D",G5=LEN(G5$),V5$="Enter The Default "
0560 LET GW=MIN(35,80-(32+G5)),NUMBER=1
0570 LET DNE$=V5$+"Code (D=Derived/E=Entered/N=No Recalculation/Blank)"
0575 LET GLV4$[1]="Inventory",GLV4$[2]="Cost Of Goods Sold"
0580 LET GLV4$[3]=GLV4$[1]+" Adjustment",GLV4$[4]=GLV4$[2]+" Adjustment"
0585 LET GLV4$[5]="Purchase Price Variance"
0600 REM " --- Additional file opens"
0610 IF DI$="Y" THEN LET FILES$[5]="ARM-10"
0615 IF GL$="Y" THEN LET FILES$[6]="GLM-01"
0620 CALL "SYC.DA",1,5,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0625 IF STATUS>0 THEN GOTO 9900
0630 LET ARM10_DEV=CHANNELS[5],GLM01_DEV=CHANNELS[6]
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0720 CALL "SYC.WC",1,0,80,0,0,13,0
0730 PRINT 'SB',@(12,4),"Product Type:",@(56),"Stocking Level:",@(14,5),"Item Class:",@(63),"Taxable:",@(14,6),"Buyer Code:",@(61),"Item Type:",@(7,7),"Distribution Code:",@(62),"ABC Code:",@(17,8),"Sale UM:",@(62),"EOQ Code:",@(13,9),"Purchase UM:",@(54),"Order Point Code:",@(7,10),"Lotted/Serialized:",@(53),"Safety Stock Code:",@(13,11),"Inventoried:",'SF',
0740 IF GL$="N" THEN GOTO 1200
0750 PRINT @(31,13),'SB'," General Ledger Accounts ",@(20,15),"Inventory:",@(11,16),"Cost Of Goods Sold:",@(9,17),"Inventory Adjustment:",@(0,18),"Cost Of Goods Sold Adjustment:",@(6,19),"Purchase Price Variance:",'SF',
1200 REM " --- Retrieve record"
1210 FIND (IVS10_DEV,KEY=A0$,DOM=1500)IOL=IVS10D
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1500 REM " --- New record initializations"
1510 LET A1$(4,4)="EAEA",A1$(8,1)="N",A1$(12,1)="N",A1$(13,1)="Y"
1520 LET A1$(17,1)="I",A1$(18,1)="A",A1$(19,3)="DDD"
1590 GOSUB 5000
1600 REM " --- Product type"
1610 LET V4$=V5$+"Product Type (<F3>=Lookup)",I0=0
1620 LET V0$="S",V1$="C",V2$=A1$(1,3),V3$="",V0=3,V1=26,V2=4
1630 GOSUB 7000
1640 ON V3 GOTO 1660,1600,3900,1650,3900
1650 CALL "IVC.LK",1,V1,V2,V$
1660 LET A1$(1,3)=V$,PRODUCT$(1)=""
1665 IF POS(A1$(1,3)="   ")=1 THEN GOTO 1680
1670 FIND (IVM10_DEV,KEY=N0$+"A"+A1$(1,3),DOM=1600)IOL=IVM10A
1675 LET PRODUCT$(1)=X0$(7,20)
1680 PRINT @(V1,V2),A1$(1,3)," ",PRODUCT$
1690 GOTO 3900
1700 REM " --- Item Class"
1710 LET V4$=V5$+"Item Class (<F3>=Lookup)"
1720 LET V0$="S",V1$="C",V2$=A1$(14,3),V3$="",V0=3,V1=26,V2=5
1730 GOSUB 7000
1740 ON V3 GOTO 1760,1700,3900,1750,3900
1750 CALL "IVC.LK",5,V1,V2,V$
1760 LET A1$(14,3)=V$,CLASS$(1)=""
1765 IF POS(A1$(14,3)="   ")=1 THEN GOTO 1780
1770 FIND (IVM13_DEV,KEY=N0$+A1$(14,3),DOM=1700)IOL=IVM13A
1775 LET CLASS$(1)=X1$(1,20)
1780 PRINT @(V1,V2),A1$(14,3)," ",CLASS$
1790 GOTO 3900
1800 REM " --- Buyer Code"
1810 LET V4$=V5$+"Buyer Code (<F3>=Lookup)",V$=""
1820 LET V0$="S",V1$="C",V2$=A1$(9,3),V3$="",V0=3,V1=26,V2=6
1825 IF PO$<>"Y" THEN GOTO 1860
1830 GOSUB 7000
1840 ON V3 GOTO 1860,1800,3900,1850,3900
1850 CALL "IVC.LK",4,V1,V2,V$
1860 LET A1$(9,3)=V$,BUYER$(1)=""
1865 IF POS(A1$(9,3)="   ")=1 THEN GOTO 1880
1870 FIND (IVM10_DEV,KEY=N0$+"F"+A1$(9,3),DOM=1800)IOL=IVM10F
1875 LET BUYER$(1)=X0$(7,20)
1880 PRINT @(V1,V2),A1$(9,3)," ",BUYER$
1890 GOTO 3900
1900 REM " --- Distribution Code"
1910 LET V4$=V5$+"Distribution Code (<F3>=Lookup)",V$=""
1920 LET V0$="S",V1$="C",V2$=A1$(23,2),V3$="",V0=2,V1=26,V2=7
1925 IF DI$<>"Y" THEN GOTO 1960
1930 GOSUB 7000
1940 ON V3 GOTO 1960,1900,3900,1950,3900
1950 GOSUB 6300
1960 LET A1$(23,2)=V$,DISTRIB$(1)=""
1965 IF DI$<>"Y" OR POS(A1$(23,2)="  ")=1 THEN GOTO 1980
1970 FIND (ARM10_DEV,KEY=N0$+"D"+A1$(23,2),DOM=1900)IOL=ARM10D
1975 LET DISTRIB$(1)=X0$(6,20)
1980 PRINT @(V1,V2),A1$(23,2),"  ",DISTRIB$
1990 GOTO 3900
2000 REM " --- Sale UM"
2010 LET V4$=V5$+"Sale Unit Of Measure"
2020 LET V0$="S",V1$="C",V2$=A1$(4,2),V3$="",V0=2,V1=26,V2=8
2030 GOSUB 7000
2040 ON V3 GOTO 2050,1800,3900,2000,3900
2050 LET A1$(4,2)=V$
2090 GOTO 3900
2100 REM " --- Purchase UM"
2110 LET V4$=V5$+"Purchase Unit Of Measure"
2120 LET V0$="S",V1$="C",V2$=A1$(6,2),V3$="",V0=2,V1=26,V2=9
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2150 LET A1$(6,2)=V$
2190 GOTO 3900
2200 REM " --- Lotted/Serialized"
2210 LET V4$=V5$+"Lotted/Serialized Code (Y/N/Blank)",V$=" "
2220 LET V0$="S",V1$="C",V2$=A1$(12,1),V3$="YN ",V0=1,V1=26,V2=10
2225 IF LS$<>"Y" THEN GOTO 2250
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 LET A1$(12,1)=V$
2280 PRINT @(V1,V2),A1$(12,1)
2290 GOTO 3900
2300 REM " --- Inventoried?"
2310 LET V4$=V5$+"Inventoried Code (Y/N/Blank)"
2320 LET V0$="S",V1$="C",V2$=A1$(13,1),V3$="YN ",V0=1,V1=26,V2=11
2325 IF LS$<>"Y" THEN GOTO 2350
2330 GOSUB 7000
2340 ON V3 GOTO 2350,2300,3900,2300,3900
2350 LET A1$(13,1)=V$
2380 PRINT @(V1,V2),A1$(13,1)
2390 GOTO 3900
2400 REM " --- Stocking Level"
2410 LET V4$=V5$+"Stocking Level (I=Item/W=Warehouse/Blank)"
2420 LET V0$="S",V1$="C",V2$=A1$(17,1),V3$="IW ",V0=1,V1=72,V2=4
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 LET A1$(17,1)=V$
2490 GOTO 3900
2500 REM " --- Taxable?"
2510 LET V4$=V5$+"Taxable Flag (Y/N/Blank)"
2520 LET V0$="S",V1$="C",V2$=A1$(8,1),V3$="YN ",V0=1,V1=72,V2=5
2530 GOSUB 7000
2540 ON V3 GOTO 2550,2500,3900,2500,3900
2550 LET A1$(8,1)=V$
2590 GOTO 3900
2600 REM " --- Item Type"
2605 LET V4$=V5$+"Item Type (<F3>=Lookup)"
2610 LET V0$="S",V1$="C",V2$=A1$(25,3),V3$="",V0=3,V1=72,V2=6
2615 GOSUB 7000
2620 ON V3 GOTO 2630,2600,3900,2625,3900
2625 CALL "IVC.LK",6,V1,V2,V$
2630 LET V$=V$+"   ",A1$(25,3)=V$(1,3)
2635 IF POS(A1$(25,3)="   ")=1 THEN GOTO 2645
2640 FIND (IVM15_DEV,KEY=N0$+A1$(25,3),DOM=2600)
2645 PRINT @(V1,V2),A1$(25,3),
2650 GOTO 3900
2700 REM " --- ABC Code"
2710 LET V4$=V5$+"ABC Code",V3$="ABCDEFGHIJKLMNOPQRSTUVWXYZ "
2720 LET V0$="S",V1$="C",V2$=A1$(18,1),V3$="",V0=1,V1=72,V2=7
2730 GOSUB 7000
2740 ON V3 GOTO 2750,2700,3900,2700,3900
2750 LET A1$(18,1)=V$
2790 GOTO 3900
2800 REM " --- EOQ Code"
2820 LET V0$="S",V1$="C",V2$=A1$(19,1),V3$="DNE ",V4$=DNE$,V0=1,V1=72,V2=8
2830 GOSUB 7000
2840 ON V3 GOTO 2850,2800,3900,2800,3900
2850 LET A1$(19,1)=V$
2890 GOTO 3900
2900 REM " --- Order Point Code"
2920 LET V0$="S",V1$="C",V2$=A1$(20,1),V3$="DNE ",V4$=DNE$,V0=1,V1=72,V2=9
2930 GOSUB 7000
2940 ON V3 GOTO 2950,2900,3900,2900,3900
2950 LET A1$(20,1)=V$
2990 GOTO 3900
3000 REM " --- Safety Stock Code"
3020 LET V0$="S",V1$="C",V2$=A1$(21,1),V3$="DNE ",V4$=DNE$,V0=1,V1=72,V2=10
3030 GOSUB 7000
3040 ON V3 GOTO 3050,3000,3900,3000,3900
3050 LET A1$(21,1)=V$
3090 GOTO 3900
3100 REM " --- GL Accounts"
3105 IF GL$="N" THEN GOTO 3900
3110 LET X=I0-14,V4$=V5$+"GL "+GLV4$[X]+" Account (<F1>=None/<F3>=Lookup)"
3115 LET P=((X-1)*G[4])+1,V0$="M",V1$="C",V2$=A2$(P,G[3]),V3$=G1$
3120 LET V0=G[4],V1=31,V2=14+X
3130 GOSUB 7000
3140 ON V3 GOTO 3200,3150,3900,3180,3900
3150 LET A2$(P,G[4])=""
3155 PRINT @(V1,V2),'CL',
3160 GOTO 3900
3180 CALL "SYC.LG",V$
3200 REM 
3210 IF V$="" THEN GOTO 3150
3220 DIM B0$(2+G[4]),B1$(GW)
3230 LET B0$(1)=N0$+V$
3240 FIND (GLM01_DEV,KEY=B0$,DOM=3100)IOL=GLM01A
3250 PRINT @(V1,V2),FNF$(B0$(3,G[3]),G5$)," ",B1$,'CL',
3260 LET A2$(P,G[4])=B0$(3)
3290 GOTO 3900
3900 REM " --- Next field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1600,1700,1800,1900,2000,2100,2200,2300,2400,2500,2600,2700,2800,2900,3000,3100,3100,3100,3100,3100,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="S",V1$="KC",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write"
4110 WRITE (IVS10_DEV,KEY=A0$)IOL=IVS10D
4190 GOTO 4900
4200 REM " ---- Remove"
4210 REMOVE (IVS10_DEV,KEY=A0$,DOM=4290)
4290 GOTO 4900
4900 REM " --- Run menu overlay"
4990 RUN "IVP.AA"
5000 REM " --- Display record"
5010 LET PRODUCT$(1)="",CLASS$(1)="",BUYER$(1)="",DISTRIB$(1)=""
5020 FIND (IVM10_DEV,KEY=N0$+"A"+A1$(1,3),DOM=5030)IOL=IVM10A
5025 LET PRODUCT$=X0$(7,20)
5030 FIND (IVM10_DEV,KEY=N0$+"F"+A1$(9,3),DOM=5040)IOL=IVM10F
5035 LET BUYER$=X0$(7,20)
5040 FIND (IVM13_DEV,KEY=N0$+A1$(14,3),DOM=5050)IOL=IVM13A
5045 LET CLASS$=X1$(1,20)
5050 IF DI$<>"Y" THEN GOTO 5090
5060 FIND (ARM10_DEV,KEY=N0$+"D"+A1$(23,2),DOM=5090)IOL=ARM10D
5065 LET DISTRIB$=X0$(6,20)
5090 PRINT @(26,4),A1$(1,3)," ",PRODUCT$,@(72),A1$(17,1),@(26,5),A1$(14,3)," ",CLASS$,@(72),A1$(8,1),@(26,6),A1$(9,3)," ",BUYER$,@(72),A1$(25,3),@(26,7),A1$(23,2),"  ",DISTRIB$,@(72),A1$(18,1),@(26,8),A1$(4,2),@(72),A1$(19,1),@(26,9),A1$(6,2),@(72),A1$(20,1),@(26,10),A1$(12,1),@(72),A1$(21,1),@(26,11),A1$(13,1),
5100 REM " --- GL accounts"
5110 IF DI$<>"N" THEN GOTO 5190
5120 IF GL$<>"Y" THEN GOTO 5190
5130 FOR X=1 TO 5
5140 DIM B0$(2+G[4]),B1$(GW)
5145 LET P=(X-1)*G[4]+1
5150 IF FNP$(A2$(P,G[4]))="" THEN GOTO 5180
5160 LET B0$(1)=N0$+A2$(P,G[4])
5165 FIND (GLM01_DEV,KEY=B0$,DOM=5180)IOL=GLM01A
5170 PRINT @(31,X+14),FNF$(B0$(3,G[3]),G5$)," ",B1$,'CL',
5180 NEXT X
5190 RETURN 
6300 REM " --- Distribution Code Lookup"
6310 LET RECORD$="D",TITLE$="Distribution Codes"
6330 LET DESCRIPTION$[0]="Code",CHANNEL=ARM10_DEV,FIELD[1]=1
6340 LET POSITION[0]=4,LENGTH[0]=2,POSITION[1]=6,LENGTH[1]=20
6400 REM " --- Call Windowed Lookup"
6410 LET FIELD[0]=1,DESCRIPTION$[1]="Description"
6440 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,ROW=V2+2,V$="SKIP"
6450 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

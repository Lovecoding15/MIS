0010 REM "POR - PO Receipt Cost Variance Report
0020 REM "Program POR.OA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=10
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="APM-01",FILES$[2]="IVM-01",FILES$[3]="IVM-10"
0130 LET FILES$[4]="IVM-14",FILES$[5]="POM-02",FILES$[6]="POT-04"
0135 LET FILES$[7]="POT-14",FILES$[8]="POT-44",FILES$[9]="POW-18"
0140 LET FILES$[10]="SYS-01"
0145 LET OPTIONS$[9]="C"
0160 CALL "SYC.DA",1,1,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET APM01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM10_DEV=CHANNELS[3]
0180 LET IVM14_DEV=CHANNELS[4],POM02_DEV=CHANNELS[5],POT04_DEV=CHANNELS[6]
0185 LET POT14_DEV=CHANNELS[7],POT44_DEV=CHANNELS[8],POW18_DEV=CHANNELS[9]
0190 LET SYS01_DEV=CHANNELS[10]
0200 REM " --- IOLists
0240 APM01A: IOLIST X0$,X1$(1)
0250 IVM10C: IOLIST X1$(1)
0260 POT14A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1),B4$(1),B5$,B6$,B[ALL]
0270 POM02A: IOLIST S0$(1),S1$(1)
0280 POW18A: IOLIST WFKEY$(1),WFPOAMT
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 APS01A: IOLIST U0$,VENDIMASK$,U2$,U3$,U4$,VENDOMASK$
0330 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0400 REM " --- Parameters
0405 DIM INFO$[20],DESCLEN[3],PARAMS[3]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO",WHEN$=F0$(7,3)
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 LET VENDLEN=NUM(U2$(1,2))
0435 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET IVPRECISION=NUM(I2$(5,1)),ITEMLEN=NUM(I2$(1,2))
0445 FOR X=1 TO 3
0450 LET DESCLEN[X]=NUM(I2$((X-1)*2+9,2))
0460 LET PARAMS[X]=DESCLEN[X]
0465 NEXT X
0475 LET PARAMS[0]=ITEMLEN
0500 REM " --- Init Data
0510 PRECISION IVPRECISION
0520 DIM IV_CHANS[14]
0530 LET IV_CHANS[1]=IVM01_DEV,IV_CHANS[14]=IVM14_DEV
0540 CALL "SYC.RM",M1$,M1,10,14
0550 CALL "SYC.RM",M2$,M2,9,12
0560 LET MPCNT$="-##0.00%",MPCNT=LEN(MPCNT$)
0570 DIM VENDFNAME$(30),VENDTNAME$(30),WHSEFNAME$(24)
0580 DIM ITEMFDESC$(30),ITEMTDESC$(30)
0700 REM " --- Background
0710 FOR X=4 TO 18 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(29,3),"Warehouse:",@(24,5),"Beginning Item:",@(27,7),"Ending Item:",@(22,9),"Beginning Vendor:",@(25,11),"Ending Vendor:",@(16,13),"Beginning Receipt Date:",@(19,15),"Ending Receipt Date:",@(11,17),"Minimum Variance Percentage:"
0760 GOSUB REFRESH
1000 REM " --- Warehouse
1010 LET V0$="S",V1$="",V2$=WHSEFOR$,V3$="",V4$="Enter A Valid Warehouse ID  (<F1>=All/<F3>=Lookup)",V0=2,V1=40,V2=3,I0=0
1020 GOSUB 7000
1030 ON V3 GOTO 1090,1040,1190,1070,4000
1040 LET WHSEFOR$=""
1050 PRINT @(V1,V2),'CL',@(V1,V2),"All",
1060 GOTO 1190
1070 CALL "IVC.LK",3,V1,V2,V$
1080 IF V$="" THEN GOTO 1000
1090 IF V$="" THEN GOTO 1040
1100 DIM X1$(160)
1110 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=1000)IOL=IVM10C
1120 LET WHSEFOR$=V$,WHSEFNAME$(1)=X1$(6,24)
1130 PRINT @(V1,V2),FNF$(WHSEFOR$,"00")," ",WHSEFNAME$(1,24),'CL',
1190 GOTO 3900
1200 REM " --- Starting Item
1205 ITEMFROM: 
1220 DIM ITEM$[6],ITEM[12]
1230 LET ACTION$="F",DEFAULT$=ITEMFROM$,V1=40,V2=5
1240 LET V4$="Enter A Valid Item Number"
1250 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1260 IF V3=2 THEN GOTO NEXT_ITEMFROM
1270 IF V3=4 THEN GOTO 4000
1280 IF V3=1 OR ITEM$[0]="" THEN PRINT @(V1,V2),'CL',"First",; LET ITEM$[0]="",ITEMFROM$=""; GOTO NEXT_ITEMFROM
1290 LET ITEMFROM$=ITEM$[0],ITEMFDESC$(1)=ITEM$[1]+FILL(28)
1300 PRINT @(V1,V2),ITEMFROM$(1,ITEMLEN)," ",ITEMFDESC$(1,28),
1310 NEXT_ITEMFROM: 
1320 GOTO 3900
1400 REM " --- Ending Item
1405 ITEMTHRU: 
1420 DIM ITEM$[6],ITEM[12]
1430 LET ACTION$="L",DEFAULT$=ITEMTHRU$,V1=40,V2=7,V$=""
1440 LET V4$="Enter A Valid Item Number"
1450 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1460 IF V3=2 THEN GOTO NEXT_ITEMTHRU
1470 IF V3=4 THEN GOTO 4000
1480 IF V3=1 OR ITEM$[0]="" THEN PRINT @(V1,V2),'CL',"Last",; LET ITEM$[0]="",ITEMTHRU$=""; GOTO NEXT_ITEMTHRU
1490 IF ITEM$[0]<ITEMFROM$ THEN GOTO 1400
1500 LET ITEMTHRU$=ITEM$[0],ITEMTDESC$(1)=ITEM$[1]+FILL(28)
1510 PRINT @(V1,V2),ITEMTHRU$(1,ITEMLEN)," ",ITEMTDESC$(1,28),
1520 NEXT_ITEMTHRU: 
1530 GOTO 3900
1600 REM " --- Beginning Vendor
1610 LET V0$="M",V1$="KCE",V2$=VENDFROM$,V3$=VENDIMASK$,V4$="Enter A Valid Vendor Number (<F1>=First/<F3>=Lookup)",V0=6,V1=40,V2=9
1620 GOSUB 7000
1630 ON V3 GOTO 1690,1640,1740,1670,4000
1640 LET VENDFROM$=""
1650 PRINT @(V1,V2),'CL',@(V1,V2),"First",
1660 GOTO 1740
1670 CALL "SYC.LV",V$
1680 IF V$="" THEN GOTO 1600
1690 IF V$="" THEN GOTO 1640
1700 DIM X1$(195)
1710 FIND (APM01_DEV,KEY=N0$+V$,DOM=1600)IOL=APM01A
1720 LET VENDFROM$=V$,VENDFNAME$(1)=X1$(1,30)
1730 PRINT @(V1,V2),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
1740 GOTO 3900
1800 REM " --- Ending Vendor
1810 LET V0$="M",V1$="KCE",V2$=VENDTHRU$,V3$=VENDIMASK$,V4$="Enter A Valid Vendor Number (<F1>=Last/<F3>=Lookup)",V0=6,V1=40,V2=11
1820 GOSUB 7000
1830 ON V3 GOTO 1890,1840,1950,1870,4000
1840 LET VENDTHRU$=""
1850 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
1860 GOTO 1950
1870 CALL "SYC.LV",V$
1880 IF V$="" THEN GOTO 1800
1890 IF V$="" THEN GOTO 1840
1900 IF VENDFROM$<>"" THEN IF V$<VENDFROM$ THEN GOTO 1800
1910 DIM X1$(195)
1920 FIND (APM01_DEV,KEY=N0$+V$,DOM=1800)IOL=APM01A
1930 LET VENDTHRU$=V$,VENDTNAME$(1)=X1$(1,30)
1940 PRINT @(V1,V2),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
1950 GOTO 3900
2000 REM " --- Beginning Receipt Date
2010 LET V0$="D",V1$="6N",V2$=DATEFROM$,V3$="",V4$="",V0=6,V1=40,V2=13
2020 GOSUB 7000
2030 ON V3 GOTO 2050,2040,2070,2000,4000
2040 LET V$=""
2050 IF V$="" THEN PRINT @(V1,V2),"First   ",
2060 LET DATEFROM$=V$
2070 GOTO 3900
2200 REM " --- Ending Receipt Date
2210 LET V0$="D",V1$="6N",V2$=DATETHRU$,V3$="",V4$="",V0=6,V1=40,V2=15
2220 GOSUB 7000
2230 ON V3 GOTO 2250,2240,2280,2250,4000
2240 LET V$=""
2250 IF V$="" THEN PRINT @(V1,V2),"Last    ",
2260 IF V$<>"" THEN IF V$<DATEFROM$ THEN GOTO 2200
2270 LET DATETHRU$=V$
2280 GOTO 3900
2400 REM " --- Minimum variance percentage
2410 LET V0$="N",V1$="C",V2$=STR(MINVARI),V3$=MPCNT$,V0=3,V1=40,V2=17
2420 LET V4$="Enter The Minimum Variance Percentage To Report"
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,4000
2450 IF V<0 THEN GOTO 2400
2460 LET MINVARI=V
2470 PRINT @(V1,V2),MINVARI:MPCNT$
2490 GOTO 3900
3900 REM " --- Next Field
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,2200,2400,4000
4000 REM " --- Correct
4010 GOSUB REFRESH
4020 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run print overlay
4120 CALL "SYC.GA",7,1,"","",STATUS
4130 IF STATUS THEN GOTO 8000
4150 IF ITEMFROM$="" THEN LET ITEMFDESC$=FILL(30)
4160 IF ITEMTHRU$="" THEN LET ITEMTDESC$=FILL(30)
4170 IF VENDFROM$="" THEN LET VENDFNAME$=FILL(30)
4180 IF VENDTHRU$="" THEN LET VENDTNAME$=FILL(30)
4185 GOSUB ACCUMPOAMTS
4190 RUN "POR.OB"
5000 REM " --- Refresh screen data
5010 REFRESH: 
5020 PRINT 'CF',
5030 IF WHSEFOR$="" THEN PRINT @(40,3),"All",'CL', ELSE PRINT @(40,3),WHSEFOR$," ",WHSEFNAME$,'CL',
5050 IF ITEMFROM$="" THEN PRINT @(40,5),"First" ELSE PRINT @(40,5),ITEMFROM$(1,ITEMLEN)," ",ITEMFDESC$(1,MIN(79-V1-ITEMLEN-1,30)),'CL',
5060 IF ITEMTHRU$="" THEN PRINT @(40,7),"Last" ELSE PRINT @(40,7),ITEMTHRU$(1,ITEMLEN)," ",ITEMTDESC$(1,MIN(79-V1-ITEMLEN-1,30)),'CL',
5090 IF VENDFROM$="" THEN PRINT @(40,9),"First",'CL', ELSE PRINT @(40,9),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5100 IF VENDTHRU$="" THEN PRINT @(40,11),"Last",'CL', ELSE PRINT @(40,11),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5120 IF DATEFROM$="" THEN PRINT @(40,13),"First   ", ELSE PRINT @(40,13),FNB6$(DATEFROM$)
5130 IF DATETHRU$="" THEN PRINT @(40,15),"Last    ", ELSE PRINT @(40,15),FNB6$(DATETHRU$)
5140 PRINT @(40,17),MINVARI:MPCNT$
5190 RETURN 
6000 REM " --- Accumulate PO Amt (POT-14)
6010 ACCUMPOAMTS: 
6020 CALL "SYC.NB","Summing PO Amounts",17,COLUMN
6030 DIM B0$(25),B1$(48),B2$(32),B3$(22),B4$(40),B[12],WFKEY$(18)
6040 READ (POT44_DEV,KEY=N0$+VENDFROM$+DATEFROM$,DOM=READSORT)
6050 READSORT: 
6060 LET WFPOAMT=0
6070 LET K$=KEY(POT44_DEV,END=DONESORT)
6080 IF POS(N0$=K$)<>1 THEN GOTO DONESORT
6090 READ (POT44_DEV)
6100 LET VENDOR$=K$(3,6),RCPTDATE$=K$(9,6)
6110 PRINT @(COLUMN,11),K$(1,2)," ",K$(3,6)," ",K$(9,7)
6120 IF VENDTHRU$<>"" AND VENDOR$>VENDTHRU$ THEN GOTO DONESORT
6130 IF DATETHRU$<>"" AND RCPTDATE$>DATETHRU$ THEN GOTO NEXTVENDOR
6140 GOTO FINDPOT14
6150 NEXTVENDOR: 
6160 READ (POT44_DEV,KEY=K$(1,8)+$FF$,DOM=NEXTSORT)
6170 FINDPOT14: 
6180 FIND (POT14_DEV,KEY=K$(1,8)+K$(15,17),DOM=NEXTSORT)IOL=POT14A
6185 LET WHSE$=B3$(1,2),ITEM$=B3$(3,20)
6190 REM " --- Valid Whse/Item?
6200 IF WHSEFOR$<>"" THEN IF WHSE$<>WHSEFOR$ THEN GOTO NEXTSORT
6210 IF ITEMFROM$<>"" THEN IF ITEM$<ITEMFROM$ THEN GOTO NEXTSORT
6220 IF ITEMTHRU$<>"" THEN IF ITEM$>ITEMTHRU$ THEN GOTO NEXTSORT
6230 REM " --- Valid line type (ie S or N)?
6240 DIM S0$(4),S1$(32)
6250 LET LINETYPE$="N"
6260 FIND (POM02_DEV,KEY=B0$(1,2)+B1$(1,2),DOM=NEXTSORT)IOL=POM02A
6270 LET LINETYPE$=S1$(21,1)
6280 IF POS(LINETYPE$="SN")=0 THEN GOTO NEXTSORT
6290 REM " --- Do POW-18
6300 LET WFKEY$=B0$(1,15)+B0$(23,3)
6310 READ (POW18_DEV,KEY=WFKEY$,DOM=6330)IOL=POW18A
6320 GOTO NEXTSORT; REM "Only write total line item value once 
6330 PRECISION 2
6340 LET UNITCOST=B[1],QTYORD=B[3]
6350 LET WFPOAMT=WFPOAMT+(UNITCOST*QTYORD)
6360 PRECISION IVPRECISION
6370 WRITE (POW18_DEV,KEY=WFKEY$)IOL=POW18A
6380 NEXTSORT: 
6390 GOTO READSORT
6400 DONESORT: 
6410 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

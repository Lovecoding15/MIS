0010 REM "OPR - On Demand Print Receipt"
0020 REM "Program OPR.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open Receipt Printer"
0110 LET PRTR_DEV=UNT
0120 CALL "SYC.GA",PRTR_DEV,0,"Cash Receipt",R2$(27,2)+FORMAT$,STATUS
0125 IF STATUS THEN GOTO 9900
0130 IF R2$(27,2)<>"PF" THEN PRINT (PRTR_DEV,ERR=0641)ATH(FNP$(R1$(1,8))),
0200 REM " --- IOLists"
0210 ARE03A: IOLIST A0$,A[ALL]
0220 ARE13A: IOLIST W0$,W1$,W[ALL]
0230 ARM10E: IOLIST Y0$(1)
0240 IVM01A: IOLIST *,X2$(1)
0500 REM " --- Initialize Data"
0520 LET J6$="####-",J7$="#,###.00-"
0530 LET T0=0
0700 REM " --- Print Receipt Heading"
0710 PRINT (PRTR_DEV)""
0720 PRINT (PRTR_DEV)FILL(20-INT(LEN(N1$)/2)),N1$
0730 PRINT (PRTR_DEV)""
0740 PRINT (PRTR_DEV)"  Ref: ",A0$(11,7),"   ",FNE$(Q$),"   ",A0$(96,3),"   ",FNB$(A0$(24,3)),'LF'
0750 PRINT (PRTR_DEV)'LF',"Qty   Desc                    Extension",'LF'
0900 REM " --- Init Invoice Detail File"
0910 READ (ARE13_DEV,KEY=A0$(1,17),DOM=1000)
1000 REM " --- Get Invoice Detail"
1020 LET K$=KEY(ARE13_DEV,END=3000)
1040 IF K$(1,17)<>A0$(1,17) THEN GOTO 3000
1060 READ (ARE13_DEV)IOL=ARE13A
1100 REM " --- Get Line Type"
1120 DIM Y0$(32)
1140 FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=1150)IOL=ARM10E
1200 REM " --- Get Item Description
1220 IF POS(Y0$(25,1)=" SDP")=0 THEN GOTO 2000
1240 DIM X2$(DESCLEN)
1260 FIND (IVM01_DEV,KEY=N0$+W0$(33,20),DOM=1270)IOL=IVM01A
1280 LET ITEM_DESC$=FNITEM$(X2$,I[4],I[5],I[6])
2000 REM " --- Print Receipt Detail"
2010 DIM R9$(40)
2020 IF POS(Y0$(25,1)="MNO")<>0 THEN LET R9$(7,24)=W1$
2030 IF POS(Y0$(25,1)=" SDP")<>0 THEN LET R9$(7,24)=ITEM_DESC$
2040 IF POS(Y0$(25,1)=" SDPN")<>0 THEN LET R9$(1,5)=STR(W[4]:J6$)
2050 IF POS(Y0$(25,1)="M")=0 THEN LET R9$(32,8)=STR(W[6]:J7$)
2100 PRINT (PRTR_DEV)R9$
2200 REM " --- Accumulate Total"
2220 LET T0=T0+W[6]
2900 REM " --- Loop Back For Next Detail Line"
2910 GOTO 1000
3000 REM " --- Print Receipt Total"
3010 PRINT (PRTR_DEV)""
3020 PRINT (PRTR_DEV)FILL(20),"Subtotal:  ",T0:J7$
3030 IF A[2]<>0 THEN PRINT (PRTR_DEV)FILL(20),"Discount:  ",A[2]:J7$
3040 IF A[0]<>0 THEN PRINT (PRTR_DEV)FILL(20),"Tax     :  ",A[0]:J7$
3050 IF A[1]<>0 THEN PRINT (PRTR_DEV)FILL(20),"Freight :  ",A[1]:J7$
3060 PRINT (PRTR_DEV)FILL(20),"Total   :  ",T0+A[0]-A[2]+A[1]:J7$
3070 IF Y9$(21,1)="$" THEN PRINT (PRTR_DEV)FILL(20),"Tendered:  ",Z[1]:J7$
3080 LET T9=Z[1]-(T0+A[0]-A[2]+A[1])
3090 IF Y9$(21,1)="$" THEN PRINT (PRTR_DEV)FILL(20),"Change  :  ",T9:J7$
3100 PRINT (PRTR_DEV)'LF',
3110 LET X=POS("  "=Y7$(7,20)); LET Y7$=Y7$(7,X)
3120 PRINT (PRTR_DEV)Y7$,"  ",Z0$(31,16),
3130 PRINT (PRTR_DEV)""
3140 PRINT (PRTR_DEV)"               Thank You ",
3150 PRINT (PRTR_DEV)'LF','LF','LF','LF','LF','LF'
4000 REM " --- Set Invoice Print Flag"
4020 LET A0$(68,1)="Y"
4030 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
4035 IF R2$(27,2)="PF" THEN GOTO 4090
4050 GOSUB 6000
4100 REM " --- Return To Invoice Detail Entry"
4120 CLOSE (PRTR_DEV,ERR=4150)
4150 RUN "OPE.CE"
6000 REM " --- Open Cash Box"
6005 IF FNP$(R1$(17,2))="" THEN GOTO 6090
6010 LET CASHBOX_DEV=UNT
6015 OPEN (CASHBOX_DEV,ERR=6090)R1$(17,2)
6020 FOR X=1 TO POS(" "<>R1$(1,8),-1) STEP 2
6025 IF R1$(X,2)="1B" THEN PRINT (CASHBOX_DEV)'ES', ELSE PRINT (CASHBOX_DEV)FNP$(ATH(R1$(X,2))),
6030 NEXT X
6035 PRINT (CASHBOX_DEV)
6040 FOR I=1 TO NUM(R1$(29,4))
6045 PRINT (CASHBOX_DEV)FNP$(ATH(R1$(21,POS(" "<>R1$(21,8),-1))))
6050 NEXT I
6060 FOR X=1 TO POS(" "<>R1$(9,8),-1) STEP 2
6065 IF R1$(8+X,2)="1B" THEN PRINT (CASHBOX_DEV)'ES', ELSE PRINT (CASHBOX_DEV)FNP$(ATH(R1$(8+X,2))),
6070 NEXT X
6078 PRINT (CASHBOX_DEV)
6080 CLOSE (CASHBOX_DEV,ERR=6090)
6090 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8050 DEF FNE$(Q$)=DATE(0:"%hz:%mz %p")
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return With Print Flag = NO"
9210 SETERR 9000
9220 LET A0$(68,1)="N"
9290 GOTO 4200
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 GOTO 9200
9999 END

0010 REM "IVM - Item Type Maintenance"
0020 REM "Program IVM.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="IVM-15"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],IVM15_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 IVM15A: IOLIST A0$(1),A1$(1)
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0500 REM " --- Initializations"
0510 DIM A0$(5),A1$(32)
0590 LET A0$(1)=N0$
0700 REM " --- Background"
0720 PRINT 'SB',@(3,3),"Item Type:",@(1,4),"Description:",'SF',
0900 REM " --- Position File"
0990 READ (IVM15_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Item Class"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(IVM15_DEV,END=1050)
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3)
1050 LET V0$="S",V1$="KE",V3$="",V4$="Enter A Valid Item Type (<F3>=Lookup)",V0=3,V1=14,V2=3
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN CALL "IVC.LK",6,V1,V2,V$
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(3)=V$
1200 REM " --- Retrieve record"
1210 FIND (IVM15_DEV,KEY=A0$,DOM=1400)IOL=IVM15A
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Initialize new record"
1510 DIM A1$(32)
2000 REM " --- Description"
2020 LET V0$="S",V1$="",V2$=A1$(1,20),V3$="",V0=20,V1=14,V2=4,I0=0
2030 LET V4$="Enter The Description For This Item Type"
2040 GOSUB 7000
2050 ON V3 GOTO 2060,2000,3900,2000,3900
2060 LET A1$(1,20)=V$
2090 GOTO 3900
3900 REM " --- Next Field"
3902 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,3990
3990 LET I0=K0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record"
4110 WRITE (IVM15_DEV,KEY=A0$)IOL=IVM15A
4190 GOTO 1000
4200 REM " --- Remove record"
4210 REMOVE (IVM15_DEV,KEY=A0$,DOM=4220)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4900,9900
4900 REM " --- Report Initializations"
4905 LET PROGRAM$="",NUMBER=1,HEADINGS=3,WIDTH=80,CHANNEL=IVM15_DEV
4915 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4920 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4925 DIM HEADINGS$[HEADINGS]
4930 LET FIELD$[0]="Type",FIELD[0]=1,POSITION[0]=3
4940 LET LENGTH[0]=3,TYPE$[0]="S",MASK$[0]=""
4945 LET FIELD$[1]="Description",FIELD[1]=2,POSITION[1]=1
4950 LET LENGTH[1]=20,TYPE$[1]="S",MASK$[1]="",TOTAL$="Item Types"
4980 LET HEADINGS$[0]=N1$,HEADINGS$[1]=F5$+" Listing",RECORD$=""
4990 RUN "SYR.AA"
5000 REM " --- Display"
5010 PRINT @(14,3),A0$(3,3),@(14,4),A1$(1,20),
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

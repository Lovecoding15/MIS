0010 REM "POC - Purchase Order Code Lookup"
0020 REM "Program POC.LK"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0042 REM "ACTION    : Indicates record to lookup (input)"
0043 REM "            1=Line Code"
0044 REM "            2=Message Code"
0045 REM "            3=Rejection Code"
0050 REM "COLUMN    : V1 Input field column (input)"
0055 REM "ROW       : V2 Input field row (input)"
0060 REM "SELECTION$: Selected key (returned)"
0065 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER ACTION,COLUMN,ROW,SELECTION$
0100 REM " --- Open/Lock Files"
0110 LET FILES=1
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="POM-02"
0130 IF ACTION=2 THEN LET FILES$[1]="POM-04"
0135 IF ACTION=3 THEN LET FILES$[1]="POM-03"
0140 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET CHANNEL=CHANNELS[1]
0400 REM " --- Parameters"
0410 LET NUMBER=4,RECORD$=""
0500 REM " --- Initializations"
0510 DIM DESCRIPTION$[NUMBER],LENGTH[NUMBER],POSITION[NUMBER],FIELDS[NUMBER]
0550 LET FIELDS[0]=1,POSITION[0]=3,LENGTH[0]=3,DESCRIPTION$[0]="Code"
0560 LET FIELDS[1]=2,POSITION[1]=1,LENGTH[1]=20,DESCRIPTION$[1]="Description"
1000 REM " --- Adjust fields based on ACTION"
1090 ON ACTION GOTO 9900,1100,1200,1300,9900
1100 REM " --- Line Codes"
1110 LET TITLE$="Purchase Order Line Codes",LENGTH[0]=2
1120 LET FIELDS[2]=2,POSITION[2]=21,LENGTH[2]=1,DESCRIPTION$[2]="Type"
1130 LET FIELDS[3]=2,POSITION[3]=22,LENGTH[3]=1,DESCRIPTION$[3]="Drop"
1140 LET FIELDS[4]=2,POSITION[4]=23,LENGTH[4]=1,DESCRIPTION$[4]="Lead"
1190 GOTO 3000
1200 REM " --- Message Codes"
1210 LET NUMBER=1,TITLE$="Standard Message Codes"
1290 GOTO 3000
1300 REM " --- Rejection Codes"
1310 LET NUMBER=1,TITLE$="Rejection Codes",LENGTH[1]=30
1390 GOTO 3000
3000 REM " --- Call standard windowed lookup"
3010 LET WIN_Y=ROW+1,WIN_X=77-LENGTH[0]-LENGTH[1]-NUMBER*4
3020 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELDS[ALL],POSITION[ALL],LENGTH[ALL],WIN_X,WIN_Y,SELECTION$
4000 REM " --- All done"
4090 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9910 CLOSE (CHANNEL,ERR=9911)
9950 EXIT 
9999 END

0010 REM "ARR - Monthly Sales Register"
0020 REM "Program OPR.ZA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=12
0115 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="ART-03",FILES$[2]="ART-13",FILES$[3]="ART-33"
0125 LET FILES$[4]="ARM-01",FILES$[5]="ARM-10",FILES$[6]="ARM-04"
0130 LET FILES$[7]="ARM-02",FILES$[8]="SYS-01",FILES$[9]="ARW-14"
0135 LET FILES$[10]="IVM-02",FILES$[11]="GLM-01",FILES$[12]="GLW-04"
0137 LET OPTIONS$[9]="CL",OPTIONS$[12]="CL"
0140 CALL "SYC.DA",1,1,12,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0145 IF STATUS>0 THEN GOTO 9900
0150 LET ART03_DEV=CHANNELS[1],ART13_DEV=CHANNELS[2],ART33_DEV=CHANNELS[3]
0155 LET ARM01_DEV=CHANNELS[4],ARM10_DEV=CHANNELS[5],ARM04_DEV=CHANNELS[6]
0160 LET ARM02_DEV=CHANNELS[7],SYS01_DEV=CHANNELS[8],ARW14_DEV=CHANNELS[9]
0165 LET IVM02_DEV=CHANNELS[10],GLM01_DEV=CHANNELS[11],GLW04_DEV=CHANNELS[12]
0200 REM " --- IOLIST's"
0210 ARM01A: IOLIST *,X0$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0405 DIM P[2],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 CALL "SYC.VA","OP",INFO$[ALL]
0445 IF INFO$[9]<>"Y" THEN GOTO 4900
0450 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=FNYY_YEAR(P4$(3,2))
0500 REM " --- Initializations"
0600 REM " --- Additional File Opens"
0700 REM " --- Background"
0710 FOR X=4 TO 14 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(8,3),"Alpha Or Numeric Sort Sequence:",@(20,5),"Beginning Customer:",@(23,7),"Ending Customer:",@(16,9),"Beginning Invoice Date:",@(19,11),"Ending Invoice Date:",@(20,13),"Summary Or Detail?: ",'SF',
1000 REM " --- Alpha/Numeric"
1005 PRINT 'CF',
1010 LET V0$="S",V1$="KRCE",V2$=R0$,V3$="AN",V0=1,V1=40,V2=3,V4$="<A>=Alternate Seq, <N>=Customer Number",I0=0
1015 IF POS(V2$="AN")=0 THEN LET V2$="N"
1020 GOSUB 7000
1025 IF V3=4 THEN GOTO 9900
1030 LET R0$=V$
1040 IF R0$="A" THEN PRINT @(V1,V2),"Alpha",'CL', ELSE PRINT @(V1,V2),"Numeric",'CL',
1050 GOTO 3900
1200 REM " --- Beginning Customer"
1210 LET V0$="M",V1$="KC",V2$=R8$,V3$=P1$,V4$="Enter A Valid Customer Number, <F1>=First, <F3>=Lookup",V0=6,V1=40,V2=5
1215 IF R0$="A" THEN LET V0$="S",V1$="C",V3$="",V4$="Enter The Beginning Alternate Sequence, <F1>=First",V0=10
1217 PRINT @(V1,V2),'CL',
1220 GOSUB 7000
1225 IF V3=2 THEN GOTO 3900
1242 IF V3=3 AND R0$<>"A" THEN CALL "SYC.LC",V$
1250 IF V3=1 THEN LET V$=""
1255 IF V$="" THEN PRINT @(V1,V2),"First",'CL',
1260 IF R0$="A" OR (R0$="N" AND V$="") THEN GOTO 1280
1265 READ (ARM01_DEV,KEY=N0$+V$,DOM=1200)IOL=ARM01A
1270 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$),"  ",X0$(1,30),'CL'
1280 LET R1$=V$
1290 GOTO 3900
1400 REM " --- Ending Customer"
1410 LET V0$="M",V1$="KC",V2$=R2$,V3$=P1$,V4$="Enter A Valid Customer Number, <F1>=Last, <F3>=Lookup",V0=6,V1=40,V2=7
1415 IF R0$="A" THEN LET V0$="S",V1$="C",V3$="",V4$="Enter The Ending Alternate Sequence, <F1>=Last",V0=10
1417 PRINT @(V1,V2),'CL',
1420 GOSUB 7000
1430 IF V3=2 THEN GOTO 3900
1440 IF V3<>3 OR R0$="A" THEN GOTO 1450
1442 IF V3=3 AND R0$<>"A" THEN CALL "SYC.LC",V$
1450 IF V3=1 THEN LET V$=""
1455 IF V$="" THEN PRINT @(V1,V2),"Last",'CL'
1460 IF R0$="A" OR (R0$="N" AND V$="") THEN GOTO 1480
1465 READ (ARM01_DEV,KEY=N0$+V$,DOM=1400)IOL=ARM01A
1470 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$),"  ",X0$(1,30),'CL'
1480 LET R2$=V$
1485 IF R2$<>"" THEN IF R2$<R1$ THEN GOTO 1400
1490 GOTO 3900
1600 REM " --- Beginning Date"
1620 LET V0$="D",V1$="",V2$=R3$,V0=8,V1=40,V2=9,V4$="Enter Starting Invoice Date, <F1>=First"
1640 GOSUB 7000
1645 IF V3=2 THEN GOTO 3900
1650 IF V3=1 THEN LET V$=""
1660 IF V$="" THEN PRINT @(V1,V2),"First",'CL',
1680 LET R3$=V$
1690 GOTO 3900
1800 REM " --- Ending Date"
1820 LET V0$="D",V1$="",V2$=R4$,V0=8,V1=40,V2=11,V4$="Enter Ending Invoice Date, <F1>=Last"
1840 GOSUB 7000
1845 IF V3=2 THEN GOTO 3900
1850 IF V3=1 THEN LET V$=""
1860 IF V$="" THEN PRINT @(V1,V2),"Last",'CL',
1880 LET R4$=V$
1890 GOTO 3900
1900 REM " --- Summary Or Detail"
1920 LET V0$="S",V1$="C",V2$=R5$,V3$="SD",V4$="<S>=Summary, <D>=Detail",V0=1,V1=40,V2=13
1930 IF POS(V2$="SD")=0 THEN LET V2$="S"
1940 GOSUB 7000
1960 IF POS(V$="SD")=0 THEN GOTO 1900
1980 LET R5$=V$
1990 GOTO 3900
3900 REM " --- Next Field"
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,1900,4000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct",V0=1,V1=63,V2=22
4020 CALL "SYC.YN",1,V4$,2,V$,V3
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4200 RUN "OPR.ZB"
4900 REM " --- G/L Not Installed"
4910 DIM MESSAGE$[1]
4920 LET MESSAGE$[0]="General Ledger Is Required For The "+N3$
4930 LET MESSAGE$[1]="               Press <Enter> To Continue"
4940 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
4990 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

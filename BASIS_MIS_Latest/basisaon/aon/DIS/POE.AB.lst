0010 REM "POE - Requisition Entry (Line Items)"
0020 REM "Program POE.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 SETERR 9000
0070 SETESC 9000
0080 PRECISION I[1]
0090 ON O0 GOTO 0200,4000
0200 REM " --- IOLIST's"
0210 POE11A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0220 IVM01A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1),B4$(1),B5$,B6$(1),B[ALL]
0230 IVM02A: IOLIST C0$(1),C1$(1),C2$,C[ALL]
0310 POM02A: IOLIST Y0$(1),Y1$(1)
0320 IVM10C: IOLIST X0$(1)
0330 POE21A: IOLIST X2$(1)
0340 POE41A: IOLIST X4$(1)
0500 REM " --- Initializations"
0510 DIM DESCRIPTION$(48),C0$(24),C1$(64),C[20]
0515 DIM Y0$(4),Y1$(32),X0$(160),FILES[14],PARAMS[10],PARAMS$[10]
0520 DIM W0$(18),W1$(48),W2$(32),W3$(22),W4$(40),W[12],ITEM$[6],ITEM[12]
0525 DIM B0$(22),B1$(60),B2$(64),B3$(40),B4$(21),B6$(50),B[12]
0550 LET W0$(1)=A0$,L=LINE,L9=19,TOTAL=0,REQ=0
0700 REM " --- Background"
0710 PRINT @(O[5]-8,L0-3),'SB'," Total: ",'SF',TOTAL:M1$,@(0,L0-2),'CE','SB',"Seq Cd Wh Item",@(O[0]),"UM",@(O[1]+M5-5),"Conv",@(O[2]+M2-9),"Quantity",@(O[4]+M3-5),"Cost",@(O[5]+M1-10),"Extension",'SF',
0900 REM " --- Display any existing lines"
0910 GOSUB 6000
0920 GOSUB 5600
0930 IF NUMBER=0 THEN GOTO 1200
0990 GOTO 4000
1000 REM " --- Overflow"
1010 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L0-1),'LD','LD',
1020 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1110 LET K$=KEY(POE11_DEV,END=1200)
1120 IF POS(A0$=K$)<>1 THEN GOTO 1200
1130 READ (POE11_DEV)IOL=POE11A
1190 GOTO 1330
1200 REM " --- Sequence Number"
1210 PRINT @(0,21),'CL',
1220 LET NUMBER=NUM(W0$(16))+INCREMENT
1230 LET V4$="Enter A 3 Digit Sequence Number"
1240 LET V0$="Z",V1$="KERC",V2$=STR(NUMBER),V3$="",V0=3,V1=0,V2=L,I0=0
1250 GOSUB 7000
1260 IF V3=4 THEN PRINT @(0,L),'CL',@(0,L+1),'CL',; GOTO 4000
1270 IF V$="000" OR V$="" THEN GOTO 1200
1300 REM " --- Display record"
1310 LET W0$(16)=V$,NEWLINE=0
1320 FIND (POE11_DEV,KEY=W0$,DOM=1400)IOL=POE11A
1330 IF W0$<=K9$ THEN GOSUB 5900
1340 GOSUB 5000
1390 GOTO 3300
1400 REM " --- New Record"
1410 DIM W1$(48),W2$(32),W3$(22),W4$(40),W[6]
1420 LET W1$(1,2)=P3$(56,2),W1$(3,6)=A1$(23,6),W1$(9,6)=A1$(11,6)
1430 LET W1$(15,6)=A1$(17,6),W3$(1,2)=A1$(1,2),Y0$(1)=N0$+W1$(1,2)
1440 LET X0$(1)=N0$+"C"+W3$(1,2),NEWLINE=1
1450 PRINT @(4,L),'CL',@(4,L+1),'CL',
1460 FIND (POM02_DEV,KEY=Y0$,DOM=1600)IOL=POM02A
1470 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=1800)IOL=IVM10C
1480 GOSUB 5000
1490 GOTO 2000
1500 REM " --- Line Code"
1510 PRINT @(0,21),'CL',
1520 LET V4$="Enter A Valid Line Code (<F3>=Lookup)",I0=1
1530 LET V0$="S",V1$="C",V2$=W1$(1,2),V3$="",V0=2,V1=4,V2=L
1540 GOSUB 7000
1550 ON V3 GOTO 1600,1500,3200,1560,1500
1560 CALL "POC.LK",1,V1,V2,V$
1570 IF V$="" THEN GOTO 1500
1600 REM " --- Valid line code?"
1610 LET Y0$(1)=N0$+V$
1620 FIND (POM02_DEV,KEY=Y0$,DOM=1500)IOL=POM02A
1630 PRINT @(0,21),'CL','SB',"Line Code: ",'SF',Y0$(3)," ",Y1$(1,20),@(38),'SB',"Dropship: ",'SF',Y1$(22,1),@(54),'SB',"Lead Time: ",'SF',Y1$(23,1),
1640 IF Y1$(22,1)<>"Y" THEN GOTO 1700
1650 LET ACTION$="I"
1660 GOSUB 6800
1670 IF STATUS THEN GOTO 1500
1700 REM " --- Changed line code?"
1710 IF Y0$(3,2)=W1$(1,2) THEN GOTO 1750
1720 DIM W4$(40),W[14]
1730 LET W1$(22)="",W3$(3)=""
1735 IF Y1$(22,1)<>"Y" THEN LET W2$(11)=""
1740 PRINT @(10,L),'CL',@(10,L+1),'CL',
1750 LET W1$(1,2)=Y0$(3),W1$(21,1)=Y1$(23,1)
1760 PRINT @(V1,V2),W1$(1,2),
1770 GOSUB 5000
1790 GOTO 3200
1800 REM " --- Warehouse ID (All Lines)"
1810 LET V4$="Enter A Valid Warehouse ID (<F3>=Lookup)",I0=2
1820 LET V0$="S",V1$="CR",V2$=W3$(1,2),V3$="",V0=2,V1=7,V2=L,V$=V2$
1830 IF I3$(1,1)="N" THEN GOTO 1900
1840 GOSUB 7000
1850 ON V3 GOTO 1900,1800,3200,1860,1800
1860 CALL "IVC.LK",3,V1,V2,V$
1870 IF V$="" THEN GOTO 1800
1900 REM " --- Display warehouse"
1910 LET X0$(1)=N0$+"C"+V$
1920 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=1800)IOL=IVM10C
1930 LET W3$(1,2)=X0$(4,2)
1940 PRINT @(0,21),'CL','SB',"Warehouse: ",'SF',X0$(4,2)," ",X0$(6,24),
1950 IF W3$(1,2)=A1$(1,2) THEN GOTO 3200
1960 LET MESSAGE$[0]="This Warehouse Does Not Match Header Ship-to (<Enter>=Continue):"
1970 CALL "SYC.XA",2,MESSAGE$[ALL],0,6,-1,V$,V3
1990 GOTO 3200
2000 REM " --- Item Number (S Lines)"
2010 IF Y1$(21,1)<>"S" THEN GOTO 3200
2020 LET FILES[1]=IVM01_DEV,FILES[14]=IVM14_DEV,PARAMS[0]=I[0]
2030 LET PARAMS[1]=I[3],PARAMS[2]=I[4],PARAMS[3]=I[5]
2035 LET V0$="",V2$=W3$(3),V4$="",V1=10,V2=L,I0=3
2040 CALL "IVC.IA",V0$,V1,V2,FILES[ALL],N0$,V2$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
2045 ON V3 GOTO 2050,2000,3200,2000,2000
2050 REM " --- Retrieve warehouse record"
2060 DIM C0$(24),C1$(64),C[20]
2070 LET C0$(1)=N0$+W3$(1,2)+ITEM$[0]
2080 IF C0$(3)=W3$ THEN GOTO 2200
2090 FIND (IVM02_DEV,KEY=C0$,DOM=2000)IOL=IVM02A
2100 REM " --- Set defaults if warehouse/item has changed"
2110 DIM W[12]
2120 LET W1$(22,2)=ITEM$[2](6,2),W3$(1)=C0$(3),W[0]=ITEM[0]
2130 IF FNP$(W1$(24,10))="" THEN LET W1$(24,10)="Stock"
2140 IF W[0]=0 THEN LET W[0]=1
2150 LET W[1]=W[0]*C[11]
2200 REM " --- Display line"
2210 GOSUB 5000
2220 IF C1$(60,1)<>"Y" THEN GOTO 3200
2230 LET ACTION$="D"
2240 GOSUB 6800
2290 GOTO 3200
2300 REM " --- Non-Stock Item Number (N Lines)"
2310 IF Y1$(21,1)<>"N" THEN GOTO 3200
2320 LET V4$="Enter The Non-Stock Item Number For This Line"
2330 LET V0$="S",V1$="CR",V2$=W3$(3),V3$="",V0=I[0],V1=10,V2=L
2340 GOSUB 7000
2350 ON V3 GOTO 2360,2300,3200,2300,2300
2360 LET W3$(3)=V$
2390 GOTO 3200
2400 REM " --- Vendor's Item Number (V Lines)/Message (M/N/O Lines)"
2410 IF POS(Y1$(21,1)="VMNO")=0 THEN GOTO 3200
2415 LET V0$="S",V1$="C",V2$=W4$,V3$="",V0=40,V1=10,V2=L+1
2420 LET V4$="Enter Up To "+STR(V0)+" Characters"
2425 IF Y1$(21,1)="V" THEN LET V4$="Enter The Vendor's Item Number",V0=I[6]
2430 IF Y1$(21,1)="N" THEN LET V4$="Enter The Description For This Item"
2440 GOSUB 7000
2450 ON V3 GOTO 2460,2400,2470,2400,2470
2460 LET W4$(1)=V$
2470 PRINT @(V1,V2),W4$,
2480 IF Y1$(21,1)="N" THEN PRINT @(46,V2),'SB',"Loc: ",'SF',
2490 GOTO 3200
2500 REM " --- Unit of Measure (S/N Lines)"
2510 IF POS(Y1$(21,1)="SN")=0 THEN GOTO 3200
2520 LET V4$="Enter The Unit Of Measure For This Item"
2530 LET V0$="S",V1$="C",V2$=W1$(22,2),V3$="",V0=2,V1=O[0],V2=L
2540 GOSUB 7000
2550 ON V3 GOTO 2560,2500,3200,2500,3200
2560 LET W1$(22,2)=V$
2590 GOTO 3200
2600 REM " --- Conversion factor (S Lines)"
2610 IF Y1$(21,1)<>"S" THEN GOTO 3200
2620 LET V4$="Enter The Conversion Factor For This Item"
2630 LET V0$="N",V1$="C",V2$=STR(W[0]),V3$=M5$,V0=M5,V1=O[1],V2=L
2640 GOSUB 7000
2650 ON V3 GOTO 2660,2600,2680,2600,2680
2660 IF V<=0 THEN GOTO 2600
2665 IF W[0]=V THEN GOTO 2680
2670 LET W[1]=W[1]/W[0],W[0]=V,W[1]=W[1]*W[0]
2680 GOSUB 6900
2685 PRINT @(V1,V2),W[0]:M5$,@(O[4]),W[1]:M3$,@(O[5]),EXTENSION:M1$,
2690 GOTO 3200
2700 REM " --- Quantity ordered (S/N Lines)"
2705 IF POS(Y1$(21,1)="SN")=0 THEN GOTO 3200
2710 LET V4$="Enter The Quantity Of This Item To Order"
2715 LET V0$="N",V1$="C",V2$=STR(W[2]),V3$=M2$,V0=M2,V1=O[2],V2=L
2720 GOSUB 7000
2725 ON V3 GOTO 2730,2700,2780,2700,2780
2730 IF V<=0 THEN GOTO 2700
2735 LET W[2]=V
2740 IF NEWLINE=0 THEN GOTO 2780
2745 IF Y1$(21,1)<>"S" THEN GOTO 2780
2750 CALL "POC.UA","R",TYPE$,IVM05_DEV,A0$,A1$,W3$,W[ALL],I[1],STATUS
2780 GOSUB 6900
2785 PRINT @(V1,V2),W[2]:M2$,@(O[5]),EXTENSION:M1$,
2790 GOTO 3200
2800 REM " --- Unit Cost (S/N/O Lines)"
2810 IF POS(Y1$(21,1)="SNO")=0 THEN GOTO 3200
2820 LET V4$="Enter The Unit Cost For This Item"
2830 LET V0$="N",V1$="C",V2$=STR(W[1]),V3$=M3$,V0=M3,V1=O[4],V2=L
2840 GOSUB 7000
2850 ON V3 GOTO 2860,2800,2880,2800,2880
2860 IF V<0 THEN GOTO 2800
2870 LET W[1]=V
2880 GOSUB 6900
2885 PRINT @(V1,V2),W[1]:M3$,@(O[5]),EXTENSION:M1$,
2890 GOTO 3200
2900 REM " --- Receiving Location (S/N Lines)"
2910 IF POS(Y1$(21,1)="SN")=0 THEN GOTO 3200
2920 LET V4$="Enter The Receiving Location For This Item"
2930 LET V0$="S",V1$="C",V2$=W1$(24,10),V3$="",V0=10,V1=51,V2=L+1
2940 GOSUB 7000
2950 ON V3 GOTO 2960,2900,3200,2900,3200
2960 LET W1$(24,10)=V$
2990 GOTO 3200
3000 REM " --- Date Required (S/N/O Lines)"
3010 IF POS(Y1$(21,1)="SNO")=0 THEN GOTO 3200
3020 LET V4$="Enter The Date That This Line Item Is Required"
3030 LET V0$="D",V1$="CR6N",V2$=W1$(3,6),V3$="",V0=6,V1=69,V2=L+1
3040 GOSUB 7000
3050 ON V3 GOTO 3060,3000,3200,3000,3200
3060 LET W1$(3,6)=V$
3090 GOTO 3200
3100 REM " --- Additional options?"
3110 IF Y1$(25,1)<>"Y" THEN GOTO 3200
3120 GOSUB 6500
3190 GOTO 3200
3200 REM " --- Sequence Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1200,1500,1800,2000,2300,2400,2500,2600,2700,2800,2900,3000,3100,3300
3300 REM " --- Line Correct?"
3310 LET V4$="<Enter>=Correct     <F1>=Additional Options     <F2>=Change     <D>=Delete"
3315 IF Y1$(22,1)="Y" THEN LET V4$="<Enter>=Correct  <F1>=Add'l Options  <F2>=Change  <S>=Drop Ship  <D>=Delete"
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L+1,K9$=W0$,I0=12
3325 IF POS(Y1$(21,1)="VM") THEN LET I0=6
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3400,3200,3300,3350
3350 PRINT @(0,21),'CL',
3360 IF V$="" THEN GOTO 3600
3370 IF V$="D" THEN GOTO 3900
3380 IF V$="S" THEN GOTO 3500
3390 GOTO 3300
3400 REM " --- Additional Options"
3410 GOSUB 6500
3490 GOTO 3300
3500 REM " --- Drop ship information"
3510 LET ACTION$="D"
3520 GOSUB 6800
3590 GOTO 3300
3600 REM " --- Write"
3610 WRITE (POE11_DEV,KEY=W0$)IOL=POE11A
3620 LET X4$(1)=N0$+W3$(3)+W0$(9)+W0$(3,6)
3630 IF Y1$(21,1)="S" THEN WRITE (POE41_DEV,KEY=X4$)IOL=POE41A
3640 GOSUB 6000
3650 LET L=L+2
3660 IF V3=4 THEN GOTO 4000
3690 GOTO 1000
3900 REM " --- Remove"
3910 REMOVE (POE11_DEV,KEY=W0$,DOM=3920)
3920 LET X4$(1)=N0$+W3$(3)+W0$(9)+W0$(3,6)
3930 IF Y1$(21,1)="S" THEN REMOVE (POE41_DEV,KEY=X4$,DOM=3940)
3940 LET OLDWO$=W2$(1,10),NEWWO$=""
3950 IF SF$="Y" THEN CALL "POC.WA",WOE22_DEV,WOE32_DEV,N0$,W0$(9),TYPE$,Y1$,OLDWO$,NEWWO$,I[1],STATUS
3960 GOSUB 6000
3970 PRINT @(0,L),'CL',@(0,L+1),'CL',
3980 IF V3=4 THEN GOTO 4000
3990 GOTO 1000
4000 REM " --- Correct?"
4010 PRINT @(0,22),"Is The Above Correct (Y/N/List/Review/Delete/Print/Line #/<F2>=Header)?",
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=72
4030 GOSUB 7000
4035 ON V3 GOTO 4040,4000,4200,4000,4100
4040 IF V$="P" OR V$="PRINT" THEN GOTO 4500
4050 IF V$="L" OR V$="LIST" THEN GOTO 4800
4055 IF V$="R" OR V$="REVIEW" THEN GOTO 4900
4060 IF V$="DELETE" THEN GOTO 4200
4065 LET V=NUM(V$,ERR=4090)
4070 IF V<1 OR V>999 THEN GOTO 4090
4075 LET V$=STR(INT(V):"000")
4080 GOTO 1300
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Update print file"
4110 LET X2$(1)=A0$
4120 IF PRINTIT THEN WRITE (POE21_DEV,KEY=X2$)IOL=POE21A
4200 REM " --- Return to header overlay"
4210 PRINT @(0,L0-2),'CE',
4220 LET O0=1,ACTION$="B"
4230 IF V3=2 THEN LET O0=2,ACTION$="BD"
4240 IF V$="DELETE" THEN LET O0=3
4250 CALL "POC.RA",TYPE$,ACTION$,RIBBON$[ALL],RIBBON[ALL],RFILES[ALL],A$[ALL],A[ALL],V3
4290 RUN "POE.AA"
4500 REM " --- On demand printing"
4510 LET O0$=PGM(-2),O0=1,SORTFILE$="POW-01",WORKFILE$="POW-11",SEQUENCE$="N"
4515 WRITE (POE21_DEV,KEY=A0$)IOL=POE21A
4520 LET POE02_DEV=POE01_DEV,POE12_DEV=POE11_DEV,POE22_DEV=POE21_DEV
4530 LET POE32_DEV=POE31_DEV,TITLE$="P U R C H A S E   R E Q U I S I T I O N"
4540 LET LABEL$="Requisition",PLAIN$="Y"; REM "No preprinted req forms"
4550 LET X2$=A0$,VENDOR_PO$=A0$,PRINTIT=0
4590 RUN "POR.BB"
4800 REM " --- List"
4810 GOSUB 5600
4890 GOTO 4000
4900 REM " --- Review"
4910 READ (POE11_DEV,KEY=A0$,DOM=4920)
4920 GOSUB 5900
4990 GOTO 1000
5000 REM " --- Display"
5010 LET NUMBER=NUMBER+1,Y0$(1)=N0$+W1$(1,2),DESCRIPTION$(1)=W4$
5030 FIND (POM02_DEV,KEY=Y0$,DOM=5590)IOL=POM02A
5040 GOSUB 6900
5080 PRINT @(0,L),'CL',W0$(16),@(4),W1$(1,2),@(7),W3$(1,2),@(0,L+1),'CL',
5090 ON POS(Y1$(21,1)="SNVMO") GOTO 5590,5100,5200,5300,5400,5500
5100 REM " --- Display Standard Line"
5110 DIM B0$(22),B1$(60),B2$(64),B3$(40),B4$(21),B6$(50),B[12]
5120 LET B0$(1)=N0$+W3$(3)
5130 FIND (IVM01_DEV,KEY=B0$,DOM=5140)IOL=IVM01A
5140 LET DESCRIPTION$(1)=FNITEM$(B1$,I[3],I[4],I[5])
5150 PRINT @(10,L),W3$(3,I[0]),@(O[0]),W1$(22,2),@(O[1]),W[0]:M5$,@(O[2]),W[2]:M2$,@(O[4]),W[1]:M3$,@(O[5]),EXTENSION:M1$,@(10,L+1),DESCRIPTION$,@(46),'SB',"Loc: ",'SF',W1$(24,10),@(62),'SB',"Req'd: ",'SF',FNB6$(W1$(3,6)),
5190 RETURN 
5200 REM " --- Display Non-Stock Line"
5210 PRINT @(10,L),W3$(3,I[0]),@(O[0]),W1$(22,2),@(O[2]),W[2]:M2$,@(O[4]),W[1]:M3$,@(O[5]),EXTENSION:M1$,@(10,L+1),DESCRIPTION$,@(46),'SB',"Loc: ",'SF',W1$(24,10),@(62),'SB',"Req'd: ",'SF',FNB6$(W1$(3,6)),
5290 RETURN 
5300 REM " --- Display Vendor Part Number Line"
5310 PRINT @(10,L+1),W4$(1,I[6]),
5390 RETURN 
5400 REM " --- Display Message Line"
5410 PRINT @(10,L+1),W4$,
5490 RETURN 
5500 REM " --- Display Other Line"
5510 PRINT @(O[4],L),W[1]:M3$,@(O[5]),EXTENSION:M1$,@(10,L+1),DESCRIPTION$,@(62),'SB',"Req'd: ",'SF',FNB6$(W1$(3,6)),
5590 RETURN 
5600 REM " --- List"
5610 LET NUMBER=0
5620 GOSUB 5900
5640 READ (POE11_DEV,KEY=A0$,DOM=5650)
5650 LET K$=KEY(POE11_DEV,END=5790)
5660 IF POS(A0$=K$)<>1 THEN GOTO 5790
5670 READ (POE11_DEV)IOL=POE11A
5690 IF L<=L9 THEN GOTO 5750
5700 REM " --- Full screen"
5710 LET V4$="Press <Enter> To Continue:"
5720 LET V0$="S",V1$="CE",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5730 GOSUB 7000
5740 IF V3=4 THEN GOTO 5790
5745 GOSUB 5900
5750 GOSUB 5000
5760 LET L=L+2,K9$=W0$
5770 GOTO 5650
5790 RETURN 
5900 REM " --- Clear Screen"
5910 LET L=L0-1
5920 PRINT @(0,L),'CE',
5990 RETURN 
6000 REM " --- Accumulate/display total for requisition"
6010 LET V1=O[5],V2=L0-3
6050 CALL "POC.TA",TYPE$,POE11_DEV,POM02_DEV,A0$,W0$,V1,V2,M1$,I[1],TOTAL,STATUS
6090 RETURN 
6500 REM " --- Process additional options window"
6510 LET PARAMS$[0]=W0$,PARAMS$[1]=W1$,PARAMS$[2]=W2$,PARAMS$[3]=Y1$
6520 LET PARAMS$[4]=SF$,PARAMS$[5]=C1$(60,1),FILES[1]=POM04_DEV
6530 LET FILES[2]=WOE01_DEV,FILES[3]=WOE22_DEV,FILES[4]=WOE32_DEV
6540 CALL "POC.AO",TYPE$,FILES[ALL],PARAMS$[ALL],L,STATUS
6550 IF FNP$(W2$(1,10))="" THEN IF FNP$(PARAMS$[2](1,10))="" THEN GOTO 6580
6560 LET OLDWO$=W2$(1,10),NEWWO$=PARAMS$[2](1,10)
6570 IF SF$="Y" THEN CALL "POC.WA",WOE22_DEV,WOE32_DEV,N0$,W0$(9),TYPE$,Y1$,OLDWO$,NEWWO$,I[1],STATUS
6580 LET W1$(1)=PARAMS$[1],W2$(1)=PARAMS$[2]
6590 RETURN 
6800 REM " --- Process drop ship window"
6810 LET FILES[1]=ARM01_DEV,FILES[2]=ARM03_DEV,FILES[3]=ARE03_DEV
6820 LET FILES[4]=ARE13_DEV,FILES[5]=ARE33_DEV,PARAMS[0]=CUSTLEN,PARAMS$[0]=N0$
6830 LET PARAMS$[1]=P7$,PARAMS$[2]=W2$,PARAMS$[3]=AR$,PARAMS$[4]=OP$
6840 CALL "POC.DS",TYPE$,ACTION$,FILES[ALL],PARAMS[ALL],PARAMS$[ALL],L,STATUS
6850 IF STATUS=0 THEN LET W2$(1)=PARAMS$[2]
6890 RETURN 
6900 REM " --- Calculate EXTENSION"
6910 LET QTY=W[2]
6920 IF Y1$(21,1)="O" THEN LET QTY=1
6930 PRECISION 2
6940 LET EXTENSION=W[1]*QTY
6950 PRECISION I[1]
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8070 DEF FNP(M1,M2,M3,M5,P)=(80-(M1+M2+M2+M3+M5))-P
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

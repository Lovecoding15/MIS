0010 REM "POM - Line Code Maintenance"
0020 REM "Program POM.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="POM-02",FILES$[3]="GLM-01"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],POM02_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 POM02A: IOLIST A0$(1),A1$(1),A2$(1)
0220 GLM01A: IOLIST B0$(1),B1$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0405 DIM G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="PO"
0430 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0440 FOR X=0 TO 4
0445 LET G[X]=NUM(G2$(X*2+1,2),ERR=0450)
0450 NEXT X
0460 LET G[2]=FNYY_YEAR(G2$(5,2))
0470 CALL "SYC.VA",N2$,INFO$[ALL]
0480 LET GL$=INFO$[9]
0500 REM " --- Initializations"
0510 DIM A0$(4),A1$(32),A2$(2*G[4]),B0$(12),B1$(35)
0590 LET A0$(1)=N0$
0600 REM " --- Additional File Opens"
0610 IF GL$="N" THEN GOTO 0700
0620 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS THEN GOTO 9900
0640 LET GLM01_DEV=CHANNELS[3]
0700 REM " --- Background"
0710 PRINT 'SB',@(9,3),"Line Code:",@(7,4),"Description:",@(9,5),"Line Type:",@(10,6),"Dropship:",@(7,7),"Landed Cost:",@(0,8),"Additional Options:",@(1,10),"Use For Lead Time",@(6,11),"Calculations:",@(3,13),"Expense Account:",@(2,14),"Variance Account:",'SF',
0900 REM " --- Position File"
0990 READ (POM02_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Line Code"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(POM02_DEV,END=1050)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3)
1050 LET V0$="S",V1$="KE",V3$="",V4$="Enter A Valid Line Code (<F3>=Lookup)",V0=2,V1=20,V2=3
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN CALL "POC.LK",1,V1,V2,V$
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(3)=V$
1200 REM " --- Retrieve record"
1210 FIND (POM02_DEV,KEY=A0$,DOM=1400)IOL=POM02A
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Initialize new record"
1510 DIM A1$(32),A2$(2*G[4])
1520 LET A1$(21,5)="SNYYN"
1530 GOSUB 5000
2000 REM " --- Description"
2010 LET V0$="S",V1$="",V2$=A1$(1,20),V3$="",V0=20,V1=20,V2=4,I0=0
2020 LET V4$="Enter The Description For This Line Code"
2040 GOSUB 7000
2050 ON V3 GOTO 2060,2000,3900,2000,3900
2060 LET A1$(1,20)=V$
2090 GOTO 3900
2200 REM " --- Line Type"
2210 LET V4$="S=Standard   V=Vendor Part Number   N=Non-stock Item   M=Message Line   O=Other"
2220 LET V0$="S",V1$="C",V2$=A1$(21,1),V3$="",V0=1,V1=20,V2=5
2230 GOSUB 7000
2240 IF POS(V$="SVONM")=0 THEN GOTO 2200
2250 LET A1$(21,1)=V$
2290 GOTO 3900
2400 REM " --- Drop Ship?"
2410 LET V4$="Are Items Ordered With This Line Code Drop Shipped (Y/N)?"
2420 LET V0$="Y",V1$="C",V2$=A1$(22,1),V3$="",V0=1,V1=20,V2=6,V$="N"
2425 IF POS(A1$(21,1)="VNMO") THEN GOTO 2450
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 LET A1$(22,1)=V$
2480 PRINT @(V1,V2),A1$(22,1),
2490 GOTO 3900
2600 REM " --- Landed Cost"
2610 LET V4$="Include Lines With This Line Code When Calculating Landed Cost (Y/N)?"
2620 LET V0$="Y",V1$="C",V2$=A1$(24,1),V3$="",V0=1,V1=20,V2=7,V$="N"
2625 IF POS(A1$(21,1)="VM") THEN GOTO 2650
2630 GOSUB 7000
2640 ON V3 GOTO 2650,2600,3900,2600,3900
2650 LET A1$(24,1)=V$
2680 PRINT @(V1,V2),A1$(24,1),
2690 GOTO 3900
2800 REM " --- Additional Options?"
2810 LET V4$="Should The Additional Options Window Be Displayed Automatically (Y/N)?"
2820 LET V0$="Y",V1$="C",V2$=A1$(25,1),V3$="",V0=1,V1=20,V2=8
2830 GOSUB 7000
2840 ON V3 GOTO 2850,2800,3900,2800,3900
2850 LET A1$(25,1)=V$
2860 PRINT @(V1,V2),A1$(25,1),
2890 GOTO 3900
3000 REM " --- Lead Time?"
3010 LET V4$="Should Lead Time Be Recalculated For Receipts With This Line Code (Y/N)?"
3020 LET V0$="Y",V1$="C",V2$=A1$(23,1),V3$="",V0=1,V1=20,V2=11,V$="N"
3025 IF POS(A1$(21,1)="VNMO") THEN GOTO 3050
3030 GOSUB 7000
3040 ON V3 GOTO 3050,3000,3900,3000,3900
3050 LET A1$(23,1)=V$
3080 PRINT @(V1,V2),A1$(23,1),
3090 GOTO 3900
3200 REM " --- Expense Account"
3210 IF POS(A1$(21,1)="NO")=0 THEN GOTO 3300
3215 IF GL$="N" THEN GOTO 3300
3220 LET V4$="Enter The GL Expense Account For This Line Code (<F3>=Lookup)"
3230 LET V0$="M",V1$="CR",V2$=A2$(1,G[3]),V3$=G5$,V0=G[4],V1=20,V2=13
3240 GOSUB 7000
3250 ON V3 GOTO 3270,3200,3900,3260,3900
3260 CALL "SYC.LG",V$
3270 GOSUB 5200
3280 IF STATUS THEN GOTO 3200
3285 LET A2$(1,G[4])=B0$(3)
3290 GOTO 3900
3300 REM " --- No expense account"
3310 LET A2$(1,G[4])=""
3320 PRINT @(20,13),'CL',
3390 GOTO 3900
3400 REM " --- Variance Account"
3410 IF POS(A1$(21,1)="NO")=0 THEN GOTO 3500
3415 IF GL$="N" THEN GOTO 3500
3420 LET V4$="Enter The GL Variance Account For This Line Code (<F3>=Lookup)"
3430 LET V0$="M",V1$="CR",V2$=A2$(G[4]+1,G[3]),V3$=G5$,V0=G[4],V1=20,V2=14
3440 GOSUB 7000
3450 ON V3 GOTO 3470,3400,3900,3460,3900
3460 CALL "SYC.LG",V$
3470 GOSUB 5200
3480 IF STATUS THEN GOTO 3400
3485 LET A2$(G[4]+1)=B0$(3)
3490 GOTO 3900
3500 REM " --- No variance account"
3510 LET A2$(G[4]+1)=""
3520 PRINT @(20,14),'CL',
3590 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,2400,2600,2800,3000,3200,3400,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record"
4110 WRITE (POM02_DEV,KEY=A0$)IOL=POM02A
4190 GOTO 1000
4200 REM " --- Remove record"
4210 REMOVE (POM02_DEV,KEY=A0$,DOM=4220)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4900,9900
4900 REM " --- Report Initializations"
4905 LET NUMBER=8,PROGRAM$="",HEADINGS=3,WIDTH=132,CHANNEL=POM02_DEV
4915 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
4920 DIM TYPE$[NUMBER],MASK$[NUMBER],HEADINGS$[HEADINGS]
4925 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=2,FIELD$[0]="Code"
4930 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=20,FIELD$[1]="Description"
4935 LET FIELD[2]=2,POSITION[2]=21,LENGTH[2]=1,FIELD$[2]="Type"
4940 LET FIELD[3]=2,POSITION[3]=22,LENGTH[3]=1,FIELD$[3]="Dropship"
4945 LET FIELD[4]=2,POSITION[4]=24,LENGTH[4]=1,FIELD$[4]="Landed Cost"
4950 LET FIELD[5]=2,POSITION[5]=25,LENGTH[5]=1,FIELD$[5]="Add Opt?"
4955 LET FIELD[6]=2,POSITION[6]=23,LENGTH[6]=1,FIELD$[6]="Lead Time"
4960 LET FIELD[7]=3,POSITION[7]=1,LENGTH[7]=G[3],FIELD$[7]="Expense"
4963 LET FIELD[8]=3,POSITION[8]=G[4]+1,LENGTH[8]=G[3],FIELD$[8]="Variance"
4965 LET TYPE$[0]="S",TYPE$[1]="S",TYPE$[2]="S",TYPE$[3]="Y",TYPE$[4]="Y"
4970 LET TYPE$[5]="Y",TYPE$[6]="Y",TYPE$[7]="M",RECORD$="",MASK$[7]=G5$
4975 LET TYPE$[8]="M",MASK$[8]=G5$,TOTAL$="Purchase Order Line Codes"
4980 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$+" Listing"
4990 RUN "SYR.AA"
5000 REM " --- Display"
5010 PRINT @(20,3),A0$(3),@(20,4),A1$(1,20),@(20,5),A1$(21,1),@(20,6),A1$(22,1),@(20,7),A1$(24,1),@(20,8),A1$(25,1),@(20,11),A1$(23,1),@(20,13),'CL',
5020 IF POS(A1$(21,1)="NO")=0 THEN GOTO 5090
5030 IF GL$="N" THEN GOTO 5090
5040 LET V1=20,V2=13,V$=A2$(1,G[4])
5050 GOSUB 5200
5060 LET V1=20,V2=14,V$=A2$(G[4]+1,G[4])
5070 GOSUB 5200
5090 RETURN 
5200 REM " --- Retrieve GL account"
5210 LET B0$(1)=N0$+V$,B1$(1)="(Not On File)",STATUS=11
5220 FIND (GLM01_DEV,KEY=B0$,DOM=5240)IOL=GLM01A
5230 LET STATUS=0
5240 PRINT @(V1,V2),FNF$(B0$(3,G[3]),G5$)," ",B1$,
5290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

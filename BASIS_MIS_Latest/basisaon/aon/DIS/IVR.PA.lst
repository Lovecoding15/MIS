0010 REM "IVR - ABC Analysis Report (Input Overlay)"
0020 REM "Program IVR.PA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=12
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0125 LET FILES$[4]="IVM-03",FILES$[5]="IVM-10",FILES$[6]="IVM-12"
0130 LET FILES$[7]="IVE-07",FILES$[8]="IVE-17",FILES$[9]="IVS-10"
0135 LET FILES$[10]="IVW-07",FILES$[11]="IVW-17",FILES$[12]="IVM-03"
0140 LET OPTIONS$[10]="C",OPTIONS$[11]="C"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0175 LET IVM03_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],IVM12_DEV=CHANNELS[6]
0180 LET IVE07_DEV=CHANNELS[7],IVE17_DEV=CHANNELS[8],IVS10_DEV=CHANNELS[9]
0185 LET IVW07_DEV=CHANNELS[10],IVW17_DEV=CHANNELS[11],IVM03_DEV2=CHANNELS[12]
0200 REM " --- IOLIST's"
0250 IVM10C: IOLIST X0$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0275 IVS01C: IOLIST I0$,PERCENTS[ALL],FACTORS[ALL]
0280 GLS01A: IOLIST G0$,G1$,G2$,G3$(1),G4$
0400 REM " --- Parameters"
0405 DIM P[5],G[1],PERCENTS[25],FACTORS[25]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="IV"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"02",DOM=9800)IOL=IVS01C
0450 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0480 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1)),G[0]=NUM(G2$(1,2))
0490 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM X0$(32)
0550 LET UPDATE$="Y",VALUE$="U",LEVEL$="B",BEGWHSE$="",ENDWHSE$=""
0700 REM " --- Background"
0710 FOR X=4 TO 12 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(15,3),"Generate A B C Analysis:",@(11,5),"Calculate Total Value Using:",@(26,7),"Detail Level:",@(19,9),"Beginning Warehouse:",@(22,11),"Ending Warehouse:",'SF',
0900 REM 
0990 GOSUB 5000
1000 REM " --- Generate new analysis?"
1010 LET V0$="Y",V1$="C",V2$=UPDATE$,V3$="YN",V0=1,V1=40,V2=3,I0=0
1020 LET V4$="Enter "+$22$+"Y"+$22$+" To Generate A New ABC Analysis Or "+$22$+"N"+$22$+" To Reprint The Existing One"
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,3900,1000,3900
1050 LET UPDATE$=V$
1090 GOTO 3900
1200 REM " --- Value by?"
1210 LET V0$="S",V1$="C",V2$=VALUE$,V3$="OUB",V0=1,V1=40,V2=5
1220 LET V4$="O=Quantity On Hand  U=Estimated Annual Usage  B=Current Period Ending Balance"
1230 GOSUB 7000
1240 ON V3 GOTO 1250,1200,3900,1200,3900
1250 IF POS(V$="OUB")=0 THEN GOTO 1200
1260 LET VALUE$=V$
1270 GOSUB 5200
1280 PRINT @(V1,V2),VALUE$," ",V4$,'CL',
1290 GOTO 3900
1400 REM " --- Detail level"
1410 LET V0$="S",V1$="C",V2$=LEVEL$,V3$="IWB",V0=1,V1=40,V2=7
1420 LET V4$="I=Report By Item (All Warehouses)  W=Report By Item Within Warehouse  B=Both"
1430 GOSUB 7000
1440 ON V3 GOTO 1450,1400,3900,1400,3900
1450 IF POS(V$="IWB")=0 THEN GOTO 1400
1460 LET LEVEL$=V$
1470 GOSUB 5400
1480 PRINT @(V1,V2),LEVEL$," ",V4$,'CL',
1490 IF POS(LEVEL$="WB")>0 THEN GOTO 3900
1500 REM " --- Reset beginning/ending warehouse if item level is selected"
1510 LET BEGWHSE$="",ENDWHSE$=""
1520 PRINT @(40,9),"First",'CL',@(40,11),"Last",'CL',
1590 GOTO 3900
1600 REM " --- Beginning warehouse"
1610 IF LEVEL$="I" THEN GOTO 3900
1620 LET V4$="Enter The First Warehouse To Include On This Report (<F1>=First/<F3>=Lookup)"
1625 LET V0$="S",V1$="C",V2$=BEGWHSE$,V3$="",V0=2,V1=40,V2=9
1630 GOSUB 7000
1640 ON V3 GOTO 1700,1650,3900,1670,3900
1650 LET V$=""
1660 GOTO 1700
1670 CALL "IVC.LK",3,V1,V2,V$
1700 REM 
1710 LET V4$="First"
1720 GOSUB 5600
1730 IF STATUS>0 THEN GOTO 1600
1740 LET BEGWHSE$=V$
1750 PRINT @(V1,V2),V4$,'CL',
1790 GOTO 3900
1800 REM " --- Ending warehouse"
1810 IF LEVEL$="I" THEN GOTO 3900
1820 LET V4$="Enter The Last Warehouse To Include On This Report (<F1>=Last/<F3>=Lookup)"
1825 LET V0$="S",V1$="C",V2$=ENDWHSE$,V3$="",V0=2,V1=40,V2=11
1830 GOSUB 7000
1840 ON V3 GOTO 1900,1850,3900,1870,3900
1850 LET V$=""
1860 GOTO 1900
1870 CALL "IVC.LK",3,V1,V2,V$
1900 REM 
1910 LET V4$="Last"
1920 GOSUB 5600
1930 IF STATUS>0 THEN GOTO 1800
1940 LET ENDWHSE$=V$
1950 PRINT @(V1,V2),V4$,'CL',
1990 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4030 ON POS(V$="YN") GOTO 4000,4050,1000
4090 IF UPDATE$="N" THEN GOTO 4900
4100 REM " --- Remove existing item total record"
4110 REMOVE (IVS10_DEV,KEY=N0$+"I",DOM=4120)
4180 LET FIRST$=N0$+"W"
4190 READ (IVS10_DEV,KEY=FIRST$,DOM=4200)
4200 REM " --- Remove existing warehouse total records"
4210 LET K$=KEY(IVS10_DEV,END=4900)
4220 IF POS(FIRST$=K$)<>1 THEN GOTO 4900
4230 REMOVE (IVS10_DEV,KEY=K$)
4290 GOTO 4200
4900 REM " --- Run next overlay"
4990 RUN "IVR.PB"
5000 REM " --- Display"
5010 GOSUB 5200
5020 LET V1$=V4$
5030 GOSUB 5400
5040 LET V2$=V4$,V$=BEGWHSE$,V4$="First"
5050 GOSUB 5600
5060 LET V3$=V4$,V$=ENDWHSE$,V4$="Last"
5070 GOSUB 5600
5080 PRINT @(40,3),UPDATE$,@(40,5),VALUE$," ",V1$,'CL',@(40,7),LEVEL$," ",V2$,'CL',@(40,9),V3$,'CL',@(40,11),V4$,'CL',
5090 RETURN 
5200 REM " --- Value description"
5210 LET V4$="(Quantity On Hand)"
5220 IF VALUE$="U" THEN LET V4$="(Estimated Annual Usage)"
5230 IF VALUE$="B" THEN LET V4$="(Current Period Ending Balance)"
5290 RETURN 
5400 REM " --- Detail level description"
5410 LET V4$="(Both Item And Warehouse)"
5420 IF LEVEL$="I" THEN LET V4$="(Item Level)"
5430 IF LEVEL$="W" THEN LET V4$="(Warehouse Level)"
5490 RETURN 
5600 REM " --- Retrieve warehouse"
5610 LET X0$(1)=N0$+"C"+V$,X0$(6)="(Not On File)",STATUS=0
5620 IF V$="" THEN GOTO 5690
5630 LET STATUS=1
5640 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=5660)IOL=IVM10C
5650 LET STATUS=0,V$=X0$(4,2)
5660 LET V4$=X0$(4,2)+" "+X0$(6,24)
5690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

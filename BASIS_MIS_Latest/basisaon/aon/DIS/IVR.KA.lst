0010 REM "IVR - Inventory Turns Report (Input Overlay)"
0020 REM "Program IVR.KA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=10
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0130 LET FILES$[4]="IVM-03",FILES$[5]="IVM-10",FILES$[6]="IVM-12"
0140 LET FILES$[7]="IVM-14",FILES$[8]="IVM-03",FILES$[9]="IVM-15"
0145 LET FILES$[10]="IVW-03",OPTIONS$[10]="C"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0180 LET IVM03_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],IVM12_DEV=CHANNELS[6]
0190 LET IVM14_DEV=CHANNELS[7],IVM03_DEV2=CHANNELS[8],IVM15_DEV=CHANNELS[9]
0195 LET IVW03_DEV=CHANNELS[10]
0200 REM " --- IOLIST's"
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0280 GLS01A: IOLIST G0$,G1$,G2$,G3$(1),G4$
0400 REM " --- Parameters"
0405 DIM P[5],G[1],G3$(325)
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="IV"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1)),G[0]=NUM(G2$(1,2))
0460 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM FILES[15],PARAMS$[3],OPTIONS$[7]
0550 LET FILES[1]=IVM01_DEV,FILES[10]=IVM10_DEV,FILES[14]=IVM14_DEV
0555 LET PARAMS$[0]=N0$,PARAMS$[1]=I1$,PARAMS$[2]=I2$,PARAMS$[3]=I3$,M9$="00"
0570 LET PERIOD$=I4$(1,2),YEAR$=I4$(3,2),XLIMIT$="",XLIMIT=0,M8$="#0.00"
0580 LET M8=LEN(M8$),M9=LEN(M9$),FILES[15]=IVM15_DEV,SEQUENCE$="D"
0700 REM " --- Background"
0710 PRINT 'SB',@(32,3),"Period:",@(34,4),"Year:",@(30,5),"Sequence:",@(24,6),"Above Or Below:",@(23,7),"Number Of Turns:",'SF',
0750 CALL "IVC.IB","B","",8,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0900 REM 
0990 GOSUB 5000
1000 REM " --- Period"
1010 LET V0$="N",V1$="C",V2$=PERIOD$,V3$=M9$,V0=2,V1=40,V2=3,I0=0
1020 LET V4$="Enter The Base Period For This Report (1 Through "+STR(G[0])+")"
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,3900,1000,3900
1050 IF V<1 OR V>G[0] THEN GOTO 1000
1060 LET PERIOD$=STR(V:M9$),PERIOD=V,DESCRIPTION$=G3$((V-1)*16+27,16)
1070 PRINT @(V1,V2),PERIOD$," ",DESCRIPTION$,
1090 GOTO 3900
1200 REM " --- Year"
1210 LET V0$="D",V1$="CY",V2$=YEAR$,V3$="",V0=2,V1=40,V2=4
1220 LET V4$="Enter The Base Year For This Report"
1230 GOSUB 7000
1240 ON V3 GOTO 1250,1200,3900,1200,3900
1250 IF V$="" THEN GOTO 1200
1260 LET YEAR$=V$
1270 PRINT @(V1,V2),FNYY21_YY$(YEAR$),
1290 GOTO 3900
1400 REM " --- Sequence"
1410 LET V0$="S",V1$="C",V2$=SEQUENCE$,V3$="AD",V0=1,V1=40,V2=5
1420 LET V4$="Sort Items By Turns Within Product In Ascending Or Descending Order (A/D)?"
1430 GOSUB 7000
1440 ON V3 GOTO 1450,1400,3900,1400,3900
1450 IF POS(V$="AD")=0 THEN GOTO 1400
1460 LET SEQUENCE$=V$
1490 GOTO 3900
1600 REM " --- Above of Below"
1610 LET V0$="S",V1$="C^",V2$=XLIMIT$,V3$="",V0=1,V1=40,V2=6
1620 LET V4$="Limit To Items Above/Below Specified Number Of Turns (A=Above/B=Below/<F1>=All)"
1630 GOSUB 7000
1640 ON V3 GOTO 1660,1650,3900,1600,3900
1650 LET V$=""
1660 IF V$="" THEN LET X$="All" ELSE LET X$=V$
1665 IF V$<>"" THEN IF POS(V$="AB")=0 THEN GOTO 1600
1670 PRINT @(V1,V2),X$,'CL',
1680 LET XLIMIT$=V$
1690 GOTO 3900
1800 REM " --- Number of turns"
1810 LET V0$="N",V1$="C",V2$=STR(XLIMIT),V3$=M8$,V0=M8,V1=40,V2=7,X$="Above"
1815 IF XLIMIT$="" THEN GOTO 1900
1820 IF XLIMIT$="B" THEN LET X$="Below"
1825 LET V4$="Only Items Whose Number Of Turns Is "+X$+" This Value Will be Listed"
1830 GOSUB 7000
1840 ON V3 GOTO 1850,1800,3900,1800,3900
1850 IF V<0 OR XLIMIT>99.99 THEN GOTO 1800
1860 LET XLIMIT=V
1870 PRINT @(V1,V2),XLIMIT:M8$,'CL',
1890 GOTO 3900
1900 REM " --- No limit"
1910 PRINT @(V1,V2),'CL',
1990 GOTO 3900
2000 REM " --- Process standard input"
2010 CALL "IVC.IB","I","",8,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
2020 ON V3 GOTO 2030,2000,3900,2000,3900
2090 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4030 ON POS(V$="YN") GOTO 4000,4050,1000
4090 RUN "IVR.KB"
5000 REM " --- Display"
5010 LET DESCRIPTION$="",X$="All"
5020 LET V=NUM(PERIOD$,ERR=5030),DESCRIPTION$=G3$((V-1)*16+27,16)
5030 IF XLIMIT$<>"" THEN LET X$=XLIMIT$,Y$=STR(XLIMIT:M8$)
5070 PRINT @(40,3),PERIOD$," ",DESCRIPTION$,'CL',@(40,4),FNYY21_YY$(YEAR$),@(40,5),SEQUENCE$,@(40,6),X$,@(40,7),Y$,'CL',
5080 CALL "IVC.IB","D","",8,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

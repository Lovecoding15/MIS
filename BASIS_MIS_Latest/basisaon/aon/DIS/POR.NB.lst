0010 REM "POR - PO Receipt Variance Report (Print Overlay)
0020 REM "Program POR.NB
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 APM01A: IOLIST C0$(1),C1$(1)
0260 IVM01A: IOLIST *,X1$(1)
0290 POT04A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0300 POT14A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1),B4$(1),B5$,B6$,B[ALL]
0310 POM02A: IOLIST S0$(1),S1$(1)
0500 REM " --- Init Data
0510 PRECISION IVPRECISION
0520 DIM A0$(22),A1$(160),A[8],VENDTOT[3],PREVK$(31),VENDOR$(6),PREVVEND$(6)
0530 LET PRINTVTOT$="N",PRINTPTOT$="N"
0600 REM " --- Headings and print positions
0610 LET HEADINGS=4
0620 DIM HEADINGS$[HEADINGS],O[14]
0630 LET WIDTH=132,PAGE=0,L9=59,L=L9+1
0650 GOSUB ASSIGNHEADS
0800 REM " --- Print positions"
0810 LET O[1]=O[0]+3,O[2]=O[1]+3,O[3]=O[2]+4,O[4]=O[3]+3
0820 LET O[5]=O[4]+3,O[6]=O[5]+ITEMLEN+1,O[7]=O[6]+DESCLEN+1,O[8]=O[7]+9
0830 LET O[9]=O[8]+9,O[10]=O[9]+10,O[11]=O[10]+MVAR+2
0840 IF O[11]+7<WIDTH THEN GOTO 0950
0850 LET O[11]=WIDTH-MVAR,O[10]=O[11]-MVAR,O[9]=O[10]-MVAR,O[8]=O[9]-MVAR
0950 REM " --- Position files
0960 CALL "SYC.GA",7,1,"","",STATUS
0970 IF STATUS THEN GOTO 9900
0980 CALL "SYC.NB","Printing",22,COLUMN
1500 REM " --- Init Read
1540 READ (POT44_DEV,KEY=N0$+VENDFROM$+DATEFROM$,DOM=READSORT)
1600 REM " --- Read through Sort file
1605 READSORT: 
1620 LET K$=KEY(POT44_DEV,END=4000)
1640 IF K$(1,2)<>N0$ THEN GOTO 4000
1650 READ (POT44_DEV); REM "Move ptr
1700 REM " --- Valid Vendor/Receipt Date?
1710 IF VENDFROM$<>FILL(6) AND K$(3,6)>VENDTHRU$ THEN GOTO 4000
1720 IF DATEFROM$<>FILL(6) AND K$(9,6)<DATEFROM$ THEN GOTO 1760
1730 IF DATETHRU$<>FILL(6) AND K$(9,6)>DATETHRU$ THEN GOTO 1790
1740 LET VENDOR$=K$(3,6),PONUM$=K$(15,7),RECEIVER$=K$(22,7),LINENUM$=K$(29,3)
1750 GOTO GETHEADER
1760 REM " --- Skip to first receipt date for vendor
1770 READ (POT44_DEV,KEY=K$(1,8)+DATEFROM$,DOM=1780)
1780 GOTO NEXTSORT
1790 REM " --- Skip to next vendor
1800 READ (POT44_DEV,KEY=K$(1,8)+$FF$,DOM=1810)
1810 GOTO NEXTSORT
1900 REM " --- Get Header & assign variables
1905 GETHEADER: 
1920 IF K$(1,28)=PREVK$(1,28) THEN GOTO GETDETAIL
1930 LET PREVK$(1)=K$
1940 FIND (POT04_DEV,KEY=N0$+VENDOR$+PONUM$+RECEIVER$,DOM=NEXTSORT)IOL=POT04A
1950 DIM RCPTDATE$(6),ORDDATE$(6)
1960 IF A1$(29,6)<>FILL(6) THEN LET RCPTDATE$=A1$(29,6)
1970 IF A1$(5,6)<>FILL(6) THEN LET ORDDATE$=A1$(5,6)
2000 REM " --- Get detail 
2005 GETDETAIL: 
2010 DIM B0$(25),B1$(48),B2$(32),B3$(22),B4$(40),B[12]
2020 FIND (POT14_DEV,KEY=A0$+LINENUM$,DOM=NEXTSORT)IOL=POT14A
2030 LET WHSE$=B3$(1,2),ITEM$=B3$(3,20)
2150 REM " --- Valid Whse/Item?
2160 IF WHSEFOR$<>"" THEN IF WHSE$<>WHSEFOR$ THEN GOTO NEXTSORT
2170 IF ITEMFROM$<>FILL(20) THEN IF ITEM$<ITEMFROM$ THEN GOTO NEXTSORT
2180 IF ITEMTHRU$<>FILL(20) THEN IF ITEM$>ITEMTHRU$ THEN GOTO NEXTSORT
2200 REM " --- Valid line type (ie S or N)?
2210 GOSUB GETLINETYPE
2220 IF POS(LINETYPE$="SN")=0 THEN GOTO NEXTSORT
2300 REM " --- Valid, so assign vars
2310 PRINT @(COLUMN,11),FNF$(A0$(3,VENDLEN),VENDOMASK$)," ",A0$(9,7)," ",A0$(16,7)
2330 LET LINECODE$=B1$(1,2)
2350 DIM DATEPROM$(6),REQDATE$(6)
2360 IF B1$(9,6)<>FILL(6) THEN LET DATEPROM$=B1$(9,6)
2370 IF B1$(3,6)<>FILL(6) THEN LET REQDATE$=B1$(3,6)
2400 REM " --- Calculate variances
2410 LET REQVARI=0,PROMVARI=0
2420 IF RCPTDATE$=FILL(6) THEN GOTO 2490
2430 IF REQDATE$=FILL(6) THEN GOTO 2460
2435 IF RCPTDATE$<REQDATE$ THEN LET MULTIPLIER=-1 ELSE LET MULTIPLIER=1
2440 LET FROMDATE$=RCPTDATE$,THRUDATE$=REQDATE$
2450 CALL "SYC.CA",FROMDATE$,THRUDATE$,REQVARI
2455 LET REQVARI=REQVARI*MULTIPLIER
2460 IF DATEPROM$=FILL(6) THEN GOTO 2490
2465 IF RCPTDATE$<DATEPROM$ THEN LET MULTIPLIER=-1 ELSE LET MULTIPLIER=1
2470 LET FROMDATE$=RCPTDATE$,THRUDATE$=DATEPROM$
2480 CALL "SYC.CA",FROMDATE$,THRUDATE$,PROMVARI
2485 LET PROMVARI=PROMVARI*MULTIPLIER
2490 IF ABS(REQVARI)<MINVARI AND ABS(PROMVARI)<MINVARI THEN GOTO NEXTSORT
2500 REM " --- Test breaks
2510 IF VENDOR$=PREVVEND$ THEN GOTO TESTPO
2520 IF PRINTPTOT$="Y" THEN GOSUB POTOTALS
2530 IF PRINTVTOT$="Y" THEN GOSUB VENDTOTALS
2540 GOSUB NEWVENDOR
2550 GOSUB NEWPO
2560 GOTO 2600
2565 TESTPO: 
2570 IF PONUM$=PREVPO$ THEN GOTO 2600
2580 IF PRINTPTOT$="Y" THEN GOSUB POTOTALS
2590 GOSUB NEWPO
2600 REM " --- Get Item Desc
2610 DIM ITEMDESC$(60),X1$(60)
2620 IF LINETYPE$="N" THEN GOTO 2700
2630 LET ITEMDESC$(1)="(Not On File)"
2640 FIND (IVM01_DEV,KEY=N0$+B3$(3,20),DOM=2800)IOL=IVM01A
2650 LET ITEMDESC$(1)=FNITEM$(X1$,DESCLEN[1],DESCLEN[2],DESCLEN[3])
2690 GOTO 2800
2700 REM " --- Non-stock description"
2710 LET ITEMDESC$(1)=B4$
2800 REM " --- Go print the data
2810 GOSUB PRINTLINE
2900 REM " --- Accumulate totals; then get next detail
2910 LET PO_REQVAR=PO_REQVAR+REQVARI,PO_PROMVAR=PO_PROMVAR+PROMVARI
2920 LET VENDREQVAR=VENDREQVAR+REQVARI,VENDPROMVAR=VENDPROMVAR+PROMVARI
2930 LET RPTREQVAR=RPTREQVAR+REQVARI,RPTPROMVAR=RPTPROMVAR+PROMVARI
3000 REM " --- Loop up for next header record via sort files
3010 NEXTSORT: 
3060 GOTO READSORT
4000 REM " --- All Done
4010 IF L+1>L9 THEN GOSUB 5000
4020 GOSUB POTOTALS
4030 GOSUB VENDTOTALS
4060 PRINT (7)@(O[10]-18),"Total For Report",@(O[10]),RPTREQVAR:MVAR$,@(O[11]),RPTPROMVAR:MVAR$
4090 GOTO 9900
5000 REM " --- Heading
5010 LET L=HEADINGS+1
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS THEN EXITTO 9900
5140 PRINT (7)@(O[7]),"Date",@(O[8]),"Date",@(O[9]),"Date",@(O[10]+MVAR-8),"Variance",@(O[11]+MVAR-8),"Variance"
5150 PRINT (7)@(O[2]),"Seq",@(O[3]),"Cd",@(O[4]),"Wh",@(O[5]),"Item",@(O[7]),"Received",@(O[8]),"Required",@(O[9]),"Promised",@(O[10]+MVAR-8),"Req/Rec",@(O[11]+MVAR-8),"Prom/Rec"
5160 PRINT (7)""
5180 LET L=L+3
5190 RETURN 
5300 REM " --- Assign headings variables
5305 ASSIGNHEADS: 
5310 DIM TMPH3$(WIDTH),TMPH4$(WIDTH)
5320 IF VENDFROM$=FILL(6) THEN LET VENDF$="First" ELSE LET VENDF$=FNF$(VENDFROM$(1,VENDLEN),VENDOMASK$)
5330 IF VENDTHRU$=FILL(6) THEN LET VENDT$="Last" ELSE LET VENDT$=FNF$(VENDTHRU$(1,VENDLEN),VENDOMASK$)
5340 IF DATEFROM$=FILL(6) THEN LET DATEF$="First" ELSE LET DATEF$=FNB6$(DATEFROM$)
5350 IF DATETHRU$=FILL(6) THEN LET DATET$="Last" ELSE LET DATET$=FNB6$(DATETHRU$)
5360 IF ITEMFROM$=FILL(20) THEN LET ITEMF$="First" ELSE LET ITEMF$=ITEMFROM$(1,ITEMLEN)
5370 IF ITEMTHRU$=FILL(20) THEN LET ITEMT$="Last" ELSE LET ITEMT$=ITEMTHRU$(1,ITEMLEN)
5380 LET MIDH3$="From Item "+ITEMF$+" Through "+ITEMT$
5390 LET TMPH3$(1)="From Vendor: "+VENDF$,TMPH3$(FNC(MIDH3$,WIDTH))=MIDH3$
5400 LET TMPH3$(WIDTH-LEN(DATEF$)-18)="From Receipt Date: "+DATEF$
5410 IF WHSEFOR$="" THEN LET MIDH4$="All Warehouses" ELSE LET MIDH4$="For Warehouse "+WHSEFOR$
5420 LET TMPH4$(1)="  To Vendor: "+VENDT$,TMPH4$(FNC(MIDH4$,WIDTH))=MIDH4$
5430 LET TMPH4$(WIDTH-LEN(DATEF$)-18)="  To Receipt Date: "+DATET$
5440 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$+" For Variances Greater Than Or Equal To "+STR(MINVARI:MPCNT$)
5450 LET HEADINGS$[2]=TMPH3$,HEADINGS$[3]=TMPH4$
5490 RETURN 
6100 REM " --- New Vendor
6105 NEWVENDOR: 
6120 DIM C0$(8),C1$(195),VENDTOT[3]
6130 LET VENDNAME$="Not On File",PREVVEND$=VENDOR$,PRINTVTOT$="Y"
6140 LET VENDREQVAR=0,VENDPROMVAR=0
6150 FIND (APM01_DEV,KEY=N0$+VENDOR$,DOM=6170)IOL=APM01A
6160 LET VENDNAME$=C1$(1,30)
6170 IF L+4>L9 THEN GOSUB 5000
6180 PRINT (7)@(0),FNF$(VENDOR$(1,VENDLEN),VENDOMASK$)," ",VENDNAME$
6200 LET L=L+1
6290 RETURN 
6300 REM " --- New PO
6305 NEWPO: 
6310 DIM RDATE$(8),ODATE$(8)
6320 LET PO_REQVAR=0,PO_PROMVAR=0,PREVPO$=PONUM$,PRINTPTOT$="Y"
6330 IF RCPTDATE$<>FILL(6) THEN LET RDATE$=FNB6$(RCPTDATE$)
6340 IF ORDDATE$<>FILL(6) THEN LET ODATE$=FNB6$(ORDDATE$)
6410 IF L+3>L9 THEN GOSUB 5000
6440 PRINT (7)@(O[1]),"PO #: ",PONUM$,@(18),"Ordered: ",ODATE$
6470 LET L=L+1
6490 RETURN 
6500 REM " --- Print Vendor total
6505 VENDTOTALS: 
6530 IF L+1>L9 THEN GOSUB 5000
6540 PRINT (7)@(O[10]-19-LEN(VENDOMASK$)),"Total For Vendor ",FNF$(PREVVEND$(1,VENDLEN),VENDOMASK$),@(O[10]),VENDREQVAR:MVAR$,@(O[11]),VENDPROMVAR:MVAR$
6550 LET L=L+1
6590 RETURN 
6600 REM " --- Print PO total
6605 POTOTALS: 
6640 IF L+1>L9 THEN GOSUB 5000
6650 PRINT (7)@(O[10]-24),"Total For PO # ",PREVPO$,@(O[10]),PO_REQVAR:MVAR$,@(O[11]),PO_PROMVAR:MVAR$
6660 LET L=L+1
6690 RETURN 
6800 REM " --- Get Line Type
6805 GETLINETYPE: 
6810 DIM S0$(4),S1$(32)
6820 LET LINETYPE$="N"
6830 FIND (POM02_DEV,KEY=B0$(1,2)+B1$(1,2),DOM=6890)IOL=POM02A
6840 LET LINETYPE$=S1$(21,1)
6890 RETURN 
6900 REM " --- Print data line
6905 PRINTLINE: 
6910 DIM DPROM$(8),QDATE$(8)
6920 IF RCPTDATE$<>FILL(6) THEN LET RDATE$=FNB6$(RCPTDATE$)
6930 IF DATEPROM$<>FILL(6) THEN LET DPROM$=FNB6$(DATEPROM$)
6940 IF REQDATE$<>FILL(6) THEN LET QDATE$=FNB6$(REQDATE$)
6960 IF L+1>L9 THEN GOSUB 5000
6970 PRINT (7)@(O[2]),LINENUM$,@(O[3]),LINECODE$,@(O[4]),WHSE$,@(O[5]),ITEM$(1,ITEMLEN),@(O[6]),ITEMDESC$(1,DESCLEN),@(O[7]),RDATE$,@(O[8]),QDATE$,@(O[9]),DPROM$,@(O[10]),REQVARI:MVAR$,@(O[11]),PROMVARI:MVAR$
6980 LET L=L+1
6990 RETURN 
8000 REM " --- Functions
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

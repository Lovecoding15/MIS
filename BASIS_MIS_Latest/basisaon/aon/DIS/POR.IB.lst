0010 REM "POR - Open Purchase Orders Report (Print Overlay)
0020 REM "Program POR.IB 
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 APM01A: IOLIST C0$(1),C1$(1)
0220 APM05A: IOLIST R0$(1),R1$(1)
0230 APM10C: IOLIST D0$(1)
0240 ARM01A: IOLIST *,CUSTINFO$(1)
0250 ARM03A: IOLIST *,DROPINFO$(1)
0260 IVM01A: IOLIST *,X1$(1)
0270 IVM05A: IOLIST *,*,VPARTNUM$(1)
0280 IVM10C: IOLIST X0$
0290 POE02A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0300 POE12A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1),B4$(1),B5$,B6$,B[ALL]
0310 POM02A: IOLIST S0$(1),S1$(1)
0500 REM " --- Init Data
0510 PRECISION IVPRECISION
0520 DIM A0$(15),A1$(160),A[8],VENDOR$(6)
0530 LET GRANDTOT=0,PRINTVTOT$="N"
0540 LET INITIALIZING$="Y"; REM "Init files 1st time through
0600 REM " --- Headings and print positions
0610 LET HEADINGS=2
0620 DIM HEADINGS$[HEADINGS],O[13]
0630 LET WIDTH=132,PAGE=0,L9=59,L=L9+1
0640 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$
0650 GOSUB ASSIGNHEADS
0660 ON POS(PRINTSEQ$="PAV") GOTO 4000,PRINTBYPO,PRINTBYALT,PRINTBYVEND
0670 PRINTBYPO: 
0680 LET HEADINGS$[1]=HEADINGS$[1]+" By Purchase Order"
0690 GOTO POSITIONS
0700 PRINTBYALT: 
0710 LET HEADINGS$[1]=HEADINGS$[1]+" By Alternate Sequence"
0720 GOTO POSITIONS
0730 PRINTBYVEND: 
0740 LET HEADINGS$[1]=HEADINGS$[1]+" By Vendor"
0750 GOTO POSITIONS
0760 POSITIONS: 
0770 LET O[1]=O[0]+4,O[2]=O[1]+3,O[3]=O[2]+3,O[4]=O[3]+ITEMLEN+1
0780 LET O[5]=O[4]+9,O[6]=O[5]+9,O[7]=O[6]+9,O[8]=O[7]+3
0790 LET O[9]=O[8]+M5+1,O[10]=O[9]+M2+1,O[11]=O[10]+M2+1,O[12]=O[11]+M2+1
0800 LET O[13]=O[12]+M3+1
0810 IF O[13]+15<WIDTH THEN GOTO 0950
0820 LET O[13]=WIDTH-M1,O[12]=O[13]-M3,O[11]=O[12]-M2,O[10]=O[11]-M2
0830 LET O[9]=O[10]-M2,O[8]=O[9]-M5
0950 REM " --- Position files
0960 CALL "SYC.GA",7,1,"","",STATUS
0970 IF STATUS THEN GOTO 9900
0980 CALL "SYC.NB","Printing",14,COLUMN
1000 REM " --- Sorts
1010 SORTORDER: 
1020 ON POS(PRINTSEQ$="PAV") GOTO 4000,SORTBYPO,SORTBYALT,SORTBYVEND
1100 REM " --- Sort by Purchase Order
1110 SORTBYPO: 
1120 IF INITIALIZING$="N" THEN GOTO KEYBYPO
1130 LET INITIALIZING$="N"
1140 READ (POE32_DEV,KEY=N0$+POFROM$+VENDFROM$,DOM=KEYBYPO)
1150 KEYBYPO: 
1160 LET K32$=KEY(POE32_DEV,END=4000)
1170 LET K$=K32$(1,2)+K32$(10,6)+K32$(3,7)
1180 READ (POE32_DEV)
1190 GOTO TESTKEY
1200 REM " --- Sort by alternate sequence
1210 SORTBYALT: 
1220 IF INITIALIZING$="N" THEN GOTO KEYBYALT
1230 LET INITIALIZING$="N"
1260 READ (APM04_DEV,KEY=N0$+"A"+VENDFROM$,DOM=KEYBYALT)
1270 REM " --- Find a POE02 record using alt seq
1280 KEYBYALT: 
1290 LET K04A$=KEY(APM04_DEV,END=4000)
1300 IF K04A$(1,3)<>N0$+"A" THEN GOTO 4000
1310 LET K04B$=K04A$(1,2)+K04A$(14,6)
1320 READ (APM04_DEV); REM "Move ptr
1330 READ (POE02_DEV,KEY=K04B$,DOM=1340)
1340 LET K01$=KEY(POE02_DEV,END=NEXTALT)
1350 IF K01$(1,8)<>K04B$ THEN GOTO NEXTALT
1360 LET K$=K01$
1370 GOTO TESTKEY
1380 NEXTALT: 
1390 GOTO KEYBYALT
1500 REM " --- Sort by vendor
1510 SORTBYVEND: 
1520 IF INITIALIZING$="N" THEN GOTO KEYBYVEND
1530 LET INITIALIZING$="N"
1540 READ (POE02_DEV,KEY=N0$+VENDFROM$+POFROM$,DOM=KEYBYVEND)
1600 REM " --- Read through PO headers
1610 KEYBYVEND: 
1620 LET K$=KEY(POE02_DEV,END=4000)
1630 TESTKEY: 
1640 IF K$(1,2)<>N0$ THEN GOTO 4000
1650 READ (POE02_DEV,KEY=K$)IOL=POE02A
1700 REM " --- Test Ranges
1710 LET HEADRECPRINTED$="N"
1720 IF POFROM$<>FILL(7) AND A0$(9,7)<POFROM$ THEN GOTO NEXTHEADER
1730 IF POTHRU$<>FILL(7) AND A0$(9,7)>POTHRU$ THEN GOTO NEXTHEADER
1740 IF VENDFROM$<>FILL(6) AND A0$(3,6)<VENDFROM$ THEN GOTO NEXTHEADER
1750 IF VENDTHRU$<>FILL(6) AND A0$(3,6)>VENDTHRU$ THEN GOTO NEXTHEADER
1760 IF WHSE$<>"ALL" AND A1$(1,2)<>WHSE$ THEN GOTO READDETAIL
1770 GOSUB GETTESTDATE_HEADER
1780 IF DATEFROM$<>"" AND TESTDATEH$<DATEFROM$ THEN GOTO READDETAIL
1790 IF DATETHRU$<>"" AND TESTDATEH$>DATETHRU$ THEN GOTO READDETAIL
1800 REM " --- Header passed tests
1810 GOSUB TESTBREAKS
1830 LET HEADRECPRINTED$="Y"
2000 REM " --- Test if any detail are w/in date range, if so, print line
2010 READDETAIL: 
2020 DIM B0$(18),B1$(48),B2$(32),B3$(22),B4$(40),B[12]
2030 READ (POE12_DEV,KEY=A0$(1,15),DOM=READ_POE12)
2100 REM " --- Test PO Detail date range
2110 READ_POE12: 
2120 LET K11$=KEY(POE12_DEV,END=DONE_DETAIL)
2130 IF K11$(1,15)<>A0$ THEN GOTO DONE_DETAIL
2140 READ (POE12_DEV,KEY=K11$)IOL=POE12A
2150 IF WHSE$<>"ALL" AND B3$(1,2)<>WHSE$ THEN GOTO NEXTDETAIL
2160 GOSUB GETTESTDATE_DETAIL
2170 IF DATEFROM$<>"" AND TESTDATED$<DATEFROM$ THEN GOTO NEXTDETAIL
2175 IF DATETHRU$<>"" AND TESTDATED$>DATETHRU$ THEN GOTO NEXTDETAIL
2180 REM " --- Detail passed, so print header if nec & print detail
2190 PRINT @(COLUMN,11),FNF$(A0$(3,VENDLEN),VENDOMASK$)," ",A0$(9,7),
2200 IF HEADRECPRINTED$="Y" THEN GOTO 2240
2210 GOSUB TESTBREAKS
2230 LET HEADRECPRINTED$="Y"
2240 GOSUB PRINTDETAIL
2250 REM " --- Accumulate totals; then get next detail
2260 NEXTDETAIL: 
2270 LET POTOT=POTOT+EXTENSION,VENDTOT=VENDTOT+EXTENSION
2280 LET GRANDTOT=GRANDTOT+EXTENSION
2285 LET EXTENSION=0,EXTENSION$=""
2290 GOTO READ_POE12
2300 REM " --- Done w/detail for this PO
2310 DONE_DETAIL: 
2320 IF HEADRECPRINTED$="Y" THEN GOSUB POTOTALS
2400 REM " --- Loop up for next header record via sort files
2410 NEXTHEADER: 
2420 IF PRINTSEQ$<>"A" THEN GOTO 2460
2430 REM " --- See if there's another PO for vend before getting next alt seq
2440 LET K$=KEY(POE02_DEV,END=2460)
2450 IF K$(3,6)=VENDOR$ THEN GOTO TESTKEY
2460 GOTO SORTORDER
4000 REM " --- All Done
4020 IF L+2>L9 THEN GOSUB 5000
4030 IF PRINTSEQ$<>"P" THEN GOSUB VENDTOTALS
4040 PRINT (7)@(O[13]-17),"Total For Report",@(O[13]),GRANDTOT:M1$
4050 GOTO 9900
4100 REM " --- Print Detail Subroutine
4105 PRINTDETAIL: 
4110 REM " --- Set up variables
4120 DIM DATEREQ$(8),DATEPROM$(8),NOTB4DATE$(8)
4130 LET WHSE_DETAIL$=B3$(1,2),ITEMNUM$=B3$(3,20),MEMO$=B4$,UNITMEAS$=B1$(22,2)
4140 IF B1$(3,6)<>FILL(6) THEN LET DATEREQ$=FNB6$(B1$(3,6))
4150 IF B1$(9,6)<>FILL(6) THEN LET DATEPROM$=FNB6$(B1$(9,6))
4160 IF B1$(15,6)<>FILL(6) THEN LET NOTB4DATE$=FNB6$(B1$(15,6))
4170 LET CONVFACT$=STR(B[0]:M5$),QTYORD$=STR(B[3]:M2$),QTYREC$=STR(B[7]:M2$)
4180 LET BALDUE$=STR(B[3]-B[7]:M2$),UNITCOST$=STR(B[1]:M3$)
4190 LET WONUM$=B2$(1,7),WOSEQ$=B2$(8,3)
4200 REM " --- Get Drop Ship flag & Line Type
4210 DIM S0$(4),S1$(32)
4220 LET LINETYPE$="N",DROPSHIP$="N"
4230 FIND (POM02_DEV,KEY=B0$(1,2)+B1$(1,2),DOM=4300)IOL=POM02A
4240 LET LINETYPE$=S1$(21,1),DROPSHIP$=S1$(22,1)
4300 REM " --- Get Item Desc for S and N type lines
4310 IF LINETYPE$="N" THEN LET ITEMDESC$=MEMO$; GOTO 4360
4320 DIM ITEMDESC$(60),X1$(60)
4330 LET ITEMDESC$(1)="Not On File"
4340 FIND (IVM01_DEV,KEY=N0$+B3$(3,20),DOM=4360)IOL=IVM01A
4350 LET ITEMDESC$(1)=FNITEM$(X1$,DESCLEN[1],DESCLEN[2],DESCLEN[3])
4360 REM " --- Get extension
4370 GOSUB CALC_EXTENSION
4380 REM " --- Get Vendor's Part #
4390 DIM VPARTNUM$(20)
4400 FIND (IVM05_DEV,KEY=B0$(1,8)+ITEMNUM$,DOM=4500)IOL=IVM05A
4500 REM " --- Format common part of print line
4510 DIM PLINE$(132)
4520 LET PLINE$(1)=B0$(16,3)+" "+B1$(1,2)+" "+WHSE_DETAIL$
4530 REM " --- Print Line Types 'M', 'O' & 'V'
4540 IF POS(LINETYPE$="MOV")=0 THEN GOTO NOT_MOV
4550 IF LINETYPE$="V" THEN LET PLINE$(O[3]+1)=MEMO$(1,20) ELSE LET PLINE$(O[3]+1)=MEMO$
4560 IF LINETYPE$="O" THEN LET PLINE$(O[12]+1,M3)=UNITCOST$,PLINE$(O[13]+1)=EXTENSION$
4570 IF L+1>L9 THEN GOSUB 5000
4580 PRINT (7)PLINE$
4590 LET L=L+1
4600 GOTO DONE_PRINTDETAIL
4610 REM " --- Print Line Types 'S' & 'N'
4615 NOT_MOV: 
4620 IF POS(LINETYPE$="SN")=0 THEN GOTO DONE_PRINTDETAIL
4630 LET PLINE$(O[3]+1)=ITEMNUM$(1,ITEMLEN),PLINE$(O[4]+1)=DATEREQ$
4640 LET PLINE$(O[5]+1)=DATEPROM$,PLINE$(O[6]+1)=NOTB4DATE$,PLINE$(O[7]+1)=UNITMEAS$
4650 IF LINETYPE$="S" THEN LET PLINE$(O[8]+1)=CONVFACT$
4660 IF L+2>L9 THEN GOSUB 5000
4670 PRINT (7)PLINE$,@(O[9]),QTYORD$,@(O[10]),QTYREC$,@(O[11]),BALDUE$,@(O[12]),UNITCOST$,@(O[13]),EXTENSION$
4680 PRINT (7)@(O[3]),ITEMDESC$(1,40),
4690 IF WONUM$<>FILL(7) THEN PRINT (7)@(O[6]+4),"WO: ",WONUM$,"-",WOSEQ$ ELSE PRINT (7)""
4700 LET L=L+2
4710 IF VPARTNUM$=FILL(20) THEN GOTO DROPSHIP
4720 IF L+1>L9 THEN GOSUB 5000
4730 PRINT (7)@(O[3]),"Vendor's Part Number ",VPARTNUM$
4740 LET L=L+1
4800 REM " --- Print drop ship info if applicable
4810 DROPSHIP: 
4820 IF DROPSHIP$<>"Y" OR AR$="N" THEN GOTO DONE_PRINTDETAIL
4830 DIM CUSTINFO$(321),DROPINFO$(173),CUSTADDR$(120),DROPADDR$(120)
4840 LET CUSTNAME$="Not On File",DROPNAME$="Not On File"
4850 FIND (ARM01_DEV,KEY=B0$(1,2)+B2$(11,6),DOM=4880)IOL=ARM01A
4860 LET CUSTNAME$=CUSTINFO$(1,30),CUSTADDR$=CUSTINFO$(31,72)+FILL(24)+CUSTINFO$(103,9)
4870 CALL "SYC.AA",CUSTADDR$,24,4,9,30
4880 FIND (ARM03_DEV,KEY=B0$(1,2)+B2$(11,6)+B2$(27,6),DOM=4910)IOL=ARM03A
4890 LET DROPNAME$=DROPINFO$(1,30),DROPADDR$=DROPINFO$(31,72)+FILL(24)+DROPINFO$(103,9)
4900 CALL "SYC.AA",DROPADDR$,24,4,9,30
4910 IF L+6>L9 THEN GOSUB 5000
4920 PRINT (7)@(O[3]),"Drop Ship Above Line To Customer",@(43),FNF$(B2$(11,CUSTLEN),CUSTOMASK$)," Order ",B2$(17,7)," ",B2$(24,3),@(73),"Ship To:",@(82),FNF$(B2$(27,CUSTLEN),CUSTOMASK$)
4930 PRINT (7)@(38),"Name",@(43),CUSTNAME$,@(82),DROPNAME$
4940 PRINT (7)@(35),"Address",@(43),CUSTADDR$(1,30),@(82),DROPADDR$(1,30)
4950 LET L=L+3
4960 IF CUSTADDR$(31,30)<>FILL(30) OR DROPADDR$(31,30)<>FILL(30) THEN PRINT (7)@(43),CUSTADDR$(31,30),@(82),DROPADDR$(31,30); LET L=L+1
4970 IF CUSTADDR$(61,30)<>FILL(30) OR DROPADDR$(61,30)<>FILL(30) THEN PRINT (7)@(43),CUSTADDR$(61,30),@(82),DROPADDR$(61,30); LET L=L+1
4980 IF CUSTADDR$(91,30)<>FILL(30) OR DROPADDR$(91,30)<>FILL(30) THEN PRINT (7)@(43),CUSTADDR$(91,30),@(82),DROPADDR$(91,30); LET L=L+1
4990 DONE_PRINTDETAIL: 
4995 RETURN 
5000 REM " --- Heading
5010 LET L=HEADINGS+1
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS>0 THEN EXITTO 9900
5110 PRINT (7)@(0),"From Vendor: ",VENDF$," ",VENDFNAME$(1,20),@(FNC(POPAGEHEAD$,WIDTH)),POPAGEHEAD$,@(117-LEN(DATETYPE$)),"From ",DATETYPE$,": ",DATEF$
5120 PRINT (7)@(2),"To Vendor: ",VENDT$," ",VENDTNAME$(1,20),@(FNC(WHSEPAGEHEAD$,WIDTH)),WHSEPAGEHEAD$,@(119-LEN(DATETYPE$)),"To ",DATETYPE$,": ",DATET$
5130 PRINT (7)""
5140 PRINT (7)@(O[4]),"Date",@(O[5]),"Date",@(O[6]),"Not",@(O[8]+M5-6),"Conv",@(O[9]+M2-9),"Quantity",@(O[10]+M2-9),"Quantity",@(O[11]+M2-8),"Balance",@(O[12]+M3-5),"Unit"
5150 PRINT (7)@(O[0]),"Seq",@(O[1]),"Cd",@(O[2]),"Wh",@(O[3]),"Item",@(O[4]),"Required",@(O[5]),"Promised",@(O[6]),"Before",@(O[7]),"UM",@(O[8]+M5-6),"Factor",@(O[9]+M2-8),"Ordered",@(O[10]+M2-9),"Received",@(O[11]+M2-8),"Due",@(O[12]+M3-5),"Cost",@(O[13]+M1-10),"Extension"
5160 PRINT (7)""
5180 LET L=L+6
5190 RETURN 
5300 REM " --- Assign headings variables
5305 ASSIGNHEADS: 
5320 IF VENDFROM$=FILL(6) THEN LET VENDF$="First" ELSE LET VENDF$=FNF$(VENDFROM$(1,VENDLEN),VENDOMASK$)
5330 IF VENDTHRU$=FILL(6) THEN LET VENDT$="Last" ELSE LET VENDT$=FNF$(VENDTHRU$(1,VENDLEN),VENDOMASK$)
5340 IF POFROM$=FILL(7) THEN LET POF$="First" ELSE LET POF$=POFROM$
5350 IF POTHRU$=FILL(7) THEN LET POT$="Last" ELSE LET POT$=POTHRU$
5360 IF DATEFROM$="" THEN LET DATEF$="First" ELSE LET DATEF$=FNB6$(DATEFROM$)
5370 IF DATETHRU$="" THEN LET DATET$="Last" ELSE LET DATET$=FNB6$(DATETHRU$)
5380 LET POPAGEHEAD$="From Purchase Order "+POF$+" Through "+POT$
5390 LET WHSEPAGEHEAD$="For Warehouse "+WHSE$+" "+CVS(WHSENAME$,2)
5395 RETURN 
5400 REM " --- Determine date to use when testing range
5410 GETTESTDATE_HEADER: 
5420 ON POS(WHICHDATE$="OPRD") GOTO ORDR_H,ORDR_H,PROM_H,REQD_H,NOTB4_H
5430 ORDR_H: 
5440 LET TESTDATEH$=A1$(5,6)
5450 GOTO 5550
5460 PROM_H: 
5470 LET TESTDATEH$=A1$(11,6)
5480 GOTO 5550
5490 REQD_H: 
5500 LET TESTDATEH$=A1$(23,6)
5510 GOTO 5550
5520 NOTB4_H: 
5530 LET TESTDATEH$=A1$(17,6)
5540 GOTO 5550
5550 RETURN 
5600 REM " --- Determine date to use when testing range in detail               
5605 GETTESTDATE_DETAIL: 
5610 ON POS(WHICHDATE$="OPRD") GOTO ORDR_D,ORDR_D,PROM_D,REQD_D,NOTB4_D
5620 ORDR_D: 
5630 LET TESTDATED$=A1$(5,6)
5640 GOTO 5740
5650 PROM_D: 
5660 LET TESTDATED$=B1$(9,6)
5670 GOTO 5740
5680 REQD_D: 
5690 LET TESTDATED$=B1$(3,6)
5700 GOTO 5740
5710 NOTB4_D: 
5720 LET TESTDATED$=B1$(15,6)
5730 GOTO 5740
5740 RETURN 
6000 REM " --- Test for Breaks
6005 TESTBREAKS: 
6010 IF A0$(3,6)=VENDOR$ THEN GOTO TESTPO
6020 IF PRINTVTOT$="Y" AND PRINTSEQ$<>"P" THEN GOSUB VENDTOTALS
6030 GOSUB NEWVENDOR
6040 IF PRINTSEQ$<>"P" THEN GOTO TESTPO
6050 GOSUB NEWPO
6060 GOTO 6090
6070 TESTPO: 
6080 IF A0$(9,7)<>PONUM$ THEN GOSUB NEWPO
6090 RETURN 
6100 REM " --- New Vendor
6110 NEWVENDOR: 
6120 DIM C0$(8),C1$(195)
6130 LET VENDTOT=0,VENDNAME$="Not On File",VENDOR$=A0$(3,6)
6140 LET PRINTVTOT$="Y"
6150 FIND (APM01_DEV,KEY=N0$+VENDOR$,DOM=6170)IOL=APM01A
6160 LET VENDNAME$=C1$(1,30)
6170 IF L+2>L9 THEN GOSUB 5000
6180 PRINT (7)@(0),"Vendor ",FNF$(VENDOR$(1,VENDLEN),VENDOMASK$)," ",VENDNAME$
6200 LET L=L+1
6210 RETURN 
6300 REM " --- New PO
6310 NEWPO: 
6320 LET POTOT=0,PONUM$=A0$(9,7)
6330 LET TERMSDESC$="Not On File",PADDRDESC$="",SHPWHSDESC$="Not On File"
6340 DIM D0$(26),R0$(10),R1$(160),X0$(160)
6350 FIND (APM10_DEV,KEY=N0$+"C"+A1$(38,2),DOM=6370)IOL=APM10C
6360 LET TERMSDESC$=D0$(6,20)
6370 FIND (APM05_DEV,KEY=N0$+VENDOR$+A1$(3,2),DOM=6390)IOL=APM05A
6380 LET PADDRDESC$=R1$(1,24)
6390 FIND (IVM10_DEV,KEY=N0$+"C"+A1$(1,2),DOM=6410)IOL=IVM10C
6400 LET SHPWHSDESC$=X0$(6,24)
6410 IF L+5>L9 THEN GOSUB 5000
6420 DIM DATEORD$(8)
6430 IF A1$(5,6)<>FILL(6) THEN LET DATEORD$=FNB6$(A1$(5,6))
6435 PRINT (7)""
6440 PRINT (7)@(0),"PO#: ",PONUM$,@(14),"Ordered ",FNB6$(A1$(5,6)),@(33),"Terms ",A1$(38,2)," ",TERMSDESC$,@(65),"Ship Via ",A1$(59,15),
6450 IF PADDRDESC$<>"" THEN PRINT (7)@(91),"Purchase From ",A1$(3,2)," ",PADDRDESC$ ELSE PRINT (7)""
6460 PRINT (7)@(32),"Ack By ",A1$(74,20),@(67),"F.O.B. ",A1$(94,15),@(97),"Ship To ",A1$(1,2)," ",SHPWHSDESC$
6470 PRINT (7)""
6480 LET L=L+4
6490 RETURN 
6500 REM " --- Print Vendor total
6510 VENDTOTALS: 
6520 IF L+1>L9 THEN GOSUB 5000
6530 PRINT (7)@(O[13]-18-LEN(VENDOMASK$)),"Total For Vendor ",FNF$(VENDOR$(1,VENDLEN),VENDOMASK$),@(O[13]),VENDTOT:M1$
6550 LET L=L+1
6560 RETURN 
6600 REM " --- Print Purchase Order total
6610 POTOTALS: 
6620 IF L+1>L9 THEN GOSUB 5000
6630 PRINT (7)@(O[13]-23),"Total For PO # ",PONUM$,@(O[13]),POTOT:M1$
6650 LET L=L+1
6660 RETURN 
6900 REM " --- Calc extension for S, N and O types
6905 CALC_EXTENSION: 
6920 IF POS(LINETYPE$="SNO")=0 THEN GOTO 6990
6930 LET QTY=B[3]-B[7]
6940 IF LINETYPE$="O" THEN LET QTY=1
6950 PRECISION 2
6960 LET EXTENSION=B[1]*QTY,EXTENSION$=STR(EXTENSION:M1$)
6970 PRECISION IVPRECISION
6990 RETURN 
8000 REM " --- Functions
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

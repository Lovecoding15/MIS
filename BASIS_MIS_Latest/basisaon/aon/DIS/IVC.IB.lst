0010 REM "IVC - Standard Report Options Input"
0020 REM "Program IVC.IB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0041 REM "ACTION$    : Action to perform (input)"
0042 REM "             B = Display background literals and lines"
0043 REM "             D = Display passed criteria and descriptions"
0044 REM "             I = Process input"
0045 REM "             L = Include lines on screen"
0050 REM "EXCLUDE$   : Fields to exclude from processing (input)
0051 REM "             D = Detail Level"
0052 REM "             W = Beginning/ending warehouse"
0053 REM "ROW        : Beginning screen row (input)"
0054 REM "CHANNELS[N]: Channel Numbers (input)"
0055 REM "              [0] = SYS-01"
0056 REM "              [1] = IVM-01"
0057 REM "             [10] = IVM-10"
0058 REM "             [14] = IVM-14"
0059 REM "             [15] = IVM-15"
0060 REM "PARAMS$[n] : Inventory Item Parameters (input)"
0061 REM "             [0]        = Firm ID"
0062 REM "             [1] to [4] = Parameter Strings (P1$ to P4$)"
0064 REM "OPTIONS$[n]: Inventory Item Information (input/returned)"
0065 REM "             [0] = Detail level"
0066 REM "             [1] = Beginning warehouse"
0067 REM "             [2] = Ending warehouse"
0068 REM "             [3] = Beginning product"
0069 REM "             [4] = Ending product"
0070 REM "             [5] = Beginning product"
0071 REM "             [6] = Ending product"
0072 REM "             [7] = Item type"
0075 REM "FKEY       : Function key value (input/returned)"
0080 REM 
0085 SETERR 9000
0087 SETESC 9000
0090 ENTER ACTION$,EXCLUDE$,ROW,CHANNELS[ALL],PARAMS$[ALL],OPTIONS$[ALL],FKEY
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1)
0220 IVM10A: IOLIST X0$(1)
0230 IVM10C: IOLIST X0$(1)
0240 IVM15A: IOLIST Y0$(1),Y1$(1)
0400 REM " --- Parameters"
0410 DIM I[3],FILES[14],FIELDS$(8,"Y")
0420 LET I2$=PARAMS$[2],I3$=PARAMS$[3],I[0]=NUM(I2$(1,2)),N0$=PARAMS$[0]
0430 LET I[1]=NUM(I2$(9,2)),I[2]=NUM(I2$(11,2)),I[3]=NUM(I2$(13,2))
0500 REM " --- Initializations"
0510 DIM O[7],LITERALS$[7],A0$(22),A1$(60),X0$(32),Y0$(5),Y1$(20)
0530 LET FILES[1]=CHANNELS[1],FILES[14]=CHANNELS[14],V3=0
0540 LET IVM01_DEV=CHANNELS[1],IVM10_DEV=CHANNELS[10],IVM15_DEV=CHANNELS[15]
0545 LET V5$="Enter The ",V6$=" To Include On This Report "
0550 LET V7$="(<F1>=First/<F3>=Lookup)",V8$="(<F1>=Last/<F3>=Lookup)"
0560 LET LITERALS$[0]="Detail Level",LITERALS$[7]="Item Type"
0565 LET LITERALS$[1]="Beginning Warehouse",LITERALS$[2]="Ending Warehouse"
0570 LET LITERALS$[3]="Beginning Product",LITERALS$[4]="Ending Product"
0575 LET LITERALS$[5]="Beginning Item",LITERALS$[6]="Ending Item"
0580 IF POS("D"=EXCLUDE$)>0 THEN LET FIELDS$(1,1)="N"
0585 IF POS("W"=EXCLUDE$)>0 THEN LET FIELDS$(2,2)="NN"
0590 IF POS(OPTIONS$[0]="IW")=0 THEN LET OPTIONS$[0]="W"
0600 REM " --- Row positions"
0610 LET LINES=POS("L"=ACTION$)>0,O[0]=ROW
0620 FOR X=1 TO 3
0630 LET O[X]=O[X-1]+1+LINES
0640 NEXT X
0650 IF FIELDS$(1,1)="N" THEN LET O[1]=O[1]-1-LINES,O[2]=O[2]-1-LINES,O[3]=O[3]-1-LINES
0660 IF FIELDS$(2,1)="N" THEN LET O[3]=O[3]-2-(LINES*2)
0670 FOR X=4 TO 7
0680 LET O[X]=O[X-1]+1+LINES
0690 NEXT X
0700 REM " --- Background"
0710 IF POS("B"=ACTION$)=0 THEN GOTO 0800
0720 FOR X=0 TO 7
0730 IF FIELDS$(X+1,1)="N" THEN GOTO 0790
0740 PRINT 'SB',@(38-LEN(LITERALS$[X]),O[X]),LITERALS$[X],":",'SF',
0750 IF LINES THEN CALL "SYC.WC",1,0,80,0,0,O[X]+1,0
0790 NEXT X
0800 REM " --- Set warehouse fields if not multi-warehouse"
0810 IF I3$(1,1)="Y" THEN GOTO 1000
0820 LET X0$(1)=N0$+"C"+I3$(6,2),OPTIONS$[1]="",OPTIONS$[2]=""
0830 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=0900)
0840 LET OPTIONS$[1]=I3$(6,2),OPTIONS$[2]=I3$(6,2)
1000 REM " --- Start at bottom?"
1010 IF FKEY<>2 THEN GOTO 1300
1020 LET I0=8,V3=2,FKEY=0
1090 GOTO 3900
1300 REM " --- Display"
1310 LET FKEY=0
1320 IF POS("D"=ACTION$)>0 OR FKEY=2 THEN GOSUB 5000
1340 IF POS("I"=ACTION$)=0 THEN GOTO 4000
2000 REM " --- Detail Level"
2010 LET V4$="Enter The Detail Level For This Report (I=By Item/W=By Warehouse)"
2020 LET V0$="S",V1$="C^",V2$=OPTIONS$[0],V3$="IW",V0=1,V1=40,V2=O[0],I0=0
2025 IF FIELDS$(1,1)="N" THEN GOTO 3900
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 IF POS(V$="IW")=0 THEN GOTO 2000
2060 LET OPTIONS$[0]=V$
2070 GOSUB 5200
2090 IF V$="W" THEN GOTO 3900
2100 REM " --- Reset beginning/ending warehouse if item level is selected"
2110 LET V$="",OPTIONS$[1]=V$,X$="First",V2=O[1]
2120 GOSUB 5300
2130 LET V$="",OPTIONS$[2]=V$,X$="Last",V2=O[2]
2140 GOSUB 5300
2190 GOTO 3900
2200 REM " --- Beginning Warehouse"
2205 IF FIELDS$(2,1)="N" THEN GOTO 3900
2210 IF FIELDS$(1,1)="Y" THEN IF OPTIONS$[0]="I" THEN GOTO 3900
2215 IF I3$(1,1)<>"Y" THEN GOTO 3900
2220 LET V4$=V5$+"Beginning Warehouse To Include "+V7$,X$="First"
2225 LET V0$="S",V1$="C",V2$=OPTIONS$[1],V3$="",V0=2,V1=40,V2=O[1]
2230 GOSUB 7000
2240 ON V3 GOTO 2300,2250,2310,2270,3900
2250 LET V$=""
2260 GOTO 2300
2270 CALL "IVC.LK",3,V1,V2,V$
2300 REM 
2310 GOSUB 5300
2320 IF STATUS>0 THEN GOTO 2200
2330 LET OPTIONS$[1]=V$
2390 GOTO 3900
2400 REM " --- Ending Warehouse"
2405 IF FIELDS$(2,1)="N" THEN GOTO 3900
2410 IF FIELDS$(1,1)="Y" THEN IF OPTIONS$[0]="I" THEN GOTO 3900
2415 IF I3$(1,1)<>"Y" THEN GOTO 3900
2420 LET V4$=V5$+"Ending Warehouse"+V6$+V8$,X$="Last"
2425 LET V0$="S",V1$="C",V2$=OPTIONS$[2],V3$="",V0=2,V1=40,V2=O[2]
2430 GOSUB 7000
2440 ON V3 GOTO 2500,2450,2510,2470,3900
2450 LET V$=""
2460 GOTO 2500
2470 CALL "IVC.LK",3,V1,V2,V$
2500 REM 
2510 GOSUB 5300
2520 IF STATUS>0 THEN GOTO 2400
2530 LET OPTIONS$[2]=V$
2590 GOTO 3900
2600 REM " --- Beginning Product"
2610 LET V4$=V5$+"Beginning Product"+V6$+V7$,X$="First"
2620 LET V0$="S",V1$="C",V2$=OPTIONS$[3],V3$="",V0=3,V1=40,V2=O[3]
2630 GOSUB 7000
2640 ON V3 GOTO 2700,2650,2710,2670,3900
2650 LET V$=""
2660 GOTO 2700
2670 CALL "IVC.LK",1,V1,V2,V$
2700 REM 
2710 GOSUB 5400
2720 IF STATUS>0 THEN GOTO 2600
2730 LET OPTIONS$[3]=V$
2790 GOTO 3900
2800 REM " --- Ending Product"
2810 LET V4$=V5$+"Ending Product"+V6$+V8$,X$="Last"
2820 LET V0$="S",V1$="C",V2$=OPTIONS$[4],V3$="",V0=3,V1=40,V2=O[4]
2830 GOSUB 7000
2840 ON V3 GOTO 2900,2850,2910,2870,3900
2850 LET V$=""
2860 GOTO 2900
2870 CALL "IVC.LK",1,V1,V2,V$
2900 REM 
2910 GOSUB 5400
2920 IF STATUS>0 THEN GOTO 2800
2930 LET OPTIONS$[4]=V$
2990 GOTO 3900
3000 REM " --- Beginning Item"
3010 DIM ITEM$[6],ITEM[12]
3020 LET V1=40,V2=O[5],V2$=OPTIONS$[5],ACTION$="F",V4$=V5$+"Beginning Item"+V6$
3030 CALL "IVC.IA",ACTION$,V1,V2,FILES[ALL],N0$,V2$,V4$,I[ALL],ITEM$[ALL],ITEM[ALL],V3
3040 ON V3 GOTO 3070,3050,3120,3000,3900
3050 LET V$=""
3060 GOTO 3100
3070 LET V$=ITEM$[0]
3100 REM 
3110 LET X$="First"
3120 GOSUB 5500
3130 IF STATUS>0 THEN GOTO 3000
3140 LET OPTIONS$[5]=V$
3190 GOTO 3900
3200 REM " --- Ending Item"
3210 DIM ITEM$[6],ITEM[12]
3220 LET V1=40,V2=O[6],V2$=OPTIONS$[6],ACTION$="L",V4$=V5$+"Ending Item"+V6$
3230 CALL "IVC.IA",ACTION$,V1,V2,FILES[ALL],N0$,V2$,V4$,I[ALL],ITEM$[ALL],ITEM[ALL],V3
3240 ON V3 GOTO 3270,3250,3320,3200,3900
3250 LET V$=""
3260 GOTO 3300
3270 LET V$=ITEM$[0]
3300 REM 
3310 LET X$="Last"
3320 GOSUB 5500
3330 IF STATUS>0 THEN GOTO 3200
3340 LET OPTIONS$[6]=V$
3390 GOTO 3900
3400 REM " --- Item Type"
3410 LET V4$="Enter A Selected Item Type"+V6$+"(<F1>=All/<F3>=Lookup)"
3420 LET V0$="S",V1$="C",V2$=OPTIONS$[7],V3$="",V0=3,V1=40,V2=O[7]
3430 GOSUB 7000
3440 ON V3 GOTO 3500,3450,3510,3470,3900
3450 LET V$="",V3=0
3460 GOTO 3500
3470 CALL "IVC.LK",6,V1,V2,V$
3500 REM 
3510 GOSUB 5600
3520 IF STATUS>0 THEN GOTO 3400
3530 LET OPTIONS$[7]=V$
3590 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3915 IF I0<1 THEN GOTO 4000
3920 LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 ON I0 GOTO 2000,2200,2400,2600,2800,3000,3200,3400,3990
3990 GOSUB 5000
4000 REM " --- All done"
4010 IF V3=2 OR V3=4 THEN LET FKEY=V3
4090 GOTO 9900
5000 REM " --- Display"
5010 LET V$=OPTIONS$[0]
5015 GOSUB 5200
5020 LET V$=OPTIONS$[1],V2=O[1],X$="First"
5025 GOSUB 5300
5030 LET V$=OPTIONS$[2],V2=O[2],X$="Last"
5035 GOSUB 5300
5040 LET V$=OPTIONS$[3],V2=O[3],X$="First"
5045 GOSUB 5400
5050 LET V$=OPTIONS$[4],V2=O[4],X$="Last"
5055 GOSUB 5400
5060 LET V$=OPTIONS$[5],V2=O[5],X$="First"
5065 GOSUB 5500
5070 LET V$=OPTIONS$[6],V2=O[6],X$="Last"
5075 GOSUB 5500
5080 LET V$=OPTIONS$[7]
5085 GOSUB 5600
5090 RETURN 
5200 REM " --- Detail Level"
5210 IF FIELDS$(1,1)="N" THEN GOTO 5290
5220 LET X$="(By Item)"
5230 IF V$="W" THEN LET X$="(By Warehouse)"
5240 PRINT @(40,O[0]),V$," ",X$,'CL',
5290 RETURN 
5300 REM " --- Warehouse"
5310 IF FIELDS$(2,1)="N" THEN GOTO 5390
5320 LET STATUS=0
5330 IF V$="" THEN GOTO 5380
5340 LET X$="",STATUS=1,X0$(1)=N0$+"C"+V$
5350 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=5380)IOL=IVM10C
5360 LET STATUS=0,V$=X0$(4,2),X$=V$+" "+X0$(6,24)
5380 PRINT @(40,V2),X$,'CL',
5390 RETURN 
5400 REM " --- Product"
5410 LET STATUS=0
5420 IF V$="" THEN GOTO 5480
5430 LET X$="",STATUS=1,X0$(1)=N0$+"A"+V$
5440 FIND (IVM10_DEV,KEY=X0$(1,6),DOM=5480)IOL=IVM10A
5450 LET STATUS=0,V$=X0$(4,3),X$=V$+" "+X0$(7,20)
5480 PRINT @(40,V2),X$,'CL',
5490 RETURN 
5500 REM " --- Item"
5510 LET STATUS=0
5520 IF V$="" THEN GOTO 5580
5530 LET X$="",STATUS=1,A0$(1)=N0$+V$
5540 FIND (IVM01_DEV,KEY=A0$,DOM=5580)IOL=IVM01A
5550 LET STATUS=0,V$=A0$(3),X$=V$(1,I[0])+" "+FNITEM$(A1$,I[1],I[2],I[3])
5560 IF LEN(X$)>39 THEN LET X$=X$(1,39)
5580 PRINT @(40,V2),X$,'CL',
5590 RETURN 
5600 REM " --- Item Type"
5610 LET X$="All",Y0$(1)=N0$+V$,Y1$(1)="",STATUS=0
5620 IF V$="" THEN GOTO 5680
5625 LET STATUS=1
5630 FIND (IVM15_DEV,KEY=Y0$,DOM=5650)IOL=IVM15A
5640 LET STATUS=0
5650 LET X$=Y0$(3)
5680 PRINT @(40,O[7]),X$," ",Y1$(1,20),'CL',
5690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9950 EXIT 
9999 END

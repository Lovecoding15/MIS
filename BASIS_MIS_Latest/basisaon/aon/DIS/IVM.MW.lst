0010 REM "IVM - Item Maintenance (BOM Where Used)"
0020 REM "Program IVM.MW"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 PRECISION P[1]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$,A6$(1),A[ALL]
0220 BMM02A: IOLIST B0$(1),B1$(1),B2$(1),B[ALL]
0500 REM " --- Initializations"
0510 DIM B0$(22),B1$(20),B2$(115),B[10],O[4]
0520 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,1]
0550 LET O[4]=H0-MAX(M2,11),O[3]=O[4]-9,O[2]=O[3]-9,O[1]=O[2]-3
0555 LET O[0]=3+MAX(P[0],17),DW=O[1]-O[0]-1,ITEM$=A0$
0560 LET HEADING$(1)=" Bill Number",HEADING$(O[0]-4)="Seq Description"
0565 LET HEADING$(O[1])="UM",HEADING$(O[2]+1)="Effect"
0570 LET HEADING$(O[3])="Obsolete",HEADING$(H0-10)="Qty Req'd"
0575 LET FOOTING$(1)="",FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0580 LET L=1,X0=0,X1=1,PAGE=1,SELECTION$=""
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0740 PRINT @(0,0),'SB','BR',HEADING$,'ER','SF',
0750 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 IF PAGES<3 THEN GOTO 4000
0990 READ (BMM04_DEV,KEY=ITEM$,DOM=1000)
1000 REM " --- Read next BMM-04 record"
1010 LET K$=KEY(BMM04_DEV,END=3000)
1020 IF POS(ITEM$=K$)<>1 THEN GOTO 3000
1030 READ (BMM04_DEV)
1200 REM " --- Display page?"
1210 IF L+NEEDED<=MAXROW THEN GOTO 1400
1220 GOSUB 5400
1230 IF FKEY=1 THEN GOTO 1000
1240 IF FKEY=4 THEN GOTO 4000
1250 IF SELECTION$<>"" THEN GOTO 1000
1290 LET L=1,X1=1
1400 REM " --- Retrieve material record and bill description"
1410 DIM B0$(25),B1$(20),B2$(115),B[10],DESCRIPTION$(DW)
1420 LET B0$(1)=N0$+K$(23)
1430 FIND (BMM02_DEV,KEY=B0$,DOM=1900)IOL=BMM02A
1440 LET A0$(1)=N0$+B0$(3),A1$(1)="(Not On File)"
1450 FIND (IVM01_DEV,KEY=A0$,DOM=1460)IOL=IVM01A
1460 LET DESCRIPTION$(1)=FNITEM$(A1$,P[3],P[4],P[5])
1500 REM " --- Store detail line"
1510 DIM G1$(WIDTH-2)
1520 LET G1$(2)=B0$(3,P[0]),G1$(O[0]-4)=B0$(23,3)+" "+DESCRIPTION$
1523 LET G1$(O[1])=B2$(33,2)
1530 IF FNP$(B2$(5,3))<>"" THEN LET G1$(O[2])=FNB$(B2$(5,3))
1540 IF FNP$(B2$(8,3))<>"" THEN LET G1$(O[3])=FNB$(B2$(8,3))
1590 LET G1$(O[4])=STR(B[0]:M2$),LLIST$[PAGE,X1,1]=G1$,X1=X1+1,X0=X0+1,L=L+1
1900 REM " --- Loop back for next record"
1990 GOTO 1000
3000 REM " --- No more transactions"
3010 IF X0>0 THEN GOTO 3900
3020 DIM MESSAGE$[1]
3030 LET MESSAGE$[0]="This Item Is Not Used In Any Bill Of Material (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
3090 GOTO 4000
3900 REM " --- Display last page"
3910 LET SELECTION$="|EOF"
3920 GOSUB 5400
4000 REM " --- Run menu overlay"
4010 CALL "SYC.WD",NAME$
4020 LET A0$(1)=ITEM$
4030 READ (IVM01_DEV,KEY=A0$,DOM=4090)IOL=IVM01A
4090 RUN "IVM.MA"
5400 REM " --- Middle window processing"
5410 LET MODE=5
5420 CALL "SYC.SA",MODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAXROW,NAME$,HEIGHT,WIDTH,FKEY
5480 IF FKEY=-16 THEN LET PAGE=PAGE+1
5485 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAXROW,5,1
5490 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9910 CALL "SYC.WD",NAME$
9950 RUN "SYS.AA"
9999 END

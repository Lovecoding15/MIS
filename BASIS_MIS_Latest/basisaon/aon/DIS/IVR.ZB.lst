0010 REM "IVR - Zero Balance Inventory Report (Report Overlay)"
0020 REM "Program IVR.ZB
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 CALL "SYC.GA",7,1,"","",STATUS
0120 IF STATUS THEN GOTO 9900
0200 REM " --- IOLists"
0210 IVT05A: IOLIST A0$,A1$
0220 IVM02A: IOLIST B0$,B1$(1),B2$,B[ALL]
0240 IVM01A: IOLIST D0$,D1$(1),D2$,D3$,D4$,D5$,D6$,D[ALL]
0250 IVM10A: IOLIST HEAD$
0260 IVM10C: IOLIST H3$
0270 APM01A: IOLIST *,VNDNAM$(1)
0500 REM " --- Init Data"
0510 LET M2=LEN(M2$),P9=P[3]+P[4]+P[5]
0520 IF P9>132-10-M2-(4+P[0])-22 THEN LET P9=132-10-M2-(4+P[0])-22
0530 LET HEADINGS=4,H=0,WIDTH=132,WHEN$=F0$(7,3),CLOCK$="",L9=59,L=L9+1
0540 DIM A0$(24),A1$(30),DESC$(P9),HEADINGS$[HEADINGS]
0550 LET H4$="All Zero Balance Items"
0560 IF P9$<>"" THEN LET H4$=H4$+" Since "+FNB$(P9$)
0570 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,HEADINGS$[3]=H4$
0700 REM " --- Background"
0710 PRINT @(0,11),'CL',
0720 CALL "SYC.NB","Printing",P[0]+SORT+3,COL
1000 REM " --- Init Read"
1010 IF P8$="I" THEN READ (IVT05_DEV,KEY=N0$,DOM=1100)
1020 READ (IVM03_DEV,KEY=N0$+SORT$,DOM=1100)
1100 REM " --- Main Read"
1110 IF P8$="I" THEN GOTO 1150
1120 LET K$=KEY(IVM03_DEV,END=4000)
1125 IF POS(" "<>K$(3))=0 THEN READ (IVM03_DEV); GOTO 1100
1130 IF POS(N0$+SORT$=K$)<>1 THEN GOTO 4000
1135 READ (IVM03_DEV)
1140 GOTO 1200
1150 LET K$=KEY(IVT05_DEV,END=4000)
1155 IF POS(" "<>K$(3))=0 THEN READ (IVT05_DEV); GOTO 1150
1160 IF POS(N0$=K$)<>1 THEN GOTO 4000
1170 READ (IVT05_DEV)IOL=IVT05A
1180 GOTO 1300
1200 REM " --- Breaks"
1220 READ (IVT05_DEV,KEY=N0$+K$(4,2)+K$(6+SORT),DOM=1100)IOL=IVT05A
1260 IF K$(4,2)<>T0$ THEN GOSUB 6000
1280 IF SUB<>0 THEN IF K$(6,SUB)<>T1$ THEN GOSUB 6200
1300 REM " --- Process Info"
1310 PRINT @(COL,11),A0$(3,2)," ",K$(6,SORT)," ",A0$(5,P[0]),
1320 IF P9$<>"" THEN IF A1$(1,3)<P9$ THEN GOTO 1100
1400 REM " --- Additional Reads"
1410 DIM B1$(30),B[20]
1440 FIND (IVM02_DEV,KEY=A0$,DOM=1500)IOL=IVM02A
1450 IF B[0]>0 THEN REMOVE (IVT05_DEV,KEY=A0$); GOTO 1100
1455 DIM D1$(60),D2$(63),D[12]; LET D1$(1)="*** Not On File ***"
1460 FIND (IVM01_DEV,KEY=N0$+A0$(5),DOM=1470)IOL=IVM01A
1470 LET DESC$(1)=FNZ$(D1$,P[3],P[4],P[5])
1500 IF AP$<>"Y" THEN GOTO 1600
1505 LET X=30
1510 IF X+P9>132-10-M2-(4+P[0])-22-(9+LEN(APMSK$)) THEN IF X>20 THEN LET X=X-5; GOTO 1510 ELSE LET P9=P9-1; GOTO 1510
1515 DIM VNDNAM$(X)
1520 FIND (APM01_DEV,KEY=N0$+B1$(22,6),DOM=1530)IOL=APM01A
2100 IF L+1>L9 THEN GOSUB 5200
2120 PRINT (7)A0$(3,2)," ",A0$(5,P[0])," ",DESC$(1,P9),@(P[0]+P9+6),D2$(1,3),"  ",B1$(11,2),"   ",B1$(1,10),
2130 IF AP$="Y" THEN PRINT (7)@(P[0]+P9+27),B1$(19,3),"  ",FNF$(B1$(22,APLEN),APMSK$)," ",VNDNAM$,
2140 PRINT (7)@(132-M2-10),FNB$(A1$(1)),@(132-M2),B[0]:M2$
2200 LET L=L+1,COUNT=COUNT+1
2900 GOTO 1100
4000 REM " --- End"
4010 IF H=0 THEN GOSUB 5000
4020 PRINT (7)""
4030 PRINT (7)STR(COUNT)," Item(s)"
4090 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS<>0 THEN EXITTO 9900
5040 PRINT (7)@(4),"Item",@(P[0]+P9+6),"Prod Cycle",
5050 IF AP$="Y" THEN PRINT (7)@(P[0]+P9+27),"Buyer",
5060 PRINT (7)@(132-8-M2),"Date",@(132-4),"On"
5070 PRINT (7)@(0),"Whs",@(4),"Code",@(P[0]+4),"Description",@(P[0]+P9+6),"Code Code  Location",
5080 IF AP$="Y" THEN PRINT (7)@(P[0]+P9+27),"Code Vendor",
5090 PRINT (7)@(132-9-M2),"Flagged",@(132-5),"Hand",'LF'
5100 LET L=L+3
5190 RETURN 
5200 REM " --- Sub Heading"
5210 IF L+2>L9 THEN GOSUB 5000
5220 IF SUB=0 THEN GOTO 5290
5240 PRINT (7)'LF',@(0),HEAD$
5250 LET L=L+2
5290 RETURN 
6000 REM " --- Warehouse Break"
6010 IF T9$<>"" THEN RETURN 
6020 DIM H3$(30)
6030 LET T0$=K$(3+LEN(SORT$),2),L=L9+1
6040 FIND (IVM10_DEV,KEY=N0$+"C"+T0$,DOM=6050)IOL=IVM10C
6050 LET H3$="Warehouse Code: "+T0$+" "+H3$(6,20),H3$=FNP$(H3$)
6060 LET HEADINGS$[2]=H3$
6090 RETURN 
6200 REM " --- Sort Sub Total"
6210 IF T9$<>"" THEN RETURN 
6220 DIM HEAD$(30)
6230 LET T1$=K$(6,SUB)
6240 FIND (IVM10_DEV,KEY=N0$+SUB$+T1$,DOM=6250)IOL=IVM10A
6250 LET HEAD$=SUBHEAD$+" "+T1$+" "+HEAD$(3+SUB,20),HEAD$=FNP$(HEAD$)
6260 GOSUB 5200
6290 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNZ$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

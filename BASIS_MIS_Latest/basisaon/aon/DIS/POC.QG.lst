0010 REM "POC - Purchase Order SpeedSearch (Requisition Line Items)"
0020 REM "Program POC.QG"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0060 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open/Lock Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APM-01",FILES$[2]="POE-01",FILES$[3]="POE-11"
0140 LET FILES$[4]="POM-02",FILES$[5]="IVM-01"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0170 LET APM01_DEV=CHANNELS[1],POE01_DEV=CHANNELS[2],POE11_DEV=CHANNELS[3]
0180 LET POM02_DEV=CHANNELS[4],IVM01_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 APM01A: IOLIST A0$(1),A1$(1)
0220 POE01A: IOLIST B0$(1),B1$(1),B2$,B3$,B[ALL]
0230 IVM01A: IOLIST C0$(1),C1$(1)
0260 APS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,I5$,M1$,M2$,M3$,M4$,M5$
0310 POE11A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0320 POM02A: IOLIST Y0$(1),Y1$(1)
0400 REM " --- Parameters"
0405 DIM P[2],I[5]
0410 LET X$=STBL("!SPEEDSEARCH"),N0$=X$(1,2),MAXPAGE=NUM(X$(66,2))
0415 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0420 LET SNAME$=FNP$(X$(19,8)),SWIDTH=NUM(X$(43,2)),SHEIGHT=NUM(X$(45,2))
0425 LET MAXOPT=NUM(X$(57,2)),SYS01_DEV=NUM(STBL("!SYS01"))
0430 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0440 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0450 LET PAGES=INT((DSZ-5000)/4000),TNAME$=FNP$(X$(3,8)),ONAME$=FNP$(X$(27,8))
0460 IF PAGES<3 THEN GOTO 9900
0470 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0480 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9900)IOL=APS01A
0485 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9900)IOL=IVS01A
0490 LET P[0]=NUM(P2$(1,2)),I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1))
0495 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0500 REM " --- Initializations"
0510 DIM A0$(8),A1$(30),B0$(15),B1$(160),B[8],C0$(22),C1$(60)
0520 DIM Y0$(4),Y1$(32),W0$(18),W1$(48),W2$(32),W3$(22),W4$(40),W[12]
0530 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),OPTLIST$[2,MAXOPT,6]
0560 LET D_STATUS=0,MAXROW=HEIGHT-5,PAGE=1,PGM$=PGM(-2)
0570 LET A0$(1)=N0$+OPTION$[3],B0$(1)=N0$+OPTION$[3],P=13+I[0]
0600 REM " --- Resize masks"
0610 CALL "SYC.RM",M1$,M1,12,12
0620 CALL "SYC.RM",M2$,M2,9,9
0630 CALL "SYC.RM",M3$,M3,12,12
0640 CALL "SYC.RM",M5$,M5,7,7
0650 REM " --- Option Window"
0660 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,ONAME$
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0720 LET HEADING$(2)="Seq CD Wh Item",HEADING$(P)="Description"
0730 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0740 PRINT @(0,0),'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",SNAME$,0
0920 PRINT 'CF',
0930 CALL "SYC.WB",TNAME$,0
0940 PRINT @(0,1),'CL',@(0,2),'CL',@(0,3),'CL',
1000 REM " --- Process key field"
1010 GOSUB 6000
1020 FIND (APM01_DEV,KEY=A0$,DOM=1030)IOL=APM01A
1030 FIND (POE01_DEV,KEY=B0$,DOM=1040)IOL=POE01A
1200 REM " --- Retrieve records for display"
1210 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,SHEIGHT-2],X$(WIDTH-2)
1220 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,SELECTION$=""
1230 LET X$(1)='SB'+" Vendor: "+'SF'+FNF$(A0$(3,P[0]),M0$)+" "+A1$(1,30)
1240 LET X1$=A0$(3,6),X2$=PARAM$
1250 CALL "SYC.QB",X$,X1$,X2$
1260 LET X$(1)='SB'+"  Req #: "+'SF'+B0$(9,7)
1270 LET X1$=B0$(9,7),X2$=SUBPARAM$
1280 CALL "SYC.QB",X$,X1$,X2$
1290 READ (POE11_DEV,KEY=B0$,DOM=1400)
1400 REM " --- Read next line item"
1410 LET K$=KEY(POE11_DEV,END=1900)
1420 IF POS(B0$=K$)<>1 THEN GOTO 1900
1430 READ (POE11_DEV)IOL=POE11A
1440 IF N0$+W1$(1,2)=Y0$ THEN GOTO 1500
1450 LET Y0$(1)=N0$+W1$(1,2)
1460 FIND (POM02_DEV,KEY=Y0$,DOM=1800)IOL=POM02A
1500 REM " --- Display page?"
1510 IF L<MAXROW THEN GOTO 1600
1520 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
1530 GOSUB 5000
1540 IF DONE THEN GOTO 4000
1570 IF ANSWER$<>"" THEN GOTO 3000
1590 LET L=1,X1=1
1600 REM " --- Middle window record"
1610 DIM G$(WIDTH-2),C0$(22),C1$(60)
1620 LET C0$(1)=N0$+W3$(3)
1630 IF POS(Y1$(21,1)="S") THEN FIND (IVM01_DEV,KEY=C0$,DOM=1640)IOL=IVM01A
1640 LET G$(2)=W0$(16,3)+" "+W1$(1,2)+" "+W3$(1,2)
1650 IF POS(Y1$(21,1)="MO") THEN LET G$(12)=W4$
1660 IF POS(Y1$(21,1)="SNV") THEN LET G$(12)=W3$(3)
1670 IF POS(Y1$(21,1)="N") THEN LET G$(P)=W4$
1680 IF POS(Y1$(21,1)="S") THEN LET G$(P)=FNITEM$(C1$,I[3],I[4],I[5])
1690 LET G$(WIDTH-2)="",LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=C0$(3)
1700 REM " --- Bottom window record"
1710 LET G$(1)="",MORE$[PAGE,X1,0]=STR(SHEIGHT-2),QTY=W[2]
1715 IF Y1$(21,1)="O" THEN LET QTY=1
1720 LET EXTENSION=(QTY*W[1])*1
1725 IF POS(Y1$(21,1)="SN") THEN LET G$(3)="{UM}"+W1$(22,2)
1730 IF POS(Y1$(21,1)="SN") THEN LET G$(14)="{Req Qty}"+STR(W[2]:M2$)
1735 IF POS(Y1$(21,1)="SNO") THEN LET G$(34)="{Unit Cost}"+STR(W[1]:M3$)
1740 LET MORE$[PAGE,X1,1]=G$,G$(1)=""
1745 IF POS(Y1$(21,1)="S") THEN LET G$(1)="{Conv}"+STR(W[0]:M5$)
1760 IF POS(Y1$(21,1)="SNO") THEN LET G$(34)="{Extension}"+STR(EXTENSION:M1$)
1765 LET MORE$[PAGE,X1,2]=G$,G$(1)=""
1770 IF FNP$(W2$(1,7))<>"" THEN LET G$(1)="{WO #}"+W2$(1,7)
1775 IF POS(Y1$(21,1)="SN") THEN LET G$(16)="{Balance}"+STR(QTY:M2$)
1780 IF FNP$(W1$(3,6))<>"" THEN LET G$(35)="{Required}"+FNB6$(W1$(3,6))
1790 LET MORE$[PAGE,X1,3]=G$,LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
1800 REM " --- Loop back for next record"
1890 GOTO 1400
1900 REM " --- No more line items"
1910 IF X0>0 THEN GOTO 2000
1920 DIM MESSAGE$[1]
1930 LET MESSAGE$[0]="No Requisition Line Items Found (<Enter>=Continue)"
1940 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
1960 IF V3=5 THEN GOTO 4000
1970 IF V3=6 THEN GOTO 4000
1990 GOTO 3000
2000 REM " --- Display last page"
2010 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2020 GOSUB 5000
2030 IF DONE THEN GOTO 4000
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$=B0$(3),CHOICE2$="",C0$(3)=ANSWER$
3030 GOSUB 5000
3040 IF CONTROL=1 THEN GOTO 1000
3050 IF FNP$(ANSWER$)<>FNP$(CHOICE1$) THEN IF DONE=0 THEN LET OPTION$[3]=C0$(3)
3090 GOTO 4000
3800 REM " --- Exit"
3810 LET OPTION$[1]="EXIT"
3890 GOTO 4000
3900 REM " --- Startover"
3910 LET OPTION$[1]="STARTOVER"
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CS',
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9950 EXIT 
9999 END

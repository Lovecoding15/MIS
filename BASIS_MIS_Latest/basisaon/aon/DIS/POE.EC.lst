0010 REM "POE - Purchase Order Invoice Entry (PO Detail)"
0020 REM "Program POE.EC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 POT04A: IOLIST B0$(1),B1$(1),B2$,B3$,B[ALL]
0220 POT14A: IOLIST C0$(1),C1$(1),C2$(1),C3$(1),C4$(1),C5$,C6$,C[ALL]
0230 POE25A: IOLIST D0$(1),D1$(1),D2$,D[ALL]
0240 IVM01A: IOLIST X0$(1),X1$(1)
0300 APM10C: IOLIST S0$(1),S[ALL]
0310 POE15A: IOLIST W0$(1),W1$(1),W2$,W[ALL]
0320 POM02A: IOLIST Y0$(1),Y1$(1)
0500 REM " --- Initializations"
0510 DIM W0$(23),W1$(32),W[4],B0$(22),B1$(160),B[8],S0$(32),S[2]
0520 DIM C0$(25),C1$(48),C2$(32),C3$(22),C4$(40),C[12],Y0$(4),Y1$(32)
0530 DIM D0$(23),D1$(96),D[4],O[8],X0$(22),X1$(60),MESSAGE$[2]
0550 LET D0$(1)=A0$,L=L0+1,TOTAL=0,O[1]=4,O[2]=7,O[3]=10,P=O[3]+I[0]+1
0560 LET OLDM1$=M1$,OLDM2$=M2$,OLDM3$=M3$,M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$)
0600 REM " --- Print positions"
0610 IF 77-M1-M2-M3-13>P THEN GOTO 0650
0620 CALL "SYC.RM",M1$,M1,10,M1-1
0630 CALL "SYC.RM",M2$,M2,9,M2-1
0640 CALL "SYC.RM",M3$,M3,6,M3-1
0645 GOTO 0600
0650 LET O[8]=77-M1,O[7]=O[8]-M3,O[6]=O[7]-3,O[5]=O[6]-M2,O[4]=O[5]-9
0660 LET DW=O[5]-O[3],MW=MIN(40,DW)
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,1,0,L0-2,0
0720 PRINT @(0,L0-1),'CE',
0730 CALL "SYC.WC",1,0,80,1,0,L0,0
0740 PRINT @(8,L0-2),'SB'," Purchase Order Line Items ",@(O[8]-8)," Total: ",'SF',TOTAL:M1$,@(0,L0-1),'SB',@(O[0]),"Seq",@(O[1]),"Cd",@(O[2]),"Wh",@(O[3]),"Item",@(O[4]),"Received",@(O[5]+M2-9),"Quantity",@(O[6]),"UM",@(O[7]+M3-5),"Cost",@(O[8]+M1-10),"Extension",'SF',
0900 REM " --- Display any existing lines"
0910 GOSUB 6000
0920 GOSUB 5600
0930 IF NUMBER=0 THEN GOTO 1200
0990 GOTO 4000
1000 REM " --- Overflow"
1010 IF L+2>L9 THEN PRINT @(0,22),'CL',@(0,L0+2),'LD','LD','LD',
1020 IF L+2>L9 THEN LET L=L9-3
1100 REM " --- Get Next Key"
1110 LET K$=KEY(POE25_DEV,END=1200)
1120 IF POS(A0$=K$)<>1 THEN GOTO 1200
1130 READ (POE25_DEV)IOL=POE25A
1190 GOTO 1330
1200 REM " --- Sequence Number"
1220 LET NUMBER=NUM(D0$(21))+INCREMENT
1230 LET V4$="Enter A 3 Digit Sequence Number"
1240 LET V0$="Z",V1$="KERC",V2$=STR(NUMBER),V3$="",V0=3,V1=0,V2=L,I0=0
1250 GOSUB 7000
1260 IF V3=4 THEN PRINT @(V1,L),FILL(3),; GOTO 4000
1270 IF V$="000" OR V$="" THEN GOTO 1200
1300 REM " --- Display record"
1310 LET D0$(21)=V$
1320 FIND (POE25_DEV,KEY=D0$,DOM=1400)IOL=POE25A
1330 IF D0$<=K9$ THEN GOSUB 5900
1340 GOSUB 5000
1390 GOTO 3300
1400 REM " --- New Record"
1410 DIM D1$(96),D[4]
1420 LET D1$(18,2)=R3$(70,2),D[1]=1
1450 PRINT @(0,L),D0$(21,3),@(4),'CL',D1$(18,2),@(4,L+1),'CL',
1490 GOTO 2400
2000 REM " --- Line Code"
2010 LET V4$="Enter A Valid Line Code (<F3>=Lookup)",I0=1
2020 LET V0$="S",V1$="C",V2$=D1$(18,2),V3$="",V0=2,V1=4,V2=L,V3=0
2025 IF FNP$(D1$(1,17))<>"" THEN GOTO 3200
2030 GOSUB 7000
2040 ON V3 GOTO 2100,2000,1200,2050,1200
2050 CALL "POC.LK",1,V1,V2,V$
2060 IF V$="" THEN GOTO 2000
2100 REM " --- Valid line code?"
2110 LET Y0$(1)=N0$+V$
2120 FIND (POM02_DEV,KEY=Y0$,DOM=2000)IOL=POM02A
2130 IF Y1$(21,1)="O" THEN GOTO 2200
2190 GOTO 2000
2200 REM " --- Store line code"
2210 LET D1$(18,2)=Y0$(3)
2220 PRINT @(V1,V2),D1$(18,2),
2290 GOTO 3200
2400 REM " --- Message (O Lines)"
2410 LET V0$="S",V1$="C",V2$=D1$(20,MW),V3$="",V0=MW,V1=O[3],V2=L+1
2420 LET V4$="Enter Up To "+STR(V0)+" Characters",I0=2,V3=0
2425 IF FNP$(D1$(1,17))<>"" THEN GOTO 3200
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,2460,2400,2460
2450 LET D1$(20,40)=V$
2460 PRINT @(V1,V2),D1$(20,MW),
2490 GOTO 3200
3000 REM " --- Cost"
3010 LET V4$="Enter The Invoice Cost For This Line Item"
3020 LET V0$="N",V1$="C",V2$=STR(D[0]),V3$=M3$,V0=M3,V1=O[7],V2=L
3030 GOSUB 7000
3040 ON V3 GOTO 3050,3000,3100,3000,3100
3050 LET D[0]=V
3100 REM " --- Display extension"
3110 LET QTY=D[1]
3120 IF FNP$(D1$(1,17))="" THEN LET QTY=1
3130 LET EXTENSION=D[0]*QTY
3140 PRINT @(V1,V2),D[0]:M3$,@(O[8]),EXTENSION:M1$,
3190 GOTO 3200
3200 REM " --- Sequence Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1200,2000,2400,3000,3300
3300 REM " --- Line Correct?"
3310 LET V4$="<Enter>=Correct     <F2>=Change     <D>=Delete"
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=D0$,I0=4
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3300,3200,3300,3350
3350 PRINT @(0,21),'CL',
3360 IF V$="" THEN GOTO 3400
3370 IF V$="D" THEN GOTO 3600
3390 GOTO 3300
3400 REM " --- Write"
3410 WRITE (POE25_DEV,KEY=D0$)IOL=POE25A
3520 GOSUB 6000
3530 LET L=L+3
3540 IF V3=4 THEN GOTO 4000
3590 GOTO 1000
3600 REM " --- Remove"
3610 REMOVE (POE25_DEV,KEY=W0$,DOM=3650)
3620 GOSUB 6000
3630 PRINT @(0,L),'CL',
3640 IF V3=4 THEN GOTO 4000
3690 GOTO 1000
4000 REM " --- Correct?"
4010 PRINT @(0,22),"Is The Above Correct (Y/N/List/Review/Delete/Line #/<F2>=PO Selection)?",
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=72
4030 GOSUB 7000
4035 ON V3 GOTO 4040,4000,4300,4000,4400
4050 IF V$="L" OR V$="LIST" THEN GOTO 4800
4055 IF V$="R" OR V$="REVIEW" THEN GOTO 4900
4060 IF V$="DELETE" THEN GOTO 4200
4065 LET V=NUM(V$,ERR=4090)
4070 IF V<1 OR V>999 THEN GOTO 4090
4075 LET V$=STR(INT(V):"000")
4080 GOTO 1300
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run GL overlay"
4110 IF GL$<>"Y" THEN GOTO 4400
4120 LET M1$=OLDM1$,M2$=OLDM2$,M3$=OLDM3$
4130 GOSUB 6000
4190 RUN "POE.ED"
4200 REM " --- Delete"
4210 PRINT @(0,L0-1),'CE',
4220 LET O0=3,M1$=OLDM1$,M2$=OLDM2$,M3$=OLDM3$
4290 RUN "POE.EA"
4300 REM " --- <F2> Return to PO selection overlay"
4310 PRINT @(0,L0-1),'CE',
4320 LET M1$=OLDM1$,M2$=OLDM2$,M3$=OLDM3$
4390 RUN "POE.EB"
4400 REM " --- <F4> Exit"
4410 PRINT @(0,L0-1),'CE',
4420 LET O0=1,M1$=OLDM1$,M2$=OLDM2$,M3$=OLDM3$
4490 RUN "POE.EA"
4800 REM " --- List"
4810 GOSUB 5600
4890 GOTO 4000
4900 REM " --- Review"
4910 READ (POE25_DEV,KEY=A0$,DOM=4920)
4920 GOSUB 5900
4990 GOTO 1000
5000 REM " --- Display"
5010 LET NUMBER=NUMBER+1,B0$(1)=N0$+A0$(5,6)+D1$(1,7)+D1$(11,7),B1$(1)=""
5020 LET C0$(1)=B0$+D1$(8,3),RECEIVED$="",EXTENSION=D[0]*D[1]
5030 IF FNP$(D1$(1,17))="" THEN GOTO 5400
5040 FIND (POT04_DEV,KEY=B0$,DOM=5590)IOL=POT04A
5050 FIND (POT14_DEV,KEY=C0$,DOM=5590)IOL=POT14A
5060 LET Y0$(1)=N0$+C1$(1,2),RECEIVED$=FNB6$(B1$(29,6))
5070 FIND (POM02_DEV,KEY=Y0$,DOM=5590)IOL=POM02A
5080 PRINT @(0,L),'CL',D0$(21),@(4),C1$(1,2),@(7),C3$(1,2),@(0,L+1),'CL',
5090 ON POS(Y1$(21,1)="SNVMO") GOTO 5590,5100,5200,5500,5500,5300
5100 REM " --- Display Standard Line"
5110 DIM X0$(22),X1$(60),DESCRIPTION$(DW)
5120 LET X0$(1)=N0$+C3$(3)
5130 FIND (IVM01_DEV,KEY=X0$,DOM=5140)IOL=IVM01A
5140 LET DESCRIPTION$(1)=FNITEM$(X1$,I[3],I[4],I[5])
5150 PRINT @(O[3],L),C3$(3,I[0]),@(O[4]),RECEIVED$,@(O[5]),C[7]:M2$,@(O[6]),C1$(22,2),@(O[7]),D[0]:M3$,@(O[8]),EXTENSION:M1$,@(O[3],L+1),DESCRIPTION$,
5190 GOTO 5500
5200 REM " --- Display Non-Stock Line"
5210 PRINT @(O[3],L),C3$(3,I[0]),@(O[4]),RECEIVED$,@(O[5]),C[7]:M2$,@(O[6]),C1$(22,2),@(O[7]),D[0]:M3$,@(O[8]),EXTENSION:M1$,@(O[3],L+1),C4$(1,MW),
5290 GOTO 5500
5300 REM " --- Display Other Line"
5310 PRINT @(O[4],L),RECEIVED$,@(O[7]),D[0]:M3$,@(O[8]),EXTENSION:M1$,@(O[3],L+1),C4$(1,MW),
5390 GOTO 5500
5400 REM " --- Display Other Line Added In Invoice Entry"
5410 PRINT @(0,L),'CL',D0$(21),@(4),D1$(18,2),@(O[7]),D[0]:M3$,@(O[8]),EXTENSION:M1$,@(0,L+1),'CL',@(O[3]),D1$(20,MW),
5500 REM " --- Display PO and Receiver"
5510 IF FNP$(D1$(1,7))="" THEN GOTO 5590
5520 PRINT @(O[3],L+2),'SB',"PO # ",'SF',D1$(1,7),"-",D1$(8,3),'SB'," Rec # ",'SF',D1$(11,7),
5590 RETURN 
5600 REM " --- List"
5610 LET NUMBER=0
5620 GOSUB 5900
5640 READ (POE25_DEV,KEY=A0$,DOM=5650)
5650 LET K$=KEY(POE25_DEV,END=5790)
5660 IF POS(A0$=K$)<>1 THEN GOTO 5790
5670 READ (POE25_DEV)IOL=POE25A
5690 IF L+2<=L9 THEN GOTO 5750
5700 REM " --- Full screen"
5710 LET V4$="Press <Enter> To Continue:"
5720 LET V0$="S",V1$="CE",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5730 GOSUB 7000
5740 IF V3=4 THEN GOTO 5790
5745 GOSUB 5900
5750 GOSUB 5000
5760 LET L=L+3,K9$=D0$
5770 GOTO 5650
5790 RETURN 
5900 REM " --- Clear Screen"
5910 LET L=L0+1
5920 PRINT @(0,L),'CE',
5990 RETURN 
6000 REM " --- Accumulate total for invoice"
6010 LET TOTAL=0,OTHER=0,X$="",LAST$="",X1$=W0$,X2$=D0$
6090 READ (POE15_DEV,KEY=A0$,DOM=6100)
6100 REM " --- Read next line item"
6110 LET K$=KEY(POE15_DEV,END=6400)
6120 IF POS(A0$=K$)<>1 THEN GOTO 6400
6130 READ (POE15_DEV)IOL=POE15A
6140 IF FNP$(W1$(1,7))="" THEN LET X$=K$
6150 LET W[0]=0,LAST$=W0$
6190 READ (POE25_DEV,KEY=A0$,DOM=6200)
6200 REM " --- Read line items and rebuild selection total"
6210 LET K$=KEY(POE25_DEV,END=6300)
6220 IF POS(A0$=K$)<>1 THEN GOTO 6300
6230 READ (POE25_DEV)IOL=POE25A
6240 IF FNP$(D1$(1,7))<>"" THEN GOTO 6260
6245 LET OTHER=1
6250 GOTO 6200
6260 IF W1$(1,7)<>D1$(1,7) THEN GOTO 6200
6270 IF FNP$(W1$(8,7))<>"" THEN IF W1$(8,7)<>D1$(11,7) THEN GOTO 6200
6280 LET W[0]=W[0]+(D[0]*D[1])
6290 GOTO 6200
6300 REM " --- Update selection total"
6310 WRITE (POE15_DEV,KEY=W0$)IOL=POE15A
6320 LET TOTAL=TOTAL+W[0]
6390 GOTO 6100
6400 REM " --- Any non-PO charges?"
6410 IF OTHER=0 THEN GOTO 6700
6420 LET OTHER=0
6490 READ (POE25_DEV,KEY=A0$,DOM=6500)
6500 REM " --- Accumulate non-PO charges"
6510 LET K$=KEY(POE25_DEV,END=6600)
6520 IF POS(A0$=K$)<>1 THEN GOTO 6600
6530 READ (POE25_DEV)IOL=POE25A
6540 IF FNP$(D1$(1,7))<>"" THEN GOTO 6500
6550 LET OTHER=OTHER+D[0]
6590 GOTO 6500
6600 REM " --- Update non-PO charges"
6610 IF X$<>"" THEN GOTO 6650
6620 LET X$=LAST$,X$(21,3)=STR(NUM(X$(21,3))+10:"000")
6650 LET W0$(1)=X$,W1$(1)=""
6660 FIND (POE15_DEV,KEY=W0$,DOM=6670)IOL=POE15A
6670 LET W[0]=OTHER,TOTAL=TOTAL+W[0]
6680 WRITE (POE15_DEV,KEY=W0$)IOL=POE15A
6700 REM " --- Done with total"
6710 PRINT @(S1,L0-2),TOTAL:M1$,
6770 READ (POE15_DEV,KEY=X1$,DOM=6780)IOL=POE15A
6780 READ (POE25_DEV,KEY=X2$,DOM=6790)IOL=POE25A
6790 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

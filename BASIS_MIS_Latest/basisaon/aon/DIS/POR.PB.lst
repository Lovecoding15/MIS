0010 REM "POR - Purchase Order Receipt History Report (Summary - Print Overlay)
0020 REM "Program POR.PB
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 APM01A: IOLIST C0$(1),C1$(1)
0220 APM05A: IOLIST R0$(1),R1$(1)
0230 APM10C: IOLIST D0$(1)
0240 ARM01A: IOLIST *,CUSTINFO$(1)
0250 ARM03A: IOLIST *,DROPINFO$(1)
0290 POT04A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0300 POT14A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1),B4$(1),B5$,B6$,B[ALL]
0310 POM02A: IOLIST S0$(1),S1$(1)
0500 REM " --- Init Data
0510 PRECISION IVPRECISION
0520 DIM A0$(22),A1$(160),A[8],VENDTOT[3],VENDOR$(6)
0530 LET PRINTVTOT$="N"
0540 LET INITIALIZING$="Y"; REM "Init files 1st time through
0550 LET MRATIO$="-##0.00%",MRATIO=LEN(MRATIO$)
0600 REM " --- Headings and print positions
0610 LET HEADINGS=4
0620 DIM HEADINGS$[HEADINGS],O[12]
0630 LET WIDTH=132,PAGE=0,L9=59,L=L9+1
0650 GOSUB ASSIGNHEADS
0760 POSITIONS: 
0770 LET O[1]=O[0]+7,O[2]=O[1]+8,O[3]=O[2]+8,O[4]=O[3]+9
0780 LET O[5]=O[4]+10,O[6]=O[5]+6,O[7]=O[6]+7,O[8]=O[7]+7
0790 LET O[9]=O[8]+16,O[10]=O[9]+16,O[11]=O[10]+M1,O[12]=O[11]+M1
0810 IF O[12]+8<WIDTH THEN GOTO 0950
0820 LET O[12]=WIDTH-MRATIO,O[11]=O[12]-M1,O[10]=O[11]-M1
0950 REM " --- Position files
0960 CALL "SYC.GA",7,1,"","",STATUS
0970 IF STATUS THEN GOTO 9900
0980 CALL "SYC.NB","Printing",22,COLUMN
1000 REM " --- Sorts
1010 SORTORDER: 
1020 ON POS(PRINTSEQ$="AN") GOTO 4000,SORTBYALT,SORTBYVEND
1200 REM " --- Sort by alternate sequence
1210 SORTBYALT: 
1220 IF INITIALIZING$="N" THEN GOTO KEYBYALT
1230 LET INITIALIZING$="N"
1260 READ (APM04_DEV,KEY=N0$+"A"+VENDFROM$,DOM=KEYBYALT)
1270 REM " --- Find a POT44 record using alt seq
1280 KEYBYALT: 
1290 LET K04A$=KEY(APM04_DEV,END=4000)
1300 IF K04A$(1,3)<>N0$+"A" THEN GOTO 4000
1310 LET K04B$=K04A$(1,2)+K04A$(14,6)
1320 READ (APM04_DEV); REM "Move ptr
1330 READ (POT44_DEV,KEY=K04B$,DOM=1340)
1340 LET K44$=KEY(POT44_DEV,END=NEXTALT)
1350 IF K44$(1,8)<>K04B$ THEN GOTO NEXTALT
1360 LET K$=K44$
1370 GOTO TESTKEY
1380 NEXTALT: 
1390 GOTO KEYBYALT
1500 REM " --- Sort by vendor
1510 SORTBYVEND: 
1520 IF INITIALIZING$="N" THEN GOTO KEYBYVEND
1530 LET INITIALIZING$="N"
1540 READ (POT44_DEV,KEY=N0$+VENDFROM$+DATEFROM$,DOM=KEYBYVEND)
1600 REM " --- Read through req headers
1610 KEYBYVEND: 
1620 LET K$=KEY(POT44_DEV,END=4000)
1630 TESTKEY: 
1640 IF K$(1,2)<>N0$ THEN GOTO 4000
1650 READ (POT44_DEV); REM "Move ptr
1670 FIND (POT04_DEV,KEY=K$(1,8)+K$(15,14),DOM=NEXTHEADER)IOL=POT04A
1750 REM " --- Test Ranges
1760 IF VENDFROM$<>FILL(6) AND A0$(3,6)<VENDFROM$ THEN GOTO NEXTHEADER
1770 IF VENDTHRU$<>FILL(6) AND A0$(3,6)>VENDTHRU$ THEN GOTO NEXTHEADER
1780 IF DATEFROM$<>"" AND A1$(29,6)<DATEFROM$ THEN GOTO NEXTHEADER
1790 IF DATETHRU$<>"" AND A1$(29,6)>DATETHRU$ THEN GOTO NEXTHEADER
1800 REM " --- Assign variables
1810 DIM RCPTDATE$(8),ORDDATE$(8)
1820 IF A1$(29,6)<>FILL(6) THEN LET RCPTDATE$=FNB6$(A1$(29,6))
1830 IF A1$(5,6)<>FILL(6) THEN LET ORDDATE$=FNB6$(A1$(5,6))
1835 IF PONUM$=A0$(9,7) THEN LET PONUM_PRNT$="" ELSE LET PONUM_PRNT$=A0$(9,7)
1840 LET PONUM$=A0$(9,7),RECEIVER$=A0$(16,7)
1850 LET TERMS$=A1$(38,2),PADDRCODE$=A1$(3,2),SHIP2WHS$=A1$(1,2)
1860 LET FREIGHT$=A1$(44,15),SHIPVIA$=A1$(59,15),ACKNOW$=A1$(74,20)
1900 REM " --- Test breaks
1920 IF A0$(3,6)=VENDOR$ THEN GOTO TESTED
1930 IF PRINTVTOT$="Y" THEN GOSUB VENDTOTALS
1940 GOSUB NEWVENDOR
1980 TESTED: 
1990 PRINT @(COLUMN,11),FNF$(A0$(3,VENDLEN),VENDOMASK$)," ",A0$(9,7)," ",A0$(16,7)
2000 REM " --- Calculate Order Value, Receipt Value and Fill Ratio
2010 READDETAIL: 
2020 DIM B0$(25),B1$(48),B2$(32),B3$(22),B4$(40),B[12]
2030 LET PO_OVAL=0,PO_RVAL=0,RATIO=0
2040 READ (POT14_DEV,KEY=A0$,DOM=READ_POT14)
2100 REM " --- Get detail numerics and accumulate column buckets
2110 READ_POT14: 
2120 LET K14$=KEY(POT14_DEV,END=DONE_DETAIL)
2130 IF K14$(1,22)<>A0$ THEN GOTO DONE_DETAIL
2140 READ (POT14_DEV,KEY=K14$)IOL=POT14A
2150 GOSUB GETLINETYPE
2160 IF POS(LINETYPE$="SNO")=0 THEN GOTO NEXTDETAIL
2170 PRECISION 2
2180 LET ORDVAL=(B[1]*B[3]),RCPTVAL=(B[1]*B[7])
2190 PRECISION IVPRECISION
2200 REM " --- Accumulate totals; then get next detail
2210 LET PO_OVAL=PO_OVAL+ORDVAL
2220 IF PONUM_PRNT$="" THEN GOTO 2250; REM "Don't accum OrdVal tots if same PO#
2230 LET VENDOVAL=VENDOVAL+ORDVAL,GRANDOVAL=GRANDOVAL+ORDVAL
2250 LET PO_RVAL=PO_RVAL+RCPTVAL,VENDRVAL=VENDRVAL+RCPTVAL
2260 LET GRANDRVAL=GRANDRVAL+RCPTVAL
2285 NEXTDETAIL: 
2290 GOTO READ_POT14
2300 REM " --- Done w/detail for this PO 
2310 DONE_DETAIL: 
2320 IF PO_OVAL>0 THEN LET RATIO=PO_RVAL*100/PO_OVAL
2340 GOSUB PRINTLINE
2390 READ (POT44_DEV,KEY=K$(1,28)+$FF$,DOM=2400); REM "Move ptr to next recpt
2400 REM " --- Loop up for next header record via sort files
2410 NEXTHEADER: 
2420 IF PRINTSEQ$<>"A" THEN GOTO 2460
2430 REM " --- See if there's another po for vend before getting next alt seq
2440 LET K$=KEY(POT44_DEV,END=2460)
2450 IF K$(3,6)=VENDOR$ THEN GOTO TESTKEY
2460 GOTO SORTORDER
4000 REM " --- All Done
4020 IF L+2>L9 THEN GOSUB 5000
4030 GOSUB VENDTOTALS
4040 IF GRANDOVAL>0 THEN LET GRANDRATIO=GRANDRVAL*100/GRANDOVAL
4050 PRINT (7)@(O[10]-17),"Total For Report",@(O[10]),GRANDOVAL:M1$,@(O[11]),GRANDRVAL:M1$,@(O[12]),GRANDRATIO:MRATIO$
4090 GOTO 9900
5000 REM " --- Heading
5010 LET L=HEADINGS+1
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS>0 THEN EXITTO 9900
5140 PRINT (7)@(O[3]),"Order",@(O[4]),"Receipt",@(O[5]-1),"Terms",@(O[6]-1),"P.Addr",@(O[7]-1),"Ship-To",@(O[8]),"Freight Terms",@(O[10]+M1-7),"Order",@(O[11]+M1-9),"Receipt",@(O[12]+MRATIO-7),"Fill"
5150 PRINT (7)@(O[0]),"Vendor",@(O[1]),"PO Numb",@(O[2]),"Receivr",@(O[3]),"Date",@(O[4]),"Date",@(O[5]-1),"Code",@(O[6]-1),"Code",@(O[7]-1),"Whse",@(O[8]),"Ship Via",@(O[9]),"Acknowledged",@(O[10]+M1-7),"Value",@(O[11]+M1-9),"Value",@(O[12]+MRATIO-7),"Ratio"
5160 PRINT (7)""
5180 LET L=L+3
5190 RETURN 
5300 REM " --- Assign headings variables
5305 ASSIGNHEADS: 
5310 DIM TMPH3$(WIDTH),TMPH4$(WIDTH)
5320 IF VENDFROM$=FILL(6) THEN LET VENDF$="First" ELSE LET VENDF$=FNF$(VENDFROM$(1,VENDLEN),VENDOMASK$)
5330 IF VENDTHRU$=FILL(6) THEN LET VENDT$="Last" ELSE LET VENDT$=FNF$(VENDTHRU$(1,VENDLEN),VENDOMASK$)
5340 IF DATEFROM$="" THEN LET DATEF$="First" ELSE LET DATEF$=FNB6$(DATEFROM$)
5350 IF DATETHRU$="" THEN LET DATET$="Last" ELSE LET DATET$=FNB6$(DATETHRU$)
5360 LET MIDH3$=LEVELDESC$+" Sorted By "+SEQDESC$
5370 LET TMPH3$(1)="From Vendor: "+VENDF$,TMPH3$(FNC(MIDH3$,WIDTH))=MIDH3$
5380 LET TMPH3$(WIDTH-LEN(DATEF$)-18)="From Receipt Date: "+DATEF$
5390 LET TMPH4$(1)="  To Vendor: "+VENDT$
5400 LET TMPH4$(WIDTH-LEN(DATEF$)-18)="  To Receipt Date: "+DATET$
5410 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$
5420 LET HEADINGS$[2]=TMPH3$,HEADINGS$[3]=TMPH4$
5490 RETURN 
6100 REM " --- New Vendor
6105 NEWVENDOR: 
6120 DIM C0$(8),C1$(195),VENDTOT[3]
6130 LET VENDNAME$="Not On File",VENDOR$=A0$(3,6)
6135 LET PONUM_PRNT$=A0$(9,7); REM "In case this & last vend had some PO #
6140 LET PRINTVTOT$="Y",VENDOVAL=0,VENDRVAL=0,VENDRATIO=0
6150 FIND (APM01_DEV,KEY=N0$+VENDOR$,DOM=6170)IOL=APM01A
6160 LET VENDNAME$=C1$(1,30)
6170 IF L+2>L9 THEN GOSUB 5000
6180 PRINT (7)@(0),FNF$(VENDOR$(1,VENDLEN),VENDOMASK$)," ",VENDNAME$
6200 LET L=L+1
6290 RETURN 
6300 REM " --- Print Vendor total
6305 VENDTOTALS: 
6320 IF VENDOVAL>0 THEN LET VENDRATIO=VENDRVAL*100/VENDOVAL
6330 IF L+1>L9 THEN GOSUB 5000
6340 PRINT (7)@(O[10]-18-LEN(VENDOMASK$)),"Total For Vendor ",FNF$(VENDOR$(1,VENDLEN),VENDOMASK$),@(O[10]),VENDOVAL:M1$,@(O[11]),VENDRVAL:M1$,@(O[12]),VENDRATIO:MRATIO$
6350 LET L=L+1
6390 RETURN 
6400 REM " --- Get Line Type
6405 GETLINETYPE: 
6420 DIM S0$(4),S1$(32)
6430 LET LINETYPE$="N"
6440 FIND (POM02_DEV,KEY=B0$(1,2)+B1$(1,2),DOM=6460)IOL=POM02A
6450 LET LINETYPE$=S1$(21,1)
6490 RETURN 
6500 REM " --- Print data line
6505 PRINTLINE: 
6510 IF L+2>L9 THEN GOSUB 5000
6520 LET PO_OVAL$=STR(PO_OVAL:M1$),PO_RVAL$=STR(PO_RVAL:M1$)
6530 IF PONUM_PRNT$="" THEN LET PO_OVAL$=""
6550 PRINT (7)@(O[1]),PONUM_PRNT$,@(O[2]),RECEIVER$,@(O[3]),ORDDATE$,@(O[4]),RCPTDATE$,@(O[5]),TERMS$,@(O[6]),PADDRCODE$,@(O[7]),SHIP2WHS$,@(O[8]),FREIGHT$,@(O[9]),ACKNOW$(1,15),@(O[10]),PO_OVAL$,@(O[11]),PO_RVAL$,@(O[12]),RATIO:MRATIO$
6560 PRINT (7)@(O[8]),SHIPVIA$,@(O[9]),CVS(ACKNOW$(16),1)
6580 LET L=L+2
6590 RETURN 
8000 REM " --- Functions
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

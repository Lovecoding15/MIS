0010 REM "OPX - Rebuild Open Order Cross-References"
0020 REM "Program OPX.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ARE-03",FILES$[2]="ARE-13",FILES$[3]="SYS-01"
0140 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET ARE03_DEV=CHANNELS[1],ARE13_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLISTS"
0210 ARE03A: IOLIST A0$
0220 ARE13A: IOLIST B0$
0230 ARE07A: IOLIST ARE07_K$
0240 ARE43A: IOLIST ARE43_K$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters "
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)
0800 REM " --- Options
0810 LET V4$="Do You Wish To "+F5$
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0840 REM " --- Clear And Lock ARE-07 & ARE-43
0850 LET FILES$[4]="ARE-07",OPTIONS$[4]="CL"
0860 LET FILES$[5]="ARE-43",OPTIONS$[5]="CL"
0870 CALL "SYC.DA",1,4,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0880 IF STATUS>0 THEN GOTO 9900
0890 LET ARE07_DEV=CHANNELS[4],ARE43_DEV=CHANNELS[5]
0950 REM " --- Initialize File "
0960 CALL "SYC.NB","Rebuilding",17,COLUMN
0990 READ (ARE03_DEV,KEY="",DOM=1000)
1000 REM " --- Read Open Order/Invoice Records "
1020 LET K$=KEY(ARE03_DEV,END=4000)
1040 READ (ARE03_DEV)IOL=ARE03A
1060 PRINT @(COLUMN,11),A0$(1,2)," ",A0$(5,6)," ",A0$(11,7),
1100 REM " --- ARE-43 Orders by Customer"
1110 LET ARE43_K$=A0$(1,4)+A0$(11,7)+A0$(5,6)
1120 WRITE (ARE43_DEV,KEY=ARE43_K$)IOL=ARE43A
1200 REM " --- Init Detail
1210 DIM B0$(117)
1220 READ (ARE13_DEV,KEY=A0$(1,17),DOM=1300)
1300 REM " --- Read Detail
1320 LET K13$=KEY(ARE13_DEV,END=3900)
1330 IF A0$(1,17)<>K13$(1,17) THEN GOTO 3900
1340 READ (ARE13_DEV)IOL=ARE13A
1400 REM " --- ARE-07 Orders by Item
1410 LET ARE07_K$=B0$(1,2)+B0$(31,22)+B0$(3,2)+B0$(11,10)+B0$(5,6)
1420 WRITE (ARE07_DEV,KEY=ARE07_K$)IOL=ARE07A
1500 REM " --- Loop back for next detail
1590 GOTO 1300
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- Done"
4020 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

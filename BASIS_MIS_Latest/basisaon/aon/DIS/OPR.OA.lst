0010 REM "OPR - Order Status Report By Item"
0020 REM "Program OPR.OA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0090 SETERR 9000
0095 SETESC 9000
0100 REM "   Open/Lock Files "
0110 LET FILES=8
0115 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="ARE-03",FILES$[2]="ARE-13",FILES$[3]="IVM-10"
0125 LET FILES$[4]="IVM-02",FILES$[5]="IVM-01",FILES$[6]="ARE-07"
0130 LET FILES$[7]="SYS-01",FILES$[8]="ARM-10"
0135 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0140 IF STATUS>0 THEN GOTO 9900
0145 LET ARE03_DEV=CHANNELS[1],ARE13_DEV=CHANNELS[2],IVM10_DEV=CHANNELS[3]
0150 LET IVM02_DEV=CHANNELS[4],IVM01_DEV=CHANNELS[5],ARE07_DEV=CHANNELS[6]
0155 LET SYS01_DEV=CHANNELS[7],ARM10_DEV=CHANNELS[8]
0200 REM " --- IOLISTS"
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0290 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,I5$,M1$,M2$,M3$
0380 IVM10A: IOLIST X0$(1)
0385 IVM10C: IOLIST X0$(1)
0400 REM " --- Parameters "
0405 DIM P[2],I[5]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0460 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1)),I[2]=NUM(I2$(7,2))
0470 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0490 LET P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1)),P[0]=NUM(P2$(1,2))
0500 REM " --- Initializations"
0510 DIM DESCRIPTION$[1],LENGTH[1],POSITION[1],FIELD[1],X0$(32)
0550 LET NUMBER=1
0560 LET P8$="",P9$="D",P6$="",P5$="",H1$=N4$,OPTN$="OQBC"
0700 REM " --- Background "
0705 FOR X=4 TO 10 STEP 2
0710 CALL "SYC.WC",1,0,80,0,0,X,0
0715 NEXT X
0720 PRINT 'SB',@(26,3),"Product Type:",@(32,5),"Option:",@(21,7),"Summary Or Detail:",@(29,9),"Warehouse:",'SF',
1000 REM " --- Product "
1010 PRINT 'CF',
1020 LET V0$="S",V1$="KC",V2$=P8$,V0=3,V1=40,V2=3,I0=0,V4$="Enter A Valid Product Type (<F1>=All/<F3>=Lookup)"
1030 LET H4$="Product Type: "
1035 GOSUB 7000
1040 IF V3=4 THEN GOTO 9900
1045 IF V3=3 THEN CALL "IVC.LK",1,V1,V2,V$
1050 IF V3=1 THEN LET V$=""
1055 IF V$="" THEN LET H4$=H4$+"All"; PRINT @(V1,V2),"All"; GOTO 1090
1060 LET V$=V$+"   ",V$=V$(1,3)
1070 FIND (IVM10_DEV,KEY=N0$+"A"+V$,DOM=1000)IOL=IVM10A
1080 PRINT @(V1,V2),X0$(4,3)," ",X0$(7,20),
1085 LET H4$=H4$+V$+" "+FNP$(X0$(7,20))
1090 LET P8$=V$
1095 GOTO 3900
1300 REM " --- Summary/Detail "
1310 LET V0$="S",V1$="C",V2$=P9$,V3$="SD",V4$="S=Summary By Item     D=Item Detail By Order",V0=1,V2=40,V2=7
1320 GOSUB 7000
1325 IF V3=2 THEN GOTO 3900
1330 LET P9$=V$,X$="Detail"
1340 IF P9$="S" THEN LET X$="Summary"
1350 PRINT @(V1,V2),X$,'CL',
1390 GOTO 3900
1400 REM " --- Warehouse Entry "
1420 LET V0$="S",V1$="KC",V2$=P6$,V4$="Enter A Valid Warehouse (<F1>=All/<F3>=Lookup)",V0=2,V1=40,V2=9
1430 GOSUB 7000
1440 IF V3=2 THEN GOTO 3900
1450 LET H5$="Warehouse: "
1460 IF V3=3 THEN CALL "IVC.LK",3,V1,V2,V$
1500 IF V3=1 THEN LET V$=""
1520 IF V$="" THEN LET H5$=H5$+"All"; PRINT @(V1,V2),"All"; GOTO 1580
1530 LET V$=V$+"  ",V$=V$(1,2)
1550 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=1400)IOL=IVM10C
1560 PRINT @(V1,V2),X0$(4,2)," ",X0$(6,20)
1570 LET H5$=H5$+V$+" "+FNP$(X0$(6,20))
1580 LET P6$=V$
1590 GOTO 3900
1600 REM " --- Option"
1610 LET V0$="S",V1$="^C",V2$=P5$,V3$="",V4$="O=Open Orders, Q=Quotes, B=Backorders, C=Credit Held Orders, <F1>=All Orders",V0=4,V1=40,V2=5,X$="",H3$=""
1620 PRINT @(V1,V2),'CL',
1630 GOSUB 7000
1635 IF V3=2 OR V3=4 THEN LET V$=P5$
1640 IF V$="" OR V3=1 THEN LET V$=OPTN$; GOTO 1670
1642 FOR X=1 TO LEN(OPTN$)
1644 IF POS(OPTN$(X,1)=V$)>0 THEN LET X$=X$+OPTN$(X,1)
1646 NEXT X
1648 IF LEN(X$)>0 THEN LET V$=X$ ELSE GOTO 1600
1650 FOR X=1 TO LEN(V$)
1655 IF X=LEN(V$) AND X<>1 THEN LET H3$=H3$+" and " ELSE IF H3$<>"" THEN LET H3$=H3$+", "
1660 IF V$(X,1)="O" THEN LET H3$=H3$+"Open" ELSE IF V$(X,1)="Q" THEN LET H3$=H3$+"Quoted" ELSE IF V$(X,1)="B" THEN LET H3$=H3$+"Backordered" ELSE IF V$(X,1)="C" THEN LET H3$=H3$+"Credit Held" ELSE EXITTO 1600
1665 NEXT X
1670 LET P5$=V$
1675 IF LEN(P5$)<4 THEN LET H3$=H3$+" Orders Only" ELSE LET H3$="All Orders"
1680 IF LEN(H3$)>80-V1-LEN(P5$)-1 THEN LET X$=H3$(1,80-V1-LEN(P5$)-1) ELSE LET X$=H3$
1685 PRINT @(V1,V2),'CL',P5$," ",X$,
1690 GOTO 3900
3900 REM " --- New Field "
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1600,1300,1400,4000
4000 REM " --- Correct ? "
4010 LET V4$="Is The Above Information Correct"
4025 CALL "SYC.YN",1,V4$,2,V$,V3
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4060,1000
4090 RUN "OPR.OB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9110 SETERR 9000
9120 RETRY
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

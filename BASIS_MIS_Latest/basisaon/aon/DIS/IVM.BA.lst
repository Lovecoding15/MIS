0010 REM "IVM - Transaction Code Maintenance"
0020 REM "Program IVM.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="IVM-10",FILES$[3]="GLM-01"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],IVM10_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 IVM10B: IOLIST A0$(1),A1$(1)
0220 GLM01A: IOLIST *,B1$(1)
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0405 DIM G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 LET G[3]=NUM(G2$(7,2)),G[4]=NUM(G2$(9,2))
0480 CALL "SYC.VA","IV",INFO$[ALL]
0490 LET GL$=INFO$[9]
0500 REM " --- Initializations"
0510 DIM A0$(30),A1$(G[4]),B1$(30)
0590 LET K0=1,K9=5,RECORD$="B",A0$(1,3)=N0$+RECORD$
0600 REM " --- G/L Installed?"
0610 IF GL$<>"Y" THEN GOTO 0700
0620 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS>0 THEN GOTO 9900
0640 LET GLM01_DEV=CHANNELS[3]
0700 REM " --- Background"
0710 PRINT 'SB',@(2,3),"Transaction:",@(2,4),"Description:",@(9,5),"Type:",'SF',
0720 IF GL$="Y" THEN PRINT 'SB',@(2,7),"Post To GL?:",@(3,8),"GL Account:",'SF',
0900 REM " --- Position File"
0990 READ (IVM10_DEV,KEY=A0$(1,3),DOM=1000)
1000 REM " --- Transaction Code"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(IVM10_DEV,END=1050)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4)
1050 LET V0$="S",V1$="KCE",V3$="",V0=2,V1=15,V2=3
1055 LET V4$="Enter A Valid Inventory Transaction Code (<F3>=Lookup)"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN CALL "IVC.LK",2,V1,V2,V$
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(4)=V$
1200 REM " --- Retrieve Record"
1210 FIND (IVM10_DEV,KEY=A0$(1,K9),DOM=1400)IOL=IVM10B
1300 REM " --- Display"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Initialize new record"
1510 LET A0$(6)="",A1$(1)=""
2000 REM " --- Description"
2020 LET V0$="S",V1$="",V2$=A0$(6,20),V3$="",V0=20,V1=15,V2=4,I0=0
2030 LET V4$="Enter The Description For This Transaction Code"
2040 GOSUB 7000
2050 ON V3 GOTO 2060,2000,3900,2000,3900
2060 LET A0$(6,20)=V$
2090 GOTO 3900
2300 REM " --- Transaction Type"
2310 LET V4$="Enter The Transaction Type (A=Adjustment/C=Committment/I=Issue/R=Receipt)"
2320 LET V0$="S",V1$="RC",V2$=A0$(26,1),V3$="ACIR",V0=1,V1=15,V2=5
2340 GOSUB 7000
2345 ON V3 GOTO 2350,2300,3900,2300,3900
2350 IF POS(V$="ACIR")=0 THEN GOTO 2300
2360 LET A0$(26,1)=V$
2390 GOTO 3900
3200 REM " --- GL Account"
3205 IF GL$<>"Y" THEN GOTO 3900
3210 IF A0$(27,1)="N" THEN GOTO 3900
3215 IF A0$(26,1)="C" THEN GOTO 3900
3220 LET V4$="Enter A Valid G/L Account Number (<F3>=Lookup)"
3225 LET V0$="M",V1$="RC",V2$=A1$(1,G[3]),V3$=G1$,V0=G[4],V1=15,V2=8,F9$=""
3230 GOSUB 7000
3233 ON V3 GOTO 3240,3200,3900,3235,3900
3235 CALL "SYC.LG",V$
3237 IF V$="" THEN GOTO 3200
3240 FIND (GLM01_DEV,KEY=N0$+V$,DOM=3200)IOL=GLM01A
3250 PRINT @(V1,V2),FNF$(V$(1,G[3]),G5$)," ",B1$,
3260 LET A1$(1)=V$
3290 GOTO 3900
3300 REM " --- Post to GL?"
3310 IF GL$<>"Y" THEN GOTO 3900
3315 LET V4$="Y=Generate G/L Postings During Transaction Update     N=No G/L Postings"
3320 LET V0$="Y",V1$="C",V2$=A0$(27,1),V0=1,V1=15,V2=7
3325 IF A0$(26,1)="C" THEN LET A0$(27,1)="N"; GOTO 3370
3340 GOSUB 7000
3350 ON V3 GOTO 3360,3300,3900,3300,3900
3360 LET A0$(27,1)=V$
3370 PRINT @(V1,V2),A0$(27,1),
3390 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2300,3300,3200,3990
3990 LET I0=K0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record"
4110 WRITE (IVM10_DEV,KEY=A0$(1,K9))IOL=IVM10B
4190 GOTO 1000
4200 REM " --- Remove record"
4210 REMOVE (IVM10_DEV,KEY=A0$(1,K9),DOM=4220)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4600,9900
4600 REM " --- Report Initializations"
4610 LET PROGRAM$="",CHANNEL=IVM10_DEV,NUMBER=4,HEADINGS=3,WIDTH=80
4630 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4640 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4650 DIM HEADINGS$[HEADINGS]
4660 LET FIELD$[0]="Code",FIELD[0]=1,POSITION[0]=4
4670 LET LENGTH[0]=2,TYPE$[0]="S",MASK$[0]=""
4680 LET FIELD$[1]="Description",FIELD[1]=1,POSITION[1]=6
4690 LET LENGTH[1]=20,TYPE$[1]="S",MASK$[1]=""
4700 LET FIELD$[2]="Type",FIELD[2]=1,POSITION[2]=26
4710 LET LENGTH[2]=1,TYPE$[2]="S",MASK$[2]=""
4720 IF GL$<>"Y" THEN GOTO 4900
4730 LET FIELD$[3]="G/L",FIELD[3]=1,POSITION[3]=27
4740 LET LENGTH[3]=1,TYPE$[3]="S",MASK$[3]=""
4750 LET FIELD$[4]="Adj Account",FIELD[4]=2,POSITION[4]=1
4770 LET LENGTH[4]=G[3],TYPE$[4]="M",MASK$[4]=G5$
4900 REM 
4910 LET NUMBER=1,TOTAL$="Transaction Codes"
4920 IF GL$="Y" THEN LET NUMBER=4
4980 LET HEADINGS$[0]=N1$,HEADINGS$[1]=F5$+" Listing"
4990 RUN "SYR.AA"
5000 REM " --- Display"
5010 PRINT @(15,3),A0$(4,2),@(15,4),A0$(6,20),@(15,5),A0$(26,1),
5020 IF GL$<>"Y" THEN GOTO 5090
5040 PRINT @(15,7),A0$(27,1),
5050 IF A0$(27,1)="N" THEN GOTO 5090
5060 DIM B1$(30)
5070 FIND (GLM01_DEV,KEY=N0$+A1$(1,G[4]),DOM=5080)IOL=GLM01A
5080 IF A0$(27,1)<>"N" THEN PRINT @(15,8),FNF$(A1$(1,G[3]),G5$)," ",B1$,
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

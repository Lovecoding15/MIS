0010 REM "IVR - ABC Analysis Report (Report Overlay)"
0020 REM "Program IVR.PC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$,A4$,A5$,A6$,A[ALL]
0230 IVM10C: IOLIST X0$(1)
0240 IVW07A: IOLIST D0$(1),D1$(1),D[ALL]
0250 IVW17A: IOLIST D0$(1),D1$(1),D[ALL]
0300 IVS10I: IOLIST S0$(1),S1$(1),S[ALL]
0310 IVS10W: IOLIST S0$(1),S1$(1),S[ALL]
0320 IVE07A: IOLIST R0$(1),R1$(1),R[ALL]
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A[12],D0$(42),D1$(16),D[6]
0520 DIM R0$(24),R1$(16),R[6],S0$(3),S1$(16),S[3],HEADINGS$[6],O[9]
0530 DIM COUNT[25],VALUE[25]
0545 LET HEADINGS$[0]=F4$,WHEN$=F0$(7,3),CLOCK$="",M7$="####0",M9$="-##0.00%"
0550 LET HEADINGS=6,WIDTH=132,PAGE=0,L9=59,L=L9+1,DONE=0,M8$="###,###,##0"
0560 LET SEQUENCE=0,M7=LEN(M7$),M8=LEN(M8$),M9=LEN(M9$),CLASS=0,TOTALPCT=0
0570 LET XLIMIT=PERCENTS[CLASS],LETTERS$="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
0575 LET XP=MAX(9,M2)
0580 IF VALUE$="U" THEN LET XP=18
0585 LET XP=XP+6
0600 REM " --- Print positions"
0605 DIM H1$(WIDTH),H2$(WIDTH)
0610 CALL "SYC.RM",M1$,M1,6,15
0615 CALL "SYC.RM",M2$,M2,8,12
0620 CALL "SYC.RM",M3$,M3,5,12
0625 LET O[1]=M7+2,O[2]=O[1]+P[0]+1,O[3]=O[2]+P[3]+P[4]+P[5]+2
0630 LET O[4]=O[3]+XP-M2,O[6]=O[4]+M2,O[7]=O[6]+M1,O[8]=O[7]+M9+2,O[9]=O[8]+4
0635 IF O[9]+1<WIDTH THEN GOTO 0650
0640 LET O[9]=WIDTH-1,O[8]=O[9]-4,O[7]=O[8]-M9,O[6]=O[7]-M1,O[4]=O[6]-M2
0645 LET O[3]=O[4]-4,O[1]=M7+2,O[2]=O[1]+P[0]+1
0650 LET H1$(O[3]+1)="Prod",X1$="Quantity",X2$="On Hand",DW=O[3]-O[2]
0660 IF VALUE$="U" THEN LET X1$="Estimated Annual",X2$="Usage"
0665 IF VALUE$="B" THEN LET X1$="Ending ",X2$="Balance"
0670 LET X=O[6]-LEN(X1$),O[5]=O[6],H1$(X)=X1$,H1$(O[6]+M1-5)="Total"
0675 LET H1$(O[8])="Old",H1$(O[9])="New",H2$(O[0]+M7-2)="Seq"
0680 LET H2$(O[1]+1)="Item",H2$(O[2]+1)="Description",H2$(O[3]+1)="Type"
0685 LET H2$(X)=X2$,H2$(O[6]+M1-5)="Value",H2$(O[7]+2)="Percent"
0690 LET H2$(O[8])="ABC",H2$(O[9])="ABC",COST$=""
0700 REM " --- Background"
0710 PRINT @(COLUMN-11),FILL(15+P[0]),@(COLUMN-9),'SB',"Printing",'SF',
0800 REM " --- Retrieve item totals"
0810 LET S0$(1)=N0$+"I"
0820 FIND (IVS10_DEV,KEY=S0$,DOM=0830)IOL=IVS10I
0830 LET HEADINGS$[1]=F5$+" By Item",X$=FNB$(S1$(5,3))
0840 IF POS(VALUE$="UB")>0 THEN LET X$="Period "+S1$(1,2)+" "+FNYY21_YY$(S1$(3,2))
0850 LET HEADINGS$[2]="Valued By "+X1$+" "+X2$+" As Of "
0860 LET HEADINGS$[2]=HEADINGS$[2]+X$,HEADINGS$[4]=H1$,HEADINGS$[5]=H2$
0900 REM " --- Position file"
0910 IF LEVEL$="W" THEN GOTO 2000
0920 LET NUMBER=0,CHANNEL=IVE07_DEV,TOTAL=0,WAREHOUSE=0
0990 READ (IVW07_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next IVW-07 record"
1010 LET K$=KEY(IVW07_DEV,END=1900)
1020 IF POS(N0$=K$)<>1 THEN GOTO 1900
1030 PRINT @(COLUMN,11),K$(23,P[0]),
1040 READ (IVW07_DEV)IOL=IVW07A
1500 REM " --- Process work file record"
1510 GOSUB 6200
1800 REM " --- Loop back for next record"
1890 GOTO 1000
1900 REM " --- Item report completed"
1910 LET TOTALS$="Total For Report",TOTALS=TOTAL
1920 GOSUB 6900
1930 GOSUB 5500
1990 IF LEVEL$="I" THEN GOTO 4000
2000 REM " --- Print positions and headings for warehouse report"
2010 LET O[1]=M7+2,O[2]=O[1]+P[0]+1,O[3]=O[2]+P[3]+P[4]+P[5]+2,O[4]=O[3]+XP-M2
2020 LET O[5]=O[4]+M2,O[6]=O[5]+M3,O[7]=O[6]+M1,O[8]=O[7]+M9+2,O[9]=O[8]+4
2030 IF O[9]+1<WIDTH THEN GOTO 2050
2040 LET O[9]=WIDTH-1,O[8]=O[9]-4,O[7]=O[8]-M9,O[6]=O[7]-M1,O[5]=O[6]-M2
2045 LET O[4]=O[5]-M3,O[3]=O[4]-4,O[1]=M7+2,O[2]=O[1]+P[0]+1
2050 LET H1$(O[3]+1)="Prod",DW=O[3]-O[2],X=O[5]-LEN(X1$)
2060 LET H1$(X)=X1$,H1$(O[5]+M3-4)="Unit",H1$(O[6]+M1-5)="Total"
2070 LET H1$(O[8])="Old",H1$(O[9])="New",H2$(O[0]+M7-2)="Seq"
2080 LET H2$(O[1]+1)="Item",H2$(O[2]+1)="Description",H2$(O[3]+1)="Type"
2085 LET H2$(X)=X2$,H2$(O[5]+M3-4)="Cost",H2$(O[6]+M1-5)="Value"
2090 LET H2$(O[7]+2)="Percent",H2$(O[8])="ABC",H2$(O[9])="ABC"
2200 REM " --- Initializations for warehouse report"
2210 DIM COUNT[25],VALUE[25]
2220 LET T0$="",CHANNEL=IVE17_DEV,HEADINGS$[1]=F5$+" By Warehouse"
2230 LET HEADINGS$[2]="Valued By "+X1$+" "+X2$,HEADINGS$[4]=H1$,NUMBER=0
2240 LET HEADINGS$[5]=H2$,SEQUENCE=0,TOTAL=0,WAREHOUSE=0,L1=0,L=L9+1
2900 REM " --- Position file"
2910 PRINT @(COLUMN,11),FILL(3+P[0]),
2990 READ (IVW17_DEV,KEY=N0$+BEGWHSE$,DOM=3000)
3000 REM " --- Read next IVW-17 record"
3010 LET K$=KEY(IVW17_DEV,END=4000)
3020 IF POS(N0$=K$)<>1 THEN GOTO 4000
3030 IF ENDWHSE$<>"" THEN IF K$(3,2)>ENDWHSE$ THEN GOTO 4000
3040 PRINT @(COLUMN,11),K$(3,2)," ",K$(23,P[0]),
3050 READ (IVW17_DEV)IOL=IVW17A
3200 REM " --- Level break?"
3210 IF K$(3,2)<>T0$ THEN GOSUB 6000
3500 REM " --- Process work file record"
3510 LET COST$=STR(D[3]:M3$)
3520 GOSUB 6200
3900 REM " --- Loop back for next record"
3990 GOTO 3000
4000 REM " --- All done"
4010 LET DONE=1
4020 IF POS(LEVEL$="WB")>0 THEN GOSUB 6000
4080 CLOSE (7,ERR=4090)
4090 RUN "IVU.PA"
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5090 RETURN 
5500 REM " --- Print totals"
5510 LET HEADINGS=2
5520 IF L+CLASS+7>L9 THEN GOSUB 5000
5530 PRINT (7)""
5540 PRINT (7)@(44+M8-8),"Number",@(59),"Percent",@(87),"Percent"
5550 PRINT (7)@(36),"Class",@(44+M8-8),"Of Items",@(59),"Of Total",@(72),"Total Value",@(87),"Of Total"
5560 PRINT (7)""
5570 LET HEADINGS=6,L=L9+1
5600 REM " --- Print line for each class"
5610 FOR X=0 TO 25
5620 IF COUNT[X]=0 AND VALUE[X]=0 THEN GOTO 5690
5630 LET PERCENT=0,AMOUNT=0
5640 IF NUMBER<>0 THEN LET PERCENT=(COUNT[X]*100)/NUMBER
5650 IF WAREHOUSE<>0 THEN LET AMOUNT=(VALUE[X]*100)/WAREHOUSE
5660 PRINT (7)@(36),LETTERS$(X+1,1),@(44),COUNT[X]:M8$,@(59),PERCENT:M9$,@(84-M1),VALUE[X]:M1$,@(87),AMOUNT:M9$
5690 NEXT X
5700 REM " --- Grand totals"
5710 PRINT (7)""
5720 PRINT (7)@(36),"Total",@(44),NUMBER:M8$,@(84-M1),WAREHOUSE:M1$
5790 RETURN 
6000 REM " --- Warehouse Break"
6010 IF T0$="" THEN GOTO 6100
6020 IF L1=0 THEN GOTO 6100
6030 LET TOTALS$="Total For "+H9$,TOTALS=WAREHOUSE
6040 GOSUB 6900
6050 LET TOTALS$="Total For Report",TOTALS=TOTAL
6060 IF DONE THEN GOSUB 6900
6090 GOSUB 5500
6100 IF DONE THEN GOTO 6190
6110 DIM X0$(32),S0$(5),S1$(16),S[3],COUNT[25],VALUE[25]
6115 LET CLASS=0,TOTALPCT=0,XLIMIT=PERCENTS[CLASS],SEQUENCE=0,NUMBER=0
6120 LET T0$=K$(3,2),X0$(6,24)="(Not on File)",L=L9+1,L1=0,WAREHOUSE=0
6130 FIND (IVM10_DEV,KEY=N0$+"C"+T0$,DOM=6140)IOL=IVM10C
6140 LET H9$="Warehouse "+T0$+" "+FNP$(X0$(6,24)),HEADINGS$[1]=F5$+" For "+H9$
6150 FIND (IVS10_DEV,KEY=N0$+"W"+T0$,DOM=6160)IOL=IVS10W
6160 LET X$=FNB$(S1$(5,3))
6170 IF POS(VALUE$="UB")>0 THEN LET X$="Period "+S1$(1,2)+" "+FNYY21_YY$(S1$(3,2))
6180 LET HEADINGS$[2]="Valued By "+X1$+" "+X2$+" As Of "+X$
6190 RETURN 
6200 REM " --- Process work file record"
6210 DIM DESCRIPTION$(DW)
6220 LET A0$(1)=N0$+D0$(23),A1$(1)="(Not On File)",A2$(1)=""
6230 FIND (IVM01_DEV,KEY=A0$,DOM=6240)IOL=IVM01A
6240 LET DESCRIPTION$(1)=FNITEM$(A1$,P[3],P[4],P[5]),OLDABC$=D1$(1,1)
6300 REM " --- Calculate percentage"
6310 LET PERCENT=0,QUANTITY=D[0],AMOUNT=D[4],X=0
6320 IF VALUE$="O" THEN LET QUANTITY=D[1],AMOUNT=D[5],X=1
6330 IF VALUE$="B" THEN LET QUANTITY=D[2],AMOUNT=D[6],X=2
6340 IF S[X]<>0 THEN LET PERCENT=(AMOUNT*100)/S[X]
6400 REM " --- Print detail line"
6410 IF L+1>L9 THEN GOSUB 5000
6420 LET SEQUENCE=SEQUENCE+1,L=L+1,NEWABC$=LETTERS$(CLASS+1,1)
6430 LET TOTAL=TOTAL+AMOUNT,WAREHOUSE=WAREHOUSE+AMOUNT,L1=L1+1
6440 PRINT (7)@(O[0]),SEQUENCE:M7$,@(O[1]),A0$(3,P[0]),@(O[2]),DESCRIPTION$,@(O[3]),A2$(1,3),@(O[4]),QUANTITY:M2$,@(O[5]),COST$,@(O[6]),AMOUNT:M1$,@(O[7]),PERCENT:M9$,@(O[8]),OLDABC$,@(O[9]),NEWABC$
6500 REM " --- New class?"
6510 LET TOTALPCT=MIN(TOTALPCT+PERCENT,100),NUMBER=NUMBER+1
6520 LET VALUE[CLASS]=VALUE[CLASS]+AMOUNT,COUNT[CLASS]=COUNT[CLASS]+1
6530 IF TOTALPCT>XLIMIT THEN LET CLASS=CLASS+1,XLIMIT=XLIMIT+PERCENTS[CLASS]
6600 REM " --- Update IVE-07/IVE-17"
6610 DIM R0$(24),R1$(16),R[6]
6620 LET R0$(1,4)=D0$(1,4),R0$(5)=D0$(23)
6630 LET R1$(1)=OLDABC$,R1$(2)=NEWABC$,R[ALL]=D[ALL]
6640 WRITE (CHANNEL,KEY=R0$)IOL=IVE07A
6690 RETURN 
6900 REM " --- Print totals"
6910 IF L+1>L9 THEN GOSUB 5000
6920 PRINT (7)@(O[6]-LEN(TOTALS$)),TOTALS$,@(O[6]),TOTALS:M1$
6930 LET L=L+1
6990 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

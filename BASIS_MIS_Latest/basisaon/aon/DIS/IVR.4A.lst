0010 REM "IVR - Price Change Reversal Register"
0020 REM "Program IVR.4A"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=5
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0130 LET FILES$[4]="IVM-10",FILES$[5]="IVW-09",OPTIONS$[5]="C"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0180 LET IVM10_DEV=CHANNELS[4],IVW09_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$,A6$,A[ALL]
0220 IVM02A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0230 IVW09A: IOLIST W0$(1),W1$,W2$,W[ALL]
0240 IVM10A: IOLIST X0$(1)
0250 IVM10C: IOLIST X0$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0400 REM " --- Parameters"
0405 DIM P[5]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="IV"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0450 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1)),WHSE$=I3$(6,2)
0460 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A[12],W0$(26),W[4]
0520 DIM B0$(24),B1$(64),B[20],X0$(32),HEADINGS$[3],O[4]
0560 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$,WHEN$=F0$(7,3),CLOCK$=""
0565 LET WIDTH=132,PAGE=0,L9=59,L=L9+1,HEADINGS=2,NUMBER=0,CODE$=""
0570 LET T0$="",T1$="",T2$="",M9$="-###0.00%",M9=LEN(M9$),DW=P[3]+P[4]+P[5]
0575 LET ACTION$="R",ENTRY$="IVW-09"
0600 REM " --- Print positions"
0610 CALL "SYC.RM",M4$,M4,11,15
0620 LET O[1]=P[0]+1,O[2]=O[1]+DW+1,O[3]=O[2]+4,O[4]=O[3]+M4+1
0630 IF O[4]+M4+7<WIDTH THEN GOTO 0800
0680 LET O[4]=WIDTH-M4-7,O[3]=O[4]-M4-1
0800 REM " --- Call price/cost change entry program"
0810 CALL "IVC.CA",ACTION$,ENTRY$,STATUS
0820 IF STATUS>0 THEN GOTO 9900
0830 READ (IVW09_DEV,KEY="",DOM=0840)
0840 LET K$=KEY(IVW09_DEV,END=0900)
0850 LET HEADINGS$[1]=F5$+" For List Price Code "+K$(3,2)
0900 REM " --- Report Initializations"
0910 CALL "SYC.GA",7,1,"","",STATUS
0920 IF STATUS THEN GOTO 9900
0930 CALL "SYC.NB","Printing",15+P[0],COLUMN
0990 READ (IVW09_DEV,KEY="",DOM=1000)
1000 REM " --- Next IVW-09 record"
1010 LET K$=KEY(IVW09_DEV,END=4000)
1030 READ (IVW09_DEV)
1040 PRINT @(COLUMN,11),K$(3,2)," ",K$(5,2)," ",K$(7,3)," ",K$(10,P[0]),
1200 REM " --- Level breaks?"
1210 IF K$(5,2)<>T0$ THEN GOSUB 6000
1220 IF K$(7,3)<>T1$ THEN GOSUB 6200
1400 REM " --- Additional reads"
1410 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A[12],B0$(24),B1$(64),B[20]
1420 LET A0$(1)=N0$+K$(10),B0$(1)=N0$+K$(5,2)+K$(10)
1430 FIND (IVM01_DEV,KEY=A0$,DOM=1440)IOL=IVM01A
1440 FIND (IVM02_DEV,KEY=B0$,DOM=1450)IOL=IVM02A
1450 IF B1$(52,2)<>K$(3,2) THEN GOTO 3900
1460 LET DESCRIPTION$(1)=FNITEM$(A1$,P[3],P[4],P[5])
2000 REM " --- New Product Type?"
2010 IF H0$="" THEN GOTO 2500
2020 IF L+4+NL>L9 THEN GOSUB 5000
2030 IF NL>0 THEN PRINT (7)""
2040 PRINT (7)H0$
2050 PRINT (7)""
2060 LET L=L+2+NL,NL=1,H0$=""
2500 REM " --- Print detail line"
2510 IF L+1>L9 THEN GOSUB 5000
2540 PRINT (7)@(O[0]),A0$(3,P[0]),@(O[1]),FNITEM$(A1$,P[3],P[4],P[5]),@(O[2]),A2$(4,2),@(O[3]),B[9]:M4$,@(O[4]),B[10]:M4$," ",B1$(54,2)
2550 IF SPLIT>0 THEN PRINT (7)@(O[0]+2),DESCRIPTION$
2560 LET L=L+NEEDED,NUMBER=NUMBER+1,NL=1
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF NUMBER>0 THEN GOTO 4100
4020 GOSUB 5000
4030 PRINT (7)"No Price Change Entries Found"
4100 REM " --- Run update overlay"
4110 LET CHANNEL=IVW09_DEV,O0=2,FIRST$=N0$
4120 CLOSE (7,ERR=4130)
4190 RUN "IVU.GA"
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+4,NL=0
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)@(O[3]+M4-11),"Current",@(O[4]+M4-11),"New",@(O[4]+M4+1),"Price"
5050 PRINT (7)@(O[0]),"Item",@(O[1]),"Description",@(O[2]),"UM",@(O[3]+M4-11),"List Price",@(O[4]+M4-11),"List Price",@(O[4]+M4+1),"Code"
5060 PRINT (7)""
5090 RETURN 
6000 REM " --- Warehouse Break"
6010 IF T0$<>"" THEN GOSUB 6200
6020 DIM X0$(32)
6030 LET T0$=K$(5,2),X0$(6,24)="(Not on File)",L=L9+1,HEADINGS=3
6040 FIND (IVM10_DEV,KEY=N0$+"C"+T0$,DOM=6050)IOL=IVM10C
6050 LET HEADINGS$[2]="Warehouse "+T0$+" "+FNP$(X0$(6,24))
6090 RETURN 
6200 REM " --- Product Break"
6210 DIM X0$(32)
6220 LET T1$=K$(7,3),X0$(7,20)="(Not On File)"
6230 FIND (IVM10_DEV,KEY=N0$+"A"+T1$,DOM=6440)IOL=IVM10A
6240 LET H0$="Product "+T1$+" "+X0$(7,20)
6290 RETURN 
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "OPU - Invoice History Purge"
0020 REM "Program OPU.DA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES=8
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ART-03",FILES$[2]="ART-13",FILES$[3]="ART-33"
0130 LET FILES$[4]="ART-23",FILES$[5]="ART-43",FILES$[6]="ART-53"
0135 LET FILES$[7]="ARM-01",FILES$[8]="ART-01",FILES$[9]="SYS-01"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0155 LET ART03_DEV=CHANNELS[1],ART13_DEV=CHANNELS[2],ART33_DEV=CHANNELS[3]
0160 LET ART23_DEV=CHANNELS[4],ART43_DEV=CHANNELS[5],ART53_DEV=CHANNELS[6]
0165 LET ARM01_DEV=CHANNELS[7],ART01_DEV=CHANNELS[8],SYS01_DEV=CHANNELS[9]
0200 REM " --- IOLISTS"
0210 ARM01A: IOLIST *,X0$(1)
0220 ART03A: IOLIST A0$,A[10]
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters "
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 DIM P[1]
0450 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2))
0500 REM " --- Initialize Data "
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 DIM A[10]
0530 LET K0=1,K9=20
0700 REM " --- Background "
0710 FOR X=4 TO 12 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(0,3),"Base Purge On Number Invoices Or Dates:",@(20,5),"Purge Through Date:",@(0,7),"Retain How Many Invoices Per Customer?:",@(20,9),"Beginning Customer: ",@(23,11),"Ending Customer:",'SF',@(0,22),'CL',
1000 REM " --- Options "
1020 LET V0$="S",V1$="ERC",V2$=P9$,V3$="ND",V4$="N=Leave A # Of Invoices Per Customer, D=Through Invoice Date, <F4>=End",V0=1,V1=40,V2=3,I0=0
1040 GOSUB 7000
1060 IF V3=4 THEN GOTO 9900
1070 IF POS(V$="ND")=0 THEN GOTO 1000
1080 LET P9$=V$
1090 GOTO 3900
1200 REM " --- Purge Through Date "
1210 IF P8$="" THEN LET P8$=F0$(7,3)
1220 IF P9$="N" THEN LET P8$=""; PRINT @(40,5),'CL'; GOTO 3900
1225 PRINT @(40,7),'CL',
1230 LET V0$="D",V1$="C",V2$=P8$,V3$="",V4$="Enter Invoice Date To Purge Through",V0=3,V1=40,V2=5
1240 GOSUB 7000
1260 IF V3=4 THEN GOTO 9900
1280 LET P8$=V$
1290 GOTO 3900
1400 REM " --- # Of Invoices To Retain For Each Customer "
1420 IF P9$="D" THEN LET P7$=""; PRINT @(40,7),'CL'; GOTO 3900
1430 LET V0$="N",V1$="C",V2$=P7$,V3$="###0",V4$="Enter Maximum Number Of Invoices To Retain For Each Customer",V0=3,V1=40,V2=7
1440 GOSUB 7000
1460 IF V3=4 THEN GOTO 9900
1470 LET P7=V,P7$=STR(V)
1490 GOTO 3900
1600 REM " --- Beginning Customer Number "
1620 LET V0$="M",V1$="CE",V2$=P6$,V3$=M0$,V4$="<F1>=First, <F3>=Customer Lookup",V0=P[0],V1=40,V2=9
1640 GOSUB 7000
1645 GOSUB CLEAR_SCREEN
1650 IF V3=2 THEN LET P6$=""; PRINT @(V1,V2),J1$(1,P[0]),'CL',; GOTO 1790
1660 IF V3=3 THEN GOTO 1700
1670 IF V$="" OR V3=4 OR V3=1 THEN LET V3=1,P6$=""; PRINT @(V1,V2),"First",'CL'; GOTO 1790
1690 GOTO 1730
1700 LET V4=2,V5=5,V6=13,V7=21,V8=P[0],V5$="C",V3$=M0$
1720 CALL "SYC.LC",V$
1725 IF LEN(V$)=0 THEN GOTO 1600
1750 DIM X0$(30)
1760 FIND (ARM01_DEV,KEY=N0$+V$,DOM=1770)IOL=ARM01A
1770 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$)," ",X0$
1780 LET P6$=V$
1790 GOTO 3900
1800 REM " --- Ending Customer Number "
1820 LET V0$="M",V1$="C",V2$=P5$,V3$=M0$,V4$="<F1>=Last, <F3>=Customer Lookup",V0=P[0],V1=40,V2=11
1840 GOSUB 7000
1845 GOSUB CLEAR_SCREEN
1850 IF V3=2 THEN LET P5$=""; PRINT @(V1,V2),J1$(1,P[0]),'CL',; GOTO 1790
1860 IF V3=3 THEN GOTO 1900
1870 IF V$="" OR V3=4 OR V3=1 THEN LET V3=1,P5$=""; PRINT @(V1,V2),"Last",'CL'; GOTO 1990
1890 GOTO 1930
1900 LET V4=2,V5=5,V6=13,V7=21,V8=P[0],V5$="C",V3$=M0$
1920 CALL "SYC.LC",V$
1925 IF LEN(V$)=0 THEN GOTO 1800
1930 IF V$<P6$ THEN GOTO 1800
1940 DIM X0$(30)
1960 FIND (ARM01_DEV,KEY=N0$+V$,DOM=1970)IOL=ARM01A
1970 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$)," ",X0$
1980 LET P5$=V$
1990 GOTO 3900
2000 REM " --- Display Processing Warning "  
2020 IF P9$="N" THEN GOTO 2100
2040 LET X$="Warning!! All Invoices With A Date Prior To "+FNB$(P8$)+" Will Be Purged"
2060 GOTO 2120
2100 LET X$="Warning!! This Will Retain Only "+P7$+" Invoices Per Customer!"
2120 PRINT @(INT((80-LEN(X$))/2),13),X$
2190 GOTO 3900
3900 REM " --- Next Field "
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,4000
4000 REM " --- Options "
4010 LET V0$="S",V1$="CE",V4$="Is The Above Information Correct? <Yes/No/End>: ",V0=3,V1=40+(INT(LEN(V4$)/2)+1),V2=22
4020 GOSUB 7000
4030 IF V3=4 THEN GOTO 9900
4040 IF V$="N" OR V$="NO" THEN FOR I=3 TO 11 STEP 2; PRINT @(40,I),'CL',; NEXT I; GOTO 1000
4060 IF V$<>"YES" THEN GOTO 9900
4090 LET EXIT_CTRL=1
5000 REM " --- Process History Data "
5007 FOR X=12 TO 14 STEP 2
5010 CALL "SYC.WC",1,0,80,0,0,X,0
5015 NEXT X
5020 PRINT @(0,13),'CE','SB',@(29,13),"Searching: ",@(31,15),"Purging:",'SF'
5030 IF P9$="" THEN GOTO 9900
5040 IF P9$="N" AND P7=0 THEN GOTO 9900
5050 IF P9$="D" AND P8$="" THEN GOTO 9900
5090 READ (ART03_DEV,KEY=N0$+"  "+P6$,DOM=5100)
5100 REM " --- Read Invoice History Header Records "
5120 LET K$=KEY(ART03_DEV,END=5900)
5140 IF K$(1,2)<>N0$ THEN GOTO 5900
5160 IF P5$<>"" AND K$(5,6)>P5$ THEN GOTO 5900
5165 READ (ART03_DEV,KEY=K$)IOL=ART03A
5170 PRINT @(40,13),FNF$(K$(5,P[0]),M0$),; REM ""  ",K$(11,7)
5190 IF P9$="D" THEN GOTO PROCESS_BY_DATE
5220 IF K$(1,10)<>T0$ THEN GOSUB PROCESS_BY_COUNT
5240 LET T0=T0+1
5280 GOTO 5100
5400 PROCESS_BY_DATE: 
5405 REM " --- Process Invoice History By Invoice Date "
5440 IF A0$(24,3)>P8$ THEN GOTO 5100
5450 GOSUB DELETE_HISTORY
5460 GOTO 5100
5500 DELETE_HISTORY: 
5505 REM " --- Process History Detail Lines "
5510 READ (ART13_DEV,KEY=A0$(1,17)+"000",DOM=5520)
5520 LET K2$=KEY(ART13_DEV,END=DELETE_SHIPTO)
5540 IF POS(A0$(1,17)=K2$)<>1 THEN GOTO DELETE_SHIPTO
5560 REMOVE (ART13_DEV,KEY=K2$)
5580 GOTO 5520
5600 DELETE_SHIPTO: 
5605 REM " --- Remove Manual Ship-To "
5630 REMOVE (ART33_DEV,KEY=A0$(1,2)+A0$(5,13),DOM=5635)
5640 REM " --- Remove Serial/Lotted History Detail "
5650 READ (ART23_DEV,KEY=A0$(1,17),DOM=5660)
5660 LET R0$=KEY(ART23_DEV,END=5700)
5665 IF R0$(1,17)<>A0$(1,17) THEN GOTO 5700
5670 REMOVE (ART23_DEV,KEY=R0$)
5680 GOTO 5660
5700 REM " --- Remove History Cross Reference Records"
5710 FIND (ART01_DEV,KEY=A0$(1,17)+"00",DOM=5730)
5720 GOTO 5750
5730 REMOVE (ART43_DEV,KEY=A0$(1,4)+A0$(11,7)+A0$(5,6),DOM=5740)
5740 REMOVE (ART53_DEV,KEY=A0$(1,4)+A0$(24,3)+A0$(11,7)+A0$(5,6),DOM=5750)
5750 REM " --- Remove History Header Record "
5760 REMOVE (ART03_DEV,KEY=A0$(1,20))
5770 PRINT @(40,15),FNF$(K$(5,P[0]),M0$),"  ",K$(11,7),"-",K$(18,3)
5790 RETURN 
5900 REM " --- Finished "
5920 IF P9$="D" THEN GOTO 9900
5940 LET T9$="E"
5950 GOSUB PROCESS_BY_COUNT
5960 GOTO 9900
6000 PROCESS_BY_COUNT: 
6005 REM " --- Purge Based On Number Of Invoices "
6020 IF T0$="" THEN GOTO 6500
6040 IF T0<=P7 THEN GOTO 6500
6080 LET T1=T0-P7,T2=0
6100 READ (ART03_DEV,KEY=T0$,DOM=6120)
6120 LET K2$=KEY(ART03_DEV,END=9900)
6140 IF K2$(1,10)<>T0$ THEN GOTO 6500
6150 FIND (ART03_DEV,KEY=K2$)IOL=ART03A
6160 GOSUB DELETE_HISTORY
6360 LET T2=T2+1
6370 IF T2<T1 THEN GOTO 6120
6500 IF T9$="E" THEN RETURN 
6520 LET T0$=K$(1,10)
6540 LET T0=0,T1=0,T2=0
6590 RETURN 
6600 CLEAR_SCREEN: 
6605 REM " --- Clear Screen "
6610 FOR I=13 TO 21
6620 PRINT @(0,I),'CL',
6630 NEXT I
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

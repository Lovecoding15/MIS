0010 REM "OPM - Pricing Table Maintenance"
0020 REM "Program OPM.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " ---Open/Lock Files "
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="ARM-10",FILES$[3]="IVM-10"
0135 LET FILES$[4]="IVM-13"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],ARM10_DEV=CHANNELS[2],IVM10_DEV=CHANNELS[3]
0170 LET IVM13_DEV=CHANNELS[4]
0200 REM " --- IOLIST's "
0210 IVM10E: IOLIST A0$(1),A[ALL]
0220 ARM10M: IOLIST X0$(1)
0230 IVM13A: IOLIST B0$(1),B1$(1)
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$,M4$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Paramaters "
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)
0500 REM " Initialize Data "
0505 PRECISION NUM(P2$(5,1))
0510 DIM MESSAGE$[1],A0$(40),A[19],B0$(5),B1$(32),X0$(40)
0550 LET K0=1,K9=10,A0$(1,3)=N0$+"E",NUMBER=1
0560 LET M2=LEN(M2$),M8$="##0.00%"
0700 REM " --- Background "
0710 PRINT 'SB',@(3,3),"Item Class:",@(1,4),"Pricing Code:",@(2,5),"Description:",@(7,6),"Method:",@(15+M2-9,8),"Quantity",@(30),"Percentage",'SF',
0760 FOR X=1 TO 10
0770 PRINT 'SB',@(11,X+8),X:"#0:",'SF',
0780 NEXT X
0900 REM " --- Position file"
0990 READ (IVM10_DEV,KEY=A0$(1,3),DOM=1000)
1000 REM " --- Item Class"
1010 PRINT 'CF',
1020 LET V2$="",V9$="",V2$=KEY(IVM10_DEV,END=1050)
1030 IF POS(A0$(1,3)=V2$)<>1 THEN LET V2$="",V9$=""
1040 IF V2$<>"" THEN LET V9$=V2$(7,4),V2$=V2$(4,3)
1050 LET V4$="Enter 3 Character Item Class (<F3>=Lookup)"
1055 LET V0$="S",V1$="KERC",V3$="",V0=3,V1=15,V2=3
1060 GOSUB 7000
1065 ON V3 GOTO 1075,1000,1000,1070,4500
1070 CALL "IVC.LK",5,V1,V2,V$
1075 LET B0$(1)=N0$+V$
1080 FIND (IVM13_DEV,KEY=B0$,DOM=1000)IOL=IVM13A
1085 PRINT @(V1,V2),B0$(3)," ",B1$(1,20),
1090 LET A0$(4,3)=V$
1100 REM " --- Price code"
1110 LET V4$="Enter A 4 Character Pricing Code (<F3>=Lookup)"
1120 LET V0$="S",V1$="KC",V3$="",V2$=V9$,V0=4,V1=15,V2=4
1130 GOSUB 7000
1135 ON V3 GOTO 1160,1100,1000,1140,4500
1140 GOSUB 6400
1150 IF V$="" THEN GOTO 1100
1160 LET X0$(1)=N0$+"M"+V$
1170 FIND (ARM10_DEV,KEY=X0$(1,7),DOM=1100)IOL=ARM10M
1180 PRINT @(V1,V2),X0$(4,4),@(V0+V1+2,V2),X0$(8,20)
1190 LET A0$(7,4)=V$
1200 REM " --- Find Key "
1210 FIND (IVM10_DEV,KEY=A0$(1,K9),DOM=1400)IOL=IVM10E
1300 REM " --- Display "
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New Record "
1410 LET V4$="Is This A New Record"
1430 CALL "SYC.YN",1,V4$,1,V$,V3
1440 IF V3=4 THEN GOTO 4500
1450 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-Up "
1510 DIM A[19]
1520 LET A0$(11)=""
2000 REM " --- Description "
2010 LET V4$="Enter The Description For This Price Table Entry"
2020 LET V0$="S",V1$="C",V2$=A0$(11,20),V3$="",V0=20,V1=15,V2=5,I0=0
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 LET A0$(11,20)=V$
2090 GOTO 3900
2100 REM " --- Quantity Breaks "
2120 FOR X=0 TO 9
2200 REM " --- Quantity "
2210 LET V4$="Enter The Quantity For This Level Break"
2220 LET V0$="N",V1$="C",V2$=STR(A[X]),V3$=M2$,V0=M2,V1=15,V2=9+X
2230 GOSUB 7000
2240 LET A[X]=V
2250 IF V3=4 THEN EXITTO 3900
2260 IF V3<>2 THEN GOTO 2300
2270 IF X>0 THEN LET X=X-1
2290 GOTO 2200
2300 REM " --- Discount "
2310 LET V4$="Enter The Percentage For This Quantity Break"
2320 LET V0$="N",V1$="C",V2$=STR(A[X+10]),V3$=M8$,V0=M8,V1=33,V2=9+X
2330 GOSUB 7000
2350 IF A0$(31,1)="M" AND V>=100 THEN GOSUB 6000; GOTO 2300
2360 LET A[X+10]=V
2370 IF V3=4 THEN EXITTO 3900
2375 IF V3=2 THEN GOTO 2200
2380 NEXT X
2390 GOTO 3900
2400 REM " --- Method "
2410 LET V4$="C=Markup From Cost   L=Markdown From List   M=Margin Over Cost"
2420 LET V0$="S",V1$="CR",V2$=A0$(31,1),V3$="CLM",V0=1,V1=15,V2=6
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 IF POS(V$="CLM")=0 THEN GOTO 2400
2460 LET A0$(31,1)=V$
2490 GOTO 3900
3900 REM " --- Next Field "
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2400,2100,3990
3990 GOSUB 5000
4000 REM " --- Options "
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="KC",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4050 IF V$="DELETE" THEN GOTO 4400
4060 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Check Sequence "
4110 FOR X=1 TO 9
4120 IF A[X]=0 THEN EXITTO 4300
4130 IF A[X]>A[X-1] THEN GOTO 4150
4140 EXITTO 4200
4150 NEXT X
4190 GOTO 4300
4200 REM 
4210 LET V4$="Quantity Breaks Not In Ascending Sequence"
4220 GOSUB 6500
4290 GOTO 2100
4300 REM " --- Write "
4310 IF A0$(31,1)<>"M" THEN GOTO 4350
4320 GOSUB 6100
4330 IF MOC$="OK" THEN GOTO 4350
4335 LET I0=1
4340 GOTO 3900
4350 WRITE (IVM10_DEV,KEY=A0$(1,K9))IOL=IVM10E
4390 GOTO 1000
4400 REM " --- Delete "
4410 REMOVE (IVM10_DEV,KEY=A0$(1,K9),DOM=4420)
4420 LET O8=0
4490 GOTO 1000
4500 REM " --- Print "
4520 LET V4$="Do You Wish To Print A List Of All Records"
4530 CALL "SYC.YN",1,V4$,1,V$,V3
4540 ON POS(V$="YN") GOTO 4500,4550,9900
4590 RUN "OPM.CB"
5000 REM " --- Display "
5020 PRINT @(15,5),A0$(11,20),@(15,6),A0$(31,1),
5040 FOR X=0 TO 9
5050 IF A[X]<>0 THEN PRINT @(15,X+9),A[X]:M2$,@(33),A[X+10]:M8$,
5080 NEXT X
5090 RETURN 
6000 REM " --- Margin Over Cost Warning"
6010 LET V4$="Margin Over Cost Must Be Less Than 100%"
6020 GOSUB 6500
6030 LET V3=0
6090 RETURN 
6100 REM " --- Verify Margin Over Cost"
6110 LET MOC$="OK"
6120 FOR X=0 TO 9
6140 IF A[X+10]>=100 THEN LET MOC$="BAD"
6160 NEXT X
6180 IF MOC$<>"OK" THEN GOSUB 6000
6190 RETURN 
6400 REM " --- Price Code Lookup"
6410 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6420 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6430 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=4
6440 LET FIELD[1]=1,POSITION[1]=8,LENGTH[1]=20
6450 LET V$="SKIP",ROW=V2+2,RECORD$="M"
6460 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6470 CALL "SYC.LK",ARM10_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6490 RETURN 
6500 REM " --- Windowed warning"
6510 LET MESSAGE$[0]=V4$+" (<Enter>=Continue)"
6520 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
6590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions "
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

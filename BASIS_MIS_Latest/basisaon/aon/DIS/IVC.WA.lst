0010 REM "IVC - Return Warehouse Information"
0020 REM "Program IVC.WA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0041 REM "ACTION$    : Action to perform (input)"
0042 REM "             ""  = Return info for specific warehouse"
0043 REM "             "A" = Return info for all warehouses"
0050 REM "CHANNELS[N]: Channel Numbers (input)"
0052 REM "             [2] = IVM-02"
0053 REM "             [3] = IVM-03"
0060 REM "ITEM$[n]   : Inventory Item Information"
0061 REM "             [0] = Firm ID"
0062 REM "             [1] = Warehouse ID (optional)"
0063 REM "             [2] = Inventory Item Number"
0064 REM "WHSE$[n]   : Warehouse Information (returned)"
0065 REM "             [0] = Warehouse ID"
0066 REM "             [1] = IVM-02 second string"
0067 REM "             [2] = IVM-02 third string"
0068 REM "WHSE[n]    : Warehouse Information (returned)"
0069 REM "             [0] to [20] = IVM-02 numerics"
0070 REM "VALUE      : Total Value (returned)"
0075 REM "STATUS     : Status Code (returned)"
0080 REM 
0085 SETERR 9000
0087 SETESC 9000
0090 ENTER ACTION$,CHANNELS[ALL],ITEM$[ALL],WHSE$[ALL],WHSE[ALL],VALUE,STATUS
0200 REM " --- IOLIST's"
0220 IVM02A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0500 REM " --- Initializations"
0510 DIM B0$(24),B1$(64),B[20]
0550 LET IVM02_DEV=CHANNELS[2],IVM03_DEV=CHANNELS[3],VALUE=0
0560 LET N0$=ITEM$[0],WAREHOUSE$=ITEM$[1],ITEM_NO$=ITEM$[2],STATUS=0
0570 LET WHSE$[0]=WAREHOUSE$,WHSE$[1]=B1$,WHSE$[2]="",XLIMIT=20
0580 FOR X=0 TO XLIMIT
0585 LET WHSE[X]=0
0590 NEXT X
1000 REM " --- Single Warehouse"
1010 IF POS("A"=ACTION$)>0 THEN GOTO 1900
1020 GOSUB 6000
1090 GOTO 4000
1900 REM " --- Position warehouse file"
1990 READ (IVM03_DEV,KEY=N0$+"G"+ITEM_NO$,DOM=2000)
2000 REM " --- All Warehouses"
2010 LET K3$=KEY(IVM03_DEV,END=4000)
2020 IF POS(N0$+"G"+ITEM_NO$=K3$)<>1 THEN GOTO 4000
2030 READ (IVM03_DEV)
2040 LET WAREHOUSE$=K3$(24,2)
2050 GOSUB 6000
2060 IF STATUS>0 THEN GOTO 4000
2090 GOTO 2000
4000 REM " --- All done"
4090 GOTO 9900
6000 REM " --- Read warehouse record and accumulate totals"
6010 LET B0$(1)=N0$+WAREHOUSE$,B0$(5)=ITEM_NO$,STATUS=11
6020 FIND (IVM02_DEV,KEY=B0$,DOM=6090)IOL=IVM02A
6030 FOR X=0 TO XLIMIT
6040 LET WHSE[X]=WHSE[X]+B[X]
6050 NEXT X
6060 LET VALUE=VALUE+B[0]*B[11],STATUS=0
6070 IF XLIMIT=20 THEN LET XLIMIT=6,WHSE$[0]=B0$(3,2),WHSE$[1]=B1$,WHSE$[2]=B2$
6090 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9950 EXIT 
9999 END

0010 REM "OPX - Rebuild Invoice History Cross-References"
0020 REM "Program OPX.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ART-01",FILES$[2]="ART-03",FILES$[3]="SYS-01"
0140 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET ART01_DEV=CHANNELS[1],ART03_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLISTS"
0210 ART01A: IOLIST A0$
0220 ART03A: IOLIST A0$
0230 ART43A: IOLIST ART43_K$
0240 ART53A: IOLIST ART53_K$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters "
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 PRINT 'SB',@(29,3),"Processing",'SF',
0800 REM " --- Options
0810 LET V4$="Do You Wish To "+F5$
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0850 REM " --- Clear And Lock ART-53"
0860 LET FILES$[4]="ART-43",FILES$[5]="ART-53"
0865 LET OPTIONS$[4]="CL",OPTIONS$[5]="CL"
0870 CALL "SYC.DA",1,4,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0880 IF STATUS THEN GOTO 9900
0890 LET ART43_DEV=CHANNELS[4],ART53_DEV=CHANNELS[5]
0900 REM " --- Initialize File "
0910 PRINT @(40,3),"Open Invoices",
0920 CALL "SYC.NB","Updating",17,COLUMN
0990 READ (ART01_DEV,KEY="",DOM=1000)
1000 REM " --- Read Open Invoice Records "
1010 LET K$=KEY(ART01_DEV,END=2000)
1020 READ (ART01_DEV)IOL=ART01A
1030 GOSUB 6000
1900 REM " --- Loop back for next record"
1990 GOTO 1000
2000 REM " --- Done with open invoices"
2900 REM " --- Position Invoice History File"
2910 PRINT @(40,3),"Invoice History",
2990 READ (ART03_DEV,KEY="",DOM=3000)
3000 REM " --- Read Invoice History Records "
3010 LET K$=KEY(ART03_DEV,END=4000)
3020 READ (ART03_DEV)IOL=ART03A
3030 GOSUB 6000
3900 REM " --- Loop back for next record"
3990 GOTO 3000
4000 REM " --- Done"
4090 GOTO 9900
6000 REM " --- ART-43 Invoices by Customer"
6010 PRINT @(COLUMN,11),A0$(1,2)," ",A0$(5,6)," ",A0$(11,7),
6020 LET ART43_K$=A0$(1,4)+A0$(11,7)+A0$(5,6)
6030 WRITE (ART43_DEV,KEY=ART43_K$)IOL=ART43A
6100 REM " --- ART-53 Invoices by Date"
6110 LET ART53_K$=A0$(1,4)+A0$(24,3)+A0$(11,7)+A0$(5,6)
6120 WRITE (ART53_DEV,KEY=ART53_K$)IOL=ART53A
6190 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "TMM - Literature Code Maintenance
0020 REM "Program TMM.DA
0030 REM "ADD+ON SOFTWARE VERSION 5.0.2 - 07/24/91"
0035 REM "11/26/91 By Jerry Koch
0040 REM "COPYRIGHT (C) 1991 MicroAccounting Systems, Inc. ALL RIGHTS RESERVED"
0041 REM 
0042 REM "01/06/97 KJS WA: #221 ADS"
0079 REM 
0080 BEGIN 
0090 SETERR 9000
0100 REM "OPEN/LOCK FILES"
0110 OPEN (1)"TMM-10"
0160 OPEN (6)"SYS-01"
0200 REM "IOLISTS"
0210 IOLIST A0$(1)
0270 IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0401 GOSUB 62000; REM "WA: #221
0410 FIND (6,KEY="T"+FID(0),DOM=9800)IOL=0270
0420 LET N0$=PRM$[FID,TFIRM],N1$=PRM$[FID,TFNAME],N2$="AR",N4$=PRM$[FID,TPNAME]; REM "WA: #221
0450 DIM P[1]
0490 LET P[0]=NUM(PRM$[AR,KEYLEN]),P[1]=NUM(PRM$[AR,KEYMAX]); REM "WA: #221
0500 REM "INIT DATA"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 DIM A0$(30)
0530 LET K0=1,K9=5
0540 LET A0$(1,3)=N0$+"D"
0550 LET M8$="###.##-",M9$="####-"
0700 REM "BACKGROUND"
0720 PRINT 'SB',@(4,3),"Lit. Code:",@(2,4),"Description:",
0790 PRINT 'SF',
0800 REM "OPTIONS"
0900 REM "INIT FILE"
0920 READ (1,KEY=A0$(1,3),DOM=0930)
1000 REM "KEY"
1010 PRINT 'CF',
1020 LET V2$="  ",V2$=KEY(1,END=1050)
1025 IF V2$(1,2)<>N0$ THEN GOTO 1050
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$="  "
1040 IF V2$<>"  " THEN LET V2$=V2$(4,2)
1050 LET V0$="S",V1$="EKC",V3$="",V0=2,V1=15,V2=3,V4$="Enter Literature Code, (<F3>=Lookup)"
1060 GOSUB 7000
1070 IF V3=4 OR V$="END" THEN GOTO 4500
1080 IF V3=3 THEN GOSUB 6000
1090 LET A0$(4)=V$
1100 PRINT @(V1,V2),A0$(4,2)
1200 REM "FIND KEY"
1210 LET O9=0
1220 FIND (1,KEY=A0$(1,K9),DOM=1400)IOL=0210
1300 REM "DISPLAY"
1320 GOSUB 5000
1340 GOTO 4000
1400 REM "NEW RECORD"
1410 LET V0$="Y",V1$="C",V2$="",V3$="",V4$="Is This A New Record? <Y/N>: ",V0=1,V1=55,V2=22
1420 GOSUB 7000
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM "SET-UP"
1510 LET A0$(6)=""
2000 REM "DESCRIPTION
2006 GOSUB 5000
2010 LET O9=1
2020 LET V0$="S",V1$="",V2$=A0$(6,20),V3$="",V0=20,V1=15,V2=4,I0=0
2040 GOSUB 7000
2060 LET A0$(6,20)=V$
2090 GOTO 3900
3900 REM "NEXT FIELD"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,3100,3990
3990 LET I0=K0
4000 REM "OPTIONS"
4010 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct? <Y/N/Delete>: ",V0=6,V1=64,V2=22
4020 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF V$="" THEN IF O9=0 THEN GOTO 1000
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM "WRITE"
4120 WRITE (1,KEY=A0$(1,K9))IOL=0210
4190 GOTO 1000
4200 REM "DELETE"
4220 REMOVE (1,KEY=A0$(1,K9),DOM=4230)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4900,9900
4900 REM " --- Report Initializations"
4905 LET PROGRAM$="",NUMBER=1,HEADINGS=3,WIDTH=80,CHANNEL=1
4915 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4920 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4925 DIM HEADINGS$[HEADINGS]
4930 LET FIELD$[0]="Code",FIELD[0]=1,POSITION[0]=4
4940 LET LENGTH[0]=4,TYPE$[0]="S",MASK$[0]=""
4945 LET FIELD$[1]="Description",FIELD[1]=1,POSITION[1]=6
4950 LET LENGTH[1]=20,TYPE$[1]="S",MASK$[1]="",TOTAL$="Literature Codes"
4980 LET HEADINGS$[0]=N1$,HEADINGS$[1]=F5$+" Listing",RECORD$="D"
4990 RUN "SYR.AA"
5000 REM "DISPLAY"
5020 PRINT @(15,4),A0$(6,20),
5090 RETURN 
6000 REM " --- Code Lookup"
6005 LET TITLE$="Literature Codes"
6010 LET CHANNEL=1,NUMBER=1,RECORD$="D"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6050 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6060 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6070 LET V$="SKIP",ROW=V2+2
6080 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR((ASC(Q$)-32)*POS(" "<>Q2$(2,1)):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(NUM(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
62000 REM " --- Set-up ADD+ON Environment (WA: #221)"
62010 DIM PRM$[0]; CALL "ADC.01",PRM$[ALL],STATUS
62020 IF STATUS<>0 THEN EXITTO 9900
62030 LET FID=0,AP=1,AR=2,BM=3,DC=4,FA=5,GL=6,IV=7,MP=8,OP=9,PO=10,PR=11,SA=12,SH=13,SY=14,WO=15,TS=21,TB=22,DC=23,V2=24,EF=25; REM " ---- Module Id Numbers"
62040 DIM J$(80,"-"),J0$(40,"0"),J1$(80)
62050 LET INSTALL=0,KEYLEN=1,KEYMAX=2,KEYMASK=3,CPERIOD=4,CYEAR=5,DMASK=6,UMASK=7,RMASK=8,TWO=9,THREE=10,FOUR=11,EMASK=12,TOPCODE=1,TDATE=2,TMENU=3,TPRINTER=4,TSECURE=5,TFIRM=6,TPMODE=7,TPROG=8,TPOPTS=9,TFNAME=10,TPNAME=11,TLDATE=12
62090 RETURN 
65000 END

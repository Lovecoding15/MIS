0010 REM "ADS - Called Window Text Editor"
0020 REM "Program: ADC.07"
0030 REM "+----------------------------------------------------------+"
0031 REM "|            ADminstrator System Version 1.0.2a            |
0032 REM "|     Copyright (C) 1994 MicroAccounting Systems, Inc.     |
0033 REM "|                 All Rights Reserved"                     |
0034 REM "+----------------------------------------------------------+
0050 ENTER MODE,DATA$[ALL],LINES,L_WIDTH,WIDTH,HEIGHT,X_POS,Y_POS,TITLE$,NAME$
0060 REM " ---------------- Declare variables"
0061 REM "     MODE       - 0=Graphics borders, 1=No borders, 2=Existing window
0062 REM "     DATA$[ALL] - One dimensional string array of up to 99 lines
0063 REM "     LINES      - Number of lines to be edited
0064 REM "     L_WIDTH    - Line length
0065 REM "     WIDTH      - Display window width excluding borders
0066 REM "     HEIGHT     - Display window height excluding borders
0067 REM "     X_POS      - Horizontal pos top left corner of text window
0068 REM "     Y_POS      - Vertical pos top left corner of text window
0069 REM "     TITLE$     - Title text for display window
0070 REM "     NAME$      - Name of existing window else assigned
0071 REM "     CREC       - Dimension of DATA$ being edited
0072 REM "     CLINE      - Current display line within window
0073 REM "     INS$       - Insert mode flag - OFF/ON
0074 REM "     EXTRA$     - Inserted text that must be formatted
0075 REM "     TAB        - Number of tab spaces to right or left
0090 SETERR 9000; SETESC 9900
0110 LET CHANNEL=UNT; OPEN (CHANNEL,ERR=9900)FID(0)
0410 CALL "ADC.FK",1
0510 DIM PAD$(L_WIDTH)
0515 IF LEN(TITLE$)=0 THEN LET TITLE$="<F4>=End" ELSE IF LEN(TITLE$)+10<WIDTH THEN LET TITLE$=TITLE$+", <F4>=END"+PAD$,TITLE$=FNP$(TITLE$)
0520 LET CLINE=0,CREC=1,MAX_ROW=HEIGHT-1,INS$="OFF"
0530 LET TAB=INT(L_WIDTH/8); IF TAB<5 THEN LET TAB=5
0600 REM "------------------------------ Check Data$[CREC] For Length
0620 FOR J=1 TO LINES
0630 IF LEN(DATA$[J])>L_WIDTH THEN LET DATA$[J]=DATA$[J](1,L_WIDTH)
0640 NEXT J
0700 REM " ----------------------------- Build window"
0710 IF NAME$="" AND MODE<>1 THEN CALL "ADC.WA",MODE,WIDTH+2,HEIGHT+2,X_POS,Y_POS,TITLE$,NAME$ ELSE IF NAME$="" AND MODE=1 THEN PRINT 'WINDOW'(X_POS,Y_POS,WIDTH+2,HEIGHT+2),'WRAP'("OFF"),; GOSUB 6000 ELSE IF NAME$="" AND MODE=2 THEN GOSUB 6000
0800 REM " ----------------------------- Initial text display"
0820 PRINT 'CURSOR'("OFF"),
0830 FOR J=0 TO MAX_ROW; PRINT @(0,J),DATA$[J+1],; NEXT J
1100 REM " ----------------------------- Input character"
1110 INPUT (CHANNEL,SIZ=1,TIM=10,ERR=7000)'CURSOR'("ON"),@(XPOS,CLINE),X$,'CURSOR'("OFF"),; LET FKEY=CTL
1112 LET FKEY$=STR(FKEY:" 00")
1120 LET BRANCH=INT(POS(FKEY$=" 05 04 01 00 39 40 34 33 41 42 43 44 45 46 47 48 49 38 37")/3)
1500 REM " ---------------------------- Test control key input"
1520 ON BRANCH GOTO 5800,1600,11000,1700,3200,3400,1600,1600,1700,3600,3800,1700,3000,4400,5000,5200,5400,1600,1600
1600 REM " ---------------------------- Test for paragraph reformat"
1610 IF INS$="ON" AND REFORM$="Y" THEN GOSUB 6100
1620 ON BRANCH GOTO 1690,4000,1690,1690,1690,1690,2000,2200,1690,1690,1690,1690,1690,1690,1690,1690,1690,2400,2600
1690 ESCAPE
1700 REM " ---------------------------- Test for partial reformat"
1710 IF INS$="ON" AND BRANCH=3 THEN LET REFORM$="Y"; IF XPOS=L_WIDTH THEN LET EXTRA$=" "+EXTRA$ ELSE LET EXTRA$=DATA$[CREC](L_WIDTH)+EXTRA$,DATA$[CREC](L_WIDTH)=" "; IF XPOS<L_WIDTH-1 THEN LET DATA$[CREC](XPOS+1)=" "+DATA$[CREC](XPOS+1)
1712 IF INS$="ON" AND REFORM$="Y" THEN LET UPPER=CREC; GOSUB 11100
1720 ON BRANCH GOTO 1790,4000,1790,4600,1790,1790,2000,2200,5600,1790,1790,2800,1790,1790,1790,1790,1790,2400,2600
1790 ESCAPE
2000 REM " ---------------------------- Fkey = -3 Down Arrow"
2010 IF XPOS=L_WIDTH THEN LET XPOS=L_WIDTH-1
2020 IF CREC>=LINES THEN PRINT 'RB',; GOTO 1100
2030 LET CREC=CREC+1
2040 IF CLINE=MAX_ROW THEN PRINT @(0,0),'LD',@(0,CLINE),DATA$[CREC](1), ELSE LET CLINE=CLINE+1
2190 GOTO 1100
2200 REM " ---------------------------- Fkey = -4 Up Arrow"
2210 IF XPOS=L_WIDTH THEN LET XPOS=L_WIDTH-1
2220 IF CREC<=1 THEN PRINT 'RB',; GOTO 1100
2230 LET CREC=CREC-1
2240 IF CLINE=0 THEN PRINT @(0,0),'LI',@(0,CLINE),DATA$[CREC], ELSE LET CLINE=CLINE-1
2280 IF XPOS=L_WIDTH-1 AND FKEY=5 THEN PRINT @(XPOS,CLINE),DATA$[CREC](XPOS+1,1),
2390 GOTO 1100
2400 REM " ---------------------------- Fkey = -16 Page Down"
2410 IF XPOS=L_WIDTH THEN LET XPOS=L_WIDTH-1
2420 IF CREC=LINES THEN PRINT 'RB',; GOTO 2490
2430 LET X=CREC,CREC=CREC+MAX_ROW+1
2432 IF CREC+MAX_ROW-CLINE>LINES THEN LET CREC=LINES+CLINE-MAX_ROW
2434 IF CREC=X THEN PRINT 'RB'; GOTO 2490
2440 FOR J=0 TO MAX_ROW; PRINT @(0,J),DATA$[CREC-CLINE+J],; NEXT J
2490 GOTO 1100
2600 REM " ---------------------------- Fkey = -17 Page Up"
2610 IF XPOS=L_WIDTH THEN LET XPOS=L_WIDTH-1
2620 IF CREC=1 THEN PRINT 'RB',; GOTO 2690
2625 IF CREC<=MAX_ROW+1 THEN LET CREC=1,CLINE=0; GOTO 2690
2630 LET X=CREC,CREC=CREC-(MAX_ROW+1)
2632 IF CREC-CLINE<1 THEN LET CREC=1+CLINE
2634 IF CREC=X THEN PRINT 'RB'; GOTO 2690
2640 FOR J=0 TO MAX_ROW; PRINT @(0,J),DATA$[CREC-CLINE+J],; NEXT J
2690 GOTO 1100
2800 REM " ---------------------------- Fkey = -8 Insert Line"
2810 LET REFORM$="",XPOS=0
2820 IF CREC=LINES OR FNS$(DATA$[LINES])<>"" THEN GOSUB 15000
2830 FOR J=LINES TO CREC STEP -1
2832 LET DATA$[J]=DATA$[J-1]
2834 NEXT J
2850 LET DATA$[CREC](1)=" "
2860 FOR J=MAX_ROW TO CLINE STEP -1
2862 PRINT @(0,J),'CURSOR'("OFF"),DATA$[CREC-CLINE+J],
2864 NEXT J
2890 GOTO 1100
3000 REM " ---------------------------- Fkey = -9 Delete Line"
3010 IF CREC=LINES THEN GOTO 3060
3012 LET REFORM$="Y"
3020 FOR J=CREC TO LINES-1
3022 LET DATA$[J](1)=DATA$[J+1]
3024 NEXT J
3040 FOR J=CLINE TO MAX_ROW
3042 PRINT @(0,J),DATA$[CREC-CLINE+J],
3044 NEXT J
3060 LET DATA$[LINES](1)=" ",EXTRA$="",XPOS=0
3090 GOTO 1100
3200 REM " ---------------------------- Fkey = -1 Right Arrow"
3220 IF XPOS>=L_WIDTH-1 AND LEN(EXTRA$)>0 THEN GOSUB 11100
3240 IF XPOS>=L_WIDTH-1 AND CREC=LINES THEN GOSUB 15000
3250 IF XPOS>=L_WIDTH-1 THEN LET XPOS=0; GOTO 2000
3260 LET XPOS=XPOS+1
3390 GOTO 1100
3400 REM " ---------------------------- Fkey = -2 Left Arrow"
3410 IF XPOS=0 AND CREC=1 THEN PRINT 'RB',@(0,0),; GOTO 3590
3420 IF XPOS=0 AND LEN(EXTRA$)>0 THEN GOSUB 11100
3440 IF XPOS=0 THEN LET XPOS=L_WIDTH-1; GOTO 2200
3460 LET XPOS=XPOS-1
3462 IF XPOS=L_WIDTH-1 THEN LET XPOS=XPOS-1
3590 GOTO 1100
3600 REM " ---------------------------- Fkey = -6 Del Key"
3610 LET REFORM$="Y"
3612 IF FNS$(DATA$[CREC])="" THEN GOTO 3000
3620 IF FNS$(DATA$[CREC](XPOS+1)+EXTRA$)<>"" THEN GOTO 3650
3622 IF CREC=LINES THEN GOTO 3690
3630 LET UPPER=CREC; GOSUB 12100; GOTO 3690
3650 LET DATA$[CREC](XPOS+1)=DATA$[CREC](XPOS+2)
3652 IF LEN(EXTRA$)>0 THEN LET DATA$[CREC](L_WIDTH)=EXTRA$(1,1)
3654 IF LEN(EXTRA$)>1 THEN LET EXTRA$=EXTRA$(2) ELSE LET EXTRA$=""
3660 PRINT @(XPOS,CLINE),DATA$[CREC](XPOS+1),
3690 GOTO 1100
3800 REM " ---------------------------- Fkey = -7 Insert Key"
3810 GOSUB 3900
3890 GOTO 1100
3900 REM "
3920 IF INS$="OFF" THEN GOTO 3980
3950 IF FNS$(EXTRA$)<>"" THEN GOSUB 6100 ELSE LET EXTRA$=""
3960 LET INS$="OFF"; PRINT (0,ERR=3990)'GOTO'("0"),@(X_POS+WIDTH,Y_POS),'ER',CHR(192),CHR(195),'GOTO'(NAME$),; GOTO 3990
3970 GOTO 3990
3980 LET INS$="ON"; PRINT (0,ERR=3990)'GOTO'("0"),@(X_POS+WIDTH,Y_POS),'BR',"<>",'ER','GOTO'(NAME$),; GOTO 3990
3990 RETURN 
4000 REM " ---------------------------- TERMINATION"
4010 IF INS$="ON" THEN GOSUB 3900
4020 FOR Z=LINES TO 0 STEP -1
4060 IF FNS$(DATA$[Z])<>"" THEN EXITTO 4100
4080 NEXT Z
4100 LET LINES=Z
4110 IF LINES<0 THEN LET LINES=0
4190 GOTO 9900
4400 REM " ---------------------------- Fkey = -10 Clear to EOL"
4410 LET REFORM$="Y"
4420 LET DATA$[CREC](XPOS+1)="",EXTRA$=""
4430 PRINT @(XPOS,CLINE),DATA$[CREC](XPOS+1),
4490 GOTO 1100
4600 REM " ---------------------------- Fkey = 0 <CR>"
4610 IF CREC=LINES THEN GOSUB 15000
4612 IF INS$="OFF" THEN LET XPOS=0; GOTO 2000
4620 IF LINES-CREC<2 OR FNS$(DATA$[LINES])<>"" THEN GOSUB 15000
4624 LET REFORM$="Y"
4640 IF XPOS<L_WIDTH THEN PRINT @(XPOS,CLINE),'CL', ELSE GOTO 4660
4642 FOR J=LINES TO CREC+2 STEP -1; LET DATA$[J](1)=DATA$[J-1]; NEXT J
4650 LET DATA$[CREC+1](1)=FNL$(DATA$[CREC](XPOS+1)),DATA$[CREC](XPOS+1)=" "
4652 LET CREC=CREC+1,UPPER=CREC,CLINE=CLINE+1,FMAX_ROW$="Y"; GOSUB 11100
4654 LET CREC=CREC-1,CLINE=CLINE-1
4660 IF FNS$(DATA$[CREC+1])="" AND CLINE<MAX_ROW THEN PRINT @(0,CLINE+1),'LI',
4690 LET XPOS=0; GOTO 2000
5000 REM " ---------------------------- Fkey = -12 Tab"
5020 IF XPOS+TAB<L_WIDTH THEN LET XPOS=XPOS+TAB ELSE LET XPOS=L_WIDTH-1
5190 GOTO 1100
5200 REM " ---------------------------- Fkey = -13 Back Tab"
5220 IF XPOS-TAB>=0 THEN LET XPOS=XPOS-TAB ELSE LET XPOS=0
5390 GOTO 1100
5400 REM " ---------------------------- Fkey = -14 Home"
5420 IF XPOS>0 THEN LET XPOS=0 ELSE LET XPOS=L_WIDTH-1
5590 GOTO 1100
5600 REM " ---------------------------- Fkey = -5 Backspace"
5610 IF XPOS=0 AND CREC=1 THEN PRINT 'RB',; GOTO 5790
5612 IF INS$="ON" THEN GOTO 5700
5620 IF XPOS=0 THEN LET XPOS=L_WIDTH-1,DATA$[CREC-1](L_WIDTH,1)=$20$; GOTO 2200
5630 LET DATA$[CREC](XPOS,1)=$20$; PRINT @(XPOS-1,CLINE),DATA$[CREC](XPOS,1),
5640 LET XPOS=XPOS-1; GOTO 5790
5700 REM "                   ---------- Backspace in insert mode"
5710 IF FNS$(DATA$[CREC])="" THEN IF XPOS>0 THEN GOTO 5640
5720 IF XPOS=0 THEN GOTO 5750; REM ELSE IF XPOS=L_WIDTH THEN GOTO 05730
5722 LET DATA$[CREC](XPOS)=DATA$[CREC](XPOS+1); PRINT @(XPOS-1,CLINE),DATA$[CREC](XPOS),; LET XPOS=XPOS-1; IF FNS$(DATA$[CREC])="" THEN GOTO 5790
5724 IF CREC=LINES OR FNS$(DATA$[CREC+1])="" THEN GOTO 5790
5726 LET X=POS(" "<>DATA$[CREC],-1)+2; IF X>L_WIDTH THEN GOTO 5790 ELSE LET Y=L_WIDTH-X+2,Z=POS(" "=DATA$[CREC+1](1,Y),-1); IF Z=0 THEN GOTO 5790 ELSE LET UPPER=CREC; GOSUB 12100; GOTO 5790
5730 REM "                   ---------- Bksp at right margin"
5732 GOTO 5640
5750 REM "                   ---------- Bksp at pos 0 - test start of para"
5760 IF FNS$(DATA$[CREC-1])="" THEN PRINT 'RB',; GOTO 5790
5762 LET DATA$[CREC-1](L_WIDTH)=" "
5770 IF DATA$[CREC](1,1)<>" " AND DATA$[CREC](2,1)=" " AND DATA$[CREC-1](L_WIDTH-1,2)="  " THEN LET DATA$[CREC-1](L_WIDTH)=DATA$[CREC](1,1),DATA$[CREC](1)=DATA$[CREC](3); LET UPPER=CREC; GOSUB 12100
5772 LET XPOS=L_WIDTH-1; GOTO 2200
5790 GOTO 1100
5800 REM " ---------------------------- Normal character entry"
5805 LET X$=FNQ$(X$)
5810 IF INS$="ON" THEN GOTO 16000
5812 IF XPOS=L_WIDTH THEN GOTO 5830
5820 LET DATA$[CREC](XPOS+1,1)=X$,XPOS=XPOS+1; PRINT @(XPOS-1,CLINE),X$,
5822 IF XPOS<>L_WIDTH OR X$<>" " THEN GOTO 5890
5830 IF CREC=LINES THEN GOSUB 15000
5832 IF FNS$(DATA$[CREC+1])="" THEN GOSUB 5900
5834 IF X$=" " THEN LET XPOS=0; GOTO 5880
5840 REM "                   ---------- Word overlap at end of line"
5842 LET X=POS(" "=DATA$[CREC],-1)
5844 IF X=0 THEN LET XPOS=1,DATA$[CREC+1](1,1)=X$; GOTO 5880
5850 LET XPOS=L_WIDTH-X+1
5852 LET DATA$[CREC+1](1,L_WIDTH-X+1)=DATA$[CREC](X+1)+X$,DATA$[CREC](X)=" "
5854 PRINT @(X-1,CLINE),'CL',
5856 IF CLINE<>MAX_ROW THEN PRINT @(0,CLINE+1),DATA$[CREC+1](1,L_WIDTH-X+1),
5880 LET CREC=CREC+1
5882 IF CLINE=MAX_ROW THEN PRINT @(0,0),'LD',@(0,CLINE),DATA$[CREC], ELSE LET CLINE=CLINE+1
5890 GOTO 1100
5900 REM "                   ---------- Insert line during typeover"
5910 IF FNS$(DATA$[LINES])<>"" THEN GOSUB 15000
5930 FOR I=LINES TO CREC+3 STEP -1; LET DATA$[I]=DATA$[I-1]; NEXT I
5940 LET DATA$[CREC+2](1)=" "
5970 IF CLINE<>MAX_ROW THEN PRINT @(0,CLINE+1),'LI',
5990 RETURN 
6000 REM "GET EXISTING WINDOW INFORMATION"
6020 CALL "ADC.WL",NAME$,X
6040 IF LEN(NAME$)>1 THEN LET NAME$=NAME$(1,1)
6090 RETURN 
6100 REM " ---------------------------- Reformat on exit"
6110 REM IF REFORM$<>"Y" THEN GOTO 06190
6112 IF XPOS=L_WIDTH THEN LET XPOS=XPOS-1
6114 IF FNS$(EXTRA$)="" THEN GOTO 6160
6122 IF DATA$[CREC](L_WIDTH,1)=" " OR EXTRA$(1,1)=" " THEN GOTO 6140
6130 REM "                   ---------- Word overlaps into extra"
6132 LET X=POS(" "=DATA$[CREC],-1); IF X=0 THEN GOTO 6140
6134 LET EXTRA$=DATA$[CREC](X+1)+EXTRA$,DATA$[CREC](X)=" "
6140 REM "                   ---------- Pull extra into body"
6142 IF FNS$(DATA$[CREC+1])="" THEN GOTO 6150
6144 IF LEN(FNS$(EXTRA$)+" "+FNS$(DATA$[CREC+1]))<=L_WIDTH THEN LET DATA$[CREC+1](1)=FNS$(EXTRA$)+" "+FNS$(DATA$[CREC+1]); GOTO 6160
6150 IF FNS$(DATA$[LINES])<>"" THEN GOSUB 15000
6152 FOR I=LINES TO CREC+1 STEP -1; LET DATA$[I]=DATA$[I-1]; NEXT I
6154 LET DATA$[CREC+1](1)=FNL$(EXTRA$),FMAX_ROW$="Y"
6160 LET EXTRA$=""; GOSUB 12000
6190 LET REFORM$=" "; RETURN 
7000 REM " ---------------------------- Input Timeout"
7990 GOTO 1100
8000 REM "---- Functions"
8020 DEF FNP$(Q$)=CVS(Q$,2); REM "Strip Trailing Spaces
8030 DEF FNU$(Q$)=CVS(Q$,4); REM "Uppercase
8040 DEF FNL$(Q$)=CVS(Q$,1); REM "Strip Leading Spaces
8050 DEF FNQ$(Q$)=CVS(Q$,16); REM "Remove Unprintable Characters
8060 DEF FNS$(Q$)=CVS(Q$,1+2); REM "Strip All Spaces-Portable Instead Of CVS(Q$,2)
9000 REM "ERRORS"
9010 PRINT 'GOTO'(0),'RB',@(0,22),"ERROR=",ERR," LINE=",TCB(5)," <CR>=RETRY: ",; INPUT WOW$; IF WOW$="E" THEN ESCAPE
9020 RETRY
9900 REM "EXIT"
9910 IF MODE<>2 THEN CALL "ADC.WD",NAME$
9912 PRINT @(X_POS+WIDTH,Y_POS),RC$
9980 CALL "ADC.FK",0
9990 EXIT 
9999 END
11000 REM " ---------------------------- Fkey = 1 Reformat Paragraph"
11010 GOSUB 12000
11090 GOTO 1100
11100 REM " ---------------------------- Partial reformat with EXTRA"
11110 IF REFORM$<>"Y" THEN GOTO 11190
11112 IF FNS$(EXTRA$)="" THEN GOTO 11170
11114 IF DATA$[CREC](L_WIDTH,1)=" " OR EXTRA$(1,1)=" " THEN GOTO 11140
11120 REM "                   ---------- Word overlaps into extra"
11122 LET X=POS(" "=DATA$[CREC],-1); IF X=0 THEN GOTO 11140
11124 LET EXTRA$=DATA$[CREC](X+1)+EXTRA$,DATA$[CREC](X)=" "
11140 REM "                   ---------- Pull extra into body"
11142 IF FNS$(DATA$[CREC+1])="" THEN GOTO 11150
11144 IF LEN(FNS$(EXTRA$)+" "+FNS$(DATA$[CREC+1]))<=L_WIDTH THEN LET DATA$[CREC+1](1)=FNS$(EXTRA$)+" "+FNS$(DATA$[CREC+1]); GOTO 11170
11150 IF FNS$(DATA$[LINES])<>"" THEN GOSUB 15000
11152 FOR I=LINES TO CREC+1 STEP -1; LET DATA$[I]=DATA$[I-1]; NEXT I
11154 LET DATA$[CREC+1](1)=FNL$(EXTRA$)
11160 LET FMAX_ROW$="Y"
11170 LET UPPER=CREC,EXTRA$=""; GOSUB 12100
11190 LET REFORM$=" "; RETURN 
12000 REM " ---------------------------- Reformat paragraph"
12010 IF FNS$(DATA$[CREC]+EXTRA$)="" THEN GOTO 12900
12020 REM "                   ---------- Find Paragraph Boundaries"
12040 FOR J=CREC TO 1 STEP -1
12050 IF J=1 OR FNS$(DATA$[J-1])="" THEN EXITTO 12070
12060 NEXT J
12070 LET UPPER=J
12100 REM " ---------------------------- Entry point for partial reformat"
12110 REM "                   ---------- UPPER must be set prior"
12140 FOR J=CREC TO LINES
12150 IF J=LINES OR FNS$(DATA$[J+1])="" THEN EXITTO 12170
12160 NEXT J
12170 LET LOWER=J
12200 REM "                   ---------- Begin reformat"
12210 LET LOWERO=LOWER
12212 IF UPPER=LOWER THEN GOTO 12500
12214 FOR J=UPPER TO LOWER-1
12220 IF FNS$(DATA$[J])="" THEN LET B=J; GOSUB 12600; IF J=LOWER THEN EXITTO 12299 ELSE GOTO 12220
12230 LET DATA$[J+1](1)=FNL$(DATA$[J+1])
12232 LET X=POS(" "<>DATA$[J],-1)+2; IF X>L_WIDTH THEN GOTO 12290
12234 LET Y=L_WIDTH-X+2
12240 IF FNS$(DATA$[J+1])="" THEN LET B=J+1; GOSUB 12600; IF J=LOWER THEN EXITTO 12299 ELSE GOTO 12240
12250 LET Z=POS(" "=DATA$[J+1](1,Y),-1); IF Z=0 THEN GOTO 12290
12260 LET DATA$[J](X)=DATA$[J+1](1,Z-1),DATA$[J+1](1)=DATA$[J+1](Z+1)
12262 GOTO 12230
12290 IF J=LOWER-1 THEN EXITTO 12299
12292 NEXT J
12500 REM "                   ---------- Redisplay after reformat"
12508 IF CLINE>MAX_ROW THEN GOTO 12590
12510 IF CREC-UPPER>CLINE THEN LET J=0 ELSE LET J=CLINE-(CREC-UPPER)
12512 LET J1=CLINE+LOWER-CREC
12514 IF J1>MAX_ROW OR LOWER<>LOWERO OR FMAX_ROW$="Y" THEN LET J1=MAX_ROW
12520 FOR J=J TO J1
12540 PRINT @(0,J),DATA$[CREC-CLINE+J],
12560 NEXT J
12590 GOTO 12900
12600 REM "                   ---------- Delete line during reformat"
12610 FOR I=B TO LINES-1
12620 LET DATA$[I]=DATA$[I+1]
12630 NEXT I
12640 LET LOWER=LOWER-1,DATA$[LINES](1)=" "
12650 IF LOWER=B THEN LET DATA$[B](1)=FNL$(DATA$[B])
12690 RETURN 
12900 REM "                   ---------- Exit Paragraph Reformat"
12910 LET FMAX_ROW$=""
12990 RETURN 
15000 REM " ---------------------------- Add five blank lines to array"
15010 IF LINES>=99 THEN EXITTO 1100
15012 LET X=5; IF LINES+5>99 THEN LET X=99-LINES
15020 DIM TEMP$[LINES+X]
15040 FOR J=1 TO LINES; LET TEMP$[J]=DATA$[J]; NEXT J
15060 DIM DATA$[LINES+X](L_WIDTH)
15070 FOR J=1 TO LINES; LET DATA$[J](1)=TEMP$[J]; NEXT J
15080 DIM TEMP$[0]
15082 LET LINES=LINES+X
15090 RETURN 
16000 REM " ---------------------------- Entry With Insert Mode On"
16020 IF CREC=LINES THEN GOSUB 15000
16030 IF LEN(EXTRA$)>=L_WIDTH THEN LET UPPER=CREC; GOSUB 11100
16040 LET REFORM$="Y"
16100 IF XPOS=L_WIDTH THEN IF X$=" " THEN LET IPOS=0; GOTO 16300 ELSE GOTO 16200
16110 PRINT 'CURSOR'("OFF"),@(L_WIDTH-1,CLINE)," ",
16112 LET EXTRA$=DATA$[CREC](L_WIDTH,1)+EXTRA$,DATA$[CREC](XPOS+1)=X$+DATA$[CREC](XPOS+1,L_WIDTH-XPOS-1); PRINT @(XPOS,CLINE),FNP$(DATA$[CREC](XPOS+1)),; LET XPOS=XPOS+1
16120 IF XPOS=L_WIDTH AND X$=" " THEN LET IPOS=0; GOTO 16300
16190 LET IPOS=XPOS; GOTO 16900
16200 REM "                   ---------- Word overlap at end of line"
16210 LET X=POS(" "=DATA$[CREC],-1); IF X=0 THEN LET IPOS=0; GOTO 16300
16212 LET IPOS=L_WIDTH-X+1
16220 LET EXTRA$=DATA$[CREC](X+1)+X$+EXTRA$,DATA$[CREC](X)=" "
16230 PRINT @(X-1,CLINE),'CL',
16300 REM "                   ---------- Pull extra into body"
16310 LET CREC=CREC+1
16312 IF FNS$(EXTRA$)="" THEN LET EXTRA$=""; IF CLINE=MAX_ROW THEN GOTO 16370 ELSE LET CLINE=CLINE+1; GOTO 16900
16320 IF FNS$(DATA$[LINES])<>"" THEN GOSUB 15000
16330 FOR I=LINES TO CREC STEP -1; LET DATA$[I]=DATA$[I-1]; NEXT I
16340 LET DATA$[CREC](1)=EXTRA$,EXTRA$=""
16370 IF CLINE=MAX_ROW THEN PRINT @(0,0),'LD', ELSE LET CLINE=CLINE+1
16380 FOR I=0 TO MAX_ROW-CLINE
16382 PRINT @(0,CLINE+I),DATA$[CREC+I],
16384 NEXT I
16900 LET XPOS=IPOS
16990 GOTO 1100

0010 REM "ADC - MAS ADministrator User/Terminal Update"
0020 REM "Program: ADC.06"
0030 REM "+----------------------------------------------------------+"
0031 REM "|            ADminstrator System Version 1.0.2a            |
0032 REM "|     Copyright (C) 1994 MicroAccounting Systems, Inc.     |
0033 REM "|                 All Rights Reserved"                     |
0034 REM "+----------------------------------------------------------+
0046 REM "KJS|10/16/96|6.0 Version"
0047 REM "GGB|07/27/94|90 Change Seterr 9000 ADD FKEY Load if error"
0048 REM "MCT|03/29/94|220 U2$ Twice & 2430-2470 SYS Menu/Re-write ADS-01U
0049 REM "PHC|03/17/94|1140 DIM F2$(10)->F2$(20) Allow 10 Printers"
0080 SETERR 9000
0090 ENTER LOGIN$
0100 REM " --- Open files"
0110 LET ADS01_DEV=UNT; OPEN (ADS01_DEV,ERR=9900)"ADS-01"
0120 LET SYS01_DEV=UNT; OPEN (SYS01_DEV,ERR=9900)"SYS-01"
0130 LET SYM02_DEV=UNT; OPEN (SYM02_DEV,ERR=9900)"SYM-02"
0140 LET SYM22_DEV=UNT; OPEN (SYM22_DEV,ERR=9900)"SYM-22"
0200 REM " --- IOLIST's"
0210 ADS01U: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A[0],A[1],A[2],A[3],A[4]
0220 SYS01T: IOLIST T$,F0$(1),F1$(1),F2$(1),F3$(1),F4$(1),F5$(1)
0230 SYM02: IOLIST U0$(1),U1$(1),U[ALL]
0400 REM " --- SETOPTS"
0420 LET A$=OPTS
0430 LET A$=IOR(A$(1,3),$088840$)
0440 REM SETOPTS A$
0500 REM " --- Init data
0510 LET DATE$=DAY,DATE$=DATE$(1,2)+DATE$(4,2)+DATE$(7,2),DATE$=FND$(DATE$)
0520 DIM U0$(3),U1$(128),U[4]
1000 REM " --- Get O/S and Login name"
1010 REM CALL "ADC.04",ERR=1011,OS$
1020 CALL "ADC.03",LOGNAME$,LNAME$,ERRFLAG
1060 IF FNP$(LOGNAME$)="" THEN GOTO 2000
1080 DIM A0$(15),A1$(40),A2$(20),A3$(30),A4$(30),A[4]
1090 LET A0$(1)="U"+LOGNAME$
1120 FIND (ADS01_DEV,KEY=A0$,DOM=2200)IOL=ADS01U
1140 DIM F0$(25),F1$(30),F2$(8),F3$(1),F4$(40),F5$(35)
1150 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=2500)IOL=SYS01T
1200 REM " --- Set Terminal Record And User Defaults"
1220 LET F0$(13,2)=A1$(9,2),A1$(16,2)=A1$(9,2),F0$(25,1)=A1$(14,1),A1$(21,1)=A1$(14,1); REM "Default/Selected Printer"
1240 LET F0$(4,3)=A1$(1,3); REM "---- ADD+ON User ID"
1260 LET F0$(7,3)=DATE$,Y$=FNC$(DATE$),F0$(18,6)=Y$(5,2)+Y$(1,4); REM "---- Set Today"
1280 LET F0$(16,2)=A1$(36,2); REM "---- Set Default Company"
1320 DIM U0$(3),U1$(128),U[4]
1340 FIND (SYM02_DEV,KEY=A1$(1,3),DOM=2300)IOL=SYM02
1360 LET F0$(15,1)=U1$(25,1); REM "---- Security ID Level"
1420 LET F2$(1,6)=A1$(33,3); REM "---- Set Default Menu, 6.0
1440 LET T$="T"+FID(0)
1490 WRITE (SYS01_DEV,KEY="T"+FID(0))IOL=SYS01T
1500 REM " --- Set valid printers by operator"
1501 REM "Remove existing records"
1505 READ (SYM22_DEV,KEY=A1$(1,3),DOM=1506)
1510 LET KK$=KEY(SYM22_DEV,END=1550)
1515 IF KK$(1,3)<>A1$(1,3) THEN GOTO 1550
1520 REMOVE (SYM22_DEV,KEY=KK$)
1530 GOTO 1510
1550 REM "Add ADS defaults"
1560 FOR II=1 TO LEN(A2$) STEP 2
1570 IF A2$(II,2)<>"  " THEN WRITE (SYM22_DEV,KEY=A1$(1,3)+A2$(II,2))
1580 NEXT II
1600 REM "---- Set Global !SCREEN Variable (disabled)
1610 GOTO 4000; REM "Let SYS.ZA do this
1620 DIM SCREEN$(32)
1640 LET SCREEN$(9,1)="Y"; REM "Help Enabled"
1650 LET SCREEN$(10,15)=U2$(10,15); REM "---- From ADD+ON User Record"
1660 LET X$=STBL("!SCREEN",SCREEN$)
1700 REM "---- Set Global !COLOR Variable (disabled)"
1720 DIM COLOR$(160); LET COLOR$(1)=U3$
1730 FOR J=1 TO 51 STEP 10
1740 CALL "ADC.CM",COLOR$(J,10),X$
1750 LET COLOR$(J,10)=X$
1760 NEXT J
1770 LET X$=STBL("!COLOR",COLOR$)
1990 GOTO 4000
2000 REM "---- Cannot Determine LOGNAME Error Exit"
2020 LET HEIGHT=3,WIDTH=60,XPOS=10,YPOS=18,TITLE$="Logname Error",NAME$=""
2060 CALL "ADC.WA",0,WIDTH,HEIGHT,XPOS,YPOS,TITLE$,NAME$
2070 CALL "ADC.WB",NAME$,0
2080 LET MSG$="Environment Variable LOGNAME Not Set! <CR>=Continue:"; PRINT @(0,0),MSG$,
2120 LET V0$="S",V1$="C",V2$="",V3$="",V4$="",V0=1,V1=LEN(MSG$)+1,V2=0; GOSUB 7000
2140 CALL "ADC.WD",NAME$
2190 GOTO 9900
2200 REM "---- Missing User ID Record"
2220 LET A0$(1)="U"+LOGNAME$,A1$(1,3)="NEW",A1$(9,2)="PF",A1$(16,2)="PF",A2$(1)="PF",A1$(28,5)=FNP$(FNQ$(FNU$(LOGNAME$))),A1$(33,3)="SYS",A1$(36,2)="01",A1$(8,1)="Y"
2240 WRITE (ADS01_DEV,KEY=A0$)IOL=ADS01U
2300 REM "---- Resolve ADD+ON User ID"
2320 DIM U0$(3),U1$(128),U[4]
2330 FIND (SYM02_DEV,KEY=A1$(1,3),DOM=2400)IOL=SYM02
2340 LET U1$(101,3)=A1$(28,3),U1$(104,6)=A1$(33,3)
2360 WRITE (SYM02_DEV,KEY=A1$(1,3))IOL=SYM02
2390 GOTO 2490
2400 REM "---- No User ID"
2420 FIND (SYM02_DEV,KEY="+ON",DOM=9900)IOL=SYM02
2430 LET U0$(1,3)="NEW",U1$(104,6)="SYS",U1$(25,1)="1",U1$(1,24)="New User",U1$(101,3)=A1$(28,3),U1$(27,1)="N",U1$(28,6)=""
2450 WRITE (SYM02_DEV,KEY=U0$(1,3))IOL=SYM02
2460 LET A1$(1,3)="NEW",A1$(33,3)="NEW"
2470 WRITE (ADS01_DEV,KEY=A0$)IOL=ADS01U
2490 GOTO 1000
2500 REM "---- No Terminal Record"
2540 LET F0$(13,2)=A1$(9,2),F0$(25,1)=A1$(14,1); REM "---- Default Printer
2550 LET F0$(4,3)=A1$(1,3),F0$(7,3)=DATE$,F0$(16,2)=A1$(36,2),F0$(15,1)=U0$(29,1),Y$=FNC$(DATE$),F0$(18,6)=Y$(5,2)+Y$(1,4),T$="T"+FID(0)
2570 WRITE (SYS01_DEV,KEY="T"+FID(0))IOL=SYS01T
2590 GOTO 1000
4000 REM " --- Done"
4120 LET LOGIN$=A1$(8,1)
4990 GOTO 9900
7000 REM " --- User Input"
7020 CALL "ADC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7040 IF V3=5 THEN GOTO 7100
7050 IF V3=6 THEN GOTO 7200
7085 LET I$=V$
7090 RETURN 
7100 REM " --- Error During Input"
7200 REM " --- Escape Requested"
7210 PRINT @(0,22),'CL','CURSOR'("ON"),
7220 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR((ASC(Q$)-32)*POS(" "<>Q2$(2,1)):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(NUM(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8120 DEF FNP$(Q$)=CVS(Q$,2)
8140 DEF FNQ$(Q$)=CVS(Q$,8)
8150 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM "---- Errors"
9020 CALL "ADC.FK",0
9900 REM "---- Exit"
9905 DROP "ADC.IA",ERR=9906
9910 ADDR "ADC.IA",ERR=9911
9920 CLOSE (ADS01_DEV,ERR=9921)
9930 CLOSE (SYS01_DEV,ERR=9931)
9940 CLOSE (SYM02_DEV,ERR=9941)
9950 CLOSE (SYM22_DEV,ERR=9951)
9990 EXIT 
9999 END

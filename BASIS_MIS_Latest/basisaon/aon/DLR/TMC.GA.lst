0010 REM "TMC - Called Routine to Change Contact Sort Key
0020 REM "Program: TMC.GA
0030 REM "ADD+ON SOFTWARE VERSION 5
0035 REM "02/12/92 by Jerry Koch
0040 REM "COPYRIGHT(C)1992 MicroAccounting Systems, Inc. ALL RIGHTS RESERVED
0050 ENTER A0$,SWAP$,CHAN[ALL]
0060 REM "A0$=Main Contact Record, SWAP$=Swap Firm Name Parameter {P0$(16,1)}
0090 SETERR 9000
0095 SETESC 9900
0500 REM "--- Init Data
0510 IF SWAP$="Y" THEN LET FIELD$="Firm" ELSE LET FIELD$="Name"
0520 DIM J1$(40," ")
0600 REM "--- Make Window
0610 LET WIDTH=70,HEIGHT=3,WIN_X=5,WIN_Y=7
0620 LET TITLE$=FIELD$+" Name Change"
0640 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0650 LET X$=STBL("WINDOW_NAME",NAME$)
1000 REM "--- Change Search Key too?
1010 PRINT @(7),"Do You Want To Change The Look-Up ",FIELD$," Also? <Y/N>:",
1020 LET V0$="Y",V1$="CR",V2$="N",V3$="YN",V4$="",V0=1,V1=59,V2=1
1040 GOSUB 7000
1050 IF V$="N" THEN GOTO 9900
1060 PRINT @(0,1),'CL',@(8,1),"*** Changing The Primary Sort Field:  Contact ",FIELD$," ***",
1080 IF SWAP$="Y" THEN LET NEW$=CVS(A0$(112,20),4) ELSE LET NEW$=CVS(A0$(52,20),4)
1090 CALL "SYC.WB","0",1
1091 REM WINDOW SELECT (NOUPDATE,"0")
1100 REM "--- Change Sort Field
1120 LET V0$="S",V1$="CRE",V2$=NEW$,V3$="",V4$="Enter The New Look-Up "+FIELD$+" For This Contact, <F4>=Keep Original "+FIELD$,V0=20,V1=6,V2=3
1140 GOSUB 7000
1150 IF V$="END" OR V3=4 THEN GOTO 9900
1170 LET NEW$=V$
1175 LET NEW$=NEW$+J1$; LET NEW$=NEW$(1,20)
1180 FIND (CHAN[1],KEY=A0$(1,2)+NEW$,DOM=1500)
1200 REM "--- Customer Already Exists
1210 GOSUB 6000
1220 PRINT @(1,1),"Contact Is Already On File! <Cr> To Change Name, <F4> To End."
1240 LET V0$="S",V1$="C",V2$="",V3$="",V4$="",V0=1,V1=70,V2=1
1250 GOSUB 7000
1260 IF V3=4 THEN GOTO 9900
1270 GOTO 1060
1500 REM "--- Change Comment Records
1510 GOSUB 6000
1520 READ (CHAN[2],KEY=A0$(1,22),DOM=1521)
1530 PRINT @(0,1),'CL',@(1),"Moving Comments To New Name...",
1540 LET K$=KEY(CHAN[2],END=1600)
1550 IF K$(1,22)<>A0$(1,22) THEN GOTO 1600
1560 READ (CHAN[2],KEY=K$)B0$,B1$
1570 LET B0$(3,20)=NEW$
1580 WRITE (CHAN[2],KEY=B0$(1,33))B0$,B1$
1585 REMOVE (CHAN[2],KEY=K$,DOM=1586)
1590 PRINT @(32,1),B0$,
1595 GOTO 1540
1600 REM "--- Change Additional Contacts
1610 GOSUB 6000
1620 READ (CHAN[12],KEY=A0$(1,22),DOM=1621)
1630 PRINT @(0,1),'CL',@(1),"Moving Contacts To New Name...",
1640 LET K$=KEY(CHAN[12],END=1700)
1650 IF K$(1,22)<>A0$(1,22) THEN GOTO 1700
1660 READ (CHAN[12],KEY=K$)B0$
1670 LET B0$(3,20)=NEW$
1680 WRITE (CHAN[12],KEY=B0$(1,33))B0$
1685 REMOVE (CHAN[12],KEY=K$,DOM=1686)
1690 PRINT @(32,1),B0$,
1695 GOTO 1640
1700 REM "--- Change Job History
1705 LET TMM04=UNT; OPEN (TMM04)"TMM-04"
1710 GOSUB 6000
1720 READ (TMM04,KEY=A0$(1,22),DOM=1721)
1730 PRINT @(0,1),'CL',@(1),"Moving Job History To New Name...",
1740 LET K$=KEY(TMM04,END=1798)
1750 IF K$(1,22)<>A0$(1,22) THEN GOTO 1798
1760 READ (TMM04,KEY=K$)B0$,B1$,B0,B1,B2
1770 LET B0$(3,20)=NEW$
1780 WRITE (TMM04,KEY=B0$(1,33))B0$,B1$,B0,B1,B2
1785 REMOVE (TMM04,KEY=K$,DOM=1786)
1790 PRINT @(32,1),B0$,
1795 GOTO 1740
1798 CLOSE (TMM04)
1800 REM "--- Now Get Rid of Old Contact Name
1810 PRINT @(0,21),'CL'
1820 REMOVE (CHAN[1],KEY=A0$(1,22),DOM=1821)
1900 REM "--- Assign the New Name to the Contact Sort
1920 LET A0$(3,20)=NEW$
2990 GOTO 9900
6000 REM "--- Go back to Little Window
6010 LET NAME$=STBL("WINDOW_NAME",ERR=9100)
6020 CALL "SYC.WB",NAME$,0
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR((ASC(Q$)-32)*POS(" "<>Q2$(2,1)):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(NUM(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8300 REM "FINANCE CHARGE"
8310 IF P9$="Y" THEN GOTO 3900
8320 LET V0$="Y",V1$="C",V2$=C1$(10,1),V3$="",V1=59,V2=10,V4$="If Calculated Should A Finance Charge Be Applied <Y/N>"
8340 GOSUB 7000
8350 LET C1$(10,1)=V$
8390 GOTO 3900
8400 REM "SALES ANALYSIS"
8410 IF P9$="Y" THEN GOTO 3900
8420 LET V0$="S",V1$="C",V2$=C1$(11,1),V3$="",V0=1,V1=59,V2=11,V4$="P=Product Summary I=Item Detail, N=None"
8440 GOSUB 7000
8450 IF POS(V$="PIN")=0 THEN GOTO 8400
8460 LET C1$(11,1)=V$
8490 GOTO 3900
8500 REM "STATEMENTS"
8510 IF P9$="Y" THEN GOTO 3900
8520 LET V0$="Y",V1$="C",V2$=C1$(20,1),V3$="",V1=59,V2=12,V4$="If Printed - Statements For This Customer?"
8540 GOSUB 7000
8550 LET C1$(20,1)=V$
8590 GOTO 3900
8600 REM "LABEL CODE"
8610 IF P9$="Y" THEN GOTO 3900
8620 LET V0$="S",V1$="C",V2$=C1$(28,2),V3$="",V0=2,V1=59,V2=14,V4$="Enter Label Code For This Customer"
8640 GOSUB 7000
8650 LET C1$(28,2)=V$
8690 GOTO 3900
8700 REM "CREDIT LIMIT"
8710 IF P9$="Y" THEN GOTO 3900
8720 LET V0$="N",V1$="C",V2$=STR(C[6]),V3$=M1$,V0=7,V1=59,V2=16,V4$="Enter Credit Limit or Zero for None"
8740 GOSUB 7000
8750 LET C[6]=V
8790 GOTO 3900
8800 REM "SHOW A0$
8810 LET Z5$="----+----1----+----2----+----3----+----4----+----5----+----6----+----7----+----8"
8820 LET Z5=INT(LEN(A0$))
8830 FOR Z=0 TO Z5
8840 PRINT Z5$,'LF',A0$(Z*80+1,80)
8850 INPUT *,
8860 NEXT Z
8890 ESCAPE
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM "END
9950 REM LET NAME$=CGV("WINDOW_NAME",ERR=09100)
9960 LET X$=STBL("!CLEAR","WINDOW_NAME",ERR=9961)
9970 CALL "SYC.WB",NAME$,0
9980 CALL "SYC.WD",NAME$
9998 EXIT 
9999 END

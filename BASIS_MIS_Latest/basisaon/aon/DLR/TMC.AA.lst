0010 REM "TMC - Called Routine: Contact Display and Select
0020 REM "Program: TMC.AA
0030 REM "ADD+ON SOFTWARE VERSION 5.1
0035 REM "12/05/91 by Jerry Koch
0040 REM "COPYRIGHT (C) 1991 MicroAccounting Systems, Inc. ALL RIGHTS RESERVED"
0041 REM 
0042 REM "06/07/96 KJS WA: #211 <F9> is now quick exit"
0079 REM 
0080 SETERR 9000; SETESC 9900
0090 ENTER SELECTION$,V3
0100 REM "OPEN FILES
0110 LET TMM01_DEV=UNT
0120 OPEN (TMM01_DEV,ERR=9900)"TMM-01"
0130 LET SYS01_DEV=UNT; OPEN (SYS01_DEV,ERR=9900)"SYS-01"
0250 IOLIST P0$,P1$,P2$
0260 IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$
0270 IOLIST X$,F0$,X$,X$,F4$,F5$
0400 REM "PARAMETERS
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=0270
0420 LET N0$=F0$(16,2),M0$="000000"
0445 FIND (SYS01_DEV,KEY=N0$+"TM"+"00",DOM=9800)IOL=0250
0450 DIM P[2]
0455 LET P[0]=6
0460 FIND (SYS01_DEV,KEY=N0$+"AR"+"00",DOM=0491)IOL=0260
0490 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(3,2)),P[2]=NUM(I3$(4,1))
0495 CLOSE (SYS01_DEV)
0500 REM --INIT DATA--
0510 LET PAGES=INT((DSZ-5000)/6000); REM "MAXIMUM NUMBER OF PAGES
0515 IF PAGES<3 THEN GOTO 9900; REM "INSUFFICIENT MEMORY AVAILABLE
0520 LET PAGE=1
0560 LET L=1,X1=1,X0=0
0570 LET M7$="(###)-###-####"
0580 DIM J0$(290)
0600 REM "SET PARAMETERS
0610 LET WIDTH=80,HEIGHT=13,WIN_X=0,WIN_Y=3
0650 LET MAX_ROW=HEIGHT-6
0652 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,HEIGHT]
0655 LET TITLE$="Contact Lookup"
0700 REM --BACKGROUND--
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0730 DIM HEADING$(WIDTH-2); LET HEADING$(2)="Contact Name",HEADING$(23)="Company Name",HEADING$(39)="Cust#   Zip",HEADING$(53)="Telephone",HEADING$(68)="MKT ORD CSR"; DIM FOOTING$(WIDTH-2); LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0740 IF P0$(16,1)="Y" THEN LET HEADING$(2,12)="Company Name",HEADING$(23,12)="Contact Name"
0750 PRINT @(0,0),'SB','BR',HEADING$,'ER',@(0,HEIGHT-3),'BR',FOOTING$,'ER','SF'
0800 REM "GET SEARCH METHOD
0805 CALL "SYC.WD",ONAME$
0810 GOSUB 5500
0815 LET V0$="S",V1$="KCE",V2$="",V3$="",V4$="",V1=2,V2=22
0818 IF OPTION$="" THEN GOTO 0870
0820 LET SRT$=OPTION$
0830 ON POS(OPTION$="01234* ") GOTO 0880,0840,0850,0860,0860,0860,0870,0870
0840 LET V0=20; GOTO 0880
0850 LET V0=15; GOTO 0880
0860 LET V0=6,V0$="Z"; GOTO 0880
0870 LET V0=10,SRT$="0"; GOTO 0880
0880 GOSUB 7000; IF V0=13 THEN LET V0=10
0883 IF V3=9 THEN GOTO 9900
0884 IF V3=4 THEN CALL "SYC.WB",ONAME$,0; GOSUB 5600; GOTO 0815
0885 PRINT @(0,2),'CL',
0899 CALL "SYC.WD",ONAME$
0900 REM "DETAIL WINDOW
0910 LET SWIDTH=80,SHEIGHT=7,SWIN_X=0,SWIN_Y=WIN_Y+HEIGHT
0920 LET STITLE$="Contact Detail"
0930 CALL "SYC.WA",0,SWIDTH,SHEIGHT,SWIN_X,SWIN_Y,STITLE$,SNAME$
0984 LET START$=V$
0986 IF OPTION$="*" THEN LET PREFIX$="*" ELSE LET PREFIX$=""
0990 CALL "SYC.WB",NAME$,0
0995 REM LET FOOTING$(LEN(FOOTING$)-LEN(METHOD$)-2)=METHOD$; PRINT @(0,22),'BR',FOOTING$,'ER',
1000 REM "INIT FILE
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,SHEIGHT-2]
1025 IF PREFIX$="*" THEN READ (TMM01_DEV,KEY=N0$,KNUM=NUM(SRT$),DOM=1400); GOTO 1400
1030 READ (TMM01_DEV,KEY=N0$+START$,KNUM=NUM(SRT$),DOM=1400)X$
1040 LET K$=X$
1050 GOTO 1412
1400 REM --NEXT RECORD--
1410 LET K$=KEY(TMM01_DEV,KNUM=NUM(SRT$),END=3000); READ (TMM01_DEV,ERR=8900)X$
1412 IF K$(1,2)<>N0$ THEN GOTO 3000
1413 IF PREFIX$="*" THEN IF POS(START$=X$)=0 THEN GOTO 1410
1414 GOTO 1470
1470 IF PREFIX$="" THEN LET X2$=K$(3,6) ELSE IF PREFIX$="*" THEN LET X2$=START$ ELSE LET X2$=K$(4,V0)
2000 REM "BUILD PAGES
2005 LET MODE=1
2010 IF L>MAX_ROW-1 THEN GOSUB 6200 ELSE GOTO 2040
2030 IF SELECTION$="END" THEN GOTO 4200 ELSE IF SELECTION$<>"" THEN GOTO 4000
2040 DIM G1$(WIDTH-2); LET G1$(2)=X$(3,20)+" "+X$(23,15)+" "+X$(253,P[0]),G1$(47)=X$(229,5)+" "+X$(38,14)+" "+X$(285,3)+" "+X$(296,3)+" "+X$(307,3)
2045 LET X1$=X1$+J0$(1,283-LEN(X1$))
2050 DIM G2$(SWIDTH*SHEIGHT); LET G2$(2)=X$(52,30)+"          Init Ent: "+FNB$(X$(331,8))+'LF'+" "+X$(82,30)+"          Last Ent: "+FNB$(X$(339,8))+'LF'+" "+X$(112,30)+"         Next Call: "+FNB$(X$(288,8))+'LF'+" "+X$(142,30)+"      1st Inv Date: "+FNB$(X$(269,8))+'LF'+" "+CVS(X$(202,25),2)+", "+X$(227,2)+" "+X$(229,5)+J0$(1,25-LEN(CVS(X$(202,25),2)))+"Last Inv Date: "+FNB$(X$(277,8))
2070 LET LLIST$[PAGE,X1,1]=G1$,MORE$[PAGE,X1,0]="5",MORE$[PAGE,X1,4]=G2$,LLIST$[PAGE,X1,5]=SNAME$
2071 REM LET LLIST$[PAGE,X1,1]=G1$, LLIST$[PAGE,X1,4]=G2$, LLIST$[PAGE,X1,5]=SNAME$
2075 LET LLIST$[PAGE,X1,2]=X$(3,20)
2080 LET X0=X0+1,X1=X1+1,L=L+1
2090 GOTO 1400
3000 REM --EOF--
3010 IF X0=0 THEN GOSUB 6500; PRINT @(1,4),"No Contacts Found. <",'BB',"Return",'EB',"> To Continue ",; LET V0$="S",V1$="S",V2$="",V3$="",V0=1,V1=43,V2=4; GOSUB 7000; PRINT @(1,4),"                                          ",; IF V3=4 THEN GOTO 4200 ELSE IF V3=9 THEN GOTO 9900 ELSE GOTO 0800
3020 LET MODE=1
3040 LET SELECTION$=SELECTION$+"|EOF"; GOSUB 6200
3090 IF SELECTION$="END" THEN GOTO 4200 ELSE IF SELECTION$<>"" THEN GOTO 4000 ELSE GOTO 4200
4000 REM --EXIT/NORMAL--
4090 GOTO 9900
4200 REM --EXIT/ABORT SEARCH--
4220 LET V$=""
4290 GOTO 9900
4400 REM --EXIT/ERROR--
4420 LET V$=""
4490 GOTO 9900
5500 REM "SEARCH METHOD
5510 DIM OLIST$[1,8,3],MORE$[2,MAX_ROW,1]
5515 LET OWIDTH=26,OHEIGHT=10,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3,OTITLE$="Search Method"
5520 CALL "SYC.WA",0,OWIDTH,OHEIGHT,OWIN_X,OWIN_Y,OTITLE$,ONAME$
5525 CALL "SYC.WC",1,0,OWIDTH-2,0,0,OHEIGHT-4,1
5526 REM WINDOW SHAPE (BOX,OWIDTH-2,1,0,OHEIGHT-4) "BORDER=LG"
5530 LET OLIST$[1,1,1]="  Contact Name      ",OLIST$[1,1,2]="0"
5535 LET OLIST$[1,2,1]="  Company Name      ",OLIST$[1,2,2]="1"
5540 IF P0$(16,1)="Y" THEN LET OLIST$[1,1,1]=OLIST$[1,2,1],OLIST$[1,2,1]="  Contact Name      "
5550 FOR J=1 TO 3
5552 IF POS(" "<>P2$(J*20-19,20))=0 THEN LET OLIST$[1,J+2,1]="  Keyword             ",OLIST$[1,J+2,2]="*"; EXITTO 5590
5554 LET OLIST$[1,J+2,1]="  "+P2$(J*20-19,20),OLIST$[1,J+2,2]=STR(J+1:"#")
5556 NEXT J
5558 GOTO 5580
5560 REM LET OLIST$[1,4,1]="  "+P2$(21,20), OLIST$[1,4,2]="3"
5570 REM LET OLIST$[1,5,1]="  "+P2$(41,20), OLIST$[1,5,2]="4"
5580 LET OLIST$[1,6,1]="  Keyword             ",OLIST$[1,6,2]="*"
5590 LET OMAX_ROW=OHEIGHT-2,PAGE=1
5600 REM --CONTINUE--
5620 CALL "SYC.SA",0,OLIST$[ALL],MORE$[ALL],OPTION$,PAGE,OMAX_ROW,ONAME$,OHEIGHT-4,OWIDTH-2,FKEY
5640 IF OPTION$="END" OR FKEY=4 THEN EXITTO 9900
5642 IF OPTION$="PRIOR" THEN EXITTO 9900
5645 FOR J=1 TO 6; IF OLIST$[1,J,2]=OPTION$ THEN LET METHOD$=FNP$(OLIST$[1,J,1](2)) FI; NEXT J
5699 RETURN 
6200 REM --CONTINUE--
6220 CALL "SYC.SA",MODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6225 IF FKEY<>1 THEN GOTO 6230
6227 LET PAGE=1,L=1,X1=1,X0=0; DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,HEIGTH]
6229 EXITTO 0800
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6235 IF PAGE>PAGES-1 THEN LET PAGE=PAGES-1; GOSUB 6300
6240 IF SELECTION$="END" OR FKEY=4 THEN EXITTO 9900
6289 LET I0$="",L=1,X1=1
6290 RETURN 
6300 REM "ROLL LLIST$ BACK ONE PAGE
6310 FOR I1=1 TO PAGES-1
6320 FOR I2=1 TO MAX_ROW
6330 FOR I3=1 TO 5
6340 LET LLIST$[I1,I2,I3]=LLIST$[I1+1,I2,I3]
6350 NEXT I3
6360 NEXT I2
6370 NEXT I1
6380 FOR I2=1 TO MAX_ROW
6390 FOR I3=1 TO 5
6400 LET LLIST$[PAGES,I2,I3]=""
6410 NEXT I3
6420 NEXT I2
6490 RETURN 
6500 REM "CLEAR WINDOW
6550 FOR J=2 TO MAX_ROW; PRINT @(0,J),'CL',; NEXT J
6599 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS
8010 DEF FNP$(Q$)=Q$(1,POS("  "=Q$+"  ")-1)
8020 DEF FNB$(Q1$)=Q1$(5,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(7,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(3,2)
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8900 REM "HANDLE BUSY RECORD
8910 LET ERR=TCB(5); IF ERR<>0 THEN GOTO 9000
8920 DIM X1$(256); LET X$=K$,X1$(1,24)="RECORD LOCKED!",X2$="*********"
8930 READ (TMM01_DEV,KEY=K$(1,LEN(K$)-1)+$FF$,DOM=8931)
8940 GOTO 2000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM "END
9910 CLOSE (TMM01_DEV,ERR=9911)
9920 CALL "SYC.WD",ONAME$
9930 CALL "SYC.WD",NAME$
9940 CALL "SYC.WD",SNAME$
9996 IF ALT$="Y" THEN LET SELECTION$=""; REM "SPECIAL ALTERNATE FILE
9997 IF SELECTION$="END" THEN LET SELECTION$=""
9998 EXIT 
9999 END

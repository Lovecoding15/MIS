0010 REM "TMC - Called Routine: Contact Maintenance
0020 REM "Program TMC.HA
0030 REM "ADD+ON SOFTWARE VERSION 5
0035 REM "12/04/91 by Jerry Koch
0039 REM "MAS * DCF * 5/14/93 * Richmark"
0040 REM "Copyright(C)1991 MicroAccounting Systems, Inc. ALL RIGHTS RESERVED
0041 REM 
0042 REM "06/05/96 KJS WA: #211 <F10> is now a hotkey, change to <F9>"
0043 REM "06/07/95 KJS WA: #211 <F9> is noe quick exit"
0079 REM 
0080 SETERR 9000
0090 ENTER CONTACT$,V3
0100 REM "OPEN FILES
0110 LET TMM03=UNT; OPEN (TMM03)"TMM-03"
0130 LET SYS01_DEV=UNT; OPEN (SYS01_DEV,ERR=9900)"SYS-01"
0140 LET X$=STBL("SYS01_DEV",STR(SYS01_DEV))
0220 IOLIST B0$
0270 IOLIST X$,F0$,X$,X$,F4$,F5$
0400 REM "PARAMETERS
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=0270
0420 LET N0$=F0$(16,2)
0430 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)*,*,*,P3$
0495 CLOSE (SYS01_DEV); LET X$=STBL("!CLEAR","SYS01_DEV",ERR=0496)
0500 REM --INIT DATA--
0520 LET PAGE=1
0530 DIM J0$(10,"0"),B1$(80),DESC$[100](60)
0540 LET M8$="(###)-###-####"
0560 LET L0=1,L9=12,X1=1,X0=0
0600 REM "SET PARAMETERS
0610 LET WIDTH=80,HEIGHT=17,WIN_X=0,WIN_Y=6
0655 LET TITLE$="Additional Contacts",NAME$=TITLE$(1,8),X$=STBL("WINDOW_NAME",NAME$)
0680 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2); LET HEADING$(1)="   ",HEADING$(5)="Name",HEADING$(27)="Position",HEADING$(49)="Telephone",HEADING$(64)="Ext.",HEADING$(69)="Best Time",FOOTING$(1)=""
0700 REM --BACKGROUND--
0720 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0790 PRINT @(0,0),'SB','BR',HEADING$,'ER','SF',
1000 REM "--- Init Read
1020 READ (TMM03,KEY=CONTACT$,DOM=1021)
1030 PRINT 'CF',
1080 LET L=L0,DT_BRK$=""
1100 REM "--- Main Read
1120 LET K$=KEY(TMM03,END=4000)
1130 IF POS(CONTACT$=K$)<>1 THEN GOTO 4000
1140 READ (TMM03,KEY=K$)IOL=0220
1190 LET MORE$=""
1200 REM "--- Page Break?
1220 IF L>L9 THEN LET MORE$="Y"; GOTO 4000
1500 REM "--- Display
1520 PRINT @(0,L),B0$(23,3),@(4),B0$(26,20),@(26),B0$(46,20),@(48),FNF$(B0$(66,10),M8$),@(63),B0$(76,4),@(68),B0$(80,6)
1580 LET L=L+1
1990 GOTO 1100
2000 REM "--- Edit Contacts
2005 IF L>L9 THEN PRINT @(0,L0),'LD'; LET L=L-1
2010 LET K$=KEY(TMM03,END=2100)
2020 IF POS(CONTACT$=K$)<>1 THEN GOTO 2100
2030 READ (TMM03)IOL=0220
2040 GOTO 2190
2100 REM SEQ
2105 IF B0$="" THEN DIM B0$(110); LET B0$(1,25)=CONTACT$+"000"
2110 LET B0=NUM(B0$(23,3))+10
2112 PRINT @(0,L),B0:"000"
2120 LET V0$="Z",V1$="CER",V2$=STR(B0),V3$="",V4$="Enter the sequence, <F4>=End, <F9>=Quit",V0=3,V1=0,V2=L,I0=0
2130 GOSUB 7000
2132 IF V<0 THEN GOTO 2120
2135 IF V3=2 THEN GOTO 1420
2140 IF V3=4 OR V$="END" THEN GOTO 3000
2145 IF V3=9 THEN GOTO 9900
2150 IF V$="000" THEN GOTO 2100
2160 DIM B0$(110); LET B0$(1,22)=CONTACT$; LET B0$(23,3)=V$
2180 READ (TMM03,KEY=B0$(1,25),DOM=2200)B0$
2190 PRINT @(0,L),B0$(23,3),@(4),B0$(26,20),@(26),B0$(46,20),@(48),FNF$(B0$(66,10),M8$),@(63),B0$(76,4),@(68),B0$(80,6)
2195 GOTO 2800
2200 REM NAME
2220 LET V0$="S",V1$="C",V2$=B0$(26,20),V3$="",V4$="",V0=20,V1=4,V2=L
2225 LET I0=1
2230 GOSUB 7000
2280 LET B0$(26,20)=V$
2290 GOTO 2900
2300 REM TITLE
2320 LET V0$="S",V1$="C",V2$=B0$(46,20),V3$="",V4$="",V0=20,V1=26,V2=L
2330 GOSUB 7000
2380 LET B0$(46,20)=V$
2390 GOTO 2900
2400 REM TELEPHONE
2410 LET V0$="H",V1$="C",V2$=B0$(66,10),V3$=M8$,V4$="",V0=10,V1=48,V2=L
2430 GOSUB 7000
2440 LET B0$(66,10)=V$
2490 GOTO 2900
2500 REM EXT
2520 LET V0$="S",V1$="C",V2$=B0$(76,4),V3$="",V4$="",V0=4,V1=63,V2=L
2530 GOSUB 7000
2550 LET B0$(76,4)=V$
2590 GOTO 2900
2600 REM BEST TIME OF DAY
2620 LET V0$="S",V1$="C",V2$=B0$(80,6),V3$="",V4$="",V0=6,V1=68,V2=L
2630 GOSUB 7000
2650 LET B0$(80,6)=V$
2690 GOTO 2900
2800 REM NEXT
2810 LET V0$="S",V1$="C",V2$="",V3$="",V4$="<Return Key>=Accept Entry, <D>=Delete",V0=1,V1=76,V2=L,I0=5
2820 GOSUB 7000
2830 IF V3=2 THEN GOTO 2900
2835 IF V$="D" THEN GOTO 2892
2840 WRITE (TMM03,KEY=B0$(1,25))B0$
2850 IF V3=4 THEN GOTO 3000
2860 IF V3=9 THEN GOTO 9900
2880 LET L=L+1
2890 GOTO 2000
2892 REM DELETE
2894 REMOVE (TMM03,KEY=B0$(1,25),DOM=2895)
2896 PRINT @(0,L),'LD'
2898 GOTO 2000
2900 REM SEQ CONTROL
2905 IF V3=9 THEN GOTO 9900
2910 IF V3=4 THEN IF I0>1 THEN LET I0=4; GOTO 3000
2920 IF V3<>2 THEN GOTO 2950
2930 IF I0>0 THEN LET I0=I0-1
2940 GOTO 2960
2950 LET I0=I0+1
2960 ON I0 GOTO 2100,2200,2300,2400,2500,2600,2800
3000 REM EDIT LINES
3010 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Is the above information correct? <Y/N/Review/F4=Exit/F9=Quit>:",V0=6,V1=40+INT(LEN(V4$)/2),V2=14
3020 GOSUB 7000
3025 IF V3=9 THEN GOTO 9900
3030 IF V$="N" THEN GOTO 2100
3040 IF V3=4 THEN GOTO 1000
3050 IF V$="R" OR V$="REVIEW" THEN GOTO 3200
3060 IF V$<>"Y" THEN GOTO 3000
3080 GOTO 1000
3200 REM REVIEW
3210 FOR I=L0 TO L9; PRINT @(0,I),'CL',; NEXT I
3220 LET L=L0
3280 READ (TMM03,KEY=CONTACT$,DOM=2000)
3290 GOTO 2000
4000 REM "--- Correct
4010 LET FOOTING$(1)="",LINE$="<F1>=Comments <F2>=Add/Change <F4>=End <R>=Restart <F9>=Quit"; IF MORE$="Y" THEN LET LINE$="<CR>=More "+LINE$
4020 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V2=14,V1=LEN(LINE$)+2,FOOTING$(1)=LINE$
4030 PRINT @(0,14),'BR',FOOTING$,'ER',
4040 GOSUB 7000
4045 PRINT @(0,14),'CL',
4047 IF V3=1 OR V3=3 THEN GOTO 9900
4048 IF V$="R" THEN GOTO 1000
4049 IF V3=2 THEN GOTO 3000
4050 IF V3=4 THEN LET V3=0; GOTO 9900
4060 IF V3=9 THEN GOTO 9900
4070 IF POS(" "<>V$)=0 THEN IF MORE$="Y" THEN LET L=L0; PRINT 'CF',; GOTO 1300 ELSE GOTO 4000
4090 GOTO 2000
5000 REM "--- Page Break
5020 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="<CR> To Continue or END, <F9>=Quit",V0=1,V1=41+INT(LEN(V4$)/2),V2=16
5040 GOSUB 7000
5045 IF V3=9 THEN EXITTO 9900
5050 IF V$="END" THEN EXITTO 4000
5070 LET L=L0,DT_BRK$=""
5080 PRINT 'CF',
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8005 REM "-- Special Date functions for YYYYMMDD Dates: FNU=Unpack (trims Century), FNP=Pack (add Century), FNB=Display YYYYMMDD as MM/DD/YY
8010 DEF FNA$(Q$,Q2$)=STR((ASC(Q$)-32)*POS(" "<>Q2$(2,1)):"00")
8020 DEF FNB$(Q1$)=Q1$(5,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(7,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(3,2)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(NUM(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8080 DEF FNH(Q$)=ASC(Q$)-32
8090 DEF FNI$(Q)=CHR(Q+32)
8094 DEF FNP$(Q1$)="19"+Q1$
8096 DEF FNU$(Q1$)=Q1$(5,4)+Q1$(3,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM "END
9901 CLOSE (TMM03)
9902 CLOSE (SYS01_DEV,ERR=9903)
9920 PRINT 'POP',
9998 EXIT 
9999 END

0010 REM "ADC - Called Printer Selection"
0020 REM "Program: ADC.05"
0030 REM "+----------------------------------------------------------+"
0031 REM "|            ADminstrator System Version 1.0.2a            |
0032 REM "|     Copyright (C) 1994 MicroAccounting Systems, Inc.     |
0033 REM "|                 All Rights Reserved"                     |
0034 REM "+----------------------------------------------------------+
0039 REM "MAS:MCT 05/09/95 - #1420 Printer Mode Limit"
0040 REM "---- Call/Enter List Documentation"
0041 REM "FLAG$=00=Only Printers In The User's Record - No Mode Selection"
0042 REM "     =10=All Printers - No Mode Selection"
0043 REM "     =01=Only Printers In The User's Record - Include Mode Selection"
0044 REM "     =11=All Printers with mode selection"
0045 REM "     =21=Printer Passed For Mode selection"
0048 REM "PRINTER$=Return Selection"
0049 REM "MODE$=Font/Print Mode Return Selection"
0050 LET X$=OPTS; LET X$(1,1)=IOR(X$(1,1),$08$); SETOPTS X$
0060 SETERR 9000
0090 ENTER FLAG$,PRINTER$,MODE$
0110 LET ADS01_DEV=UNT; OPEN (ADS01_DEV,ERR=9800)"ADS-01"; REM "Open Parameter File"
0210 ADS01PA: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$(1),A6$(1),A[0],A[1],A[2],A[3],A[4]
0220 ADS01PB: IOLIST B0$(1),B1$(1),B2$(1),B3$(1),B4$(1),B5$(1),B6$(1),B[0],B[1],B[2],B[3],B[4]
0230 ADS01U: IOLIST U0$(1),U1$(1),U2$(1),U3$(1),U4$(1),U[0],U[1],U[2],U[3],U[4]
0260 SYS01T: IOLIST TFID0$,F0$,F1$,F2$,F3$,F4$,F5$
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0420 IF PAGES<3 THEN GOTO 9900
0430 LET FLAG$=FLAG$+"   ",PAGE=1,WIDTH=26,HEIGHT=18,MAX_ROW=HEIGHT-4,XPOS=27,YPOS=5,FKEY=0,SHEIGHT=HEIGHT-1,SWIDTH=26,SMAX_ROW=SHEIGHT-3,SXPOS=XPOS+WIDTH,SYPOS=YPOS+2,X1=1,X2=1,MODE=1
0440 DIM U0$(15),U1$(30),U2$(20),U3$(30),U4$(30),U[4]
0450 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,SHEIGHT-2]
0520 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2); LET HEADING$(INT(WIDTH/2)-4)="Printer",FOOTING$(WIDTH-10)="<F4>=End",TITLE$="Printer Selection",NAME$="",STITLE$="Printer Options"
0600 REM "---- User Record"
0610 IF FLAG$(1,1)<>"0" THEN GOTO 0700
0620 CALL "ADC.03",LOGNAME$,LNAME$,ERRFLAG
0630 IF ERRFLAG<>0 THEN LET STATUS=ERRFLAG; GOTO 9900
0640 DIM X$(15); LET X$(1)="U"+LOGNAME$; READ (ADS01_DEV,KEY=X$,DOM=9800)IOL=ADS01U
0650 LET VALID$=FNP$(U2$)
0700 REM "---- Background/Windows"
0710 IF FLAG$(1,1)="2" AND FNP$(PRINTER$)<>"" THEN GOTO 0740
0720 CALL "ADC.WA",0,WIDTH,HEIGHT,XPOS,YPOS,TITLE$,NAME$
0730 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0740 IF FLAG$(2,1)="0" THEN GOTO 0800
0750 CALL "ADC.WA",0,SWIDTH,SHEIGHT,SXPOS,SYPOS,STITLE$,SNAME$
0760 LET NOUPDATE=0; CALL "ADC.WB",NAME$,NOUPDATE
1000 REM "Select Printer"
1010 IF FLAG$(1,1)="2" AND FNP$(PRINTER$)<>"" THEN GOTO 2000
1020 READ (ADS01_DEV,KEY="P",DOM=1021)
1120 LET K$=KEY(ADS01_DEV,END=1800)
1140 IF K$(1,1)<>"P" THEN GOTO 1800
1145 REM FID(0)="T136" THEN ESCAPE
1160 IF K$(4,1)<>$20$ THEN READ (ADS01_DEV,KEY=K$); GOTO 1100
1170 DIM A0$(9),A1$(30),A2$(30),A3$(30),A4$(250),A5$(250),A6$(30),A[4]
1180 READ (ADS01_DEV,KEY=K$)IOL=ADS01PA
1190 IF FLAG$(1,1)="0" AND POS(A0$(2,2)=VALID$,2)=0 THEN GOTO 1100
1210 IF X1>9 THEN LET CHAR$=CHR(55+X1) ELSE LET CHAR$=STR(X1) FI
1220 LET LLIST$[PAGE,X1,1]=" "+CHAR$+"-"+A0$(2,2)+" "+A1$,LLIST$[PAGE,X1,2]=A0$(2,2),LLIST$[PAGE,X1,5]=SNAME$
1240 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2)
1320 LET K1$=KEY(ADS01_DEV,END=1800)
1340 IF K1$(1,3)<>A0$(1,3) THEN GOTO 1700
1350 DIM B0$(8),B1$(30),B2$(30),B3$(30),B4$(250),B5$(250),B6$(30),B[5]
1360 READ (ADS01_DEV,KEY=K1$)IOL=ADS01PB
1380 LET MORE$[PAGE,X1,X2]=" "+B0$(7,1)+" "+B1$,X2=X2+1
1420 IF X2>SMAX_ROW THEN GOTO 1700
1490 GOTO 1300
1740 LET X1=X1+1,X2=1,MODE=1
1750 IF X1>=MAX_ROW-1 THEN GOSUB 6000
1790 GOTO 1100
1800 REM "---- EOF|Printer Names"
1810 IF X1=1 AND PAGE=1 THEN GOTO 1900
1815 IF FLAG$(1,2)="10" AND SELECTION$<>"" THEN GOTO 1830
1820 LET MODE=5,SELECTION$=SELECTION$+"|EOF"
1825 GOSUB 6000
1830 IF FNP$(SELECTION$)="" OR SELECTION$="END" THEN GOTO 9900
1840 LET PRINTER$=SELECTION$
1860 IF FLAG$(2,1)="1" THEN GOTO 2000
1890 GOTO 9900
1900 REM "---- No Records Found"
1920 PRINT @(1,INT(MAX_ROW/2)),'SF',"No Printers Found: ",; LET V0$="S",V1$="",V2$="",V3$="",V4$="",V0=1,V1=21,V2=INT(MAX_ROW/2); GOSUB 7000; GOTO 9900
2000 REM "---- Printer Mode Selection"
2020 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,SHEIGHT-2]
2040 LET X1=1,X2=1,PAGE=1,MODE=0
2050 LET NOUPDATE=0; CALL "ADC.WB",SNAME$,NOUPDATE
2120 READ (ADS01_DEV,KEY="P"+PRINTER$,DOM=2121)
2220 LET K$=KEY(ADS01_DEV,END=2800)
2240 IF K$(1,3)<>"P"+PRINTER$ THEN GOTO 2800
2260 IF FNP$(K$(7,2))="" THEN READ (ADS01_DEV,KEY=K$); GOTO 2200
2320 DIM B0$(15),B1$(30),B2$(30),B3$(30),B4$(150),B5$(150),B6$(30),B[4]
2340 READ (ADS01_DEV,KEY=K$)IOL=ADS01PB
2420 LET LLIST$[PAGE,X1,1]=" "+B0$(7,1)+" "+B1$,LLIST$[PAGE,X1,2]=B0$(7,1)
2440 LET X1=X1+1
2460 IF X1>MAX_ROW THEN GOSUB 6000
2790 GOTO 2200
2800 REM "---- EOF|Printer Modes"
2805 IF POS(PRINTER$="PZPF",2)>0 THEN GOTO 2890
2810 IF X1=1 AND PAGE=1 THEN GOTO 2900
2820 GOSUB 6000
2830 IF FNP$(SELECTION$)="" OR SELECTION$="END" THEN GOTO 9900
2840 LET MODE$=SELECTION$
2890 GOTO 4000
2900 REM "---- No Mode Records Found"
2920 PRINT @(1,INT(SMAX_ROW/2)),'SF',"No Fonts Found: ",; LET V0$="S",V1$="",V2$="",V3$="",V4$="",V0=1,V1=21,V2=INT(SMAX_ROW/2); GOSUB 7000; GOTO 9900
4000 REM "---- Write User & ADD+ON Terminal Records & Exit"
4010 IF POS(PRINTER$="PZPF",2)>0 THEN LET MODE$=" "; GOTO 4140
4120 IF FNP$(PRINTER$)="" OR FNP$(MODE$)="" THEN GOTO 9900
4140 IF FLAG$(1,2)<>"01" THEN GOTO 9900
4250 DIM U0$(15),U1$(40),U2$(20),U3$(30),U4$(30),U[4]
4260 DIM X$(15); LET X$(1)="U"+LOGNAME$
4270 READ (ADS01_DEV,KEY=X$)IOL=ADS01U
4280 LET U1$(16,2)=PRINTER$,U1$(21,1)=MODE$
4320 WRITE (ADS01_DEV,KEY=U0$)IOL=ADS01U
4340 LET SYS01_DEV=UNT; OPEN (SYS01_DEV,ERR=9900)"SYS-01"
4360 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
4380 LET F0$(13,2)=PRINTER$,F0$(25,1)=MODE$
4420 WRITE (SYS01_DEV,KEY="T"+FID(0))IOL=SYS01T
4990 GOTO 9900
6000 REM "---- Light Bar Select Printer"
6020 LET X1=1,X2=1
6120 CALL "ADC.SA",MODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6130 IF FKEY=-16 AND PAGE<PAGES THEN LET PAGE=PAGE+1 ELSE IF FKEY=-16 AND PAGE=PAGES THEN GOTO 6100
6140 IF FKEY=-17 AND PAGE=1 THEN GOTO 6100 ELSE IF FKEY=-17 AND PAGE>1 THEN LET PAGE=PAGE-1; GOTO 6100 FI FI
6190 RETURN 
6200 REM "---- Light Bar Select Mode"
6390 RETURN 
7000 REM " --- User Input"
7020 CALL "ADC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-1),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7040 IF V3=5 THEN GOTO 7100
7050 IF V3=6 THEN GOTO 7200
7085 LET I$=V$
7090 RETURN 
7100 REM " --- Error During Input"
7200 REM " --- Escape Requested"
7210 PRINT @(0,22),'CL','CURSOR'("ON"),
7220 ESCAPE
7290 GOTO 7000
8060 DEF FNP$(Q$)=CVS(Q$,2)
8070 DEF FNQ$(Q$)=CVS(Q$,8)
8080 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Called Error Processing"
9010 LET E$=LST(PGM(TCB(5))),E1$=STR(TCB(5)),E2$=PGM(-1),E0=ERR,E1=0
9030 CALL "ADC.EA",ERR=9200,E$,E1$,E2$,E0,E1
9050 IF E1<>1 THEN GOTO 9200
9100 REM " --- Retry"
9110 RETRY
9200 REM " --- Exit to host"
9210 SETERR 9000
9290 GOTO 9900
9800 REM "---- Error Exit"
9820 IF ERR=0 THEN LET STATUS=99 ELSE LET STATUS=ERR
9900 REM "---- Exit"
9910 CALL "ADC.WD",NAME$
9920 CALL "ADC.WD",SNAME$
9930 IF LEN(F0$)<25 THEN GOTO 9960
9940 PRINT 'SB',@(16,1),"    ",@(16,1),F0$(13,2),'SF',
9950 IF F0$(25,1)<>" " THEN PRINT 'SB',"-",F0$(25,1),'SF',
9960 CLOSE (SYS01_DEV,ERR=9961)
9980 CLOSE (ADS01_DEV,ERR=9981)
9990 EXIT 
9999 END

0010 REM "TMM - TeleMarketing Masterfile Maintenance (Callable)"
0020 REM "Program: TMMCMA"
0030 REM "ADD+ON SOFTWARE VERSION 5
0031 REM "11/27/91 by Jerry Koch
0032 REM "MAS * DCF * 5/12/93 * RICHMARK COMPANY
0033 REM "MAS * JCM * Racing Strollers * WA:205 (4046)
0040 REM "COPYRIGHT(C)1991 MicroAccounting Systems, Inc. ALL RIGHTS RESERVED
0041 REM 
0042 REM "06/04/96 KJS WA: #211 <F10> hotkey to T/M Maint
0043 REM "01/06/97 KJS WA: #221 ADS"
0080 ENTER XX
0089 REM 
0090 SETERR 9000
0095 REM GOSUB 10000; IF FAIL THEN GOTO 9980
0100 REM "OPEN FILES"
0105 DIM CHAN[13]
0110 LET CHAN[1]=UNT; OPEN (CHAN[1])"TMM-01"
0120 LET CHAN[2]=UNT; OPEN (CHAN[2])"TMM-02"
0130 LET CHAN[3]=UNT; OPEN (CHAN[3],ERR=0151)"ARM-01"
0140 LET CHAN[4]=UNT; OPEN (CHAN[4],ERR=0151)"ARM-02"
0150 LET CHAN[5]=UNT; OPEN (CHAN[5],ERR=0151)"ARM-04"
0160 LET CHAN[6]=UNT; OPEN (CHAN[6])"SYS-01"
0180 LET CHAN[8]=UNT; OPEN (CHAN[8],ERR=0181)"ARM-10"
0190 LET CHAN[9]=UNT; OPEN (CHAN[9])"TMM-01"
0195 LET CHAN[12]=UNT; OPEN (CHAN[12])"TMM-03"
0200 REM "IOLISTS"
0210 IOLIST A0$,A1$(1),A2$(1),A3$(1),A4$(1)
0260 IOLIST P0$,P1$,P2$,P3$,P4$
0400 REM " --- Parameters"
0401 GOSUB 62000; REM "WA: #221
0420 LET N0$=PRM$[FID,TFIRM],N1$=PRM$[FID,TFNAME],N2$="TM",N4$=PRM$[FID,TPNAME]; REM "WA: #221
0430 FIND (CHAN[6],KEY=N0$+N2$+"00",DOM=9800)IOL=0260
0440 LET M0$="000000",R9$=PRM$[AR,INSTALL]; REM "WA: #221, I2$="06009"
0490 LET M1$="###,###.00-",M1=LEN(M1$)
0500 REM "INIT DATA"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 LET L1=15,L9=21
0530 LET K0=1,K9=22
0540 LET M7$="00000",M8$="(###)-###-####"
0550 DIM P[2]
0560 LET P[0]=NUM(PRM$[AR,KEYLEN]),P[1]=NUM(PRM$[AR,KEYMAX]),P[2]=NUM(PRM$[AR,THREE](4,1)); REM "WA: #221
0570 IF P[2]=9 THEN LET M7$=M7$+"-0000"
0580 DIM A0$(450)
0590 LET SRT$="0",A0$(1)=N0$
0595 LET QUIT=0
0620 CLOSE (CHAN[6]); OPEN (CHAN[6])"TMM-10"
0640 LET CHAN[13]=UNT; OPEN (CHAN[13])"SYS-01"
0700 REM "BACKGROUND"
0703 REM "Can't use SYC.WA because a) it clears the window, and b) it draws aborder, even if MODE=1"
0704 REM "If you have to use SYC.WA, you will have to redraw the first three rows of the screen, see SYS.AA (not fun)"
0705 PRINT 'PUSH','SB',@(22,2),"Telemarketing Masterfile Maintenance",'SF',@(0,3),'CE',; REM "Copy the current window and paint screen
0710 IF P0$(16,1)="Y" THEN PRINT 'SB',@(0,3),"Firm:",@(27),"Cont:", ELSE PRINT 'SB',@(0,3),"Cont:",@(27),"Firm:",
0720 PRINT @(49),"Tele:",@(70),"Ext:",@(1,4),"Cont:",@(50),"Fax:",
0730 PRINT @(0,5),"Title:",@(38),"Territory:",@(59),"Init Ent:",@(1,6),"Firm:",@(38),"Next Call:",@(59),"Last Ent: "
0740 PRINT @(0,7),"Addr1:",@(40),"Ord Rep:",@(61),"Source:",@(0,8),"Addr2:",@(41),"Ord Dt:",@(63),"Intr: ",
0750 PRINT @(1,9),"City:",@(40),"CSR Rep:",@(59),"Send Lit:",@(0,10),"State:",@(11),"Zip:",@(39),"CSR Code: ",@(59),"Lit Code: ",
0760 PRINT @(0,11),"Cntry:",@(41),"Logged:",@(61),"Lit Dt:",@(0,12),"Cust#:",@(20),"Terms:",@(41),"Recall:",@(63),"Dear:",@(40,13),"1st Inv:",@(59),"Last Inv:",
0765 PRINT @(1,13),"Type:",@(21,13),"Disc:",
0770 PRINT @(0,14),J$
0800 REM "--- Set Parameters (User Def'd Fields)
0810 REM LET X9=24, X8=LEN(P1$)/X9-1, X2$="", X3$="", X4$=""
0811 LET X9=21,X8=LEN(P1$)/X9-1,X2$="",X3$="",X4$=""
0820 LET X0=0,Y0=L1
0830 FOR X=1 TO X9
0840 LET X0$=P1$((X-1)*(X8+1)+1,X8+1)
0850 IF X0$(1,1)="S" THEN LET X1$=X1$+STR(X:"00")
0860 IF X0$(1,1)="D" THEN LET X2$=X2$+STR(X:"00")
0870 IF X0$(1,1)="Y" THEN LET X3$=X3$+STR(X:"00")
0880 IF X0$(1,1)="N" THEN LET X4$=X4$+STR(X:"00")
0890 IF POS(X0$(1,1)="SDYN")=0 THEN GOTO 0940
0900 IF Y0>L9 THEN LET Y0=L1,X0=X0+40
0910 LET X0$(2,X8)=J1$(1,X8-LEN(CVS(X0$(2,X8),2)))+X0$(2)
0920 PRINT @(X0,Y0),X0$(2,X8),":",
0930 LET Y0=Y0+1
0940 NEXT X
0950 PRINT 'SF',
0970 DIM A1$(LEN(X1$)/2*25),A2$(LEN(X2$)/2*8),A3$(LEN(X3$)/2),A4$(LEN(X4$)/2*10)
0975 LET A1LEN=LEN(A1$),A2LEN=LEN(A2$),A3LEN=LEN(A3$),A4LEN=LEN(A4$)
0980 REM "--- Init File
0990 READ (CHAN[1],KEY=N0$,KNUM=NUM(SRT$),DOM=0991)
1000 REM "KEY"
1010 PRINT 'CF',
1020 LET V2$="",SORTKEY$=KEY(CHAN[1],KNUM=NUM(SRT$),END=1050),V2$=SORTKEY$
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF V2$<>"" THEN READ (CHAN[1],KEY=V2$,KNUM=NUM(SRT$))V2$; LET V2$=V2$(3,20)
1050 IF P0$(16,1)="Y" THEN LET V4$="Enter The Contact Look-Up Firm" ELSE LET V4$="Enter The Contact Look-Up Name"
1060 LET V0$="S",V1$="KC",V3$="",V0=20,V1=6,V2=3,V4$=V4$+" (<F1>=Change Sort, <F3>=Lookup, <F9>=Quit)"
1070 IF R9$="Y" THEN PRINT @(18,23),"<F2>=Create Contact From Customer Masterfile",
1080 GOSUB 7000
1085 IF V3=9 THEN GOTO 9900
1090 PRINT @(0,23),'CL',@(0,22),
1110 IF V3=1 THEN GOSUB 6600; GOTO 1020
1120 IF R9$="Y" AND V3=2 THEN LET V2$=V$; CALL "TMC.CA",A0$,A1$,A2$,A3$,A4$,CHAN[ALL],V3; IF V3=9 THEN GOTO 4035 FI; IF POS(" "<>A0$(3,20))=0 THEN GOTO 1050 ELSE GOSUB 5000; GOTO 1600
1130 IF V3=4 OR V$="END" THEN GOTO 9900
1140 IF V3=3 THEN LET V2$=V$,V$=""; CALL "TMC.AA",V$,V3; IF V3=9 THEN GOTO 4035
1150 IF V$="" THEN GOTO 0980
1160 IF POS(" "<>V$)=0 THEN GOTO 1000
1170 IF NUM(V$,ERR=1190)>0 THEN READ (CHAN[1],KEY=A0$(1,2)+STR(NUM(V$):M0$),KNUM=2,DOM=1171)
1172 LET K$=KEY(CHAN[1],KNUM=2,END=1000); IF K$(3,LEN(M0$))<>STR(NUM(V$):M0$) THEN GOTO 1000
1175 READ (CHAN[1],KNUM=2,END=1000)IOL=0210
1180 GOTO 1230
1190 LET A0$(3)=V$
1200 REM "FIND KEY"
1220 READ (CHAN[1],KEY=A0$(1,K9),KNUM=0,DOM=1400)IOL=0210
1230 READ (CHAN[1],KEY=SORTKEY$,KNUM=NUM(SRT$),DOM=1231)
1240 LET CONTACT$=""
1250 READ (CHAN[12],KEY=A0$(1,K9),DOM=1251)
1260 LET K12$=KEY(CHAN[12],END=1300)
1270 IF K12$(1,K9)<>A0$(1,K9) THEN GOTO 1300
1280 LET CONTACT$="YES"
1300 REM "DISPLAY"
1320 GOSUB 5000
1340 GOTO 4000
1400 REM "NEW RECORD"
1410 LET V0$="Y",V1$="CR",V2$="",V3$="",V4$="Is This A New Record? <Y/N>: ",V0=1,V1=41+INT(LEN(V4$)/2),V2=22
1420 GOSUB 7000
1430 LET V$=CVS(V$,4)
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM "SET-UP"
1510 LET A0$(23)="",A1$(1)="",A2$(1)="",A3$(1)="",A4$(1)=""
1520 LET A0$(331,8)=FNP$(PRM$[FID,TLDATE]),A0$(339,8)=A0$(331,8),A0$(360,1)="N"; REM "WA: #221
1530 LET CONTACT$=""
1590 PRINT @(69,5),FNB$(A0$(331,8)),@(69,6),FNB$(A0$(339,8)),
1600 REM "--- Identification Firm Name
1620 LET V0$="S",V1$="C",V2$=A0$(23,15),V3$="",V4$="Enter The Look-Up Firm For This Contact",V0=15,V1=33,V2=3,I0=0
1630 IF P0$(16,1)="Y" THEN LET V4$="Enter The Look-Up Name For This Contact"
1640 GOSUB 7000
1680 LET A0$(23,15)=V$
1690 GOTO 3900
1700 REM "--- Phone
1720 LET V0$="H",V1$="C",V2$=A0$(38,10),V3$=M8$,V4$="Enter The Phone Number For This Contact",V0=10,V1=55,V2=3
1740 GOSUB 7000
1780 LET A0$(38,10)=V$
1790 GOTO 3900
1800 REM "--- Extension
1810 LET V0$="S",V1$="C",V2$=A0$(48,4),V3$="",V4$="Enter The Telephone Extension",V0=4,V1=75,V2=3
1820 GOSUB 7000
1830 LET A0$(48,4)=V$
1840 GOTO 3900
1850 REM "--- Contact Name
1855 IF A0$(52,30)=J1$(1,30) THEN GOSUB 6700; IF X0$<>"" THEN LET A0$(52,30)=X0$
1860 LET V0$="S",V1$="C",V2$=A0$(52,30),V3$="",V4$="Enter The Full Contact Name",V0=30,V1=7,V2=4
1870 GOSUB 7000
1880 LET OLD$=A0$(52,30),A0$(52,30)=V$
1885 IF P0$(16,1)<>"Y" AND POS(" "<>OLD$)<>0 THEN IF A0$(52,30)<>OLD$ THEN CALL "TMC.GA",A0$,P0$(16,1),CHAN[ALL]; PRINT @(6,3),A0$(3,20),
1890 GOTO 3900
1900 REM "--- Title
1910 LET V0$="S",V1$="C",V2$=A0$(82,30),V3$="",V4$="Enter This Contact's Title",V0=30,V1=7,V2=5
1920 GOSUB 7000
1930 LET A0$(82,30)=V$
1940 GOTO 3900
1950 REM "--- Company
1955 IF A0$(112,30)=J1$(1,30) THEN LET A0$(112,30)=A0$(3,20)
1960 LET V0$="S",V1$="C",V2$=A0$(112,30),V3$="",V4$="Enter The Contact's Full Company Name",V0=30,V1=7,V2=6
1970 GOSUB 7000
1980 LET OLD$=A0$(112,30),A0$(112,30)=V$
1985 IF P0$(16,1)="Y" AND POS(" "<>OLD$)<>0 THEN IF A0$(112,30)<>OLD$ THEN CALL "TMC.GA",A0$,P0$(16,1),CHAN[ALL]; PRINT @(6,3),A0$(3,20),
1990 GOTO 3900
2000 REM "--- Address 1
2010 LET V0$="S",V1$="C",V2$=A0$(142,30),V3$="",V4$="Enter The First Address Line",V0=30,V1=7,V2=7
2020 GOSUB 7000
2030 LET A0$(142,30)=V$
2040 GOTO 3900
2050 REM "--- Address 2
2060 LET V0$="S",V1$="C",V2$=A0$(172,30),V3$="",V4$="Enter The Second Address Line",V0=30,V1=7,V2=8
2070 GOSUB 7000
2080 LET A0$(172,30)=V$
2090 GOTO 3900
2100 REM "--- City
2110 LET V0$="S",V1$="C",V2$=A0$(202,25),V3$="",V4$="Enter The City",V0=25,V1=7,V2=9
2120 GOSUB 7000
2130 LET A0$(202,25)=V$
2140 GOTO 3900
2150 REM "--- State
2160 LET V0$="S",V1$="C",V2$=A0$(227,2),V3$="",V4$="Enter The 2 Character State",V0=2,V1=7,V2=10
2170 GOSUB 7000
2180 LET A0$(227,2)=V$
2190 GOTO 3900
2200 REM "--- Zip
2210 LET V0$="S",V1$="C",V2$=A0$(229,P[2]),V3$="",V4$="Enter The Zip Code",V0=P[2],V1=16,V2=10
2220 GOSUB 7000
2225 IF LEN(V$)<9 THEN LET V$=V$+J1$(9-LEN(V$))
2230 LET A0$(229,9)=V$
2235 PRINT @(V1,V2),FNZ$(A0$(229)),
2240 GOTO 3900
2250 REM "--- Country
2260 LET V0$="S",V1$="C",V2$=A0$(238,15),V3$="",V4$="Enter The Country For This Address",V0=15,V1=7,V2=11
2270 GOSUB 7000
2280 LET A0$(238,15)=V$
2290 GOTO 3900
2300 REM "--- Customer
2301 IF A0$(253,6)<>"      " AND PRM$[FID,TSECURE]<"9" THEN GOTO 2390; REM "WA: #221
2305 IF R9$="Y" THEN LET V4$="Enter A Valid A/R Customer Number, (<F3>=Lookup)" ELSE LET V4$="Enter A Customer Number For This Contact"
2310 LET V0$="M",V1$="C",V2$=A0$(253,6),V3$=M0$,V0=P[0],V1=7,V2=12
2320 GOSUB 7000
2330 IF R9$<>"Y" THEN GOTO 2380
2332 IF V3<>3 THEN GOTO 2350
2336 CALL "SYC.LC",V$
2340 IF V$="" THEN GOTO 2300
2350 IF POS("0"<>V$)=0 THEN LET A0$(269,16)="",A0$(253,6)=V$; GOTO 2385
2355 FIND (CHAN[3],KEY=N0$+V$,DOM=2300)*,CUST$
2356 IF V$=A0$(253,6) THEN GOTO 2360 ELSE READ (CHAN[1],KEY=N0$+V$,KNUM=2,DOM=2357)
2357 LET K$=KEY(CHAN[1],KNUM=2,END=2359); IF K$(1,LEN(M0$)+2)<>N0$+V$ THEN GOTO 2359 ELSE INPUT @(0,22),'CE',"Customer exists in telemarketing <CR>: ",*,@(0,22),'CL'; READ (CHAN[1],KEY=SORTKEY$,KNUM=NUM(SRT$),DOM=2358)
2358 GOTO 2300
2359 READ (CHAN[1],KEY=SORTKEY$,KNUM=NUM(SRT$),DOM=2360)
2360 IF V3=1 THEN LET A0$(253,6)=V$; GOTO 2380
2365 IF A0$(253,6)=V$ THEN GOTO 2390
2367 IF NUM(A0$(253,6),ERR=2368)<>0 THEN INPUT @(0,22),'CE',"Cannot change customer number - only add or delete <CR>: ",*,@(0,22),'CL'; GOTO 2300
2370 LET A0$(253,6)=V$
2375 CALL "TMC.DA",A0$; GOSUB 5000
2380 LET LINE$="Customer: "+CVS(CUST$(1,30),2); GOSUB 5260
2390 GOTO 3900
2400 REM "--- Cust Type
2405 IF R9$="Y" THEN LET V4$="Enter A Valid A/R Customer Type, (<F3>=Lookup)",V5$="L" ELSE LET V4$="Enter A Customer Type For This Contact",V5$=""
2410 LET V0$="S",V1$="C",V2$=A0$(260,3),V3$="",V0=3,V1=7,V2=13
2420 GOSUB 7000
2430 IF R9$<>"Y" THEN GOTO 2480
2432 IF V3<>3 THEN GOTO 2450
2435 GOSUB 11000
2440 IF V$="" THEN GOTO 2400
2450 LET V$=V$+"   ",V$=V$(1,3)
2455 IF V$="   " THEN LET V5$=""; GOTO 2480
2460 FIND (CHAN[8],KEY=N0$+V5$+V$,DOM=2400)X0$
2465 PRINT @(V1,V2),V$
2470 LET LINE$="Type: "+CVS(X0$(7,20),2); GOSUB 5260
2480 LET A0$(259,4)=V5$+V$
2490 GOTO 3900
2500 REM "--- Discount
2505 IF R9$="Y" THEN LET V4$="Enter A Valid A/R Discount Code, (<F3>=Lookup)",V5$="I" ELSE LET V4$="Enter A Discount Code For This Contact",V5$=""
2510 LET V0$="S",V1$="C",V2$=A0$(264,2),V3$="",V0=2,V1=27,V2=13
2520 GOSUB 7000
2530 IF R9$<>"Y" THEN GOTO 2580
2532 IF V3<>3 THEN GOTO 2550
2535 GOSUB 11000
2540 IF V$="" THEN GOTO 2500
2550 LET V$=V$+"  ",V$=V$(1,2)
2555 IF V$="  " THEN LET V5$=""; GOTO 2580
2560 FIND (CHAN[8],KEY=N0$+V5$+V$,DOM=2500)X0$
2565 PRINT @(V1,V2),V$
2570 LET LINE$="Disc: "+CVS(X0$(6,20),2); GOSUB 5260
2580 LET A0$(263,3)=V5$+V$
2590 GOTO 3900
2600 REM "--- Terms
2601 REM IF A0$(267,2)<>"  " AND PRM$[FID,TSECURE]<"9" THEN GOTO 2690; REM "WA: #221
2605 IF R9$="Y" THEN LET V4$="Enter A Valid A/R Terms Code (<F3>=Lookup)",V5$="A" ELSE LET V4$="Enter A Terms Code For This Contact",V5$=""
2610 LET V0$="S",V1$="C",V2$=A0$(267,2),V3$="",V0=2,V1=27,V2=12
2615 REM IF A0$(267,2)="  " AND PRM$[FID,TSECURE]<"9" THEN LET V$="10"; PRINT @(V1,V2),V$,; GOTO 2680; REM "WA: #221
2620 GOSUB 7000
2630 IF R9$<>"Y" THEN GOTO 2680
2632 IF V3<>3 THEN GOTO 2650
2635 GOSUB 11200
2640 IF V$="" THEN GOTO 2600
2650 LET V$=V$+"  ",V$=V$(1,2)
2655 IF V$="  " THEN LET V5$=""; GOTO 2680
2660 FIND (CHAN[8],KEY=N0$+V5$+V$,DOM=2600)X0$
2665 PRINT @(V1,V2),V$
2670 LET LINE$="Terms: "+CVS(X0$(6,20),2); GOSUB 5260
2680 LET A0$(266,3)=V5$+V$
2690 GOTO 3900
2700 REM "--- TERRITORY
2720 LET V0$="S",V1$="C",V2$=A0$(285,3),V3$="",V4$="Enter The Territory For This Contact (<F3>=Look-Up)",V0=3,V1=49,V2=5
2740 GOSUB 7000
2750 IF V3<>3 THEN GOTO 2770
2755 GOSUB 11300
2765 IF V$="" THEN GOTO 2700
2780 LET A0$(285,3)=V$
2783 FIND (CHAN[8],KEY=N0$+"H"+A0$(285,3),DOM=2700)X0$
2784 PRINT @(V1,V2),V$
2786 LET LINE$="Terr: "+CVS(X0$(7,20),2); GOSUB 5260
2790 GOTO 3900
2800 REM "--- Marketing Date
2810 REM IF POS(" "<>A0$(285,3))=0 THEN GOTO 03900
2820 LET V0$="D",V1$="C6",V2$=FNU$(A0$(288,8)),V3$="",V4$="Enter The Marketing Callback Date (<F1>="+CVS(P0$(7,3),2)+" Days)",V0=6,V1=49,V2=6
2840 GOSUB 7000
2850 IF V3<>1 THEN GOTO 2875
2860 LET V$=PRM$[FID,TLDATE]; REM "WA: #221
2870 CALL "TMC.BA",V$,P0$(7,3)
2875 IF V$<>"" THEN PRINT @(V1,V2),FNB$(FNP$(V$)),
2880 LET A0$(288,8)=FNP$(V$)
2885 IF POS(" "<>V$)=0 THEN LET A0$(288,8)=""
2890 GOTO 3900
2900 REM "--- Order Rep
2920 LET V0$="S",V1$="C",V2$=A0$(296,3),V3$="",V4$="Enter The Ordering Rep For This Contact",V0=3,V1=49,V2=7
2940 GOSUB 7000
2970 IF POS(" "<>V$)=0 THEN LET A0$(299,8)=""; PRINT @(49,8),FNB$(A0$(299,8)),
2980 LET A0$(296,3)=V$
2990 GOTO 3900
3000 REM "--- Order Date
3010 IF POS(" "<>A0$(296,3))=0 THEN GOTO 3900
3020 LET V0$="D",V1$="C6",V2$=FNU$(A0$(299,8)),V3$="",V4$="Enter The Ordering Date (<F1>=Today)",V0=6,V1=49,V2=8
3040 GOSUB 7000
3050 IF V3=1 THEN LET V$=PRM$[FID,TLDATE]; PRINT @(V1,V2),FNB$(FNP$(V$)),; REM  "WA: #221
3080 LET A0$(299,8)=FNP$(V$)
3090 GOTO 3900
3100 REM "--- CSR Rep
3120 LET V0$="S",V1$="C",V2$=A0$(307,3),V3$="",V4$="Enter The Customer Service Rep For This Contact",V0=3,V1=49,V2=9
3140 GOSUB 7000
3170 IF POS(" "<>V$)=0 THEN LET A0$(310,21)=""; PRINT @(49,9),"    ",@(49,10),FNB$(A0$(315,8)),@(49,11),FNB$(A0$(323,8)),
3180 LET A0$(307,3)=V$
3190 GOTO 3900
3200 REM "--- Literature Date
3210 LET V0$="D",V1$="C6",V2$=FNU$(A0$(361,8)),V3$="",V4$="Enter The Date To Send Literature",V0=8,V1=69,V2=11,V5$="A"
3220 GOSUB 7000
3240 IF V$="" THEN LET V$=J1$(1,8)
3250 PRINT @(V1,V2),FNB$(FNP$(V$)),
3280 LET A0$(361,8)=FNP$(V$)
3290 GOTO 3900
3300 REM "--- Logged Date
3320 LET V0$="D",V1$="C6",V2$=FNU$(A0$(315,8)),V3$="",V4$="Enter The CSR Logged Date (<F1>=Today)",V0=6,V1=49,V2=11
3340 GOSUB 7000
3350 IF V3=1 THEN LET V$=PRM$[FID,TLDATE]; REM "WA: #221
3360 IF V$<>"" THEN PRINT @(V1,V2),FNB$(FNP$(V$)),
3380 LET A0$(315,8)=FNP$(V$)
3390 GOTO 3900
3400 REM "--- Recall Date
3420 LET V0$="D",V1$="C6",V2$=FNU$(A0$(323,8)),V3$="",V4$="Enter The CSR Recall Date (<F1>="+CVS(P0$(7,3),2)+" Days)",V0=6,V1=49,V2=12
3440 GOSUB 7000
3450 IF V3<>1 THEN GOTO 3475
3460 LET V$=PRM$[FID,TLDATE]; REM "WA: #221
3470 CALL "TMC.BA",V$,P0$(7,3)
3475 IF V$<>"" THEN PRINT @(V1,V2),FNB$(FNP$(V$)),
3480 LET A0$(323,8)=FNP$(V$)
3490 GOTO 3900
3500 REM "--- Source Code
3510 LET V0$="S",V1$="C",V2$=A0$(348,4),V3$="",V4$="Enter A Valid Source Code, (<F3>=Lookup)",V0=4,V1=69,V2=7,V5$="B"
3520 GOSUB 7000
3530 IF V3<>3 THEN GOTO 3550
3535 GOSUB 11400
3540 IF V$="" THEN GOTO 3500
3550 LET V$=V$+"    ",V$=V$(1,4)
3560 FIND (CHAN[6],KEY=N0$+V5$+V$,DOM=3500)X0$
3565 PRINT @(V1,V2),V$
3570 LET LINE$="Source: "+CVS(X0$(8,20),2); GOSUB 5260
3580 LET A0$(347,5)=V5$+V$
3590 GOTO 3900
3600 REM "--- Interest
3610 LET V0$="S",V1$="C",V2$=A0$(353,4),V3$="",V4$="Enter A Valid Interest Code, (<F3>=Lookup)",V0=4,V1=69,V2=8,V5$="C"
3620 GOSUB 7000
3630 IF V3<>3 THEN GOTO 3650
3635 GOSUB 11500
3640 IF V$="" THEN GOTO 3600
3650 LET V$=V$+"    ",V$=V$(1,4)
3660 FIND (CHAN[6],KEY=N0$+V5$+V$,DOM=3600)X0$
3665 PRINT @(V1,V2),V$
3670 LET LINE$="Interest: "+CVS(X0$(8,20),2); GOSUB 5260
3680 LET A0$(352,5)=V5$+V$
3690 GOTO 3900
3700 REM "--- Lit Code
3705 IF A0$(360,1)="N" THEN GOTO 3900
3710 LET V0$="S",V1$="C",V2$=A0$(358,2),V3$="",V4$="Enter A Valid Literature Code, (<F3>=Lookup)",V0=2,V1=69,V2=10,V5$="D"
3720 GOSUB 7000
3730 IF V3<>3 THEN GOTO 3750
3735 GOSUB 11600
3740 IF V$="" THEN GOTO 3700
3750 LET V$=V$+"  ",V$=V$(1,2)
3760 FIND (CHAN[6],KEY=N0$+V5$+V$,DOM=3700)X0$
3765 PRINT @(V1,V2),V$
3770 LET LINE$="Literature: "+CVS(X0$(6,20),2); GOSUB 5260
3780 LET A0$(357,3)=V5$+V$
3790 GOTO 3900
3800 REM "--- Fax Phone
3820 LET V0$="H",V1$="C",V2$=A0$(379,10),V3$=M8$,V4$="Enter The Fax Number For This Contact",V0=10,V1=55,V2=4
3840 GOSUB 7000
3880 LET A0$(379,10)=V$
3890 GOTO 3900
3900 REM "NEXT FIELD"
3905 IF V3=9 THEN LET QUIT=1; GOTO 4100 ELSE LET QUIT=0
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 IF V3=4 THEN GOTO 4000
3950 LET I0=I0+1
3960 ON I0 GOTO 1600,1700,1800,1850,3800,1900,1950,2000,2050,2100,2150,2200,2250,2300,2600,2400,2500,2700,2800,2900,3000,3100,5400,3300,3400,3500,3600,5500,3700,3200,5700,5900,5800,6000
3970 REM 0 GOTO 01600,01700,01800,01850,01900,01950,02000,02050,02100,02150,02200,02250,02300,02400,02500,02600,02700,02800,02900,03000,03100,03200,03300,03400,03500,03600,05500,03700,05600,05700,06000
3990 LET I0=K0
4000 REM "OPTIONS"
4010 LET V4$="<F1>=Comments, "; IF CONTACT$="YES" THEN LET V4$=V4$+'BR'+"<F2>=Add'l Contacts"+'ER' ELSE LET V4$=V4$+"<F2>=Add'l Contacts"
4011 REM LET V4$=V4$+", <F3>=Prev Orders"
4012 IF P0$(19,1)="Y" THEN LET V4$=V4$+", <C>=Create New Cust"
4013 LET V4$=V4$+", <F9>=quit"
4015 PRINT @(40-INT(LEN(V4$)/2),23),V4$,
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="<Return Key> for Next Firm, <N> to Edit Firm, <P> to Print Report   .",V0=6,V1=41+INT(LEN(V4$)/2),V2=22
4030 GOSUB 7000
4035 REM "Quit here
4036 IF V3=9 THEN LET QUIT=1; GOTO 4100 ELSE LET QUIT=0
4040 PRINT @(0,23),'CL',@(0,22),
4044 REM "Comments"
4045 IF V3<>1 THEN GOTO 4050
4046 LET FLAG$=""; CALL "TMC.MA",ERR=4000,A0$(1,K9),FLAG$,A0$(253,6),V3,CHAN[ALL]; REM "WA:205
4047 IF FLAG$<>"1" THEN GOTO 4049
4048 LET A0$(339,8)=FNP$(PRM$[FID,TLDATE]); REM "WA: #221
4049 IF V3=2 OR V3=3 THEN GOTO 4050 ELSE IF V3=9 THEN GOTO 4035 ELSE GOTO 4000
4050 IF P0$(19,1)="Y" AND V$="C" THEN CALL "TMC.FA",A0$,V3; IF V3=9 THEN GOTO 4035 FI; PRINT @(7,12),FNF$(A0$(253,P[0]),M0$),; GOTO 4000
4051 IF V3=2 THEN CALL "TMC.HA",A0$(1,K9),V3; IF V3=1 OR V3=3 THEN GOTO 4045 ELSE IF V3=9 THEN GOTO 4035 ELSE GOTO 4000; REM "Contacts"
4052 IF V3=3 THEN CALL "TMC.IA",A0$(1,K9),V3; IF V3=1 OR V3=2 THEN GOTO 4045 ELSE IF V3=9 THEN GOTO 4035 ELSE GOTO 4000; REM "Prev orders"
4055 LET V$=CVS(V$,2)
4060 IF V$="DELETE" THEN GOTO 4200
4065 IF V$="PRINT" OR V$="P" THEN WRITE (CHAN[9],ERR=4140)IOL=0210; CALL "TMC.JA",A0$(1,K9),A1LEN,A2LEN,A3LEN,A4LEN,CHAN[ALL]; READ (CHAN[1],KNUM=NUM(SRT$),KEY=SORTKEY$,DOM=4000); GOTO 4000
4070 IF V$<>"Y" THEN LET A0$(339,8)=FNP$(PRM$[FID,TLDATE]); PRINT @(69,6),FNB$(A0$(339,8)),; REM "WA: #221
4080 LET I0=NUM(V$,ERR=4081)-2; IF I0>50 THEN GOTO 4000 ELSE GOTO 3900
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM "WRITE"
4110 CALL "TMC.EA",A0$
4120 WRITE (CHAN[9],ERR=4140)IOL=0210
4130 GOTO 4190
4140 IF ERR<>11 THEN GOTO 9900
4150 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Duplicate Multi-Key Record. <Cr> And Change Field.",V0=1,V1=41+INT(LEN(V4$)/2),V2=22
4160 GOSUB 7000
4170 GOTO 4000
4190 IF QUIT THEN GOTO 9900 ELSE GOTO 1000
4200 REM "DELETE"
4210 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Confirm: Delete This Contact And Related Comments? <Yes/No>: ",V0=3,V1=41+INT(LEN(V4$)/2),V2=22
4220 GOSUB 7000
4225 IF V$="N" OR V$="NO" THEN GOTO 4000
4230 IF V$<>"YES" THEN GOTO 4200
4240 REMOVE (CHAN[9],KEY=A0$(1,K9),DOM=4241)
4250 READ (CHAN[2],KEY=A0$(1,K9),DOM=4251)
4260 LET K1$=KEY(CHAN[2],END=1000)
4270 IF POS(A0$(1,K9)=K1$)<>1 THEN GOTO 1000
4280 REMOVE (CHAN[2],KEY=K1$,DOM=4181)
4290 GOTO 4260
5000 REM "--- Display
5005 REM "CALL "TMC.DA",A0$
5010 PRINT @(6,3),A0$(3,20),@(33,3),A0$(23,15),@(55,3),FNF$(A0$(38,10),M8$),@(75,3),A0$(48,4),@(7,4),A0$(52,30),@(55,4),FNF$(A0$(379,10),M8$),
5020 PRINT @(7,5),A0$(82,30),@(49,5),A0$(285,3),@(69,5),FNB$(A0$(331,8)),@(7,6),A0$(112,30),@(49,6),FNB$(A0$(288,8)),@(69,6),FNB$(A0$(339,8)),@(69,7),A0$(348,4),
5030 PRINT @(7,7),A0$(142,30),@(7,8),A0$(172,30),@(69,8),A0$(353,4),@(49,9),A0$(307,3),@(69,9),A0$(360,1),
5035 PRINT @(49,7),A0$(296,3),@(49,8),FNB$(A0$(299,8))
5040 PRINT @(7,9),A0$(202,25),@(49,11),FNB$(A0$(315,8)),@(7,10),A0$(227,2),@(16,10),FNZ$(A0$(229)),@(69,11),FNB$(A0$(361,8)),@(49,10),A0$(311,4),@(69,10),A0$(358,2),@(69,11),FNB$(A0$(361,8)),
5050 PRINT @(7,11),A0$(238,15),@(49,12),FNB$(A0$(323,8)),@(69,12),A0$(369,10)
5055 DIM Y0$(26); FIND (CHAN[8],KEY=N0$+"A"+A0$(267,2),DOM=5056)Y0$
5060 PRINT @(7,12),FNF$(A0$(253,P[0]),M0$),@(27,12),A0$(267,2),@(49,13),FNB$(A0$(269,8)),@(69,13),FNB$(A0$(277,8)),
5065 PRINT @(7,13),A0$(260,3),@(27,13),A0$(264,2)
5100 REM "--- Display User Defined Fields
5110 LET X0=8,Y0=L1,X=1
5111 LET X0=14
5120 GOSUB 6400; IF V3$<>"" THEN LET Y$=STR(NUM(Y$):V3$)
5130 IF POS(X0$(1,1)="SDYN")=0 THEN GOTO 5230
5180 PRINT @(X0,Y0),Y$,
5200 REM "--- Next Field
5210 LET Y0=Y0+1
5220 IF Y0>L9 THEN LET Y0=L1,X0=X0+40
5230 LET X=X+1
5240 IF X<=X9 THEN GOTO 5120
5250 RETURN 
5260 REM "--- Display Code Info
5270 PRINT @(0,21),'CL',@(10,21),LINE$
5290 RETURN 
5400 REM "--- CSR Code (was 3200)
5405 IF POS(" "<>A0$(307,3))=0 THEN GOTO 3900
5410 LET V0$="S",V1$="C",V2$=A0$(311,4),V3$="",V4$="Enter A Valid CSR Code (<F3>=Lookup)",V0=4,V1=49,V2=10,V5$="A"
5420 GOSUB 7000
5430 IF V3<>3 THEN GOTO 5450
5435 GOSUB 11700
5440 IF V$="" THEN GOTO 5400
5450 LET V$=V$+"    ",V$=V$(1,4)
5460 FIND (CHAN[6],KEY=N0$+V5$+V$,DOM=5400)X0$
5470 LET LINE$="CSR Code: "+CVS(X0$(8,20),2); GOSUB 5260
5480 LET A0$(310,5)=V5$+V$
5485 PRINT @(V1,V2),A0$(311,4)
5490 GOTO 3900
5500 REM "--- Send Lit?
5520 LET V0$="Y",V1$="C",V2$=A0$(360,1),V3$="",V4$="Cue This Contact For Literature Mailing?",V0=1,V1=69,V2=9
5540 GOSUB 7000
5570 REM " IF V$="N" THEN LET A0$(261,8)="", A0$(358,2)=""; PRINT @(69,9),"  ",@(69,10),"        ",
5580 LET A0$(360,1)=V$
5590 GOTO 3900
5600 REM "--- Lit Date
5610 IF A0$(360,1)="N" THEN GOTO 3900
5620 LET V0$="D",V1$="C6",V2$=FNU$(A0$(361,8)),V3$="",V4$="Date To Send Literature",V0=8,V1=69,V2=10
5640 GOSUB 7000
5680 LET A0$(361,8)=FNP$(V$)
5690 GOTO 3900
5700 REM "--- Dear
5720 LET V0$="S",V1$="C",V2$=A0$(369,10),V3$="",V4$="Enter The Salutation/Title For Beginning Letters",V0=10,V1=69,V2=12
5740 GOSUB 7000
5780 LET A0$(369,10)=V$
5790 GOTO 3900
5800 REM "--- Last Invoice Date
5820 LET V0$="D",V1$="C6",V2$=FNU$(A0$(277,8)),V3$="",V4$="Enter The Last Invoice Date",V0=6,V1=69,V2=13
5840 GOSUB 7000
5850 REM "IF V3<>1 THEN GOTO 05875
5860 REM "LET V$=PRM$[FID,TLDATE]; REM "WA: #221
5870 REM "CALL "TMC.BA",V$,P0$(7,3)
5875 IF V$<>"" THEN PRINT @(V1,V2),FNB$(FNP$(V$)),
5880 LET A0$(277,8)=FNP$(V$)
5890 GOTO 3900
5900 REM "--- First Invoice Date
5920 LET V0$="D",V1$="C6",V2$=FNU$(A0$(269,8)),V3$="",V4$="Enter The First Invoice Date",V0=6,V1=49,V2=13
5940 GOSUB 7000
5975 IF V$<>"" THEN PRINT @(V1,V2),FNB$(FNP$(V$)),
5980 LET A0$(269,8)=FNP$(V$)
5990 GOTO 3900
6000 REM "--- Routines for User Defined Fields
6020 LET X0=8,Y0=L1,X=1,SKIP=0
6021 LET X0=14
6030 REM IF I0>23 THEN LET SKIP=I0-23,I0=23
6050 REM "--- Set Correct Element
6060 GOSUB 6400
6070 IF POS(X0$(1,1)="SDYN")=0 THEN IF V3=2 THEN GOTO 6350 ELSE GOTO 6250
6080 IF SKIP>0 THEN LET SKIP=SKIP-1; GOTO 6200
6100 REM "--- User Defined Field Entry
6120 LET V0$=X0$(1,1),V1$="C6",V2$=Y$,V1=X0,V2=Y0
6140 GOSUB 7000
6160 LET Y$=V$
6170 GOSUB 6450
6180 IF V3=4 THEN GOTO 3900
6190 IF V3=2 THEN GOTO 6300
6200 REM "--- Next User Defined Field
6210 LET Y0=Y0+1
6220 IF Y0>L9 THEN LET Y0=L1,X0=X0+40
6250 LET X=X+1
6260 IF X>X9 THEN GOTO 4000
6290 GOTO 6050
6300 REM "--- Previous User Defined Field
6310 LET Y0=Y0-1
6320 IF Y0<L1 THEN LET Y0=L9,X0=X0-29
6350 LET X=X-1
6360 IF X<1 THEN GOTO 3900
6390 GOTO 6050
6400 REM "--- Get User Defined Data Elements
6405 LET V0=25
6410 LET Y$="",X0$=P1$((X-1)*(X8+1)+1,X8+1),V4$="",V3$=""
6420 IF X0$(1,1)="S" THEN LET Y=INT(POS(STR(X:"00")=X1$,2)/2),Y$=A1$(Y*25+1,V0),V4$="Enter Up To "+STR(V0)+" Characters"
6425 IF X0$(1,1)="D" THEN LET Y=INT(POS(STR(X:"00")=X2$,2)/2),Y$=FNU$(A2$(Y*8+1,8)),V0=8,V4$="Enter A Date"
6430 IF X0$(1,1)="Y" THEN LET Y=INT(POS(STR(X:"00")=X3$,2)/2),Y$=A3$(Y+1,1),V4$="Y=Yes, N=No",V0=1
6435 IF X0$(1,1)="N" THEN LET Y=INT(POS(STR(X:"00")=X4$,2)/2),Y$=A4$(Y*10+1,10),V0=M1,V3$=M1$
6440 IF POS(" "<>Y$)=0 THEN LET Y$=""
6445 RETURN 
6450 REM "--- Store User Defined Data Elements
6460 IF X0$(1,1)="S" THEN LET A1$(Y*25+1,25)=Y$
6465 IF X0$(1,1)="D" THEN LET A2$(Y*8+1,8)=FNP$(Y$)
6470 IF X0$(1,1)="Y" THEN LET A3$(Y+1,1)=Y$
6475 IF X0$(1,1)="N" THEN LET A4$(Y*10+1,10)=Y$
6490 RETURN 
6600 REM "--- Change Sort
6605 DIM SORT$[4]; LET SORT$[0]="Cont",SORT$[1]="Firm",SORT$[2]=CVS(P2$(1,20),2),SORT$[3]=CVS(P2$(21,20),2),SORT$[4]=CVS(P2$(41,20),2)
6610 IF P0$(16,1)="Y" THEN LET SORT$[0]="Firm",SORT$[1]="Name"
6615 LET SORTS$="0="+SORT$[0]+", 1="+SORT$[1]+", 2="+SORT$[2]+", 3="+SORT$[3]+", 4="+SORT$[4]
6620 IF LEN(SORTS$)<80 THEN PRINT @(40-INT(LEN(SORTS$)/2),23),SORTS$, ELSE PRINT @(0,23),SORTS$(1,79),
6630 LET V0$="S",V1$="C",V2$=SRT$,V3$="01234",V4$="Enter Sort Sequence:",V0=1,V1=41+INT(LEN(V4$)/2),V2=22
6635 GOSUB 7000
6640 PRINT @(0,23),'CL',@(0,22),
6645 IF V3=4 THEN GOTO 6690
6650 LET SRT$=V$
6660 LET V0$="S",V2$="",V3$="",V4$="Enter "+SORT$[NUM(SRT$)]+" To Start: ",V0=11,V1=41+INT(LEN(V4$)/2),V2=22
6670 GOSUB 7000
6680 READ (CHAN[1],KEY=N0$+V$,KNUM=NUM(SRT$),DOM=6681)
6690 RETURN 
6700 REM " --- Build Contact From Contact Lookup Field"
6710 LET X0$="",X1$=A0$(23,15)
6720 IF FNW$(X1$)="" THEN GOTO 6790
6735 LET X=POS(" "=X1$),Y=POS(","=X1$)
6740 IF X=0 AND Y=0 THEN GOTO 6790
6745 IF X>Y THEN LET X0$=FNW$(X1$(X))+" "+FNW$(X1$(1,X)); GOTO 6790
6790 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8005 REM "-- Special Date functions for YYYYMMDD Dates: FNU=Unpack (trims Century), FNP=Pack (add Century), FNB=Display YYYYMMDD as MM/DD/YY
8010 DEF FNA$(Q$,Q2$)=STR((ASC(Q$)-32)*POS(" "<>Q2$(2,1)):"00")
8020 DEF FNB$(Q1$)=Q1$(5,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(7,2)+CHR(32+15*(Q1$(5,1)<>" "))+Q1$(3,2)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(NUM(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE$(Q$)=STR(INT(TIM):"00")+":"+STR(FPT(TIM)*60:"00")
8060 DEF FNF$(Q$,Q1$)=STR(-NUM(Q$):Q1$)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8080 DEF FNH(Q$)=ASC(Q$)-32
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8090 DEF FNV$(Q)=CHR(Q+32)
8094 DEF FNP$(Q1$)="19"+Q1$
8096 DEF FNU$(Q1$)=Q1$(5,4)+Q1$(3,2)
8099 DEF FNW$(Q$)=Q$(1,POS(" "<>Q$,-1))
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Exit"
9910 FOR I=1 TO 12
9920 CLOSE (CHAN[I],ERR=9921)
9930 NEXT I
9940 PRINT (0,ERR=9941)'POP',
9960 LET X$=STBL("TMMCMA","")
9979 REM "Fail enters here
9980 PRINT (0,ERR=9951)'CURSOR'("ON"),
9990 EXIT 
10000 REM " --- Have we been called by ourselves?"
10010 LET FAIL=1,X$=""
10020 LET X$=STBL("TMMCMA",ERR=10021)
10030 IF X$="IN TMMCMA" THEN GOTO 10090
10040 LET X$=STBL("TMMCMA","IN TMMCMA")
10050 LET FAIL=0
10090 RETURN 
11000 REM " --- Customer Type Lookup
11010 LET NMBR=1
11020 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11030 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3
11040 LET DESCRIPT$[1]="Customer Type",FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
11050 CALL "SYC.LK",CHAN[8],"L",NMBR,"Customer Type Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11090 RETURN 
11100 REM " --- Discount Code Lookup
11110 LET NMBR=1
11120 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11130 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
11140 LET DESCRIPT$[1]="Sales Discount",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
11150 CALL "SYC.LK",CHAN[8],"I",NMBR,"Discount Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11190 RETURN 
11200 REM " --- Terms Code Lookup
11210 LET NMBR=1
11220 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11230 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
11240 LET DESCRIPT$[1]="Terms",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
11250 CALL "SYC.LK",CHAN[8],"A",NMBR,"Terms Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11290 RETURN 
11300 REM " --- Territory Code Lookup
11310 LET NMBR=1
11320 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11330 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3
11340 LET DESCRIPT$[1]="Territory",FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
11350 CALL "SYC.LK",CHAN[8],"H",NMBR,"Territory Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11390 RETURN 
11400 REM " --- Source Code Lookup
11410 LET NMBR=1
11420 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11430 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=4
11440 LET DESCRIPT$[1]="Lead Source",FIELD[1]=1,POSITION[1]=8,LENGTH[1]=20
11450 CALL "SYC.LK",CHAN[6],"B",NMBR,"Source Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11490 RETURN 
11500 REM " --- Interest Code Lookup
11510 LET NMBR=1
11520 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11530 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=4
11540 LET DESCRIPT$[1]="Interest",FIELD[1]=1,POSITION[1]=8,LENGTH[1]=20
11550 CALL "SYC.LK",CHAN[6],"C",NMBR,"Interest Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11590 RETURN 
11600 REM " --- Literature Code Lookup
11610 LET NMBR=1
11620 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11630 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
11640 LET DESCRIPT$[1]="Liturature",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
11650 CALL "SYC.LK",CHAN[6],"D",NMBR,"Liturature Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11690 RETURN 
11700 REM " --- Customer Service Request Code Lookup
11710 LET NMBR=1
11720 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
11730 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=4
11740 LET DESCRIPT$[1]="Customer Service Request",FIELD[1]=1,POSITION[1]=8,LENGTH[1]=20
11750 CALL "SYC.LK",CHAN[6],"A",NMBR,"Customer Service Request Code Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
11790 RETURN 
62000 REM " --- Set-up ADD+ON Environment"
62010 DIM PRM$[0]; CALL "ADC.01",PRM$[ALL],STATUS
62020 IF STATUS<>0 THEN EXITTO 9900
62030 LET FID=0,AP=1,AR=2,BM=3,DC=4,FA=5,GL=6,IV=7,MP=8,OP=9,PO=10,PR=11,SA=12,SH=13,SY=14,WO=15,TS=21,TB=22,DC=23,V2=24,EF=25; REM " ---- Module Id Numbers"
62040 DIM J$(80,"-"),J0$(40,"0"),J1$(80)
62050 LET INSTALL=0,KEYLEN=1,KEYMAX=2,KEYMASK=3,CPERIOD=4,CYEAR=5,DMASK=6,UMASK=7,RMASK=8,TWO=9,THREE=10,FOUR=11,EMASK=12,TOPCODE=1,TDATE=2,TMENU=3,TPRINTER=4,TSECURE=5,TFIRM=6,TPMODE=7,TPROG=8,TPOPTS=9,TFNAME=10,TPNAME=11,TLDATE=12
62090 RETURN 
65000 END

0010 REM "GLU - Account Allocation Update"
0020 REM "Program GLU.IA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLT-04",FILES$[2]="GLT-05"
0140 LET FILES$[3]="GLM-03",FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLT04_DEV=CHANNELS[1],GLT05_DEV=CHANNELS[2]
0190 LET GLM03_DEV=CHANNELS[3],SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLIST's"
0210 GLE03A: IOLIST A0$(1),A1$,A[0],A[1],A[2],A[3],A[4]
0220 GLE13A: IOLIST B0$(1),B1$(1),B2$,B[0],B[1],B[2],B[3],B[4]
0230 GLM03A: IOLIST C0$(1),C1$,C[0],C[1],C[2]
0500 REM " --- Initializations"
0510 DIM A0$(2+P[4]),A[4],B0$(5+P[4]),B1$(P[4]),B[4],C0$(32),C[2]
0520 DIM GLCONTROL$(640)
0550 LET GLCONTROL$(1)=STBL("!GLCONTROL",ERR=9900)
0700 REM " --- Background"
0710 PRINT @(0,10),'CL',@(0,11),'CL',@(0,12),'CL',
0750 CALL "SYC.NB","Updating",13,COLUMN
0800 REM " --- Options"
0810 LET V4$="Are You Ready to Update the Account Allocation Register"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0850 REM " --- Disallow 'M'enu option in Error Routine
0860 LET EXIT_CTRL=1
0900 REM " --- Position File"
0910 CALL "SYC.PA",SYS01_DEV,NUM(BEGPER$),FNYY_YEAR(BEGYEAR$),X$,WHEN$,STATUS
0920 LET A0$(1)=N0$,C0$(1)=N0$+GLCONTROL$(18,2)
0930 FIND (GLM03_DEV,KEY=C0$(1,4))IOL=GLM03A
0940 LET MEMO$=C0$(5,20),REF3$=""
0990 READ (GLE03_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next GLE-03 header record"
1010 READ (GLE03_DEV,END=4000)IOL=GLE03A
1020 IF POS(N0$=A0$)<>1 THEN GOTO 4000
1030 PRINT @(COLUMN,11),FNF$(A0$(3,P[3]),M0$),
1100 REM " --- Post header record"
1110 LET ACCOUNT$=A0$(3,P[4]),AMOUNT=-A[0],UNITS=-A[1]
1120 LET REF1$="Source",REF2$="",REF3$=""
1190 GOSUB GLPOST
1900 REM " --- Position GLE-13"
1990 READ (GLE13_DEV,KEY=A0$,DOM=2000)
2000 REM " --- Read next GLE-13 detail record"
2010 LET K$=KEY(GLE13_DEV,END=3000)
2020 IF POS(A0$=K$)<>1 THEN GOTO 3000
2030 READ (GLE13_DEV)IOL=GLE13A
2100 REM " --- Post detail record"
2110 LET ACCOUNT$=B1$,REF1$="Target",REF2$=FNF$(A0$(3,P[3]),M0$)
2120 LET REF3$="Line "+B0$(K8+1,3),AMOUNT=B[1],UNITS=B[2]
2190 GOSUB GLPOST
2800 REM " --- Remove detail record"
2810 REMOVE (GLE13_DEV,KEY=B0$)
2900 REM " --- Loop back for next detail record"
2990 GOTO 2000
3000 REM " --- Remove header record"
3010 REMOVE (GLE03_DEV,KEY=A0$)
3900 REM " --- Loop back for next header"
3990 GOTO 1000
4000 REM " --- All done"
4010 CALL "GLC.CA",STATUS
4020 CALL "SYC.BB",STATUS
4090 GOTO 9900
6900 REM " --- Standard G/L Posting Routine"
6910 GLPOST: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,WHEN$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

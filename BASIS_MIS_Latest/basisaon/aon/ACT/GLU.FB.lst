0010 REM "GLU - Create Budget Revision (Generation Overlay)"
0020 REM "Program GLU.FB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0220 GLM02A: IOLIST X0$(1),A[ALL],U[ALL]
0500 REM " --- Initializations"
0510 DIM X0$(3+P[4]),A[MP],U[MP]
0520 LET ROUND=10^POS(C1$(61,1)="D-HT")/10
0530 LET WILDCARD$=FNP$(C4$)
0590 LET K$=N0$+C2$
0700 REM " --- Background"
0750 CALL "SYC.NB","Processing",M0,COLUMN
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
0980 READ (GLM01_DEV,KEY=K$,DOM=1000)
0990 GOTO 1100
1000 REM " --- Get next GLM-01 key"
1010 LET K$=KEY(GLM01_DEV,END=4000)
1100 REM " --- Valid key?"
1110 IF POS(N0$=K$)<>1 THEN GOTO 4000
1120 IF FNP$(C3$)<>"" THEN IF K$(3,P[3])>C3$(1,P[3]) THEN GOTO 4000
1150 READ (GLM01_DEV,KEY=K$)
1160 GOSUB 6800; IF STATUS>0 THEN GOTO 3900
1190 PRINT @(COLUMN,11),FNF$(K$(3,P[3]),M0$),
1500 REM " --- Retrieve source GLM-02 record"
1510 DIM X0$(3+P[4]),A[MP],U[MP]
1520 LET X0$(1)=K$,X0$(3+P[4],1)=C1$(58,1)
1530 FIND (GLM02_DEV,KEY=X0$,DOM=1540)IOL=GLM02A
2000 REM " --- Amounts or Units?"
2010 DIM B[MP]
2020 FOR X=0 TO P[0]
2030 IF C0$(4,1)="A" THEN LET B[X]=A[X]
2040 IF C0$(4,1)="U" THEN LET B[X]=U[X]
2050 IF ROUND>=1 THEN LET B[X]=ROUND*INT(B[X]/ROUND+.5*SGN(B[X]))
2060 NEXT X
2500 REM " --- Generate budget number"
2510 FOR X=0 TO P[0]
2520 IF C1$(59,1)="A" THEN LET B[X]=B[X]+C[0]
2530 IF C1$(59,1)="P" THEN LET B[X]=B[X]+B[X]*C[0]/100
2540 NEXT X
3000 REM " --- Write budget record"
3010 DIM A[MP],U[MP]
3020 LET X0$(3+P[4],1)=C0$(3,1)
3030 FIND (GLM02_DEV,KEY=X0$,DOM=3050)IOL=GLM02A
3040 IF C1$(60,1)<>"Y" THEN GOTO 3900
3050 FOR X=0 TO P[0]
3060 IF C0$(4,1)="A" THEN LET A[X]=B[X]
3070 IF C0$(4,1)="U" THEN LET U[X]=B[X]
3080 NEXT X
3090 WRITE (GLM02_DEV,KEY=X0$)IOL=GLM02A
3900 REM " --- Loop back for next GLM-01 record"
3990 GOTO 1000
4000 REM " --- All Done"
4090 GOTO 9900
6800 REM " --- Test account number against specified wildcard mask"
6810 LET STATUS=0
6820 IF WILDCARD$="" THEN GOTO 6890
6825 IF WILDCARD<>LEN(WILDCARD$) THEN LET WILDCARD=LEN(WILDCARD$)
6830 LET X$=FNF$(K$(3,P[3]),M0$)
6840 FOR X=1 TO WILDCARD
6850 IF WILDCARD$(X,1)="?" THEN GOTO 6880
6860 IF X$(X,1)<>WILDCARD$(X,1) THEN LET STATUS=1
6880 NEXT X
6890 RETURN 
8000 REM " --- Functions"
8090 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9700 REM " --- Position file"
9900 RUN "SYS.AA"
9999 END

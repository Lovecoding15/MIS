0010 REM "APE - PAYMENT SELECTION ENTRY/MULTIPLE SELECTION"
0020 REM "Program APE.ED"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0240 APE04A: IOLIST X0$,X[ALL]
0250 APM01A: IOLIST C0$,C1$(1)
0300 APT01A: IOLIST A0$,A[ALL]
0310 APT11A: IOLIST W0$,W1$,W[ALL]
0700 REM " --- Invoices
0705 IF P9$="5" THEN IF P5$="E" THEN LET V$="N"; GOTO 0750
0710 CALL "SYC.YN",0,"Do You Want To Take Overdue Discounts",4,V$,V3
0725 PRINT @(0,3),'CL',
0730 IF V3=4 THEN GOTO 9900
0740 IF POS(V$="YN")=0 THEN GOTO 0700
0750 LET Q5$=V$,Q6$=F0$(7,3)
0780 PRINT @(0,3),'CL','SB',@(29,3),"Selecting:",'SF',
0790 IF P9$="5" THEN IF P5$="E" THEN PRINT 'SB',@(29,3)," Removing:",'SF',
1000 REM " --- Init File
1010 READ (APT01_DEV1,KEY=N0$,DOM=1020)
1020 IF P9$="5" THEN READ (APT01_DEV1,KEY=N0$+P6$+P8$,DOM=1030)
1040 IF P9$<>"5" AND P3$(1,1)="Y" AND P6$<>"" THEN READ (APT01_DEV1,KEY=N0$+P6$,DOM=1050)
1100 REM " --- Main Read
1120 LET K0$=KEY(APT01_DEV1,END=4000)
1140 IF K0$(1,2)<>N0$ THEN GOTO 4000
1160 IF P9$="5" THEN IF K0$(1,10)<>N0$+P6$+P8$ THEN GOTO 4000
1170 READ (APT01_DEV1)IOL=APT01A
1182 IF A0$(5,6)="TEMP  " THEN GOTO 1200
1185 DIM C1$(196)
1190 READ (APM01_DEV,KEY=N0$+A0$(5,6),DOM=1100)IOL=APM01A
1195 IF C1$(159,1)="Y" THEN GOTO 1100
1200 REM " --- AP Type
1210 IF P3$(1,1)<>"Y" THEN GOTO 1290
1220 IF P6$="" THEN GOTO 1290
1240 IF A0$(3,2)<>P6$ THEN GOTO 4000
1290 ON POS(P9$="123456") GOTO 1290,2000,1300,1400,1600,1800,2000
1300 REM " --- Due Date
1320 IF A0$(35,3)<P7$ OR A0$(35,3)>P8$ THEN GOTO 1100
1340 GOSUB 6000
1360 GOSUB 6400
1390 GOTO 1100
1400 REM " --- Discount Date
1420 IF A0$(38,3)<P7$ OR A0$(38,3)>P8$ THEN GOTO 1100
1440 GOSUB 6000
1460 GOSUB 6400
1490 GOTO 1100
1600 REM " --- Payment Group
1620 IF A0$(25,2)<>P8$ THEN GOTO 1100
1640 GOSUB 6000
1660 GOSUB 6400
1690 GOTO 1100
1800 REM " --- Vendor
1810 IF P5$="E" THEN GOTO 1900
1820 GOSUB 6000
1840 GOSUB 6400
1890 GOTO 1100
1900 REM " --- Remove
1920 PRINT @(40,3),K0$(1,20),
1940 REMOVE (APE04_DEV,KEY=K0$(1,20),DOM=1950)
1990 GOTO 1100
2000 REM " --- All Invoices
2020 GOSUB 6000
2040 GOSUB 6400
2090 GOTO 1100
4000 REM " --- End
4010 PRINT @(0,3),'CL',
4020 IF P9$="5" THEN RUN "APE.EC"
4040 RUN "APE.EB"
6000 REM " --- Get Open Invoice Amount
6010 DIM X0$(60),X[3]
6020 LET X[0]=A[0],X[1]=A[1],X[2]=A[2],X[3]=A[0],X0$=A0$(1,20)+A0$(29,3)+A0$(35,6)+A0$(41,26)
6090 READ (APT11_DEV,KEY=K0$(1,20),DOM=6100)
6100 LET K$=KEY(APT11_DEV,END=6300)
6110 IF K$(1,20)<>K0$(1,20) THEN GOTO 6300
6120 READ (APT11_DEV)IOL=APT11A
6140 IF W1$(1,1)=" " THEN GOTO 6190
6150 LET X[0]=X[0]+W[0],X[1]=X[1]+W[1],X[2]=X[2]+W[2]
6160 IF POS(W1$(1,1)="A")<>0 THEN LET X[3]=X[3]+W[0]
6200 GOTO 6100
6300 RETURN 
6400 REM " --- Write Selection
6405 IF A0$(67,1)="Y" THEN GOTO 6590
6407 IF Q5$="N" THEN IF A0$(38,3)<Q6$ THEN LET X[1]=0
6410 IF X[0]<=0 THEN GOTO 6460
6430 IF X[1]>X[0] THEN LET X[1]=X[0]
6440 IF X[2]>X[0] THEN LET X[2]=X[0]
6450 GOTO 6500
6460 IF X[1]>0 THEN LET X[1]=0
6470 IF X[1]<X[0] THEN LET X[1]=X[0]
6480 IF X[2]<X[0] THEN LET X[2]=X[0]
6500 IF X[0]-X[2]=0 THEN GOTO 6590
6505 IF X[1]=0 AND P9$="3" THEN GOTO 6590
6510 WRITE (APE04_DEV,KEY=X0$(1,20),DOM=6590)IOL=APE04A
6520 PRINT @(40,3),K0$(1,20),
6590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

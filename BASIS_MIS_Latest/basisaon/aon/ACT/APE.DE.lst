0010 REM "APE - Recurring Invoice Entry - Detail"
0020 REM "Program APE.DE"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM "OPEN/LOCK FILES"
0200 REM "IOLISTS"
0210 APE13A: IOLIST W0$,W1$,W2$(1),W[0],W[1]
0230 GLM01A: IOLIST X0$,X1$
0400 REM "PARAMETERS"
0500 REM "INIT DATA"
0520 LET W0$(1)=A0$(1,20),W1$(1)=C2$,W[0]=A[0]
0700 REM "BACKGROUND"
0720 PRINT 'SB',@(46,12),"Dist Balance: ",J1$(1,M1),'SF',
0800 REM "OPTIONS"
0820 GOSUB 5100
0840 GOSUB 6000
1000 REM "CHECK LINE"
1020 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L0),'LD',
1040 IF L>L9 THEN LET L=L9
1050 IF W1=0 THEN LET W1=1
1100 REM "GET NEXT KEY"
1120 LET K$=KEY(APE13_DEV,END=1400)
1130 IF LEN(K$)<20 THEN GOTO 1400
1140 IF K$(1,20)<>A0$(1,20) THEN GOTO 1400
1150 READ (APE13_DEV)IOL=APE13A
1160 GOTO 1490
1400 REM "SEQ"
1405 IF V3=2 THEN GOTO 1420
1410 LET W0=NUM(W0$(21,2))+1
1420 LET V0$="Z",V1$="KER",V2$=STR(W0),V3$="",V0=2,V1=0,V2=L,I0=0
1425 PRINT @(0,V2),'CL',
1430 GOSUB 7000
1440 IF V3=4 THEN PRINT @(0,L),"  "; GOTO 4000
1450 IF V$="00" THEN GOTO 1400
1460 LET W0$(21,2)=V$
1480 FIND (APE13_DEV,KEY=W0$(1,22),DOM=1600)IOL=APE13A
1490 IF W0$(1,22)<=K9$ THEN GOSUB 5400
1500 GOSUB 5000
1510 LET W9=-W[0]
1530 GOTO 3000
1600 REM "SETUP"
1610 LET W9=0
2000 REM "ACCOUNT"
2010 IF GL$<>"Y" THEN GOTO 2090
2020 LET V0$="M",V1$="RC",V2$=W1$(1,G[3]),V3$=G1$,V0=G[4],V1=5,V2=L,I0=1
2025 LET V4$="Enter A Valid G/L Account, <F3>=Lookup"
2030 GOSUB 7000
2035 IF V3=2 THEN GOTO 1400
2040 IF V3<>3 THEN GOTO 2050
2044 CALL "SYC.LG",V$
2046 IF V$="" THEN GOTO 2000
2050 LET W1$=V$
2060 FIND (GLM01_DEV,KEY=N0$+W1$,DOM=2000)IOL=GLM01A
2070 PRINT @(5,L),FNF$(W1$(1,G[3]),G5$),
2080 IF P3$(16,1)<>"Y" THEN PRINT @(6+LEN(G5$),L),X1$(1,35),
2085 IF P3$(16,1)="Y" THEN PRINT @(6,L+1),'SB',X1$(1,35),'SF','CL',
2090 IF P3$(16,1)="Y" THEN GOSUB 2400
2095 IF V3=2 THEN IF GL$="Y" THEN GOTO 2000 ELSE GOTO 1400
2100 REM "AMOUNT"
2110 IF W[0]=0 THEN LET W[0]=A[0]-T0
2120 LET V0$="N",V2$=STR(W[0]),V3$=M1$,V0=0,V1=50,V2=L
2130 GOSUB 7000
2140 LET W[0]=V
2150 IF V3=3 THEN LET W[0]=-W[0]
2220 PRINT @(V1,V2),W[0]:M1$,
2230 IF V3=2 THEN GOTO 2090
2290 GOTO 2320
2300 REM "UNITS"
2310 IF G4$(3,1)<>"Y" THEN GOTO 2100
2320 IF G4$(3,1)<>"Y" THEN GOTO 3000
2330 IF GL$<>"Y" THEN GOTO 3000
2340 LET V0$="N",V2$=STR(W[1]),V3$=G7$,V0=0,V1=65,V2=L
2350 GOSUB 7000
2360 LET W[1]=V
2370 IF V3=2 THEN GOTO 2100
2390 GOTO 3000
2400 REM "MISCELLANEOUS
2420 LET V0$="S",V1$="",V2$=W2$(1),V0=10,V1=25,V2=L
2435 GOSUB 7000
2440 IF V3=2 THEN GOTO 2490
2450 LET W2$(1)=V$
2490 RETURN 
3000 REM "CORRECT"
3020 LET V0$="S",V1$="C^",V2$="",V3$="",V0=1,V1=77,V2=L
3025 LET V4$="<Enter>=OK, <F2>=Change, <D>=Delete"
3030 GOSUB 7000
3040 IF V3=2 THEN IF GL$="N" THEN GOTO 2100 ELSE GOTO 2300
3060 IF V$="" THEN GOTO 3100
3080 IF V$="D" THEN GOTO 3200
3090 GOTO 3000
3120 WRITE (APE13_DEV,KEY=W0$(1,22))IOL=APE13A
3130 LET T0=T0+W9+W[0],W[0]=0,W[1]=0
3135 PRINT @(60,12),A[0]-T0:M1$,
3140 LET L=L+1,K9$=W0$(1,22),W9$=""
3150 PRINT @(0,L),'CL',
3160 IF V3=4 THEN GOTO 4000
3190 GOTO 1000
3200 REMOVE (APE13_DEV,KEY=W0$(1,22),DOM=3210)
3210 LET T0=T0+W9
3215 PRINT @(60,12),A[0]-T0:M1$,
3220 PRINT @(0,L),'CL',@(0,L+1),'CL',
3290 GOTO 1000
4000 REM "OPTIONS"
4010 LET V0$="S",V1$="KC^",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/Review/List/Delete)? ",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4035 IF V$="R" OR V$="Review" THEN LET V$="REVIEW" ELSE IF V$="L" OR V$="List" THEN LET V$="LIST"
4040 IF V$="REVIEW" THEN GOTO 4300
4050 IF V$="LIST" THEN GOTO 4400
4060 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM "CHECK BALANCE"
4120 IF T0=A[0] THEN RUN "APE.DB"
4130 PRINT @(8,22),"*** This Transaction Is Out Of Balance By ",T0-A[0]:M1$," <Enter>: ",'RB',
4140 LET V0$="S",V1$="C",V2$="",V3$="",V4$="",V0=1,V1=58+LEN(M1$),V2=22
4150 GOSUB 7000
4160 GOTO 1000
4200 REM "DELETE"
4220 READ (APE13_DEV,KEY=A0$(1,20),DOM=4230)
4230 LET K$=KEY(APE13_DEV,END=4270)
4240 IF K$(1,20)<>A0$(1,20) THEN GOTO 4270
4250 REMOVE (APE13_DEV,KEY=K$)
4260 GOTO 4230
4270 REMOVE (APE03_DEV,KEY=A0$(1,22),DOM=4271)
4280 IF A0$(5,4)="TEMP" THEN IF O1=0 THEN REMOVE (APM03_DEV,KEY=D0$,DOM=4300)
4290 RUN "APE.DB"
4300 REM "REVIEW"
4320 READ (APE13_DEV,KEY=A0$(1,22),DOM=4330)
4340 GOSUB 5400
4390 GOTO 1000
4400 REM "LIST"
4420 GOSUB 5100
4490 GOTO 4000
5000 REM "DISPLAY"
5005 PRINT @(0,L),'CL',
5007 IF GL$<>"Y" THEN GOTO 5040
5010 LET X1$="Not On File"
5020 FIND (GLM01_DEV,KEY=N0$+W1$,DOM=5040)IOL=GLM01A
5030 LET X1$=X1$(1,35),W1=1
5040 IF G4$(3,1)="Y" THEN PRINT @(65,L),W[1]:G7$,
5045 PRINT @(0,L),W0$(21,2),
5050 IF GL$="Y" THEN PRINT @(5),FNF$(W1$(1,G[3]),G5$),
5055 PRINT @(50),W[0]:M1$,
5060 IF P3$(16,1)="Y" THEN PRINT @(25,L),W2$
5070 IF P3$(16,1)<>"Y" THEN PRINT @(6+LEN(G5$),L),X1$
5090 RETURN 
5100 REM "LIST"
5110 GOSUB 5400
5120 LET L=L0
5130 READ (APE13_DEV,KEY=A0$(1,22),DOM=5140)
5140 LET K$=KEY(APE13_DEV,END=5300)
5150 IF K$(1,20)<>A0$(1,20) THEN GOTO 5300
5160 READ (APE13_DEV)IOL=APE13A
5200 IF L<=L9 THEN GOTO 5250
5205 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Continue (<Enter>/<F4>=End)? ",V0=1,V1=FNV(V4$),V2=22
5210 GOSUB 7000
5220 IF V3=4 THEN GOTO 5300
5230 PRINT @(0,L0),'CE',
5240 LET L=L0
5250 GOSUB 5000
5260 LET L=L+1,K9$=W0$(1,22),W[0]=0
5290 GOTO 5140
5300 RETURN 
5400 REM "CLEAR SCREEN"
5420 PRINT @(0,L0),'CE',
5440 LET L=L0
5490 RETURN 
6000 REM "ACCUMULATE"
6020 READ (APE13_DEV,KEY=A0$(1,22),DOM=6030)
6030 LET T0=0
6040 LET K$=KEY(APE13_DEV,END=6090)
6050 IF K$(1,20)<>A0$(1,20) THEN GOTO 6090
6060 READ (APE13_DEV)IOL=APE13A
6070 LET T0=T0+W[0]
6080 GOTO 6040
6090 PRINT @(60,12),A[0]-T0:M1$,
6095 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "ARM - CUSTOMER COMMENTS MAINTENANCE"
0020 REM "Program ARM.MK"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0310 ARM05A: IOLIST W0$(1),W1$(1)
0500 REM " --- Init"
0510 DIM W0$(10),W1$(48)
0515 LET L0=6
0520 LET W0$(1)=A0$(1,8),T0=0,L=L0,L9=20
0700 REM " --- Background"
0720 PRINT 'SB',@(15,5),"Comment Maintenance",@(0),"Seq",
0740 PRINT @(0,6),'CE','SF',
0920 GOSUB 5100
0930 IF Q1=0 THEN GOTO 1400
0940 GOTO 4000
1000 REM " --- Overflow"
1020 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L0),'LD',
1040 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1120 LET K$=KEY(ARM05_DEV,END=1400)
1140 IF K$(1,8)<>A0$(1,8) THEN GOTO 1400
1150 READ (ARM05_DEV)IOL=ARM05A
1160 GOTO 1490
1400 REM " --- Seq"
1410 LET W0=NUM(W0$(9,2))+1
1420 LET V0$="Z",V1$="KERC",V2$=STR(W0),V3$="",V0=2,V1=0,V2=L,I0=0,V4$="Enter A 2 Digit Sequence Number, (<F4>=End)"
1430 GOSUB 7000
1440 IF V3=4 THEN PRINT @(0,L),"   "; GOTO 4000
1450 IF V$="00" OR V$="" THEN GOTO 1400
1460 LET W0$(9,2)=V$
1480 FIND (ARM05_DEV,KEY=W0$(1,10),DOM=1600)IOL=ARM05A
1490 IF W0$(1,10)<=K9$ THEN GOSUB 5400
1500 GOSUB 5000
1520 LET I0=2
1530 GOTO 3300
1600 REM " --- Set-up"
1620 LET W1$(1)=""
2100 REM " --- Description"
2120 LET V0$="S",V1$="RC",V2$=W1$(1,48),V0=48,V1=7,I0=1,V4$="Enter Up To "+STR(V0)+" Characters"
2130 GOSUB 7000
2160 LET W1$(1,48)=V$
2190 GOTO 3200
3200 REM " --- Seq Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,2100,3300
3300 REM " --- Correct"
3320 LET V0$="S",V1$="C",V2$="",V0=1,V1=78,V2=L,K9$=W0$(1,10),V4$="<Enter>=Correct, D=Delete"
3330 GOSUB 7000
3340 IF V3=2 THEN GOTO 3200
3350 IF V$="" THEN GOTO 3400
3360 IF V$="D" THEN GOTO 3700
3370 IF V$="M" THEN GOTO 3800
3380 GOTO 3300
3400 REM " --- Write"
3420 WRITE (ARM05_DEV,KEY=W0$)IOL=ARM05A
3450 LET L=L+1
3460 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3700 REM " --- Delete"
3720 REMOVE (ARM05_DEV,KEY=W0$,DOM=3730)
3750 PRINT @(0,L),'CL',
3760 IF V3=4 THEN GOTO 4000
3790 GOTO 1000
4000 REM " --- Options"
4005 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N/List/Review)? ",V0=1,V1=67,V2=22
4010 GOSUB 7000
4020 IF V$="L" OR V$="LIST" THEN GOTO 4400
4040 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4060 IF V$="" THEN LET V$="Y"
4070 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Overlay"
4190 RUN "ARM.MA"
4400 REM " --- List"
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4620 READ (ARM05_DEV,KEY=A0$(1,8),DOM=4630)
4640 GOSUB 5400
4690 GOTO 1000
5000 REM " --- Display"
5020 PRINT @(0,L),'CL',W0$(9,2),@(7),W1$(1,48),
5030 LET Q1=1
5090 RETURN 
5100 REM " --- List"
5105 LET Q1=0
5110 GOSUB 5400
5120 READ (ARM05_DEV,KEY=A0$(1,8)+"  ",DOM=5130)
5130 LET K$=KEY(ARM05_DEV,END=5300)
5150 IF K$(1,8)<>A0$(1,8) THEN GOTO 5300
5160 READ (ARM05_DEV)IOL=ARM05A
5200 IF L<=L9 THEN GOTO 5250
5205 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="<Enter>=Continue?  <F4>=End",V0=1,V1=50,V2=22
5210 GOSUB 7000
5220 IF V3=4 THEN GOTO 5300
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+1,K9$=A0$(1,8)
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5420 PRINT @(0,L0+1),'CE',
5440 LET L=L0
5490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 RUN "ARM.MA"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

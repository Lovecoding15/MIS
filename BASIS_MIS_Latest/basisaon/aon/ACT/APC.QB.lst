0010 REM "APC - Vendor SpeedSearch (Open Invoices)"
0020 REM "Program APC.QB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APM-01",FILES$[2]="APM-02",FILES$[3]="APM-10"
0135 LET FILES$[4]="APT-01",FILES$[5]="APT-11"
0140 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET APM01_DEV=CHANNELS[1],APM02_DEV=CHANNELS[2],APM10_DEV=CHANNELS[3]
0165 LET APT01_DEV=CHANNELS[4],APT11_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 APT01A: IOLIST A0$(1),A[ALL]
0220 APM01A: IOLIST B0$(1),B1$(1)
0230 APM10A: IOLIST APDESC$(1)
0260 APS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0310 APT11A: IOLIST W0$(1),W1$(1),W[ALL]
0400 REM " --- Parameters"
0410 DIM P[2],X$(128)
0420 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2),MAXPAGE=NUM(X$(66,2))
0425 LET TNAME$=FNP$(X$(3,8)),ONAME$=FNP$(X$(27,8))
0430 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0435 LET SNAME$=FNP$(X$(19,8)),SWIDTH=NUM(X$(43,2)),SHEIGHT=NUM(X$(45,2))
0445 LET MAXOPT=NUM(X$(57,2)),SYS01_DEV=NUM(STBL("!SYS01"))
0450 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0455 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0460 LET PAGES=INT((DSZ-5000)/4000)
0465 IF PAGES<3 THEN GOTO 9900
0470 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0480 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9900)IOL=APS01A
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(9,1))
0500 REM " --- Initializations"
0510 DIM A0$(73),A[2],W0$(22),W1$(11),W[2],B0$(8),B1$(30)
0520 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),OPTLIST$[2,MAXOPT,6]
0540 LET D_STATUS=0,MAXROW=HEIGHT-5,B0$(1)=N0$+OPTION$[3],DONE=0,PGM$=PGM(-2)
0550 LET M1=LEN(M1$),W=M1+11,A0$(1)=N0$+"  "+OPTION$[3],OPTION=0
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,ONAME$
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0730 LET HEADING$(2)="Invoice       Date      Due     Type  Terms"
0735 LET HEADING$(WIDTH-10)="Balance"
0740 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0750 PRINT @(0,0),'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",SNAME$,0
0920 PRINT 'CF',
0930 CALL "SYC.WB",TNAME$,0
0940 PRINT @(0,1),'CL',@(0,2),'CL',@(0,3),'CL',
0990 GOTO 1100
1000 REM " --- Process key field"
1010 GOSUB 6000
1015 CALL "SYC.WB",TNAME$,0
1030 PRINT @(1,0),'CL','SB'," Vendor:",'SF',@(0,1),'CL',@(0,2),'CL',
1035 LET V2$="",V2$=KEY(APM01_DEV,END=1050)
1040 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1045 IF V2$<>"" THEN LET V2$=V2$(3)
1050 LET V0$="M",V1$="K",V3$=M0$,V0=6,V1=10,V2=0,V4$=""
1055 PRINT @(V0+V1+1,V2),'CL',
1060 GOSUB 7000
1065 IF V3=5 THEN LET OPTION$[1]="STARTOVER"; GOTO 4000
1070 IF V3=4 OR V3=6 THEN LET OPTION$[1]="EXIT"; GOTO 4000
1080 IF V3=3 THEN CALL "SYC.LV",V$
1090 IF V$="" THEN GOTO 0900
1095 LET B0$(3)=V$
1100 REM " --- Retrieve records for display"
1105 DIM X$(WIDTH-2),APDESC$(30)
1110 READ (APM01_DEV,KEY=B0$,DOM=1000)IOL=APM01A
1115 LET X$(1)='SB'+"  Vendor: "+'SF'+FNF$(B0$(3,P[0]),M0$)+" "+B1$(1,30)
1120 LET X1$=B0$(3,6),X2$=PARAM$,APTYPE$="  ",A0$(3,2)=APTYPE$
1125 CALL "SYC.QB",X$,X1$,X2$
1130 REM " --- AP Type"
1133 IF P3$(1,1)<>"Y" THEN LET V$=""; GOTO 1190
1135 PRINT @(1,1),'CL','SB',"AP Type:",'SF'
1140 READ (APM02_DEV,KEY=B0$,DOM=1145)
1145 LET V2$="",V2$=KEY(APM02_DEV,END=1160)
1150 IF V2$(1,8)<>B0$(1,8) THEN LET V2$=""
1155 IF V2$<>"" THEN LET V2$=V2$(9,2)
1160 LET V0$="S",V1$="",V3$="",V0=2,V1=10,V2=1,V4$=""
1165 PRINT @(V0+V1+1,V2),'CL',
1170 GOSUB 7000
1175 IF V3=5 THEN LET OPTION$[1]="STARTOVER"; GOTO 4000
1180 IF V3=4 OR V3=6 THEN LET OPTION$[1]="EXIT"; GOTO 4000
1183 IF V3=3 THEN GOSUB 5200
1185 IF V3=2 THEN GOTO 1000
1190 LET V$=V$+"  ",APTYPE$=V$(1,2),A0$(3,2)=APTYPE$
1200 IF P3$(1,1)<>"Y" THEN GOTO 1240
1210 FIND (APM10_DEV,KEY=B0$(1,2)+"A"+APTYPE$,DOM=1130)IOL=APM10A
1220 READ (APM02_DEV,KEY=B0$+APTYPE$,DOM=1130)
1240 READ (APT01_DEV,KEY=A0$(1,22),DOM=1250)IOL=APT01A
1245 GOTO 1300
1250 REM " --- Invoice Number"
1255 PRINT @(1,1),'CL','SB',"Invoice:",'SF',
1260 LET V0=10,V1=10,V2=1,V0$="S",V1$="KE",V2$="",V3$="",V4$=""
1265 GOSUB 7000
1270 IF V3=6 THEN LET OPTION$[1]="EXIT"; GOTO 4000
1275 IF V3=5 THEN LET OPTION$[1]="STARTOVER"; GOTO 4000
1280 IF V3=4 THEN LET OPTION$[1]="END"; GOTO 4000
1285 IF V3=2 THEN IF P3$(1,1)="N" THEN GOTO 1000
1287 IF V3=2 THEN IF P3$(1,1)="Y" THEN GOTO 1130
1290 LET A0$(1)=B0$(1,2)+APTYPE$+B0$(3)+V$,A0$(21,2)="00"
1300 REM " --- Retrieve records for display"
1310 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,SHEIGHT-2],X$(WIDTH-2)
1320 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,SELECTION$=""
1330 READ (APT01_DEV,KEY=A0$(1,22),DOM=1400)IOL=APT01A
1340 LET X$(1)='SB'+"Invoice: "+'SF'+A0$(11,10)
1350 LET X1$=A0$(11,10),X2$=SUBPARAM$
1360 CALL "SYC.QB",X$,X1$,X2$
1370 GOSUB 6500
1390 GOTO 1500
1400 REM " --- Read next invoice"
1410 LET K$=KEY(APT01_DEV,END=1900)
1420 IF POS(N0$=K$)<>1 THEN GOTO 1900
1425 IF K$(3,8)<>APTYPE$+B0$(3) THEN GOTO 1900
1480 READ (APT01_DEV,KEY=K$)IOL=APT01A
1490 GOSUB 6500
1500 REM " --- Display page?"
1510 IF L<MAXROW THEN GOTO 1600
1520 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
1530 GOSUB 5000
1540 IF DONE THEN GOTO 4000
1580 IF ANSWER$<>"" THEN GOTO 3000
1590 LET L=1,X1=1
1600 REM " --- Middle window record"
1610 DIM G1$(WIDTH-2)
1620 LET G1$(2)=A0$(11,10),G1$(14)=FNB$(A0$(29,3))
1630 LET G1$(24)=FNB$(A0$(35,3)),G1$(34)=A0$(25,2),G1$(40)=A0$(27,2)
1650 LET G1$(WIDTH-2-M1)=STR(BALANCE:M1$)
1690 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$(5,13)
1700 REM " --- Bottom window record"
1710 DIM G2$(SWIDTH-2)
1720 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2)
1730 LET G2$(1)=" {Invoice}"+STR(INVOICE:M1$)
1740 LET G2$(W)="    {Ref}"+A0$(41,10),G2$(LEN(G2$))="",MORE$[PAGE,X1,1]=G2$
1750 LET G2$(1)=" {Applied}"+STR(APPLIED:M1$)
1760 LET G2$(W)="   {Memo}"+A0$(51,16),G2$(LEN(G2$))="",MORE$[PAGE,X1,2]=G2$
1770 LET G2$(1)="{Discount}"+STR(DISCOUNT:M1$)
1780 IF P3$(1,1)="Y" THEN LET G2$(W)="{AP Type}"+APTYPE$+" "+APDESC$(6,20),G2$(LEN(G2$))=" "
1790 LET MORE$[PAGE,X1,3]=G2$,LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
1800 REM " --- Loop back for next invoice"
1890 GOTO 1400
1900 REM " --- No more invoices"
1910 IF X0>0 THEN GOTO 2000
1920 DIM MESSAGE$[1]
1930 LET MESSAGE$[0]="No Invoices Found (<Enter>=Continue)"
1940 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
1960 IF V3=5 THEN GOTO 4000
1970 IF V3=6 THEN GOTO 4000
1990 GOTO 3000
2000 REM " --- Display last page"
2010 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2020 GOSUB 5000
2030 IF DONE THEN GOTO 4000
2060 IF ANSWER$<>"" THEN GOTO 3000
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$=A0$(5,6),CHOICE2$=A0$(11,7)
3030 GOSUB 5000
3040 IF CONTROL<>1 THEN GOTO 4000
3090 GOTO 1000
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
5200 REM " --- A/P Type Lookup"
5210 DIM DESCRIPTION$[1],FIELD[1],POSITION[1],LENGTH[1]
5220 LET NUMBER=1,COLUMN=79,ROW=3,LTITLE$="AP Type Lookup"
5230 LET DESCRIPTION$[0]="Type",FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
5250 LET DESCRIPTION$[1]="Description",FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
5270 CALL "SYC.LK",APM10_DEV,"A",NUMBER,LTITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
5290 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CF',
6090 RETURN 
6500 REM " --- Retrieve payment and adjustments"
6510 LET INVOICE=A[0],DISCOUNT=A[1],APPLIED=0
6515 READ (APT11_DEV,KEY=A0$(1,20),DOM=6516)
6520 LET K$=KEY(APT11_DEV,END=6580)
6530 IF K$(1,20)<>A0$(1,20) THEN GOTO 6580
6540 DIM W[2]
6550 READ (APT11_DEV,KEY=K$)IOL=APT11A
6560 IF W1$(1,1)="A" THEN LET INVOICE=INVOICE+W[0]
6570 IF W1$(1,1)<>"A" THEN LET APPLIED=APPLIED+W[0]
6575 GOTO 6520
6580 LET BALANCE=INVOICE+APPLIED
6590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9921)
9930 NEXT X
9950 EXIT 
9999 END

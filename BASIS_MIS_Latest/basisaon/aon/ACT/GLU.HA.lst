0010 REM "GLU - Recurring Journal Entry Update"
0020 REM "Program GLU.HA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLE-01",FILES$[2]="GLE-11"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLE01_DEV=CHANNELS[1],GLE11_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 GLE01A: IOLIST A0$,A1$,A2$,A[ALL]
0220 GLE02A: IOLIST A0$,A1$,A2$,A[ALL]
0230 GLE11A: IOLIST W0$,W1$,W2$,W3$,W[ALL]
0240 GLE12A: IOLIST W0$,W1$,W2$,W3$,W[ALL]
0500 REM " --- Initializations"
0510 DIM A[2],W[4]
0520 LET YYMM$=F0$(18,4),DAYS$="D"
0700 REM " --- Background"
0710 PRINT @(0,5),'CE','SB',@(26,5),"Posting Date:",@(7,7),"Reversal By Calculation Or Date:",'SF',
0720 CALL "SYC.WC",1,0,80,0,0,8,0
0800 REM " --- Options"
0805 CALL "SYC.YN",0,"Are You Ready to Update the Recurring Journal Register",0,V$,V3
0810 IF V$<>"YES" THEN GOTO 9900
0815 REM " --- Posting Date"
0820 LET V0$="Z",V1$="C",V2$=FNYY21_YY$(YYMM$),V3$="",V4$="Enter The Month/Year To Be Used For These Journal Entries as MMYY",V0=4,V1=40,V2=5
0825 GOSUB 7000
0830 ON V3 GOTO 0835,0815,0815,0815,9900
0835 LET YEAR=NUM(V$(1,2),ERR=0815),MONTH=NUM(V$(3,2),ERR=0815)
0840 IF MONTH<1 OR MONTH>12 THEN GOTO 0815
0845 LET YYMM$=FNYY_YY21$(V$)
0850 REM " --- Calculate or Date"
0855 LET V0$="S",V1$="C",V2$=DAYS$,V3$="CD",V4$="C=Calculate, D=Date, <F1>=More Information",V0=1,V1=40,V2=7
0860 GOSUB 7000
0865 ON V3 GOTO 0875,0870,0815,0850,9900
0870 GOSUB 6100; GOTO 0850
0875 LET DAYS$=V$
0900 REM " --- Correct?"
0910 LET V4$="Is The Above Information Correct (Y/N)?"
0915 LET V0$="Y",V1$="CER",V2$="N",V3$="",V0=1,V2=22,V1=FNV(V4$)
0920 GOSUB 7000
0925 ON V3 GOTO 0930,0900,0900,0900,9900
0930 ON POS(V$="YN") GOTO 0900,0940,0850
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
0955 REM " --- Position File"
0960 CALL "SYC.NB","Updating",13,COLUMN
0990 READ (GLE02_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Get next header record"
1010 LET K$=KEY(GLE02_DEV,END=4000)
1020 READ (GLE02_DEV,END=4000)IOL=GLE02A
1030 IF POS(N0$=K$)<>1 THEN GOTO 4000
1100 REM 
1110 PRINT @(COLUMN,11),A0$(3,2)," ",A0$(5,10),
1120 IF CYCLE$<>"" THEN IF A1$(37,2)<>CYCLE$ THEN GOTO 3900
1140 IF A1$(34,3)="   " THEN GOSUB 6000; GOTO 1500
1150 IF DAYS$="C" THEN GOSUB 6200
1160 IF DAYS$="D" THEN GOSUB 6300
1500 REM " --- Copy header to GLE-01"
1510 WRITE (GLE01_DEV,KEY=A0$)IOL=GLE01A
1990 READ (GLE12_DEV,KEY=A0$,DOM=2000)
2000 REM " --- Position GLE-12"
2010 LET K$=KEY(GLE12_DEV,END=3900)
2020 IF POS(A0$=K$)<>1 THEN GOTO 3900
2030 READ (GLE12_DEV)IOL=GLE12A
2100 REM " --- Copy detail to GLE-11"
2110 WRITE (GLE11_DEV,KEY=W0$)IOL=GLE11A
2900 REM " --- End of detail loop"
2990 GOTO 2000
3900 REM " --- End of header loop"
3990 GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
6000 REM " --- End of Month"
6010 LET P8$=YYMM$+FNA$(A1$(33,1),"00")
6020 LET X1=NUM(P8$(3,2)),X2=NUM(P8$(5,2)),X3=FNYY_YEAR(P8$(1,2))
6030 IF X1=2 THEN IF FPT(X3/4)=0 AND X2>29 THEN LET X2=29 ELSE IF X2>28 THEN LET X2=28
6040 IF (X1=4 OR X1=6 OR X1=9 OR X1=11) AND X2>30 THEN LET X2=30
6070 LET P8$=FNYEAR_YY21$(X3)+STR(X1:"00")+STR(X2:"00")
6080 LET A1$(31,3)=FND$(FNN$(P8$))
6090 RETURN 
6100 REM " --- Additional Info"
6110 DIM MESS$[5]
6120 LET MESS$[0]="If you choose C(alculate), the new reversal date is based on the"
6130 LET MESS$[1]="difference in days between the original Transaction and Reversal"
6140 LET MESS$[2]="Dates. If you choose D(ate), the new reversal date retains the"
6150 LET MESS$[3]="day from the original Reversal Date, and changes the Month and"
6160 LET MESS$[4]="Year based on the difference between the original Transaction"
6170 LET MESS$[5]="and Reversal Dates. Press <Enter> to continue."
6190 CALL "SYC.XA",0,MESS$[ALL],5,-1,-1,V$,V3
6195 RETURN 
6200 REM " --- Calculate Reversal Date"
6210 LET X0$=A1$(31,3),X1$=A1$(34,3),X0=0
6220 CALL "SYC.CA",X0$,X1$,X0
6230 GOSUB 6000
6240 LET X0$=A1$(31,3),X1$=""
6250 CALL "SYC.CA",X0$,X1$,X0
6260 LET A1$(34,3)=X1$
6290 RETURN 
6300 REM " --- Use Day for Reversal Date"
6310 LET X0$=FNYY_YY21$(FNM$(FNC$(A1$(31,3)))),X1$=FNYY_YY21$(FNM$(FNC$(A1$(34,3)))),YEAR=FNYY_YEAR(X1$(1,2))-FNYY_YEAR(X0$(1,2)),MONTH=NUM(X1$(3,2))-NUM(X0$(3,2)),NEW_DAY=NUM(X1$(5,2))
6315 IF NEW_DAY<1 THEN LET NEW_DAY=1
6320 GOSUB 6000
6330 LET X0$=FNYY_YY21$(FNM$(FNC$(A1$(31,3)))),NEW_MTH=NUM(X0$(3,2))+MONTH,NEW_YEAR=FNYY_YEAR(X0$(1,2))+YEAR
6340 IF NEW_MTH<1 THEN LET NEW_MTH=NEW_MTH+12,NEW_YEAR=NEW_YEAR+1; GOTO 6340
6350 LET X=JUL(1900+NEW_YEAR,NEW_MTH,NEW_DAY,ERR=6370); REM "Check for valid date
6360 GOTO 6390
6370 LET NEW_DAY=NEW_DAY-1; REM "Reduce invalid date by one day, and try again
6380 GOTO 6350
6390 LET A1$(34,3)=FND$(STR(NEW_MTH:"00")+STR(NEW_DAY:"00")+FNYEAR_YY21$(NEW_YEAR))
6395 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8070 DEF FNN$(Q$)=Q$(3,4)+Q$(1,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

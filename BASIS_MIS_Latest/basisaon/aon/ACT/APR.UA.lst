0010 REM "APR ACCOUNTS PAYABLE - CHECK PRINTING/BLANK STUB"
0020 REM "Program : APR.UA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 0900
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=8
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APE-04",FILES$[2]="APE-04",FILES$[3]="APM-01"
0140 LET FILES$[4]="APM-03",FILES$[5]="APW-01",FILES$[6]="APT-02"
0150 LET FILES$[7]="SYS-01",FILES$[8]="APM-10"
0160 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET APE04_DEV1=CHANNELS[1],APE04_DEV2=CHANNELS[2],APM01_DEV=CHANNELS[3]
0185 LET APM03_DEV=CHANNELS[4],APW01_DEV=CHANNELS[5],APT02_DEV=CHANNELS[6]
0190 LET SYS01_DEV=CHANNELS[7],APM10_DEV=CHANNELS[8]
0199 SETERR 9000
0200 REM " --- IOLists"
0250 APM10A: IOLIST X0$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0290 APM01A: IOLIST *,X0$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[2],G[4]
0460 FOR X=0 TO 4
0470 LET G[X]=NUM(G2$(X*2+1,2),ERR=0480)
0480 NEXT X
0485 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(9,1))
0500 REM " --- Init Data"
0510 LET H0=2
0520 LET L9=11,L=0
0530 DIM J$(80,"-"),J0$(10,"0"),J1$(20," "),H9$(20,$0A$)
0540 DIM T[2],A[3],W[3],E[1]
0545 LET M1$="########.00CR",M7$="00000"
0550 LET M0=LEN(M0$),M1=LEN(M1$),G5=LEN(G5$)
0560 LET P9$=F0$(7,3),P6$="  "
0570 LET M8$="*#######",M9$="*"+M1$
0580 LET M9=LEN(M9$)
0590 IF P[2]=9 THEN LET M7$="00000-0000"
0595 DIM MESSAGE$[1]
0700 REM " --- Background"
0710 FOR X=4 TO 12 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(28,3),"Check Date:",@(26,5),"Check Number:",@(25,7),"Restart (Y/N):",
0760 PRINT @(25,9),"Vendor Number:",
0780 IF P3$(1,1)="Y" THEN PRINT @(0,8),'CL',@(31,8),'SB',"AP Type:",
1000 REM " --- Check Date"
1010 PRINT 'CF',
1020 LET V0$="D",V1$="ER",V2$=P9$,V0=8,V1=40,V2=3,I0=0
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1060 LET P9$=V$
1090 GOTO 3900
1100 REM " --- Check Number"
1120 LET V0$="Z",V1$="R",V2$=P8$,V0=7,V1=40,V2=5
1140 GOSUB 7000
1160 LET P8$=V$
1190 GOTO 3900
1200 REM " --- Restart"
1220 LET V0$="Y",V1$="R",V2$="N",V0=1,V1=40,V2=7
1240 GOSUB 7000
1260 LET P7$=V$
1290 GOTO 3900
1300 REM " --- AP Type"
1310 IF P7$="N" THEN GOTO 3900
1320 IF P3$(1,1)="N" THEN GOTO 3900
1330 LET V0$="S",V1$="R",V2$="",V0=2,V1=40,V2=8
1340 GOSUB 7000
1350 LET P6$=V$
1360 FIND (APM10_DEV,KEY=N0$+"A"+P6$,DOM=1300)IOL=APM10A
1370 PRINT @(43,V2),X0$(6,20),
1390 GOTO 3900
1400 REM " --- Vendor"
1410 IF P7$="N" THEN GOTO 3900
1420 LET V0$="M",V2$="",V3$=P1$,V0=P[1],V1=40,V2=9
1430 GOSUB 7000
1440 LET P5$=V$
1450 FIND (APM01_DEV,KEY=N0$+P5$,DOM=1400)IOL=APM01A
1460 PRINT @(41+M0,V2),X0$(1,30),
3900 REM " --- New Field"
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,1200,1300,1400,4000
4000 REM " --- Correct"
4010 LET V0$="Y",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4040 IF V=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 CALL "SYC.GA",7,1,"","",STATUS
4110 IF STATUS THEN GOTO 9900
4200 RUN "APR.UB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "GLX - Cross-Reference Rebuild"
0020 REM "Program GLX.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-01",FILES$[2]="GLM-04",FILES$[3]="GLT-04"
0140 LET FILES$[4]="GLT-05",FILES$[5]="SYS-01"
0150 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET GLM01_DEV=CHANNELS[1],GLM04_DEV=CHANNELS[2],GLT04_DEV=CHANNELS[3]
0180 LET GLT05_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$,A1$(1)
0220 GLM04A: IOLIST X0$(1)
0230 GLT04A: IOLIST C0$(1),C1$(1),C2$,C[ALL]
0240 GLT05A: IOLIST X1$(1)
0260 GLS01A: IOLIST X$,X$,P2$(1),X$,X$,M0$(1),X$,X$,X$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N4$=F5$
0430 GOSUB 6000
0500 REM " --- Initializtions"
0510 DIM A1$(40),X0$(38+P[4]),X1$(26+P[4]),C0$(26+P[4]),C1$(64),C[4]
0550 LET N0$=""
0800 REM " --- Options"
0810 LET V4$="Do you wish to rebuild the G/L Cross-References"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM " --- Clear and position file"
0910 CALL "SYC.FA",GLM04_DEV
0920 CALL "SYC.FA",GLT05_DEV
0990 CALL "SYC.NB","Processing",18,COLUMN
1000 REM " --- Rebuild GLM-04"
1010 READ (GLM01_DEV,END=1900)IOL=GLM01A
1020 IF POS(N0$=A0$)=1 THEN GOTO 1100
1030 LET N0$=A0$(1,2)
1040 GOSUB 6000
1100 REM 
1110 PRINT @(COLUMN,11),A0$(1,2)," ",FNF$(A0$(3,P[3]),M0$),
1120 LET X0$(1)=A0$(1,2)+"A"+A1$(1,35)+A0$(3,P[4])
1130 WRITE (GLM04_DEV,KEY=X0$)IOL=GLM04A
1190 GOTO 1000
1900 REM 
1910 PRINT @(0,3),'CE',
1990 CALL "SYC.NB","Processing",28,COLUMN
2000 REM " --- Rebuild GLT-05"
2010 READ (GLT04_DEV,END=4000)IOL=GLT04A
2015 IF POS(N0$=C0$)<>1 THEN LET N0$=A0$(1,2); GOSUB 6000
2020 PRINT @(COLUMN,11),C0$(1,2)," ",C0$(3,2)," ",C0$(5,7)," ",FNYY21_YY$(C0$(12,2))," ",C0$(14,2)," ",FNB6$(C0$(16,6)),
2030 LET X1$(1,2)=C0$(1,2),X1$(3,6)=C0$(6+P[4],6),X1$(9,2)=C0$(3,2)
2040 LET X1$(11,P[4])=C0$(22,P[4]),X1$(11+P[4],7)=C0$(5,7)
2050 LET X1$(18+P[4],4)=C0$(12,4),X1$(22+P[4],5)=C0$(22+P[4],5)
2060 WRITE (GLT05_DEV,KEY=X1$)IOL=GLT05A
2090 GOTO 2000
4000 REM " --- All done"
4090 GOTO 9900
6000 REM " --- Load G/L Parameters"
6010 DIM P[4],P2$(10)
6020 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
6030 LET M0=LEN(M0$)
6040 FOR X=0 TO 4
6050 LET P[X]=NUM(P2$(X*2+1,2),ERR=6060)
6060 NEXT X
6070 LET P[2]=FNYY_YEAR(P2$(5,2))
6090 RETURN 
8000 REM " --- Functions"
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

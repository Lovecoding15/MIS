0010 REM "APP - A/P Parameter Maintenance"
0020 REM "Program APP.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 APS01A: IOLIST A0$,A1$,A2$(1),A3$(1),A4$(1),A5$,A6$,A7$
0500 REM " --- Initializations"
0510 DIM A2$(4),A3$(35),A4$(4)
0590 LET A0$=N0$+N2$+"00",A2$(3,2)=STR(P8:"00")
0700 REM " --- Background"
0720 PRINT 'SB',@(27,4),"Vendor Mask:",@(20,5),"Multiple A/P Types:",@(16,6),"Multiple Distributions:",@(21,7),"Retention Account:",@(28,8),"Cash Basis:",@(18,9),"Retain Zero Invoices:",@(11,10),"Print Checks In AP Type Seq:",@(14,11),"Print Memo On Check Stub:",@(14,12),"Print Dist On Check Stub:",@(25,13),"Zip Code Size:",@(24,14),"Current Period:",@(26,15),"Current Year:",@(27,16),"Amount Mask:",@(29,17),"Labels Up:",@(26,18),"Label Length:",@(27,19),"Label Width:",@(19,20),"Miscellaneous Entry:",@(19,21),"Bank Reconciliation:",@(48,5),"Post to Closed Periods:",@(46,6),"Replenishment Processing:",@(48,7),"Beginning Comment Line:",@(51,8),"Ending Comment Line:",'SF',
1200 REM " --- Retrieve record"
1220 FIND (SYS01_DEV,KEY=A0$,DOM=1400)IOL=APS01A
1240 LET P0=NUM(A2$(1,2))
1300 REM " --- Display"
1320 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New Record"
1410 DIM A1$(P8,"#")
1430 LET A3$(1)="NNNNNNNN",A3$(9,1)="9",A3$(16,3)="NNN",A3$(20,2)=" N"
1440 LET A3$(10,6)="010636"
2000 REM " --- Vendor Mask"
2010 LET J0=1
2015 LET V4$="Enter Between 4 And "+STR(P8)+" "+$22$+"#"+$22$+" Characters (Optionally Separated By A "+$22$+"-"+$22$+")"
2020 LET V0$="S",V1$="CE",V2$=A1$,V3$="",V0=11,V1=40,V2=4,I0=0
2030 GOSUB 7000
2035 IF V3=4 THEN GOTO 9900
2040 GOSUB 6000
2050 IF V$="" THEN GOTO 2020
2060 LET A1$=V$
2070 GOSUB 6200
2073 IF P0<4 THEN LET P0=P8+1
2075 IF P0>P8 THEN GOTO 2000
2080 LET A2$(1,2)=STR(P0:"00")
2090 GOTO 3900
2100 REM " --- Options"
2110 FOR X=J0 TO 8
2115 IF X=6 THEN IF A3$(1,1)="N" THEN GOTO 2160
2120 IF X=8 AND GL$="N" THEN LET V$="N"; PRINT @(40,4+X),V$,; GOTO 2150
2130 LET V0$="Y",V1$="RC",V2$=A3$(X,1),V3$="",V4$="Y=Yes    N=No",V0=1,V1=40,V2=4+X
2140 GOSUB 7000
2150 LET A3$(X,1)=V$
2155 IF X=1 THEN IF A3$(X,1)="N" THEN LET A3$(6,1)="N"; PRINT @(40,10),"N",
2160 IF V3=4 THEN EXITTO 4000
2170 IF V3<>2 THEN GOTO 2280
2180 IF X<=1 THEN EXITTO 3900
2190 LET X=X-1
2200 GOTO 2115
2280 NEXT X
2290 GOTO 3900
2300 REM "ZIP CODE"
2310 LET V4$="Enter the length of the zip/postal code to be used (5 or 9)"
2320 LET V0$="Z",V1$="CR",V2$=A3$(9,1),V3$="59",V0=1,V1=40,V2=13
2330 GOSUB 7000
2340 LET A3$(9,1)=V$
2350 IF V3=2 THEN LET J0=8
2390 GOTO 3900
2400 REM " --- Period"
2410 LET V4$="Enter the Current A/P Period"
2420 LET V0$="Z",V1$="CR",V2$=A4$(1,2),V3$="",V0=2,V1=40,V2=14
2440 GOSUB 7000
2450 IF V<1 OR V>G0 THEN GOTO 2400
2460 LET A4$(1,2)=V$
2490 GOTO 3900
2500 REM "YEAR"
2510 LET V4$="Enter the Current Fiscal Year"
2520 LET V0$="D",V1$="CRY",V2$=A4$(3,2),V3$="",V0=2,V1=40,V2=15
2540 GOSUB 7000
2560 LET A4$(3,2)=V$
2590 GOTO 3900
2600 REM "AMOUNT MASK"
2610 LET V4$="Enter The Mask To Be Used For Displaying Amounts"
2620 LET V0$="S",V1$="C",V2$=A6$,V3$="",V0=15,V1=40,V2=16
2630 GOSUB 7000
2640 GOSUB 6100
2650 IF V$="" THEN GOTO 2600
2660 LET A6$=V$
2690 GOTO 3900
2800 REM "LABELS"
2805 IF A3$(12,2)<"04" THEN LET A3$(12,2)="04"
2810 LET X0=I0-6,V4$=""
2820 LET V0$="Z",V1$="RC",V2$=A3$(10+X0*2,2),V0=2,V1=40,V2=17+X0
2840 GOSUB 7000
2850 IF X0=1 AND NUM(V$)<4 THEN LET V4$="Minimum Of 4 Lines/Label Required"; GOTO 2820
2860 LET A3$(10+X0*2,2)=V$
2890 GOTO 3900
2900 REM "MISC ENTRY
2920 LET V0$="Y",V1$="RC",V2$=A3$(16,1),V3$="",V4$="Y=Yes   N=No",V0=1,V1=40,V2=20
2940 GOSUB 7000
2950 LET A3$(16,1)=V$
2990 GOTO 3900
3000 REM "BANK REC"
3020 LET V0$="Y",V1$="RC",V2$=A3$(17,1),V3$="",V4$="Y=Yes   N=No",V0=1,V1=40,V2=21
3030 IF GL$<>"Y" THEN LET V$="N"; GOTO 3050
3040 GOSUB 7000
3050 LET A3$(17,1)=V$
3060 PRINT @(V1,V2),V$,
3090 GOTO 3900
3200 REM "CLOSED GL"
3220 LET V0$="Y",V1$="RC",V2$=A3$(18,1),V3$="",V4$="Allow Transaction Entry For Closed G/L Periods (Y/N)?",V1=72,V2=5
3230 IF GL$<>"Y" THEN LET V$="N"; GOTO 3260
3240 GOSUB 7000
3260 LET A3$(18,1)=V$
3270 PRINT @(V1,V2),V$,
3290 GOTO 3900
3400 REM "REPLENISHMENT?
3410 LET V0$="Y",V1$="C",V2$=A3$(21,1),V3$="",V4$="Calculate Automatic Replenishment For Inventory (Y/N)?",V0=1,V1=72,V2=6
3420 GOSUB 7000
3430 LET A3$(21,1)=V$
3490 GOTO 3900
3500 REM " --- Beginning Comment Line"
3510 LET V4$="Enter The Beginning Comment Line To Display (01 Through 99)"
3520 LET V0$="Z",V1$="CR",V2$=A3$(22,2),V3$="",V0=2,V1=72,V2=7
3540 GOSUB 7000
3550 IF V=0 THEN GOTO 3500
3560 LET A3$(22,2)=V$
3590 GOTO 3900
3600 REM " --- Ending Comment Line"
3610 LET V4$="Enter The Ending Comment Line To Display ("+A3$(22,2)+" Through 99)"
3620 LET V0$="Z",V1$="CR",V2$=A3$(24,2),V3$="",V0=2,V1=72,V2=8
3640 GOSUB 7000
3650 IF V<NUM(A3$(22,2)) THEN GOTO 3600
3660 LET A3$(24,2)=V$
3690 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2300,2400,2500,2600,2800,2800,2800,2900,3000,3200,3400,3500,3600,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V0$="S",V4$="Is The Above Information Correct (Y/N)?"
4020 LET V1$="KC",V2$="Y",V3$="",V0=6,V1=42+INT(LEN(V4$)/2),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write record"
4110 WRITE (SYS01_DEV,KEY=A0$)IOL=APS01A
4180 IF O0>0 THEN RUN "SYM.RP"
4190 GOTO 4900
4200 REM " --- Remove records"
4210 REMOVE (SYS01_DEV,KEY=A0$,DOM=4220)
4220 REMOVE (SYS01_DEV,KEY=N0$+N2$+"02",DOM=4230)
4290 GOTO 9900
4900 REM " --- Run menu overlay"
4990 RUN O0$,ERR=9900
5000 REM " --- Display"
5020 PRINT @(40,4),A1$,@(40,5),
5040 FOR X=1 TO 9
5045 IF X=8 AND GL$="N" THEN LET A3$(X,1)="N"
5050 PRINT @(40,4+X),A3$(X,1),
5060 NEXT X
5070 PRINT @(40,14),A4$(1,2),@(40,15),FNYY21_YY$(A4$(3,2)),@(40,16),A6$,@(40,17),A3$(10,2),@(40,18),A3$(12,2),@(40,19),A3$(14,2),
5080 PRINT @(40,20),A3$(16,1),
5085 PRINT @(40,21),A3$(17,1),@(72,5),A3$(18,1),@(72,6),A3$(21,1),@(72,7),A3$(22,2),@(72,8),A3$(24,2),
5090 RETURN 
6000 REM " --- Verify Mask"
6020 IF V$="" THEN GOTO 6090
6040 FOR X=1 TO LEN(V$)
6050 IF POS(V$(X,1)="#-")=0 THEN EXITTO 6080
6060 NEXT X
6070 GOTO 6090
6080 LET V$=""
6090 RETURN 
6100 REM " --- Test Mask"
6120 SETERR 6170
6130 PRINT @(0,22),0:V$,@(0,22),'CL',
6140 GOTO 6180
6170 LET V$=""
6180 SETERR 9000
6190 RETURN 
6200 REM " --- Mask Length"
6210 LET P0=0,A5$=""
6220 FOR X=1 TO LEN(A1$)
6230 IF POS("#"=A1$(X,1))<>0 THEN LET P0=P0+1,A5$=A5$+"0"
6240 IF POS("#"=A1$(X,1))=0 THEN LET A5$=A5$+A1$(X,1)
6280 NEXT X
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 4190
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

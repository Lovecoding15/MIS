0010 REM "GLR - Standard G/L Report Input Screen Processing"
0020 REM "Program GLR.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0041 REM "ACTION$ determines which items to prompt for. Valid values are:"
0042 REM 
0044 REM "null = No prompting. Perform all other processing"
0046 REM "1    = Beginning Period/Year & Ending Period/Year"
0048 REM "2    = Beginning Account/Ending Account/Wildcard Mask"
0050 REM "3    = Journal ID"
0052 REM "4    = By Date or Audit & Page Break (Y/N)"
0054 REM "5    = Detail or Summary"
0056 REM "6    = Reserved for future use"
0058 REM "7    = For Period/Year"
0060 REM "8    = Include Account Numbers?"
0061 REM "P    = Prompt with menu description"
0062 REM "R    = GLM-02 Record ID's & Amounts/Units"
0064 REM 
0066 REM "Valid modifiers (include in ACTION$ on input)"
0068 REM 
0069 REM "B = Only permit budget selections when R is specified"
0070 REM "C = When ACTION$=1 or 7 restricts year to current G/L year"
0071 REM "D = Special Headings in Daily Detail Format"
0072 REM "H = Include Budget Header (BUDGET$) in report heading"
0073 REM "I = Don't initialize GLM-02 Record ID's"
0078 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ON O0 GOTO 0100,0400
0100 REM " --- Open/Lock Files"
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-01",FILES$[2]="GLM-02",FILES$[3]="GLM-03"
0140 LET FILES$[4]="GLM-08",FILES$[5]="GLM-11",FILES$[6]="GLM-21"
0150 LET FILES$[7]="SYS-01"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM01_DEV=CHANNELS[1],GLM02_DEV=CHANNELS[2],GLM03_DEV=CHANNELS[3]
0185 LET GLM08_DEV=CHANNELS[4],GLM11_DEV=CHANNELS[5],GLM21_DEV=CHANNELS[6]
0190 LET SYS01_DEV=CHANNELS[7]
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$(1),A1$(1)
0230 GLM03A: IOLIST C0$(1),C1$,C[0],C[1],C[2]
0260 GLS01A: IOLIST X$,P1$,P2$,P3$(1),P4$(1),M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0405 DIM P[4],P3$(325),P4$(32)
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0425 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N4$=F5$,NUMBER=1
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0435 LET MP=13,P1=16,P2=3,NUMBER=1
0440 FOR X=0 TO 4
0445 LET P[X]=NUM(P2$(X*2+1,2),ERR=0450)
0450 NEXT X
0455 LET P[2]=FNYY_YEAR(P2$(5,2))
0460 LET PERIODS$=P3$(27,P1*P[0]),ABBR$=P3$(235,P2*P[0])
0465 LET YEAR1=P[2]-1,YEAR3=P[2]+1
0470 IF P4$(6,1)<>"Y" THEN LET YEAR1=YEAR1-1,YEAR3=YEAR3-1
0500 REM " --- Initializations"
0510 DIM A0$(2+P[4]),A1$(40),C0$(32),C[2],O[8],IMAGE$[21]
0540 LET M0=LEN(M0$),M4$="00",W=79-(42+M0),INCLUDE$="Y"
0550 LET BEGPER$=P2$(3,2),BEGYEAR$=P2$(5,2),VALID$="",RECOFF=13
0560 LET ENDPER$=P2$(3,2),ENDYEAR$=P2$(5,2),JOURNAL$=""
0565 LET DETAIL$="D",PAGEBREAK$="N",SEQUENCE$="A",FIELDS=0
0570 LET BEGVAL$="",ENDVAL$=""
0580 IF OFFSET=0 THEN LET OFFSET=3
0585 IF POS("B"=ACTION$)>0 THEN LET VALID$="B"
0590 LET A0$(1,2)=N0$,A0$(3)=ACCOUNT$,C0$(1,2)=N0$,ROW=OFFSET
0600 REM " --- Determine which records to display"
0610 IF POS("R"=ACTION$)=0 THEN GOTO 0700
0615 IF POS("I"=ACTION$)>0 THEN GOTO 0700
0620 LET ID$=P4$(8,4),AU$=P4$(12,4)
0625 IF POS("B"=VALID$)>0 THEN IF P4$(2,1)="Y" THEN LET ID$=P4$(16,4),AU$=P4$(20,4)
0630 LET ID=LEN(FNP$(ID$))
0645 IF ID>0 THEN GOTO 0700
0650 REM " --- No columns specified. Use historical precedents."
0660 LET ID$="0123",ID=4,AU$="AAAA"
0670 IF P4$(2,1)="Y" THEN GOTO 0700
0680 LET ID$="0022",AU$="AUAU"
0690 IF P4$(3,1)="N" THEN LET ID$="02",ID=2,AU$="AA"
0700 REM " --- Background"
0710 PRINT @(0,ROW),'CE',
0715 IF ACTION$="" THEN GOTO 4500
0720 IF POS("1"=ACTION$)>0 THEN GOSUB 6400
0725 IF POS("2"=ACTION$)>0 THEN GOSUB 6450
0730 IF POS("3"=ACTION$)>0 THEN GOSUB 6500
0735 IF POS("4"=ACTION$)>0 THEN GOSUB 6550
0740 IF POS("5"=ACTION$)>0 THEN GOSUB 6600
0745 IF POS("7"=ACTION$)>0 THEN GOSUB 6650
0750 IF POS("8"=ACTION$)>0 THEN GOSUB 6700
0755 IF POS("R"=ACTION$)>0 THEN GOSUB 6750
0760 IF FIELDS=0 THEN GOTO 4000
0765 FOR X=OFFSET TO 21
0770 IF IMAGE$[X]<>"" THEN LET L2=X+1
0775 NEXT X
0780 IF L2>20 THEN LET L2=20
0785 FOR X=OFFSET+1 TO L2 STEP 2
0790 CALL "SYC.WC",1,0,80,0,0,X,0
0795 NEXT X
0800 REM " --- Display background"
0810 FOR X=OFFSET TO 21
0820 IF IMAGE$[X]<>"" THEN PRINT 'SB',@(38-LEN(IMAGE$[X]),X),IMAGE$[X],":",'SF',
0830 NEXT X
0900 REM " --- Display defaults"
0910 GOSUB 5000
1000 REM " --- Beginning Period"
1005 LET I0=0,V3=0
1010 IF POS("1"=ACTION$)=0 AND POS("7"=ACTION$)=0 THEN GOTO 3900
1015 LET V0$="N",V1$="C",V2$=BEGPER$,V3$=M4$,V0=2,V1=40,V2=O[1]
1020 LET V4$="Enter A Fiscal Period Between 1 And "+STR(P[0])
1023 IF POS("7"=ACTION$)>0 THEN LET V2=O[7]
1025 IF POS("1"=ACTION$)>0 THEN LET V4$=V4$+" (<F1>=First)"
1030 GOSUB 7000
1035 ON V3 GOTO 1060,1040,3900,1000,3900
1040 IF POS("1"=ACTION$)=0 THEN GOTO 1070
1045 LET BEGPER$=""
1050 GOTO 1080
1060 IF V$="" THEN GOTO 1045
1070 IF V<1 OR V>P[0] THEN GOTO 1000
1075 LET BEGPER$=STR(V:M4$)
1080 GOSUB 5400
1090 GOTO 3900
1100 REM " --- Beginning Year"
1110 IF POS("1"=ACTION$)=0 AND POS("7"=ACTION$)=0 THEN GOTO 3900
1115 LET V0$="D",V1$="CY",V2$=BEGYEAR$,V3$="",V0=2,V1=40,V2=O[1]+2
1120 LET V4$="Enter A Fiscal Year Between "+STR(1900+YEAR1)+" And "+STR(1900+YEAR3)
1125 IF POS("7"=ACTION$)>0 THEN LET V2=O[7]+2
1130 IF POS("C"=ACTION$)>0 THEN GOTO 1160
1140 GOSUB 7000
1150 ON V3 GOTO 1170,1100,3900,1100,3900
1160 LET V$=BEGYEAR$
1170 IF V$<FNYEAR_YY21$(YEAR1) OR V$>FNYEAR_YY21$(YEAR3) THEN GOTO 1100
1180 LET BEGYEAR$=V$
1185 GOSUB 5500
1190 GOTO 3900
1200 REM " --- Ending Period"
1210 IF POS("1"=ACTION$)=0 THEN GOTO 3900
1215 LET V0$="N",V1$="C",V2$=ENDPER$,V3$=M4$,V0=2,V1=40,V2=O[1]+4
1220 IF BEGPER$="" THEN LET X$="1" ELSE LET X$=STR(NUM(BEGPER$))
1225 LET V4$="Enter A Fiscal Period Between 1 And "+STR(P[0])+" (<F1>=Last)"
1230 GOSUB 7000
1235 ON V3 GOTO 1260,1240,3900,1200,3900
1240 LET ENDPER$=""
1250 GOTO 1280
1260 IF V$="" THEN GOTO 1240
1265 IF V<1 OR V>P[0] THEN GOTO 1200
1275 LET ENDPER$=STR(V:M4$)
1280 GOSUB 5450
1290 GOTO 3900
1300 REM " --- Ending Year"
1310 IF POS("1"=ACTION$)=0 THEN GOTO 3900
1315 LET V0$="D",V1$="CY",V2$=ENDYEAR$,V3$="",V0=2,V1=40,V2=O[1]+6
1317 IF BEGYEAR$="" THEN LET X$=STR(1900+YEAR1) ELSE LET X$=STR(1900+FNYY_YEAR(BEGYEAR$))
1320 LET V4$="Enter A Fiscal Year Between "+X$+" And "+STR(1900+YEAR3)
1325 IF POS("C"=ACTION$)>0 THEN GOTO 1340
1330 GOSUB 7000
1335 ON V3 GOTO 1350,1300,3900,1300,3900
1340 LET V$=ENDYEAR$
1350 IF V$<FNYEAR_YY21$(YEAR1) OR V$>FNYEAR_YY21$(YEAR3) THEN GOTO 1300
1360 IF BEGYEAR$<>"" THEN IF V$<BEGYEAR$ THEN GOTO 1300
1370 LET ENDYEAR$=V$
1380 GOSUB 5500
1390 GOTO 3900
1400 REM " --- Beginning Account Number"
1405 IF POS("2"=ACTION$)=0 THEN GOTO 3900
1410 LET V4$="Enter A Beginning Account Number (<F1>=First/<F3>=Lookup)"
1415 LET V0$="M",V1$="C",V3$=M0$,V0=P[4],V1=40,V2=O[2]
1417 IF V2$="" AND BEGACCT$<>"" THEN LET V2$=BEGACCT$(1,P[3])
1420 GOSUB 7000
1425 ON V3 GOTO 1460,1430,3900,1450,3900
1430 LET BEGACCT$=""
1435 GOSUB 5600
1440 GOTO 3900
1450 CALL "SYC.LG",V$
1455 IF V$="" THEN GOTO 1400
1460 IF V$="" THEN GOTO 1430
1465 GOSUB 5700
1470 IF STATUS>0 THEN GOTO 1400
1480 IF V$<>"" THEN LET BEGACCT$=A0$(3)
1490 GOTO 3900
1500 REM " --- Ending Account Number"
1505 IF POS("2"=ACTION$)=0 THEN GOTO 3900
1510 LET V4$="Enter An Ending Account Number (<F1>=Last/<F3>=Lookup)"
1515 LET V0$="M",V1$="C",V3$=M0$,V0=P[4],V1=40,V2=O[2]+2
1517 IF V2$="" AND ENDACCT$<>"" THEN LET V2$=ENDACCT$(1,P[3])
1520 GOSUB 7000
1525 ON V3 GOTO 1560,1530,3900,1550,3900
1530 LET ENDACCT$=""
1535 GOSUB 5650
1540 GOTO 3900
1550 CALL "SYC.LG",V$
1555 IF V$="" THEN GOTO 1500
1560 IF V$="" THEN GOTO 1530
1565 GOSUB 5700
1570 IF STATUS>0 THEN GOTO 1500
1575 IF BEGACCT$<>"" THEN IF A0$(3)<BEGACCT$ THEN GOTO 1500
1580 LET ENDACCT$=A0$(3)
1590 GOTO 3900
1600 REM " --- Wildcard Mask"
1605 IF POS("2"=ACTION$)=0 THEN GOTO 3900
1610 LET V4$="Enter The Wildcard Mask To Be Used In Selecting Accounts (<F1>=None)"
1615 LET V0$="W",V1$="CK",V2$=WILDCARD$,V3$=M0$,V0=P[4],V1=40,V2=O[2]+4
1620 GOSUB 7000
1625 ON V3 GOTO 1630,1635,3900,1600,3900
1630 IF V$<>"" THEN GOTO 1650
1635 LET WILDCARD$=""
1645 GOTO 1680
1650 LET WILDCARD$=V$
1680 GOSUB 5550
1690 GOTO 3900
1700 REM " --- Journal ID Code"
1705 IF POS("3"=ACTION$)=0 THEN GOTO 3900
1715 LET V4$="Enter The Journal ID Code To Be Included On This Report (<F1>=All/<F3>=Lookup)"
1720 LET V0$="S",V1$="C",V2$=JOURNAL$,V3$="",V0=2,V1=40,V2=O[3]
1725 GOSUB 7000
1730 ON V3 GOTO 1760,1735,3900,1750,3900
1735 LET JOURNAL$=""
1740 GOSUB 5750
1745 GOTO 3900
1750 GOSUB 6000
1755 IF V$="" THEN GOTO 1700
1760 IF V$="" THEN GOTO 1735
1765 LET V$=V$+"  ",V$=V$(1,2)
1770 GOSUB 5750
1775 IF STATUS>0 THEN GOTO 1700
1780 LET JOURNAL$=C0$(3,2)
1790 GOTO 3900
1800 REM " --- By Date or Audit Number"
1810 IF POS("4"=ACTION$)=0 THEN GOTO 3900
1815 LET V0$="S",V1$="C^",V2$=SEQUENCE$,V3$="DA",V0=1,V1=40,V2=O[4]
1820 LET V4$="Generate Report By Date Or Audit Number Sequence (D=Date/A=Audit Number)"
1825 GOSUB 7000
1830 ON V3 GOTO 1835,1800,3900,1800,3900
1835 IF POS(V$="DA")=0 THEN GOTO 1800
1840 LET SEQUENCE$=V$
1880 GOSUB 5800
1890 GOTO 3900
1900 REM " --- Page Break?"
1910 IF POS("4"=ACTION$)=0 THEN GOTO 3900
1915 LET V0$="Y",V1$="C^",V2$=PAGEBREAK$,V3$="",V0=1,V1=40,V2=O[4]+6
1920 IF SEQUENCE$="A" THEN LET V4$="Audit Number" ELSE LET V4$="Transaction Date"
1925 LET V4$="Page Break When "+V4$+" Changes (Y/N)?"
1930 GOSUB 7000
1935 ON V3 GOTO 1940,1900,3900,1900,3900
1940 IF V$="" THEN GOTO 1900
1950 LET PAGEBREAK$=V$
1980 GOSUB 5850
1990 GOTO 3900
2000 REM " --- Detail or Summary?"
2010 IF POS("5"=ACTION$)=0 THEN GOTO 3900
2015 LET V0$="S",V1$="C^",V2$=DETAIL$,V3$="DS",V0=1,V1=40,V2=O[5]
2020 LET V4$="Display Detail Or Summarized Information (D=Detail/S=Summarized)?"
2025 GOSUB 7000
2030 ON V3 GOTO 2035,2000,3900,2000,3900
2035 IF POS(V$="DS")=0 THEN GOTO 2000
2040 LET DETAIL$=V$
2080 GOSUB 5900
2090 GOTO 3900
2100 REM " --- Include Account Numbers?"
2110 IF POS("8"=ACTION$)=0 THEN GOTO 3900
2115 LET V0$="Y",V1$="C^",V2$=INCLUDE$,V3$="",V0=1,V1=40,V2=O[8]
2120 LET V4$="Include G/L Account Numbers On The Report (Y/N)?"
2130 GOSUB 7000
2135 ON V3 GOTO 2140,2100,3900,2100,3900
2140 IF V$="" THEN GOTO 2100
2150 LET INCLUDE$=V$
2180 GOSUB 5950
2190 GOTO 3900
2200 REM " --- Record ID"
2205 IF POS("R"=ACTION$)=0 THEN GOTO 3900
2210 LET P=INT(MOD(I0,RECOFF)/2)+1,V0=1,V1=20,V2=O[6]+(P-1)
2220 LET V0$="S",V1$="C^",V2$=ID$(P,1),V3$=""
2230 LET V4$="Enter the ID of the record to be displayed (<F1>=None/<F3>=Lookup)"
2240 GOSUB 7000
2250 ON V3 GOTO 2350,2260,3900,2300,3900
2260 LET ID$(P,1)="",AU$(P,1)=""
2290 GOTO 3900
2300 REM 
2310 CALL "GLC.LA",ACTION$,SELECTION$,FLAG$,DESCRIPTION$,TITLE$
2320 IF SELECTION$="" THEN GOTO 2200
2330 GOSUB 6200
2335 IF STATUS<>0 THEN GOTO 2200
2340 LET ID$(P,1)=SELECTION$,AU$(P,1)=FLAG$
2345 GOTO 3900
2350 IF V$="" THEN GOTO 2260
2360 LET ID$(P,1)=V$
2370 PRINT @(V1,V2),ID$(P,1),
2390 GOTO 3900
2400 REM " --- Type"
2405 IF POS("R"=ACTION$)=0 THEN GOTO 3900
2410 LET P=INT(MOD(I0,RECOFF)/2),V0=1,V1=24,V2=O[6]+(P-1)
2420 LET V0$="S",V1$="C^",V2$=AU$(P,1),V3$="AU"
2425 LET SELECTION$=ID$(P,1),V$=""
2430 IF FNP$(SELECTION$)="" THEN GOTO 2500
2435 LET V4$="Select information to be displayed (A=Amounts/U=Units)"
2440 GOSUB 7000
2450 ON V3 GOTO 2460,2400,3900,2400,3900
2460 IF POS(V$="AU")=0 THEN GOTO 2400
2470 LET FLAG$=V$,AU$(P,1)=V$
2480 GOSUB 6200
2485 IF STATUS<>0 THEN GOTO 2400
2490 GOTO 3900
2500 REM 
2510 LET ID$(P,1)="",FLAG$=""
2520 GOSUB 6200
2590 GOTO 3900
2600 REM " --- Beginning Value"
2605 IF POS("4"=ACTION$)=0 THEN GOTO 3900
2610 IF SEQUENCE$<>"A" THEN GOTO 2700
2620 LET V0$="Z",V1$="C",V2$=BEGVAL$,V3$="",V0=7,V1=40,V2=O[4]+2
2630 LET V4$="Enter The Beginning Audit Number For The Report (<F1>=First)"
2640 GOSUB 7000
2650 ON V3 GOTO 2660,2780,3900,2600,3900
2660 IF V$="" THEN GOTO 2780
2665 LET BEGVAL$=V$
2670 PRINT @(V1,V2),BEGVAL$,'CL',
2690 GOTO 3900
2700 REM " --- Beginning Value"
2720 LET V0$="D",V1$="C6N",V2$=BEGVAL$,V3$="",V0=6,V1=40,V2=O[4]+2
2730 LET V4$="Enter The Beginning Date For The Report (<F1>=First/<F3>=Current Date)"
2740 GOSUB 7000
2750 ON V3 GOTO 2760,2780,3900,2755,3900
2755 LET V$=F0$(18,6)
2760 LET V$=V$+"      ",V$=V$(1,6)
2762 IF FNP$(V$)="" THEN GOTO 2780
2765 LET BEGVAL$=V$
2770 PRINT @(V1,V2),FNB6$(BEGVAL$),'CL',
2775 GOTO 2790
2780 LET BEGVAL$=""
2785 PRINT @(V1,V2),"First",'CL',
2790 GOTO 3900
2800 REM " --- Ending Value"
2805 IF POS("4"=ACTION$)=0 THEN GOTO 3900
2810 IF SEQUENCE$<>"A" THEN GOTO 2900
2820 LET V0$="Z",V1$="C",V2$=ENDVAL$,V3$="",V0=7,V1=40,V2=O[4]+4
2830 LET V4$="Enter The Ending Audit Number For The Report (<F1>=Last)"
2840 GOSUB 7000
2850 ON V3 GOTO 2860,2980,3900,2800,3900
2860 IF V$="" THEN GOTO 2980
2865 IF BEGVAL$<>"" THEN IF V$<BEGVAL$ THEN GOTO 2800
2870 LET ENDVAL$=V$
2880 PRINT @(V1,V2),ENDVAL$,'CL',
2890 GOTO 3900
2900 REM " --- Ending Date"
2920 LET V0$="D",V1$="C6N",V2$=ENDVAL$,V3$="",V0=6,V1=40,V2=O[4]+4
2930 LET V4$="Enter The Ending Date For The Report (<F1>=Last/<F3>=Current Date)"
2940 GOSUB 7000
2950 ON V3 GOTO 2960,2980,3900,2955,3900
2955 LET V$=F0$(18,6)
2960 LET V$=V$+"      ",V$=V$(1,6)
2962 IF FNP$(V$)="" THEN GOTO 2980
2963 IF BEGVAL$<>"" THEN IF V$<BEGVAL$ THEN GOTO 2900
2965 LET ENDVAL$=V$
2970 PRINT @(V1,V2),FNB6$(ENDVAL$),'CL',
2975 GOTO 2990
2980 LET ENDVAL$=""
2985 PRINT @(V1,V2),"Last",'CL',
2990 GOTO 3900
3900 REM " --- Next field"
3915 IF V3=4 THEN GOSUB 5000; GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,1200,1300,1400,1500,1600,1700,1800,2600,2800,1900,2000,2100,2200,2400,2200,2400,2200,2400,2200,2400,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 IF POS("P"=ACTION$)>0 THEN GOTO 4500
4020 IF FIELDS=0 THEN GOTO 4500
4030 IF POS("1"=ACTION$)=0 THEN GOTO 4050
4035 GOSUB 6900
4040 IF STATUS>0 THEN GOTO 1000
4050 LET V4$="Is The Above Information Correct (Y/N)?"
4060 LET V0$="Y",V1$="C^",V2$="Y",V3$="",V0=1,V2=22,V1=FNV(V4$)
4070 GOSUB 7000
4080 ON V3 GOTO 4090,4000,4000,4000,4900
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4500 REM " --- Run overlay"
4590 RUN "GLR.AB"
4900 REM " --- Run Menu Overlay"
4990 RUN MENU$,ERR=9900
5000 REM " --- Display"
5010 LET V1=40
5020 IF POS("1"=ACTION$)=0 THEN GOTO 5100
5025 LET V2=O[1]
5035 GOSUB 5400
5040 LET V$=BEGYEAR$,V2=O[1]+2
5050 GOSUB 5500
5055 LET V2=O[1]+4
5060 GOSUB 5450
5070 LET V$=ENDYEAR$,V2=O[1]+6
5080 GOSUB 5500
5100 IF POS("2"=ACTION$)=0 THEN GOTO 5150
5110 LET V$=BEGACCT$,V2=O[2]
5120 IF V$="" THEN GOSUB 5600 ELSE GOSUB 5700
5130 LET V$=ENDACCT$,V2=O[2]+2
5135 IF V$="" THEN GOSUB 5650 ELSE GOSUB 5700
5140 GOSUB 5550
5150 IF POS("3"=ACTION$)>0 THEN LET V$=JOURNAL$; GOSUB 5750
5160 IF POS("4"=ACTION$)>0 THEN GOSUB 5800; GOSUB 5850
5170 IF POS("5"=ACTION$)>0 THEN GOSUB 5900
5180 IF POS("7"=ACTION$)=0 THEN GOTO 5210
5185 LET V2=O[7]
5190 GOSUB 5400
5195 LET V$=BEGYEAR$,V2=O[7]+2
5200 GOSUB 5500
5210 IF POS("8"=ACTION$)>0 THEN GOSUB 5950
5215 IF POS("R"=ACTION$)=0 THEN GOTO 5290
5220 FOR X=1 TO ID
5230 LET SELECTION$=ID$(X,1),FLAG$=AU$(X,1),V2=O[6]+(X-1)
5240 GOSUB 6200
5250 NEXT X
5290 RETURN 
5400 REM " --- Display Beginning Period"
5410 IF BEGPER$="" THEN GOTO 5435
5415 LET V=NUM(BEGPER$,ERR=5435)
5420 CALL "SYC.PA",SYS01_DEV,V,P[2],V0$,V1$,STATUS
5425 PRINT @(V1,V2),BEGPER$," ",PERIODS$((V-1)*P1+1,P1)," (",FNM$(V0$)," to ",FNM$(V1$),")",'CL',
5430 GOTO 5440
5435 PRINT @(V1,V2),'CL',"First"
5440 RETURN 
5450 REM " --- Display Ending Period"
5460 IF ENDPER$="" THEN GOTO 5485
5465 LET V=NUM(ENDPER$,ERR=5485)
5470 CALL "SYC.PA",SYS01_DEV,V,P[2],V0$,V1$,STATUS
5475 PRINT @(V1,V2),ENDPER$," ",PERIODS$((V-1)*P1+1,P1)," (",FNM$(V0$)," to ",FNM$(V1$),")",'CL',
5480 GOTO 5490
5485 PRINT @(V1,V2),"Last",'CL',
5490 RETURN 
5500 REM " --- Display Beginning/Ending Year"
5510 PRINT @(V1,V2),FNYY21_YY$(V$),'CL',
5540 RETURN 
5550 REM " --- Display Wildcard"
5560 IF WILDCARD$="" THEN PRINT @(V1,O[2]+4),"None",'CL', ELSE PRINT @(V1,O[2]+4),WILDCARD$,'CL',
5590 RETURN 
5600 REM " --- Display Beginning G/L Account"
5610 PRINT @(V1,O[2]),"First",'CL',
5640 RETURN 
5650 REM " --- Display Ending G/L Account"
5660 PRINT @(V1,O[2]+2),"Last",'CL',
5690 RETURN 
5700 REM " --- Display G/L Account"
5710 LET A0$(3)=V$,A1$(1,35)="(Not On File)",STATUS=1
5720 FIND (GLM01_DEV,KEY=A0$,DOM=5730)IOL=GLM01A
5725 LET STATUS=0
5730 PRINT @(V1,V2),FNF$(A0$(3,P[3]),M0$)," ",A1$(1,W),'CL',
5740 RETURN 
5750 REM " --- Display Journal ID Code"
5760 IF V$<>"" THEN GOTO 5775
5763 LET STATUS=0
5765 PRINT @(V1,O[3]),"All",'CL',
5770 GOTO 5790
5775 LET C0$(1)=N0$+V$,C0$(5,20)="(Not On File)",STATUS=1
5780 FIND (GLM03_DEV,KEY=C0$(1,4),DOM=5785)IOL=GLM03A
5783 LET STATUS=0
5785 PRINT @(V1,O[3]),C0$(3,2)," ",C0$(5,20),
5790 RETURN 
5800 REM " --- Display By Date/By Audit Number"
5805 IF SEQUENCE$="A" THEN GOTO 5830
5810 PRINT @(V1,O[4]),SEQUENCE$," (By Date)",'CL',
5815 IF BEGVAL$<>"" THEN PRINT @(V1,O[4]+2),FNB6$(BEGVAL$),'CL', ELSE PRINT @(V1,O[4]+2),"First",'CL',
5820 IF ENDVAL$<>"" THEN PRINT @(V1,O[4]+4),FNB6$(ENDVAL$),'CL', ELSE PRINT @(V1,O[4]+4),"Last",'CL',
5825 GOTO 5845
5830 PRINT @(V1,O[4]),SEQUENCE$," (By Audit Number)",'CL',
5835 IF BEGVAL$<>"" THEN PRINT @(V1,O[4]+2),BEGVAL$,'CL', ELSE PRINT @(V1,O[4]+2),"First",'CL',
5840 IF ENDVAL$<>"" THEN PRINT @(V1,O[4]+4),ENDVAL$,'CL', ELSE PRINT @(V1,O[4]+4),"Last",'CL',
5845 RETURN 
5850 REM " --- Display Page Break"
5860 IF PAGEBREAK$="Y" THEN PRINT @(V1,O[4]+6),"Yes" ELSE PRINT @(V1,O[4]+6),"No "
5890 RETURN 
5900 REM " --- Display Detail or Summary"
5910 IF DETAIL$="D" THEN PRINT @(V1,O[5]),"Detail",'CL', ELSE PRINT @(V1,O[5]),"Summarized",'CL',
5940 RETURN 
5950 REM " --- Display Include Account Numbers"
5960 IF INCLUDE$="Y" THEN PRINT @(V1,O[8]),"Yes" ELSE PRINT @(V1,O[8]),"No "
5990 RETURN 
6000 REM " --- Journal ID Lookup"
6010 LET NUMBER=1,CHANNEL=GLM03_DEV,RECORD$="",TITLE$="Journal ID Codes"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=2
6050 LET FIELD[1]=1,POSITION[1]=5,LENGTH[1]=20
6060 LET ROW=V2+1,COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,V$="SKIP"
6070 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
6200 REM " --- Print GLM-02 description/title"
6210 GOSUB 6300
6220 PRINT @(20,V2),'CL',SELECTION$,@(24),FLAG$,@(30),DESCRIPTION$,
6290 RETURN 
6300 REM " --- Retrieve GLM-02 description and column heading"
6310 LET STATUS=0,DESCRIPTION$="",TITLE$=""
6320 IF FNP$(SELECTION$)="" AND FNP$(FLAG$)="" THEN GOTO 6390
6350 CALL "GLC.FA",GLM08_DEV,N0$,SELECTION$,FLAG$,DESCRIPTION$,X$,STATUS
6390 RETURN 
6400 REM " --- Beginning/Ending Period/Year"
6410 LET IMAGE$[ROW]="Beginning Period",IMAGE$[ROW+2]="Beginning Year"
6415 LET IMAGE$[ROW+4]="Ending Period",IMAGE$[ROW+6]="Ending Year"
6420 LET O[1]=ROW,L1=ROW+1,ROW=ROW+8,L2=ROW-1,FIELDS=FIELDS+1
6440 RETURN 
6450 REM " --- Accounts"
6460 LET IMAGE$[ROW]="Beginning Account Number"
6465 LET IMAGE$[ROW+2]="Ending Account Number",IMAGE$[ROW+4]="Wildcard Mask"
6470 LET O[2]=ROW,L1=ROW+1,ROW=ROW+6,L2=ROW-1,FIELDS=FIELDS+1
6490 RETURN 
6500 REM " --- Journal"
6510 LET IMAGE$[ROW]="Journal ID"
6520 LET O[3]=ROW,L1=ROW+1,ROW=ROW+2,L2=ROW-1,FIELDS=FIELDS+1
6540 RETURN 
6550 REM " --- Date/Audit"
6560 LET IMAGE$[ROW]="By Date Or Audit Number",IMAGE$[ROW+2]="Beginning Audit Number Or Date"
6565 LET IMAGE$[ROW+4]="Ending Audit Number Or Date",IMAGE$[ROW+6]="Page Break"
6570 LET O[4]=ROW,L1=ROW+1,ROW=ROW+8,L2=ROW-1,FIELDS=FIELDS+1
6590 RETURN 
6600 REM " --- Detail/Summary"
6610 LET IMAGE$[ROW]="Detail Or Summary"
6620 LET O[5]=ROW,L1=ROW+1,ROW=ROW+2,L2=ROW-1,FIELDS=FIELDS+1
6640 RETURN 
6650 REM " --- Period/Year"
6660 LET IMAGE$[ROW]="For Period",IMAGE$[ROW+2]="For Year"
6670 LET O[7]=ROW,L1=ROW+1,ROW=ROW+4,L2=ROW-1,FIELDS=FIELDS+1
6690 RETURN 
6700 REM " --- Include Accounts?"
6710 LET IMAGE$[ROW]="Include Account Numbers"
6720 LET O[8]=ROW,L1=ROW+1,ROW=ROW+2,L2=ROW-1,FIELDS=FIELDS+1
6740 RETURN 
6750 REM " --- Record ID's"
6770 PRINT 'SB',@(20,ROW+1),"ID  Type  Description",'SF',
6780 LET O[6]=ROW+2,FIELDS=FIELDS+1
6790 RETURN 
6900 REM " --- Verify that beginning period/year > ending period/year"
6910 DIM MESSAGE$[1]
6920 LET STATUS=0,X1$=BEGYEAR$+BEGPER$,X2$=ENDYEAR$+ENDPER$
6930 IF BEGPER$="" THEN LET X1$=X1$+"01"
6940 IF ENDPER$="" THEN LET X2$=X2$+STR(P[0]:"00")
6950 IF X1$<=X2$ THEN GOTO 6990
6960 LET MESSAGE$[0]="Beginning Period/Year Cannot Be Greater Than Ending Period/Year"
6970 LET MESSAGE$[1]="            Press <Enter> To Continue",STATUS=1
6980 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8065 DEF FNM$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

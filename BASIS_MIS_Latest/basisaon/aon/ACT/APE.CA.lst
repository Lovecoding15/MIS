0010 REM "APE - Manual Check Entry"
0020 REM "Program APE.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETESC 9000
0055 SETERR 9000
0060 ON O0 GOTO 0070,1000
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- OPEN/LOCK FILES"
0110 LET FILES=20
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="APE-02",FILES$[2]="APE-12",FILES$[3]="APE-22"
0130 LET FILES$[4]="APM-01",FILES$[5]="APM-02",FILES$[6]="APM-03"
0135 LET FILES$[7]="APT-01",FILES$[8]="APT-11",FILES$[9]="SYS-01"
0140 LET FILES$[10]="APE-05",FILES$[11]="APM-04",FILES$[12]="APM-09"
0145 LET FILES$[13]="APM-10"
0160 CALL "SYC.DA",1,1,13,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET APE02_DEV=CHANNELS[1],APE12_DEV=CHANNELS[2],APE22_DEV=CHANNELS[3]
0180 LET APM01_DEV=CHANNELS[4],APM02_DEV=CHANNELS[5],APM03_DEV=CHANNELS[6]
0185 LET APT01_DEV=CHANNELS[7],APT11_DEV=CHANNELS[8],SYS01_DEV=CHANNELS[9]
0190 LET APE05_DEV=CHANNELS[10],APM04_DEV=CHANNELS[11],APM09_DEV=CHANNELS[12]
0195 LET APM10_DEV=CHANNELS[13]
0200 REM " --- IOLISTS"
0260 APS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters
0405 DIM P[2],G[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 FOR X=0 TO 4
0455 LET G[X]=NUM(G2$(X*2+1,2),ERR=0460)
0460 NEXT X
0465 LET G[2]=FNYY_YEAR(G2$(5,2))
0480 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(9,1))
0500 REM " --- Init data
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 DIM A0$(33),A[2],Y0$(44),Y[2],W0$(40),W1$(G[4]),W[1],X[8],W2$(10)
0530 LET K0=1,K9=8,L0=15,L9=20,A0$(1,2)=N0$
0550 CALL "SYC.RM",M1$,M1,7,14
0560 CALL "SYC.RM",G7$,G7,5,7
0580 LET M8$="(###)-###-####",M9$="###-##-####",G5=LEN(G5$),M1=LEN(M1$),G6=LEN(G6$)
0600 REM " --- Additional File Opens
0650 LET GL$="N",STATUS=0
0655 CALL "GLC.BA",ERR=0660,SOURCE$,N2$,GLW11$,GL$,STATUS
0660 IF STATUS<>0 THEN GOTO 9900
0665 IF GL$<>"Y" THEN GOTO 0700
0670 LET FILES$[19]="GLM-01",FILES$[20]=GLW11$,OPTIONS$[20]="C"
0675 CALL "SYC.DA",1,19,20,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS>0 THEN GOTO 9900
0690 LET GLM01_DEV=CHANNELS[19],GLW11_DEV=CHANNELS[20]
0700 REM " --- Background
0710 PRINT 'SB',
0720 IF P3$(1,1)="Y" THEN PRINT @(6,3),"AP Type:",
0725 CALL "SYC.WC",1,0,80,0,0,6,0
0730 PRINT 'SB',@(1,4),"Check Number:",@(27),"Transaction:",@(7,5),"Vendor:",@(0,7),"Check Date",@(14+M1,7),"Inv Amt",@(13+2*M1),"Discount",@(12+3*M1),"Check Amt",
0740 IF P3$(3,1)="Y" THEN PRINT @(12+4*M1,7),"Retention",
0745 CALL "SYC.WC",1,0,80,0,0,9,0
0750 PRINT 'SB',@(0,10),"Invoice No",@(13),"Date",@(14+M1),"Inv Amt",@(13+2*M1),"Discount",@(14+3*M1),"Payment",
0755 IF P3$(2,1)="Y" THEN PRINT @(20,10),"DC",
0760 IF P3$(3,1)="Y" THEN PRINT @(12+4*M1,10),"Retention",
0770 CALL "SYC.WC",1,0,80,0,0,13,0
0775 PRINT 'SB',@(0,14),"Seq",@(5),"Account",@(47+M1),"Amount",
0780 IF G4$(3,1)="Y" THEN PRINT @(64+G7),"Units",
0785 IF P3$(16,1)="Y" THEN PRINT @(20,14),"Miscellaneous",
0790 PRINT 'SF',
4000 REM " --- Overlay
4020 LET V=G[1]
4040 CALL "SYC.PA",SYS01_DEV,V,G[2],V0$,V9$,STATUS
4060 LET R9$=V0$
4100 RUN "APE.CB"
8000 REM " --- Functions
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "GLE - Allocation Entry (Heading Overlay)"
0020 REM "Program GLE.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0060 ON O0 GOTO 0070,1000
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-01",FILES$[2]="GLE-03",FILES$[3]="GLE-13"
0140 LET FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM01_DEV=CHANNELS[1],GLE03_DEV=CHANNELS[2],GLE13_DEV=CHANNELS[3]
0185 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLIST's"
0210 GLE03A: IOLIST A0$(1),A1$,A[ALL]
0230 GLM01A: IOLIST C0$(1),C1$(1)
0260 GLS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$,M4$,M5$
0270 SYS01T: IOLIST X$,F0$,X$,F2$,X$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM P[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0440 FOR X=0 TO 4
0450 LET P[X]=NUM(P2$(X*2+1,2),ERR=0460)
0460 NEXT X
0465 LET P[2]=FNYY_YEAR(P2$(5,2))
0490 LET K9=P[4]+5,K8=K9-3,O0=1,L1=5
0500 REM " --- Initializations"
0510 DIM A0$(K8),A[4],C0$(2+P[4]),C1$(35)
0560 LET M0=LEN(M0$),M4$="##0.00%",M4=LEN(M4$)
0590 LET A0$(1)=N0$,X$=" Destination Accounts "
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,L1-1,0
0720 PRINT 'SB',@(0,3),"Source Account:",@(FNC(X$,80),L1-1),X$,@(1,L1),"Seq",@(6),"Account Number",@(67),"Percentage",'SF',
0900 REM " --- Position File"
0990 READ (GLE03_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Source Account"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(GLE03_DEV,END=1050)
1030 IF V2$(1,2)<>N0$ THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3,P[3])
1050 LET V4$="Enter A Valid G/L Account Number (<F3>=Lookup)"
1055 LET V0$="M",V1$="KC",V3$=M0$,V0=P[4],V1=16,V2=3
1060 GOSUB 7000
1063 IF V3<>3 THEN GOTO 1075
1065 CALL "SYC.LG",V$
1070 IF V$="" THEN GOTO 0900
1075 IF V3=4 THEN GOTO 9900
1080 LET A0$(1,2)=N0$,A0$(3)=V$,C0$(1)=A0$
1085 FIND (GLM01_DEV,KEY=C0$,DOM=1000)IOL=GLM01A
1090 PRINT @(V1,V2),FNF$(C0$(3,P[3]),M0$)," ",C1$,
1200 REM " --- Retrieve record"
1210 LET A0$(1)=C0$
1220 READ (GLE03_DEV,KEY=A0$,DOM=1400)IOL=GLE03A
1300 REM " --- Display"
1340 GOTO 4000
1400 REM " --- New record"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1490,1400,1400,1400,9900
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Initializations for new record"
1510 DIM A[4]
4000 REM " --- Write and run detail overlay"
4010 WRITE (GLE03_DEV,KEY=A0$)IOL=GLE03A
4090 RUN "GLE.EB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "APR - CHECK PRINTING/MOORE FORM #7010A.O."
0020 REM "Program APR.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- OPEN/LOCK FILES"
0110 LET FILES=12
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APE-04",FILES$[2]="APE-04",FILES$[3]="APM-01"
0140 LET FILES$[4]="APM-03",FILES$[5]="APM-04",FILES$[6]="APM-10"
0150 LET FILES$[7]="APW-01",FILES$[8]="APT-02",FILES$[9]="SYS-01"
0155 LET FILES$[10]="SYS-01"
0160 CALL "SYC.DA",1,1,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET APE04_DEV1=CHANNELS[1],APE04_DEV2=CHANNELS[2],APM01_DEV=CHANNELS[3]
0180 LET APM03_DEV=CHANNELS[4],APM04_DEV=CHANNELS[5],APM10_DEV=CHANNELS[6]
0185 LET APW01_DEV=CHANNELS[7],APT02_DEV=CHANNELS[8],SYS01_DEV1=CHANNELS[9]
0190 LET SYS01_DEV2=CHANNELS[10]
0200 REM " --- IOLISTS"
0210 APM10A: IOLIST X0$
0220 APM01A: IOLIST *,X0$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- PARAMETERS"
0405 DIM P[2],G[4]
0410 FIND (SYS01_DEV1,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N3$=F5$
0430 FIND (SYS01_DEV1,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0440 FIND (SYS01_DEV1,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 FOR X=0 TO 4
0455 LET G[X]=NUM(G2$(X*2+1,2),ERR=0460)
0460 NEXT X
0465 LET G[2]=FNYY_YEAR(G2$(5,2))
0470 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(9,1))
0500 REM " --- INIT DATA"
0530 DIM J$(80,"-"),J0$(10,"0"),J1$(20," "),H9$(20,$0A$)
0540 DIM T[2],A[3],W[3],E[1]
0545 LET M1$="########.00CR",M7$="00000"
0550 LET M0=LEN(M0$),M1=LEN(M1$),G5=LEN(G5$),P9$=F0$(7,3),P6$="  "
0560 LET L9=12,L=0,H0=0
0570 LET M8$="*#######",M9$="*"+M1$(2,10),M9=LEN(M9$),M7=M9-10
0586 IF M7<0 THEN LET M7=0
0590 IF P[2]=9 THEN LET M7$="00000-0000"
0600 REM  " --- Additional File Opens"
0610 LET GL$="N",STATUS=0
0620 CALL "GLC.BA",ERR=0630,SOURCE$,N2$,GLW11$,GL$,STATUS
0630 IF STATUS<>0 THEN GOTO 9900
0640 IF GL$="N" THEN LET P3$(8,1)="N"
0650 IF GL$<>"Y" THEN GOTO 0700
0660 LET FILES$[11]="GLM-01",FILES$[12]=GLW11$,OPTIONS$[10]="C"
0670 CALL "SYC.DA",1,11,12,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS>0 THEN GOTO 9900
0690 LET GLM01_DEV=CHANNELS[11],GLW11_DEV=CHANNELS[12]
0700 REM " --- BACKGROUND"
0710 FOR X=4 TO 14 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(28,3),"Check Date:",@(26,5),"Check Number:",@(25,7),"Restart (Y/N):",
0760 PRINT @(25,11),"Vendor Number:",
0780 IF P3$(1,1)="Y" THEN PRINT @(31,9),"AP Type:",
0790 PRINT 'SF',
1000 REM " --- CHECK DATE"
1002 PRINT 'CF',
1005 IF P3$(1,1)<>"N" THEN LET I0=0,V3=0; GOTO 3900
1020 LET V0$="D",V1$="KER",V2$=P9$,V0=8,V1=40,V2=3,I0=0
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1060 LET P9$=V$,STATUS=0
1070 IF GL$="Y" THEN CALL "GLC.EA",P9$,"Y",PERIOD$,YEAR$,STATUS
1075 IF STATUS>99 THEN GOTO 1000
1090 GOTO 3900
1100 REM " --- CHECK NUMBER"
1105 IF P3$(1,1)<>"N" THEN LET V3=0; GOTO 3900
1120 LET V0$="Z",V1$="KR",V2$=P8$,V0=7,V1=40,V2=5
1140 GOSUB 7000
1160 LET P8$=V$
1190 GOTO 3900
1200 REM " --- RESTART"
1220 LET V0$="Y",V1$="R",V2$="N",V0=1,V1=40,V2=7
1240 GOSUB 7000
1260 LET P7$=V$
1290 GOTO 3900
1300 REM " --- AP TYPE"
1310 IF P7$="N" THEN GOTO 3900
1320 IF P3$(1,1)="N" THEN GOTO 3900
1325 LET P6$="  "
1330 LET V0$="S",V1$="K",V2$="",V0=2,V1=40,V2=9,V4$="Enter A Valid AP Type (<F3>=Lookup)"
1340 GOSUB 7000
1342 IF V3=4 THEN GOTO 9900
1345 IF V3=3 THEN GOSUB 3100
1350 LET P6$(1,2)=V$
1360 FIND (APM10_DEV,KEY=N0$+"A"+P6$,DOM=1300)IOL=APM10A
1370 PRINT @(43,V2),X0$(6,20),
1390 GOTO 3900
1400 REM " --- VENDOR"
1410 IF P7$="N" THEN GOTO 3900
1420 LET V0$="M",V1$="K",V2$="",V3$=P1$,V0=P[1],V1=40,V2=11,V4$="Enter A Valid Vendor Number (<F1>=Temporary Vendor/<F3>=Lookup)"
1430 GOSUB 7000
1432 IF V3=2 THEN GOTO 3900
1435 IF V3=3 THEN GOSUB 3000
1437 IF V3=4 THEN GOTO 4000
1438 IF V3=1 THEN LET P5$="TEMP  "; PRINT @(V1,V2),"Temp  ",@(41+M0,V2),'CL',"Temporary Vendor"; GOTO 1470
1440 LET P5$=V$
1450 FIND (APM01_DEV,KEY=N0$+P5$,DOM=1400)IOL=APM01A
1460 PRINT @(41+M0,V2),X0$(1,30),
1490 GOTO 3900
3000 REM " --- ALPHA SEARCH"
3040 CALL "SYC.LV",V$
3050 PRINT @(V1,V2),J1$(1,10),@(V1),
3060 IF V$="" THEN GOTO 3090
3070 PRINT @(V1,V2),'CL',V$,
3090 RETURN 
3100 REM " --- A/P TYPE LOOK-UP"
3110 LET V4=1,V5=APM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="A"
3140 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
3190 RETURN 
3900 REM " --- NEW FIELD"
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,1200,1300,1400,4000
4000 REM " --- CORRECT"
4010 LET V0$="Y",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 CALL "SYC.GA",7,1,"","",STATUS
4110 IF STATUS THEN GOTO 9900
4200 RUN "APR.EB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- FUNCTIONS"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

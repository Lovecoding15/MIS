0010 REM "GLM - G/L Financial Report Maintenance (Menu Overlay)"
0020 REM "Program GLM.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0060 ON O0 GOTO 0070,4900,1000
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-12",FILES$[2]="GLM-22",FILES$[3]="GLM-32"
0140 LET FILES$[4]="GLE-06",FILES$[5]="SYS-01"
0160 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM12_DEV=CHANNELS[1],GLM22_DEV=CHANNELS[2],GLM32_DEV=CHANNELS[3]
0185 LET GLE06_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 GLM12A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1)
0220 GLM22A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1)
0230 GLM32A: IOLIST C0$(1),C1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 GLS01A: IOLIST P0$,P1$,P2$(1),P3$(1),P4$(1),M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 DIM P2$(10),P3$(325),P4$(32),P[4]
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N3$=F5$
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0450 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),MP=13
0480 FOR X=0 TO 4
0485 LET P[X]=NUM(P2$(X*2+1,2),ERR=0490)
0490 NEXT X
0495 LET P[2]=FNYY_YEAR(P2$(5,2))
0500 REM " --- Initializations"
0510 DIM A0$(30),A1$(15),A2$(12),A3$(10),A4$(10),B0$(8),B1$(56),B2$(20),B3$(24),C0$(7),C1$(7)
0530 DIM PERIOD$[7](1),ACT_BUDG$[7](1),AMT_UNIT$[7](1),RATIO_PER$[7](1),DEFAULT_POS$[7](3); REM "Temp screen vars for GLM-32 C1$"
0590 LET A0$(1)=N0$,L0=5,O0=1,K9=8
0600 REM " --- Channel Variables (Used by Overlays)"
0610 LET PRINTER=7
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0750 PRINT 'SB',@(0,3),"Report Number:",'SF',
0900 REM " --- Position file"
0990 READ (GLM12_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Report Number"
1005 PRINT 'CF',
1010 LET V2$="",V2$=KEY(GLM12_DEV,END=1040)
1020 IF V2$(1,2)<>N0$ THEN LET V2$=""
1030 IF V2$<>"" THEN LET V2$=V2$(3,2)
1040 LET V0$="Z",V1$="C",V3$="##",V0=2,V1=15,V2=3,I0=0
1045 LET V4$="Enter A Valid Financial Report Number (<F3>=Lookup)"
1050 GOSUB 7000
1055 ON V3 GOTO 1070,1000,1000,1060,9900
1060 CALL "GLC.LF",N0$,V$
1070 IF V$="" THEN GOTO 1000
1090 LET A0$(1)=N0$+V$,A0$(5)="0000"
1200 REM " --- Retrieve record"
1210 READ (GLM12_DEV,KEY=A0$(1,K9),DOM=1400)IOL=GLM12A
1220 GOSUB GET_TEMPS; REM "Get C1$ data from GLM-32"
1300 REM " --- Display record"
1320 GOSUB 5000
1390 GOTO 4900
1400 REM " --- New record"
1410 LET V4$="Is This A New Record (Y/N)?"
1420 LET V0$="Y",V1$="C",V2$="N",V3$="",V0=1,V1=FNV(V4$),V2=22
1430 GOSUB 7000
1440 ON V3 GOTO 1490,1400,1400,1400,1000
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record"
1510 DIM C1$(7)
1520 LET A1$=M1$,A2$=M2$,A3$=M3$,A4$=M3$
4000 REM " --- Run header maintenance overlay"
4010 LET O0=2
4090 RUN "GLM.DB"
4900 REM " --- Process submenu"
4905 PRINT @(0,22),'CL',
4910 CLOSE (PRINTER,ERR=4915)
4920 DIM MENUS$[1]
4925 LET MENUS=1,MENUS$[MENUS]="GLM.DA",WIN_X=0,WIN_Y=L0+1,O0=1
4930 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4940 CALL "SYC.WC",1,0,80,0,0,L0-1,0
4950 PRINT @(0,L0),'CE',
4960 IF SELECTION$="" THEN GOTO 1000
4980 PRINT @(FNC(DESCRIPTION$,80),L0-1),'SB',DESCRIPTION$,'SF',
4990 RUN SELECTION$,ERR=4900
5000 REM " --- Display"
5010 PRINT @(15,3),A0$(3,2),@(18),A0$(9,20),
5090 RETURN 
6000 REM " --- Assign GLM-32 C1$ vars to temps"
6010 GET_TEMPS: 
6020 READ (GLM32_DEV,KEY=A0$(1,4),DOM=6030)
6030 FOR CNT=0 TO 7
6040 LET K$=KEY(GLM32_DEV,END=6090)
6050 IF K$(1,4)<>A0$(1,4) THEN EXITTO 6095
6060 READ (GLM32_DEV,KEY=K$)IOL=GLM32A
6080 LET PERIOD$[CNT](1)=C1$(1,1),ACT_BUDG$[CNT](1)=C1$(2,1),AMT_UNIT$[CNT](1)=C1$(3,1),RATIO_PER$[CNT](1)=C1$(4,1),DEFAULT_POS$[CNT](1)=C1$(5,3)
6090 NEXT CNT
6095 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "GLC - Transaction Date Verification (Public/Called)"
0020 REM "Program GLC.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0047 REM "WHEN$    : Transaction Date (Input)"
0050 REM "VERIFY$  : Verification Flag (Input)"
0053 REM "PERIOD$  : Fiscal Period (Returned)"
0055 REM "YEAR$    : Fiscal Year (Returned)"
0057 REM "STATUS   : 0=No Error (Returned)"
0060 REM 
0080 SETESC 9900
0085 SETERR 9000
0090 ENTER WHEN$,VERIFY$,PERIOD$,YEAR$,STATUS
0400 REM " --- Parameters"
0410 DIM GLCONTROL$(640)
0420 LET MP=13,STATUS=0,PERIOD$="",YEAR$=""
0430 LET GLCONTROL$(1)=STBL("!GLCONTROL",ERR=9900)
0440 LET NP=NUM(GLCONTROL$(28,2))
0500 REM " --- Initializations"
0510 DIM LOCKED$(MP*13,"N")
0530 LET LOCKED$(1,MP)="YYYYYYYYYYYYY",LOCKED$(MP+1,MP)=GLCONTROL$(559,MP)
0540 LET APPERIOD$=GLCONTROL$(14,2),APYEAR$=FNYY_YY21$(GLCONTROL$(16,2))
0545 LET GLPERIOD$=GLCONTROL$(30,2),GLYEAR$=FNYY_YY21$(GLCONTROL$(32,2))
0550 LET APPERIOD=NUM(APPERIOD$),APYEAR=FNYY_YEAR(APYEAR$)
0555 LET GLPERIOD=NUM(GLPERIOD$),GLYEAR=FNYY_YEAR(GLYEAR$)
0560 LET YYMMDD$="",NEXT_APPERIOD=APPERIOD+1,NEXT_APYEAR=APYEAR
0570 IF NEXT_APPERIOD>NP THEN LET NEXT_APPERIOD=1,NEXT_APYEAR=APYEAR+1
0580 LET YEAR1=GLYEAR-1,YEAR2=GLYEAR,YEAR3=GLYEAR+1
0585 IF GLCONTROL$(43,1)="Y" THEN GOTO 0600
0590 LET YEAR1=GLYEAR-2,YEAR2=GLYEAR-1,YEAR3=GLYEAR
0595 LET LOCKED$(MP+1,MP)=LOCKED$(1,MP),LOCKED$(MP*2+1,MP)=GLCONTROL$(559,MP)
0600 REM " --- Standardize Date Formate To YYMMDD"
0610 IF LEN(WHEN$)=3 THEN LET YYMMDD$=FNYY_YY21$(FNM$(FNC$(WHEN$)))
0620 IF LEN(WHEN$)=6 THEN LET YYMMDD$=FNYY_YY21$(WHEN$)
0630 IF YYMMDD$="" THEN GOTO 9200
0800 REM " --- Post to G/L?"
0810 IF GLCONTROL$(13,1)="N" THEN GOTO 0850
0820 IF GLCONTROL$(44,1)="N" THEN GOTO 0850
0830 IF POS(GLCONTROL$(44,1)="PD")>0 THEN GOTO 0900
0850 REM " --- Not posting, set period/year based on application"
0860 LET PERIOD$=APPERIOD$,YEAR$=APYEAR$
0890 GOTO 4000
0900 REM " --- Is this date same as the last one verified?"
0910 IF YYMMDD$<>GLCONTROL$(81,6) THEN GOTO 1000
0920 LET PERIOD$=GLCONTROL$(87,2),YEAR$=FNYY_YY21$(GLCONTROL$(89,2))
0990 GOTO 9900
1000 REM " --- Determine which period/year that date falls into"
1010 LET PERIOD=0
1020 FOR Y=1 TO 3
1030 IF Y=1 THEN LET YEAR=YEAR1,X=91
1040 IF Y=2 THEN LET YEAR=YEAR2,X=169
1050 IF Y=3 THEN LET YEAR=YEAR3,X=247
1060 FOR M=1 TO NP
1070 IF YYMMDD$>GLCONTROL$(X+234,6) THEN GOTO 1110
1080 IF YYMMDD$<GLCONTROL$(X,6) THEN GOTO 1110
1090 LET PERIOD=M
1100 EXITTO 1150
1110 LET X=X+6
1120 NEXT M
1150 IF PERIOD>0 THEN EXITTO 1200
1190 NEXT Y
1200 REM " --- Does date fall into 3 year G/L range?"
1210 LET PERIOD$=STR(PERIOD:"00"),YEAR$=FNYEAR_YY21$(YEAR)
1220 IF PERIOD>0 THEN GOTO 1250
1230 LET STATUS=101
1240 GOTO 2000
1250 REM " --- Does date fall into the current application period?"
1260 IF PERIOD=APPERIOD AND YEAR=APYEAR THEN GOTO 2000
1300 REM " --- Does date fall into the next application period?"
1310 IF PERIOD=NEXT_APPERIOD AND YEAR=NEXT_APYEAR THEN GOTO 2100
1400 REM " --- Does date occur after end of the next application period?"
1410 LET STATUS=1
1420 IF FNN(NEXT_APYEAR,NEXT_APPERIOD)>FNN(YEAR,PERIOD) THEN GOTO 1450
1430 LET STATUS=2
1440 GOTO 2200
1450 REM " --- Processing Journal Entries when posting by period?"
1460 IF GLCONTROL$(44,1)<>"P" THEN GOTO 1500
1470 IF GLCONTROL$(45,1)="Y" THEN GOTO 1500
1490 GOTO 2000
1500 REM " --- Does PERIOD/YEAR fall into the current fiscal year?"
1510 IF YEAR=GLYEAR THEN GOTO 2400
1520 IF YEAR3>GLYEAR THEN IF YEAR=YEAR3 THEN GOTO 2200
1550 REM " --- Is Prior Year Closed flag set to Y?"
1560 IF GLCONTROL$(43,1)<>"Y" THEN GOTO 1600
1570 LET STATUS=103
1580 IF GLCONTROL$(44,1)<>"P" THEN GOTO 2000
1590 GOTO 2300
1600 REM " --- Is Allow Year End Adjustments flag set to Y?"
1610 IF GLCONTROL$(46,1)="Y" THEN GOTO 2400
1620 LET STATUS=103
1630 IF GLCONTROL$(44,1)<>"P" THEN GOTO 2000
1640 GOTO 2300
2000 REM " --- Set PERIOD/YEAR equal to current application period/year"
2010 LET PERIOD$=APPERIOD$,YEAR$=APYEAR$,PERIOD=APPERIOD,YEAR=APYEAR
2090 GOTO 2200
2100 REM " --- Set PERIOD/YEAR equal to next application period/year"
2110 LET PERIOD=NEXT_APPERIOD,YEAR=NEXT_APYEAR
2120 LET PERIOD$=STR(PERIOD:"00"),YEAR$=FNYEAR_YY21$(YEAR)
2200 REM " --- Does PERIOD/YEAR occur before current G/L period/year?"
2210 IF FNN(YEAR,PERIOD)>=FNN(GLYEAR,GLPERIOD) THEN GOTO 2400
2220 LET STATUS=3
2230 IF GLCONTROL$(44,1)<>"P" THEN GOTO 2400
2300 REM " --- Set PERIOD/YEAR equal to G/L period/year"
2310 LET PERIOD$=GLPERIOD$,YEAR$=GLYEAR$
2400 REM " --- Is PERIOD/YEAR locked?"
2410 LET Y=0
2420 IF YEAR=YEAR2 THEN LET Y=MP
2430 IF YEAR=YEAR3 THEN LET Y=MP*2
2440 IF LOCKED$(Y+PERIOD,1)<>"Y" THEN GOTO 3500
2500 REM " --- Is Post To Locked Periods flag set to Y?"
2510 IF GLCONTROL$(47,1)="Y" THEN GOTO 3500
2520 LET PERIOD$=APPERIOD$,YEAR$=APYEAR$
2530 LET STATUS=102
2540 IF GLCONTROL$(44,1)<>"P" THEN GOTO 3500
2550 LET PERIOD$=GLPERIOD$,YEAR$=GLYEAR$
3500 REM " --- Display warning/error message if verification requested"
3510 IF STATUS=0 THEN GOTO 4000
3520 IF VERIFY$<>"Y" THEN GOTO 4000
3525 LET MESSAGE1$="The Current "+GLCONTROL$(11,2)+" Period Is "+STR(APPERIOD)+" ("+FNB6$(GLCONTROL$(572,6))+" To "+FNB6$(GLCONTROL$(578,6))+")"
3530 IF STATUS=3 THEN LET MESSAGE1$="The Current GL Period Is "+STR(GLPERIOD)+" ("+FNB6$(GLCONTROL$(584,6))+" To "+FNB6$(GLCONTROL$(590,6))+")"
3535 LET X$="The "+FNB6$(YYMMDD$)+" Date "
3540 LET MESSAGE2$=X$+"Occurs "
3550 IF STATUS=1 THEN LET MESSAGE2$=MESSAGE2$+"Prior to The Current Period"
3560 IF STATUS=2 THEN LET MESSAGE2$=MESSAGE2$+"After The Next Period"
3570 IF STATUS=3 THEN LET MESSAGE2$=X$+"Falls Into A Prior G/L Period"
3580 IF STATUS=101 THEN LET MESSAGE2$=X$+"Is Not In The Prior, Current Or Next G/L Year"
3585 IF STATUS=102 THEN LET MESSAGE2$=X$+"Falls Into A Locked G/L Period"
3590 IF STATUS=103 THEN LET MESSAGE2$=X$+"Falls Into A Closed Fiscal Year"
3900 REM " --- Call message subroutine"
3910 LET MESSAGE3$="Press <Enter> To Continue"
3920 LET M=MAX(LEN(MESSAGE1$),LEN(MESSAGE2$))
3930 DIM MESSAGE$[2](M)
3940 LET MESSAGE$[0](FNE(MESSAGE1$,M))=MESSAGE1$
3950 LET MESSAGE$[1](FNE(MESSAGE2$,M))=MESSAGE2$
3960 LET MESSAGE$[2](FNE(MESSAGE3$,M))=MESSAGE3$
3990 CALL "SYC.XA",2,MESSAGE$[ALL],2,22,-1,V$,V3
4000 REM " --- All done"
4020 LET YEAR$=FNYY_YY21$(YEAR$)
4040 LET GLCONTROL$(81,6)=FNYY_YY21$(YYMMDD$),GLCONTROL$(87,4)=PERIOD$+YEAR$
4060 IF STATUS<>0 THEN LET GLCONTROL$(81,10)=""
4080 LET GLCONTROL$=STBL("!GLCONTROL",GLCONTROL$)
4100 GOTO 9900
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNE(Q$,Q)=MAX(INT((Q-LEN(Q$))/2+.5),1)
8060 DEF FNN(Q1,Q2)=Q1*100+Q2
8080 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 LET STATUS=E0
9230 IF STATUS=0 THEN LET STATUS=256
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Calling Program"
9950 EXIT 
9999 END

0010 REM "GLU - Purge Transaction History"
0020 REM "Program GLU.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLT-06",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLT06_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 GLT06A: IOLIST A0$(1),A1$(1),A2$,A[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 GLS01A: IOLIST P0$,P1$,P2$(1),P3$(1),P4$(1),M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 DIM P2$(10),P3$(325),P4$(32),P[4]
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N3$=F5$
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0450 LET M0=LEN(M0$)
0480 FOR X=0 TO 4
0485 LET P[X]=NUM(P2$(X*2+1,2),ERR=0490)
0490 NEXT X
0495 LET P[2]=FNYY_YEAR(P2$(5,2))
0500 REM " --- Initializations"
0510 DIM A0$(26+P[4]),A1$(64),A[4]
0550 LET PERIOD=P[1]-1,YEAR=P[2],M4$="00",LAST$=""
0560 IF PERIOD<1 THEN LET PERIOD=P[0],YEAR=YEAR-1
0570 LET PERIOD$=STR(PERIOD:"00"),YEAR$=FNYEAR_YY21$(YEAR)
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,6,0
0760 PRINT 'SB',@(32,3),"Period:",@(34,5),"Year:",'SF',
0800 REM " --- Period"
0805 LET V0$="N",V1$="C",V2$=PERIOD$,V3$=M4$,V0=2,V1=40,V2=3
0810 LET V4$="Purge Transactions Through Which Fiscal Period (1 Through "+STR(P[0])+")?"
0815 GOSUB 7000
0820 ON V3 GOTO 0825,0800,0800,0800,9900
0825 IF V<1 OR V>P[0] THEN GOTO 0800
0830 LET PERIOD$=STR(V:M4$),DESCRIPTION$=P3$((V-1)*16+27,16)
0835 PRINT @(V1,V2),PERIOD$," ",DESCRIPTION$,
0850 REM " --- Year"
0855 LET V0$="D",V1$="CY",V2$=YEAR$,V3$="",V0=2,V1=40,V2=5
0860 LET V4$="Purge Transactions Through Which Fiscal Year?"
0865 GOSUB 7000
0870 ON V3 GOTO 0875,0850,0800,0850,9900
0875 LET YEAR$=V$
0880 PRINT @(V1,V2),FNYY21_YY$(YEAR$),
0900 REM " --- Correct?"
0910 LET V4$="Is The Above Information Correct (Y/N)?"
0920 LET V0$="Y",V1$="C",V2$="Y",V3$="",V0=1,V2=22,V1=FNV(V4$)
0930 GOSUB 7000
0940 ON V3 GOTO 0950,0900,0900,0900,9900
0950 ON POS(V$="YN") GOTO 0900,0960,0800
0960 CALL "SYC.NB","Updating",M0,COLUMN
0970 REM " --- Disallow 'M'enu option in Error Routine
0975 LET EXIT_CTRL=1
0980 LET WHEN$=YEAR$+PERIOD$
0990 READ (GLT06_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Purge loop"
1010 LET K$=KEY(GLT06_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 IF K$(3,P[4])=LAST$ THEN GOTO 1100
1040 LET LAST$=K$(3,P[4])
1050 PRINT @(COLUMN,11),FNF$(K$(3,P[3]),M0$),
1100 REM 
1110 READ (GLT06_DEV)IOL=GLT06A
1140 IF K$(3+P[4],4)>WHEN$ THEN GOTO 1900
1150 REMOVE (GLT06_DEV,KEY=K$)
1900 REM 
1990 GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

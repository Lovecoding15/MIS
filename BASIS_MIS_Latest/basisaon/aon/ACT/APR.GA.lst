0010 REM "APR - Manual Check Register"
0020 REM "Program APR.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=13
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APE-02",FILES$[2]="APE-12",FILES$[3]="APE-22"
0140 LET FILES$[4]="APM-01",FILES$[5]="APM-03",FILES$[6]="APE-05"
0150 LET FILES$[7]="APT-01",FILES$[8]="SYS-01"
0155 LET FILES$[9]="APM-10",FILES$[10]="APT-02"
0157 LET OPTIONS$[1]="L",OPTIONS$[2]="L",OPTIONS$[3]="L"
0160 CALL "SYC.DA",1,1,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET APE02_DEV=CHANNELS[1],APE12_DEV=CHANNELS[2],APE22_DEV=CHANNELS[3]
0185 LET APM01_DEV=CHANNELS[4],APM03_DEV=CHANNELS[5],APE05_DEV=CHANNELS[6]
0190 LET APT01_DEV=CHANNELS[7],SYS01_DEV=CHANNELS[8]
0195 LET APM10_DEV=CHANNELS[9],APT02_DEV=CHANNELS[10]
0200 REM " --- IOLists
0210 APE02A: IOLIST A0$,A1$,A[ALL]
0215 APE05A: IOLIST D0$,D1$(1),D[ALL]
0220 APE12A: IOLIST W0$,W1$,W2$,W[ALL]
0225 APE22A: IOLIST Y0$,Y[ALL]
0230 APM01A: IOLIST B0$,B1$
0240 APM03A: IOLIST B0$,B1$
0250 APM10A: IOLIST H3$(1)
0260 APM10B: IOLIST *,X9$
0270 APT01A: IOLIST S0$,S1$,S[ALL]
0280 APT01B: IOLIST L0$,L1$,L[ALL]
0290 APT02A: IOLIST C0$,C[ALL]
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0320 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters
0405 DIM P[2],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N3$=F5$,SOURCE$=PGM(-2)
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0425 LET RT$=P3$(3,1)
0430 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0435 FOR X=0 TO 4
0440 LET G[X]=NUM(G2$(X*2+1,2),ERR=0445)
0445 NEXT X
0450 LET G[2]=FNYY_YEAR(G2$(5,2))
0460 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(9,1))
0500 REM " --- Initializations
0510 DIM A[2],W[1],D[2],T[2],U[3],HEADINGS$[1]
0520 DIM X[2],Y[2],O[12],H[12],S[5]
0530 LET L9=59,L=L9+1,WIDTH=132,PAGE=0,HEADINGS=1,WHEN$=F0$(7,3)
0535 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,CLOCK$=""
0540 LET M0=LEN(M0$),M1=LEN(M1$),G5=LEN(G5$),R=0,W=30,M=M1
0545 IF G5<7 THEN LET G5=7
0550 LET DIST$="",GL1$="",GL2$="",RT1$="",RT2$=""
0553 LET H6$="Invoice #",H7$="Inv Date"
0565 IF RT$="Y" THEN LET RT1$="Retention",RT2$="   Amount",R=1
0570 LET X=28+M0+W+4*M+R*M
0575 IF X<WIDTH THEN GOTO 0600
0580 LET M=M-1,W=W-1
0590 GOTO 0570
0645 REM " --- Open GL Files
0650 LET GL$="N",STATUS=0
0655 CALL "GLC.BA",ERR=0660,SOURCE$,N2$,GLW11$,GL$,STATUS
0660 IF STATUS<>0 THEN GOTO 9900
0665 IF GL$<>"Y" THEN GOTO 0700
0670 LET FILES$[12]="GLM-01",FILES$[13]=GLW11$,OPTIONS$[13]="CL"
0675 CALL "SYC.DA",1,12,13,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS>0 THEN GOTO 9900
0690 LET GLM01_DEV=CHANNELS[12],GLW11_DEV=CHANNELS[13]
0700 REM " --- Set print positions
0710 IF GL$="Y" THEN LET GL1$="Account",GL2$="Amount",DIST$="Distribution"
0715 IF M<>M1 THEN CALL "SYC.RM",M1$,M1,9,M
0720 LET O[0]=0,O[1]=O[0]+8,O[2]=O[1]+9,O[3]=O[2]+9,O[4]=MAX(O[3]+7,O[3]+M0+1)
0725 LET O[5]=O[4]+11,O[12]=WIDTH,O[11]=O[12],O[10]=O[11]
0730 IF GL$="Y" THEN LET O[11]=O[12]-M1,O[10]=O[11]-G5-1
0735 LET O[9]=O[10]-1-M1*R,O[8]=O[9]-M1,O[7]=O[8]-M1,O[6]=O[7]-M1
0740 LET H[0]=O[0],H[1]=O[1]+2,H[2]=O[2],H[3]=O[3],H[4]=O[4],H[5]=O[5]
0745 LET H[6]=O[6]+M1-8,H[7]=O[7]+M1-9,H[8]=O[8]+M1-8,D=H[8]
0750 IF R>0 THEN LET H[9]=O[9]+M1-10
0770 IF GL$<>"Y" THEN GOTO 0800
0780 LET H[10]=O[10],H[11]=O[11]+M1-7,D=O[11]+M1-O[10]
0790 LET D=O[10]+INT((D-LEN(DIST$))/2)
0800 REM " --- Run Report?
0810 LET V4$="Do You Want To Print The "+N3$
0820 CALL "SYC.YN",1,V4$,2,V$,V3
0830 IF V3=4 THEN GOTO 9900
0840 IF V$<>"Y" THEN GOTO 9900
0900 REM " --- File Initializations
0910 CALL "SYC.NB","Printing",10,COLUMN
0980 CALL "SYC.GA",7,1,"","",STATUS
0990 IF STATUS THEN GOTO 9900
1000 REM " --- Init Read
1020 READ (APE02_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Main Read
1120 LET K0$=KEY(APE02_DEV,END=4000)
1130 IF K0$(1,2)<>N0$ THEN GOTO 4000
1140 READ (APE02_DEV)IOL=APE02A
1160 PRINT @(COLUMN,11),A0$(3,2)," ",A0$(5,7),
1200 REM " --- Breaks
1220 IF K0$(3,2)<>T0$ THEN GOSUB 6000
1240 IF LEN(K0$)<>11 THEN GOTO 1100
1250 LET GLDATE$=A0$(28,3),GLSTATUS=0
1260 IF GL$="Y" THEN CALL "GLC.EA",GLDATE$,"N",PERIOD$,YEAR$,GLSTATUS
1300 REM " --- Init Check
1320 LET L1=0
1340 DIM X[3]
1600 REM " --- Check Header
1620 ON POS(A0$(31,1)="VRM") GOTO 1100,1800,2000,2500
1800 REM " --- VOID
1820 IF L+2>L9 THEN GOSUB 5000
1840 PRINT (7)@(O[0]),A0$(5,7),@(O[1]),FNB$(A0$(28,3)),@(O[2]),"Void"
1845 PRINT (7)""
1850 LET L=L+2
1890 GOTO 1100
2000 REM " --- Reversal
2020 IF L+1>L9 THEN GOSUB 5000
2030 LET X1$=""
2040 READ (APE05_DEV,KEY=K0$,DOM=2050)
2050 LET K$=KEY(APE05_DEV,END=2080)
2060 IF K$(1,11)<>K0$ THEN GOTO 2080
2070 LET A0$(18,10)=K$(18,10)
2080 GOSUB 6400
2090 PRINT (7)@(O[0]),A0$(5,7),@(O[1]),FNB$(A0$(28,3)),@(O[2]),"Reversal",@(O[3]),X0$,@(O[4]),X1$; LET L=L+1
2095 LET REF1$=X0$,REF2$=A0$(18,10),REF3$=A0$(5,7)+" "+A0$(3,2),MEMO$=X1$
2100 LET K$=KEY(APE05_DEV,END=3000)
2120 IF K$(1,11)<>K0$ THEN GOTO 3000
2130 DIM D1$(40)
2140 READ (APE05_DEV)IOL=APE05A
2150 IF K$(1,17)<>A0$(1,17) THEN GOTO 2100
2160 IF L+1>L9 THEN GOSUB 5400
2170 IF P3$(3,1)="Y" THEN PRINT (7)@(49+M0+3*M1),-D[2]:M1$,
2180 PRINT (7)@(O[4]),D0$(18,10),@(O[5]),FNB$(D0$(28,3)),@(O[6]),-D[0]:M1$,@(O[7]),-D[1]:M1$,@(O[8]),(-D[0])+D[2]+D[1]:M1$
2200 LET L=L+1,L1=L1+1
2210 FOR X=0 TO 2
2220 LET X[X]=X[X]-D[X],T[X]=T[X]-D[X],U[X]=U[X]-D[X]
2230 NEXT X
2235 LET X=0
2240 DIM X9$(4*G[4],"0")
2245 FIND (APM10_DEV,KEY=N0$+"B"+D0$(32,2),DOM=2250)IOL=APM10B
2250 FIND (APT01_DEV,KEY=D0$(1,4)+D0$(12,16)+"00",DOM=2260)IOL=APT01A
2252 IF P3$(4,1)="Y" THEN GOTO 2260
2256 LET ACCOUNT$=X9$(1,G[4]),AMOUNT=(-D[0])+D[2]
2258 GOSUB GLRECAP
2260 LET ACCOUNT$=X9$(G[4]+1,G[4]),AMOUNT=D[0]-D[2]-D[1]
2270 GOSUB GLRECAP
2280 LET ACCOUNT$=X9$(2*G[4]+1,G[4]),AMOUNT=D[1]
2290 GOSUB GLRECAP
2300 REM " --- Reverse Dist
2302 IF P3$(4,1)<>"Y" THEN GOTO 2400
2305 DIM C[1]
2310 LET D$=A0$(1,4)+A0$(12,6)+"C"+A0$(5,7)
2315 IF DOLD$=D$ THEN GOTO 2400 ELSE LET DOLD$=D$
2320 READ (APT02_DEV,KEY=D$,DOM=2330)
2330 LET K1$=KEY(APT02_DEV,END=2400)
2340 IF K1$(1,18)<>D$ THEN GOTO 2400
2350 READ (APT02_DEV)IOL=APT02A
2360 LET ACCOUNT$=C0$(22,G[4]),AMOUNT=-C[0]
2370 GOSUB GLRECAP
2380 GOTO 2330
2400 REM " --- Inv. Distribution
2405 DIM C[1]; LET DIST_AMT=-D[0],ACCOUNT1$=""
2410 IF P3$(4,1)<>"Y" THEN GOTO 2486
2420 LET D$=D0$(1,4)+D0$(12,6)+"I"+D0$(18,10)
2430 READ (APT02_DEV,KEY=D$,DOM=2440)
2440 LET K2$=KEY(APT02_DEV,END=2490)
2450 IF LEN(K2$)<21 THEN GOTO 2490 ELSE IF K2$(1,21)<>D$ THEN GOTO 2490
2460 READ (APT02_DEV)IOL=APT02A
2465 IF S[0]<>0 THEN LET C[0]=C[0]*D[0]/S[0] ELSE LET C[0]=0
2466 LET DIST_AMT=DIST_AMT+C[0]
2470 LET ACCOUNT$=C0$(22,G[4]),AMOUNT=-C[0]
2475 LET ACCOUNT1$=ACCOUNT$
2480 GOSUB GLRECAP
2485 GOTO 2440
2490 IF ACCOUNT1$="" THEN GOTO 2495
2492 LET ACCOUNT$=ACCOUNT1$,AMOUNT=-DIST_AMT
2493 GOSUB GLRECAP
2495 GOTO 2100
2500 REM " --- Manual Check
2520 IF L+1>L9 THEN GOSUB 5000
2530 GOSUB 6400
2540 PRINT (7)@(O[0]),A0$(5,7),@(O[1]),FNB$(A0$(28,3)),@(O[2]),"Manual",@(O[3]),X0$,@(O[4]),X1$
2580 LET REF1$=X0$,REF2$=A0$(18,10),REF3$=A0$(5,7)+" "+A0$(3,2),MEMO$=X1$
2590 LET L=L+1
2600 READ (APE22_DEV,KEY=K0$(1,11),DOM=2610)
2610 LET K$=KEY(APE22_DEV,END=3000)
2620 IF K$(1,11)<>K0$ THEN GOTO 3000
2640 READ (APE22_DEV)IOL=APE22A
2650 LET L3=0,RX$=""
2670 IF L+2>L9 THEN GOSUB 5400
2680 IF RT$="Y" THEN LET RX$=STR(Y[2]:M1$)
2690 PRINT (7)@(O[4]),Y0$(18,10),@(O[5]),FNB$(Y0$(30,3)),@(O[6]),Y[0]:M1$,@(O[7]),Y[1]:M1$,@(O[8]),Y[0]-Y[2]-Y[1]:M1$,@(O[9]),RX$,
2700 LET L1=L1+1,L2=0,Y0=0
2710 FOR X=0 TO 2
2720 LET X[X]=X[X]+Y[X],T[X]=T[X]+Y[X],U[X]=U[X]+Y[X]
2730 NEXT X
2740 DIM X9$(4*G[4],"0")
2750 FIND (APM10_DEV,KEY=N0$+"B"+Y0$(33,2),DOM=2752)IOL=APM10B
2752 IF P3$(4,1)="Y" THEN GOSUB 6300; GOTO 2760
2754 FIND (APT01_DEV,KEY=K$(1,4)+K$(12,16)+"00",DOM=2760)IOL=APT01A
2756 LET ACCOUNT$=X9$(1,G[4]),AMOUNT=Y[0]-Y[2]
2758 GOSUB GLRECAP
2760 LET ACCOUNT$=X9$(G[4]+1,G[4]),AMOUNT=Y[1]-Y[0]+Y[2]
2770 GOSUB GLRECAP
2780 LET ACCOUNT$=X9$(2*G[4]+1,G[4]),AMOUNT=-Y[1]
2790 GOSUB GLRECAP
2800 READ (APE12_DEV,KEY=Y0$(1,27),DOM=2810)
2810 LET K$=KEY(APE12_DEV,END=2900)
2820 IF LEN(K$)<27 THEN GOTO 2900
2840 IF K$(1,27)<>K0$(1,4)+Y0$(5,23) THEN GOTO 2900
2850 READ (APE12_DEV)IOL=APE12A
2860 IF L+1>L9 THEN GOSUB 5400
2870 IF GL$="Y" THEN PRINT (7)@(O[10]),FNF$(W1$(1,G[3]),G5$),@(O[11]),W[0]:M1$,
2877 PRINT (7)""
2880 LET L=L+1,U[3]=U[3]+W[0],Y0=Y0+W[0]
2882 LET ACCOUNT$=W1$(1,G[4]),AMOUNT=W[0]
2884 GOSUB GLRECAP
2886 IF RT$="Y" THEN LET L2=L2+1
2890 GOTO 2810
2910 IF L2=1 THEN PRINT (7)'LF'; LET L=L+2 ELSE PRINT (7)'LF',; LET L=L+1
2950 FIND (APT01_DEV,KEY=Y0$(1,4)+Y0$(12,16)+"00",DOM=2970)
2960 GOTO 2610
2970 IF Y0=Y[0] THEN GOTO 2610
2975 IF L+1>L9 THEN GOSUB 5000
2980 PRINT (7)@(O[2]),"WARNING: G/L Distribution Out Of Balance"
2985 LET L=L+1,ERRORS=ERRORS+1
2990 GOTO 2610
3000 REM " --- Total"
3010 IF L1<=1 THEN GOTO 3100
3020 LET RX$=""
3030 IF RT$="Y" THEN LET RX$=STR(X[2]:M1$)
3035 IF L+3>L9 THEN GOSUB 5000
3040 PRINT (7)""
3050 PRINT (7)@(O[2]),"Totals For Vendor",@(O[6]),X[0]:M1$,@(O[7]),X[1]:M1$,@(O[8]),X[0]-X[2]-X[1]:M1$,@(O[9]),RX$
3060 LET L=L+2
3100 REM 
3110 IF GLSTATUS>0 THEN CALL "GLC.GA",7,GLDATE$,GLSTATUS,L,ERRORS,STATUS
3140 IF L+2>L9 THEN GOSUB 5000; GOTO 3190
3150 PRINT (7)""
3160 LET L=L+1
3190 GOTO 1100
4000 REM " --- Totals
4010 IF L+2>L9 THEN GOSUB 5000
4020 LET T9$="E"
4040 GOSUB 6000
4050 PRINT (7)""
4055 LET RX$="",GX$=""
4060 IF RT$="Y" THEN LET RX$=STR(U[2]:M1$)
4065 IF GL$="Y" THEN LET GX$=STR(U[3]:M1$)
4070 PRINT (7)@(O[2]),"Totals For Report",@(O[6]),U[0]:M1$,@(O[7]),U[1]:M1$,@(O[8]),U[0]-U[2]-U[1]:M1$,@(O[11]),GX$
4075 IF ERRORS=0 THEN GOTO 4100
4080 IF L+3>L9 THEN GOSUB 5000
4085 PRINT (7)""
4090 PRINT (7)'LF',@(O[2]),"WARNING: This Report Is Missing Information"
4100 REM " --- Run next overlay
4110 LET UPDATE$="APU.CA"
4180 IF GL$="Y" THEN RUN "GLR.XA"
4190 RUN UPDATE$
5000 REM " --- Heading
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 IF H3$<>"" THEN PRINT (7)@(FNC(H3$,WIDTH)),H3$; LET L=L+1
5050 PRINT (7)""
5060 PRINT (7)@(H[0]),"Check",@(H[1]),"Check",@(H[6]),"Invoice",@(H[7]),"Discount",@(H[8]),"Payment",@(H[9]),RT1$,@(D),DIST$
5070 PRINT (7)@(H[0]),"Number",@(H[1]),"Date",@(H[2]),"Type",@(H[3]),"Vendor",@(H[4]),H6$,@(H[5]),H7$,@(H[6])," Amount",@(H[7]),"  Amount",@(H[8])," Amount",@(H[9]),RT2$,@(H[10]),GL1$,@(H[11]),GL2$
5080 PRINT (7)""
5085 LET L=L+4
5090 RETURN 
5200 REM " --- Vendor
5210 IF L+1>L9 THEN GOSUB 5000
5220 PRINT (7)@(O[3]),X0$,@(O[4]),X1$
5230 LET L=L+1
5290 RETURN 
5400 REM " --- Both Headings
5420 GOSUB 5000
5440 GOSUB 5200
5490 RETURN 
6000 REM " --- AP Type Break
6010 IF T0$="" THEN GOTO 6100
6040 IF P3$(1,1)<>"Y" THEN GOTO 6100
6050 PRINT (7)""
6055 LET RX$="",T1$=""
6060 IF RT$="Y" THEN LET RX$=STR(T[2]:M1$)
6070 PRINT (7)@(O[2]),"AP Type Totals",@(O[6]),T[0]:M1$,@(O[7]),T[1]:M1$,@(O[8]),T[0]-T[2]-T[1]:M1$,@(O[9]),RX$
6100 IF T9$="E" THEN RETURN 
6120 DIM T[2],H3$(32)
6140 LET T0$=K0$(3,2),L=L9+1,T9$=""
6150 IF P3$(1,1)<>"Y" THEN GOTO 6290
6160 LET X$="AP Type "+T0$+" ",H3$(6)="(Unknown AP Type)",E0=1
6170 FIND (APM10_DEV,KEY=N0$+"A"+T0$,DOM=6200)IOL=APM10A
6180 LET E0=0
6200 LET ERRORS=ERRORS+E0,H3$=X$+FNP$(H3$(6,20))
6290 RETURN 
6300 REM " --- Cash Basis Inside Invoice Distribution"
6305 DIM C[1]; LET INVOICE$=Y0$(1,4)+Y0$(12,6)+"I"+Y0$(18,10),ACCOUNT$="",TOT_DIST=0,AMOUNT=0,T9=0
6310 READ (APT02_DEV,KEY=INVOICE$,DOM=6315)
6315 LET K10$=KEY(APT02_DEV,END=6340)
6320 IF POS(INVOICE$=K10$)<>1 THEN GOTO 6340
6325 READ (APT02_DEV)IOL=APT02A
6330 LET TOT_DIST=TOT_DIST+C[0]
6335 GOTO 6315
6340 IF TOT_DIST=0 THEN GOTO 6399
6345 READ (APT02_DEV,KEY=INVOICE$,DOM=6350)
6350 LET K10$=KEY(APT02_DEV,END=6390)
6355 IF POS(INVOICE$=K10$)<>1 THEN GOTO 6390
6360 READ (APT02_DEV)IOL=APT02A
6365 IF C0$(22,G[4])=ACCOUNT$ THEN GOTO 6380
6370 IF ACCOUNT$<>"" THEN GOSUB GLRECAP
6375 LET ACCOUNT$=C0$(22,G[4]),T9=T9+AMOUNT,AMOUNT=0
6380 LET AMOUNT=AMOUNT+C[0]*(Y[0]-Y[2])/TOT_DIST
6385 GOTO 6350
6390 LET AMOUNT=Y[0]-Y[2]-T9; REM "Prevent rounding error
6395 IF ACCOUNT$<>"" THEN GOSUB GLRECAP
6399 RETURN 
6400 REM " --- Vendor Name
6500 IF T9$<>"" THEN RETURN 
6510 DIM X0$(M0)
6520 LET X1$="(Unknown Vendor)",E0=1
6530 IF A0$(12,4)="TEMP" THEN GOTO 6600
6550 FIND (APM01_DEV,KEY=N0$+A0$(12,6),DOM=6570)IOL=APM01A
6560 LET X1$=B1$(1,30),E0=0
6570 LET X0$(1)=FNF$(A0$(12,P[0]),M0$)
6590 GOTO 6700
6600 REM " --- Temp Name
6610 LET X0$(1)=A0$(12,4),X1$=A1$,E0=0
6615 LET X1$=X1$+FILL(50-LEN(X1$))
6620 IF A0$(31,1)="M" THEN GOTO 6700
6640 LET X1$=A1$
6650 FIND (APM03_DEV,KEY=A0$(1,4)+A0$(18,10),DOM=6700)IOL=APM03A
6660 LET X1$=B1$(1,30)
6790 RETURN 
6900 REM " --- Standard G/L Recap Routine
6910 GLRECAP: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLW11_DEV,GLT05_DEV,ACCOUNT$,GLDATE$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

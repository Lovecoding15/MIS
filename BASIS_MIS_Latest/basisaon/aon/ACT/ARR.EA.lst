0010 REM "ARR - LABELS"
0020 REM "Program: ARR.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ARM-02",FILES$[3]="ARM-04"
0130 LET FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET ARM01_DEV=CHANNELS[1],ARM02_DEV=CHANNELS[2],ARM04_DEV=CHANNELS[3]
0185 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLists
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0450 DIM P[2]
0480 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0500 REM " --- Init Data
0510 LET H0=65,H1=120,H=0,H1$=N1$,H2$=N3$
0530 LET W0=NUM(P3$(5,2)),W1=NUM(P3$(9,2)),W2=NUM(P3$(7,2)),W3=1
0532 IF W0<1 THEN LET W0=1
0534 IF W1<1 THEN LET W1=1
0536 IF W2<1 THEN LET W2=1
0540 DIM W0$(W0*W1*6)
0545 DIM W9$(W2,$0A$)
0550 LET M7$="00000"
0560 IF P[2]=9 THEN LET M7$="00000-0000"
0700 REM " --- Background
0710 FOR X=4 TO 16 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(24,3),"Label Sequence:",@(21,5),"Labels Per Record:",@(28,7),"Label Code:",@(24,9),"Restart? (Y/N):",@(19,11),"Enter Starting Info: ",@(21,13),"Enter Ending Info:",'SF',
1000 REM " --- Type
1010 PRINT 'CF',
1020 LET V0$="S",V1$="KREC",V2$="N",V3$="NAZ",V4$="N=Numeric A=Alternate Sequence Z=Zip/Postal Code",V0=1,V1=40,V2=3
1040 GOSUB 7000
1045 IF V3=4 THEN GOTO 9900
1060 LET P7$=V$
1070 IF P7$="N" THEN LET R0=6,R0$="M",R3$=P1$
1080 IF P7$="A" THEN LET R0=10,R0$="S",R3$=""
1090 IF P7$="Z" THEN LET R0=P[2],R0$="M",R3$=M7$
1200 REM " --- Labels per Record
1220 LET V0$="Z",V1$="R",V2$="1",V3$="",V0=3,V1=40,V2=5
1240 GOSUB 7000
1250 IF V3=2 THEN GOTO 1000
1260 IF V<1 THEN GOTO 1200
1280 LET W3=V
1290 PRINT @(0,22),'CL',
1300 REM " ---  Label Code
1310 LET V0$="S",V1$="C",V2$="",V0=2,V1=40,V2=7,V4$="Enter A Customer Label Code To Print Or <F3> For All"
1320 GOSUB 7000
1330 IF V3=2 THEN GOTO 1200
1340 LET X9$=V$
1345 IF V3=3 THEN PRINT @(40,7),'CL',"All",
1350 IF LEN(X9$)<2 THEN LET X9$=X9$+"  "
1360 LET X9$=X9$(1,2)
1400 REM " --- Restart
1420 LET V0$="Y",V1$="CR",V2$="N",V4$="Y=Yes N=No",V0=1,V1=40,V2=9
1440 GOSUB 7000
1445 IF V3=2 THEN GOTO 1300
1450 LET P9$=V$
1460 IF P9$="N" THEN LET P6$="",P8$=""; GOTO 2000
1600 REM " --- Starting Code
1620 LET V0$=R0$,V1$="C",V2$=P6$,V3$=R3$,V4$="<F1>=First",V0=R0,V1=40,V2=11
1640 GOSUB 7000
1650 IF V3=2 THEN GOTO 1400
1660 LET P6$=V$
1680 IF P6$="" OR V3=1 THEN LET P6$=""; PRINT @(V1,V2),"First",'CL',
1700 REM " --- Ending Code
1710 LET V0$=R0$,V1$="C",V2$=P8$,V3$=R3$,V4$="<F1>=Last",V0=R0,V1=40,V2=13
1720 GOSUB 7000
1730 IF V3=2 THEN GOTO 1600
1740 LET P8$=V$
1780 IF P8$="" OR V3=1 THEN LET P8$=""; PRINT @(V1,V2),"Last",'CL',; GOTO 1800
1790 IF P6$<>"" THEN IF P8$<P6$ THEN GOTO 1700
4000 REM " --- Correct?
4010 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V3=4 THEN GOTO 9900
4040 IF V$="N" THEN GOTO 1000
4060 IF V$<>"Y" THEN GOTO 4000
4100 RUN "ARR.EB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

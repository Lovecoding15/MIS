0010 REM "GLE - Financial Report Selection (Detail)"
0020 REM "Program GLE.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=8
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-12",FILES$[2]="GLM-13",FILES$[3]="GLM-22"
0140 LET FILES$[4]="GLM-32",FILES$[5]="GLE-06",FILES$[6]="GLE-07"
0150 LET FILES$[7]="GLE-08",FILES$[8]="GLE-09"
0160 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM12_DEV=CHANNELS[1],GLM13_DEV=CHANNELS[2],GLM22_DEV=CHANNELS[3]
0185 LET GLM32_DEV=CHANNELS[4],GLE06_DEV=CHANNELS[5],GLE07_DEV=CHANNELS[6]
0190 LET GLE08_DEV=CHANNELS[7],GLE09_DEV=CHANNELS[8]
0200 REM " --- IOLIST's"
0250 GLE04A: IOLIST A0$(1),A1$(1),A2$,A[ALL]
0260 GLS01C: IOLIST X0$(1)
0270 GLM12A: IOLIST H0$(1),M1$,M2$,M3$,M4$
0500 REM " --- Initializations"
0510 DIM A0$(4),A1$(16),A[2],H0$(30)
0550 LET L0=8,L9=21,L=L0,I0=0,K9=8,A2$="",A0$(1)=N0$
0560 LET X$=" Report Selection "
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,L0-2,0
0750 PRINT 'SB',@(FNC(X$,80),L0-2),X$,@(0,L0-1),"Report",@(8),"Description",'SF',
0900 REM " --- Clear work file"
0910 IF GLE05_DEV THEN LET GLE04_DEV=GLE05_DEV; REM "13 Period Financial Reporting
0920 CALL "SYC.FA",GLE04_DEV
1000 REM " --- Overflow"
1010 IF L<=L9 THEN GOTO 1100
1020 PRINT @(0,22),'CL',@(0,L0),'LD',
1040 LET L=L9
1100 REM " --- Next key"
1120 LET K$=KEY(GLE04_DEV,END=1400)
1140 IF POS(N0$=K$)<>1 THEN GOTO 1400
1150 READ (GLE04_DEV)IOL=GLE04A
1190 GOTO 1490
1400 REM " --- Report Number"
1410 LET V0$="Z",V1$="KERC",V2$="",V3$="",V0=2,V1=2,V2=L,I0=0
1415 LET V4$="Enter A Valid Report Number (<F1>=Select All Reports/<F3>=Lookup)"
1420 GOSUB 7000
1425 ON V3 GOTO 1460,1440,1400,1430,1450
1430 CALL "GLC.LF",N0$,V$
1435 IF V$="" THEN GOTO 1400
1437 GOTO 1460
1440 GOSUB 6000
1445 GOTO 4000
1450 PRINT @(V1,V2),"  ",
1455 GOTO 4000
1460 IF V$="00" OR V$="" THEN GOTO 1400
1470 LET A0$(3,2)=V$
1480 FIND (GLE04_DEV,KEY=A0$,DOM=1600)IOL=GLE04A
1490 IF A0$<=K9$ THEN GOSUB 5400
1500 GOSUB 5000
1530 GOTO 3300
1600 REM " --- Retrieve GLM-12 report header"
1610 DIM H0$(30),A1$(16)
1620 LET H0$(1)=A0$,H0$(5)="0000"
1630 FIND (GLM12_DEV,KEY=H0$(1,K9),DOM=1400)IOL=GLM12A
1640 IF A0$<=K9$ THEN GOSUB 5400
1650 GOSUB 5000
3300 REM " --- Line Correct?"
3310 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=A0$
3320 LET V4$="<Enter>=Correct   <D>=Delete"
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3300,1400,3300,3400
3350 IF V$="" THEN GOTO 3400
3360 IF V$="D" THEN GOTO 3700
3380 GOTO 3300
3400 REM " --- Write record"
3410 WRITE (GLE04_DEV,KEY=A0$)IOL=GLE04A
3480 LET L=L+1
3485 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3700 REM " --- Delete"
3710 REMOVE (GLE04_DEV,KEY=A0$,DOM=3720)
3720 PRINT @(0,L),'CL',
3780 IF V3=4 THEN GOTO 4000
3790 GOTO 1000
4000 REM " --- Screen Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/List/Review/Reprint)?"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V0=7,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4050 IF V$="L" OR V$="LIST" THEN GOTO 4400
4060 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4070 IF V$="REPRINT" THEN GOTO 4100
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Update parameter record"
4110 DIM X0$(12)
4120 LET X0$(1)=N0$+N2$+"02",X0$(9,4)="0000"
4130 EXTRACT (SYS01_DEV,KEY=X0$(1,6),DOM=4140)IOL=GLS01C
4140 LET X0$(7,2)=BEGPER$,PERIOD=NUM(BEGPER$)
4145 LET SEQUENCE=NUM(X0$(FLAGS,1)),SEQUENCE$=STR(SEQUENCE)
4150 WRITE (SYS01_DEV,KEY=X0$(1,6))IOL=GLS01C
4170 READ (GLE04_DEV,KEY=N0$,DOM=4180)
4190 IF V$="REPRINT" THEN GOTO 4900
4200 REM " --- Run update overlay"
4210 LET SEQUENCE=NUM(X0$(FLAGS,1))+1
4220 IF SEQUENCE>9 THEN LET SEQUENCE=0
4230 LET SEQUENCE$=STR(SEQUENCE),X0$(FLAGS,1)=SEQUENCE$
4240 WRITE (SYS01_DEV,KEY=X0$(1,6))IOL=GLS01C
4290 RUN UPDATE$,ERR=9900
4400 REM " --- List"
4410 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4610 READ (GLE04_DEV,KEY=N0$,DOM=4620)
4620 GOSUB 5400
4690 GOTO 1000
4900 REM " --- Run report overlay"
4990 RUN "GLR.FA",ERR=9900
5000 REM " --- Display"
5010 DIM H0$(30)
5020 LET H0$(1)=A0$,H0$(5)="0000"
5030 FIND (GLM12_DEV,KEY=H0$(1,K9),DOM=5040)IOL=GLM12A
5080 PRINT @(0,L),'CL',@(2),A0$(3),@(8),H0$(9,20),
5090 RETURN 
5100 REM " --- List"
5105 LET Q1=0
5110 GOSUB 5400
5120 READ (GLE04_DEV,KEY=N0$,DOM=5130)
5130 LET K$=KEY(GLE04_DEV,END=5300)
5150 IF POS(N0$=K$)<>1 THEN GOTO 5300
5160 READ (GLE04_DEV)IOL=GLE04A
5170 LET Q1=1
5200 IF L<=L9 THEN GOTO 5250
5203 LET V4$="<Enter>=Continue/<F4>=End:"
5205 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5210 GOSUB 7000
5220 IF V3=4 THEN GOTO 5300
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+1,K9$=A0$
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5410 PRINT @(0,L0),'CE',
5420 LET L=L0
5490 RETURN 
6000 REM " --- Select all reports"
6090 READ (GLM12_DEV,KEY=N0$,DOM=6100)
6100 REM " --- Read next GLM-12 record"
6110 LET K$=KEY(GLM12_DEV,END=6200)
6120 IF POS(N0$=K$)<>1 THEN GOTO 6200
6150 READ (GLM12_DEV)IOL=GLM12A
6160 LET A0$(1)=N0$,A0$(3)=H0$(3,2)
6170 WRITE (GLE04_DEV,KEY=A0$)IOL=GLE04A
6190 GOTO 6100
6200 REM " --- List selections on the screen"
6210 GOSUB 5100
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

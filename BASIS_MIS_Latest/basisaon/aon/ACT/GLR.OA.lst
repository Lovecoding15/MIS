0010 REM "GLR - Totals/Report Cross-Reference"
0020 REM "Program GLR.OA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-12",FILES$[2]="GLM-22",FILES$[3]="GLW-01"
0140 LET FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM12_DEV=CHANNELS[1],GLM22_DEV=CHANNELS[2],GLW01_DEV=CHANNELS[3]
0185 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLIST's"
0210 GLM12A: IOLIST A0$(1)
0220 GLM22A: IOLIST B0$(1),B1$(1),B2$(1)
0240 GLW01A: IOLIST X0$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N3$=F5$,HEADINGS=2
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)
0500 REM " --- Initializations"
0510 DIM A0$(30),B0$(8),B1$(56),B2$(20),HEADINGS$[HEADINGS]
0550 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,CLOCK$="",WHEN$=F0$(7,3)
0560 LET H0=80,L9=59,L=L9+1
0700 REM " --- Background"
0710 CALL "SYC.NB"," Sorting",23,COLUMN
0800 REM " --- Options"
0810 LET TITLE=0,PROMPT$="Do You Want To Print The "+N3$,PROMPT=2
0820 CALL "SYC.YN",TITLE,PROMPT$,PROMPT,V$,V3
0830 IF V$<>"Y" THEN GOTO 9900
0900 REM " --- Clear and position sort file"
0910 CALL "SYC.GA",7,1,"","",STATUS
0920 IF STATUS THEN GOTO 9900
0930 CALL "SYC.FA",GLW01_DEV
0990 READ (GLM12_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next GLM-12 record"
1010 READ (GLM12_DEV,END=2000)IOL=GLM12A
1020 IF POS(N0$=A0$)<>1 THEN GOTO 2000
1050 PRINT @(COLUMN,11),A0$(3,2)," ",A0$(9,20),
1060 READ (GLM22_DEV,KEY=A0$(1,8),DOM=1800)
1200 REM " --- Read next detail"
1210 LET K$=KEY(GLM22_DEV,END=2000)
1220 IF K$(1,4)<>A0$(1,4) THEN GOTO 1900
1230 READ (GLM22_DEV,END=2000)IOL=GLM22A
1300 REM 
1310 LET X$=B1$(45,3)
1320 IF X$="000" THEN GOTO 1400
1330 LET X0$=X$+B0$(3,6)
1340 WRITE (GLW01_DEV,KEY=X0$)IOL=GLW01A
1400 REM 
1410 FOR X=1 TO 13 STEP 3
1430 LET X$=B2$(X,3)
1440 IF X$="000" THEN GOTO 1490
1450 LET X0$=X$+B0$(3,6)
1460 WRITE (GLW01_DEV,KEY=X0$)IOL=GLW01A
1490 NEXT X
1800 REM " --- Loop back for next detail"
1890 GOTO 1200
1900 REM " --- Loop back for next header"
1990 GOTO 1000
2000 REM 
2010 PRINT @(0,3),'CE',
2020 CALL "SYC.NB","Printing",3,COLUMN
2900 REM " --- Position sort file"
2910 LET T0$=""
2990 READ (GLW01_DEV,KEY="",DOM=3000)
3000 REM " --- Read next sort record"
3010 LET K$=KEY(GLW01_DEV,END=4000)
3020 READ (GLW01_DEV)
3200 REM " --- Level break?"
3210 IF K$(1,3)=T0$ THEN GOTO 3400
3220 LET T0$=K$(1,3),H0$=T0$
3230 PRINT @(COLUMN,11),T0$
3400 REM " --- Additional reads"
3410 DIM A0$(30),B0$(8),B1$(56),B2$(20)
3420 FIND (GLM12_DEV,KEY=N0$+K$(4,2)+"0000",DOM=3430)IOL=GLM12A
3430 FIND (GLM22_DEV,KEY=N0$+K$(4,6),DOM=3060)IOL=GLM22A
3500 REM " --- Print detail line"
3510 IF L+2>L9 THEN GOSUB 5000
3520 IF H0$="" THEN GOTO 3550
3530 PRINT (7)""
3540 LET L=L+1
3550 PRINT (7)H0$,@(10),A0$(3,2),@(15),A0$(9,20),@(37),B0$(5,4),@(45),B1$(2,35)
3560 LET L=L+1,H0$=""
3900 REM " --- Loop back for next sort record"
3990 GOTO 3000
4000 REM " --- All done"
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1,H0$=T0$
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5050 PRINT (7)""
5060 PRINT (7)"Total",@(8),"Report",@(37),"Line"
5070 PRINT (7)"Number",@(8),"Number",@(15),"Description",@(36),"Number",@(45),"Description"
5080 LET L=L+3
5090 RETURN 
8000 REM " --- Functions"
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

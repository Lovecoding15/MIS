0010 REM "ARU - FINANCE CHARGE UPDATE"
0020 REM "Program ARU.HA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=10
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARE-02",FILES$[2]="ARE-03",FILES$[3]="ARE-05"
0130 LET FILES$[4]="ARM-02",FILES$[5]="ARM-10",FILES$[6]="ARS-10"
0135 LET FILES$[7]="ART-01",FILES$[8]="ART-01",FILES$[9]="ART-11"
0140 LET FILES$[10]="SYS-01"
0160 CALL "SYC.DA",1,1,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET ARE02_DEV=CHANNELS[1],ARE03_DEV=CHANNELS[2],ARE05_DEV=CHANNELS[3]
0185 LET ARM02_DEV=CHANNELS[4],ARM10_DEV=CHANNELS[5],ARS10_DEV=CHANNELS[6]
0190 LET ART01_DEV1=CHANNELS[7],ART01_DEV2=CHANNELS[8],ART11_DEV=CHANNELS[9]
0195 LET SYS01_DEV=CHANNELS[10]
0200 REM " --- IOLists
0210 ARE02A: IOLIST X0$
0230 ARM10A: IOLIST U0$,U[ALL]
0240 ARM10D: IOLIST X0$
0360 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$,M4$,M5$
0370 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0380 SYS01T: IOLIST X$,F0$,X$,F2$,X$,F4$,F5$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[6],G[6]
0460 LET P[0]=NUM(P2$(1,2))
0470 FOR X=0 TO 4
0480 LET G[X]=NUM(G2$(X*2+1,2),ERR=0490)
0490 NEXT X
0495 LET G[2]=FNYY_YEAR(G2$(5,2))
0500 REM " --- Init Data
0520 LET K0=1,K9=14,L0=6,L9=20,O1=4
0530 DIM A[1],W[1],U[2]
0555 IF P[6]=9 THEN LET M7$="00000-0000"
0560 LET P7$="  "
0580 LET M1=LEN(M1$),M0=LEN(M0$)
0590 LET P9$=F0$(7,3)
0700 REM " --- Background
0710 FOR X=4 TO 16 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'CF','SB',@(19,3),"Finance Charge Date:",@(26,5),"Days Overdue:",@(15,7),"Minimum Past Due Amount:",@(22,9),"Finance Charge %:",@(9,11),"Minimum Finance Charge Amount:",@(21,13),"Distribution Code:",@(28,15),"Terms Code:",'SF',@(0,22),'CL',
1000 REM " --- Finance Charge Date
1010 PRINT 'CF',
1020 LET V0$="D",V1$="ER",V2$=P9$,V0=8,V1=40,V2=3,I0=0
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1060 LET P9$=V$
1090 GOTO 3900
1200 REM " --- Days Overdue
1220 LET V0$="N",V2$=STR(P9),V3$="##0",V1=40,V2=5
1240 GOSUB 7000
1250 IF V<0 THEN GOTO 1200
1260 LET P9=V
1290 GOTO 3900
1400 REM " --- Minimum Amt
1420 LET V0$="N",V2$=STR(P8),V3$=M1$,V1=40,V2=7
1440 GOSUB 7000
1450 IF V<0 THEN GOTO 1400
1460 LET P8=V
1490 GOTO 3900
1600 REM " --- Percent
1620 LET V0$="N",V1$="C",V2$=STR(P7),V3$=M3$,V4$="Enter Monthly Finance Charge Percent (e.g. 2.0 = 2%)",V1=40,V2=9
1640 GOSUB 7000
1650 IF V<0 THEN GOTO 1600
1660 LET P7=V
1690 GOTO 3900
1700 REM " --- Minimum Finance Charge
1720 LET V0$="N",V2$=STR(P6),V3$=M1$,V1=40,V2=11
1730 GOSUB 7000
1740 LET P6=V
1750 IF V<0 THEN GOTO 1700
1790 GOTO 3900
1800 REM " --- Dist Code
1820 LET V0$="S",V1$="KC",V2$=P8$,V4$="Enter Code For Distribution Of Finance Charge To GL (<F3>=Lookup)",V0=2,V1=40,V2=13
1830 GOSUB 7000
1831 IF V3<>3 THEN GOTO 1835
1832 LET V4=1,V5=ARM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="D"
1833 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
1834 IF V$="" THEN GOTO 1800
1835 IF V3=2 THEN GOTO 3900
1840 LET P8$=V$
1850 FIND (ARM10_DEV,KEY=N0$+"D"+P8$,DOM=1800)IOL=ARM10D
1860 PRINT @(V1+V0+1,V2),X0$(6,20),
1890 GOTO 3900
2000 REM " --- Terms Code
2020 LET V0$="S",V1$="KC",V2$=P7$,V4$="Enter Terms Code To Be Assigned To Finance Charge (<F3>=Lookup)",V0=2,V1=40,V2=15
2030 GOSUB 7000
2031 IF V3<>3 THEN GOTO 2035
2032 LET V4=1,V5=ARM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="A"
2033 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
2034 IF V$="" THEN GOTO 2000
2035 IF V3=2 THEN GOTO 3900
2037 IF LEN(V$)<2 THEN LET V$=V$+FILL(2-LEN(V$))
2040 LET P7$=V$
2050 FIND (ARM10_DEV,KEY=N0$+"A"+P7$,DOM=2000)IOL=ARM10A
2060 PRINT @(V1+V0+1,V2),U0$(6,20),
2090 GOTO 3900
3900 REM " --- Sequence
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1700,1800,2000,4000
4000 REM " --- Correct?
4010 PRINT @(0,17),'CL',
4020 LET V0$="S",V1$="KCE",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N/Purge)?",V0=5,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V$="N" THEN GOTO 1000
4050 IF V3=4 THEN GOTO 9900
4055 IF V$="PURGE" THEN GOTO 4200
4060 IF V$<>"Y" THEN GOTO 4020
4100 RUN "ARU.HB"
4200 REM " --- Purge Existing Finance Charges
4260 CALL "SYC.NB","Deleting: ",M0+8,COLUMN
4300 READ (ARE02_DEV,KEY=N0$,DOM=4310)
4310 LET K$=KEY(ARE02_DEV,END=4000)
4320 IF K$(1,2)<>N0$ THEN GOTO 4000
4330 READ (ARE02_DEV,KEY=K$)IOL=ARE02A
4340 IF X0$(31,1)<>"F" THEN GOTO 4310
4350 REMOVE (ARE02_DEV,KEY=K$)
4370 PRINT @(COLUMN,11),'SF',FNF$(X0$(5,P[0]),M0$)," ",X0$(11,7),
4390 GOTO 4310
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

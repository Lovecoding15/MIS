0010 REM "APR - Purchase Journal"
0020 REM "Program APR.OA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=10
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APE-05",FILES$[2]="APM-01",FILES$[3]="APM-03"
0135 LET FILES$[4]="APM-10",FILES$[5]="APT-02",FILES$[6]="APW-04"
0140 LET FILES$[7]="APW-14",FILES$[8]="SYS-01"
0145 LET OPTIONS$[6]="C",OPTIONS$[7]="C"
0150 LET FILES$[9]="GLM-01",FILES$[10]="GLW-04"
0155 LET OPTIONS$[10]="CL"
0160 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0165 IF STATUS>0 THEN GOTO 9900
0170 LET APE05_DEV=CHANNELS[1],APM01_DEV=CHANNELS[2],APM03_DEV=CHANNELS[3]
0175 LET APM10_DEV=CHANNELS[4],APT02_DEV=CHANNELS[5],APW04_DEV=CHANNELS[6]
0180 LET APW14_DEV=CHANNELS[7],SYS01_DEV=CHANNELS[8]
0200 REM " --- IOLIST's"
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0405 DIM P[2],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0445 FOR X=0 TO 4
0450 LET G[X]=NUM(G2$(X*2+1,2),ERR=0455)
0455 NEXT X
0460 LET G[2]=FNYY_YEAR(G2$(5,2))
0470 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P4$(1,2)),P[2]=FNYY_YEAR(P4$(3,2))
0480 CALL "SYC.VA",N2$,INFO$[ALL]
0490 LET GL$=INFO$[9]
0495 IF GL$="Y" THEN LET M1$=G6$
0500 REM " --- Initializations"
0570 LET PERIOD=P[1],SEQUENCE$="N",BEGDATE$="",ENDDATE$=""
0600 REM " --- Additional File Opens"
0610 IF GL$<>"Y" THEN GOTO 0700
0640 CALL "SYC.DA",1,9,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0650 LET GLM01_DEV=CHANNELS[9],GLW04_DEV=CHANNELS[10]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,6,0
0740 PRINT 'SB',@(28,3),"For Period:",@(23,5),"Report Sequence:",'SF',
1000 REM " --- For Period"
1005 LET I0=0
1010 PRINT 'CF',
1015 LET V0$="N",V1$="E",V2$=STR(PERIOD),V3$="##",V0=2,V1=40,V2=3
1020 LET V4$="Enter The Period For Which The Report Should Be Generated (1 Through "+STR(G[0])+")"
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1000,9900
1050 IF V<1 OR V>G[0] THEN GOTO 1000
1060 LET PERIOD=V
1100 REM " --- Determine Period Limits"
1110 CALL "SYC.PA",SYS01_DEV,V,P[2],V0$,V9$,STATUS
1120 LET BEGDATE$=V0$,ENDDATE$=V9$
1130 LET H3$="For Period "+STR(PERIOD)+" From "+FNB$(BEGDATE$)+" To "+FNB$(ENDDATE$)
1140 PRINT @(40,3),PERIOD:"#0-","(",FNB$(BEGDATE$)," To ",FNB$(ENDDATE$),")"
1190 GOTO 3900
1300 REM " --- Report Sequence"
1310 LET V0$="S",V1$="RC",V2$=SEQUENCE$,V3$="AN",X$="",V0=1,V1=40,V2=5
1315 IF GL$="Y" THEN LET V3$=V3$+"D",X$="D=By Distribution Account  "
1320 LET V4$="A=By Vendor Alternate Sequence  "+X$+"N=By Vendor Number"
1340 GOSUB 7000
1350 ON V3 GOTO 1360,1300,3900,1300,3900
1360 LET SEQUENCE$=V$
1370 IF POS(V$="AND")=0 THEN GOTO 1300
1390 GOTO 3900
3900 REM " --- Next Field
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1300,4000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4015 LET V0$="Y",V1$="C",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 ON V3 GOTO 4040,4000,4000,4000,9900
4040 ON POS(V$="YN") GOTO 4000,4090,1000
4090 RUN "APR.OB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

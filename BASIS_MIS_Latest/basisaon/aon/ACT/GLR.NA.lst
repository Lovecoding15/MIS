0010 REM "GLR - Recurring Journal Entry Register (Lead Overlay)"
0020 REM "Program GLR.NA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0051 REM "  NOTE: GLE01_DEV & GLE11_DEV really refer to GLE-02 & GLE-12"
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLE-02",FILES$[2]="GLE-12",FILES$[3]="GLM-07"
0140 LET FILES$[4]="SYS-01"
0150 LET OPTIONS$[1]="L"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET GLE02_DEV=CHANNELS[1],GLE12_DEV=CHANNELS[2],GLM07_DEV=CHANNELS[3]
0180 LET GLE01_DEV=CHANNELS[1],GLE11_DEV=CHANNELS[2]; REM "So GLR.MB accesses Recurring Files"
0185 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLIST's"
0210 GLM07A: IOLIST X0$(1),X1$(1),X2$,X[ALL]
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,X$,TASK$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="GL"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)
0500 REM " --- Initializations"
0510 DIM X0$(4),X1$(20),X[2]
0550 LET PROGRAM$="GLU.HA",SOURCE$=PGM(-2),CYCLES$="Y",JE$="N",H3$=""
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 PRINT 'SB',@(28,3),"Cycle Code:",'SF',
1000 REM " --- Cycle Code"
1010 LET V4$="Enter The (Optional) Cycle Code For This Register (<F1>=All/<F3>=Lookup)"
1020 LET V0$="S",V1$="C",V2$=CYCLE$,V3$="",V0=2,V1=40,V2=3
1030 GOSUB 7000
1035 ON V3 GOTO 1050,1053,1000,1040,4000
1040 GOSUB 6000
1050 IF FNP$(V$)<>"" THEN GOTO 1070
1053 LET CYCLE$=""
1055 PRINT @(V1,V2),"All",'CL',
1057 LET H3$="All Cycle Codes"
1060 GOTO 4000
1070 LET V$=V$+"  ",V$=V$(1,2)
1075 FIND (GLM07_DEV,KEY=N0$+V$,DOM=1000)IOL=GLM07A
1080 LET CYCLE$=X0$(3,2)
1090 PRINT @(V1,V2),X0$(3,2)," ",X1$(1,20),
1095 LET H3$="Cycle Code "+X0$(3,2)+" "+FNP$(X1$(1,20))
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="Y",V1$="CR",V2$="Y",V3$="",V0=1,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run Overlay"
4110 CLOSE (SYS01_DEV,ERR=4190)
4190 RUN "GLR.MB"
6000 REM " --- Cycle Code Lookup"
6010 LET NUMBER=1,CHANNEL=GLM07_DEV,RECORD$="",TITLE$="Cycle Codes"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=2
6050 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=20
6060 LET ROW=V2+1,COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,V$="SKIP"
6070 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

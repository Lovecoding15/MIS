0010 REM "APM - Bank Reconciliation"
0020 REM "Program APM.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 SETERR 9000
0050 ON O0 GOTO 0060,4000
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APM-11",FILES$[2]="APM-12",FILES$[3]="APM-13"
0140 LET FILES$[4]="SYS-01",FILES$[5]="GLM-01",FILES$[6]="GLM-02"
0150 LET FILES$[7]="GLT-06"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET APM11_DEV=CHANNELS[1],APM12_DEV=CHANNELS[2],APM13_DEV=CHANNELS[3]
0185 LET SYS01_DEV=CHANNELS[4],GLM01_DEV=CHANNELS[5],GLM02_DEV=CHANNELS[6]
0190 LET GLT06_DEV=CHANNELS[7]
0200 REM " --- IOLists"
0210 GLM01A: IOLIST A0$,A1$,A2$,A3$
0260 GLS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM P[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)
0440 CALL "SYC.VA","GL",INFO$[ALL]
0450 IF INFO$[20]<>"Y" THEN GOTO 4900
0470 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0480 FOR X=0 TO 4
0485 LET P[X]=NUM(P2$(X*2+1,2),ERR=0490)
0490 NEXT X
0495 LET P[2]=FNYY_YEAR(P2$(5,2))
0500 REM " --- Init Data"
0510 DIM J$(80,"-")
0520 LET I0=0,K9=P[4]+2,L0=6
0530 DIM A0$(P[4]+9),W0$(P[4]+9),W1$(50),W[2]
0540 LET A0$(1,2)=N0$
0550 LET A=POS("CR"=M1$)
0560 IF A>0 THEN LET M1$(A)="-"
0570 LET M1=LEN(M1$)
0600 REM " --- Additional File Opens"
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,5,0
0720 PRINT 'SB',@(3,3),"Account No:",@(2,4),"Description:",'SF',
0900 REM " --- Init Key"
0910 READ (GLM01_DEV,KEY=N0$,DOM=0920)
1000 REM " --- Key"
1010 LET K$=KEY(GLM01_DEV,END=1100)
1020 IF K$(1,2)<>N0$ THEN GOTO 1100
1100 REM 
1110 PRINT 'CF',
1120 LET V0$="M",V1$="CE",V3$=P1$,V0=P[4],V1=15,V2=3,I0=0
1130 LET V2$="",V4$="Enter A Valid G/L Account (<F3>=Lookup)"
1135 IF LEN(K$)>0 THEN IF POS(N0$=K$)=1 THEN LET V2$=K$(3,P[3])
1140 GOSUB 7000
1145 IF V3=3 THEN CALL "SYC.LG",V$
1150 IF V3=4 THEN GOTO 9900
1180 LET A0$(3)=V$
1200 REM " --- Find Key"
1210 READ (GLM01_DEV,KEY=A0$(1,K9),DOM=1000)IOL=GLM01A
1300 REM " --- Display"
1320 PRINT @(15,3),FNF$(A0$(3,P[3]),M0$),@(15,4),A1$(1,35),
4000 REM " --- Options"
4010 DIM MENUS$[1]
4020 LET MENUS=1,MENUS$[MENUS]="APM.EA",WIN_X=40,WIN_Y=L0+1
4030 LET O0=1
4040 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4050 CALL "SYC.WC",1,0,80,0,0,L0-1,0
4060 PRINT @(0,L0),'CE',
4070 IF SELECTION$="" THEN GOTO 1000
4090 RUN SELECTION$,ERR=4000
4900 REM " --- G/L Not Installed"
4910 DIM MESSAGE$[1]
4920 LET MESSAGE$[0]="General Ledger Is Required In Order To Use Bank Reconciliation"
4930 LET MESSAGE$[1]="               Press <Enter> To Continue"
4940 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
4990 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

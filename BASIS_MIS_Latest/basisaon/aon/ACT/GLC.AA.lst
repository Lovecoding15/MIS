0010 REM "GLC - Post To Daily Detail File"
0020 REM "Program GLC.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0043 REM "GLM01_DEV: GLM-01 Channel"
0045 REM "GLT04_DEV: GLT-04 Channel"
0046 REM "GLT05_DEV: GLT-05 Channel"
0047 REM "ACCOUNT$ : G/L Account Number"
0050 REM "WHEN$    : Transaction Date (packed or YYMMDD)"
0053 REM "REF1$    : Reference 1"
0055 REM "REF2$    : Reference 2"
0057 REM "REF3$    : Reference 3"
0060 REM "MEMO$    : Posting Memo"
0063 REM "AMOUNT   : Posting Amount"
0065 REM "UNITS    : Posting Units"
0067 REM "STATUS   : Status (0=No Error)"
0070 REM 
0073 REM "All arguments are input except STATUS, which is returned"
0075 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,WHEN$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$(1),A1$(1)
0220 GLT04A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0230 GLT05A: IOLIST X0$
0400 REM " --- Parameters"
0405 PRECISION 2
0410 DIM GLCONTROL$(640),P[4]
0420 LET STATUS=0,GLCONTROL$(1)=STBL("!GLCONTROL",ERR=9900)
0430 IF GLCONTROL$(13,1)="N" THEN GOTO 9900
0440 IF GLCONTROL$(44,1)="N" THEN GOTO 9900
0450 FOR X=0 TO 4
0460 LET P[X]=NUM(GLCONTROL$(28+X*2,2),ERR=0470)
0470 NEXT X
0475 LET P[2]=FNYY_YEAR(GLCONTROL$(32,2))
0480 LET SYSTEM$=GLCONTROL$(11,2),JOURNAL$=GLCONTROL$(18,2)
0485 LET AUDIT$=GLCONTROL$(20,7),N0$=GLCONTROL$(1,2)
0500 REM " --- Initializations"
0510 DIM A0$(2+P[4]),A1$(40),B0$(26+P[4]),B1$(64),B[4],X0$(26+P[4])
0520 DIM J0$(P[4],"0")
0550 LET VERIFY$="",DETAIL$="Y",K9=22+P[4],SEQUENCE=0,E0=0
0560 LET GLREF1$=REF1$,GLREF2$=REF2$,GLREF3$=REF3$,YMD$=""
0570 LET GLMEMO$=MEMO$,GLYEAR$="  ",GLPERIOD$="  "
0600 REM " --- Date conversion required?"
0610 IF LEN(WHEN$)=3 THEN LET YMD$=WHEN$
0620 IF LEN(WHEN$)=6 THEN LET YMD$=FND$(FNN$(WHEN$))
0630 IF YMD$="" THEN GOTO 9200
0640 LET YYMMDD$=FNYY_YY21$(FNM$(FNC$(YMD$)))
1000 REM " --- Determine period and year"
1010 IF ABS(AMOUNT)+ABS(UNITS)=0 THEN GOTO 4000
1020 IF YYMMDD$<>GLCONTROL$(81,6) THEN GOTO 1050
1030 LET PERIOD$=GLCONTROL$(87,2),YEAR$=GLCONTROL$(89,2)
1040 GOTO 1060
1050 CALL "GLC.EA",YYMMDD$,VERIFY$,PERIOD$,YEAR$,STATUS
1060 LET GLYEAR$=YEAR$,GLPERIOD$=PERIOD$
1100 REM " --- Retrieve account master"
1110 LET A0$(1)=N0$+ACCOUNT$,A1$(37,1)="Y",DETAIL$="Y"
1120 FIND (GLM01_DEV,KEY=A0$,DOM=2000)IOL=GLM01A
1130 IF GLCONTROL$(38,1)="N" THEN LET DETAIL$="N"
1140 IF GLCONTROL$(38,1)="Y" THEN LET DETAIL$="Y"
1150 IF GLCONTROL$(38,1)="A" THEN LET DETAIL$=A1$(37,1)
1160 IF FNP$(A0$(3))="" OR A0$(3,P[4])=J0$(1,P[4]) THEN LET DETAIL$="Y"
1190 GOTO 2000
2000 REM " --- Determine sequence number for posting"
2010 LET B0$(1)=N0$+JOURNAL$+AUDIT$+GLYEAR$+GLPERIOD$+YYMMDD$+ACCOUNT$
2020 READ (GLT04_DEV,KEY=B0$(1,K9-1)+$FF$,DOM=2030,ERR=4000)
2030 LET K$=KEYP(GLT04_DEV,END=2100)
2040 LET B0$(K9)=STR(SEQUENCE:"00000")
2050 IF K$(1,K9-1)<>B0$(1,K9-1) THEN GOTO 2500
2060 LET SEQUENCE=NUM(K$(K9)),B0$(1)=K$
2070 IF SEQUENCE>99998 THEN LET DETAIL$="N"
2500 REM " --- Detail Posting?"
2510 IF DETAIL$="Y" THEN GOTO 3500
3000 REM " --- Summary Posting"
3010 LET GLMEMO$=GLCONTROL$(51,30),GLREF1$="",GLREF2$="",GLREF3$=""
3020 EXTRACT (GLT04_DEV,KEY=B0$,DOM=3500)IOL=GLT04A
3030 IF B1$(31,30)=GLMEMO$ THEN GOTO 3500
3100 REM " --- New summary record required"
3110 DIM B[4]
3120 LET DETAIL$="Y"
3500 REM " --- Format remaining GLT-04 fields"
3510 IF DETAIL$="Y" THEN LET SEQUENCE=SEQUENCE+1
3520 LET B0$(K9)=STR(SEQUENCE:"00000"),B1$(1)=GLREF1$,B1$(11)=GLREF2$
3530 LET B1$(21)=GLREF3$,B1$(31)=GLMEMO$,B[0]=B[0]+AMOUNT
3540 IF GLCONTROL$(40,1)="Y" THEN LET B[1]=B[1]+UNITS
3600 REM " --- Format GLT-05 fields"
3610 LET X0$(1,2)=B0$(1,2),X0$(3,6)=B0$(6+P[4],6),X0$(9,2)=B0$(3,2)
3620 LET X0$(11,P[4])=B0$(22,P[4]),X0$(11+P[4],7)=B0$(5,7)
3630 LET X0$(18+P[4],4)=B0$(12,4),X0$(22+P[4],5)=B0$(22+P[4],5)
3900 REM " --- Write GLT-04/GLT-05 records"
3910 WRITE (GLT04_DEV,KEY=B0$)IOL=GLT04A
3920 IF GLT05_DEV THEN WRITE (GLT05_DEV,KEY=X0$)IOL=GLT05A
4000 REM " --- All done"
4090 GOTO 9900
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8070 DEF FNN$(Q$)=Q$(3,4)+Q$(1,2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9220 LET STATUS=E0
9230 IF STATUS=0 THEN LET STATUS=256
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Calling Program"
9950 EXIT 
9999 END

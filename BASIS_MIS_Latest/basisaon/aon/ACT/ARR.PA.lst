0010 REM "ARR - INVOICE DATE MODIFICATION REGISTER"
0020 REM "Program ARR.PA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=10
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARE-06",FILES$[2]="ARM-01",FILES$[3]="ART-01"
0135 LET FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET ARE06_DEV=CHANNELS[1],ARM01_DEV=CHANNELS[2],ART01_DEV=CHANNELS[3]
0185 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLists
0210 ARE06A: IOLIST A0$,A[ALL]
0220 ARM01A: IOLIST B0$,B1$
0230 ART01A: IOLIST X0$,X[ALL]
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$,WHEN$=F0$(7,3)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 DIM P[2]
0450 LET P[0]=NUM(P2$(1,2))
0500 REM " --- Init Data
0510 LET H0=65,H1=120,H=0,H1$=N1$,H2$=N3$,HEADINGS=2,WIDTH=132,CLOCK$=""
0520 DIM HEADINGS$[HEADINGS],A[1]
0530 LET L9=59,L=L9+1
0550 LET M0=LEN(M0$),M1=LEN(M1$)
0560 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$
0600 CALL "SYC.GA",7,1,"","",STATUS
0605 IF STATUS THEN GOTO 9900
0700 REM " --- Background
0720 CALL "SYC.NB","Printing:",M0,COLUMN
1000 REM " --- Initial Read
1020 READ (ARE06_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Customer Read
1120 READ (ARE06_DEV,END=4000)IOL=ARE06A
1200 REM " --- Breaks
1220 IF A0$(1,2)<>N0$ THEN GOTO 4000
1240 PRINT @(COLUMN,11),FNF$(A0$(5,P[0]),M0$),
1500 REM " --- Print
1520 IF L>L9 THEN GOSUB 5000
1540 GOSUB 6000
1560 PRINT (7)@(0),FNF$(A0$(5,P[0]),M0$)," ",B1$(1,30),@(38),A0$(11,7),@(51),A0$(20,2),@(62),FNB$(A0$(22,3)),@(74),FNB$(A0$(25,3)),@(86),FNB$(A0$(28,3))
1580 LET L=L+1,T0=T0+A[0]
1900 GOTO 1100
4000 REM " --- All done"
4100 REM " --- Update?"
4110 CLOSE (7,ERR=4120)
4120 CALL "SYC.NB","Updating:",M0,COLUMN
4130 CALL "SYC.YN",0,"Are You Ready To Update The Invoice Date Modification Register",0,V$,V3
4160 IF V$="NO" THEN GOTO 9900
4180 IF V$<>"YES" THEN GOTO 4140
4190 READ (ARE06_DEV,KEY=N0$,DOM=4200)
4200 REM " --- Update
4210 LET K$=KEY(ARE06_DEV,END=9900)
4220 IF K$(1,2)<>N0$ THEN GOTO 9900
4230 READ (ARE06_DEV)IOL=ARE06A
4240 PRINT @(COLUMN,11),FNF$(A0$(5,P[0]),M0$),
4320 DIM X0$(50),X[1]
4340 LET X0$(1)=A0$(1,17)+"00"
4350 EXTRACT (ART01_DEV,KEY=X0$(1,19),DOM=4390)IOL=ART01A
4355 IF LEN(X0$)<40 THEN LET X0$=X0$+FILL(40-LEN(X0$))
4360 LET X0$(22,11)=A0$(20,11),X0$(40,1)="*"
4370 WRITE (ART01_DEV,KEY=X0$(1,19))IOL=ART01A
4380 REMOVE (ARE06_DEV,KEY=K$)
4390 GOTO 4200
5000 REM " --- Heading"
5020 LET L=HEADINGS+1
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,WIDTH,WHEN$,CLOCK$,STATUS
5100 PRINT (7)@(0),"Customer",@(38),"Invoice",@(50),"Terms",@(62),"Invoice",@(76),"Due",@(86),"Discount"
5110 PRINT (7)@(0),"No.",@(M0+1),"Name",@(38),"Number",@(50),"Code",@(64),"Date",@(76),"Date",@(88),"Date",'LF'
5180 LET L=L+3
5190 RETURN 
6000 REM " --- Find Customer
6020 DIM B1$(30)
6040 LET B1$(1)="Not On File"
6060 FIND (ARM01_DEV,KEY=N0$+A0$(5,6),DOM=6080)IOL=ARM01A
6090 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

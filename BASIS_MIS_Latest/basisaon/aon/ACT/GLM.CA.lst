0010 REM "GLM - Journal ID Code Maintenance"
0020 REM "Program GLM.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-03",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM03_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 GLM03A: IOLIST A0$(1),A1$,A[ALL]
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)
0500 REM " --- Initializations
0510 DIM A0$(32),A[2]
0540 LET A0$(1,2)=N0$,K9=4,CHANNEL=GLM03_DEV,RECORD$="",NUMBER=2,PROGRAM$=""
0700 REM " --- Background
0750 PRINT 'SB',@(12,3),"Journal ID:",@(11,4),"Description:",@(9,5),"Journal Entry:",@(2,6),"Year End Adjustments:",@(0,7),"Post To Locked Periods:",'SF',
0900 REM " --- Position File
0920 READ (GLM03_DEV,KEY=N0$,DOM=0930)
1000 REM " --- Key Field
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(GLM03_DEV,END=1050)
1030 IF V2$(1,2)<>N0$ THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(3)
1050 LET V0$="S",V1$="CRK",V3$="",V0=2,V1=24,V2=3,I0=0
1055 LET V4$="Enter A Valid Journal ID Code (<F3>=Lookup)"
1060 GOSUB 7000
1070 ON V3 GOTO 1080,1000,1000,1075,4500
1075 GOSUB 6000
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(3)=V$
1200 REM " --- Retrieve record
1220 FIND (GLM03_DEV,KEY=A0$(1,K9),DOM=1400)IOL=GLM03A
1230 IF A0$(26,1)=" " THEN LET A0$(26,1)="N"
1240 IF A0$(27,1)=" " THEN LET A0$(27,1)="N"
1300 REM " --- Display"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record
1410 LET V4$="Is This A New Record"
1420 CALL "SYC.YN",1,V4$,1,V$,V3
1440 ON V3 GOTO 1490,1400,1400,1400,9900
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Initializations for New Record
1510 DIM A[2]
1520 LET A0$(25,3)="NNN",A1$=""
1590 GOTO 3900
2000 REM " --- Description
2010 LET V4$="Enter The Description Of This Journal ID Code"
2020 LET V0$="S",V1$="C",V2$=A0$(5,20),V3$="",V0=20,V1=24,V2=4,I0=1
2040 GOSUB 7000
2050 ON V3 GOTO 2060,2000,3900,2000,3900
2060 LET A0$(5,20)=V$
2090 GOTO 3900
2100 REM " --- Permit Journal Entry?
2110 LET V4$="Permit Journal Entries Using This Journal ID Code (Y/N)?"
2120 LET V0$="Y",V1$="RC",V2$=A0$(25,1),V3$="",V0=1,V1=24,V2=5
2140 GOSUB 7000
2150 ON V3 GOTO 2160,2000,3900,2100,3900
2160 LET A0$(25,1)=V$
2190 GOTO 3900
2200 REM " --- Permit Year End Adjustments?
2210 LET V4$="Permit Year End Adjusting Entries Using This Journal ID Code (Y/N)?"
2220 LET V0$="Y",V1$="RC",V2$=A0$(26,1),V3$="",V0=1,V1=24,V2=6
2240 GOSUB 7000
2250 ON V3 GOTO 2260,2000,3900,2200,3900
2260 LET A0$(26,1)=V$
2290 GOTO 3900
2300 REM " --- Post to Locked Periods?
2310 LET V4$="Permit Posting To Locked Periods Using This Journal ID Code (Y/N)?"
2320 LET V0$="Y",V1$="RC",V2$=A0$(27,1),V3$="",V0=1,V1=24,V2=7
2340 GOSUB 7000
2350 ON V3 GOTO 2360,2000,3900,2300,3900
2360 LET A0$(27,1)=V$
2390 GOTO 3900
3900 REM " --- Next field
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>K0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 LET V3=0
3960 ON I0 GOTO 1000,2000,2100,2200,2300,3990
3990 LET I0=K0
4000 REM " --- Correct?
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record
4120 WRITE (GLM03_DEV,KEY=A0$(1,K9))IOL=GLM03A
4190 GOTO 1000
4200 REM " --- Remove record
4220 REMOVE (GLM03_DEV,KEY=A0$(1,K9),DOM=4230)
4290 GOTO 1000
4500 REM " --- Print?
4510 LET V4$="Print A List Of All Records"
4520 CALL "SYC.YN",1,V4$,1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4800,9900
4800 REM " --- Report Initializations
4810 LET NUMBER=4,TOTAL$="Journal ID Codes",HEADINGS=3,WIDTH=80
4820 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4830 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4840 DIM HEADINGS$[HEADINGS]
4850 LET FIELD$[0]="Code",FIELD[0]=1,POSITION[0]=3
4860 LET LENGTH[0]=2,TYPE$[0]="S",MASK$[0]=""
4870 LET FIELD$[1]="Description",FIELD[1]=1,POSITION[1]=5
4880 LET LENGTH[1]=20,TYPE$[1]="S",MASK$[1]=""
4890 LET FIELD$[2]="Journal Entry?",FIELD[2]=1,POSITION[2]=25
4900 LET LENGTH[2]=1,TYPE$[2]="Y",MASK$[2]=""
4910 LET FIELD$[3]="Year End?",FIELD[3]=1,POSITION[3]=26
4920 LET LENGTH[3]=1,TYPE$[3]="Y",MASK$[3]=""
4930 LET FIELD$[4]="Locked Periods?",FIELD[4]=1,POSITION[4]=27
4940 LET LENGTH[4]=1,TYPE$[4]="Y",MASK$[4]=""
4980 LET HEADINGS$[0]=N1$,HEADINGS$[1]=TOTAL$+" Listing"
4990 RUN "SYR.AA"
5000 REM " --- Display record
5010 PRINT @(24,3),A0$(3,2),@(24,4),A0$(5,20),@(24,5),A0$(25,1),@(24,6),A0$(26,1),@(24,7),A0$(27,1),
5090 RETURN 
6000 REM " --- Lookup
6010 LET NUMBER=1,TITLE$="Journal ID Codes"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6025 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6035 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=2
6040 LET FIELD[1]=1,POSITION[1]=5,LENGTH[1]=20
6055 LET ROW=V2+1,COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6060 LET V$="SKIP"
6070 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

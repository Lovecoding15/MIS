0010 REM "APE - MANUAL CHECK ENTRY 2"
0020 REM "Program APE.CB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0060 ON O0 GOTO 0070,1200
0100 REM " --- OPEN/LOCK FILES"
0200 REM " --- IOLISTS"
0210 APE02A: IOLIST A0$(1),A1$,A[0],A[1],A[2]
0220 APM01A: IOLIST B0$,B1$
0230 APM02A: IOLIST C0$,C1$,C2$
0240 APM03A: IOLIST D0$(1),D1$(1)
0260 APM10A: IOLIST X0$
0310 APE22A: IOLIST X0$,X[0],X[1],X[2]
0360 APM09A: IOLIST K0$,K1$
0370 APE05A: IOLIST X0$,X1$,X[0],X[1],X[2]
1000 REM " --- AP TYPE"
1010 PRINT 'CF',@(15,3),'CL',@(15,5),'CL',@(15,4),J1$(1,10),@(40,4),'CL',@(11,12),J1$(1,7),@(40,12),J1$(1,8)
1020 IF P3$(1,1)<>"Y" THEN GOTO 1200
1050 LET V0$="S",V1$="KEC",V2$=A0$(3,2),V0=2,V1=15,V2=3,V4$="Enter A Valid AP Type, <F3>=Code Lookup"
1060 GOSUB 7000
1065 IF V3=4 THEN GOTO 9900
1070 IF V3<>3 THEN GOTO 1080
1072 LET V4=1,V5=APM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="A"
1074 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
1076 IF V$="" THEN GOTO 1000
1080 LET A0$(3,2)=V$
1090 FIND (APM10_DEV,KEY=N0$+"A"+A0$(3,2),DOM=1000)IOL=APM10A
1100 PRINT 'SB',@(15,V2),A0$(3,2)," ",X0$(6,20),'SF',
1200 REM " --- CHECK NO"
1205 LET A0$(5)="",S$=""
1210 PRINT 'CF',@(15,4),J1$(1,10),@(40,4),'CL',@(15,5),'CL',@(0,16),'CL',
1220 LET V0$="Z",V1$="KE",V2$=A0$(5,7),V0=7,V1=15,V2=4
1230 GOSUB 7000
1235 IF V3=2 THEN GOTO 1000
1240 IF V3=4 THEN GOTO 9900
1250 IF V=0 THEN GOTO 1200
1260 IF POS("-"=V$)<>0 THEN GOTO 1200
1270 LET A0$(5,7)=V$,A$=A0$(1,11),O9=0
1280 IF P3$(1,1)<>"Y" THEN LET A$(3,2)=""
1290 DIM T[2]
1300 READ (APE02_DEV,KEY=A$,DOM=1400)IOL=APE02A
1320 GOSUB 5000
1330 GOSUB 6000
1340 GOSUB 5200
1380 LET O9=1
1390 IF A0$(31,1)="M" THEN GOTO 4100
1395 GOTO 4000
1400 REM " --- OPEN CHECK"
1410 READ (APE05_DEV,KEY=A$,DOM=1420)
1420 LET K$=KEY(APE05_DEV,END=1600)
1430 IF K$(1,11)<>A$ THEN GOTO 1600
1440 READ (APE05_DEV)IOL=APE05A
1450 LET A0$(1)=X0$(1,17)+J1$(1,10)+X0$(28,4),A$=A0$(1,11)
1460 IF P3$(1,1)<>"Y" THEN LET A$(3,2)=""
1500 REM " --- REVERSAL"
1510 IF POS(A0$(31,1)="CM")=0 THEN GOTO 1700
1520 LET S$="Y"
1580 LET A0$(31,1)="R"
1590 GOTO 1670
1600 REM " --- MANUAL/VOID/REVERSAL"
1610 IF A0$(31,1)<>" " THEN LET V2$=A0$(31,1) ELSE LET V2$="M"
1620 LET V0$="S",V1$="R",V3$="",V4$="M=Manual, V=Void, R=Reversal",V0=1,V1=40,V2=4
1625 IF S$="Y" THEN LET V4$="M=Manual, V=Void"
1630 GOSUB 7000
1635 IF V3=4 THEN GOTO 9900
1640 IF V3=2 THEN GOTO 1200
1645 IF POS(V$="MVR")=0 THEN GOTO 1620
1647 IF S$="Y" AND POS(V$="MV")=0 THEN GOTO 1620
1650 LET A0$(31,1)=V$
1670 LET O0=1,O9=0
1690 GOTO 4100
1700 REM " --- RECYCLE"
1710 LET V0$="Y",V1$="C",V0=1,V2=22
1720 IF A0$(31,1)="V" THEN LET V4$="This Check Was Voided, Do You Wish To Re-Use This Check (Y/N)? ",V1=71
1725 IF A0$(31,1)="R" THEN LET V4$="This Check Was Reversed, Do You Wish To Re-Use This Check (Y/N)? ",V1=72
1727 IF POS(A0$(31,1)="VR")=0 THEN GOTO 1750
1729 PRINT @(V1,V2),'RB',
1730 GOSUB 7000
1735 IF V$="Y" THEN LET S$="Y"
1740 ON POS(V$="YN") GOTO 1700,1750,1200
1750 LET A0$(31,1)="M"
1790 GOTO 1600
2110 PRINT 'CF',
4000 REM " --- CORRECT"
4010 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct (Y/Delete)? ",V0=6,V1=63,V2=22
4020 GOSUB 7000
4040 IF V$="DELETE" THEN GOTO 4200
4050 IF V$<>"Y" THEN GOTO 4000
4090 GOTO 1000
4100 GOSUB 5000
4120 RUN "APE.CC"
4200 REM " --- DELETE"
4210 LET A$=A0$(1,11)
4220 IF P3$(1,1)<>"Y" THEN LET A$(3,2)=""
4230 REMOVE (APE02_DEV,KEY=A$,DOM=4240)
4240 LET K$=KEY(APE02_DEV,END=4290)
4250 IF K$(1,11)<>A$ THEN GOTO 4290
4260 REMOVE (APE02_DEV,KEY=K$)
4270 GOTO 4240
4290 IF A0$(12,4)="TEMP" THEN REMOVE (APM03_DEV,KEY=D0$,DOM=4291)
4295 GOTO 1200
5000 REM " --- DISPLAY HEADING"
5020 PRINT 'SB',@(15,4),A0$(5,7)
5030 IF A0$(31,1)="M" THEN PRINT @(40,4),"Manual" ELSE IF A0$(31,1)="R" THEN PRINT @(40,4),"Reversal" ELSE PRINT @(40,4),"Void"
5040 PRINT 'SF'
5090 RETURN 
5200 REM " --- DISPLAY DETAIL"
5205 DIM C1$(22)
5210 LET B1$=A1$
5220 IF A0$(12,4)="TEMP" THEN GOTO 5300
5230 FIND (APM01_DEV,KEY=N0$+A0$(12,6),DOM=5270)IOL=APM01A
5240 LET B1$=B1$(1,30)
5250 FIND (APM02_DEV,KEY=N0$+A0$(12,6)+A0$(3,2),DOM=5260)IOL=APM02A
5260 PRINT @(15,5),FNF$(A0$(12,P[0]),M0$)," ",B1$,
5265 GOSUB 6200
5270 GOTO 5350
5300 PRINT 'SB',@(15,5),"Temp",'SF',@(25,5),B1$,
5310 DIM D0$(14),D1$(105)
5311 IF A0$(31,1)<>"R" THEN GOTO 5315
5312 READ (APE05_DEV,KEY=A0$(1,17),DOM=5313)
5313 LET K2$=KEY(APE05_DEV,END=5350)
5314 IF K2$(1,17)<>A0$(1,17) THEN GOTO 5350 ELSE GOTO 5324
5315 READ (APE02_DEV,KEY=A0$(1,17)+" ",DOM=5320)
5320 LET K2$=KEY(APE02_DEV,END=5350)
5321 IF LEN(K2$)<29 THEN GOTO 5350
5322 IF K2$(28,2)<>"00" THEN GOTO 5350
5324 READ (APM03_DEV,KEY=K2$(1,4)+K2$(18,10),DOM=5350)IOL=APM03A
5330 IF D0$<>K2$(1,4)+K2$(18,10) THEN GOTO 5350
5340 PRINT 'SB',@(6,15),"Address:",'SF',@(25,5),D1$(1,30),@(15,15),D1$(31,24),@(15,16),D1$(55,24),@(15,17),D1$(79,24),
5350 PRINT @(1,8),FNB$(A0$(28,3)),@(23),T[0]:M1$,T[1]:M1$,T[0]-T[2]-T[1]:M1$,
5360 IF P3$(3,1)="Y" THEN PRINT @(23+3*M1,8),T[2]:M1$,
5390 RETURN 
6000 REM " --- GET TOTAL"
6010 DIM T[2]
6015 READ (APE22_DEV,KEY=A0$(1,11),DOM=6020)
6020 LET K$=KEY(APE22_DEV,END=6090)
6030 IF K$(1,11)<>A$ THEN GOTO 6090
6040 READ (APE22_DEV)IOL=APE22A
6070 LET T[0]=T[0]+X[0],T[1]=T[1]+X[1],T[2]=T[2]+X[2]
6080 GOTO 6020
6090 RETURN 
6200 REM " --- COMMENTS
6210 LET L2=15
6220 READ (APM09_DEV,KEY=N0$+A0$(12,6),DOM=6230)
6230 LET K$=KEY(APM09_DEV,END=6290)
6235 IF K$(1,8)<>A0$(1,2)+A0$(12,6) THEN GOTO 6290
6240 READ (APM09_DEV,KEY=K$)IOL=APM09A
6250 IF L2>20 THEN GOTO 6300
6260 PRINT @(14,L2),'SF',"<",K1$,">",'SB',
6270 LET L2=L2+1
6280 GOTO 6230
6290 RETURN 
6300 REM " --- COMMENTS CONTINUED
6320 LET V0$="S",V1$="C",V2$="",V3$="",V4$="<Enter>=Continue, <F4>=End:",V0=1,V1=55,V2=22
6330 GOSUB 7000
6340 IF V3=4 THEN GOTO 6390
6350 PRINT @(0,15),'CE'
6360 LET L2=15
6370 GOTO 6260
6390 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

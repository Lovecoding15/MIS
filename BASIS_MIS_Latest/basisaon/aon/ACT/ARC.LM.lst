0010 REM "ARC - Standard Message Lookup"
0020 REM "Program ARC.LM"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "SELECTION$: User selection (returned)"
0050 REM 
0080 SETERR 9000
0085 SETESC 9900
0090 ENTER SELECTION$
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-10",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET ARM10_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 ARM10G: IOLIST A0$(1)
0280 SYS01C: IOLIST *,COMPANY$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0425 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),SPEEDSEARCH$="N",COMPANY$=""
0440 LET X$=STBL("!SPEEDSEARCH",ERR=0450),N0$=X$(1,2),SPEEDSEARCH$="Y"
0450 FIND (SYS01_DEV,KEY=N0$,DOM=0460)IOL=SYS01C
0460 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)
0500 REM " --- Initializations"
0510 DIM OPTLIST$[1,MAXROW,5],PARAM$[1,MAXROW,3],A0$(32)
0540 LET WIDTH=40,HEIGHT=12,WIN_X=1,WIN_Y=3,MAX_ROW=HEIGHT-5,M0$="00"
0560 LET PGM$=PGM(-2),OPTIONS=0,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3,RECORD$="G"
0570 LET OTITLE$="Code",OWIDTH=10,OHEIGHT=3,TITLE$="Message Codes"
0650 REM " --- Window arrays"
0670 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0680 LET HEADING$(2)="Number",HEADING$(10)="Description"
0690 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Options"
0815 CALL "SYC.WB",NAME$,0
0820 PRINT 'CF',
0825 CALL "SYC.WA",0,OWIDTH,OHEIGHT,OWIN_X,OWIN_Y,OTITLE$,ONAME$
0830 LET V0$="Z",V1$="KCE",V2$="",V3$=M0$,V4$="",V0=2,V1=1,V2=0
0840 GOSUB 7000
0850 IF V3=6 THEN GOTO 9900
0860 IF V3=5 THEN GOTO 9900
0870 IF V3=4 THEN GOTO 9900
0880 LET START$=V$
0890 CALL "SYC.WD",ONAME$
0900 REM " --- Detail Window
0915 CALL "SYC.WB",NAME$,0
0930 IF SPEEDSEARCH$<>"Y" THEN GOTO 1000
0950 REM " --- Display SpeedSearch Company"
0955 DIM X$(WIDTH+SWIDTH)
0960 LET X$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
0965 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
0970 CALL "SYC.WA",0,WIDTH+SWIDTH,3,WIN_X,WIN_Y+HEIGHT,"",CNAME$
0975 PRINT @(0,0),'BR',X$,'ER',
0980 CALL "SYC.WB",NAME$,0
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW-1,1]
1020 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$=""
1300 REM " --- Position file"
1390 READ (ARM10_DEV,KEY=N0$+RECORD$+START$,DOM=1400)
1400 REM " --- Get next key"
1410 LET K$=KEY(ARM10_DEV,END=3000)
1420 IF POS(N0$+RECORD$=K$)=0 THEN GOTO 3000
1500 REM " --- Read record"
1510 READ (ARM10_DEV,KEY=K$,ERR=3000)IOL=ARM10G
1520 IF K$(6,1)<>"1" THEN GOTO 1900
1600 REM " --- Position past "2" record"
1610 LET K$(6,1)="2"
1620 READ (ARM10_DEV,KEY=K$,DOM=1630)
2000 REM " --- Display page?"
2010 IF L<MAX_ROW THEN GOTO 2400
2020 LET MODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 0800
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2400 REM " --- Lookup window record"
2410 DIM G1$(WIDTH-2)
2420 LET G1$(2)=A0$(4,2),G1$(10)=A0$(7,20)
2430 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$(4,2)
2490 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2990 GOTO 1400
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=WIN_X+2
3035 LET MESSAGE$[0]="No Messages Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page"
3510 LET MODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",MODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,MAX_ROW,5
6280 LET L=1,X1=1
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to calling program"
9910 CLOSE (SYS01_DEV,ERR=9911)
9915 CLOSE (ARM10_DEV,ERR=9916)
9925 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9930 IF ONAME$<>"" THEN CALL "SYC.WD",ONAME$
9940 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9945 IF SELECTION$="END" THEN LET SELECTION$=""
9950 EXIT 
9999 END

0010 REM "GLU - Journal Entry Update"
0020 REM "Program GLU.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLT-04",FILES$[2]="GLT-05"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLT04_DEV=CHANNELS[1],GLT05_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 GLE01A: IOLIST A0$(1),A1$(1),A2$,A[ALL]
0220 GLE11A: IOLIST B0$(1),B1$(1),B2$(1),B3$,B[ALL]
0240 GLM03A: IOLIST D0$(1)
0400 REM " --- Parameters"
0410 DIM INFO$[20]
0420 CALL "SYC.VA","GL",INFO$[ALL]
0430 LET GL$=INFO$[9]
0500 REM " --- Initializations"
0510 DIM A0$(K8),A1$(48),A[2],B0$(K9),B1$(P[4]),B2$(30),B[4]
0700 REM " --- Background"
0710 PRINT @(0,10),'CE',
0750 CALL "SYC.NB","Updating",13,COLUMN
0800 REM " --- Options"
0810 LET V4$="Are You Ready to Update the Journal Entry Register"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
0970 REM " --- Position File"
0980 LET A0$(1)=N0$
0990 READ (GLE01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next GLE-01 header record"
1010 READ (GLE01_DEV,END=4000)IOL=GLE01A
1020 IF POS(N0$=A0$)<>1 THEN GOTO 4000
1030 PRINT @(COLUMN,11),A0$(3,2)," ",A0$(5,10),
1500 REM " --- Set Journal ID in !GLCONTROL$"
1510 DIM GLCONTROL$(640),D0$(32)
1520 LET D0$(1)=N0$+A0$(3,2)
1530 FIND (GLM03_DEV,KEY=D0$(1,4),DOM=1540)IOL=GLM03A
1570 LET GLCONTROL$=STBL("!GLCONTROL",ERR=1900)
1580 LET GLCONTROL$(18,2)=D0$(3,2),GLCONTROL$(45,3)="Y"+D0$(26,2)
1590 LET GLCONTROL$=STBL("!GLCONTROL",GLCONTROL$)
1900 REM " --- Position GLE-11"
1990 READ (GLE11_DEV,KEY=A0$,DOM=2000)
2000 REM " --- Read next GLE-11 detail record"
2010 LET K$=KEY(GLE11_DEV,END=3000)
2020 IF POS(A0$=K$)<>1 THEN GOTO 3000
2030 READ (GLE11_DEV)IOL=GLE11A
2100 REM " --- Post detail record"
2110 LET ACCOUNT$=B1$,WHEN$=A1$(31,3),REF1$=A0$(5,10),REF2$=B0$(K8+1,3)
2120 LET MEMO$=B2$,AMOUNT=B[0],UNITS=B[1]
2130 IF A1$(37,2)<>"  " THEN LET REF3$="Cycle "+A1$(37,2) ELSE LET REF3$=""
2190 GOSUB GLPOST
2200 REM " --- Reversal?"
2210 IF FNP$(A1$(34,3))="" THEN GOTO 2800
2220 LET WHEN$=A1$(34,3),REF3$="Reversal",AMOUNT=-B[0],UNITS=-B[1]
2290 GOSUB GLPOST
2800 REM " --- Remove detail record"
2810 REMOVE (GLE11_DEV,KEY=B0$)
2900 REM " --- Loop back for next detail record"
2990 GOTO 2000
3000 REM " --- Remove header record"
3010 REMOVE (GLE01_DEV,KEY=A0$)
3900 REM " --- Loop back for next header"
3990 GOTO 1000
4000 REM " --- All done"
4010 CALL "GLC.CA",STATUS
4020 CALL "SYC.BB",STATUS
4090 GOTO 9900
6900 REM " --- Standard G/L Posting Routine"
6910 GLPOST: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,WHEN$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "GLC - Account/Budget Maintenance Column Entry"
0020 REM "Program GLC.SA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "SYS01_DEV: SYS-01 Channel (Input)"
0050 REM "GLM08_DEV: GLM-08 Channel (Input)"
0055 REM "WIN_Y    : Window Row (Input)"
0060 REM "TITLE$   : Window Title (Input)"
0065 REM "VALID$   : Valid Options (Input)"
0066 REM "         : Null = All
0067 REM "         : A    = Actuals Only"
0068 REM "         : B    = Budgets Only"
0070 REM "COLUMNS$ : GLM-02 Record ID's (Input/Returned)"
0075 REM "TYPES$   : Amounts/Units (Input/Returned)"
0080 SETESC 9000
0085 SETERR 9000
0090 ENTER SYS01_DEV,GLM08_DEV,WIN_Y,WIN_TITLE$,VALID$,COLUMNS$,TYPES$
0200 REM " --- IOLIST's"
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 GLS01A: IOLIST P0$,P1$,P2$(1),P3$(1),P4$(1),M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 DIM P2$(10),P3$(325),P4$(32),P[4]
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),N2$="GL"
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0500 REM " --- Initializations"
0510 DIM D0$(4)
0540 LET WIDTH=69,HEIGHT=10,WIN_X=6
0550 IF WIN_Y=0 THEN LET WIN_Y=8
0560 IF WIN_Y+HEIGHT>23 THEN LET WIN_Y=23-HEIGHT
0570 LET ACTUALS$="024",BUDGETS$="135ABCDEFGHIJKLMNOPQRSTUVWXYZ"
0600 REM " --- Window heading"
0610 DIM HEADING$(WIDTH-2)
0650 LET HEADING$(2)="Column  ID  Type  Description"
0660 LET HEADING$(52)="Column Title"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,WIN_TITLE$,NAME$
0750 PRINT 'SB','BR',HEADING$,'ER','SF',
0770 FOR X=1 TO 4
0780 PRINT 'SB',@(1,X+1),X:"0",'SF',
0790 NEXT X
1300 REM " --- Display"
1310 GOSUB 5000
1390 GOTO 4000
2000 REM " --- Record ID"
2010 LET P=INT(I0/2)+1,V0=1,V1=9,V2=P+1
2015 IF I0=0 THEN LET V3=0
2020 LET V0$="S",V1$="C^",V2$=COLUMNS$(P,1),V3$=""
2025 IF VALID$="A" THEN IF POS(V2$=BUDGETS$)>0 THEN GOTO 3900
2030 IF VALID$="B" THEN IF POS(V2$=ACTUALS$)>0 THEN GOTO 3900
2040 LET V4$="Enter the ID of the record to be displayed (<F1>=None/<F3>=Lookup)"
2050 GOSUB 7000
2070 ON V3 GOTO 2150,2080,3900,2100,3900
2080 LET COLUMNS$(P,1)=""
2090 GOTO 3900
2100 REM 
2110 CALL "GLC.LA",VALID$,SELECTION$,FLAG$,DESCRIPTION$,TITLE$
2115 IF SELECTION$="" THEN GOTO 2000
2120 IF VALID$="A" THEN IF POS(SELECTION$=BUDGETS$)>0 THEN GOTO 2000
2125 IF VALID$="B" THEN IF POS(SELECTION$=ACTUALS$)>0 THEN GOTO 2000
2130 GOSUB 5200
2135 IF STATUS<>0 THEN GOTO 2000
2140 LET COLUMNS$(P,1)=SELECTION$,TYPES$(P,1)=FLAG$
2145 GOTO 3900
2150 IF V$="" THEN GOTO 2060
2155 IF VALID$="A" THEN IF POS(V$=BUDGETS$)>0 THEN GOTO 2000
2160 IF VALID$="B" THEN IF POS(V$=ACTUALS$)>0 THEN GOTO 2000
2165 LET COLUMNS$(P,1)=V$
2170 PRINT @(V1,V2),COLUMNS$(P,1),
2190 GOTO 3900
2200 REM " --- Type"
2210 LET P=INT(I0/2)+1,V0=1,V1=13,V2=P+1
2215 LET V0$="S",V1$="C^",V2$=TYPES$(P,1),V3$="AU"
2220 LET SELECTION$=COLUMNS$(P,1),V$=""
2225 IF FNP$(SELECTION$)="" THEN GOTO 2400
2230 IF P4$(3,1)<>"Y" THEN LET V$="A"; GOTO 2260
2235 IF VALID$="A" THEN IF POS(SELECTION$=BUDGETS$)>0 THEN GOTO 3900
2240 IF VALID$="B" THEN IF POS(SELECTION$=ACTUALS$)>0 THEN GOTO 3900
2245 LET V4$="Select information to be displayed (A=Amounts/U=Units)"
2250 GOSUB 7000
2255 ON V3 GOTO 2260,2200,3900,2200,3900
2260 IF POS(V$="AU")=0 THEN GOTO 2200
2270 LET FLAG$=V$,TYPES$(P,1)=V$
2280 GOSUB 5200
2285 IF STATUS<>0 THEN GOTO 2200
2290 GOTO 3900
2400 REM 
2410 LET TYPES$(P,1)="",FLAG$=""
2420 GOSUB 5200
2490 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,2000,2200,2000,2200,2000,2200,3990
3990 LET I0=0
3995 GOSUB 5000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V0=1,V1=FNV1(V4$,WIDTH),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Verify that all entries are valid"
4110 FOR X=1 TO 4
4120 LET SELECTION$=COLUMNS$(X,1),FLAG$=TYPES$(X,1),P=0
4130 IF P4$(2,1)<>"Y" THEN IF POS(SELECTION$=" 024")=0 THEN EXITTO 4200
4140 IF P4$(3,1)<>"Y" THEN IF FLAG$="U" THEN EXITTO 4200
4160 GOSUB 5500
4170 IF STATUS<>0 THEN EXITTO 4200
4180 NEXT X
4190 GOTO 9900
4200 REM " --- Screen contains invalid entry"
4210 LET I0=(X-1)*2-1
4230 LET V4$="Column "+STR(X)+" contains an invalid entry (<Enter>=Continue):"
4240 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV1(V4$,WIDTH),V2=22
4250 GOSUB 7000
4260 LET V3=0
4290 GOTO 3900
5000 REM " --- Display"
5010 PRINT 'CF',
5020 FOR X=1 TO 4
5030 LET SELECTION$=COLUMNS$(X,1),FLAG$=TYPES$(X,1),V2=X+1
5040 GOSUB 5200
5080 NEXT X
5090 RETURN 
5200 REM " --- Print description/title"
5210 GOSUB 5500
5220 PRINT @(9,V2),'CL',SELECTION$,@(13),FLAG$,@(19),DESCRIPTION$,@(51),TITLE$,
5290 RETURN 
5500 REM " --- Retrieve description and column heading"
5510 LET STATUS=0,DESCRIPTION$="",TITLE$=""
5520 IF FNP$(SELECTION$)="" AND FNP$(FLAG$)="" THEN GOTO 5590
5550 CALL "GLC.FA",GLM08_DEV,N0$,SELECTION$,FLAG$,DESCRIPTION$,TITLE$,STATUS
5590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV1(Q$,Q)=FNC(Q$,Q)+LEN(Q$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to calling program"
9910 CALL "SYC.WD",NAME$
9950 EXIT 
9999 END

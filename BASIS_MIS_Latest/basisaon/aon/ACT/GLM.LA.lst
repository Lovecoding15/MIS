0010 REM "GLM - G/L Recurring Journal Entry Cycle Code Maintenance"
0020 REM "Program GLM.LA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-07",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM07_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 GLM07A: IOLIST A0$(1),A1$(1),A2$,A[0],A[1],A[2]
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N4$=F5$,NUMBER=1
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)
0500 REM " --- Initializations
0510 DIM A0$(4),A1$(48),A[2]
0590 LET RECORD$="",PROGRAM$="",A0$(1)=N0$
0700 REM " --- Background
0750 PRINT 'SB',@(5,4),"Cycle Code:",@(4,5),"Description:",@(0,6),"Process Through:",@(6,7),"Last Used:",'SF',
0900 REM " --- Position file
0990 READ (GLM07_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Cycle Code
1005 PRINT 'CF',
1010 LET V2$="",V2$=KEY(GLM07_DEV,END=1040)
1020 IF V2$(1,2)<>N0$ THEN LET V2$=""
1030 IF V2$<>"" THEN LET V2$=V2$(3)
1040 LET V0$="S",V1$="C",V3$="",V0=2,V1=17,V2=4,I0=0
1045 LET V4$="Enter A Valid Cycle Code (<F3>=Lookup)"
1050 GOSUB 7000
1055 ON V3 GOTO 1070,1000,1000,1060,4500
1060 GOSUB 6000
1070 IF V$="" THEN GOTO 1000
1080 LET A0$(3)=V$
1200 REM " --- Retrieve record
1210 READ (GLM07_DEV,KEY=A0$,DOM=1400)IOL=GLM07A
1300 REM " --- Display record
1320 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record
1410 LET V4$="Is This A New Record?"
1420 CALL "SYC.YN",1,V4$,1,V$,V3
1440 ON V3 GOTO 1490,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record
1510 DIM A1$(48),A[2]
1590 GOSUB 5000
1600 REM " --- Description
1610 LET V0=20,V0$="S",V1$="C",V2$=A1$(1,V0),V3$="",V1=17,V2=5,I0=1
1620 LET V4$="Enter The Description Of This Cycle Code"
1630 GOSUB 7000
1640 ON V3 GOTO 1650,1600,3900,1600,3900
1650 LET A1$(1,V0)=V$
1690 GOTO 3900
1700 REM " --- Process Through Date
1710 LET V0$="D",V1$="C6N",V2$=A1$(21,6),V3$="",V0=6,V1=17,V2=6
1720 LET V4$="Enter The Last Date That This Cycle Code Should Be Used (Or Blank For None)"
1730 GOSUB 7000
1740 ON V3 GOTO 1750,1700,3900,1700,3900
1750 LET A1$(21,6)=V$
1790 GOTO 3900
3900 REM " --- Next field
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1600,1700,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record
4110 WRITE (GLM07_DEV,KEY=A0$)IOL=GLM07A
4190 GOTO 1000
4200 REM " --- Remove record
4210 REMOVE (GLM07_DEV,KEY=A0$,DOM=4230)
4290 GOTO 1000
4500 REM " --- Print?
4510 LET V4$="Print A List Of All Records"
4520 CALL "SYC.YN",1,V4$,1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4800,9900
4800 REM " --- Report Initializations
4810 LET NUMBER=4,HEADINGS=3,WIDTH=80,CHANNEL=GLM07_DEV
4820 LET TOTAL$="G/L Cycle Codes"
4830 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4840 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4850 DIM HEADINGS$[HEADINGS]
4855 LET FIELD$[0]="Code",FIELD[0]=1,POSITION[0]=3
4860 LET LENGTH[0]=2,TYPE$[0]="S",MASK$[0]=""
4865 LET FIELD$[1]="Description",FIELD[1]=2,POSITION[1]=1
4870 LET LENGTH[1]=20,TYPE$[1]="S",MASK$[1]=""
4875 LET FIELD$[2]="Process Thru",FIELD[2]=2,POSITION[2]=21
4880 LET LENGTH[2]=6,TYPE$[2]="D6",MASK$[2]=""
4885 LET FIELD$[3]="Last Used",FIELD[3]=2,POSITION[3]=27
4890 LET LENGTH[3]=6,TYPE$[3]="D6",MASK$[3]=""
4900 LET FIELD$[4]="Time",FIELD[4]=2,POSITION[4]=33
4905 LET LENGTH[4]=4,TYPE$[4]="T",MASK$[4]=""
4985 LET HEADINGS$[0]=N1$,HEADINGS$[1]=TOTAL$+" Listing",N0$=""
4990 RUN "SYR.AA"
5000 REM " --- Display record"
5010 PRINT @(17,4),A0$(3,2),@(17,5),A1$(1,20),
5020 IF FNP$(A1$(21,6))<>"" THEN PRINT @(17,6),FNB6$(A1$(21,6))
5030 IF FNP$(A1$(27,6))<>"" THEN PRINT @(17,7),FNB6$(A1$(27,6))," ",FNT$(A1$(33,4))
5090 RETURN 
6000 REM " --- Cycle Code Lookup"
6010 LET NUMBER=1,TITLE$="G/L Cycle Codes"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=3,LENGTH[0]=2,V$="SKIP"
6050 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=20
6070 LET ROW=V2+1,COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6080 CALL "SYC.LK",GLM07_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8065 DEF FNP$(Q$)=CVS(Q$,2)
8070 DEF FNT$(Q$)=Q$(1,2)+":"+Q$(3,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

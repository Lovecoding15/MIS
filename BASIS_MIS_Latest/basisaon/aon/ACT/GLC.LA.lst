0010 REM "GLC - GLM-02 Record Selection Lookup"
0020 REM "Program GLC.LA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "ACTION$     : Display Control"
0046 REM "            : Null = Display all valid records"
0047 REM "            : A    = Only display actual records"
0048 REM "            : B    = Only display budget records"
0049 REM "            : R    = Only display budget revisions"
0055 REM "SELECTION$  : Record ID 0-5 Std/A-Z Budgets (Returned)"
0060 REM "FLAGS$      : A=Amount/U=Units (Returned)"
0065 REM "DESCRIPTION$: 30 Character Description (Returned)"
0070 REM "TITLE$      : 15 Character Column Title (Returned)"
0075 REM 
0080 SETERR 9000
0085 SETESC 9900
0090 ENTER ACTION$,SELECTION$,FLAG$,DESCRIPTION$,TITLE$
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-08",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET GLM08_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 GLM08A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$,A5$,A[0],A[1],A[2],A[3],A[4],A[5],A[6],A[7],A[8]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 GLS01A: IOLIST P0$,P1$,P2$(1),P3$(1),P4$(1),M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 DIM P2$(10),P3$(325),P4$(32),P[4]
0415 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0420 IF PAGES<3 THEN GOTO 9900
0425 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0430 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0435 LET N0$=F0$(16,2),N2$="GL",MP=13
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0480 FOR X=0 TO 4
0485 LET P[X]=NUM(P2$(X*2+1,2),ERR=0490)
0490 NEXT X
0495 LET P[2]=FNYY_YEAR(P2$(5,2))
0500 REM " --- Initializations"
0510 DIM OPTLIST$[1,MAXROW,6],PARAM$[1,MAXROW,3]
0520 DIM A0$(4),A1$(64),A2$(P[4]),A3$(P[4]),A[8]
0540 LET WIDTH=70,HEIGHT=18,WIN_X=6,WIN_Y=22-HEIGHT,MAX_ROW=HEIGHT-5
0550 LET REVISIONS$="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
0560 LET ACTUALS$="024",BUDGETS$="135"+REVISIONS$
0600 REM " --- Window dimensions"
0610 LET PGM$=PGM(-2),OPTIONS=0,OTITLE$="Method"
0620 CALL "SYC.OL",PGM$,OPTIONS,OWIDTH,OPTLIST$[ALL],PARAM$[ALL],WTITLE$,MAXROW,M0$,2
0630 IF OPTIONS<1 THEN GOTO 9900
0640 LET OWIDTH=OWIDTH+4,OHEIGHT=OPTIONS+4,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3
0650 REM " --- Window arrays"
0670 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0680 LET HEADING$(2)="ID  Type",HEADING$(15)="Description"
0685 LET HEADING$(52)="Column Title"
0690 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0700 REM " --- Background"
0710 IF ACTION$="B" THEN LET WTITLE$="G/L Budget Selection Lookup"
0720 IF ACTION$="R" THEN LET WTITLE$="G/L Budget Revision Lookup"
0730 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,WTITLE$,NAME$
0740 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Which search option?"
0810 DIM STRINGS$[10],NUMBERS[10]
0820 LET STRINGS$[0]=PGM$,STRINGS$[1]=M0$,NUMBERS[0]=MAXROW
0830 LET NUMBERS[1]=OWIN_X,NUMBERS[2]=OWIN_Y,NUMBERS[3]=P[0]
0840 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
0850 CALL "SYC.SM",STRINGS$[ALL],NUMBERS[ALL],FKEY
0860 IF FKEY THEN GOTO 9900
0870 LET PREFIX$=STRINGS$[2],START$=STRINGS$[3],METHOD$=STRINGS$[4]
0880 LET V3$=STRINGS$[5],V0=NUMBERS[4],START$=FNU$(START$)
0900 REM " --- Switch back to main window"
0910 CALL "SYC.WB",NAME$,0
0920 PRINT @(0,1),'CF',
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,5]
1020 LET PAGE=1,L=1,X1=1,X0=0
1030 LET SELECTION$="",FLAG$="",DESCRIPTION$="",TITLE$=""
1100 REM " --- Load up standard record types"
1105 IF ACTION$="R" THEN GOTO 1300
1110 FOR X=0 TO 5
1115 IF MOD(X,2)<>0 THEN IF P4$(2,1)<>"Y" THEN GOTO 1190
1120 LET SELECTION$=STR(X),FLAG$="A"
1125 IF ACTION$="A" THEN IF POS(SELECTION$=BUDGETS$)>0 THEN GOTO 1150
1130 IF ACTION$="B" THEN IF POS(SELECTION$=ACTUALS$)>0 THEN GOTO 1150
1135 GOSUB 6100
1140 IF STATUS=0 THEN GOSUB 6000
1150 IF P4$(3,1)<>"Y" THEN GOTO 1190
1155 LET SELECTION$=STR(X),FLAG$="U"
1160 IF ACTION$="A" THEN IF POS(SELECTION$=BUDGETS$)>0 THEN GOTO 1190
1165 IF ACTION$="B" THEN IF POS(SELECTION$=ACTUALS$)>0 THEN GOTO 1190
1170 GOSUB 6100
1180 IF STATUS=0 THEN GOSUB 6000
1190 NEXT X
1200 REM " --- Budgets?"
1210 IF P4$(2,1)<>"Y" THEN GOTO 3000
1300 REM " --- Position file"
1310 READ (GLM08_DEV,KEY=N0$,DOM=1400)
1400 REM " --- Get next key"
1410 LET K$=KEY(GLM08_DEV,END=3000)
1420 IF POS(N0$=K$)<>1 THEN GOTO 3000
1900 REM " --- Read record"
1910 READ (GLM08_DEV,ERR=1950)IOL=GLM08A
1915 IF START$="" THEN GOTO 2000
1920 IF PREFIX$="A" THEN IF K$(4,1)<>START$ THEN GOTO 2900
1930 IF PREFIX$="*" THEN IF POS(START$=FNU$(A1$))=0 THEN GOTO 2900
1940 GOTO 2000
1950 REM " --- Error reading record"
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1990 READ (GLM08_DEV,KEY=K$(1,LEN(K$)-1)+$FF$,DOM=2900)
2000 REM " --- Test record against ACTION$"
2010 IF ACTION$="A" THEN GOTO 2900
2100 REM " --- Display page?"
2110 IF L<MAX_ROW THEN GOTO 2400
2120 LET XMODE=5
2130 GOSUB 6200
2140 IF FKEY=1 THEN GOTO 0800
2150 IF FKEY=4 THEN GOTO 9900
2160 IF SELECTION$<>"" THEN GOTO 4000
2400 REM " --- Store record"
2410 GOSUB 6000
2900 REM " --- Loop back for next record"
2990 GOTO 1400
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3025 LET V0$="S",V1$="",V2$="",V3$="",V4$=""
3030 LET V0=1,V1=38,V2=INT(HEIGHT/2)-1
3035 PRINT @(1,V2),"No records found (<Enter>=Continue)"
3040 GOSUB 7000
3050 PRINT @(0,V2),'CL',
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page"
3510 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
4000 REM " --- All done"
4010 IF LEN(SELECTION$)<>2 THEN GOTO 9900
4020 LET FLAG$=SELECTION$(2,1),SELECTION$=SELECTION$(1,1)
4090 GOTO 9900
6000 REM " --- Store current record"
6010 DIM G1$(WIDTH-2)
6020 LET X$="Amounts"
6030 IF A0$(4,1)="U" THEN LET X$="Units"
6050 LET G1$(2)=A0$(3,1),G1$(6)=X$
6060 LET G1$(15)=A1$(1,30),G1$(52)=A1$(31,15)
6070 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$(3)
6080 LET X0=X0+1,X1=X1+1,L=L+1
6090 RETURN 
6100 REM " --- Retrieve and store record information"
6110 CALL "GLC.FA",GLM08_DEV,N0$,SELECTION$,FLAG$,DESCRIPTION$,TITLE$,STATUS
6120 LET A0$(1)=N0$,A0$(3)=SELECTION$,A0$(4)=FLAG$
6130 LET A1$(1)=DESCRIPTION$,A1$(31)=TITLE$,STATUS=0
6140 IF START$="" THEN GOTO 6190
6145 IF PREFIX$="" THEN IF A0$(3,1)<START$ THEN LET STATUS=1
6150 IF PREFIX$="*" THEN IF POS(START$=FNU$(A1$))=0 THEN LET STATUS=1
6160 IF PREFIX$="A" THEN IF A0$(4,1)<>START$ THEN LET STATUS=1
6190 RETURN 
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,5
6280 LET L=1,X1=1
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9250 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 CLOSE (SYS01_DEV,ERR=9911)
9915 CLOSE (GLM08_DEV,ERR=9916)
9930 CALL "SYC.WD",ONAME$
9940 CALL "SYC.WD",NAME$
9945 IF SELECTION$="END" THEN LET SELECTION$=""
9950 EXIT 
9999 END

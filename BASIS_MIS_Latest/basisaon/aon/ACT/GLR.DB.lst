0010 REM "GLR - G/L Daily Detail Register (Report Overlay)"
0020 REM "Program GLR.DB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$(1),A1$(1)
0220 GLM03A: IOLIST B0$(1),B1$,B[ALL]
0230 GLT04A: IOLIST C0$(1),C1$(1),C2$,C[ALL]
0500 REM " --- Initializations"
0510 DIM A0$(2+P[4]),A1$(40),B0$(32),B[2],X0$(26+P[4])
0520 DIM C0$(26+P[4]),C1$(64),C[4],O[12],AT[2],DT[2],JT[2],RT[2],XT[2]
0530 DIM MESSAGE$[1]
0590 LET NUMBER=0
0600 REM " --- Initialize Print Positions"
0605 CALL "SYC.RM",M2$,M2,6,10
0610 LET O[9]=H0-1,UNITHDR=H0-1,UNITHDR$="",X=MAX(M0,8)
0615 IF P4$(3,1)="Y" THEN LET O[9]=H0-M2,UNITHDR=H0-6,UNITHDR$="Units"
0620 LET O[8]=O[9]-M1,O[7]=O[8]-M1
0625 LET O[6]=O[7]-8,O[5]=O[6]-3,O[4]=O[5]-4
0630 IF SEQUENCE$="A" THEN LET O[4]=O[7]
0640 LET O[3]=O[4]-48,O[2]=O[3]-36,O[1]=O[2]-X-1
0650 IF SEQUENCE$="D" THEN GOTO 0670
0655 LET O[0]=O[1]-9
0660 IF O[0]<0 THEN LET O[0]=0,O[1]=O[0]+9,O[2]=O[1]+X+1
0665 GOTO 0680
0670 IF O[1]<0 THEN LET O[1]=0,O[2]=O[1]+X+1
0680 LET W=O[3]-O[2]-2
0690 IF W>35 THEN LET W=35
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",M0+9,COLUMN
0900 REM " --- Position File"
0910 LET T0$="",T1$="",T2$="",T3$="",H0$="",H1$="",H2$=""
0920 IF SEQUENCE$="A" THEN GOTO 0950
0930 LET X0$(1)=N0$+BEGVAL$
0940 READ (GLT05_DEV,KEY=X0$,DOM=1000)
0950 LET C0$(1)=N0$+JOURNAL$
0990 READ (GLT04_DEV,KEY=C0$,DOM=1000)
1000 REM " --- Get next GLT-05 key"
1010 IF SEQUENCE$="A" THEN GOTO 1100
1020 LET K$=KEY(GLT05_DEV,END=4000)
1025 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 READ (GLT05_DEV)
1040 LET K$=K$(1,2)+K$(9,2)+K$(11+P[4],7)+K$(18+P[4],4)+K$(3,6)+K$(11,P[4])+K$(22+P[4])
1090 GOTO 1200
1100 REM " --- Get next GLT-04 key"
1110 LET K$=KEY(GLT04_DEV,END=4000)
1120 IF POS(N0$=K$)<>1 THEN GOTO 4000
1200 REM " --- Read next GLT-04 record"
1210 READ (GLT04_DEV,KEY=K$,DOM=3900)IOL=GLT04A
1220 IF BEGACCT$<>"" THEN IF C0$(22,P[4])<BEGACCT$ THEN GOTO 3900
1230 IF ENDACCT$<>"" THEN IF C0$(22,P[4])>ENDACCT$ THEN GOTO 3900
1240 IF JOURNAL$<>"" THEN IF C0$(3,2)<>JOURNAL$ THEN GOTO 3900
1250 GOSUB 6800
1255 IF STATUS>0 THEN GOTO 3900
1260 IF SEQUENCE$="D" THEN GOTO 1280
1265 IF BEGVAL$<>"" THEN IF C0$(5,7)<BEGVAL$ THEN GOTO 3900
1270 IF ENDVAL$<>"" THEN IF C0$(5,7)>ENDVAL$ THEN GOTO 3900
1275 GOTO 1300
1280 IF BEGVAL$<>"" THEN IF C0$(16,6)<BEGVAL$ THEN GOTO 3900
1290 IF ENDVAL$<>"" THEN IF C0$(16,6)>ENDVAL$ THEN GOTO 3900
1300 REM " --- Display account being processed"
1310 LET X$=C0$(5,7)
1320 IF SEQUENCE$="D" THEN LET X$=FNB6$(C0$(16,6))
1330 LET X$=X$+" "+FNF$(C0$(22,P[3]),M0$)
1350 PRINT @(COLUMN,11),X$
1400 REM " --- Level breaks?"
1410 IF C0$(16,6)<>T0$ THEN GOSUB 6000
1420 IF C0$(3,2)<>T1$ THEN GOSUB 6200
1430 IF C0$(5,7)+C0$(12,4)<>T2$ THEN GOSUB 6400
1440 IF C0$(22,P[3])=T3$ THEN GOTO 1500
1450 GOSUB 6600
1460 IF L+1>L9 THEN GOSUB 5000; GOTO 1500
1470 PRINT (7)""
1480 LET L=L+1
1500 REM " --- Print subheadings"
1510 IF H0$<>"" THEN GOSUB 5300
1520 IF H1$<>"" THEN GOSUB 5400
1530 IF H2$<>"" THEN GOSUB 5500
1600 REM " --- Additional file reads"
1610 DIM DESCRIPTION$(W)
1620 LET A0$(1)=N0$+C0$(22,P[4]),A1$(1,35)="(Not On File)"
1630 FIND (GLM01_DEV,KEY=A0$,DOM=1640)IOL=GLM01A
1640 LET DESCRIPTION$(1)=A1$(1,35)
2000 REM " --- Format detail line fields"
2010 LET DEBITS=0,CREDITS=0,UNITS=C[1],DEBIT$="",CREDIT$="",UNIT$=""
2020 IF C[0]>0 THEN LET DEBITS=C[0],DEBIT$=STR(C[0]:M1$)
2030 IF C[0]<0 THEN LET CREDITS=C[0],CREDIT$=STR(ABS(C[0]):M1$)
2040 IF P4$(3,1)<>"Y" THEN GOTO 2100
2050 IF UNITS=0 THEN GOTO 2100
2060 LET UNIT$=STR(UNITS:M2$)
2070 IF LEN(UNIT$)<=LEN(M2$) THEN GOTO 2100
2080 LET UNIT$=UNIT$(2)
2090 GOTO 2070
2100 REM " --- Detail line by audit number"
2110 IF L+2>L9 THEN GOSUB 5000
2120 IF SEQUENCE$="D" THEN GOTO 2200
2130 PRINT (7)@(O[0]),FNB6$(C0$(16,6)),@(O[1]),FNF$(C0$(22,P[3]),M0$),@(O[2]),DESCRIPTION$,@(O[3]),C1$(31,30),@(O[7]),DEBIT$,@(O[8]),CREDIT$,@(O[9]),UNIT$
2190 GOTO 2400
2200 REM " --- Detail line by date
2210 LET M$="",M=NUM(C0$(14,2),ERR=2220),M$=P3$(235+(M-1)*3,3)
2220 PRINT (7)@(O[1]),FNF$(C0$(22,P[3]),M0$),@(O[2]),DESCRIPTION$,@(O[3]),C1$(31,30),@(O[4]),M$,@(O[5]),FNYY21_YY$(C0$(12,2)),@(O[6]),C0$(5,7),@(O[7]),DEBIT$,@(O[8]),CREDIT$,@(O[9]),UNIT$
2400 REM " --- Any reference fields?"
2410 LET REF$="",L=L+1,NUMBER=NUMBER+1
2420 IF FNP$(C1$(1,10))<>"" THEN LET REF$=REF$+"Ref 1 "+C1$(1,10)+" "
2440 IF FNP$(C1$(11,10))<>"" THEN LET REF$=REF$+"Ref 2 "+C1$(11,10)+" "
2450 IF FNP$(C1$(21,10))<>"" THEN LET REF$=REF$+"Ref 3 "+C1$(21,10)
2470 IF REF$="" THEN GOTO 2500
2480 PRINT (7)@(O[3]),REF$
2490 LET L=L+1
2500 REM " --- Accumulate totals"
2510 LET AT[0]=AT[0]+DEBITS,AT[1]=AT[1]+CREDITS,AT[2]=AT[2]+UNITS
2520 LET DT[0]=DT[0]+DEBITS,DT[1]=DT[1]+CREDITS,DT[2]=DT[2]+UNITS
2530 LET JT[0]=JT[0]+DEBITS,JT[1]=JT[1]+CREDITS,JT[2]=JT[2]+UNITS
2540 LET RT[0]=RT[0]+DEBITS,RT[1]=RT[1]+CREDITS,RT[2]=RT[2]+UNITS
2550 LET XT[0]=XT[0]+DEBITS,XT[1]=XT[1]+CREDITS,XT[2]=XT[2]+UNITS
3900 REM " --- Loop back for next GLT-04 record"
3990 GOTO 1000
4000 REM " --- All Done"
4010 LET DONE=1
4020 IF SEQUENCE$="A" THEN GOSUB 6200
4030 IF SEQUENCE$="D" THEN GOSUB 6000
4040 LET TOTAL$="Total For Report",DEBITS=RT[0],CREDITS=RT[1],UNITS=RT[2]
4050 IF L+2>L9 THEN GOSUB 5000
4060 GOSUB 6900
4100 REM " --- In Balance?"
4110 LET BALANCE=RT[0]+RT[1],DEBITS=0,CREDITS=0
4120 IF BALANCE=0 THEN GOTO 4200
4130 IF L+2>L9 THEN GOSUB 5000
4140 IF BALANCE>0 THEN LET DEBITS=BALANCE
4150 IF BALANCE<0 THEN LET CREDITS=BALANCE
4160 LET TOTAL$="This Report Is Out Of Balance By"
4170 GOSUB 6900
4180 LET MESSAGE$[0]=TOTAL$+" "+FNX$(BALANCE,M1$)+" (<Enter>=Continue)"
4190 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
4195 IF V$<>"SPECIAL" THEN GOTO 9900
4800 REM " --- No update if any ranges were selected"
4810 IF NUMBER=0 THEN GOTO 9900
4820 IF BEGACCT$<>"" THEN GOTO 9900
4830 IF ENDACCT$<>"" THEN GOTO 9900
4840 IF JOURNAL$<>"" THEN GOTO 9900
4850 IF WILDCARD$<>"" THEN GOTO 9900
4860 IF BEGVAL$<>"" THEN GOTO 9900
4870 IF ENDVAL$<>"" THEN GOTO 9900
4900 REM " --- Run update overlay"
4910 CLOSE (7,ERR=4990)
4990 RUN "GLU.BA"
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5090 IF SEQUENCE$="D" THEN GOTO 5200
5100 REM " --- By Audit Number"
5110 PRINT (7)""
5120 PRINT (7)@(O[0]+2),"Date",@(O[1]),"Account",@(O[2]),"Description",@(O[3]),"Memo/Reference",@(O[7]+M1-7),"Debits",@(O[8]+M1-8),"Credits",@(UNITHDR),UNITHDR$
5130 LET L=L+2
5190 RETURN 
5200 REM " --- By Date"
5210 PRINT (7)""
5220 PRINT (7)@(O[1]),"Account",@(O[2]),"Description",@(O[3]),"Memo/Reference",@(O[4]),"Per",@(O[5]),"Yr",@(O[6]),"Audit #",@(O[7]+M1-7),"Debits",@(O[8]+M1-8),"Credits",@(UNITHDR),UNITHDR$
5230 LET L=L+2
5290 RETURN 
5300 REM " --- Date Heading"
5310 IF L+7>L9 THEN GOSUB 5000
5320 PRINT (7)""
5330 PRINT (7)H0$
5340 LET L=L+2,H0$=""
5350 GOSUB 5400
5390 RETURN 
5400 REM " --- Journal Heading"
5410 IF L+6>L9 THEN GOSUB 5000
5420 PRINT (7)""
5430 PRINT (7)H1$
5440 PRINT (7)""
5450 LET L=L+3,H1$=""
5490 RETURN 
5500 REM " --- Audit Number Heading"
5510 IF L+4>L9 THEN GOSUB 5000
5520 PRINT (7)H2$
5530 PRINT (7)""
5540 LET L=L+2,H2$=""
5590 RETURN 
5700 REM " --- Build journal subheading"
5710 LET H1$="Journal "+B0$(3,2)+" "+FNP$(B0$(5,20))
5790 RETURN 
6000 REM " --- Date break"
6010 IF SEQUENCE$="A" THEN GOTO 6190
6020 IF T0$="" THEN GOTO 6100
6030 GOSUB 6600
6040 GOSUB 6200
6050 LET TOTAL$="Totals For "+FNB6$(T0$)
6060 LET DEBITS=DT[0],CREDITS=DT[1],UNITS=DT[2]
6070 GOSUB 6900
6080 IF PAGEBREAK$="Y" THEN LET L=L9+1
6100 REM 
6110 IF DONE>0 THEN GOTO 6190
6120 DIM DT[2]
6130 LET T0$=C0$(16,6),H0$="Date "+FNB6$(T0$)
6190 RETURN 
6200 REM " --- Journal break"
6210 IF T1$="" THEN GOTO 6300
6220 IF SEQUENCE$="A" THEN GOSUB 6400
6230 LET TOTAL$="Total For Journal "+B0$(3,2)+" "+FNP$(B0$(5,20))
6240 LET DEBITS=JT[0],CREDITS=JT[1],UNITS=JT[2]
6250 GOSUB 6900
6300 REM 
6310 IF DONE>0 THEN GOTO 6390
6320 DIM JT[2],B[2]
6330 LET B0$(1,4)=N0$+C0$(3,2),B0$(5)="(Not On File)"
6340 FIND (GLM03_DEV,KEY=B0$(1,4),DOM=6350)IOL=GLM03A
6350 LET T1$=C0$(3,2)
6360 GOSUB 5700
6390 RETURN 
6400 REM " --- Audit number break"
6410 IF SEQUENCE$="D" THEN GOTO 6590
6420 IF T2$="" THEN GOTO 6500
6425 GOSUB 6600
6430 LET X$=FNP$(T2$(1,7))
6435 IF X$="" THEN LET X$="NONE"
6440 LET TOTAL$="Total For Audit Number "+X$+" - Period "+T2$(10,2)+" ("+P3$((PERIOD-1)*3+235,3)+" "+FNYY21_YY$(T2$(8,2))+")"
6450 LET DEBITS=AT[0],CREDITS=AT[1],UNITS=AT[2]
6460 GOSUB 6900
6470 IF PAGEBREAK$="N" THEN GOTO 6500
6480 LET L=L9+1
6490 GOSUB 5700
6500 REM 
6510 IF DONE>0 THEN GOTO 6590
6520 DIM AT[2]
6530 LET T2$=C0$(5,7)+C0$(12,4),PERIOD=NUM(C0$(14,2))
6535 LET X$=FNP$(T2$(1,7))
6540 IF X$="" THEN LET X$="NONE"
6550 LET H2$="Audit Number "+X$+" - Period "+T2$(10,2)+" ("+P3$((PERIOD-1)*3+235,3)+" "+FNYY21_YY$(T2$(8,2))+")"
6590 RETURN 
6600 REM " --- Account number break"
6610 IF T3$="" THEN GOTO 6700
6630 LET TOTAL$="Total for Account Number "+FNF$(T3$,M0$)
6640 LET DEBITS=XT[0],CREDITS=XT[1],UNITS=XT[2]
6650 GOSUB 6900
6700 REM 
6710 IF DONE>0 THEN GOTO 6790
6720 DIM XT[2]
6730 LET T3$=C0$(22,P[3])
6790 RETURN 
6800 REM " --- Test account number against specified wildcard mask"
6810 LET STATUS=0
6820 IF WILDCARD$="" THEN GOTO 6890
6825 IF WILDCARD<>LEN(WILDCARD$) THEN LET WILDCARD=LEN(WILDCARD$)
6830 LET X$=FNF$(C0$(22,P[3]),M0$)
6840 FOR X=1 TO WILDCARD
6850 IF WILDCARD$(X,1)="?" THEN GOTO 6880
6860 IF X$(X,1)<>WILDCARD$(X,1) THEN LET STATUS=1
6880 NEXT X
6890 RETURN 
6900 REM " --- Print totals line"
6910 LET DEBIT$=STR(DEBITS:M1$),CREDIT$=STR(ABS(CREDITS):M1$),UNIT$=""
6920 IF P4$(3,1)="Y" THEN LET UNIT$=STR(UNITS:M2$)
6930 IF LEN(UNIT$)>LEN(M2$) THEN LET UNIT$=UNIT$(2); GOTO 6930
6935 IF L+2>L9 THEN GOSUB 5000
6940 PRINT (7)""
6950 PRINT (7)@(O[7]-LEN(TOTAL$)-1),TOTAL$,@(O[7]),DEBIT$,@(O[8]),CREDIT$,@(O[9]),UNIT$
6960 LET L=L+2
6990 RETURN 
8000 REM " --- Functions"
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8060 DEF FNX$(Q,Q1$)=FNP$(FNS$(STR(Q:Q1$)))
8065 DEF FNS$(Q$)=Q$(POS(" "<>Q$))
8090 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return To Menu"
9910 RUN MENU$,ERR=9950
9950 RUN "SYS.AA"
9999 END

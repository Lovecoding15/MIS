0010 REM "APR - Temporary Vendor Report"
0020 REM "Program APR.SA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APM-03",FILES$[2]="APM-10",FILES$[3]="APT-01"
0140 LET FILES$[4]="APT-11",FILES$[5]="SYS-01"
0160 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET APM03_DEV=CHANNELS[1],APM10_DEV=CHANNELS[2],APT01_DEV=CHANNELS[3]
0185 LET APT11_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5]
0200 REM " --- IOLists
0210 APM03A: IOLIST A0$(1),A1$(1)
0220 APM10A: IOLIST H3$
0230 APT01A: IOLIST C0$,C0,C1,C2
0240 APT11A: IOLIST C0$,C1$,C0,C1,C2
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 APS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AP",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=APS01A
0450 DIM P[2],G[4]
0490 LET P[0]=NUM(P2$(1,2))
0500 REM " --- Init Data
0510 LET L9=59,L=L9+1,WHEN$=F0$(7,3),CLOCK$="",HEADINGS=1
0520 DIM J$(80,"-"),W0$(2),Z0$(100),A0$(14),A1$(103),C0$(72),HEADINGS$[HEADINGS]
0530 LET H0=80,H=0,HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0550 IF P3$(3,1)="Y" THEN LET X9$="Retention" ELSE LET X9$=""
0560 CALL "SYC.RM",M1$,M1,11,15
0800 REM " --- Run Report?
0810 LET V4$="Do You Want To Print The "+N3$
0820 CALL "SYC.YN",1,V4$,2,V$,V3
0830 IF V3=4 THEN GOTO 9900
0840 IF V$<>"Y" THEN GOTO 9900
0900 REM 
0910 CALL "SYC.GA",7,1,"","",STATUS
0920 IF STATUS THEN GOTO 9900
0930 CALL "SYC.NB","Printing",12,COLUMN
1000 REM " --- Initial Read
1020 READ (APM03_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Temp Vendor Read
1120 READ (APM03_DEV,END=4000)IOL=APM03A
1200 REM " --- Breaks
1220 IF A0$(1,2)<>N0$ THEN GOTO 4000
1240 IF A0$(3,2)<>T0$ THEN GOSUB 6000
1260 PRINT @(COLUMN,11),A0$(3),
1300 LET T1=0,T2=0,T3=0,D0$="   "
1305 READ (APT01_DEV,KEY=A0$(1,4)+"TEMP  "+A0$(5,10),DOM=1306)
1306 LET C0$=KEY(APT01_DEV,END=1400)
1310 READ (APT01_DEV,KEY=C0$)IOL=APT01A
1315 IF C0$(1,22)<>A0$(1,4)+"TEMP  "+A0$(5,10)+"00" THEN GOTO 1400
1320 LET T1=T1+C0+C3,T2=T2+C1+C4,T3=T3+C2+C5,D0$=C0$(29,3)
1330 LET K11$=KEY(APT11_DEV,END=1400)
1340 READ (APT11_DEV,KEY=K11$)IOL=APT11A
1360 IF C0$(1,20)<>A0$(1,4)+"TEMP  "+A0$(5,10) THEN GOTO 1400
1370 LET T1=T1+C0+C3+C6,T2=T2+C1+C4+C7,T3=T3+C2+C5+C8
1380 GOTO 1330
2000 REM " --- Print
2020 IF L+5>L9 THEN GOSUB 5000
2040 PRINT (7)@(0),A0$(5,10),@(11),A1$(1,30),@(H0-M1*2-9),FNB$(D0$),@(H0-M1*2),T1:M1$,
2045 IF P3$(3,1)="Y" THEN PRINT (7)@(H0-M1),T3:M1$ ELSE PRINT (7)'LF',
2050 PRINT (7)@(11),A1$(31,24),'LF',@(11),A1$(55,24),'LF',@(11),A1$(79,24)
2055 PRINT (7)'LF',
2060 LET L=L+5,T0=T0+1
2090 GOTO 1100
4000 REM " --- Totals
4020 IF L>L9 THEN GOSUB 5000
4030 IF H=0 THEN GOSUB 5000
4050 LET T9$="E"
4060 PRINT (7)'LF',@(10),"Total Temporary Vendors:",T0
4100 GOTO 9900
5000 REM " --- Report Heading
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5050 IF P3$(1,1)="Y" THEN PRINT (7)@(FNC(H3$,H0)),H3$; LET L=L+1
5060 PRINT (7)""
5100 PRINT (7)"Invoice"
5110 PRINT (7)"Number",@(11),"Name/Address",@(H0-M1*2-9),"Inv Date",@(H0-M1-11),"Inv Amount",@(H0-LEN(X9$)-1),X9$,'LF'
5120 LET L=L+4
5190 RETURN 
6000 REM " --- AP Type Break
6020 IF P3$(1,1)<>"Y" THEN GOTO 6100
6040 IF T0$="" THEN GOTO 6100
6050 LET L=L9+1
6100 IF T9$<>"" THEN RETURN 
6120 LET T0$=A0$(3,2)
6140 IF P3$(1,1)<>"Y" THEN RETURN 
6150 LET H3$="***"+T0$+" Not On File***"
6160 FIND (APM10_DEV,KEY=N0$+"A"+T0$,DOM=6190)IOL=APM10A
6170 LET H3$=FNP$(H3$(4,2)+" "+H3$(6,20))
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

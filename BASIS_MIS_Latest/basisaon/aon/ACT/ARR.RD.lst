0010 REM " ARR - COMMENT LISTING 2"
0020 REM "Program ARR.RD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLists
0210 ARM01A: IOLIST A0$,A1$
0220 ARM05A: IOLIST W0$,W1$
0230 ARM10F: IOLIST X0$
0240 ARM10H: IOLIST X0$
0500 REM " --- Initializations
0510 LET HEADINGS=2,CLOCK$=""
0520 DIM HEADINGS$[HEADINGS]
0530 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$
0800 REM " --- Options
0820 IF P7$="S" THEN LET I3$="B",I4$="F",I5$="Salesperson: "
0830 IF P7$="T" THEN LET I3$="C",I4$="H",I5$="Territory: "
0900 REM " --- Determine Start
0920 CALL "SYC.NB","Printing:",M1,COLUMN
1000 REM " --- Initial Read
1020 READ (ARM04_DEV,KEY=N0$+I3$+I1$,DOM=1030)
1100 REM " --- Main Read
1120 LET K$=KEY(ARM04_DEV,END=4000)
1130 IF LEN(K$)<7 THEN GOTO 4000
1140 IF K$(1,3)<>N0$+I3$ THEN GOTO 4000
1145 IF I2$<>"" THEN IF K$(4,3)>I2$ THEN GOTO 4000
1150 READ (ARM04_DEV)
1160 FIND (ARM01_DEV,KEY=N0$+K$(7),DOM=1100)IOL=ARM01A
1180 IF K$(4,3)<>T0$ THEN LET L=L9+1; LET T0$=K$(4,3)
1200 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$)
1400 IF L+4>L9 THEN GOSUB 5000
1500 PRINT (7)'LF','LF',@(0),FNF$(A0$(3,P[0]),M0$),"  ",A1$(1,30),@(60),A1$(112,3)," ",A1$(115,3),"-",A1$(118,4)
1520 LET L=L+3
1600 READ (ARM05_DEV,KEY=A0$(1,8),DOM=1601)
1610 LET K$=KEY(ARM05_DEV,END=1990)
1620 IF K$(1,8)<>A0$(1,8) THEN GOTO 1990
1630 READ (ARM05_DEV)IOL=ARM05A
1640 IF L<L9 THEN GOTO 1700
1650 GOSUB 5000
1660 PRINT (7)'LF','LF',@(0),FNF$(A0$(3,P[0]),M0$),"  ",A1$(1,30),@(60),A1$(112,3)," ",A1$(115,3),"-",A1$(118,4)
1670 LET L=L+3
1700 PRINT (7)@(10),W1$
1710 LET L=L+1
1720 GOTO 1610
1990 GOTO 1100
4000 REM " --- End
4100 GOTO 9900
5000 REM " --- Heading"
5020 LET L=HEADINGS+1
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS>0 THEN EXITTO 9900
5050 GOSUB 5400
5060 PRINT (7)@(0),I5$,X0$,'LF'
5100 PRINT (7)@(0),"Customer Number And Name",'LF'
5120 LET L=L+4
5190 RETURN 
5400 REM " --- Find Desc
5410 LET X0$=T0$+" - Not On File"
5420 IF I4$="F" THEN FIND (ARM10_DEV,KEY=N0$+I4$+T0$,DOM=5480)IOL=ARM10F
5430 IF I4$="H" THEN FIND (ARM10_DEV,KEY=N0$+I4$+T0$,DOM=5480)IOL=ARM10H
5450 LET X0$=X0$(4,3)+" - "+X0$(7,20)
5490 RETURN 
8000 REM " --- Functions
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "GLU - 13 Month Comparison Update"
0020 REM "Program GLU.EB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$(1),A1$(1)
0220 GLM11A: IOLIST X0$,A2$(1)
0230 GLM21A: IOLIST X0$,A2$(1)
0240 GLM02A: IOLIST B0$,A[ALL],U[ALL]
0250 GLE08A: IOLIST D0$(1),D1$(1),PA[ALL],PU[ALL]
0500 REM " --- Initializations"
0510 DIM A0$(2+P[4]),A1$(40),A2$(6),D0$(8),D1$(1)
0540 LET RECORDS$="",CURREC$="",PRIREC$="",LAST$=""
0545 LET YEAR=0,YEAR=FNYY_YEAR(BEGYEAR$,ERR=0550)
0550 IF P4$(6,1)="Y" THEN GOTO 0600
0560 IF YEAR=P[2] THEN LET RECORDS$="40"
0570 IF YEAR=P[2]-1 THEN LET RECORDS$="02"
0580 IF YEAR=P[2]-2 THEN LET RECORDS$="2 "
0590 GOTO 0650
0600 IF YEAR=P[2] THEN LET RECORDS$="02"
0610 IF YEAR=P[2]-1 THEN LET RECORDS$="2 "
0620 IF YEAR=P[2]+1 THEN LET RECORDS$="40"
0650 IF RECORDS$="" THEN GOTO 0700
0660 LET CURREC$=RECORDS$(1,1),PRIREC$=RECORDS$(2,1)
0700 REM " --- Background"
0710 CALL "SYC.NB","Updating",M0+3,COLUMN
0950 REM " --- Position GLM-01"
0980 READ (GLM01_DEV,KEY=N0$,DOM=0990)
0990 IF N0$="99" THEN READ (GLM01_DEV,KEY="",DOM=1000)
1000 REM " --- Read next GLM-01 record"
1010 LET K$=KEY(GLM01_DEV,END=4000)
1020 IF N0$="99" THEN GOTO 1040
1030 IF POS(N0$=K$)<>1 THEN GOTO 4000
1040 READ (GLM01_DEV)IOL=GLM01A
1050 IF FNP$(A1$)="" THEN GOTO 3990
1060 LET FIRM$=""
1070 IF N0$="99" THEN LET FIRM$=A0$(1,2)+" "
1080 PRINT @(COLUMN,11),FIRM$,FNF$(A0$(3,P[3]),M0$),
1100 REM " --- Retrieve prior year record"
1110 DIM B0$(3+P[4]),A[MP],U[MP],TA[MP*3],TU[MP*3]
1120 LET AMOUNT=0,UNITS=0,NUMBER=1,B0$(1)=A0$+PRIREC$
1130 FIND (GLM02_DEV,KEY=B0$,DOM=1200)IOL=GLM02A
1200 REM " --- Accumulate balances and fill prior year buckets"
1210 FOR X=0 TO PERIOD-1
1220 LET AMOUNT=AMOUNT+A[X],UNITS=UNITS+U[X]
1230 NEXT X
1240 FOR X=PERIOD TO P[0]
1250 LET TA[NUMBER]=A[X],TU[NUMBER]=U[X],NUMBER=NUMBER+1
1260 NEXT X
1300 REM " --- Retrieve current year record"
1310 DIM B0$(3+P[4]),A[MP],U[MP]
1320 LET B0$(1)=A0$+CURREC$
1330 FIND (GLM02_DEV,KEY=B0$,DOM=1400)IOL=GLM02A
1400 REM " --- Fill current year buckets"
1410 FOR X=1 TO PERIOD
1420 LET TA[NUMBER]=A[X],TU[NUMBER]=U[X],NUMBER=NUMBER+1
1430 NEXT X
1800 REM " --- Set normal sign based on account type"
1810 LET SIGN=1
1820 IF POS(A1$(36,1)="LCI")<>0 THEN LET SIGN=-1
3000 REM " --- Update GLE-08"
3010 IF N0$="99" THEN READ (GLM21_DEV,KEY=A0$,DOM=3020) ELSE READ (GLM11_DEV,KEY=A0$,DOM=3020)
3020 IF N0$="99" THEN LET K1$=KEY(GLM21_DEV,END=3990) ELSE LET K1$=KEY(GLM11_DEV,END=3990)
3030 IF POS(A0$=K1$)<>1 THEN GOTO 3990
3040 IF N0$="99" THEN READ (GLM21_DEV,KEY=K1$)IOL=GLM21A ELSE READ (GLM11_DEV,KEY=K1$)IOL=GLM11A
3050 LET X$=A2$,GLREPORT$=X$(1,2),LINE$=X$(3,4)
3060 IF GLREPORT$="00" THEN GOTO 3900
3070 IF FNP$(GLREPORT$)="" THEN GOTO 3900
3080 IF LINE$="0000" THEN GOTO 3900
3090 IF FNP$(LINE$)="" THEN GOTO 3900
3100 REM " --- Update GLE-08 record"
3110 GOSUB 6000
3120 IF FNP$(D0$)="" THEN GOTO 3900
3190 WRITE (GLE08_DEV,KEY=D0$)IOL=GLE08A
3900 REM " --- End of report update loop"
3910 GOTO 3020
3990 GOTO 1000
4000 REM " --- Run lead report overlay"
4090 RUN "GLR.FA"
6000 REM " --- Retrieve GLE-08 record"
6010 IF X$=LAST$ THEN GOTO 6100
6020 LET LAST$=X$,D0$(1)=N0$+X$+SEQUENCE$,D1$(1)=""
6050 DIM PA[MP],PU[MP]
6060 FIND (GLM22_DEV,KEY=N0$+X$,DOM=6190)
6070 FIND (GLE08_DEV,KEY=D0$,DOM=6090)IOL=GLE08A
6080 IF D1$(1,1)=SEQUENCE$ THEN GOTO 6100
6090 LET D1$(1)=SEQUENCE$
6095 DIM PA[MP],PU[MP]
6100 LET BEGAMT=AMOUNT*SIGN,BEGUNT=UNITS*SIGN
6110 LET PA[0]=BEGAMT,PU[0]=BEGUNT
6120 FOR X=1 TO MP
6130 LET PA[X]=PA[X]+TA[X]*SIGN,PU[X]=PU[X]+TU[X]*SIGN
6150 NEXT X
6190 RETURN 
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

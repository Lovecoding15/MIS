0010 REM "GLR - 13 Month Comparison (Report Overlay)"
0020 REM "Program GLR.FC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOList's"
0210 GLE08A: IOLIST D0$(1),D1$(1),PA[ALL],PU[ALL]
0220 GLE09A: IOLIST T0$(1),TA[ALL],TU[ALL]
0230 GLM22A: IOLIST C0$(1),C1$(1),C2$(1),C3$
0500 REM " --- Initializations"
0510 LET L9=59,L=L9+1,H=0
0520 DIM C0$(8),C1$(56),C2$(20),D0$(8),D1$(1),T0$(7),PA[MP],PU[MP],TA[MP],TU[MP]
0530 DIM W[MP],X[MP],H3$(H0)
0590 LET LAST$="",M1$="-#######",M1=LEN(M1$)
0600 REM " --- Build 13 period headings"
0610 LET N=PERIOD
0620 FOR X=1 TO MP
0630 LET X$="Per "+STR(N),Y=LEN(X$),P=28+M1*X
0645 LET X$=STR(N)+" "+P3$(235+(N-1)*3,3),Y=LEN(X$)-1,H3$(P-Y)=X$,N=N+1
0650 IF N>P[0] THEN LET N=1
0660 NEXT X
0675 LET HEADINGS$[3]=H3$,HEADINGS=3
0900 REM " --- Position GLM-22 file"
0990 READ (GLM22_DEV,KEY=A0$(1,4),DOM=1000)IOL=GLM22A
1000 REM " --- Read next GLR-01 detail record"
1010 LET K$=KEY(GLM22_DEV,END=4000)
1020 IF K$(1,4)<>A0$(1,4) THEN GOTO 4000
1030 READ (GLM22_DEV)IOL=GLM22A
1040 PRINT @(COLUMN+24,11),C0$(5,4)
1300 IF C1$(1,1)="H" THEN GOTO 3000
1310 DIM PA[MP],PU[MP]
1312 LET D0$(1)=K$,D1$(1)=""
1320 FIND (GLE08_DEV,KEY=D0$,DOM=1340)IOL=GLE08A
1330 IF D1$(1)=SEQUENCE$ THEN GOTO 1400
1340 LET D1$(1)=SEQUENCE$
1360 WRITE (GLE08_DEV,KEY=K$)IOL=GLE08A
1400 REM " --- Create Work Records"
1420 FOR X=0 TO MP
1440 LET W[X]=PA[X],X[X]=PU[X]
1460 NEXT X
2000 REM " --- Check For Total-In"
2020 LET W0$=C1$(45,3)
2040 IF POS(W0$="   000",3) THEN GOTO 2100
2050 LET W0$="T"+C1$(45,3),U0$=PRINTFLAG$
2060 GOSUB 6000
2080 GOSUB 6100
2090 GOSUB 6400
2100 REM " --- Balance Accumulation"
2120 IF POS("B"=C1$(40,5))=0 THEN GOTO 2200
2122 LET Q9=P[0]-W9+2
2124 IF Q9>12 THEN LET Q9=Q9-12
2140 FOR X=1 TO MP
2142 IF Q9=X THEN GOTO 2160
2144 IF Q9=1 THEN IF X=MP THEN GOTO 2160
2150 LET PA[X]=PA[X]+PA[X-1],PU[X]=PU[X]+PU[X-1],W[X]=PA[X],X[X]=PU[X]
2160 NEXT X
2400 REM " --- Total Out"
2410 IF POS(" "<>C2$)=0 THEN GOTO 3000
2420 FOR X=0 TO 4
2430 LET W0$=C2$(X*3+1,3),W1$=C2$(X+16,1),W1=1
2440 IF POS(W0$="   000",3) THEN GOTO 2590
2460 LET W0$="T"+W0$,U0$=PRINTFLAG$
2470 IF W1$="-" THEN LET W1=-1
2480 GOSUB 6000
2485 IF POS("C"=C1$(40,5))<>0 THEN DIM TA[MP],TU[MP]
2490 GOSUB 6200
2500 LET T0$(1)=N0$+W0$+U0$
2510 WRITE (GLE09_DEV,KEY=T0$(1,4))IOL=GLE09A
2590 NEXT X
3000 REM " --- Print"
3010 IF POS("P"=C1$(40,5))<>0 THEN GOSUB 5000
3020 IF L>L9 THEN GOSUB 5000
3100 REM " --- Editing"
3120 IF POS("S"=C1$(10,5))=0 THEN GOTO 3150
3130 PRINT (7)""
3140 LET L=L+1
3150 IF POS("D"=C1$(40,5))=0 AND POS("U"=C1$(40,5))=0 THEN GOTO 3280
3160 LET W1$="-"
3170 IF POS("D"=C1$(40,5))<>0 THEN LET W1$="="
3180 FOR X=1 TO 13
3200 DIM W$(LEN(M1$),W1$)
3240 PRINT (7)@(28+M1*(X-1)),W$,
3250 NEXT X
3260 PRINT (7)""
3270 LET L=L+1
3300 IF POS("-"=C1$(40,5))=0 THEN GOTO 3350
3320 FOR X=0 TO 13
3330 LET W[X]=-W[X]
3340 NEXT X
3350 IF C1$(1,1)="N" THEN GOTO 3900
3352 LET X$=C1$(2,35)
3354 FOR X=35 TO 1 STEP -1
3355 IF X$(X,1)<>" " THEN EXITTO 3358
3356 NEXT X
3358 IF X>0 THEN PRINT (7)@(NUM(C1$(37,3))),X$(1,X),
3360 IF C1$(1,1)="H" THEN GOTO 3500
3400 REM " --- Print Amounts"
3410 FOR X=1 TO 13
3420 IF W[X]=0 THEN GOTO 3490
3450 PRINT (7)@(28+M1*(X-1)),W[X]:M1$,
3490 NEXT X
3500 PRINT (7)""
3520 LET L=L+1
3900 GOTO 1000
4000 REM " --- Run lead overlay"
4010 LET O0=1
4090 RUN "GLR.FA"
5000 REM " --- Print report heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS<>0 THEN GOTO 9900
5090 RETURN 
6000 REM " --- Retrieve totals record"
6010 IF W0$=LAST$ THEN GOTO 6090
6020 DIM TA[MP],TU[MP]
6030 LET T0$(1)=N0$+W0$+U0$
6040 FIND (GLE09_DEV,KEY=N0$+W0$,DOM=6060)IOL=GLE09A
6050 IF T0$(7,1)=U0$ THEN GOTO 6080
6060 DIM TA[MP],TU[MP]
6080 LET LAST$=W0$,T0$(7,1)=U0$
6090 RETURN 
6100 REM " --- From Total"
6110 FOR Y=0 TO MP
6120 LET W[Y]=W[Y]+TA[Y],X[Y]=X[Y]+TU[Y]
6170 IF POS("F"=C1$(40,5))<>0 THEN LET W[Y]=-W[Y],X[Y]=-X[Y]
6180 NEXT Y
6190 RETURN 
6200 REM " --- Accumulate To Total"
6220 FOR Y=0 TO MP
6230 LET TA[Y]=TA[Y]+PA[Y]*W1,TU[Y]=TU[Y]+PU[Y]*W1
6240 NEXT Y
6290 RETURN 
6400 REM " --- Get Total"
6420 FOR Y=0 TO MP
6460 LET PA[Y]=PA[Y]+TA[Y],PU[Y]=PU[Y]+TU[Y]
6470 NEXT Y
6490 RETURN 
8000 REM " --- Functions"
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

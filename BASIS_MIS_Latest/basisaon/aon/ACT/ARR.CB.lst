0010 REM "ARR - Customer Statements (Moore Form #2090A.O.)"
0020 REM "Program ARR.CB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLists
0210 ARM01A: IOLIST B0$,B1$
0220 ARM02A: IOLIST C0$,C1$,C[ALL]
0230 ARM10A: IOLIST X1$
0240 ART01A1: IOLIST A0$,A[ALL]
0250 ART01A2: IOLIST AX0$,AX[ALL]
0260 ART11A: IOLIST W0$,W1$,W[ALL]
0500 REM " --- Initializations
0510 LET H0=0,L=0,L9=24,HEADINGS=0
0520 DIM A[1],C[10],W[1],HEADINGS$[HEADINGS]
0530 LET SPACING=2; REM "SPACING=1 for single-line spacing, SPACING=2 for double-line spacing
0540 LET AGE=NUM(AGE$,ERR=0550)
0600 REM " --- Determine 30-Day Aging Period Dates"
0610 LET APD$=""
0620 FOR X=-5 TO 0
0630 LET X$=""
0640 CALL "SYC.CA",STMT$,X$,X*30
0650 IF X$="N" THEN EXITTO 9900
0660 LET APD$=X$+APD$
0670 NEXT X
0700 REM " --- Test Pattern?
0720 LET V4$="Do You Wish To Print A Test Pattern"
0740 CALL "SYC.YN",0,V4$,4,V$,V3
0760 ON V3 GOTO 0780,0700,0700,0700,9900
0780 ON POS(V$="YN") GOTO 0700,0800,0900
0800 REM " --- Print Test Pattern
0810 DIM B0$(6,"9"),B1$(245,"X"),C1$(40,"X"),A0$(50,"X"),X$(20,"X"),X1$(20,"X"),T1$(P[0],"X")
0820 LET A[0]=9999.99,A0$(24,3)=$646464$,AMNT=A[0],B1$(103,9)="999999999"
0830 GOSUB 6500
0840 GOSUB 5000
0850 LET T1$=""
0860 FOR X=1 TO 5
0870 GOSUB 5200
0880 NEXT X
0890 GOSUB 5400
0895 GOTO 0700
0900 REM " --- Background
0920 IF SEQ$="N" THEN LET X=M0
0940 IF SEQ$="A" THEN LET X=10
0960 CALL "SYC.NB","Printing",X,COLUMN
1000 REM " --- Trip Read
1020 IF SEQ$="N" THEN READ (ART01_DEV,KEY=N0$+"  "+CUST$,DOM=1030)
1030 IF SEQ$="A" THEN READ (ARM04_DEV,KEY=N0$+"A"+CUST$,DOM=1040)
1100 REM " --- Alphabetic Sequence
1105 IF SEQ$<>"A" THEN GOTO 1130
1110 LET R9$=KEY(ARM04_DEV,END=4000)
1115 READ (ARM04_DEV)
1120 IF R9$(1,3)<>N0$+"A" THEN GOTO 4000
1125 READ (ART01_DEV,KEY=N0$+"  "+R9$(14,6),DOM=1130)
1130 REM " --- Next ART-01 AR Open Invoice Header
1135 IF SEQ$<>"A" THEN GOTO 1155
1140 LET K$=KEY(ART01_DEV,END=1100)
1145 IF K$(1,10)<>N0$+"  "+R9$(14,6) THEN GOTO 1100
1150 GOTO 1170
1155 LET K$=KEY(ART01_DEV,END=4000)
1160 IF K$(1,2)<>N0$ THEN GOTO 4000
1165 IF OPT$="S" AND K$(5,6)<>CUST$ THEN GOTO 4000
1170 REM " --- Read ART-01 AR Open Invoice Header
1180 READ (ART01_DEV)IOL=ART01A1
1185 IF SEQ$="N" THEN LET X$=FNF$(A0$(5,P[0]),M0$)
1190 IF SEQ$="A" THEN LET X$=R9$(4,10)
1195 PRINT @(COLUMN,11),X$,
1200 REM " --- Breaks
1220 IF K$(5,6)=T1$ THEN GOTO 2000
1240 GOSUB 6000
1260 IF T8$<>"N" THEN GOTO 2000
1280 READ (ART01_DEV,KEY=K$(1,10)+$FF$,DOM=1100)
1300 GOTO 1100
2000 REM " --- Determine Inv Amt
2010 LET AMNT=0
2020 IF A0$(24,3)>STMT$ THEN GOTO 1130
2030 READ (ART11_DEV,KEY=A0$(1,17),DOM=2040)
2100 REM " --- Read ART-11 AR Open Invoice Detail
2120 LET K11$=KEY(ART11_DEV,END=2200)
2140 IF K11$(1,17)<>A0$(1,17) THEN GOTO 2200
2150 READ (ART11_DEV)IOL=ART11A
2170 IF W1$(2,3)<=STMT$ THEN LET AMNT=AMNT+W[0]+W[1]
2190 GOTO 2100
2200 REM " --- Zero
2220 IF A[0]+AMNT=0 THEN GOTO 1130
2500 REM " --- Print
2520 GOSUB 5200
2900 GOTO 1130
4000 REM " --- Totals
4020 LET T9$="E"
4040 GOSUB 6000
4100 GOTO 9900
5000 REM " --- Heading
5010 LET L=0,P=P+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5025 IF STATUS>0 THEN EXITTO 9900
5030 PRINT (7)@(H0+51),P:"##",@(H0+76),P:"##"
5040 PRINT (7)'LF','LF','LF','LF','LF','LF'
5060 PRINT (7)@(H0+9),ADDRESS$(1,30),'LF',@(H0+9),ADDRESS$(31,30),@(H0+49),T1$(1,P[0]),@(H0+59),ADDRESS$(1,21),'LF',@(H0+9),ADDRESS$(61,30),'LF',@(H0+9),ADDRESS$(91,30),'LF',@(H0+9),ADDRESS$(121,30),@(H0+48),FNB$(STMT$),@(H0+59),FNB$(STMT$),@(H0+74),T1$(1,P[0]),'LF',@(H0+9),ADDRESS$(151,30),'LF',@(H0+9),ADDRESS$(181,30)
5080 PRINT (7)'LF','LF'
5190 RETURN 
5200 REM " --- Detail
5210 IF L>=L9-1 THEN GOSUB 5600
5220 LET X$=A0$(33,1)
5230 IF X$="S" THEN LET X$="I"
5240 IF X$="F" THEN LET X$="FC"
5250 PRINT (7)@(H0+1),FNB$(A0$(24,3)),@(H0+11),X$,@(H0+14),A0$(11,7),@(H0+23),A[0]:M1$,@(H0+35),AMNT:M1$,@(H0+46),A[0]+AMNT:M1$,@(H0+58),A0$(11,7),@(H0+66),X$,@(80-M1),A[0]+AMNT:M1$
5260 LET L=L+1
5280 IF SPACING=2 THEN PRINT (7)'LF',; LET L=L+1
5390 RETURN 
5400 REM " --- Total
5410 WHILE L<L9
5420 PRINT (7)'LF',
5430 LET L=L+1
5440 WEND
5450 PRINT (7)@(H0),MSG$,'LF'
5460 PRINT (7)@(H0+46),C[0]+C[1]+C[2]+C[3]+C[4]+C[5]:M1$,@(80-M1),C[0]+C[1]+C[2]+C[3]+C[4]+C[5]:M1$,'LF'
5470 PRINT (7)@(H0),C[0]+C[1]:M1$,C[2]:M1$,C[3]:M1$,C[4]+C[5]:M1$
5480 PRINT (7)@(H0+M1-8),"Current",@(H0+2*M1-8),"30-Days",@(H0+3*M1-8),"60-Days",@(H0+4*M1-8),"90-Days",'LF','LF','LF'
5490 RETURN 
5600 REM " --- Continue
5610 WHILE L<L9+2
5620 PRINT (7)'LF',
5630 LET L=L+1
5640 WEND
5650 PRINT (7)@(H0+45),"(Continued)",@(H0+69),"(Continued)"
5660 PRINT (7)'LF','LF','LF','LF','LF'
5680 GOSUB 5000
5690 RETURN 
6000 REM " --- Customer Break
6020 IF T1$="" THEN GOTO 6100
6040 GOSUB 5400
6100 IF T9$<>"" THEN RETURN 
6110 LET T1$=A0$(5,6),T8$="",P=0,F9=0
6120 DIM B1$(250),C[10],C1$(40)
6125 LET B1$(1)="Not On File"
6130 FIND (ARM01_DEV,KEY=N0$+A0$(5,6),DOM=6140)IOL=ARM01A
6140 FIND (ARM02_DEV,KEY=N0$+A0$(5,6)+"  ",DOM=6145)IOL=ARM02A
6150 IF C1$(20,1)<>"Y" THEN GOTO 6280
6155 IF TERR$<>"" AND TERR$<>C1$(21,3) THEN GOTO 6280
6160 GOSUB 6500
6170 GOTO 6200
6180 LET X1$=""
6185 FIND (ARM10_DEV,KEY=N0$+"A"+C1$(4,2),DOM=6195)IOL=ARM10A
6190 LET X1$=X1$(6,20)
6195 GOSUB 5000
6199 RETURN 
6200 REM " --- Determine Aging
6210 GOSUB 6600
6220 LET A_BAL=0
6230 FOR X=1+AGE TO 5
6240 LET A_BAL=A_BAL+C[X]
6250 NEXT X
6260 IF A_BAL>=M_BAL THEN GOTO 6180
6280 LET T8$="N",T1$=""
6290 RETURN 
6500 REM " --- Format address block
6510 LET W=30,LINES=6,WIDTH=W,ZIP=9
6520 DIM ADDRESS$(W*LINES)
6530 LET ADDRESS$(1)=B1$(1,30),ADDRESS$(31)=B1$(31,24)
6540 LET ADDRESS$(61)=B1$(55,24),ADDRESS$(91)=B1$(79,24)
6550 LET ADDRESS$(121)=B1$(179,24),ADDRESS$(151)=B1$(203,24)
6555 LET ADDRESS$=ADDRESS$+B1$(103,ZIP)
6560 CALL "SYC.AA",ADDRESS$,W,LINES,ZIP,WIDTH
6590 RETURN 
6600 REM " --- Pre-Age For Selection
6605 DIM AX[1],C[10]
6610 READ (ART01_DEV,KEY=A0$(1,10),DOM=6620)
6620 REM " --- Invoice Records
6630 LET KX$=KEY(ART01_DEV,END=6880)
6640 IF POS(A0$(1,10)=KX$)<>1 THEN GOTO 6880
6660 READ (ART01_DEV)IOL=ART01A2
6670 IF AX0$(24,3)>STMT$ THEN GOTO 6620
6680 LET BAL=AX[0]
6690 READ (ART11_DEV,KEY=AX0$(1,17),DOM=6700)
6700 REM " --- Payment/Adjustment Records
6710 LET KX11$=KEY(ART11_DEV,END=6800)
6720 IF POS(AX0$(1,17)=KX11$)<>1 THEN GOTO 6800
6730 READ (ART11_DEV)IOL=ART11A
6750 IF W1$(2,3)<=STMT$ THEN LET BAL=BAL+W[0]+W[1]
6790 GOTO 6700
6800 REM " --- Age
6810 IF DATE$="I" THEN LET X3$=AX0$(24,3) ELSE LET X3$=AX0$(27,3)
6820 LET X0=POS(X3$>APD$,3)
6830 IF X0=0 THEN LET X0=5 ELSE LET X0=INT(X0/3)
6840 LET C[X0]=C[X0]+BAL
6850 GOTO 6620
6880 REM " --- Reset ART-01
6885 READ (ART01_DEV,KEY=K$)IOL=ART01A1
6890 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

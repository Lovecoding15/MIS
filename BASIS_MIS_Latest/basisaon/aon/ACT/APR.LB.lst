0010 REM "APR - Vendor Ledger Cards"
0020 REM "Program APR.LB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLISTs"
0210 APM01A: IOLIST A0$,A1$
0220 APM02A: IOLIST B0$,B1$,B2$,B[ALL]
0240 APT01A: IOLIST X0$,X[0],X[1],X[2]
0340 APT11A: IOLIST X0$,X1$,X[0],X[1],X[2]
0500 REM " --- Init Data"
0505 LET HEADINGS=3
0510 LET H0=80,H=0,H1$=N4$+" Listing",WHEN$=F0$(7,3),CLOCK$=""
0520 LET L9=59,L=L9+1,T0=0,T1=0
0530 LET M1=LEN(M1$)
0540 DIM X[2],W[2],B[13],HEADINGS$[HEADINGS]
0550 LET H2$="Vendor: "
0560 IF P5$="" THEN LET H2$=H2$+"First" ELSE LET H2$=H2$+FNF$(P5$,M0$)
0570 LET H2$=H2$+" Through "
0580 IF P6$="" THEN LET H2$=H2$+"Last" ELSE LET H2$=H2$+FNF$(P6$,M0$)
0590 LET H2$=H2$+", Dates Through "+FNB$(P7$)
0610 CALL "SYC.GA",7,1,"","",STATUS
0620 IF STATUS THEN GOTO 9900
0630 LET HEADINGS$[0]=N1$,HEADINGS$[1]=H1$,HEADINGS$[2]=H2$
0700 REM " --- Background"
0720 CALL "SYC.NB","Now Printing...",M0,COLUMN
1000 REM " --- Init Read"
1010 READ (APM01_DEV,KEY=N0$+P5$,DOM=1011)IOL=APM01A; GOTO 1111
1100 REM " --- Main Read"
1110 READ (APM01_DEV,END=4000)IOL=APM01A
1120 IF A0$(1,2)>N0$ THEN GOTO 4000
1130 IF P6$<>"" THEN IF A0$(3,P[0])>P6$ THEN GOTO 4000
1150 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$),
1190 LET T0=0,T1=0
1200 REM " --- Get Vendor Detail"
1220 READ (APM02_DEV,KEY=A0$,DOM=1221)
1240 LET K$=KEY(APM02_DEV,END=1100)
1250 IF K$(1,8)<>A0$ THEN GOTO 1100
1260 READ (APM02_DEV)IOL=APM02A
2000 REM " --- Print Invoice Detail"
2020 LET W0$=B0$(9,2)
2030 READ (APT01_DEV,KEY=N0$+W0$+A0$(3,6),DOM=2040)
2100 REM " --- Main Invoice Read"
2120 LET K$=KEY(APT01_DEV,END=2900)
2140 IF K$(1,10)<>N0$+W0$+A0$(3,6) THEN GOTO 2900
2160 READ (APT01_DEV)IOL=APT01A
2165 IF X0$(32,3)>P7$ THEN READ (APT01_DEV,KEY=X0$(1,20)+$FF$,DOM=2100)
2167 IF T9$<>A0$(1,8) THEN GOSUB 5500
2170 IF L+1>L9 THEN GOSUB 5500
2200 IF P3$(3,1)="Y" THEN PRINT (7)@(44+2*M1),X[2]:M1$,
2210 PRINT (7)@(6),X0$(11,10),"    ",FNB$(X0$(29,3))," ",FNB$(X0$(35,3))," ",X0$(25,2)," ",X0$(67,1),@(44),X[0]:M1$,X[1]:M1$
2220 LET L=L+1
2230 LET W0=X[0],W1=X[1],W2=X[2],Q9=0
2290 READ (APT11_DEV,KEY=K$(1,20),DOM=2300)
2300 LET K$=KEY(APT11_DEV,END=2400)
2310 IF K$(1,20)<>X0$(1,20) THEN GOTO 2400
2320 READ (APT11_DEV)IOL=APT11A
2335 IF X1$(1,1)=" " THEN GOTO 2370
2340 IF X1$(2,3)>P7$ THEN GOTO 2360
2341 IF L+1>L9 THEN GOSUB 5500
2342 IF P3$(3,1)="Y" THEN PRINT (7)@(44+2*M1),X[2]:M1$,
2344 PRINT (7)@(10),X1$(1,1)," ",X1$(5,7)," ",FNB$(X1$(2,3)),@(44),X[0]:M1$,X[1]:M1$
2345 LET L=L+1
2350 LET W0=W0+X[0],W1=W1+X[1],W2=W2+X[2]
2370 GOTO 2300
2400 REM " --- Print Total"
2405 IF Q9=0 THEN GOTO 2480
2407 IF L+1>L9 THEN GOSUB 5000
2410 IF P3$(3,1)="Y" THEN PRINT (7)@(44+2*M1),W2:M1$,
2420 PRINT (7)@(21),"Invoice Total:",@(44),W0:M1$,W1:M1$
2440 LET L=L+1
2480 LET T0=T0+W0,T1=T1+W1,T2=T2+W0,T3=T3+W1
2490 GOTO 2100
2900 REM " --- End Display"
2905 IF T9$<>A0$(1,8) THEN GOTO 2990
2907 IF L+1>L9 THEN GOSUB 5000
2910 PRINT (7)@(22),"Vendor Total:",@(44),T0:M1$
2920 LET L=L+1
2990 GOTO 1240
4000 REM " --- End"
4005 IF L+1>L9 THEN GOSUB 5000
4010 PRINT (7)@(22),"Report Total:",@(44),T2:M1$
4100 RUN "SYS.AA"
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS<>0 THEN EXITTO 9900
5420 IF P3$(3,1)="Y" THEN PRINT (7)@(33+3*M1),"Retention",
5440 PRINT (7)@(0),"Vendor Invoice",@(22),"Date",@(31),"Due",@(38),"Pg H",@(44+M1-7),"Amount",@(44+2*M1-9),"Discount"
5450 LET L=L+1
5490 RETURN 
5500 REM " --- Print Vendor"
5510 IF L+2>L9 THEN GOSUB 5000
5520 PRINT (7)'LF',A0$(3,P[0])," ",A1$(1,30)
5530 LET L=L+2,T9$=A0$(1,8)
5540 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

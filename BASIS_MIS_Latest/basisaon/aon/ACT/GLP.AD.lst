0010 REM "GLP - Account/Budget Maintenance Default Columns"
0020 REM "Program GLP.AD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0210 GLS01A: IOLIST A0$,A1$,A2$(1),A3$(1),A4$,A5$,A6$,A7$,A8$
0500 REM " --- Initializations"
0510 DIM D0$(4)
0590 LET A0$=N0$+N2$+"00",I0=0
0700 REM " --- Background"
0720 PRINT @(0,3),'CE','SB',@(31,4),"Account Maintenance",@(6,6),"Column  ID  Type  Description",@(56),"Column Title",'SF',
0730 IF A4$(2,1)<>"Y" THEN GOTO 0760
0740 CALL "SYC.WC",1,0,80,0,0,12,0
0750 PRINT 'SB',@(31,14),"Budget Maintenance",@(6,16),"Column  ID  Type  Description",@(56),"Column Title",'SF'
0760 FOR X=1 TO 4
0770 PRINT 'SB',@(6,6+X),X:"0",'SF',
0780 IF A4$(2,1)="Y" THEN PRINT 'SB',@(6,16+X),X:"0",'SF',
0790 NEXT X
1200 REM " --- Retrieve Record"
1210 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
1220 IF FNP$(A4$(8,4))="" THEN GOTO 1500
1300 REM " --- Display"
1310 GOSUB 5000
1390 GOTO 4000
1500 REM " --- Initializations for new record. Use historical precedents."
1510 LET A4$(8,4)="0123",A4$(12,4)="AAAA",A4$(16,4)="23",A4$(20,4)="AA"
1520 IF A4$(2,1)="Y" THEN GOTO 1550
1530 LET A4$(8,4)="0101",A4$(12,4)="AUAU",A4$(16,4)="",A4$(20,4)=""
1540 IF A4$(3,1)="N" THEN LET A4$(8,4)="01",A4$(12,4)="AA"
1590 GOSUB 5000
2000 REM " --- Record ID"
2010 LET X=INT(I0/8),N=I0/2-X*4,P=X*8+8+N,V0=1,V1=14,V2=X*10+7+N
2020 IF A4$(2,1)<>"Y" THEN IF I0>7 THEN GOTO 2060
2030 LET V0$="S",V1$="C",V2$=A4$(P,1),V3$=""
2035 LET V4$="Enter the ID of the record to be displayed (<F1>=None/<F3>=Lookup)"
2040 GOSUB 7000
2050 ON V3 GOTO 2150,2060,3900,2100,3900
2060 LET A4$(P,1)=""
2090 GOTO 3900
2100 REM 
2110 CALL "GLC.LA",ACTION$,SELECTION$,FLAG$,DESCRIPTION$,TITLE$
2120 IF SELECTION$="" THEN GOTO 2000
2130 GOSUB 5200
2135 IF STATUS<>0 THEN GOTO 2000
2140 LET A4$(P,1)=SELECTION$,A4$(P+4,1)=FLAG$
2145 GOTO 3900
2150 IF V$="" THEN GOTO 2060
2160 LET A4$(P,1)=V$
2170 PRINT @(V1,V2),A4$(P,1),
2190 GOTO 3900
2200 REM " --- Type"
2210 LET X=INT(I0/8),N=INT(I0/2)-X*4,P=X*8+12+N,V0=1,V1=18,V2=X*10+7+N
2215 IF A4$(2,1)<>"Y" THEN IF I0>7 THEN GOTO 2400
2220 LET V0$="S",V1$="C",V2$=A4$(P,1),V3$="AU"
2225 LET SELECTION$=A4$(P-4,1),V$=""
2230 IF FNP$(SELECTION$)="" THEN GOTO 2400
2235 LET V4$="Select information to be displayed (A=Amounts/U=Units)"
2240 GOSUB 7000
2250 ON V3 GOTO 2260,2200,3900,2200,3900
2260 IF POS(V$="AU")=0 THEN GOTO 2200
2270 LET FLAG$=V$,A4$(P,1)=V$
2280 GOSUB 5200
2285 IF STATUS<>0 THEN GOTO 2200
2290 GOTO 3900
2400 REM 
2410 LET A4$(P,1)="",FLAG$=""
2420 GOSUB 5200
2490 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,2000,2200,3990
3990 LET I0=0
3995 GOSUB 5000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?",I0=0
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4400
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Verify that all entries are valid"
4110 FOR X=1 TO 4
4120 LET SELECTION$=A4$(7+X,1),FLAG$=A4$(11+X,1),P=0
4125 IF FNP$(SELECTION$)="" THEN GOTO 4150
4130 IF A4$(2,1)<>"Y" THEN IF POS(SELECTION$="024")=0 THEN EXITTO 4200
4135 IF A4$(3,1)<>"Y" THEN IF FLAG$="U" THEN EXITTO 4200
4140 GOSUB 5500
4145 IF STATUS<>0 THEN EXITTO 4200
4150 IF A4$(2,1)<>"Y" THEN GOTO 4180
4155 LET SELECTION$=A4$(15+X,1),FLAG$=A4$(19+X,1),P=1
4157 IF FNP$(SELECTION$)="" THEN GOTO 4180
4160 IF A4$(2,1)<>"Y" THEN IF POS(SELECTION$="024")=0 THEN EXITTO 4200
4165 IF A4$(3,1)<>"Y" THEN IF FLAG$="U" THEN EXITTO 4200
4170 GOSUB 5500
4175 IF STATUS<>0 THEN EXITTO 4200
4180 NEXT X
4190 GOTO 4300
4200 REM " --- Screen contains invalid entry"
4205 DIM MESSAGE$[1]
4210 LET V4$="Account",I0=(X-1)*2-1
4220 IF P=1 THEN LET V4$="Budget",I0=(X+3)*2-1
4230 LET MESSAGE$[0]=V4$+" Column "+STR(X)+" contains an invalid entry (<Enter>=Continue):"
4250 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
4260 LET V3=0
4290 GOTO 3900
4300 REM " --- Write"
4320 WRITE (SYS01_DEV,KEY=A0$)IOL=GLS01A
4400 REM " --- Return to menu"
4410 LET O0=1
4490 RUN "GLP.AA"
5000 REM " --- Display"
5010 PRINT 'CF',
5020 FOR X=1 TO 4
5030 LET SELECTION$=A4$(7+X,1),FLAG$=A4$(11+X,1),V2=X+6
5040 GOSUB 5200
5045 IF A4$(2,1)<>"Y" THEN GOTO 5080
5050 LET SELECTION$=A4$(15+X,1),FLAG$=A4$(19+X,1),V2=X+16
5060 GOSUB 5200
5080 NEXT X
5090 RETURN 
5200 REM " --- Print description/title"
5210 GOSUB 5500
5220 PRINT @(14,V2),'CL',SELECTION$,@(18),FLAG$,@(24),DESCRIPTION$,@(56),TITLE$,
5290 RETURN 
5500 REM " --- Retrieve description and column heading"
5510 LET STATUS=0,DESCRIPTION$="",TITLE$=""
5520 IF FNP$(SELECTION$)="" AND FNP$(FLAG$)="" THEN GOTO 5590
5550 CALL "GLC.FA",GLM08_DEV,N0$,SELECTION$,FLAG$,DESCRIPTION$,TITLE$,STATUS
5590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

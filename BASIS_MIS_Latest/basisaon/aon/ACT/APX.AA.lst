0010 REM "APX - Rebuild AP Cross-References"
0020 REM "Program APX.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="APM-01",FILES$[3]="APM-04"
0140 LET FILES$[4]="APT-01",FILES$[5]="APT-03"
0160 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1],APM01_DEV=CHANNELS[2],APM04_DEV=CHANNELS[3]
0190 LET APT01_DEV=CHANNELS[4],APT03_DEV=CHANNELS[5]
0200 REM " --- IOLists"
0210 APM01A: IOLIST A0$(1),A1$(1)
0220 APM04A: IOLIST C0$
0230 APM04B: IOLIST D0$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 APT01A: IOLIST W0$(1)
0320 APT03A: IOLIST XA$
0330 APT03B: IOLIST XB$
0340 APT03C: IOLIST XC$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N2$="AP"
0500 REM " --- Initializations"
0510 DIM A0$(8),A1$(195),W0$(71)
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 PRINT 'SB',@(33,3),"Phase:",'SF',
0800 REM " --- Options
0810 LET V4$="Do You Wish To "+F5$
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM " --- Clear Files"
0910 CALL "SYC.FA",APM04_DEV
0920 CALL "SYC.FA",APT03_DEV
0930 PRINT @(40,3),"Vendor Cross-References",
0940 CALL "SYC.NB","Updating",23,COLUMN
0990 READ (APM01_DEV,KEY="",DOM=1000)
1000 REM " --- Vendor X-Refs"
1010 READ (APM01_DEV,END=1900)IOL=APM01A
1020 PRINT @(COLUMN,11),A0$(1,2)," ",A0$(3),
1030 LET C0$=A0$(1,2)+"A"+A1$(146,10)+A0$(3,6)
1040 LET D0$=A0$(1,2)+"B"+A1$(103,9)+A0$(3,6)
1050 WRITE (APM04_DEV,KEY=C0$)IOL=APM04A
1060 WRITE (APM04_DEV,KEY=D0$)IOL=APM04B
1090 GOTO 1000
1900 REM " --- Position invoice header file"
1910 PRINT @(40,3),"Invoice Cross-References",@(COLUMN,11),FILL(23),
1990 READ (APT01_DEV,KEY="",DOM=2000)
2000 REM " --- Read next invoice"
2010 READ (APT01_DEV,END=4000)IOL=APT01A
2020 PRINT @(COLUMN,11),W0$(1,2)," ",W0$(3,2)," ",W0$(5,6)," ",W0$(11,10),
2100 REM " --- Write APT-03 xrefs"
2110 LET XA$=W0$(1,2)+"A"+W0$(3,2)+W0$(5,6)+W0$(29,3)+W0$(11,10)
2120 LET XB$=W0$(1,2)+"B"+W0$(3,2)+W0$(5,6)+W0$(41,10)+W0$(11,10)
2130 LET XC$=W0$(1,2)+"C"+W0$(11,10)+W0$(3,2)+W0$(5,6)
2140 WRITE (APT03_DEV,KEY=XA$)IOL=APT03A
2150 WRITE (APT03_DEV,KEY=XB$)IOL=APT03B
2160 WRITE (APT03_DEV,KEY=XC$)IOL=APT03C
2190 GOTO 2000
4000 REM " --- End"
4100 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

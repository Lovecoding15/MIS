0010 REM "ARU - Age A/R (No Report)"
0020 REM "Program ARU.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ARM-02",FILES$[3]="ARM-04"
0130 LET FILES$[4]="ARM-10",FILES$[5]="ART-01",FILES$[6]="ART-11"
0135 LET FILES$[7]="SYS-01"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET ARM01_DEV=CHANNELS[1],ARM02_DEV=CHANNELS[2],ARM04_DEV=CHANNELS[3]
0185 LET ARM10_DEV=CHANNELS[4],ART01_DEV=CHANNELS[5],ART11_DEV=CHANNELS[6]
0190 LET SYS01_DEV=CHANNELS[7]
0200 REM " --- IOLists
0220 ARM01A: IOLIST B0$,B1$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[1],G[4]
0460 FOR X=0 TO 4
0470 LET G[X]=NUM(G2$(X*2+1,2),ERR=0480)
0480 NEXT X
0485 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2))
0500 REM " --- Initializations"
0520 LET M0=LEN(M0$)
0570 LET AGEDT$=F0$(7,3),DATE$="I",START$="",END$=""
0700 REM " --- Background"
0710 FOR X=4 TO 10 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(28,3),"Aging Date:",@(7,5),"Age On Due Date Or Invoice Date:",@(20,7),"Beginning Customer:",@(23,9),"Ending Customer:",'SF',
0790 GOSUB 5000
1000 REM " --- Aging Date"
1020 LET V0$="D",V1$="ER",V2$=AGEDT$,V3$="",V4$="",V0=8,V1=40,V2=3,I0=0
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1000,3900
1060 LET AGEDT$=V$
1090 GOTO 3900
1300 REM " --- Age On Due Or Invoice Date?"
1320 LET V0$="S",V1$="C",V2$=DATE$,V3$="DI",V4$="D=Due Date  I=Invoice Date",V0=1,V1=40,V2=5
1330 GOSUB 7000
1340 ON V3 GOTO 1350,1300,3900,1300,3900
1360 GOSUB 5100
1380 LET DATE$=V$
1390 GOTO 3900
1700 REM " --- Beginning Customer"
1720 LET V0$="M",V1$="KC",V2$=START$,V3$=P1$,V4$="Enter Beginning Customer Number, <F1>=First, <F3>=Lookup",V0=6,V1=40,V2=7
1730 GOSUB 7000
1740 ON V3 GOTO 1770,1750,3900,1760,3900
1750 LET V$=""; GOTO 1770
1760 CALL "SYC.LC",V$
1770 GOSUB 5200
1780 IF STATUS=0 THEN LET START$=V$ ELSE GOTO 1700
1790 GOTO 3900
1800 REM " --- Ending Customer"
1820 LET V0$="M",V1$="KC",V2$=END$,V3$=P1$,V4$="Enter Ending Customer Number, <F1>=Last, <F3>=Lookup",V0=6,V1=40,V2=9
1830 GOSUB 7000
1840 ON V3 GOTO 1870,1850,3900,1860,3900
1850 LET V$=""; GOTO 1870
1860 CALL "SYC.LC",V$
1870 GOSUB 5300
1880 IF STATUS=0 THEN LET END$=V$ ELSE GOTO 1800
1890 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1300,1700,1800,4000
4000 REM " --- Correct?
4005 GOSUB 5000
4010 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run Overlay"
4110 RUN "ARU.EB"
5000 REM " --- Display"
5010 PRINT @(40,3),FNB$(AGEDT$),
5020 LET V0=1,V1=40,V2=5,V$=DATE$; GOSUB 5100
5030 LET V0=6,V1=40,V2=7,V$=START$; GOSUB 5200
5040 LET V0=6,V1=40,V2=9,V$=END$; GOSUB 5300
5090 RETURN 
5100 REM " --- Age On Due Or Invoice Date Display"
5120 IF V$="D" THEN LET X$="Due Date" ELSE LET V$="I",X$="Invoice Date"
5180 PRINT @(V1,V2),'CL',V$,@(V1+V0+2,V2),X$,
5190 RETURN 
5200 REM " --- Beginning Customer Display"
5210 LET STATUS=1
5220 DIM X$(M0),X1$(80-V1-M0-2)
5230 IF V$="" THEN LET X1$(1)="First"; GOTO 5270
5240 IF END$<>"" THEN IF V$>END$ THEN GOTO 5290
5250 FIND (ARM01_DEV,KEY=N0$+V$,DOM=5290)IOL=ARM01A
5260 LET X$=FNF$(B0$(3,P[0]),M0$),X1$(1)=B1$(1,30)
5270 LET STATUS=0
5280 PRINT @(V1,V2),'CL',X$,@(V1+M0+2),X1$,
5290 RETURN 
5300 REM " --- Ending Customer Display"
5310 LET STATUS=1
5320 DIM X$(M0),X1$(80-V1-M0-2)
5330 IF V$="" THEN LET X1$(1)="Last"; GOTO 5370
5340 IF START$<>"" THEN IF V$<START$ THEN GOTO 5390
5350 FIND (ARM01_DEV,KEY=N0$+V$,DOM=5390)IOL=ARM01A
5360 LET X$=FNF$(B0$(3,P[0]),M0$),X1$(1)=B1$(1,30)
5370 LET STATUS=0
5380 PRINT @(V1,V2),'CL',X$,@(V1+M0+2),X1$,
5390 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

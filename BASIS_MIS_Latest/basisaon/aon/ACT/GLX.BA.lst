0010 REM "GLX - Copy G/L Account Numbers"
0020 REM "Program GLX.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-01",FILES$[2]="GLM-11",FILES$[3]="GLM-21"
0140 LET FILES$[4]="GLM-01",FILES$[5]="GLM-11",FILES$[6]="GLM-21"
0150 LET FILES$[7]="SYS-01"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM01_DEV1=CHANNELS[1],GLM11_DEV1=CHANNELS[2],GLM21_DEV1=CHANNELS[3]
0185 LET GLM01_DEV2=CHANNELS[4],GLM11_DEV2=CHANNELS[5],GLM21_DEV2=CHANNELS[6]
0190 LET SYS01_DEV=CHANNELS[7]
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$,A1$(1)
0220 GLM11A: IOLIST R0$,R1$(1)
0230 GLM21A: IOLIST S0$,S1$(1)
0250 SYS01C: IOLIST C0$(1),C1$(1)
0260 GLS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,F3$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM P[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0440 LET M0=LEN(M0$)
0450 FOR X=0 TO 4
0460 LET P[X]=NUM(P2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET P[2]=FNYY_YEAR(P2$(5,2))
0500 REM " --- Initializations"
0510 DIM A0$(2+P[4]),A1$(40),R0$(14),R1$(6),S0$(14),S1$(6)
0550 LET BEGACCT$="",ENDACCT$="",WILDCARD$=""
0560 LET BEGFIRM$=N0$,ENDFIRM$="",OVERWRITE$="N"
0580 LET W=79-(41+M0)
0590 IF W>35 THEN LET W=35
0700 REM " --- Background"
0710 PRINT 'SB',@(26,3),"From Company:",@(28,4),"To Company:",@(14,5),"Beginning Account Number:",@(17,6),"Ending Account Number:",@(25,7),"Wildcard Mask:",@(12,8),"Overwrite Existing Records:",'SF',
0750 GOSUB 5000
1000 REM " --- From Firm"
1010 LET V0$="Z",V1$="C",V2$=BEGFIRM$,V0=2,V1=40,V2=3,I0=0
1020 LET V4$="Enter a Valid Company Number (<F3>=Lookup)"
1030 GOSUB 7000
1040 ON V3 GOTO 1060,1000,1000,1050,3900
1050 CALL "SYC.LX",V$
1060 IF V$="" THEN GOTO 1000
1070 IF F3$<>"" THEN IF POS(V$=F3$,2)=0 THEN GOTO 1000
1075 GOSUB 5400
1080 IF STATUS>0 THEN GOTO 1000
1085 LET BEGFIRM$=C0$
1090 GOTO 3900
1200 REM " --- To Firm"
1210 LET V0$="Z",V1$="C",V2$=ENDFIRM$,V0=2,V1=40,V2=4
1220 LET V4$="Enter a Valid Company Number (<F3>=Lookup)"
1230 GOSUB 7000
1240 ON V3 GOTO 1260,1200,3900,1250,3900
1250 CALL "SYC.LX",V$
1260 IF V$="" THEN GOTO 1200
1270 IF F3$<>"" THEN IF POS(V$=F3$,2)=0 THEN GOTO 1200
1273 IF V$=BEGFIRM$ THEN GOTO 1200
1275 GOSUB 5400
1280 IF STATUS>0 THEN GOTO 1200
1285 LET ENDFIRM$=C0$
1290 GOTO 3900
1400 REM " --- Beginning Account Number"
1410 LET V4$="Enter A Beginning Account Number (<F1>=First/<F3>=Lookup)"
1415 LET V0$="M",V1$="C",V2$=BEGACCT$,V3$=M0$,V0=P[4],V1=40,V2=5
1420 GOSUB 7000
1425 ON V3 GOTO 1460,1430,3900,1450,3900
1430 LET BEGACCT$=""
1435 GOSUB 5600
1440 GOTO 3900
1450 CALL "SYC.LG",V$
1455 IF V$="" THEN GOTO 1400
1460 IF V$="" THEN GOTO 1430
1465 GOSUB 5700
1470 IF STATUS>0 THEN GOTO 1400
1480 IF V$<>"" THEN LET BEGACCT$=A0$(3)
1490 GOTO 3900
1500 REM " --- Ending Account Number"
1510 LET V4$="Enter An Ending Account Number (<F1>=Last/<F3>=Lookup)"
1515 LET V0$="M",V1$="C",V2$=ENDACCT$,V3$=M0$,V0=P[4],V1=40,V2=6
1520 GOSUB 7000
1525 ON V3 GOTO 1560,1530,3900,1550,3900
1530 LET ENDACCT$=""
1535 GOSUB 5650
1540 GOTO 3900
1550 CALL "SYC.LG",V$
1555 IF V$="" THEN GOTO 1500
1560 IF V$="" THEN GOTO 1530
1565 GOSUB 5700
1570 IF STATUS>0 THEN GOTO 1500
1580 LET ENDACCT$=A0$(3)
1590 GOTO 3900
1600 REM " --- Wildcard Mask"
1610 LET V4$="Enter The Wildcard Mask To Be Used In Selecting Accounts (<F1>=None)"
1615 LET V0$="S",V1$="C",V2$=WILDCARD$,V3$="",V0=P[4],V1=40,V2=7
1620 GOSUB 7000
1625 ON V3 GOTO 1630,1635,3900,1600,3900
1630 IF V$<>"" THEN GOTO 1650
1635 LET WILDCARD$=""
1645 GOTO 1680
1650 LET WILDCARD$=V$
1680 GOSUB 5500
1690 GOTO 3900
1700 REM " --- Overwrite Existing Accounts"
1710 LET V4$="Should Existing Accounts For The Destination Company Be Overwritten (Y/N)?"
1715 LET V0$="Y",V1$="C",V2$=OVERWRITE$,V3$="",V0=1,V1=40,V2=8
1720 GOSUB 7000
1730 ON V3 GOTO 1740,1700,3900,1700,3900
1740 IF POS(V$="YN")=0 THEN GOTO 1700
1750 LET OVERWRITE$=V$
1760 PRINT @(V1,V2),OVERWRITE$,
1790 GOTO 3900
3900 REM " --- Next field"
3915 IF V3=4 THEN GOSUB 5000; GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1500,1600,1700,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="Y",V1$="C",V2$="N",V3$="",V0=1,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Edit selections"
4110 LET V4$=""
4120 IF ENDFIRM$="" THEN LET V4$="A Destination Company Must Be Selected"
4130 IF BEGFIRM$=ENDFIRM$ THEN LET V4$="Source And Destination Companies Must Be Different"
4170 IF V4$="" THEN GOTO 4200
4180 GOSUB 6000
4190 GOTO 1000
4200 REM " --- Position File"
4210 LET A0$(1)=BEGFIRM$,A0$(3)=BEGACCT$
4220 CALL "SYC.NB","Copying",M0,COLUMN
4250 READ (GLM01_DEV1,KEY=A0$,DOM=4300)
4260 LET K$=A0$
4290 GOTO 4320
4300 REM " --- Copy accounts"
4310 LET K$=KEY(GLM01_DEV1,END=4900)
4320 IF POS(BEGFIRM$=K$)<>1 THEN GOTO 4900
4330 IF ENDACCT$<>"" THEN IF K$(3,P[4])>ENDACCT$ THEN GOTO 4900
4340 READ (GLM01_DEV1,KEY=K$)IOL=GLM01A
4370 PRINT @(COLUMN,11),FNF$(A0$(3,P[3]),M0$),
4380 GOSUB 6800
4390 IF STATUS>0 THEN GOTO 4300
4400 REM 
4410 GOSUB WRITE_GLM11
4420 GOSUB WRITE_GLM21
4430 LET A0$(1,2)=ENDFIRM$
4440 IF OVERWRITE$="Y" THEN GOTO 4470
4450 WRITE (GLM01_DEV2,KEY=A0$,DOM=4300)IOL=GLM01A
4460 GOTO 4300
4470 WRITE (GLM01_DEV2,KEY=A0$)IOL=GLM01A
4490 GOTO 4300
4900 REM 
4990 GOTO 9900
5000 REM " --- Display"
5010 PRINT 'CF',
5020 LET V$=BEGFIRM$,V2=3,V1=40
5025 GOSUB 5400
5030 LET V$=ENDFIRM$,V2=4
5035 GOSUB 5400
5050 LET V$=BEGACCT$,V2=5
5055 IF V$="" THEN GOSUB 5600 ELSE GOSUB 5700
5060 LET V$=ENDACCT$,V2=6
5065 IF V$="" THEN GOSUB 5650 ELSE GOSUB 5700
5070 LET V$=WILDCARD$,V2=7
5075 GOSUB 5500
5080 PRINT @(40,8),OVERWRITE$,
5090 RETURN 
5400 REM " --- Display Firm"
5410 DIM C0$(2),C1$(35)
5420 LET STATUS=1,C0$(1)=V$
5430 FIND (SYS01_DEV,KEY=C0$,DOM=5490)IOL=SYS01C
5440 PRINT @(V1,V2),C0$," ",C1$,'CL',
5450 LET STATUS=0
5490 RETURN 
5500 REM " --- Display Wildcard"
5510 IF WILDCARD$="" THEN PRINT @(V1,V2),"None",'CL', ELSE PRINT @(V1,V2),WILDCARD$,'CL',
5590 RETURN 
5600 REM " --- Display Beginning G/L Account"
5610 IF V$<>"" THEN GOTO 5700
5620 PRINT @(V1,V2),"First",'CL',
5640 RETURN 
5650 REM " --- Display Ending G/L Account"
5660 IF V$<>"" THEN GOTO 5700
5670 PRINT @(V1,V2),"Last",'CL',
5690 RETURN 
5700 REM " --- Display G/L Account"
5710 LET A0$(1)=N0$,A0$(3)=V$,A1$(1,35)="(Not On File)",STATUS=1
5720 FIND (GLM01_DEV1,KEY=A0$,DOM=5730)IOL=GLM01A
5725 LET STATUS=0
5730 PRINT @(V1,V2),FNF$(A0$(3,P[3]),M0$)," ",A1$(1,W),'CL',
5790 RETURN 
6000 REM " --- Standard Warning/Error"
6010 DIM MESSAGE$[1]
6020 LET MESSAGE$[0]=V4$+" (<Enter>=Continue)"
6030 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
6090 RETURN 
6200 REM " --- Write Financial Report Assignments
6205 WRITE_GLM11: 
6210 READ (GLM11_DEV1,KEY=A0$,DOM=6220)
6220 LET K11$=KEY(GLM11_DEV1,END=6390)
6230 IF K11$(1,12)<>A0$ THEN GOTO 6390
6240 READ (GLM11_DEV1,KEY=K11$)IOL=GLM11A
6250 LET R0$(1,2)=ENDFIRM$
6270 IF OVERWRITE$="Y" THEN GOTO 6300
6280 WRITE (GLM11_DEV2,KEY=R0$,DOM=6310)IOL=GLM11A
6290 GOTO 6310
6300 WRITE (GLM11_DEV2,KEY=R0$)IOL=GLM11A
6310 GOTO 6220
6390 RETURN 
6400 REM " --- Write Consolidated Financial Report Assignments
6405 WRITE_GLM21: 
6410 READ (GLM21_DEV1,KEY=A0$,DOM=6420)
6420 LET K21$=KEY(GLM21_DEV1,END=6590)
6430 IF K21$(1,12)<>A0$ THEN GOTO 6590
6440 READ (GLM21_DEV1,KEY=K21$)IOL=GLM21A
6450 LET S0$(1,2)=ENDFIRM$
6470 IF OVERWRITE$="Y" THEN GOTO 6500
6480 WRITE (GLM21_DEV2,KEY=S0$,DOM=6510)IOL=GLM21A
6490 GOTO 6510
6500 WRITE (GLM21_DEV2,KEY=S0$)IOL=GLM21A
6510 GOTO 6420
6590 RETURN 
6800 REM " --- Test account number against specified wildcard mask"
6810 LET STATUS=0
6820 IF WILDCARD$="" THEN GOTO 6990
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

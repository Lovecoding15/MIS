0010 REM "GLM - G/L Account Maintenance"
0020 REM "Program GLM.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0060 ON O0 GOTO 0070,4900
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=14
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-01",FILES$[2]="GLM-02",FILES$[3]="GLM-03"
0135 LET FILES$[4]="GLM-04",FILES$[5]="GLM-08",FILES$[6]=""
0140 LET FILES$[7]="GLM-11",FILES$[8]="GLM-12",FILES$[9]="GLM-22"
0145 LET FILES$[12]="GLT-06"
0150 LET FILES$[14]="SYS-01"
0160 CALL "SYC.DA",1,1,14,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET GLM01_DEV=CHANNELS[1],GLM02_DEV=CHANNELS[2],GLM03_DEV=CHANNELS[3]
0180 LET GLM04_DEV=CHANNELS[4],GLM08_DEV=CHANNELS[5]
0185 LET GLM11_DEV=CHANNELS[7],GLM12_DEV=CHANNELS[8],GLM22_DEV=CHANNELS[9]
0190 LET GLT06_DEV=CHANNELS[12]
0195 LET SYS01_DEV=CHANNELS[14]
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$(1),A1$(1)
0220 GLM11A: IOLIST R0$(1),R1$(1)
0230 GLM21A: IOLIST S0$(1),S1$(1)
0240 GLM04A: IOLIST X$(1)
0250 GLM02A: IOLIST X0$(1),A[ALL],U[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 GLS01A: IOLIST P0$,P1$,P2$(1),P3$(1),P4$(1),M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 DIM P2$(10),P3$(325),P4$(32),P[4]
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N3$=F5$
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=GLS01A
0450 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),MP=13
0480 FOR X=0 TO 4
0485 LET P[X]=NUM(P2$(X*2+1,2),ERR=0490)
0490 NEXT X
0495 LET P[2]=FNYY_YEAR(P2$(5,2))
0500 REM " --- Initializations"
0510 DIM A0$(2+P[4]),A1$(40),R0$(14),R1$(6),S0$(14),S1$(6),J1$(80),MASTER$(2+P[4])
0530 LET L0=5,O0=1,COLHDR$="Account Summary Inquiry Display Columns"
0540 LET A0$(1)=N0$,CONSOLIDATED$="N",HISTORY$="Y",VALID$=""
0550 REM " --- Channel Variables (Used by Overlays)"
0580 LET PRINTER=7
0600 REM " --- Determine which summary records to display"
0610 LET COLUMNS$=P4$(8,4),MC=LEN(FNP$(COLUMNS$)),TYPES$=P4$(12,4)
0620 IF MC>0 THEN GOTO 0700
0650 REM " --- No columns specified. Use historical precedents."
0660 LET COLUMNS$="0123",MC=4,TYPES$="AAAA"
0670 IF P4$(2,1)="Y" THEN GOTO 0700
0680 LET COLUMNS$="0101",TYPES$="AUAU"
0690 IF P4$(3,1)="N" THEN LET COLUMNS$="02  ",MC=2,TYPES$="AA  "
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0750 PRINT 'SB',@(0,3),"Account:",@(61),"Type:",@(70),"Detail?",'SF',
0900 REM " --- Position file"
0990 READ (GLM01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Account Number"
1005 PRINT 'CF',
1010 LET V2$="",V2$=KEY(GLM01_DEV,END=1040)
1020 IF V2$(1,2)<>N0$ THEN LET V2$=""
1030 IF V2$<>"" THEN LET V2$=V2$(3,P[3])
1040 LET V0$="M",V1$="C",V3$=P1$,V0=P[4],V1=9,V2=3,I0=0
1045 LET V4$="Enter A Valid G/L Account Number (<F3>=Lookup)"
1050 GOSUB 7000
1055 ON V3 GOTO 1070,1000,1000,1060,9900
1060 CALL "SYC.LG",V$
1070 IF V$="" THEN GOTO 1000
1080 LET A0$(1)=N0$+V$,MASTER$(1)=N0$+V$
1200 REM " --- Retrieve record"
1210 READ (GLM01_DEV,KEY=A0$,DOM=1400)IOL=GLM01A
1220 LET OLDDESC$=A1$(1,35)
1300 REM " --- Display record"
1320 GOSUB 5000
1390 GOTO 4900
1400 REM " --- New record"
1410 LET V4$="Is This A New Record (Y/N)?"
1420 LET V0$="Y",V1$="C",V2$="N",V3$="",V0=1,V1=FNV(V4$),V2=22
1430 GOSUB 7000
1440 ON V3 GOTO 1490,1400,1400,1400,1000
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record"
1510 DIM A1$(40),R0$(14),R1$(6),S0$(14),S1$(6)
1520 LET OLDDESC$="",A1$(37,1)=P4$(1,1)
2000 REM " --- Account Description"
2010 LET V4$="Enter The Description For This Account",I0=1
2020 LET V0$="S",V1$="C",V2$=A1$(1,35),V3$="",V0=35,V1=M0+10,V2=3
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,2000,2000,3900
2050 LET A1$(1,35)=V$
2090 GOTO 3900
2200 REM " --- Account Type"
2210 LET V0$="S",V1$="RC",V2$=A1$(36,1),V3$="ALICE",V0=1,V1=67,V2=3
2220 LET V4$="A=Asset   L=Liability   C=Capital   I=Income   E=Expense"
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 LET A1$(36,1)=V$
2290 GOTO 3900
2400 REM " --- Post Detail?"
2410 LET V0$="Y",V1$="C",V2$=A1$(37,1),V3$="",V0=1,V1=78,V2=3
2415 IF P4$(1,1)="N" THEN GOTO 2480
2420 LET V4$="Y=Post Detail To This Account   N=Post Summary Only To This Account"
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 LET A1$(37,1)=V$
2480 PRINT @(V1,V2),A1$(37,1)
2490 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,2000,2200,2400,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V0$="S",V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V1$="C",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4110 WRITE (GLM01_DEV,KEY=A0$(1,2+P[4]))IOL=GLM01A
4115 IF OLDDESC$=A1$(1,35) THEN GOTO 4150
4120 DIM X$(38+P[4])
4125 LET X$(1)=A0$(1,2)+"A"+OLDDESC$,X$(39)=A0$(3)
4130 REMOVE (GLM04_DEV,KEY=X$,DOM=4135)
4135 LET X$(4,35)=A1$(1,35)
4140 WRITE (GLM04_DEV,KEY=X$)IOL=GLM04A
4190 GOTO 4900
4200 REM " --- Remove"
4210 DIM X0$(3+P[4]),A[MP],U[MP]
4220 PRINT @(0,22),'CL',@(29),"Checking For Account Activity...",
4290 READ (GLM02_DEV,KEY=A0$,DOM=4300)
4300 REM " --- Check for GLM-02 Activity"
4310 LET K$=KEY(GLM02_DEV,END=4390)
4320 IF POS(A0$=K$)<>1 THEN GOTO 4390
4330 READ (GLM02_DEV,KEY=K$)IOL=GLM02A
4340 FOR X=0 TO MP
4350 IF A[X]<>0 THEN EXITTO 4500
4360 IF U[X]<>0 THEN EXITTO 4500
4370 NEXT X
4380 GOTO 4300
4390 READ (GLT06_DEV,KEY=A0$,DOM=4400)
4400 REM " --- Check for GLT-06 Activity"
4410 LET K$=KEY(GLT06_DEV,END=4600)
4420 IF POS(A0$=K$)<>1 THEN GOTO 4600
4500 REM " --- Activity exists, cannot delete account"
4510 DIM MESSAGE$[1]
4520 LET MESSAGE$[0]="Account Is Active And Cannot Be Deleted (<Enter>=Continue)"
4530 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
4590 GOTO 4000
4600 REM " --- Remove GLM-02 records"
4610 READ (GLM02_DEV,KEY=A0$,DOM=4620)
4620 LET K$=KEY(GLM02_DEV,END=4700)
4630 IF POS(A0$=K$)<>1 THEN GOTO 4700
4640 REMOVE (GLM02_DEV,KEY=K$)
4650 GOTO 4620
4700 REM " --- Remove GLM-01/04 records"
4710 DIM X$(38+P[4])
4720 LET X$(1)=A0$(1,2)+"A"+A1$(1,35),X$(39)=A0$(3)
4730 REMOVE (GLM04_DEV,KEY=X$,DOM=4740)
4770 REMOVE (GLM01_DEV,KEY=A0$,DOM=4780)
4790 GOTO 1000
4800 REM " --- Account comments"
4810 DIM INFO$[5],INFO[5]
4820 LET X$="Account "+FNF$(A0$(3,P[3]),M0$)+" "+FNP$(A1$(1,35))
4830 LET INFO$[0]="GLM-09",INFO$[1]=A0$,INFO$[2]=X$,ACTION=0,INFO[1]=L0
4850 CALL "SYC.KA",ACTION,INFO$[ALL],INFO[ALL]
4900 REM " --- Process submenu"
4905 PRINT @(0,22),'CL',
4910 CLOSE (PRINTER,ERR=4915)
4915 LET A0$(1)=MASTER$
4920 READ (GLM01_DEV,KEY=A0$,DOM=4925)IOL=GLM01A
4925 DIM MENUS$[1]
4930 LET MENUS=1,MENUS$[MENUS]="GLM.AA",WIN_X=0,WIN_Y=L0+1,O0=1
4935 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4940 CALL "SYC.WC",1,0,80,0,0,L0-1,0
4950 PRINT @(0,L0),'CE',
4960 IF SELECTION$="" THEN GOTO 1000
4970 IF SELECTION$=MENUS$[1] THEN GOTO 2000
4975 IF SELECTION$="SYC.KA" THEN GOTO 4800
4980 PRINT @(FNC(DESCRIPTION$,80),L0-1),'SB',DESCRIPTION$,'SF',
4985 LET INQUIRY$=DESCRIPTION$,MENU$="GLM.AA"
4990 RUN SELECTION$,ERR=4900
5000 REM " --- Display"
5010 PRINT @(9,3),FNF$(A0$(3,P[3]),M0$),@(10+M0),A1$(1,35),@(67),A1$(36,1),@(78),A1$(37,1),
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "GLR - Chart Of Accounts Listing"
0020 REM "Program GLR.KB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$,A1$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY=N0$+N2$+"01",DOM=9800)
0440 CALL "GLC.RG",GLM10_DEV,N0$,BREAK$,DESC$
0500 REM " --- Initializations"
0550 LET H0=80,N=0,H3$=""
0590 LET A0$(1)=N0$+BEGACCT$
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",M0,COLUMN
0900 REM " --- Position File"
0980 READ (GLM01_DEV,KEY=A0$,DOM=1000)IOL=GLM01A
0990 GOTO 1020
1000 REM " --- Top Of Loop"
1010 READ (GLM01_DEV,END=4000)IOL=GLM01A
1020 IF POS(N0$=A0$)<>1 THEN GOTO 4000
1030 IF ENDACCT$<>"" THEN IF A0$(3,P[4])>ENDACCT$ THEN GOTO 4000
1080 GOSUB 6800
1090 IF STATUS>0 THEN GOTO 3900
1100 REM 
1110 PRINT @(COLUMN,11),FNF$(A0$(3,P[3]),M0$)
1120 LET N=N+1
1400 REM " --- Breaks"
1410 IF BREAK$="" THEN GOTO 2000
1420 IF BREAK$<>"" THEN IF A0$(3,P[4])<BREAK$(1,P[4]) THEN GOTO 2000
1430 LET H3$=DESC$(1,20),H4$=H3$
1500 REM 
1510 IF LEN(BREAK$)>P[4] THEN LET BREAK$=BREAK$(P[4]+1),DESC$=DESC$(21)
1520 IF LEN(BREAK$)<=P[4] THEN LET BREAK$=""
1530 IF BREAK$<>"" THEN IF POS(" "<>BREAK$(1,P[4]))=0 THEN GOTO 1500
1590 GOTO 1400
2000 REM " --- Print record"
2005 IF L+1>L9 THEN GOSUB 5000
2010 IF H3$<>"" THEN GOSUB 5200
2020 PRINT (7)@(5),FNF$(A0$(3,P[3]),M0$),"  ",A1$(1,35)
2030 LET L=L+1
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF L+2>L9 THEN GOSUB 5000
4020 PRINT (7)""
4030 PRINT (7)@(5),STR(N)," Account Numbers Listed"
4090 GOTO 9900
5000 REM " --- Report heading"
5010 LET L=HEADINGS+1,H3$=H4$
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5090 RETURN 
5200 REM " --- Subheading"
5210 IF L+4>L9 THEN GOSUB 5000
5220 PRINT (7)""
5230 PRINT (7)H3$
5240 PRINT (7)""
5250 LET L=L+3,H3$=""
5290 RETURN 
6800 REM " --- Test account number against specified wildcard mask"
6810 LET STATUS=0
6820 IF WILDCARD$="" THEN GOTO 6890
6825 IF WILDCARD<>LEN(WILDCARD$) THEN LET WILDCARD=LEN(WILDCARD$)
6830 LET X$=FNF$(A0$(3,P[3]),M0$)
6840 FOR X=1 TO WILDCARD
6850 IF WILDCARD$(X,1)="?" THEN GOTO 6880
6860 IF X$(X,1)<>WILDCARD$(X,1) THEN LET STATUS=1
6880 NEXT X
6890 RETURN 
8000 REM " --- Functions"
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return To Menu"
9950 RUN "SYS.AA"
9999 END

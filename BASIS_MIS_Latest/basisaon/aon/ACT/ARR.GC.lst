0010 REM "ARR - Monthly Cash Receipts Journal (Distribution)"
0020 REM "Program ARR.GC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 GLM01A: IOLIST A0$,A1$(1)
0220 ARM01A: IOLIST B0$,B1$(1)
0240 ARW07A: IOLIST X0$(1),X0
0500 REM " --- Initializations"
0550 LET HEADINGS=6,HEADINGS$[5]=HEADINGS$[4],HEADINGS$[4]=HEADINGS$[3]
0560 LET HEADINGS$[3]=HEADINGS$[2],HEADINGS$[2]="G/L Account Distribution"
0580 LET M1$=G6$,M1=LEN(M1$),L=L9+1,T0=0,T1=0,T9$=""
0590 LET T0$="",T1$="",H9=0,D1=0,D2=0,D3=0,C1=0,C2=0,C3=0
0600 REM " --- Print positions"
0610 DIM O[6]
0620 LET M9=MAX(M1,10),G9=MAX(9,G5)
0630 LET O[0]=G9+1,O[1]=O[0]+10,O[2]=O[1]+MAX(M0,8)+1
0640 LET O[3]=O[2]+31,O[4]=O[3]+10,O[5]=O[4]+10,O[6]=O[5]+M9
0900 REM " --- Position File"
0990 READ (ARW07_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next ARW-07 record"
1010 DIM X0$(37)
1020 READ (ARW07_DEV,END=4000)IOL=ARW07A
1030 IF X0$(1,2)<>N0$ THEN GOTO 4000
1050 PRINT @(COL,11),FNB$(X0$(5,3))," ",FNF$(X0$(8,G[3]),G5$)," ",FNF$(X0$(18,P[0]),M0$)
1200 REM " --- Level breaks?"
1210 IF X0$(5,3)<>T0$ THEN GOSUB 6200
1220 IF X0$(8,10)<>T1$ THEN GOSUB 6400
1400 REM " --- Other reads"
1410 DIM B1$(30)
1420 LET B1$(1)="Unknown Customer"
1430 FIND (ARM01_DEV,KEY=X0$(1,2)+X0$(18,6),DOM=1440)IOL=ARM01A
2000 REM " --- New account?"
2010 IF H2$="" THEN GOTO 2200
2020 IF L+3>L9 THEN GOSUB 5000
2030 PRINT (7)H2$
2040 PRINT (7)""
2050 LET L=L+2,H2$=""
2200 REM " --- Print detail line"
2210 IF L+1>L9 THEN GOSUB 5000
2220 LET DEBIT$="",CREDIT$=""
2230 IF X0>=0 THEN LET DEBIT$=STR(X0:M1$),D1=D1+X0,D2=D2+X0,D3=D3+X0
2240 IF X0<0 THEN LET CREDIT$=STR(ABS(X0):M1$),C1=C1+X0,C2=C2+X0,C3=C3+X0
2250 PRINT (7)@(O[0]),FNB$(X0$(5,3)),@(O[1]),FNF$(X0$(18,P[0]),M0$),@(O[2]),B1$,@(O[3]),X0$(24,7),@(O[4]),X0$(31,7),@(O[5]),DEBIT$,@(O[6]),CREDIT$
2260 LET L=L+1
3900 REM 
3990 GOTO 1000
4000 REM " --- All done"
4005 LET T9$="E"
4010 GOSUB 6200
4020 IF L+2>L9 THEN GOSUB 5000
4030 LET X$="Totals For Report"
4040 PRINT (7)@(O[5]),J1$(1,M1*2)
4050 PRINT (7)@(O[5]-LEN(X$)),X$,@(O[5]),D3:G6$,@(O[6]),ABS(C3):G6$
4060 DIM J1$(10+G5+M0)
4070 PRINT @(COL,11),J1$,
4080 LET PAGE=H,WIDTH=H0,COLUMN=COL
4090 RUN "GLR.XB"
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)"G/L",@(O[0]),"Receipt",@(O[1]),"Customer",@(O[3]),"Check",@(O[4]),"Invoice"
5050 PRINT (7)"Account",@(O[0]),"  Date",@(O[1]),"Number",@(O[2]),"Name",@(O[3]),"Number",@(O[4]),"Number",@(O[5]+M9-6),"Debit",@(O[6]+M9-7),"Credit"
5060 PRINT (7)""
5080 LET L=L+3
5090 RETURN 
6200 REM " --- Date Break"
6210 GOSUB 6400
6220 IF T0$="" THEN GOTO 6300
6230 IF L+3>L9 THEN GOSUB 5000
6240 LET X$="Totals For "+FNB$(T0$)
6250 PRINT (7)@(O[5]),J$(1,M1*2)
6260 PRINT (7)@(O[5]-LEN(X$)),X$,@(O[5]),D2:G6$,@(O[6]),ABS(C2):G6$
6270 PRINT (7)""
6280 LET L=L+3
6300 REM 
6310 IF T9$<>"" THEN GOTO 6390
6320 LET T0$=X0$(5,3),D2=0,C2=0
6390 RETURN 
6400 REM " --- Account Break"
6410 IF T1$="" THEN GOTO 6500
6450 IF L+3>L9 THEN GOSUB 5000
6455 LET X$="Totals For "+FNF$(T1$(1,G[3]),G5$)+" "+FNP$(A1$)
6460 PRINT (7)@(O[5]),J$(1,M1*2)
6470 PRINT (7)@(O[5]-LEN(X$)),X$,@(O[5]),D1:G6$,@(O[6]),ABS(C1):G6$
6480 PRINT (7)""
6490 LET L=L+3
6500 REM 
6505 IF T9$<>"" THEN GOTO 6590
6510 DIM A1$(35)
6520 LET A1$(1)="Unknown Account",D1=0,C1=0
6525 IF LEN(X0$)>16 THEN LET T1$=X0$(8,10)
6530 FIND (GLM01_DEV,KEY=N0$+T1$(1,10),DOM=6540)IOL=GLM01A
6540 LET H2$=FNF$(T1$(1,G[3]),G5$)+" "+FNP$(A1$)
6590 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

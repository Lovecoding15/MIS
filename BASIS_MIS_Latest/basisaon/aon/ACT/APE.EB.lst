0010 REM "APE - PAYMENT SELECTION ENTRY 2"
0020 REM "Program APE.EB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 APE06A1: IOLIST N0$,X0
0220 APE06A2: IOLIST N0$,0
0230 APE06A3: IOLIST *,X0
0240 APE16A: IOLIST X0$,Q9$
0250 APM10A: IOLIST X0$
0260 APM10D: IOLIST X0$
0800 REM " --- Options
0820 PRINT @(0,5),'CE',@(20,6),"1) Select All Invoices",@(20,7),"2) Select By Invoice Due Date",@(20,8),"3) Select By Discount Due Date",@(20,9),"4) Select By Payment Group",@(20,10),"5) Select Specific Invoices",@(20,11),"6) Clear All Previous Selections",
0830 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Enter Your Option:",V0=1,V1=FNV(V4$),V2=22
0840 GOSUB 7000
0850 IF V3=4 THEN GOTO 9900
0860 LET P9$=V$
0870 LET P7$="",P8$=""
0900 ON POS(P9$="123456") GOTO 0830,3500,1000,1000,1800,2000,3000
1000 REM " --- Dates
1020 IF P9$="2" THEN LET X$="Due Date"
1040 IF P9$="3" THEN LET X$="Discount Date"
1060 PRINT 'CF','SB',@(29,5),"Select By ",X$,@(29,7),"From Date:",@(29,8),"Thru Date:",'SF',
1080 IF P3$(1,1)="Y" THEN PRINT 'SB',@(31,6),"AP Type:",'SF',
1200 REM " --- AP Type
1210 IF P3$(1,1)<>"Y" THEN GOTO 1500
1220 GOSUB 6400
1240 IF V3=4 THEN GOTO 0800
1500 REM " --- From
1520 LET V0$="D",V1$="ER",V2$=P7$,V0=8,V1=40,V2=7
1540 GOSUB 7000
1550 IF V3=4 THEN GOTO 0800
1555 IF V3=2 THEN GOTO 1200
1560 LET P7$=V$
1600 REM " --- Thru
1620 LET V0$="D",V1$="ER",V2$=P8$,V0=8,V1=40,V2=8
1640 GOSUB 7000
1650 IF V3=2 THEN GOTO 1500
1660 LET P8$=V$
1680 IF P8$<P7$ THEN GOTO 1600
1690 LET Q9$="Invoice Due"
1700 IF P9$="3" THEN LET Q9$="Discount Due"
1720 LET Q9$="Include All Invoices By "+Q9$+" Date From "+FNB$(P7$)+" Thru "+FNB$(P8$)
1790 GOTO 4000
1800 REM " --- Payment Group
1810 PRINT 'SB',@(30,5),"Select By Payment Group:",'SF',
1820 PRINT 'CF','SB',@(25,7),"Payment Group:",'SF',
1830 IF P3$(1,1)<>"Y" THEN GOTO 1900
1840 PRINT 'SB',@(31,6),"AP Type:",'SF',
1860 GOSUB 6400
1870 IF V3=4 THEN GOTO 0800
1900 REM " --- Select
1910 DIM P8$(2)
1920 LET V0$="S",V1$="KEC",V2$=P8$,V0=2,V1=40,V2=7,V4$="Enter A Valid Payment Group, (<F3>=Lookup)"
1930 GOSUB 7000
1932 IF V3=2 THEN GOTO 1830
1935 IF V3=4 THEN GOTO 0800
1940 IF V3<>3 THEN GOTO 1946
1942 LET V4=1,V5=APM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="D"
1944 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
1946 IF V$="" THEN GOTO 1900
1948 LET P8$=V$
1950 FIND (APM10_DEV,KEY=N0$+"D"+P8$,DOM=1900)IOL=APM10D
1960 PRINT @(V1+3,V2),X0$(6,20),
1980 LET Q9$="Include All Invoices In Payment Group "+P8$
1990 GOTO 4000
2000 REM " --- Invoices
2020 RUN "APE.EC"
3000 REM " --- Clear File
3010 PRINT 'CF','SB',@(30,5),"Clear All Previous Selections",'SF',
3020 GOSUB 6800
3040 GOTO 0800
3500 REM " --- Select All Invoices
3510 PRINT 'CF','SB',@(30,5),"Select All Invoices",'SF',
3520 LET Q9$="Include All Open Invoices"
3530 IF P3$(1,1)="Y" THEN GOTO 3600
3540 GOTO 4000
3600 REM " --- AP Type
3610 IF P3$(1,1)<>"Y" THEN GOTO 3690
3620 PRINT 'CF','SB',@(31,6),"AP Type:",'SF',
3640 GOSUB 6400
3650 IF V3=4 THEN GOTO 0800
3690 GOTO 4000
4000 REM " --- Correct?
4010 LET V0$="Y",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4040 IF V3=4 THEN GOTO 0800
4050 ON POS(V$="YN") GOTO 4000,4100,0900
4100 GOSUB 6600
4200 RUN "APE.ED"
6400 REM " --- AP Type
6410 DIM P6$(2)
6420 LET V0$="S",V1$="KEC",V2$=P6$,V0=2,V1=40,V2=6,V4$="Enter A Valid AP Type, <F1>=All AP Types Or <F3>=Code Lookup"
6430 GOSUB 7000
6440 IF V3=1 THEN GOTO 6480
6445 IF V3=4 THEN GOTO 6475
6447 IF V3=3 THEN GOTO 6500
6450 LET P6$(1)=V$
6460 FIND (APM10_DEV,KEY=N0$+"A"+P6$,DOM=6420)IOL=APM10A
6470 PRINT @(V0+V1+1,V2),X0$(6,20),
6475 RETURN 
6480 LET P6$=""
6485 PRINT @(V1,V2),"All",'CL',
6490 RETURN 
6500 REM " --- AP Type Lookup
6520 LET V4=1,V5=APM10_DEV,V6=4,V7=2,V8=6,V9=20,V5$="A"
6540 CALL "SYC.LZ",V1,V2,V4,V5,V6,V7,V8,V9,V$,V5$,N0$
6560 IF V$="" THEN GOTO 6400
6580 GOTO 6450
6600 REM " --- Write Transaction
6610 LET X0=0
6620 EXTRACT (APE06_DEV,KEY=N0$,DOM=6630)IOL=APE06A3
6640 LET X0=X0+1
6650 WRITE (APE06_DEV,KEY=N0$)IOL=APE06A1
6660 LET X0$=N0$+STR(X0:"000")
6670 WRITE (APE16_DEV,KEY=X0$)IOL=APE16A
6690 RETURN 
6800 REM " --- Clear Selections
6810 PRINT @(33,3),"Clearing .....",
6820 READ (APE04_DEV,KEY=N0$,DOM=6830)
6840 LET K$=KEY(APE04_DEV,END=6890)
6860 IF K$(1,2)<>N0$ THEN GOTO 6890
6870 REMOVE (APE04_DEV,KEY=K$)
6880 GOTO 6840
6900 READ (APE16_DEV,KEY=N0$,DOM=6910)
6910 LET K$=KEY(APE16_DEV,END=6950)
6920 IF K$(1,2)<>N0$ THEN GOTO 6950
6930 REMOVE (APE16_DEV,KEY=K$)
6940 GOTO 6910
6950 REMOVE (APE06_DEV,KEY=N0$,DOM=6951)
6970 WRITE (APE06_DEV,KEY=N0$)IOL=APE06A2
6980 PRINT @(0,3),'CL',
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8075 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "APM - Vendor Maintenance (Receipt Hist Inquiry - Summary & Detail)
0020 REM "Program APM.MI"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0200 REM " --- IOLists
0210 APM10A: IOLIST APDESC$(1)
0220 IVM01A: IOLIST *,ITEMDESCRIPT$(1)
0230 POM02A: IOLIST Y0$(1),Y1$(1)
0240 POT04A: IOLIST X0$(1),X1$(1),X2$,X3$,X[ALL]
0250 POT14A: IOLIST W0$,W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0260 POT05A: IOLIST C0$(1),C1$(1)
0270 POT25A: IOLIST D0$(1),D1$(1),D2$,D[ALL]
0280 POT35A: IOLIST XREF$(1)
0400 REM " --- Parameters
0450 LET WIDTH=80,HEIGHT=18,WIN_X=0,WIN_Y=5,PAGES=20
0460 LET TITLE$="Receipt History Inquiry",MAXROW=HEIGHT-5
0500 REM " --- Initializations
0510 DIM X0$(22),X1$(160),X[8],Y0$(4),Y1$(32)
0520 DIM W0$(25),W1$(48),W2$(32),W3$(22),W4$(40),W[12]
0530 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),APDESC$(32)
0540 LET ROW=2,M1IV$=M5$,M2IV$=M6$,M3IV$=M7$
0550 CALL "SYC.RM",M1IV$,M1IV,9,11
0560 CALL "SYC.RM",M2IV$,M2IV,8,9
0570 CALL "SYC.RM",M3IV$,M3IV,6,9
0580 CALL "SYC.RM",MCONVF$,MCONVF,5,6
0590 LET LEVEL$="S",LEVELDESC$="Summary",STARTPO$=""
0595 LET DASHES$=FILL(9,"-")
0700 REM " --- Background
0705 PRINT @(0,11),'CE',
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0750 PRINT 'CF','SB',@(21,ROW),"Summary Or Detail: ",'SF',
0760 PRINT 'CF','SB',@(26,ROW+1),"Starting PO#: "
0800 REM " --- Summary or detail level
0810 LET V4$="S=Summary    D=Detail"
0815 PRINT @(0,HEIGHT-4),'CL',@(FNC(V4$,WIDTH-2),HEIGHT-4),V4$,
0820 LET V0$="S",V1$="^",V2$=LEVEL$,V3$="SD",V$="",V0=1,V1=40,V2=ROW,V4$=""
0825 GOSUB 7000
0830 ON V3 GOTO 0835,0800,0800,0800,9900
0835 LET LEVEL$=V$
0840 IF LEVEL$="S" THEN LET LEVELDESC$="Summary" ELSE LET LEVELDESC$="Detail"
0845 PRINT @(V1,V2),'CL',LEVELDESC$,
0850 REM " --- Beginning PO Number
0855 LET V4$="Enter A Beginning Purchase Order Number (<Enter>=All)"
0860 PRINT @(0,HEIGHT-4),'CL',@(FNC(V4$,WIDTH-2),HEIGHT-4),V4$,
0865 LET V0$="S",V1$="",V2$=STARTPO$,V3$="",V4$="",V$="",V0=7,V1=40,V2=ROW+1
0870 GOSUB 7000
0875 ON V3 GOTO 0880,0850,0800,0850,9900
0880 LET STARTPO$=V$
0885 LET FIRST$=V$
0890 IF STARTPO$="" THEN PRINT @(V1,V2),'CL',"First", ELSE PRINT @(V1,V2),STARTPO$,
0900 REM " --- Heads and foots etc
0910 GOSUB 5200
0920 PRINT @(0,ROW),'CL',@(0,ROW+1),'CL',@(0,ROW+2),'CL',@(0,HEIGHT-4),'CL',
0930 REM " --- Position POT-04
0940 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,5]
0950 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,ROW=2
0960 LET POORDTOT=0,PORECTOT=0,POINVTOT=0,RPTORDTOT=0,RPTRECTOT=0,RPTINVTOT=0
0965 LET SHOWPONUM=1,SAMEREC=0,ENDINVS=0,PONUM$=""
0970 LET X0$(1)=A0$(1,8)+STARTPO$
0980 READ (POT04_DEV,KEY=X0$(1,22),DOM=1000)IOL=POT04A
1000 REM " --- Read next PO
1005 READPOT04: 
1010 LET K$=KEY(POT04_DEV,END=3700)
1020 IF POS(N0$=K$)<>1 THEN GOTO 3700
1030 IF K$(3,6)<>A0$(3,6) THEN GOTO 3700
1040 IF LEVEL$<>"S" THEN GOTO 1070
1050 IF K$(9,7)<>PONUM$ AND PONUM$<>"" AND POLINES>1 THEN GOSUB POTOTAL
1060 IF K$(9,7)<>PONUM$ THEN LET POLINES=0,SHOWPONUM=1,POORDTOT=0,PORECTOT=0,POINVTOT=0
1070 REM " --- 
1080 LET FKEY=0
1090 REM " --- Receipt header
1100 READ (POT04_DEV,KEY=K$)IOL=POT04A
1110 LET VENDOR$=X0$(3,6),PONUM$=X0$(9,7),RECEIVER$=X0$(16,7),INVNUM$=""
1120 LET RCPTDATE$=FILL(8)
1130 IF X1$(29,6)<>FILL(6) THEN LET RCPTDATE$=FNB6$(X1$(29,6))
1140 IF LEVEL$="D" THEN GOTO DODETAIL
1150 REM " --- Summary level inquiry
1170 LET ORDAMT=0,RECAMT=0
1180 LET INVAMT=0,INVDATE$=FILL(8),INVNUM_PRINT$=""
1190 GOSUB GETORD_REC
1200 GOSUB GETINV
1210 IF FKEY=4 THEN GOTO 0750
1220 IF ENDINVS<=1 THEN GOSUB LOADDISP ELSE GOTO 1250
1230 IF FKEY=4 THEN GOTO 0750
1235 IF K35$(9,7)=PONUM$ THEN LET SHOWPONUM=0
1240 IF K35$(9,7)=PONUM$ AND K35$(16,7)=RECEIVER$ THEN LET SAMEREC=1; GOTO 1200
1250 LET SAMEREC=0,ENDINVS=0
1260 GOTO NEXTPOT04
1300 REM " --- Detail level inquiry
1305 DODETAIL: 
1400 REM " ---
1410 IF PONUM$<>PREVPO$ THEN GOSUB NEWPO
1420 READ (POT14_DEV,KEY=X0$,DOM=1450)
1450 READPOT14: 
1460 LET K14$=KEY(POT14_DEV,END=NEXTPOT04)
1470 IF K14$(1,22)<>X0$ THEN GOTO NEXTPOT04
1480 READ (POT14_DEV,KEY=K14$)IOL=POT14A
1500 REM " --- 
1510 LET Y0$(1)=N0$+W1$(1,2)
1520 FIND (POM02_DEV,KEY=Y0$,DOM=NEXTPOT04)IOL=POM02A
1530 LET QTY=W[3]-W[7]
1540 IF Y1$(21,1)="O" THEN LET QTY=1
1545 PRECISION 2
1550 LET EXTENSION=W[1]*QTY
1560 PRECISION IVPRECISION
1565 REM " --- Load display string - common parts
1570 DIM DISP$(WIDTH-2),DISP2$(WIDTH-2)
1580 LET DISP$(2)=W0$(23)+" "+W1$(1,2)+" "+W3$(1,2)
1590 ON POS(Y1$(21,1)="SNVMO") GOTO NEXTPOT14,STOCK,NONSTOCK,VPARTNUM,MESSAGE,OTHER
1600 REM " --- Load display strings - Standard Line
1605 STOCK: 
1620 DIM ITEMDESCRIPT$(60),ITEMDESC$(60)
1640 FIND (IVM01_DEV,KEY=N0$+W3$(3),DOM=1660)IOL=IVM01A
1650 LET ITEMDESC$(1)=FNITEM$(ITEMDESCRIPT$,DESCLEN[1],DESCLEN[2],DESCLEN[3])
1660 LET DISP$(12)=W3$(3,ITEMLEN),DISP$(33)=W1$(22,2)
1670 LET DISP$(O[0])=STR(W[0]:MCONVF$),DISP$(O[1])=STR(W[3]:M2IV$)
1680 LET DISP$(O[2])=STR(W[7]:M2IV$),DISP$(O[3])=STR(W[1]:M3IV$)
1690 LET DISP$(O[4])=STR(EXTENSION:M1IV$)
1700 LET DISP2$(12)=ITEMDESC$(1,40),DISP2$(63)="Req'd: "+FNB6$(W1$(3,6))
1710 GOTO LOADDISP_DETAIL
1800 REM " --- Load display strings - Non-Stock Line
1805 NONSTOCK: 
1820 LET DISP$(12)=W3$(3,ITEMLEN),DISP$(33)=W1$(22,2)
1830 LET DISP$(O[1])=STR(W[3]:M2IV$),DISP$(O[2])=STR(W[7]:M2IV$)
1840 LET DISP$(O[3])=STR(W[1]:M3IV$),DISP$(O[4])=STR(EXTENSION:M1IV$)
1850 LET DISP2$(12)=W4$,DISP2$(63)="Req'd: "+FNB6$(W1$(3,6))
1860 GOTO LOADDISP_DETAIL
1900 REM " --- Load display strings - Vendor Part Number Line
1905 VPARTNUM: 
1920 LET DISP$(12)=W4$(1,VPRODLEN)
1930 GOTO LOADDISP_DETAIL
1950 REM " --- Load display strings - Message Line
1955 MESSAGE: 
1970 LET DISP2$(12)=W4$
1980 GOTO LOADDISP_DETAIL
2000 REM " --- Load display strings - Other Line
2005 OTHER: 
2020 LET DISP$(O[3])=STR(W[1]:M3IV$),DISP$(O[4])=STR(EXTENSION:M1IV$)
2030 LET DISP2$(12)=W4$,DISP2$(63)="Req'd: "+FNB6$(W1$(3,6))
2040 GOTO LOADDISP_DETAIL
2200 REM " --- Load LLIST$[]
2205 LOADDISP_DETAIL: 
2220 IF L+2>MAXROW THEN GOSUB 5400
2230 IF FKEY=4 THEN GOTO 0750
2240 LET LLIST$[PAGE,X1,1]=DISP$,X0=X0+1,X1=X1+1,L=L+1
2270 IF FKEY=4 THEN GOTO 0750
2275 IF DISP2$=FILL(WIDTH-2) THEN GOTO 2400; REM "Vendor Part #
2290 LET LLIST$[PAGE,X1,1]=DISP2$,X0=X0+1,X1=X1+1,L=L+1
2400 REM " --- Loop up for next PO line number
2405 NEXTPOT14: 
2430 GOTO READPOT14
2500 REM " --- Loop up for next PO/Receiver
2505 NEXTPOT04: 
2520 GOTO READPOT04
3700 REM " --- No more Receipts
3710 IF X0>0 THEN GOTO 3800
3720 CALL "SYC.WB",NAME$,0
3730 LET V0$="Sa",V1$="",V2$="",V3$="",V4$=""
3740 LET V0=1,V1=35,V2=INT(HEIGHT/2)-1
3750 PRINT @(1,V2),"No records found (<Enter>=Continue):"
3760 GOSUB 7000
3770 GOTO 0750
3800 REM " --- Display last page
3810 IF LEVEL$="D" THEN GOTO 3850
3820 IF POLINES>1 THEN GOSUB POTOTAL
3830 IF FKEY=4 THEN GOTO 0750
3840 GOSUB REPORTTOTAL
3850 IF FKEY=4 THEN GOTO 0750
3860 IF X1=0 THEN GOTO 4000
3870 LET ANSWER$="|EOF"
3880 GOSUB 5400
4000 REM " --- All done
4090 GOTO 0750
5200 REM " --- Set up and print headers and footers
5210 IF LEVEL$="D" THEN GOTO DETAILHEAD
5220 DIM O[2]
5230 LET O[2]=WIDTH-M1IV-1,O[1]=O[2]-M1IV,O[0]=O[1]-M1IV
5240 LET HEADING$(2)="PO #    Rec #   Received Invoice #  Invoiced"
5250 LET HEADING$(O[0]+M1IV-7)="Ordered",HEADING$(O[1]+M1IV-9)="Received"
5260 LET HEADING$(O[2]+M1IV-10)="Invoiced"
5280 GOTO 5360
5305 DETAILHEAD: 
5310 DIM O[4]
5320 LET O[4]=WIDTH-M1IV-1,O[3]=O[4]-M3IV,O[2]=O[3]-M3IV
5325 LET O[1]=O[2]-M3IV,O[0]=O[1]-MCONVF
5330 LET HEADING$(2)="Seq Cd Wh Item",HEADING$(33)="UM"
5340 LET HEADING$(O[0]+MCONVF-5)="Conv",HEADING$(O[1]+M2IV-8)="Ordered"
5350 LET HEADING$(O[2]+M2IV-6)="Rec'd",HEADING$(O[3]+M3IV-5)="Cost"
5355 LET HEADING$(O[4]+M1IV-10)="Extension"
5360 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
5370 PRINT @(0,0),'CS','SB','BR',HEADING$,'ER','SF',
5380 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
5390 RETURN 
5400 REM " --- Middle window processing
5410 LET MODE=5,X1=1,L=1
5420 CALL "SYC.SA",MODE,LLIST$[ALL],MORE$[ALL],ANSWER$,PAGE,MAXROW,NAME$,HEIGHT,WIDTH,FKEY
5460 IF FKEY=4 THEN GOTO 5490
5470 IF FKEY<>-17 THEN LET PAGE=PAGE+1
5480 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAXROW,5,SHEIGHT-2
5490 RETURN 
6000 REM " --- PO Total
6005 POTOTAL: 
6010 IF L+1>MAXROW THEN GOSUB 5400
6020 IF FKEY=4 THEN GOTO 6090
6030 DIM DISP$(WIDTH-2)
6040 LET DISP$(O[0]-23)="Total For PO #"+PONUM$,DISP$(O[0])=STR(POORDTOT:M1IV$)
6050 LET DISP$(O[1])=STR(PORECTOT:M1IV$),DISP$(O[2])=STR(POINVTOT:M1IV$)
6060 LET LLIST$[PAGE,X1,1]=DISP$,X0=X0+1,X1=X1+1,L=L+1
6070 LET POORDTOT=0,PORECTOT=0,POINVTOT=0
6090 RETURN 
6100 REM " --- Report Total
6105 REPORTTOTAL: 
6120 IF L+1>MAXROW THEN GOSUB 5400
6130 IF FKEY=4 THEN GOTO 6190
6140 DIM DISP$(WIDTH-2)
6150 LET DISP$(O[0]-19)="Total For Inquiry",DISP$(O[0])=STR(RPTORDTOT:M1IV$)
6160 LET DISP$(O[1])=STR(RPTRECTOT:M1IV$),DISP$(O[2])=STR(RPTINVTOT:M1IV$)
6170 LET LLIST$[PAGE,X1,1]=DISP$,X0=X0+1,X1=X1+1,L=L+1
6190 RETURN 
6200 REM " --- Accumulate ordered and received qtys for this receipt
6205 GETORD_REC: 
6210 READ (POT14_DEV,KEY=X0$,DOM=6220)
6220 LET K14$=KEY(POT14_DEV,END=6290)
6230 IF K14$(1,22)<>X0$(1,22) THEN GOTO 6290
6240 DIM W[12]
6250 READ (POT14_DEV,KEY=K14$)IOL=POT14A
6260 LET UNITCOST=W[1]
6270 LET ORDAMT=ORDAMT+(UNITCOST*W[3])
6275 LET RECAMT=RECAMT+(UNITCOST*W[7])
6280 GOTO 6220
6290 RETURN 
6300 REM " --- Get invoice data
6405 GETINV: 
6410 DIM XREF$(37),C0$(20),C1$(64),D0$(23),D1$(31),D[4],K35$(40)
6420 IF SAMEREC=0 THEN READ (POT35_DEV,KEY=X0$,DOM=6430)
6430 LET K35$=KEY(POT35_DEV,END=6580)
6440 IF K35$(1,22)<>X0$ THEN GOTO 6590
6450 IF K35$(25,10)<>INVNUM$ AND INVNUM$<>"" THEN LET INVNUM$=K35$(25,10); GOTO 6590
6470 READ (POT35_DEV,KEY=K35$)
6480 REM " --- 
6490 DIM D[12]
6500 LET D0$=K35$(1,2)+K35$(23,2)+K35$(3,6)+K35$(25,16)
6510 FIND (POT05_DEV,KEY=D0$(1,23),DOM=6590)IOL=POT05A
6520 IF C1$(1,6)<>FILL(6) THEN LET INVDATE$=FNB6$(C1$(1,6))
6530 LET INVNUM$=C0$(11,10),INVNUM_PRINT$=INVNUM$
6540 FIND (POT25_DEV,KEY=D0$,DOM=6580)IOL=POT25A
6550 LET UNITCOST=D[0],QTYREC=D[1]
6560 LET INVAMT=INVAMT+(UNITCOST*QTYREC)
6570 GOTO 6430
6580 LET ENDINVS=ENDINVS+1
6590 RETURN 
6600 REM " --- Fill Display array for summary inquiry
6605 LOADDISP: 
6610 DIM DISP$(WIDTH-2)
6620 IF L+1>MAXROW THEN GOSUB 5400
6630 IF FKEY=4 THEN GOTO 6790
6640 IF SHOWPONUM THEN LET DISP$(2)=PONUM$
6650 LET DISP$(10)=RECEIVER$+" "+RCPTDATE$+" "+INVNUM_PRINT$+" "+INVDATE$
6670 LET DISP$(O[0])=STR(ORDAMT:M1IV$),DISP$(O[1])=STR(RECAMT:M1IV$)
6680 LET DISP$(O[2])=STR(INVAMT:M1IV$)
6690 LET LLIST$[PAGE,X1,1]=DISP$,X0=X0+1,X1=X1+1,L=L+1
6700 LET POORDTOT=POORDTOT+ORDAMT,PORECTOT=PORECTOT+RECAMT
6710 LET POINVTOT=POINVTOT+INVAMT
6720 LET RPTORDTOT=RPTORDTOT+ORDAMT,RPTRECTOT=RPTRECTOT+RECAMT
6730 LET RPTINVTOT=RPTINVTOT+INVAMT
6740 IF SAVEINVNUM$<>"" THEN LET INVNUM$=SAVEINVNUM$,SAVEINVNUM$=""
6750 LET POLINES=POLINES+1; REM "Cnt of lines printed for PO; used for total
6790 RETURN 
6800 REM " --- New PO for detail level inquiry
6805 NEWPO: 
6810 DIM DISP$(WIDTH-2)
6820 IF L+1>MAXROW THEN GOSUB 5400
6830 IF FKEY=4 THEN GOTO 6790
6840 LET DISP$(2)=DASHES$(1,8)+" PO # "+PONUM$
6850 LET DISP$(28)="Receiver # "+RECEIVER$
6860 LET DISP$(51)="Received "+RCPTDATE$+" "+DASHES$
6870 LET LLIST$[PAGE,X1,1]=DISP$,X0=X0+1,X1=X1+1,L=L+1
6880 LET PREVPO$=PONUM$
6890 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8070 DEF FNH$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9220 LET O0=0
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to customer maintenance menu"
9910 CALL "SYC.WD",NAME$
9950 RUN "APM.MA"
9999 END

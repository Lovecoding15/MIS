0010 REM "ARU - Conversion Invoice/Finance Charge Update"
0020 REM "Program ARU.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="GLT-04",FILES$[2]="GLT-05"
0200 REM " --- IOLists
0210 ARE02A: IOLIST A0$,A[ALL]
0220 ARM01A: IOLIST *,B1$(1)
0250 ARM10D: IOLIST *,Z0$
0260 ART01A: IOLIST X0$,X[ALL]
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0380 ART43A: IOLIST X4$
0390 ART53A: IOLIST X5$
0400 REM " --- Parameters
0405 DIM P[6],G[4]
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL"+"00",DOM=9800)IOL=GLS01A
0450 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[6]=NUM(P3$(4,1))
0455 FOR X=0 TO 4
0460 LET G[X]=NUM(G2$(X*2+1,2),ERR=0465)
0465 NEXT X
0470 LET G[2]=FNYY_YEAR(G2$(5,2))
0500 REM " --- Initializations"
0510 DIM A[1]
0520 LET H0=132,H=0,L9=50,L=L9+1,ERRORS=0
0540 LET HEADINGS=2,HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0570 LET M0=LEN(M0$),M1=LEN(M1$)
0600 REM " --- Additional File Opens"
0610 IF GL$<>"Y" THEN GOTO 0700
0620 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS>0 THEN GOTO 9900
0640 LET GLT04_DEV=CHANNELS[1],GLT05_DEV=CHANNELS[2]
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0720 CLOSE (7,ERR=0800)
0800 REM " --- Run Report?
0810 LET V4$="Are You Ready To Update The "+N3$
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM " --- File initializations"
0910 CALL "SYC.NB","Updating",M0,COLUMN
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
0990 READ (ARE02_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next ARE-02 record"
1010 LET K$=KEY(ARE02_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 READ (ARE02_DEV)IOL=ARE02A
1040 PRINT @(COLUMN,11),FNF$(A0$(5,P[0]),M0$),
1100 REM " --- Update GL"
1110 IF GL$<>"Y" THEN GOTO 2000
1120 GOSUB 6000
1130 GOSUB 6100
1140 LET ACCOUNT$=Z0$(1,G[4]),WHEN$=A0$(22,3),MEMO$=B1$
1150 LET REF1$=FNF$(A0$(5,P[0]),M0$),REF2$=A0$(11,7),REF3$=""
1160 LET AMOUNT=A[0],UNITS=0
1170 GOSUB GLPOST
1180 LET ACCOUNT$=Z0$(2*G[4]+1,G[4]),AMOUNT=-A[0],UNITS=0
1190 GOSUB GLPOST
2000 REM " --- Update ART-01
2010 DIM X0$(50),X[1]
2020 LET X0$(1)=A0$(1,17)+"00"+A0$(18,14),X[0]=A[0],X[1]=A[1]
2030 WRITE (ART01_DEV,KEY=X0$(1,19))IOL=ART01A
3000 REM " --- Update xrefs"
3010 LET X4$=X0$(1,4)+X0$(11,7)+X0$(5,6)
3020 WRITE (ART43_DEV,KEY=X4$)IOL=ART43A
3030 LET X5$=X0$(1,4)+X0$(24,3)+X0$(11,7)+X0$(5,6)
3040 WRITE (ART53_DEV,KEY=X5$)IOL=ART53A
3900 REM 
3910 REMOVE (ARE02_DEV,KEY=K$)
3990 GOTO 1000
4000 REM " --- All Done"
4010 IF GL$="Y" THEN CALL "GLC.CA",STATUS
4020 CALL "SYC.BB",STATUS
4090 GOTO 9900
6000 REM " --- Find Customer
6020 DIM B1$(30)
6040 LET B1$(1)="Customer Not On File"
6060 FIND (ARM01_DEV,KEY=N0$+A0$(5,6),DOM=6080)IOL=ARM01A
6090 RETURN 
6100 REM " --- Dist Code
6110 DIM Z0$(10*G[4])
6120 FIND (ARM10_DEV,KEY=N0$+"D"+A0$(18,2),DOM=6150)IOL=ARM10D
6190 RETURN 
6900 REM " --- Standard G/L Posting Routine"
6910 GLPOST: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,WHEN$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

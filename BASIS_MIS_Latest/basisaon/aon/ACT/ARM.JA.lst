0010 REM "ARM - Tax Code Maintenance"
0020 REM "Program ARM.JA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0095 PRECISION 4
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="ARM-10"
0135 LET FILES$[3]="ARM-10",FILES$[4]="GLM-01"
0140 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],ARM10_DEV=CHANNELS[2],ARM10_DEV2=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 ARM10B: IOLIST A0$(1),A[ALL]
0220 ARM10B2: IOLIST B0$(1),B[ALL]
0230 GLM01A: IOLIST C0$,C1$(1)
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0405 DIM INFO$[20],P[1],G[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0475 LET G[2]=FNYY_YEAR(G2$(5,2))
0480 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2))
0485 CALL "SYC.VA","OP",INFO$[ALL]
0490 LET GL$=INFO$[9]
0500 REM " --- Initializations"
0510 DIM J0$(60,"0"),A0$(60),A[1],B0$(60),B[1],C1$(35)
0540 LET RECORD$="B",GLPROMPT$="GL Account:",NUMBER=1
0545 IF GL$<>"Y" THEN LET GLPROMPT$=""
0550 LET M8$="-##0.0000%",M8=LEN(M8$),M1=LEN(M1$)
0590 LET K0=1,K9=5,A0$(1,3)=N0$+"B",NUMBER=1
0600 REM " --- Additional File Opens"
0610 IF GL$<>"Y" THEN GOTO 0700
0620 CALL "SYC.DA",1,4,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS>0 THEN GOTO 9900
0640 LET GLM01_DEV=CHANNELS[4]
0700 REM " --- Background"
0720 PRINT 'SB',@(7,3),"Tax Code:",@(4,4),"Description:",@(4,5),"Tax Percent:",@(16-LEN(GLPROMPT$),6),GLPROMPT$,@(0,7),"Freight Taxable:",@(4,8),"Maximum Tax:",@(42+M8-5),"Rate",@(25,20),"Total Tax Rate",
0730 FOR X=1 TO 10
0740 PRINT 'SB',@(4,8+X),"Tax Code ",STR(X:"#0"),":",'SF',
0750 NEXT X
0770 CALL "SYC.WC",1,0,M8,0,42,19,0
0900 REM " --- Position File"
0990 READ (ARM10_DEV,KEY=A0$(1,3),DOM=1000)
1000 REM " --- Tax Code"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(ARM10_DEV,END=1050)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4)
1050 LET V0$="S",V1$="KC",V3$="",V0=2,V1=17,V2=3,V4$="Enter A Valid Tax Code (<F3>=Lookup)"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN GOSUB 6000
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(4)=V$
1200 REM " --- Get record"
1210 READ (ARM10_DEV,KEY=A0$(1,K9),DOM=1400)IOL=ARM10B
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Initialize new record"
1510 DIM A[1]
1550 LET A0$(6)="",A0$(56,1)="N"
1590 GOSUB 5000
2000 REM " --- Description"
2010 LET V4$="Enter The Description For This Tax Code"
2020 LET V0$="S",V1$="E",V2$=A0$(6,20),V3$="",V0=20,V1=17,V2=4,I0=0
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2060 LET A0$(6,20)=V$
2090 GOTO 3900
2200 REM " --- Tax Percent"
2210 LET V4$="Enter The Tax Rate Percentage For This Tax Code"
2220 LET V0$="N",V1$="E",V2$=STR(A[0]),V3$="-##0.0000",V1=17,V2=5
2230 IF A0$(4,2)="  " THEN LET V=0; GOTO 2260
2240 GOSUB 7000
2250 ON V3 GOTO 2260,2200,3900,2200,3900
2260 LET A[0]=V
2270 PRINT @(V1,V2),A[0]:M8$
2280 GOSUB 6600
2285 GOSUB 5100
2290 GOTO 3900
2400 REM " --- GL Account"
2410 IF GL$<>"Y" THEN GOTO 3900
2420 LET V0$="M",V1$="EC",V2$=A0$(46,G[3]),V3$=G1$,V0=G[4],V1=17,V2=6
2430 LET V4$="Enter A Valid GL Account (<F3>=Lookup)"
2435 IF A[0]=0 THEN GOTO 2550
2440 GOSUB 7000
2450 ON V3 GOTO 2500,2400,3900,2460,3900
2460 CALL "SYC.LG",V$
2470 IF V$="" THEN GOTO 2400
2500 REM 
2510 GOSUB 6200
2520 IF STATUS>0 THEN GOTO 2400
2530 PRINT @(V1,V2),FNF$(C0$(3,G[3]),G5$)," ",C1$
2540 LET A0$(46,G[4])=C0$(3)
2545 GOTO 2590
2550 LET A0$(46,G[4])=""
2560 PRINT @(V1,V2),'CL',
2590 GOTO 3900
2600 REM " --- Freight Taxable?"
2610 LET V4$="Is Freight Taxable In This Tax Area (Y/N)?"
2620 LET V0$="Y",V1$="C",V2$=A0$(56,1),V3$="",V0=1,V1=17,V2=7
2630 GOSUB 7000
2640 ON V3 GOTO 2650,2600,3900,2600,3900
2650 LET A0$(56,1)=V$
2690 GOTO 3900
2800 REM " --- Maximum Tax Amount"
2810 LET V4$="Enter The Maximum Tax Amount For This Tax Code Or Zero If No Limit Applies"
2820 LET V0$="N",V1$="E",V2$=STR(A[1]),V3$=M1$,V1=17,V2=8
2830 IF A0$(4,2)="  " THEN LET V=0; GOTO 2860
2840 GOSUB 7000
2850 ON V3 GOTO 2860,2800,3900,2800,3900
2860 LET A[1]=V
2870 PRINT @(V1,V2),A[1]:M1$
2890 GOTO 3900
3000 REM " --- Additional tax codes"
3010 LET X=I0-4,V0=2,V1=17,V2=X+8,V4$="Enter A Valid Tax Code (<F3>=Lookup)"
3020 LET V0$="S",V1$="KEC",V2$=A0$(26+2*(X-1),2),V3$="",V$=""
3030 IF FNP$(A0$(4,2))="" THEN GOTO 3100
3040 GOSUB 7000
3050 ON V3 GOTO 3070,3000,3900,3060,3900
3060 GOSUB 6000
3065 IF V$="" THEN GOTO 3000
3070 IF FNP$(V$)="" THEN GOTO 3100
3075 GOSUB 6400
3080 IF STATUS>0 THEN GOTO 3000
3090 PRINT @(V1,V2),B0$(4,2)," ",B0$(6,20),@(42),B[0]:M8$,
3100 REM " --- Recalculate total"
3110 LET A0$(26+2*(X-1),2)=V$
3120 IF FNP$(V$)="" THEN PRINT @(V1,V2),'CL',
3130 GOSUB 6600
3140 GOSUB 5100
3190 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2200,2400,2600,2800,3000,3000,3000,3000,3000,3000,3000,3000,3000,3000,4000
4000 REM " --- Correct?"
4005 GOSUB 6600
4010 GOSUB 5100
4015 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="KC",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4110 WRITE (ARM10_DEV,KEY=A0$(1,K9))IOL=ARM10B
4190 GOTO 1000
4200 REM " --- Delete"
4210 REMOVE (ARM10_DEV,KEY=A0$(1,K9),DOM=4220)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4900,9900
4900 REM " --- Report overlay"
4990 RUN "ARM.JB"
5000 REM " --- Display record"
5010 LET TOTAL=A[0],ACCOUNT$="",V$=A0$(46,G[4])
5020 IF GL$="Y" THEN GOSUB 6200
5030 PRINT @(17,4),A0$(6,20),@(17,5),A[0]:M8$,@(17,6),ACCOUNT$," ",C1$,@(17,7),A0$(56,1),@(17,8),A[1]:M1$,
5040 FOR X=1 TO 10
5045 LET V$=A0$(26+2*(X-1),2)
5050 IF FNP$(V$)="" THEN GOTO 5070
5055 GOSUB 6400
5060 PRINT @(17,X+8),B0$(4,2)," ",B0$(6,20),@(42),B[0]:M8$,
5065 LET TOTAL=TOTAL+B[0]
5070 NEXT X
5100 REM " --- Display total"
5110 PRINT @(42,20),TOTAL:M8$
5190 RETURN 
6000 REM " --- Code Lookup"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
6050 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
6060 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6070 LET V$="SKIP",ROW=V2+2
6080 CALL "SYC.LK",ARM10_DEV2,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
6200 REM " --- Retrieve GL account"
6210 DIM C1$(35)
6220 LET STATUS=1,C0$=N0$+V$
6230 FIND (GLM01_DEV,KEY=C0$,DOM=6290)IOL=GLM01A
6240 LET ACCOUNT$=FNF$(C0$(3,G[3]),G5$),STATUS=0
6290 RETURN 
6400 REM " --- Retrieve additional tax code"
6410 DIM B0$(60),B[1]
6420 LET STATUS=1,B0$(1)=N0$+"B"+V$,B0$(7)="(Not On File)"
6430 FIND (ARM10_DEV2,KEY=B0$(1,K9),DOM=6490)IOL=ARM10B2
6440 LET STATUS=0
6490 RETURN 
6600 REM " --- Accumulate total percentage"
6610 LET TOTAL=A[0]
6620 FOR X=1 TO 10
6630 LET V$=A0$(26+2*(X-1),2)
6640 IF FNP$(V$)="" THEN GOTO 6680
6650 GOSUB 6400
6660 LET TOTAL=TOTAL+B[0]
6680 NEXT X
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

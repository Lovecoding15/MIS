0010 REM "APM - Vendor Maintenance (Replenishment)"
0020 REM "Program APM.MJ"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0230 APM06A: IOLIST C0$,C1$(1),C[ALL]
0260 IVM10F: IOLIST X0$,X0
0500 REM " --- Init Data
0520 DIM C0$(11),C1$(22),C[11]
0540 LET C0$(1)=A0$
0700 REM " --- Background
0720 PRINT @(0,L0),'CE','SB',@(12,10),"Replenishment Parameters Maintenance",
0740 PRINT @(10,11),"Buyer Code: ",@(3,12),"Buyer Description:",
0745 PRINT @(2,14),"Purchasing Address: ",@(3,15),"Review Cycle Days:",@(4,16),"Next Review Date:",@(3,17),"Prior Review Date:",@(3,18),"Last Receipt Date:"
0750 PRINT @(7,19),"YTD Receipt $:",@(2,20),"YTD Receipt Weight:",@(3,21),"YTD Receipt Units:",
0760 PRINT @(33,14),"Purchase Target Type:",@(45,15),"Target $: ",@(40,16),"Target Weight: ",@(41,17),"Target Units: "
0790 PRINT 'SF',
0900 REM " --- Init File
0920 READ (APM06_DEV,KEY=A0$,DOM=0930)
1000 REM " --- Key
1020 LET V2$="",V2$=KEY(APM06_DEV,END=1050)
1030 IF V2$(1,8)<>A0$ THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(9,3)
1050 LET V0$="S",V1$="CKE",V4$="Enter a Valid Buyer Code (<F3>=Lookup/<F4>=End)",V0=3,V1=22,V2=11,O9=0,HISTORY$=""
1060 GOSUB 7000
1065 IF V3=3 THEN GOSUB 5200
1070 IF V3=4 THEN RUN "APM.MA"
1080 LET V$=V$+"   ",V$=V$(1,3)
1090 FIND (IVM10_DEV,KEY=N0$+"F"+V$,DOM=1000)IOL=IVM10F
1100 LET C0$(9)=V$
1120 PRINT @(V1,V2),V$,@(V1,V2+1),X0$(7,20)
1200 REM " --- Find Key
1210 LET O9=0
1220 READ (APM06_DEV,KEY=C0$,DOM=1400)IOL=APM06A
1300 REM " --- Display
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-up
1510 LET C1$(1)=""; DIM C[11]
1520 LET C1$(9,1)="N",C[0]=X0
2000 REM " --- Purchasing Address
2005 EXTRACT (APM06_DEV,KEY=C0$,DOM=2006)IOL=APM06A
2007 GOSUB 5000
2010 LET O9=1
2020 LET V0$="Z",V1$="CK",V2$=C1$(7,2),V3$="",V4$="Enter Selected Purchasing Address Code (<F1>=None/<F3>=Lookup)",V0=2,V1=22,V2=14,I0=0
2030 GOSUB 7000
2035 IF V3=1 THEN LET V$=""; GOTO 2070
2040 IF V3=3 THEN CALL "SYC.LT","P",A0$(3,6),V$
2050 LET V$=V$+"00",V$=V$(1,2)
2055 IF NUM(V$)=0 THEN GOTO 2070
2060 FIND (APM05_DEV,KEY=A0$+V$,DOM=2000)
2070 LET C1$(7,2)=V$
2080 PRINT @(V1,V2),C1$(7,2)
2090 GOTO 3900
2100 REM " --- Review Cycle Days
2120 LET V0$="N",V1$="C",V2$=STR(C[0]),V3$="##0",V4$="Enter Minimum Number Of Days Between Reviews",V0=3,V1=22,V2=15
2140 GOSUB 7000
2150 IF V<0 THEN GOTO 2100
2160 LET C[0]=V
2190 GOTO 3900
2200 REM " --- Last Review Date
2205 GOTO 2290
2220 LET V0$="D",V1$="C",V2$=C1$(1,3),V3$="",V4$="Enter Last Review Date.",V0=3,V1=22,V2=16
2230 GOSUB 7000
2240 LET C1$(1,3)=V$
2290 GOTO 3900
2300 REM " --- Next Review Date
2320 LET V0$="D",V1$="C",V2$=C1$(4,3),V3$="",V4$="Enter The Next Scheduled Review Date.",V0=3,V1=22,V2=16
2330 GOSUB 7000
2340 LET C1$(4,3)=V$
2390 GOTO 3900
2400 REM " --- Purchase Target Type
2420 LET V0$="S",V1$="C",V2$=C1$(9,1),V3$="DWUN",V4$="Enter Target Basis (D=Dollars/W=Weight/U=Units/N=No Target)",V0=1,V1=56,V2=14
2430 GOSUB 7000
2440 LET C1$(9,1)=V$
2490 GOTO 3900
2500 REM " --- Target Dollars
2520 LET V0$="N",V1$="C",V2$=STR(C[1]),V3$=M5$,V4$="Enter Minimum Purchase Order Amount For Best Price.",V0=1,V1=56,V2=15
2530 GOSUB 7000
2540 LET C[1]=V
2590 GOTO 3900
2600 REM " --- Target Weight
2620 LET V0$="N",V1$="C",V2$=STR(C[2]),V3$="###,##0",V4$="Enter Minimum Purchase Order Weight For Best Price.",V0=1,V1=56,V2=16
2630 GOSUB 7000
2640 LET C[2]=V
2690 GOTO 3900
2700 REM " --- Target Units
2720 LET V0$="N",V1$="C",V2$=STR(C[3]),V3$=M6$,V4$="Enter Minimum Purchase Order Units For Best Price.",V0=1,V1=56,V2=17
2730 GOSUB 7000
2740 LET C[3]=V
2790 GOTO 3900
3000 REM " --- YTD Receipt Dollars
3010 IF HISTORY$<>"YES" THEN GOTO 3090
3020 LET V0$="N",V1$="C",V2$=STR(C[4]),V3$=M5$,V4$="Enter Year To Date Purchase Receipts Amount.",V0=1,V1=22,V2=19
3030 GOSUB 7000
3040 LET C[4]=V
3090 GOTO 3900
3100 REM " --- YTD Receipt Weight
3110 IF HISTORY$<>"YES" THEN GOTO 3190
3120 LET V0$="N",V1$="C",V2$=STR(C[5]),V3$=M6$,V4$="Enter Year To Date Purchase Receipts Weight.",V0=1,V1=22,V2=20
3130 GOSUB 7000
3140 LET C[5]=V
3190 GOTO 3900
3200 REM " --- YTD Receipt Units
3210 IF HISTORY$<>"YES" THEN GOTO 3290
3220 LET V0$="N",V1$="C",V2$=STR(C[6]),V3$=M6$,V4$="Enter Year To Date Purchase Receipts Units.",V0=1,V1=22,V2=21
3230 GOSUB 7000
3240 LET C[6]=V
3290 GOTO 3900
3900 REM " --- Next Field
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 IF ABS(I0)>1000 THEN GOTO 3990
3960 ON I0 GOTO 2020,2100,2200,2300,2400,2500,2600,2700,3000,3100,3200,3990
4000 REM " --- Options
4010 LET V0$="S",V1$="KCS",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/##/Restore/Delete)?",V0=7,V1=FNV(V4$),V2=22,I0=0
4020 GOSUB 7000
4024 LET I=INT(NUM(V$,ERR=4027))
4026 IF I>0 THEN GOTO 4800
4028 IF V$="HISTORY" THEN LET HISTORY$="YES",V$="N"
4030 IF V$="DELETE" THEN GOTO 4200
4045 IF V$="R" OR V$="RESTORE" THEN GOTO 1200
4047 IF V$="M" THEN RUN "APM.MA"
4050 ON POS(V$="YN") GOTO 4000,4100,2020
4100 REM " --- Write
4120 WRITE (APM06_DEV,KEY=C0$)IOL=APM06A
4180 GOSUB 6100
4190 GOTO 1000
4200 REM " --- Delete
4220 READ (IVM03_DEV,KEY=N0$+"F"+C0$(9,3)+C0$(3,6),DOM=4230)
4230 LET KEY$=KEY(IVM03_DEV,END=4320)
4240 IF POS(N0$+"F"+C0$(9,3)+C0$(3,6)=KEY$)<>1 THEN GOTO 4320
4245 DIM MESSAGE$[1]
4250 LET MESSAGE$[0]="Items are assigned to this Vendor/Buyer. Cannot Delete (<Enter>=Continue)"
4260 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
4290 GOTO 4000
4320 REMOVE (APM06_DEV,KEY=C0$,DOM=4330)
4390 GOTO 0700
4800 REM " --- ##
4820 LET I0=I-2
4830 IF O9=1 THEN GOTO 4890
4840 EXTRACT (APM06_DEV,KEY=C0$,DOM=4850)IOL=APM06A
4890 GOTO 3900
5000 REM " --- Display
5010 PRINT @(22,14),C1$(7,2),@(22,15),C[0]:"##0",@(22,16),FNB$(C1$(4)),@(22,17),FNB$(C1$(1)),@(22,18),FNB$(C1$(10))
5020 PRINT @(56,14),C1$(9,1),@(56,15),C[1]:M5$,@(56,16),C[2]:"###,##0",@(56,17),C[3]:M6$
5040 PRINT @(22,19),C[4]:M5$,@(22,20),C[5]:M6$,@(22,21),C[6]:M6$
5090 RETURN 
5200 REM " --- Buyer Code Lookup
5210 DIM DESCRIPTION$[1],FIELD[1],POSITION[1],LENGTH[1]
5220 LET NUMBER=1,COLUMN=79,ROW=V2+2,LTITLE$="Buyer Code Lookup"
5230 LET DESCRIPTION$[0]="Code",FIELD[0]=1
5240 LET POSITION[0]=4,LENGTH[0]=3
5250 LET DESCRIPTION$[1]="Description",FIELD[1]=1
5260 LET POSITION[1]=7,LENGTH[1]=20
5270 CALL "SYC.LK",IVM10_DEV,"F",NUMBER,LTITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
5290 RETURN 
6100 REM " --- Clear Lines
6120 FOR X=21 TO 11 STEP -1
6130 IF X>13 AND X<18 THEN PRINT @(22,X),J1$(1,11),@(56,X), ELSE PRINT @(22,X),
6140 PRINT 'CL',
6160 NEXT X
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 RUN "APM.MA"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

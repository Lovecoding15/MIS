0010 REM "APR - FORM 1099 PRINTING 2"
0020 REM "Program APR.JB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " ----- OPEN/LOCK FILES"
0200 REM " ----- IOLISTS"
0210 APM01A: IOLIST A0$,A1$
0220 APM02A: IOLIST B0$,B1$,B2$,B[0],B[1],B[2],B[3],B[4],B[5],B[6],B[7],B[8],B[9],B[10],B[11],B[12],B[13]
0500 REM " ----- INIT DATA"
0700 REM " ----- BACKGROUND"
0800 REM " ----- TEST PATTERN"
0804 CALL "SYC.YN",0,"Do You Wish To Print A Test Pattern",4,V$,V3
0811 PRINT @(0,5),'CL',
0820 IF V3=4 THEN GOTO 9900
0840 ON POS(V$="YN") GOTO 0800,0850,1000
0850 DIM A1$(200,"X"),A0$(8,"9")
0860 LET T0=99999.99,A1$(160,15)="99-9999999999",A1$(103,9)="999999999"
0900 GOSUB 5000
0990 GOTO 0800
1000 REM " ----- INIT READ"
1010 PRINT 'SB',@(30,5),"Printing:",'SF',
1020 READ (APM01_DEV,KEY=N0$,DOM=1030)
1100 REM " ----- MAIN READ"
1120 LET K$=KEY(APM01_DEV,END=4000)
1130 IF K$(1,2)<>N0$ THEN GOTO 4000
1140 READ (APM01_DEV)IOL=APM01A
1160 IF A1$(175,1)<>"Y" THEN GOTO 1100
1200 REM " ----- ACCUMULATE"
1210 READ (APM02_DEV,KEY=A0$,DOM=1220)
1220 LET T0=0
1300 REM " ----- GET VENDOR DETAIL"
1310 LET K$=KEY(APM02_DEV,END=2000)
1320 IF K$(1,8)<>A0$ THEN GOTO 2000
1340 READ (APM02_DEV)IOL=APM02A
1360 LET T0=T0+B[11]
1390 GOTO 1300
2000 REM " ----- PRINT"
2005 IF T0<P9 THEN GOTO 1100
2010 IF F0$(13,1)<>"T" THEN PRINT @(40,5),FNF$(A0$(3,P[0]),M0$),
2020 IF POS(" "<>A1$(79,24))=0 THEN LET A1$(79,24+P[2])=A1$(103,P[2])
2040 GOSUB 5000
2090 GOTO 1100
4000 REM " ----- END"
4100 GOTO 9900
5000 REM " ----- PRINT FORM"
5020 PRINT (7)@(H0+7),R0$(1,30),'LF',@(H0+7),R0$(31,24),'LF',@(H0+7),R0$(55,24),'LF',@(H0+7),R0$(79,24)
5040 PRINT (7)'LF','LF',@(H0+7),R1$,@(H0+24),A1$(160,15)
5060 PRINT (7)'LF',@(H0+7),A1$(1,30),@(H0+55),T0:M1$
5070 PRINT (7)'LF','LF',@(H0+7),A1$(31,24)
5080 LET V0$=A1$(79,24),V1$=A1$(55,24)
5090 GOSUB 6000
5100 LET V0$=A1$(103,5),V1$=V$
5110 GOSUB 6000
5120 LET X$=V$
5130 PRINT (7)'LF',@(H0+7),X$
5140 PRINT (7)'LF',@(H0+7),FNF$(A0$(3,P[0]),M0$)
5150 FOR X=1 TO 6
5160 PRINT (7)'LF',
5170 NEXT X
5190 RETURN 
6000 REM " ----- COMBINE STINGS"
6020 LET V$=V1$
6030 IF V$="" THEN GOTO 6080
6040 FOR V=LEN(V$) TO 1 STEP -1
6050 IF V$(V,1)<>" " THEN EXITTO 6070
6060 NEXT V
6070 IF V>1 THEN LET V$=V$(1,V)
6080 IF V$<>"" THEN LET V$=V$+" "
6085 LET V$=V$+V0$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- FUNCTIONS"
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

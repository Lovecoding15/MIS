0010 REM "SYM - File Definition Listing (Report)"
0020 REM "Program SYM.FB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLists
0210 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0220 DDM03A: IOLIST B0$(1),B1$(1),B[ALL]
0500 REM " --- Initializations
0510 DIM A0$(6),A1$(77),A2$(30),A[2],B0$(7),B1$(64),B[2],MILESTONE$(43)
0520 LET ELEMENTS=0,L9=59,L=L9+1,H0=132,PAGE=0,HEADINGS=3,VERSION$="6.0.0"
0550 DIM DASHES$(H0,"-"),HEADINGS$[HEADINGS],TYPES$[8]
0560 LET TYPES$[1]="Serial",TYPES$[2]="Direct",TYPES$[3]="Global"
0570 LET TYPES$[4]="Index",TYPES$[5]="MKeyed",TYPES$[6]="Ref"
0580 LET TYPES$[7]="Sort",TYPES$[8]="Text"
0590 LET VERSION$=STBL("!VERSION",ERR=0600)
0600 LET H1$="ADD+ON Software - Version "+VERSION$,H2$=N4$,CLOCK$=""
0610 LET H4$="From ",X$="All Revisions",DH$="Description"
0615 IF MAPPING$="Y" THEN LET DH$=DH$+"/Mapping Info"
0620 IF REVISION$<>"" THEN LET X$="Revised On or After "+FNB6$(REVISION$)
0630 IF BEGFILE$<>"" THEN LET H4$=H4$+BEGFILE$ ELSE LET H4$=H4$+"First File Name"
0640 LET X$=X$+" Sorted By ",H4$=H4$+" Through "
0650 IF ENDFILE$<>"" THEN LET H4$=H4$+ENDFILE$ ELSE LET H4$=H4$+"Last File Name"
0660 IF SORTBY$="A" THEN LET X$=X$+"Alias" ELSE LET X$=X$+"Filename"
0670 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$+" "+H4$,HEADINGS$[2]=X$
0700 REM " --- Background
0720 CALL "SYC.NB","Printing",43,COLUMN
0900 REM " --- Position file
0910 IF SORTBY$="F" THEN GOTO 0950
0920 READ (DDM07_DEV,KEY=BEGFILE$,DOM=1000)
0930 GOTO 1000
0950 READ (SYM03_DEV,KEY=BEGFILE$,DOM=1000)IOL=SYM03A
0960 LET K$=A0$
0990 GOTO 1200
1000 REM " --- Get next sort record"
1010 IF SORTBY$="F" THEN GOTO 1100
1020 LET K$=KEY(DDM07_DEV,END=4000)
1030 IF ENDFILE$<>"" THEN IF K$(1,12)>ENDFILE$ THEN GOTO 4000
1040 READ (DDM07_DEV)
1060 LET ALIAS$=K$(1,12),K$=K$(13)
1090 GOTO 1200
1100 REM " --- Get next file master key"
1110 LET K$=KEY(SYM03_DEV,END=4000)
1120 IF ENDFILE$<>"" THEN IF K$(1,6)>ENDFILE$ THEN GOTO 4000
1200 REM " --- Read file master"
1210 READ (SYM03_DEV,KEY=K$)IOL=SYM03A
1220 IF SORTBY$="F" THEN GOTO 1300
1230 IF A1$(40,12)<>ALIAS$ THEN GOTO 1000
1300 REM " --- Range/Edit Tests
1310 IF FILETYPE$<>"" THEN IF POS(A1$(37,1)=FILETYPE$)=0 THEN GOTO 3900
1320 IF REVISION$<>"" THEN IF A1$(31,6)<REVISION$ THEN GOTO 3900
1350 LET MILESTONE$(1)=A0$+" "+A1$(1,30)
1360 IF SORTBY$="A" THEN LET MILESTONE$(1)=A1$(40,12)+" "+A1$(1,30)
1370 PRINT @(COLUMN,11),MILESTONE$
1500 REM " --- Format fields for file line"
1510 LET X$=A1$(37,1),X=POS(X$="CDGIMRST"),MAPINFO$=""
1520 LET EXPORT$="No",PROTECT$="No",RECS$=STR(A[1]:"###,###")
1530 IF X>0 THEN LET X$=TYPES$[X]
1540 IF A1$(38,1)="Y" THEN LET PROTECT$="Yes"
1550 IF A1$(39,1)="Y" THEN LET EXPORT$="Yes"
1560 IF A1$(37,1)="M" THEN IF A[1]=0 THEN LET RECS$="Dynamic"
1570 IF MAPPING$="Y" THEN LET MAPINFO$=A2$(1,30)
1600 REM " --- Print file information"
1610 IF L+2>L9 THEN GOSUB 5000
1620 PRINT (7)A0$,@(8),A1$(1,30),@(40),A1$(40,12),@(54),EXPORT$,@(61),A1$(52,1),"   ",A1$(53,6),@(73),A1$(59,1),"  ",A1$(60,6),@(85),PROTECT$,@(93),X$,@(101),A[0]:"####",@(107),A[2]:"##,###",@(115),RECS$,@(124),FNB6$(A1$(31,6))
1630 PRINT (7)@(8),MAPINFO$
1640 LET L=L+2,ELEMENTS=ELEMENTS+1
1900 REM " --- Print record information?"
1910 IF RECORDS$<>"Y" THEN GOTO 3900
1990 READ (DDM03_DEV,KEY=A0$,DOM=2000)
2000 REM " --- Read next record for this file"
2010 LET K$=KEY(DDM03_DEV,END=3000)
2020 IF POS(A0$=K$)<>1 THEN GOTO 3000
2030 READ (DDM03_DEV)IOL=DDM03A
2200 REM " --- Print record line"
2210 IF L+1>L9 THEN GOSUB 5000
2220 PRINT (7)@(6),B0$(7,1),@(8),B1$(1,30),@(40),B1$(37,12),@(124),FNB6$(B1$(31,6))
2230 LET L=L+1
2900 REM " --- Loop back for next record"
2990 GOTO 2000
3000 REM " --- Done printing records"
3010 IF L+1>L9 THEN GOSUB 5000
3020 PRINT (7)""
3030 LET L=L+1
3900 REM " --- Loop back for next record
3990 GOTO 1000
4000 REM " --- All done
4010 IF L+2>L9 THEN GOSUB 5000
4020 LET X$=STR(ELEMENTS)
4030 IF ELEMENTS=0 THEN LET X$="No"
4040 PRINT (7)""
4050 PRINT (7)X$," File Definitions Listed"
4090 GOTO 9900
5000 REM " --- Print report heading
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,H0,F0$(7,3),CLOCK$,STATUS
5030 IF STATUS THEN GOTO 9900
5040 PRINT (7)DASHES$
5050 PRINT (7)"File",@(61),"Read/Write  Read Only   Write   File    Key   Record  Numb Of  Last"
5060 PRINT (7)"Name",@(8),DH$,@(40),"Alias",@(54),"Export",@(61),"Sec Passwd  Sec Passwd  Protect Type    Size  Length  Records  Revision"
5070 PRINT (7)DASHES$
5080 LET L=L+4
5090 RETURN 
8000 REM " --- Functions
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM 
9950 RUN "SYS.AA"
9999 END

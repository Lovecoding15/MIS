0010 REM "SYC - Data File/Record Lookup"
0020 REM "Program SYC.LD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "ACTION$   : Reserved for future use (input)"
0055 REM "COLUMN    : Input field column (input)"
0060 REM "ROW       : Input field row (input)"
0065 REM "SELECTION$: User selection (returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER ACTION$,COLUMN,ROW,SELECTION$
0100 REM " --- Open Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SYM-03",FILES$[3]="SYM-06"
0140 LET FILES$[4]="DDM-07"
0150 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],SYM03_DEV=CHANNELS[2],SYM06_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 SYM03A: IOLIST A0$(1),A1$(1)
0250 SYM06A: IOLIST *,COMPANY$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM SPEED$(128),INFO$[20]
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0430 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0440 LET N0$=F0$(16,2),SPEEDSEARCH$="N",DD$="N"
0450 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=0470)
0460 LET N0$=SPEED$(1,2),SPEEDSEARCH$="Y"
0470 FIND (SYM06_DEV,KEY=N0$,DOM=9900)IOL=SYM06A
0480 CALL "SYC.VA","DD",INFO$[ALL]
0485 LET DD$=INFO$[20]
0490 IF DD$<>"Y" THEN GOTO 3900
0500 REM " --- Initializations"
0510 DIM A0$(6),A1$(77)
0550 LET WIDTH=68,HEIGHT=12,WIN_X=7,WIN_Y=ROW+1,MAX_ROW=HEIGHT-5
0560 IF WIN_Y+HEIGHT>22 THEN LET WIN_Y=4
0570 LET MAX_ROW=HEIGHT-5,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3,PGM$=PGM(-2)
0600 REM " --- Additional file opens"
0610 CALL "SYC.DA",1,4,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS THEN GOTO 9900
0630 LET DDM07_DEV=CHANNELS[4]
0650 REM " --- Window arrays"
0660 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0670 LET HEADING$(2)="File",HEADING$(9)="Description",HEADING$(40)="File Alias"
0680 LET HEADING$(53)="Key",TITLE$="ADD+ON File Lookup"
0690 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Which search option?"
0810 DIM STRINGS$[10],NUMBERS[10]
0820 LET STRINGS$[0]=PGM$,STRINGS$[1]=M0$,NUMBERS[0]=MAXROW
0830 LET NUMBERS[1]=OWIN_X,NUMBERS[2]=OWIN_Y,NUMBERS[3]=P
0850 CALL "SYC.SM",STRINGS$[ALL],NUMBERS[ALL],FKEY
0860 IF FKEY THEN GOTO 9900
0870 LET PREFIX$=STRINGS$[2],START$=STRINGS$[3],METHOD$=STRINGS$[4]
0880 LET V3$=STRINGS$[5],V0=NUMBERS[4]
0900 REM " --- Detail Window"
0910 CALL "SYC.WB",NAME$,0
0920 LET HEADING$(52)=METHOD$
0925 PRINT @(0,0),'CF','SB','BR',HEADING$,'ER','SF',
0930 IF SPEEDSEARCH$<>"Y" THEN GOTO 1000
0950 REM " --- Display SpeedSearch Company"
0955 DIM X$(WIDTH+SWIDTH)
0960 LET X$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
0965 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
0970 CALL "SYC.WA",0,WIDTH,3,WIN_X,WIN_Y+HEIGHT,"",CNAME$
0975 PRINT @(0,0),'BR',X$,'ER',
0980 CALL "SYC.WB",NAME$,0
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,5]
1020 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$=""
1030 IF PREFIX$="" THEN GOTO 1200
1040 IF PREFIX$="*" THEN GOTO 1300
1100 REM " --- Alias"
1110 READ (DDM07_DEV,KEY=START$,DOM=1400)
1190 GOTO 1400
1200 REM " --- Name"
1220 READ (SYM03_DEV,KEY=START$,DOM=1400)IOL=SYM03A
1230 LET K$=A0$,X$=A0$
1240 GOTO 1900
1300 REM " --- Keyword"
1310 LET START$=FNU$(START$)
1320 READ (SYM03_DEV,KEY="",DOM=1400)
1400 REM " --- Read next record"
1410 IF PREFIX$="" THEN GOTO 1800
1420 IF PREFIX$="*" THEN GOTO 1800
1500 REM " --- Next DDM-07 alias record"
1510 LET K$=KEY(DDM07_DEV,END=3000)
1530 READ (DDM07_DEV)
1550 LET X$=K$(1,12),K$=K$(13,6)
1590 GOTO 1900
1800 REM " --- Next SYM-03 key"
1810 LET K$=KEY(SYM03_DEV,END=3000)
1820 LET X$=K$
1900 REM " --- Read SYM-03 file record"
1905 LET LOCKED=0
1910 READ (SYM03_DEV,KEY=K$,ERR=1950)IOL=SYM03A
1915 IF PREFIX$<>"*" THEN GOTO 2000
1920 LET X$=START$
1925 IF START$="" THEN GOTO 2000
1930 IF POS(START$=FNU$(A1$)) THEN GOTO 2000
1935 IF POS(START$=FNU$(A2$)) THEN GOTO 2000
1940 GOTO 2900
1950 REM " --- Error reading master record"
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1980 LET LOCKED=1
2000 REM " --- Display page?"
2010 IF L<MAX_ROW THEN GOTO 2400
2020 LET XMODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 0800
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2400 REM " --- Lookup window record"
2410 DIM G1$(WIDTH-2)
2415 IF LOCKED THEN LET G1$(2)=K$+" Record In Use"; GOTO 2470
2430 LET G1$(2)=A0$,G1$(9)=A1$(1,30),G1$(40)=A1$(40,12),G1$(53+M0)=X$
2490 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$
2690 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2910 IF LOCKED=0 THEN GOTO 1400
2920 LET LOCKED=0
2930 IF PREFIX$<>"" AND PREFIX$<>"*" THEN GOTO 1400
2940 LET K$=K$(1,LEN(K$)-1)+CHR(ASC(K$(LEN(K$),1))+1)
2950 READ (SYM03_DEV,KEY=K$,DOM=1400,END=3000)IOL=SYM03A
2990 GOTO 1820
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET MESSAGE$[0]="No Files Found (<Enter>=Continue)"
3040 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=INT((WIDTH-(LEN(MESSAGE$[0])+9))/2)
3050 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page"
3510 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
3590 GOTO 4000
3900 REM " --- Data Dictionary is not installed. Use standard lookup"
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,5
6280 LET L=1,X1=1
6290 RETURN 
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 FOR X=1 TO FILES
9915 CLOSE (CHANNELS[X],ERR=9916)
9920 NEXT X
9925 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9930 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9935 IF SELECTION$="END" THEN LET SELECTION$=""
9940 IF SELECTION$="PRIOR" AND SPEEDSEARCH$="N" THEN LET SELECTION$=""
9950 EXIT 
9999 END

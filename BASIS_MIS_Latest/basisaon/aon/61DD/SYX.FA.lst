0010 REM "SYX - Entry File Verification"
0020 REM "Program SYX.FA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETERR 9000
0060 SETESC 9900
0065 ON O0 GOTO 0070,0100
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET SYS01_DEV=UNT
0120 OPEN (SYS01_DEV,ERR=9700)"SYS-01"; REM "v6.0 when O0=0, v5.2 when O0=1
0130 LET SYM03_DEV=UNT
0140 OPEN (SYM03_DEV,ERR=9750)"SYM-03"; REM "v6.0 when O0=0, v5.2 when O0=1
0200 REM " --- IOLIST's"
0210 SYM03A: IOLIST A0$(1),A1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 ON O0 GOTO 0420,0500
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),N1$=F4$,N2$="SY",N3$=F5$
0500 REM " --- Initializations"
0510 DIM A0$(6),A1$(77),B0$(6),B1$(64)
0550 LET L0=7,L9=21,L=L0-1,NUMBER=0,ERRORS=0,SKIP$="",INCLUDE$=""
0560 LET SKIP$=SKIP$+"APE-03APE-13ARE-03ARE-04ARE-13ARE-23ARE-33"
0565 LET SKIP$=SKIP$+"ARE-43GLE-02GLE-04GLE-05MPE-01MPE-02"
0570 LET SKIP$=SKIP$+"PRE-01POE-01POE-02POE-03POE-04POE-07POE-08"
0575 LET SKIP$=SKIP$+"POE-09POE-11POE-12POE-14SHE-01SHE-02WOE-01"
0580 LET SKIP$=SKIP$+"WOE-02WOE-04WOE-06WOE-07WOE-13WOE-15"
0585 LET INCLUDE$=INCLUDE$+"APM-04APT-03APW-01APW-03ARE-07ARE-43ARM-04"
0590 LET INCLUDE$=INCLUDE$+"ART-43ART-53BMM-04BMM-06GLM-04GLT-05IVM-03"
0595 LET INCLUDE$=INCLUDE$+"IVM-08IVT-02IVT-03PRM-03SAM-21SHM-06SHT-04WOM-03"
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,6,0
0750 PRINT 'SB',@(0,3),"File",@(0,5),"File",@(7),"Description",@(39),"Firm(s)",'SF',
0800 REM " --- Continue?"
0810 ON O0 GOTO 0820,0900
0820 LET V4$="Do You Wish Verify Entry File Status"
0830 CALL "SYC.YN",0,V4$,0,V$,V3
0840 IF V$<>"YES" THEN GOTO 9900
0900 REM " --- Position file"
0990 READ (SYM03_DEV,KEY="",DOM=1000)
1000 REM " --- Next SYM-03 key"
1010 LET K$=KEY(SYM03_DEV,END=4000)
1020 IF K$(3,1)<>"E" AND POS(K$=INCLUDE$,6)=0 THEN GOTO 3800
1030 IF POS(K$=SKIP$,6)>0 THEN GOTO 3800
1200 REM " --- Read SYM-03 record"
1210 READ (SYM03_DEV)IOL=SYM03A
1220 IF POS(A1$(37,1)="GR")>0 THEN GOTO 3800
1230 PRINT @(5,3),A0$," ",A1$(1,30),
1500 REM " --- Open file"
1510 LET CHANNEL=UNT
1520 OPEN (CHANNEL,ERR=3900)A0$
1900 REM " --- Any records?"
1910 LET K$=KEY(CHANNEL,END=3000)
1920 IF POS("Z"=K$)=1 THEN GOTO 3000
2000 REM " --- Got some!"
2010 LET L=L+1
2020 IF L>L9 THEN GOTO 2100
2090 GOTO 2200
2100 REM " --- Continue?"
2110 GOSUB 6000
2120 IF V3=4 THEN GOTO 9200
2200 REM " --- Display file"
2210 LET COLUMN=36,NUMBER=NUMBER+1,ERRORS=ERRORS+1
2220 PRINT @(0,L),A0$," ",A1$(1,30),
2290 READ (CHANNEL,KEY="",DOM=2300)
2500 REM " --- Which firms?"
2510 LET K$=KEY(CHANNEL,END=3000)
2520 LET COLUMN=COLUMN+3
2530 IF COLUMN<74 THEN GOTO 2600
2540 PRINT @(74,L),"(More)"
2590 GOTO 3000
2600 REM " --- Print firm"
2610 PRINT @(COLUMN,L),K$(1,2)
2620 READ (CHANNEL,KEY=K$(1,2)+$FF$,DOM=2500)
2690 GOTO 2500
3000 REM " --- Done with file"
3010 CLOSE (CHANNEL,ERR=3020)
3090 GOTO 3900
3800 REM " --- Skip record"
3810 READ (SYM03_DEV)
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF NUMBER>0 THEN GOSUB 6000
4020 IF ERRORS=0 THEN REMOVE (SYS01_DEV,KEY="FOOBAR",DOM=4030)
4090 GOTO 9900
6000 REM " --- Continue?"
6010 LET V4$="Press <Enter> To Continue (<F4>=Exit):"
6020 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
6030 GOSUB 7000
6040 ON V3 GOTO 6050,6000,6000,6000,6090
6050 PRINT @(0,L0),'CE',
6060 LET L=L0,NUMBER=0
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 LET ERRORS=999
9290 ON O0 GOTO 9900,9600
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9600 REM " --- Distribution installation and/or rename problem"
9605 ON O0 GOTO 9900,9610
9610 PRINT @(0,11),'CE',
9620 CALL "SYC.WC",ERR=9630,0,0,80,8,0,11,0
9630 PRINT @(2,12),"If you are upgrading to ADD+ON Software Version 6.0:"
9640 PRINT @(2,14),"Reload the Administrator media and try the installation procedure again."
9650 PRINT @(2,16),"If problems persist:"
9660 PRINT @(2,17),"Call ADD+ON Software Support at (800) 275-6350"
9670 LET V4$="Press <Enter> to continue:"
9680 LET V0$="S",V1$="CF",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
9685 GOSUB 7000
9690 PRINT @(1,20),'CE','SF',"ADD+ON Software Installation Terminated."
9695 RELEASE 
9700 REM " --- Missing SYS-01"
9710 ON O0 GOTO 9000,9720
9720 PRINT @(0,3),'CE',@(1,4),"ERROR: Unable to locate or open the ADD+ON System Control File (SYS-01)."
9730 GOTO 9600
9750 REM " --- Missing SYM-03"
9760 ON O0 GOTO 9000,9770
9770 PRINT @(0,3),'CE',@(1,4),"ERROR: Unable to locate or open the ADD+ON File Definitions File (SYM-03)."
9780 GOTO 9600
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9910 ON O0 GOTO 9920,9950
9920 RUN "SYS.AA"
9950 CLOSE (SYS01_DEV,ERR=9960)
9960 CLOSE (SYM03_DEV,ERR=9970)
9970 RUN O0$,ERR=9980
9980 PRINT @(1,20),'CE','SF',"ADD+ON Software Installation Terminated."
9990 RELEASE 
9999 END

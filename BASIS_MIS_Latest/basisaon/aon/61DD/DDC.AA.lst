0010 REM "DDM - Data Dictionary Maintenance"
0020 REM "Program DDC.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0042 REM "WINDOW$   : YES to perform inside a window (input)"
0044 REM "WIN_X     : Window column location (input)"
0046 REM "WIN_Y     : Window row location (input)"
0048 REM "SELECTION$: Entered data name (returned)"
0050 REM "FILES[n]  : Channel numbers (input)"
0051 REM "            [0]=not used
0052 REM "            [1]=SYS-01
0053 REM "            [2]=not used
0054 REM "            [3]=DDM-01
0055 REM "            [4]=DDM-06
0056 REM "            [5]=DDM-10
0057 REM "            [6]=DDM-20
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER WINDOW$,WIN_X,WIN_Y,SELECTION$,FILES[ALL]
0100 REM " --- Assign Channel Numbers
0110 LET SYS01_DEV=FILES[1]
0120 LET DDM01_DEV=FILES[3],DDM06_DEV=FILES[4]
0130 LET DDM10_DEV=FILES[5],DDM20_DEV=FILES[6]
0200 REM " --- IOLists
0210 DDM01A: IOLIST A0$(1),A1$(1),A[ALL]
0220 DDM10A: IOLIST X0$,X1$,X2$
0230 DDM20A: IOLIST X0$,X1$,X2$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2)
0500 REM " --- Initializations
0510 DIM A0$(12),A1$(105),A[2]
0520 LET M$="##0",OFFSET=4,J9=22,WIDTH=80,LOOKUP=2
0600 LET MASKSYS$="APARBMGLIVPRWO",VALIDTYPES$="ACHMNOPRUV%"
0605 DIM MASKTYPES$[LEN(MASKSYS$)/2],MASKDESC$[LEN(VALIDTYPES$)]
0610 LET MASKTYPES$[INT((POS("AP"=MASKSYS$,2)+1)/2)]="NAUR"
0615 LET MASKTYPES$[INT((POS("AR"=MASKSYS$,2)+1)/2)]="NAUR"
0620 LET MASKTYPES$[INT((POS("BM"=MASKSYS$,2)+1)/2)]="HMO"
0625 LET MASKTYPES$[INT((POS("GL"=MASKSYS$,2)+1)/2)]="NAU%"
0630 LET MASKTYPES$[INT((POS("IV"=MASKSYS$,2)+1)/2)]="AUCPV"
0635 LET MASKTYPES$[INT((POS("PR"=MASKSYS$,2)+1)/2)]="NAUR"
0640 LET MASKTYPES$[INT((POS("WO"=MASKSYS$,2)+1)/2)]="NAUR"
0645 LET MASKDESC$[POS("A"=VALIDTYPES$)]="Amount"
0650 LET MASKDESC$[POS("C"=VALIDTYPES$)]="Cost"
0655 LET MASKDESC$[POS("H"=VALIDTYPES$)]="Hour"
0660 LET MASKDESC$[POS("M"=VALIDTYPES$)]="Material Factor"
0665 LET MASKDESC$[POS("N"=VALIDTYPES$)]=$22$+"Number"+$22$
0670 LET MASKDESC$[POS("O"=VALIDTYPES$)]="Overhead Factor"
0675 LET MASKDESC$[POS("P"=VALIDTYPES$)]="Price"
0680 LET MASKDESC$[POS("R"=VALIDTYPES$)]="Rate"
0685 LET MASKDESC$[POS("U"=VALIDTYPES$)]="Unit"
0690 LET MASKDESC$[POS("V"=VALIDTYPES$)]="conVersion"
0695 LET MASKDESC$[POS("%"=VALIDTYPES$)]="percent"
0700 REM " --- Background
0705 IF WINDOW$<>"YES" THEN GOTO 0750
0710 LET WIDTH=70,HEIGHT=14,TITLE$="Data Dictionary Maintenance"
0720 IF WIN_Y=0 THEN LET WIN_Y=3
0725 IF WIN_Y+HEIGHT>22 THEN LET WIN_Y=22-HEIGHT
0730 IF WIN_X=0 THEN LET WIN_X=1
0735 IF WIN_X+WIDTH>79 THEN LET WIN_X=79-WIDTH
0740 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0745 LET OFFSET=0,J9=HEIGHT-2,LOOKUP=WIN_Y+2
0750 PRINT 'SB',@(11,OFFSET),"Data Name",@(9,OFFSET+1),"Description",@(11,OFFSET+2),"Data Type",@(16,OFFSET+3),"Mask",@(5,OFFSET+4),"Physical Length",@(6,OFFSET+5),"Display Length",@(4,OFFSET+6),"Compression Code",@(6,OFFSET+7),"Expansion Code",@(1,OFFSET+8),"READ/WRITE Security",@(30,OFFSET+8),"Password",@(2,OFFSET+9),"READ ONLY Security",@(30,OFFSET+9),"Password",@(7,OFFSET+10),"Last Revision",'SF',
0900 REM " --- Position file
0990 READ (DDM01_DEV,KEY="",DOM=1000)
1000 REM " --- Data Name
1005 PRINT 'CF',
1010 LET V0$="S",V1$="CK^",V2$="",V3$="",V0=12,V1=21,V2=OFFSET,I0=0
1015 LET V4$="Enter Data Name (<F3>=Lookup/<Enter>=Next Record)"
1020 GOSUB 7000
1025 ON V3 GOTO 1070,1000,1000,1030,9900
1030 GOSUB 6000
1040 IF V$="" THEN GOTO 1000
1070 IF V$="" THEN GOTO 1100
1080 LET A0$(1)=V$
1090 GOTO 1200
1100 REM " --- Get next record
1110 LET K$=KEY(DDM01_DEV,END=0900)
1120 READ (DDM01_DEV)
1130 LET A0$=K$
1200 REM " --- Retrieve record
1210 READ (DDM01_DEV,KEY=A0$,DOM=1400)IOL=DDM01A
1220 FIND RECORD(DDM01_DEV,KEY=A0$)OLDRECORD$
1300 REM " --- Display record
1320 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record
1410 LET V4$="Is This A New Record"
1420 CALL "SYC.YN",1,V4$,1,V$,V3
1440 ON V3 GOTO 1490,1400,1400,1400,9900
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record
1510 DIM A1$(105),A[2]
1520 LET OLDRECORD$=""
1600 REM " --- Description
1610 LET V0$="S",V1$="C",V2$=A1$(1,30),V3$="",V0=30,V1=21,V2=OFFSET+1
1620 LET V4$="Enter The Description Of This Data Element",I0=1
1630 GOSUB 7000
1640 ON V3 GOTO 1650,1600,3900,1600,3900
1650 LET A1$(1,30)=V$
1690 GOTO 3900
1700 REM " --- Data Type?
1710 LET V0$="S",V1$="C^",V2$=A1$(37,1),V3$="ASZMHTDNY",V0=1,V1=21,V2=OFFSET+2
1720 LET V4$="S=Str/Z=Zero/M=Mask/H=Hyph/T=Time/D=Date/N=Num/Y=YN"
1730 GOSUB 7000
1740 ON V3 GOTO 1750,1700,3900,1700,3900
1750 LET A1$(37,1)=V$
1790 GOTO 3900
1800 REM " --- System Source For Mask
1810 LET V0$="S",V1$="C",V2$=A1$(38,2),V3$="",V0=2,V1=21,V2=OFFSET+3
1820 LET V4$="Enter The System ID To Use For Source Of This Mask (<F3>=Lookup)"
1830 GOSUB 7000
1840 ON V3 GOTO 1860,1800,3900,1845,3900
1845 CALL "SYC.LM",V$
1850 IF V$="" THEN GOTO 1800
1855 PRINT @(V1,V2),V$,
1860 LET V$=V$+FILL(2),V$=V$(1,2)
1870 IF POS(V$=FILL(2)+MASKSYS$,2)=0 THEN GOTO 1800
1880 LET A1$(38,2)=V$
1890 GOTO 3900
1900 REM " --- Mask Type
1905 IF POS(A1$(38,2)=MASKSYS$,2)=0 THEN GOTO 1990
1910 LET V0$="S",V1$="C",V2$=A1$(40,1),V3$=MASKTYPES$[INT((POS(A1$(38,2)=MASKSYS$,2)+1)/2)],V4$="",V0=1,V1=25,V2=OFFSET+3
1915 IF POS(A1$(40,1)=V3$)=0 THEN LET A1$(40,1)="",V2$=""
1920 FOR I=1 TO LEN(V3$)
1925 LET V4$=V4$+V3$(I,1)+"="+MASKDESC$[POS(V3$(I,1)=VALIDTYPES$)]+FILL(2)
1930 NEXT I
1935 GOSUB 7000
1940 ON V3 GOTO 1960,1900,3900,1900,3900
1960 LET A1$(40,1)=V$,A1$(41,15)="",MASK$=FNP$(A1$(41,15))
1970 CALL "DDC.MA",N0$,A1$(38,2),A1$(40,1),MASK$,SYS01_DEV
1980 PRINT @(28,OFFSET+3),MASK$,'CL',
1990 GOTO 3900
2000 REM " --- Special Masks
2005 IF FNP$(A1$(38,2))<>"" THEN GOTO 2090
2010 LET A1$(40,1)="",V4$="Enter Special Mask If Needed"
2020 LET V0$="S",V1$="C",V2$=A1$(41,15),V3$="",V0=15,V1=28,V2=OFFSET+3
2025 PRINT @(25,OFFSET+3),A1$(40,1),
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 SETERR 2070
2055 PRINT @(28,OFFSET+3),0:V$,@(28,OFFSET+3),'CL',
2060 GOTO 2080
2070 SETERR 9000
2075 GOTO 2000
2080 LET A1$(41,15)=V$
2085 PRINT @(28,OFFSET+3),A1$(41,15),
2090 GOTO 3900
2100 REM " --- Physical Field Length
2110 LET V0$="N",V1$="CR",V2$=STR(A[0]),V3$=M$,V0=LEN(M$),V1=21,V2=OFFSET+4
2120 LET V4$="Enter The Physical Length Of This Field"
2125 IF A1$(37,1)="Y" THEN LET A[0]=1; GOTO 2180
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2160 LET A[0]=V
2180 PRINT @(V1,V2),'CL',STR(A[0]:M$)
2190 GOTO 3900
2200 REM " --- Display Field Length
2205 IF A[1]=0 THEN LET A[1]=A[0]
2210 LET V0$="N",V1$="CR",V2$=STR(A[1]),V3$=M$,V0=LEN(M$),V1=21,V2=OFFSET+5
2220 LET V4$="Enter The Display Length Of This Field"
2225 IF A1$(37,1)="Y" THEN LET A[1]=1; GOTO 2280
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2260 LET A[1]=V
2280 PRINT @(V1,V2),'CL',STR(A[1]:M$)
2290 GOTO 3900
2300 REM " --- Compression Code
2310 LET V0$="S",V1$="C",V2$=A1$(68,12),V3$="",V0=12,V1=21,V2=OFFSET+6
2320 LET V4$="Enter Data Compression Code If Needed (<F3>=Lookup)"
2330 GOSUB 7000
2340 ON V3 GOTO 2360,2300,3900,2345,3900
2345 GOSUB COMP_LOOKUP
2350 IF V$="" THEN GOTO 2300
2360 LET V$=V$+FILL(12),V$=V$(1,12),X1$=""
2370 IF FNP$(V$)<>"" THEN FIND (DDM10_DEV,KEY=V$,DOM=2300)IOL=DDM10A
2380 LET A1$(68,12)=V$
2385 PRINT @(V1,V2),A1$(68,12),"  ",X1$,'CL',
2390 GOTO 3900
2400 REM " --- Expansion Code
2410 LET V0$="S",V1$="C",V2$=A1$(56,12),V3$="",V0=12,V1=21,V2=OFFSET+7
2420 LET V4$="Enter Data Expansion Code If Needed (<F3>=Lookup)"
2430 GOSUB 7000
2440 ON V3 GOTO 2460,2400,3900,2445,3900
2445 GOSUB EXP_LOOKUP
2450 IF V$="" THEN GOTO 2400
2460 LET V$=V$+FILL(12),V$=V$(1,12),X1$=""
2470 IF FNP$(V$)<>"" THEN FIND (DDM20_DEV,KEY=V$,DOM=2400)IOL=DDM20A
2480 LET A1$(56,12)=V$
2485 PRINT @(V1,V2),A1$(56,12),"  ",X1$,'CL',
2490 GOTO 3900
2500 REM " --- READ/WRITE Security
2505 IF A1$(80,1)=" " THEN LET A1$(80,1)="0"
2510 LET V4$="Enter Minimum Security Level Required For READ/WRITE Permission (0 Through 9)"
2520 LET V0$="Z",V1$="C",V2$=A1$(80,1),V3$="",V0=1,V1=21,V2=OFFSET+8
2530 GOSUB 7000
2540 ON V3 GOTO 2560,2500,3900,2500,3900
2560 IF V$="" THEN LET V$="0"
2565 IF V$<A1$(87,1) THEN GOTO 2500
2570 LET A1$(80,1)=V$
2580 PRINT @(V1,V2),A1$(80,1),
2590 GOTO 3900
2600 REM " --- READ/WRITE Password
2610 LET V4$="Enter An Optional Password For READ/WRITE Permission"
2620 LET V0$="S",V1$="C",V2$=A1$(81,6),V3$="",V0=6,V1=39,V2=OFFSET+8
2630 GOSUB 7000
2640 ON V3 GOTO 2660,2600,3900,2600,3900
2660 LET A1$(81,6)=V$
2690 GOTO 3900
2700 REM " --- READ ONLY Security
2705 IF A1$(87,1)=" " THEN LET A1$(87,1)="0"
2710 LET V4$="Enter Minimum Security Level Required For READ ONLY Permission (0 Through 9)"
2720 LET V0$="Z",V1$="C",V2$=A1$(87,1),V3$="",V0=1,V1=21,V2=OFFSET+9
2730 GOSUB 7000
2740 ON V3 GOTO 2760,2700,3900,2700,3900
2760 IF V$="" THEN LET V$="0"
2765 IF V$>A1$(80,1) THEN GOTO 2700
2770 LET A1$(87,1)=V$
2780 PRINT @(V1,V2),A1$(87,1),
2790 GOTO 3900
2800 REM " --- READ ONLY Password
2810 LET V4$="Enter An Optional Password For READ ONLY Permission"
2820 LET V0$="S",V1$="C",V2$=A1$(88,6),V3$="",V0=6,V1=39,V2=OFFSET+9
2830 GOSUB 7000
2840 ON V3 GOTO 2860,2800,3900,2800,3900
2860 LET A1$(88,6)=V$
2890 GOTO 3900
3900 REM " --- Next field
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1600,1700,1800,1900,2000,2100,2200,2300,2400,2500,2600,2700,2800,3990
3990 LET I0=0
4000 REM " --- Correct?
4010 LET V4$="Is The Above Correct (Y/N/Delete/<F3>=Where Used)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=J9,V1=FNV1(V4$,WIDTH)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4400,9900
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record
4110 WRITE (DDM01_DEV,KEY=A0$)IOL=DDM01A
4120 FIND RECORD(DDM01_DEV,KEY=A0$)NEWRECORD$
4130 IF NEWRECORD$=OLDRECORD$ THEN GOTO 1000
4140 LET A1$(31,6)=F0$(18,6)
4150 WRITE (DDM01_DEV,KEY=A0$)IOL=DDM01A
4190 GOTO 1000
4200 REM " --- Remove record
4210 READ (DDM06_DEV,KEY=A0$,DOM=4220)
4220 LET K$=KEY(DDM06_DEV,END=4250)
4230 IF POS(A0$=K$)=1 THEN GOTO 4300
4250 REMOVE (DDM01_DEV,KEY=A0$,DOM=4260)
4290 GOTO 1000
4300 REM " --- Can't delete!
4310 DIM MESSAGE$[1]
4320 LET MESSAGE$[0]=$22$+FNP$(A0$)+$22$+" Cannot Be Deleted (<Enter>=Continue)"
4330 CALL "SYC.XA",2,MESSAGE$[ALL],0,V2,V1,V$,V3
4390 GOTO 1000
4400 REM " --- Where used inquiry
4410 LET ROW=OFFSET+8
4420 CALL "DDC.WA",A0$,ROW
4490 GOTO 4000
5000 REM " --- Display record
5010 PRINT @(21,OFFSET),A0$,@(21,OFFSET+1),A1$(1,30),@(21,OFFSET+2),A1$(37,1),
5020 LET MASK$=FNP$(A1$(41,15))
5030 CALL "DDC.MA",N0$,A1$(38,2),A1$(40,1),MASK$,SYS01_DEV
5040 PRINT @(21,OFFSET+3),A1$(38,2),@(25,OFFSET+3),A1$(40,1),@(28,OFFSET+3),MASK$,'CL',
5050 PRINT @(21,OFFSET+4),STR(A[0]:M$),@(21,OFFSET+5),STR(A[1]:M$),
5060 IF FNP$(A1$(68,12))="" THEN LET X1$="" ELSE LET X1$="Not On File"
5070 FIND (DDM10_DEV,KEY=A1$(68,12),DOM=5080)IOL=DDM10A
5080 PRINT @(21,OFFSET+6),A1$(68,12),"  ",X1$,
5090 IF FNP$(A1$(56,12))="" THEN LET X1$="" ELSE LET X1$="Not On File"
5100 FIND (DDM20_DEV,KEY=A1$(56,12),DOM=5110)IOL=DDM20A
5110 PRINT @(21,OFFSET+7),A1$(56,12),"  ",X1$,
5120 PRINT @(21,OFFSET+8),A1$(80,1),@(39,OFFSET+8),A1$(81,6),
5130 PRINT @(21,OFFSET+9),A1$(87,1),@(39,OFFSET+9),A1$(88,6),
5180 PRINT @(21,OFFSET+10),FNB6$(A1$(31,6)),
5190 RETURN 
6000 REM " --- Dictionary Lookup
6010 LET CHANNEL=DDM01_DEV,ROW=LOOKUP+V2
6020 CALL "DDC.LA",CHANNEL,ROW,V$
6090 RETURN 
6100 REM " --- Compression Code Lookup
6105 COMP_LOOKUP: 
6110 LET CHANNEL=DDM10_DEV,TITLE$="Data Compression Codes"
6140 GOTO 6200
6150 REM " --- Expansion Code Lookup
6155 EXP_LOOKUP: 
6160 LET CHANNEL=DDM20_DEV,TITLE$="Data Expansion Codes"
6200 REM " --- Standard windowed lookup"
6210 LET NUMBER=1,RECORD$=""
6220 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6230 LET DESCRIPTION$[0]="Code",FIELD[0]=1,POSITION[0]=1,LENGTH[0]=12
6240 LET DESCRIPTION$[1]="Description",FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6250 LET ROW=L0+1,COLUMN=79-LENGTH[0]-LENGTH[1]-NUMBER*4,V$="NO COMPANY"
6280 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8080 DEF FNV1(Q$,Q)=FNC(Q$,Q)+LEN(Q$)
8090 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to Caller"
9910 IF FNP$(A0$)<>"" THEN LET SELECTION$=A0$
9940 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9950 EXIT 
9999 END

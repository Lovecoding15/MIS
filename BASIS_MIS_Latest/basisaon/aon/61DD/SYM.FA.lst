0010 REM "SYM - Record Layout Maintenance (Header)"
0020 REM "Program SYM.FA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETERR 9000
0060 SETESC 9000
0065 ON O0 GOTO 0070,1000
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=14
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="SYM-03"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SYM03_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 DDM01A: IOLIST X0$(1),X1$(1),X[ALL]
0220 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0230 DDM03A: IOLIST B0$(1),B1$(1),B[ALL]
0240 DDM04A: IOLIST C0$(1),C1$(1),C[ALL]
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0405 DIM INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="DD",N4$=F5$
0430 CALL "SYC.VA",N2$,INFO$[ALL]
0440 LET DD$=INFO$[20]
0500 REM " --- Initializations"
0510 DIM A0$(6),A1$(77),A2$(30),A[2],B0$(7),B1$(64),B[2],J1$(80),O[2]
0520 DIM C0$(10),C1$(122),C[2],X0$(12),X1$(105),X[2],XREF$(57)
0550 LET L0=13,L9=21,REVISION$=F0$(18,6)
0600 REM " --- Additional file opens"
0610 IF DD$="N" THEN GOTO 0700
0615 LET FILES$[4]="DDM-01",FILES$[5]="DDM-03",FILES$[6]="DDM-04"
0620 LET FILES$[7]="DDM-04",FILES$[8]="DDM-06",FILES$[9]="DDM-12"
0625 LET FILES$[10]="DDM-13",FILES$[11]="DDM-14",FILES$[12]="DDM-10"
0630 LET FILES$[13]="DDM-20",FILES$[14]="DDM-07"
0650 CALL "SYC.DA",1,4,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0655 IF STATUS>0 THEN GOTO 9900
0660 LET DDM01_DEV=CHANNELS[4],DDM03_DEV=CHANNELS[5],DDM04_DEV=CHANNELS[6]
0665 LET DDM04_DEV2=CHANNELS[7],DDM06_DEV=CHANNELS[8],DDM12_DEV=CHANNELS[9]
0670 LET DDM13_DEV=CHANNELS[10],DDM14_DEV=CHANNELS[11],DDM10_DEV=CHANNELS[12]
0675 LET DDM20_DEV=CHANNELS[13],DDM07_DEV=CHANNELS[14]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,L0-4,0
0720 PRINT 'SB',@(4,3),"File Name",@(60),"File Type",@(2,4),"Description",@(61),"Key Size",@(1,5),"Mapping Info",@(56),"Record Length",@(8,6),"Alias",@(30),"Export",@(52),"Number of Records",@(3,7),"Read/Write",@(28),"Password",@(56),"Write Protect",@(4,8),"Read Only",@(28),"Password",@(56),"Last Revision",'SF',
0730 IF DD$="N" THEN GOTO 0800
0740 CALL "SYC.WC",1,0,80,0,0,L0-2,0
0750 PRINT 'SB',@(0,L0-3),"ID",@(37),"Alias",@(56),"Last Revision",'SF',
0900 REM " --- Position file"
0990 READ (SYM03_DEV,KEY="",DOM=1000)
1000 REM " --- File Name"
1005 PRINT 'CF',
1010 LET V2$="",V2$=KEY(SYM03_DEV,END=1020)
1020 LET V0$="S",V1$="CK^",V3$="",V0=6,V1=14,V2=3,I0=0
1030 LET V4$="Enter A Valid File Name (<F3>=Lookup/<Enter>=Next Record)"
1040 GOSUB 7000
1050 ON V3 GOTO 1070,1000,1000,1060,4700
1060 CALL "SYC.LD","",V1,V2,V$
1070 IF V$="" THEN GOTO 1100
1080 LET A0$(1)=V$
1090 GOTO 1200
1100 REM " --- Get next record"
1110 LET K$=KEY(SYM03_DEV,END=0900)
1120 READ (SYM03_DEV)
1130 LET A0$=K$
1200 REM " --- Retrieve record"
1210 READ (SYM03_DEV,KEY=A0$,DOM=1400)IOL=SYM03A
1220 FIND RECORD(SYM03_DEV,KEY=A0$)OLDRECORD$
1230 IF A1$(52,1)=" " THEN LET A1$(52,1)="0"
1240 IF A1$(59,1)=" " THEN LET A1$(59,1)="0"
1250 IF FNP$(A1$(40,12))="" THEN LET A1$(40,12)=A0$(1,3)+"_"
1300 REM " --- Display record"
1320 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,4700
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record"
1510 DIM A1$(77),A2$(30),A[2]
1520 LET OLDRECORD$="",A1$(38,1)="N",A1$(39,1)="Y",A1$(52,1)="0",A1$(59,1)="0"
1600 REM " --- Description"
1610 LET V0$="S",V1$="C",V2$=A1$(1,30),V3$="",V0=30,V1=14,V2=4,I0=1
1620 LET V4$="Enter The Description Of This File"
1630 GOSUB 7000
1640 ON V3 GOTO 1650,1600,3900,1600,3900
1650 LET A1$(1,30)=V$
1690 GOTO 3900
1700 REM " --- Mapping Info"
1710 LET V0$="S",V1$="C",V2$=A2$(1,30),V3$="",V0=30,V1=14,V2=5
1720 LET V4$="Enter Sizing Information To Help When Mapping This Data File"
1730 GOSUB 7000
1740 ON V3 GOTO 1750,1700,3900,1700,3900
1750 LET A2$(1,30)=V$
1790 GOTO 3900
1800 REM " --- Write Protect?"
1810 LET V0$="Y",V1$="C",V2$=A1$(38,1),V3$="",V0=1,V1=70,V2=7
1820 LET V4$="Should This File Be Write-Protected (Y/N)?"
1830 GOSUB 7000
1840 ON V3 GOTO 1850,1800,3900,1800,3900
1850 LET A1$(38,1)=V$
1890 GOTO 3900
1900 REM " --- File Type"
1910 LET V0$="S",V1$="CR",V2$=A1$(37,1),V3$="GCDIMSTR",V0=1,V1=70,V2=3
1920 LET V4$="C=Serial/D=Direct/M=MKeyed/I=Index/R=Reference/S=Sort/T=Text/G=Global Variable"
1930 GOSUB 7000
1940 ON V3 GOTO 1950,1900,3900,1900,3900
1950 IF POS(V$="GCDIMSTR")=0 THEN GOTO 1900
1960 LET A1$(37,1)=V$
1990 GOTO 3900
2000 REM " --- Key Length"
2010 LET V0$="N",V1$="CR",V2$=STR(A[0]),V3$="##",V0=2,V1=70,V2=4
2015 IF POS(A1$(37,1)="CITG")>0 THEN LET A[0]=0; GOTO 2080
2020 LET V4$="Enter The Length Of The Key To This File"
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 IF V<1 THEN GOTO 2000
2060 LET A[0]=V
2080 PRINT @(V1,V2),'CL',STR(A[0])
2090 GOTO 3900
2100 REM " --- Record Length"
2110 LET V0$="N",V1$="CR",V2$=STR(A[2]),V3$="####",V0=4,V1=70,V2=5
2115 IF POS(A1$(37,1)="ST")>0 THEN LET A[2]=0; GOTO 2180
2120 LET V4$="Enter The Record Length For This File"
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2150 IF V<1 THEN GOTO 2100
2160 LET A[2]=V
2180 PRINT @(V1,V2),'CL',STR(A[2])
2190 GOTO 3900
2200 REM " --- Number of Records"
2210 LET V0$="N",V1$="CR",V2$=STR(A[1]),V3$="#####",V0=5,V1=70,V2=6
2215 IF POS(A1$(37,1)="CMTG")>0 THEN LET A[1]=0; GOTO 2280
2220 LET V4$="Enter The Number Of Records To Specify When Defining This File"
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 IF V<0 THEN GOTO 2200
2260 LET A[1]=V
2280 PRINT @(V1,V2),'CL',STR(A[1])
2290 GOTO 3900
2300 REM " --- Alias"
2310 LET V0$="S",V1$="C",V2$=A1$(40,12),V3$="",V0=12,V1=14,V2=6
2320 LET V4$="Enter Reference Name For This File When Using TAOS or ODBC"
2330 GOSUB 7000
2340 ON V3 GOTO 2350,2300,3900,2300,3900
2350 LET A1$(40,12)=V$
2390 GOTO 3900
2400 REM " --- Export?"
2405 IF A1$(39,1)=" " THEN LET A1$(39,1)="Y"
2410 LET V0$="Y",V1$="C",V2$=A1$(39,1),V3$="",V0=1,V1=37,V2=6
2420 LET V4$="Should This File Be Exported To The BASIS Data Dictionary (Y/N)?"
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 LET A1$(39,1)=V$
2490 GOTO 3900
2500 REM " --- READ/WRITE Security Level"
2505 IF A1$(52,1)=" " THEN LET A1$(52,1)="0"
2510 LET V4$="Enter Minimum Security Level Required For READ/WRITE Permission (0 Through 9)"
2520 LET V0$="Z",V1$="C",V2$=A1$(52,1),V3$="",V0=1,V1=14,V2=7
2530 GOSUB 7000
2540 ON V3 GOTO 2560,2500,3900,2500,3900
2560 IF V$="" THEN LET V$="0"
2565 IF V$<A1$(59,1) THEN GOTO 2500
2570 LET A1$(52,1)=V$
2580 PRINT @(V1,V2),A1$(52,1),
2590 GOTO 3900
2600 REM " --- READ/WRITE Password"
2610 LET V4$="Enter An Optional Password For READ/WRITE Permission"
2620 LET V0$="S",V1$="C",V2$=A1$(53,6),V3$="",V0=6,V1=37,V2=7
2630 GOSUB 7000
2640 ON V3 GOTO 2660,2600,3900,2600,3900
2660 LET A1$(53,6)=V$
2690 GOTO 3900
2700 REM " --- READ ONLY Security Level"
2705 IF A1$(59,1)=" " THEN LET A1$(59,1)="0"
2710 LET V4$="Enter Minimum Security Level Required For READ ONLY Permission (0 Through 9)"
2720 LET V0$="Z",V1$="C",V2$=A1$(59,1),V3$="",V0=1,V1=14,V2=8
2730 GOSUB 7000
2740 ON V3 GOTO 2760,2700,3900,2700,3900
2760 IF V$="" THEN LET V$="0"
2765 IF V$>A1$(52,1) THEN GOTO 2700
2770 LET A1$(59,1)=V$
2780 PRINT @(V1,V2),A1$(59,1),
2790 GOTO 3900
2800 REM " --- READ ONLY Password"
2810 LET V4$="Enter An Optional Password For READ ONLY Permission"
2820 LET V0$="S",V1$="C",V2$=A1$(60,6),V3$="",V0=6,V1=37,V2=8
2830 GOSUB 7000
2840 ON V3 GOTO 2860,2800,3900,2800,3900
2860 LET A1$(60,6)=V$
2890 GOTO 3900
3900 REM " --- Next field"
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1600,1700,2300,2400,2500,2600,2700,2800,1900,2000,2100,2200,1800,3990
3990 LET I0=0
4000 REM " --- Correct?"
4005 GOSUB 6000; IF NOTVALID THEN GOTO 3900
4010 LET V4$="Is The Above Information Correct (Y/N/Delete"
4015 IF DD$="Y" THEN LET V4$=V4$+"/Copy/Comment"
4020 LET V0$="S",V1$="^CR",V2$="Y",V3$="",V4$=V4$+")?",V0=7,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4700
4050 IF V$="DELETE" THEN GOTO 4200
4060 IF DD$="Y" AND V$="COPY" THEN GOTO 4600
4070 IF DD$="Y" AND V$="COMMENT" THEN GOTO 4800
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record"
4110 WRITE (SYM03_DEV,KEY=A0$)IOL=SYM03A
4120 FIND RECORD(SYM03_DEV,KEY=A0$)NEWRECORD$
4130 IF NEWRECORD$=OLDRECORD$ THEN GOTO 4180
4140 LET A1$(31,6)=REVISION$
4150 WRITE (SYM03_DEV,KEY=A0$)IOL=SYM03A
4180 IF DD$="Y" THEN GOTO 4900
4190 GOTO 1000
4200 REM " --- Remove record"
4210 IF DD$="N" THEN GOTO 4580
4220 LET V4$="OK To Delete All Record Layouts For This File"
4230 CALL "SYC.YN",0,V4$,0,V$,V3
4240 IF V$<>"YES" THEN GOTO 4000
4300 REM " --- Delete remarks"
4305 READ (DDM12_DEV,KEY=A0$,DOM=4310)
4310 LET K$=KEY(DDM12_DEV,END=4335)
4315 IF POS(A0$=K$)<>1 THEN GOTO 4335
4320 REMOVE (DDM12_DEV,KEY=K$)
4325 GOTO 4310
4335 READ (DDM13_DEV,KEY=A0$,DOM=4340)
4340 LET K$=KEY(DDM13_DEV,END=4365)
4345 IF POS(A0$=K$)<>1 THEN GOTO 4365
4350 REMOVE (DDM13_DEV,KEY=K$)
4355 GOTO 4340
4365 READ (DDM14_DEV,KEY=A0$,DOM=4370)
4370 LET K$=KEY(DDM14_DEV,END=4395)
4375 IF POS(A0$=K$)<>1 THEN GOTO 4395
4380 REMOVE (DDM14_DEV,KEY=K$)
4385 GOTO 4370
4400 REM " --- Delete fields"
4405 READ (DDM04_DEV,KEY=A0$,DOM=4410)
4410 LET K$=KEY(DDM04_DEV,END=4490)
4420 IF POS(A0$=K$)<>1 THEN GOTO 4490
4430 READ (DDM04_DEV,KEY=K$)IOL=DDM04A
4440 LET X0$(1)=C1$(1),X1$(1,30)=C1$(17,30)
4445 LET X1$(37,1)=C1$(53,1),X[0]=C[0]
4450 FIND (DDM01_DEV,KEY=X0$,DOM=4455)IOL=DDM01A
4455 LET LENGTH=X[0]
4460 IF C[1]>0 THEN LET LENGTH=LENGTH*C[1]
4465 LET XREF$(1)=X0$(1,12)+X1$(1,30)+X1$(37,1)+STR(LENGTH:"0000")+C0$
4470 REMOVE (DDM06_DEV,KEY=XREF$,DOM=4475)
4475 REMOVE (DDM04_DEV,KEY=K$)
4480 GOTO 4410
4490 READ (DDM03_DEV,KEY=A0$,DOM=4500)
4500 REM " --- Remove records"
4510 LET K$=KEY(DDM03_DEV,END=4580)
4520 IF POS(A0$=K$)<>1 THEN GOTO 4580
4530 REMOVE (DDM03_DEV,KEY=K$)
4550 GOTO 4500
4580 REMOVE (SYM03_DEV,KEY=A0$,DOM=4590)
4590 GOTO 1000
4600 REM " --- Run copy overlay"
4690 RUN "DDM.BF",ERR=4000
4700 REM " --- Run report overlay"
4710 IF DD$="Y" THEN GOTO 9900
4720 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4730 ON V3 GOTO 4740,4700,4700,4700,9900
4740 ON POS(V$="YN") GOTO 4700,4750,9900
4750 LET BEGFILE$="",ENDFILE$="",REVISION$=""
4760 LET MAPPING$="Y",RECORDS$="N",SORTBY$="F"
4790 RUN "SYM.FB"
4800 REM " --- Run comments overlay"
4810 DIM COMINFO$[2],COMINFO[5]
4820 LET COMINFO$[0]="DDM-12",COMINFO$[1]=A0$
4830 LET COMINFO$[2]="File "+A0$+" "+A1$(1,30)
4840 LET COMINFO[1]=L0-3,COMINFO[3]=3,COMINFO[4]=60,COMINFO[5]=10
4850 CALL "SYC.KA",0,COMINFO$[ALL],COMINFO[ALL]
4890 GOTO 4000
4900 REM " --- Run ribbon overlay"
4910 LET O0=0
4990 RUN "DDM.BB",ERR=4000
5000 REM " --- Display record"
5010 PRINT @(14,3),A0$,@(14,4),A1$(1,30),@(14,5),A2$(1,30),@(70,3),A1$(37,1),@(70,4),STR(A[0]),@(70,5),STR(A[2]),@(14,6),A1$(40,12),@(37,6),A1$(39,1),@(70,6),STR(A[1]),@(14,7),A1$(52,1),@(37,7),A1$(53,6),@(70,7),A1$(38,1),@(14,8),A1$(59,1),@(37,8),A1$(60,6),@(70,8),FNB6$(A1$(31,6))
5090 RETURN 
6000 REM " --- Verify File Type Can Be Exported"
6010 LET NOTVALID=0
6020 IF A1$(39,1)<>"Y" THEN GOTO 6090
6030 IF POS(A1$(37,1)="MDS") THEN GOTO 6090
6040 LET NUMBER=1
6050 DIM MESSAGE$[NUMBER]
6060 LET MESSAGE$[0]="Only MKeyed(M), Direct(D) and Sort(S) files can be exported."
6065 LET MESSAGE$[1]="                <Enter> to Continue"
6070 CALL "SYC.XA",2,MESSAGE$[ALL],NUMBER,-1,-1,V$,V3
6080 LET NOTVALID=1,I0=3
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

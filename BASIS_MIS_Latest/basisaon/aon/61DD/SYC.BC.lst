0010 REM "SYC - Verify That Batched Files Are Empty"
0020 REM "Program SYC.BC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0043 REM "FIRM$       : Firm ID to use
0045 REM "BASENAME$   : Base File Name To Check (Input)"
0050 REM "DESCRIPTION$: File Description (Returned)"
0055 REM "NUMBER      : Optional Batch Number (Input)"
0060 REM "STATUS      : 0=No Error (Returned)"
0065 REM 
0070 REM "If NUMBER=0 then check base file and all open batches"
0073 REM "If NUMBER>0 then check specified batch file only"
0075 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER FIRM$,BASENAME$,DESCRIPTION$,NUMBER,STATUS
0100 REM " --- Open Files"
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]=BASENAME$,FILES$[2]="SYM-03",FILES$[3]="SYM-04"
0130 LET FILES$[4]="SYM-09",FILES$[5]="SYM-59",FILES$[6]="SYM-69"
0140 IF NUMBER=0 THEN GOTO 0150
0145 LET FILES$[1]=BASENAME$+"."+STR(NUMBER:"000"),OPTIONS$[1]="F"
0150 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET ENTRY_DEV=CHANNELS[1],SYM03_DEV=CHANNELS[2],SYM04_DEV=CHANNELS[3]
0180 LET SYM09_DEV=CHANNELS[4],SYM59_DEV=CHANNELS[5],SYM69_DEV=CHANNELS[6]
0200 REM " --- IOLIST's"
0210 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0220 SYM09A: IOLIST B0$(1),B1$(1),B2$(1),B3$,B[ALL]
0230 SYM04A: IOLIST C0$(1),C1$(1),C3$,C4$,C[ALL]
0400 REM " --- Parameters"
0500 REM " --- Initializations"
0510 DIM A0$(6),A1$(77),A2$(30),A[2],B0$(10),B1$(30),B2$(24),B[3]
0520 DIM C0$(2),C1$(64),C[2]
0550 LET STATUS=0,DESCRIPTION$=""
1000 REM " --- Check specified batch file"
1010 IF ENTRY_DEV>0 THEN GOSUB 6000
1020 IF STATUS>0 THEN GOTO 4000
1030 IF NUMBER>0 THEN GOTO 4000
1900 REM " --- Position File/Process sort"
1990 READ (SYM69_DEV,KEY=BASENAME$,DOM=2000)
2000 REM " --- Read next File/Process record"
2010 LET K$=KEY(SYM69_DEV,END=4000)
2020 IF POS(BASENAME$=K$)<>1 THEN GOTO 4000
2030 READ (SYM69_DEV)
2040 LET B0$(1)=K$(7)
2050 FIND (SYM09_DEV,KEY=B0$,DOM=3900)IOL=SYM09A
2060 IF B1$(3,1)<>"Y" THEN GOTO 3900
2070 LET C0$(1)=B1$(1,2)
2080 FIND (SYM04_DEV,KEY=C0$,DOM=3900)IOL=SYM04A
2090 IF C1$(47,1)<>"Y" THEN GOTO 3900
2900 REM " --- Position Process/Batch sort"
2990 READ (SYM59_DEV,KEY=B0$,DOM=3000)
3000 REM " --- Read next Process/batch record"
3010 LET K$=KEY(SYM59_DEV,END=3900)
3020 IF POS(B0$=K$)<>1 THEN GOTO 3900
3030 LET FILES$[1]=BASENAME$+"."+K$(13),OPTIONS$[1]="F"
3040 CLOSE (ENTRY_DEV,ERR=3050)
3050 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
3060 IF STATUS>0 THEN GOTO 9900
3065 LET ENTRY_DEV=CHANNELS[1]
3070 IF ENTRY_DEV>0 THEN GOSUB 6000
3080 IF STATUS>0 THEN GOTO 4000
3090 GOTO 3000
3900 REM " --- Loop back for next File/Process sort"
3990 GOTO 2000
4000 REM " --- All done"
4090 GOTO 9900
6000 REM " --- Check for firm records in file open on ENTRY_DEV"
6010 READ (ENTRY_DEV,KEY=FIRM$,DOM=6020)
6020 LET K$=KEY(ENTRY_DEV,END=6090)
6030 IF POS(FIRM$=K$)<>1 THEN GOTO 6090
6040 LET A0$(1)=BASENAME$,A1$(1)="(Unknown File)",STATUS=999
6050 FIND (SYM03_DEV,KEY=A0$,DOM=6060)IOL=SYM03A
6060 LET DESCRIPTION$=FNP$(A1$(1,30))
6090 RETURN 
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 LET STATUS=MAX(ERR,999)
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9921)
9930 NEXT X
9950 EXIT 
9999 END

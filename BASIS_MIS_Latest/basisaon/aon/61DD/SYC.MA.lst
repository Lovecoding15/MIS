0010 REM "SYC - Create A File"
0020 REM "Program SYC.MA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0041 REM "FILETYPE$: File Type (Input)"
0042 REM "           null = Use file parameters in SYM-03 (except for number"
0043 REM "                  of records when RECORDS is greater than zero)"
0044 REM "           D    = Direct File
0045 REM "           I    = Indexed File"
0046 REM "           S    = Sort File"
0047 REM "           M    = Multi-Keyed File"
0048 REM "           T    = Text File"
0049 REM "           C    = Cereal (Serial) File"
0051 REM "FILENAME$: File Name (Input)"
0052 REM "MODULE$  : Application ID (Input)"
0053 REM "KEYLEN$  : Key Size/Key Definition for MKeyed Files (Input)"
0054 REM "RECORDS  : Number Of Records (Input)"
0055 REM "LENGTH   : Bytes Per Record (Input)"
0056 REM "STATUS   : Zero=No Error/Not Zero=Error (Returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER FILETYPE$,FILENAME$,MODULE$,KEYLEN$,RECORDS,LENGTH,STATUS
0100 REM " --- Open/Lock Files"
0105 LET FILES=2
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYM-03",FILES$[2]="SYM-04"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYM03_DEV=CHANNELS[1],SYM04_DEV=CHANNELS[2]
0200 REM " --- IOLISTs
0210 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0220 SYM04A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1),B[ALL]
0500 REM " --- Initializations"
0510 DIM A0$(6),A1$(77),A2$(30),A[2],B0$(2),B1$(64),B2$(50),B3$(50),B[2]
0550 IF MODULE$="" THEN LET MODULE$=FILENAME$(1,2)
0590 LET STATUS=999
0600 REM " --- Set data path"
0610 FIND (SYM04_DEV,KEY=MODULE$,DOM=9900)IOL=SYM04A
0620 LET PATH$=FNP$(B2$)
0630 IF PATH$(LEN(PATH$),1)<>"/" THEN LET PATH$=PATH$+"/"
0800 REM " --- Get file information if FILETYPE$ not specified"
0810 IF FILETYPE$<>"" THEN GOTO 0900
0820 FIND (SYM03_DEV,KEY=FILENAME$,DOM=9900)IOL=SYM03A
0830 LET FILETYPE$=A1$(37,1),KEYLEN$=STR(A[0]),LENGTH=A[2]
0840 IF RECORDS<1 THEN LET RECORDS=A[1]
0900 REM " --- Branch based on FILETYPE$"
0910 LET KEYLEN=0,KEYLEN=NUM(KEYLEN$,ERR=0920)
0920 IF RECORDS<1 THEN LET RECORDS=100
0990 ON POS(FILETYPE$="DISMTC") GOTO 9900,1000,1200,1400,1600,1800,2000
1000 REM " --- DIRECT"
1010 DIRECT PATH$+FILENAME$,KEYLEN,RECORDS,LENGTH,ERR=9900
1090 GOTO 4000
1200 REM " --- INDEXED"
1210 INDEXED PATH$+FILENAME$,RECORDS,LENGTH,ERR=9900
1290 GOTO 4000
1400 REM " --- SORT"
1410 SORT PATH$+FILENAME$,KEYLEN,RECORDS,ERR=9900
1490 GOTO 4000
1600 REM " --- MKEYED with single key"
1610 LET KEYLEN=NUM(KEYLEN$,ERR=1700)
1620 MKEYED PATH$+FILENAME$,KEYLEN,0,LENGTH,ERR=9900
1690 GOTO 4000
1700 REM " --- MKEYED with multiple keys"
1790 GOTO 4000
1800 REM " --- STRING"
1810 STRING PATH$+FILENAME$,ERR=9900
1890 GOTO 4000
2000 REM " --- SERIAL"
2010 SERIAL PATH$+FILENAME$,ERR=9900
4000 REM " --- All done"
4010 LET STATUS=0
4090 GOTO 9900
8000 REM " --- Functions"
8090 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Exit
9910 CLOSE (SYM03_DEV,ERR=9911)
9920 CLOSE (SYM04_DEV,ERR=9921)
9950 EXIT 
9999 END

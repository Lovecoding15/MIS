0010 REM "DDR - File/Alias Cross-Reference Listing (Report)"
0020 REM "Program DDR.EB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLists
0210 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0220 DDM03A: IOLIST B0$(1),B1$(1)
0500 REM " --- Initializations
0510 DIM A0$(6),A1$(77),A2$(30),A[2],B0$(7),B1$(64),MILESTONE$(43)
0520 LET ELEMENTS=0,L9=59,L=L9+1,H0=80,PAGE=0,HEADINGS=8,VERSION$="6.0.0"
0550 DIM BUF$(H0),HEADINGS$[HEADINGS]
0560 LET VERSION$=STBL("!VERSION",ERR=0570)
0570 LET H1$="ADD+ON Software - Version "+VERSION$,H2$=N4$,CLOCK$=""
0580 LET H4$="From ",HEADINGS$[3]="Exported And Non-Exported Files"
0590 IF EXPORT$="Y" THEN LET HEADINGS$[3]="Exported File(s) Only"
0600 IF EXPORT$="N" THEN LET HEADINGS$[3]="Non-Exported File(s) Only"
0610 IF BEGFILE$<>"" THEN LET H4$=H4$+BEGFILE$ ELSE LET H4$=H4$+"First File Name"
0620 LET H4$=H4$+" Through "
0630 IF ENDFILE$<>"" THEN LET H4$=H4$+ENDFILE$ ELSE LET H4$=H4$+"Last File Name"
0640 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$,HEADINGS$[2]=H4$
0650 LET BUF$(1)="File",BUF$(38)="Record",BUF$(69)="Alias"
0660 IF SORTBY$="A" THEN LET BUF$(1)="Alias",BUF$(15)="File",BUF$(52)="Record"
0670 LET HEADINGS$[5]=FILL(H0,"-"),HEADINGS$[6]=BUF$,HEADINGS$[7]=FILL(H0,"-")
0700 REM " --- Background
0720 CALL "SYC.NB","Printing",43,COLUMN
0900 REM " --- Position file
0910 IF SORTBY$="F" THEN GOTO 0950
0920 READ (DDM07_DEV,KEY=BEGFILE$,DOM=1000)
0930 GOTO 1000
0950 READ (SYM03_DEV,KEY=BEGFILE$,DOM=1000)IOL=SYM03A
0960 LET K$=A0$
0990 GOTO 1200
1000 REM " --- Get next sort record"
1010 IF SORTBY$="F" THEN GOTO 1100
1020 LET K$=KEY(DDM07_DEV,END=4000)
1030 IF ENDFILE$<>"" THEN IF K$(1,12)>ENDFILE$ THEN GOTO 4000
1040 READ (DDM07_DEV)
1060 LET ALIAS$=K$(1,12),K$=K$(13)
1090 GOTO 1200
1100 REM " --- Get next file master key"
1110 LET K$=KEY(SYM03_DEV,END=4000)
1120 IF ENDFILE$<>"" THEN IF K$(1,6)>ENDFILE$ THEN GOTO 4000
1200 REM " --- Read file master"
1210 READ (SYM03_DEV,KEY=K$)IOL=SYM03A
1300 REM " --- Range/Edit Tests
1310 LET MILESTONE$(1)=A0$+" "+A1$(1,30)
1320 IF SORTBY$="A" THEN LET MILESTONE$(1)=ALIAS$+" "+A1$(1,30)
1330 PRINT @(COLUMN,11),MILESTONE$
1340 IF EXPORT$<>"" THEN IF A1$(39,1)<>EXPORT$ THEN GOTO 3900
1900 REM " --- Print each record"
1980 READ (DDM03_DEV,KEY=A0$,DOM=1990)
1990 IF SORTBY$="A" THEN GOTO 3000
2000 REM " --- Read next record for this file"
2010 LET K$=KEY(DDM03_DEV,END=3900)
2020 IF POS(A0$=K$)<>1 THEN GOTO 3900
2030 READ (DDM03_DEV)IOL=DDM03A
2200 REM " --- Print record line"
2210 GOSUB 5200
2900 REM " --- Loop back for next record"
2990 GOTO 2000
3000 REM " --- Search for matching record alias"
3010 LET K$=KEY(DDM03_DEV,END=3900)
3020 IF POS(A0$=K$)<>1 THEN GOTO 3900
3030 READ (DDM03_DEV)IOL=DDM03A
3040 IF B1$(37,12)<>ALIAS$ THEN GOTO 3000
3050 GOSUB 5200
3900 REM " --- Loop back for next record
3990 GOTO 1000
4000 REM " --- All done
4010 IF L+2>L9 THEN GOSUB 5000
4020 LET X$=STR(ELEMENTS)
4030 IF ELEMENTS=0 THEN LET X$="No"
4040 PRINT (7)""
4050 PRINT (7)X$," File/Record Aliases Listed"
4090 GOTO 9900
5000 REM " --- Print report heading
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,H0,F0$(7,3),CLOCK$,STATUS
5030 IF STATUS THEN GOTO 9900
5090 RETURN 
5200 REM " --- Print detail line"
5205 LET FD$=A0$+" "+A1$(1,30),RD$=B0$(7,1)+" "+B1$(1,30),AD$=B1$(37,12)
5210 LET BUF$(1)=FD$,BUF$(37)=" "+RD$,BUF$(68)=" "+AD$
5220 IF SORTBY$="A" THEN LET BUF$(1)=AD$,BUF$(14)=" "+FD$,BUF$(51)=" "+RD$
5230 IF L+1>L9 THEN GOSUB 5000
5240 PRINT (7)BUF$
5250 LET L=L+1
5290 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM 
9950 RUN "SYS.AA"
9999 END

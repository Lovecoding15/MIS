0010 REM "SYX - Entry and Work File Redefinition"
0020 REM "Program SYX.WA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETERR 9000
0060 SETESC 9900
0065 ON O0 GOTO 0070,0100
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET SYS01_DEV=UNT
0120 OPEN (SYS01_DEV,ERR=9700)"SYS-01"; REM "v6.0 when O0=0, v5.2 when O0=1
0130 LET SYM03_DEV=UNT
0140 OPEN (SYM03_DEV,ERR=9750)"SYM-03"; REM "v6.0 when O0=0, v5.2 when O0=1
0200 REM " --- IOLIST's"
0210 SYM03A: IOLIST A0$(1),A1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 MPM01A: IOLIST M0$
0400 REM " --- Parameters"
0410 ON O0 GOTO 0420,0500
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),N1$=F4$,N2$="SY",N3$=F5$
0500 REM " --- Initializations"
0510 DIM A0$(6),A1$(77),B0$(6),B1$(64),M0$(5)
0550 LET L0=7,L9=21,L=L0-1,NUMBER=0,ERRORS=0,SKIP$=""
0560 LET SKIP$=SKIP$+"APE-03APE-13ARE-03ARE-04ARE-13ARE-23ARE-33"
0565 LET SKIP$=SKIP$+"ARE-43GLE-02GLE-04GLE-05MPE-01MPE-02"
0570 LET SKIP$=SKIP$+"PRE-01POE-01POE-02POE-03POE-04POE-07POE-08"
0575 LET SKIP$=SKIP$+"POE-09POE-11POE-12POE-14SHE-01SHE-02WOE-01"
0580 LET SKIP$=SKIP$+"WOE-02WOE-04WOE-06WOE-07WOE-13WOE-15"
0585 LET INCLUDE$=INCLUDE$+"APM-04APT-03APW-01APW-03ARE-07ARE-43ARM-04"
0590 LET INCLUDE$=INCLUDE$+"ART-43ART-53BMM-04BMM-06GLM-04GLT-05IVM-03"
0595 LET INCLUDE$=INCLUDE$+"IVM-08IVT-02IVT-03PRM-03SAM-21SHM-06SHT-04WOM-03"
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,6,0
0750 PRINT 'SB',@(0,3),"File",@(0,5),"File",@(7),"Description",
0800 REM " --- Continue?"
0810 DIM MESSAGE$[2]
0820 LET MESSAGE$[0]="This Task Will Erase And Redefine All Entry And Work Files Used By The"
0830 LET MESSAGE$[1]="System. Any Data Contained In Those Files Will Be Permanently Lost."
0840 LET MESSAGE$[2]="          Press <Enter> To Continue Or <F4> To Exit"
0850 CALL "SYC.XA",2,MESSAGE$[ALL],2,-1,-1,V$,V3
0860 IF V3=4 THEN GOTO 9900
0870 LET V4$="Do You Wish To Erase And Redefine Entry And Work Files"
0880 CALL "SYC.YN",0,V4$,0,V$,V3
0890 IF V$<>"YES" THEN GOTO 9900
0900 REM " --- Position file"
0990 READ (SYM03_DEV,KEY="",DOM=1000)
1000 REM " --- Next SYM-03 key"
1010 LET K$=KEY(SYM03_DEV,END=4000)
1020 IF K$(3,1)<>"E" AND K$(3,1)<>"W" AND POS(K$=INCLUDE$,6)=0 THEN GOTO 3800
1030 IF POS(K$=SKIP$,6)>0 THEN GOTO 3800
1200 REM " --- Read SYM-03 record"
1210 READ (SYM03_DEV)IOL=SYM03A
1220 IF POS(A1$(37,1)="GR")>0 THEN GOTO 3900
1230 IF A0$="MPE-03" THEN GOTO 6200
1240 IF POS(A0$="MPE-04MPE-05",6)>0 THEN GOTO 1000
1250 PRINT @(5,3),A0$," ",A1$(1,30),
2000 REM " --- Got some!"
2010 LET L=L+1
2020 IF L>L9 THEN GOTO 2100
2090 GOTO 2200
2100 REM " --- Continue?"
2110 GOSUB 6000
2120 IF V3=4 THEN GOTO 9200
2200 REM " --- Display file"
2210 LET NUMBER=NUMBER+1
2220 PRINT @(0,L),A0$," ",A1$(1,30),
2300 REM " --- Erase to extinction
2320 ERASE A0$,ERR=2500
2330 GOTO 2320
2500 REM " --- Redefine file using SYC.DA
2510 LET FILES=1
2520 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
2530 LET FILES$[1]=A0$,OPTIONS$[1]="F"
2540 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
2550 IF STATUS<>0 THEN GOTO 9400
3000 REM " --- Done with file"
3010 CLOSE (CHANNELS,ERR=3020)
3020 CLOSE (CHANNELS[1],ERR=3090)
3090 GOTO 3900
3800 REM " --- Skip record"
3810 READ (SYM03_DEV)
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF NUMBER>0 THEN GOSUB 6000
4020 IF ERRORS=0 THEN REMOVE (SYS01_DEV,KEY="FOOBAR",DOM=4090)
4090 GOTO 9900
6000 REM " --- Continue?"
6010 LET V4$="Press <Enter> To Continue (<F4>=Exit):"
6020 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
6030 GOSUB 7000
6040 ON V3 GOTO 6050,6000,6000,6000,6090
6050 PRINT @(0,L0),'CE',
6060 LET L=L0,NUMBER=0
6090 RETURN 
6200 REM " --- Erase Dynamic MRP Files (MP3/MP4/MP5)"
6210 LET MPNAME$="MP3MP4MP5"
6220 LET MPM01_DEV=UNT
6230 OPEN (MPM01_DEV,ERR=6380)"MPM-01"
6240 LET KEYM$=KEY(MPM01_DEV,END=6370)
6250 REM " --- Read Through MPM-01 For Forcast Info "
6260 READ (MPM01_DEV,KEY=KEYM$)IOL=MPM01A
6270 REM " --- Get Firm & Forcast ID "          
6280 LET FILE_NAME$=CVS(M0$,2)
6290 REM " --- Create MP3/4/5 File Names "
6300 FOR MPLOOP=0 TO 2
6310 LET MPFILE$=MPNAME$(MPLOOP*3+1)+FILE_NAME$
6320 PRINT @(5,3),MPFILE$," ","MRP Requirements File         "
6330 REM " --- Erase To Extinction "
6340 ERASE MPFILE$,ERR=6350
6350 NEXT MPLOOP
6360 GOTO 6240
6370 CLOSE (MPM01_DEV)
6380 GOTO 1000
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 LET ERRORS=999
9290 ON O0 GOTO 9900,9600
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9400 REM " --- Redefinition problem
9410 DIM MESSAGE$[1]
9420 LET MESSAGE$[0]="Unable to redefine file "+A0$+"."
9430 CALL "SYC.XA",1,MESSAGE$[ALL],0,22,-1,V$,V3
9490 GOTO 9900
9600 REM " --- Distribution installation and/or rename problem"
9605 ON O0 GOTO 9900,9610
9610 PRINT @(0,11),'CE',
9620 CALL "SYC.WC",ERR=9630,0,0,80,8,0,11,0
9630 PRINT @(2,12),"If you are upgrading to ADD+ON Software Version 6.0:"
9640 PRINT @(2,14),"Reload the Administrator media and try the installation procedure again."
9650 PRINT @(2,16),"If problems persist:"
9660 PRINT @(2,17),"Call ADD+ON Software Support at (800) 275-6350"
9670 LET V4$="Press <Enter> to continue:"
9680 LET V0$="S",V1$="CF",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
9685 GOSUB 7000
9690 PRINT @(1,20),'CE','SF',"ADD+ON Software Installation Terminated."
9695 RELEASE 
9700 REM " --- Missing SYS-01"
9710 ON O0 GOTO 9000,9720
9720 PRINT @(0,3),'CE',@(1,4),"ERROR: Unable to locate or open the ADD+ON System Control File (SYS-01)."
9730 GOTO 9600
9750 REM " --- Missing SYM-03"
9760 ON O0 GOTO 9000,9770
9770 PRINT @(0,3),'CE',@(1,4),"ERROR: Unable to locate or open the ADD+ON File Definitions File (SYM-03)."
9780 GOTO 9600
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9910 ON O0 GOTO 9920,9950
9920 RUN "SYS.AA"
9950 CLOSE (SYS01_DEV,ERR=9960)
9960 CLOSE (SYM03_DEV,ERR=9970)
9970 RUN O0$,ERR=9980
9980 PRINT @(1,20),'CE','SF',"ADD+ON Software Installation Terminated."
9990 RELEASE 
9999 END

0010 REM "DDC - Where-Used Display"
0020 REM "Program DDC.WA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "DATANAME$: Data name for display (input)"
0060 REM "WIN_Y    : Window row location (input)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER DATANAME$,WIN_Y
0100 REM " --- Open/Lock Files
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="DDM-01",FILES$[2]="DDM-04",FILES$[3]="DDM-06"
0130 LET FILES$[4]="SYM-03"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET DDM01_DEV=CHANNELS[1],DDM04_DEV=CHANNELS[2],DDM06_DEV=CHANNELS[3]
0185 LET SYM03_DEV=CHANNELS[4]
0200 REM " --- IOLists
0210 DDM01A: IOLIST A0$(1),A1$(1),A[ALL]
0220 SYM03A: IOLIST B0$(1),B1$(1)
0230 DDM04A: IOLIST C0$(1),C1$(1),C[ALL]
0400 REM " --- Parameters
0405 DIM A0$(12),A1$(105),A[2]
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21,A0$(1)=DATANAME$
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0425 FIND (DDM01_DEV,KEY=A0$,DOM=9800)IOL=DDM01A
0500 REM " --- Initializations
0510 DIM OPTLIST$[2,MAXROW,5],PARAM$[2,MAXROW,3]
0520 DIM B0$(6),B1$(77),C0$(10),C1$(122),C[2]
0540 LET WIDTH=68,HEIGHT=12,WIN_X=6,MAX_ROW=HEIGHT-5,START$=A0$
0550 IF WIN_Y+HEIGHT>22 THEN LET WIN_Y=23-HEIGHT
0600 REM " --- Window Title
0610 DIM TITLE$(WIDTH-6)
0620 LET TITLE$(1)="Where Used Inquiry: "+FNP$(A0$)+" "+FNP$(A1$(1,30))
0630 LET TITLE$=FNP$(TITLE$)
0650 REM " --- Window arrays
0660 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0665 LET HEADING$(2)="File",HEADING$(10)="Description"
0670 LET HEADING$(43)="Record",HEADING$(51)="Field"
0675 LET HEADING$(58)="Rep",HEADING$(63)="Occ"
0690 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0700 REM " --- Background
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
1000 REM " --- Position file
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,1]
1020 LET PAGE=1,L=1,X1=1,X0=0
1200 REM " --- Data Name
1290 READ (DDM06_DEV,KEY=START$,DOM=1400)
1400 REM " --- Get next key
1410 LET K$=KEY(DDM06_DEV,END=3000)
1420 IF K$(1,12)<>A0$ THEN GOTO 3000
1430 READ (DDM06_DEV)
2000 REM " --- Display page?
2010 IF L<MAX_ROW THEN GOTO 2200
2020 LET MODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 1000
2050 IF FKEY=4 THEN GOTO 9900
2200 REM " --- Additional file reads
2210 DIM B0$(6),B1$(77),C0$(10),C1$(122),C[2]
2220 LET B0$(1)=K$(48,6),B1$(1)="(Not On File)"
2230 LET C0$(1)=K$(48,10),C1$(17,30)="(Not On File)"
2240 FIND (SYM03_DEV,KEY=B0$,DOM=2250)IOL=SYM03A
2250 FIND (DDM04_DEV,KEY=C0$,DOM=2900)IOL=DDM04A
2400 REM " --- Lookup window record
2410 DIM G1$(WIDTH-2)
2420 LET G1$(2)=B0$,G1$(10)=B1$(1,30),G1$(43)=C0$(7,1),G1$(52)=C0$(8)
2430 IF C[1]<2 THEN LET C[1]=0
2450 LET G1$(58)=STR(C[1]:"###"),G1$(63)=STR(C[2]:"###")
2490 LET LLIST$[PAGE,X1,1]=G1$,X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record
2990 GOTO 1400
3000 REM " --- End of file
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3035 LET MESSAGE$[0]="This Data Name Is Not Used (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3090 GOTO 4000
3500 REM " --- Display last page
3510 LET MODE=5,SELECTION$="|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 1000
4000 REM " --- All done
4090 GOTO 9900
6200 REM " --- Display page
6210 CALL "SYC.SA",MODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9940 CALL "SYC.WD",NAME$
9950 EXIT 
9999 END

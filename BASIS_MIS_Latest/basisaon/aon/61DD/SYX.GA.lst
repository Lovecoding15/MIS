0010 REM "SYX - Rebuild Direct/Mkeyed File"
0020 REM "Program SYX.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=3
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYM-03"
0130 REM "FILES$[2]="source",FILES$[3]="target"
0140 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET SYM03_DEV=CHANNELS[1]
0200 REM " --- IOLIST's"
0210 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[0],A[1],A[2]
0500 REM " --- Initializations"
0510 DIM A0$(6),A1$(77),A2$(30),A[2]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,7,0
0750 PRINT 'SB',@(29,3),"File Name:",@(34,5),"Path:",@(30,6),"Key Size:",'SF',
0800 REM " --- Options"
0810 LET V0$="S",V1$="CE",V2$=FILENAME$,V3$="",V0=6,V1=40,V2=3
0820 LET V4$="Enter The Name Of The File To Be Rebuilt (<F3>=Lookup)"
0830 GOSUB 7000
0840 ON V3 GOTO 0850,0800,0800,0845,9900
0845 GOSUB 6000
0850 LET A0$(1)=V$
0860 FIND (SYM03_DEV,KEY=A0$,DOM=0800)IOL=SYM03A
0870 CLOSE (SOURCE_DEV,ERR=0880)
0880 LET FILES$[2]=A0$,OPTIONS$[2]="F"
0890 CALL "SYC.DA",1,2,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0895 IF STATUS THEN GOTO 0800
0900 LET SOURCE_DEV=CHANNELS[2]
0905 IF SOURCE_DEV=0 THEN GOTO 0800
0910 CALL "SYC.JA",SOURCE_DEV,SOURCE$,PATH$,TYPE$,KEYLEN,RECORDS,RECLEN,NUMREC,STATUS
0920 IF STATUS>0 THEN GOTO 4900
0930 IF POS(TYPE$="KM")=0 THEN GOTO 4850
0940 PRINT @(V1,V2),'CL',A0$," ",A1$(1,30),@(V1,V2+2),'CL',PATH$+SOURCE$,@(V1,V2+3),'CL',STR(KEYLEN),
0950 REM " --- Correct?"
0960 LET V0$="Y",V1$="C",V2$="N",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
0970 GOSUB 7000
0980 ON V3 GOTO 0990,0950,0950,0950,4000
0990 ON POS(V$="YN") GOTO 0950,1000,0800
1000 REM " --- Make Temporary File"
1010 LET TARGET$="TEMP"+FID(0)
1020 ERASE TARGET$,ERR=1050
1030 GOTO 1020
1050 CALL "SYC.ML",TARGET$,SOURCE$,STATUS
1060 IF STATUS>0 THEN GOTO 4800
1200 REM " --- Open Temporary File"
1210 CLOSE (TARGET_DEV,ERR=1220)
1220 LET FILES$[3]=TARGET$
1230 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
1240 IF STATUS>0 THEN GOTO 4750
1250 LET TARGET_DEV=CHANNELS[3]
1290 CALL "SYC.NB","Building",KEYLEN,COLUMN
2000 REM " --- Copy records from old file to new file"
2010 READ RECORD(SOURCE_DEV,END=3000)R$
2020 LET K$=R$(1,KEYLEN)
2030 PRINT @(COLUMN,11),K$,
2040 WRITE RECORD(TARGET_DEV,KEY=K$)R$
2090 GOTO 2000
3000 REM " --- Rename Temporary File To Source"
3010 CLOSE (SYM03_DEV,ERR=3020)
3020 CLOSE (SOURCE_DEV,ERR=3030)
3030 CLOSE (TARGET_DEV,ERR=3040)
3040 LET NBR=1
3050 DIM FROM$[NBR],TO$[NBR],ERASE$[NBR]
3060 LET FROM$[1]=TARGET$,TO$[1]=SOURCE$,ERASE$[1]="N"
3070 CALL "SYC.RN",FROM$[ALL],TO$[ALL],ERASE$[ALL],NBR,STATUS
3080 IF STATUS>0 THEN GOTO 4700
4000 REM " --- All done"
4090 GOTO 9900
4700 REM " --- Warn Can Rename Temporary File"
4710 LET NUMBER=1
4720 DIM MESSAGE$[NUMBER]
4730 LET MESSAGE$[0]="Cannot rename temporary file after rebuild."
4740 GOTO 4950
4750 REM " --- Warn Can Open Temporary File"
4760 LET NUMBER=1
4770 DIM MESSAGE$[NUMBER]
4780 LET MESSAGE$[0]="Cannot open temporary file for rebuilding."
4790 GOTO 4950
4800 REM " --- Warn Can Make Temporary File"
4810 LET NUMBER=1
4820 DIM MESSAGE$[NUMBER]
4830 LET MESSAGE$[0]="Cannot make temporary file for rebuilding."
4840 GOTO 4950
4850 REM " --- Warn Not Direct/Mkeyed"
4860 LET NUMBER=1
4870 DIM MESSAGE$[NUMBER]
4880 LET MESSAGE$[0]="Must be Direct or Mkeyed file to rebuild."
4890 GOTO 4950
4900 REM " --- Warn Corrupted FID"
4910 LET NUMBER=1
4920 DIM MESSAGE$[NUMBER]
4930 LET MESSAGE$[0]="Corrupted FID.  Not able to rebuild file."
4950 REM " --- Display Warning"
4960 LET MESSAGE$[NUMBER]="               <F4>=Exit"
4970 CALL "SYC.XA",2,MESSAGE$[ALL],NUMBER,22,-1,V$,V3
4990 GOTO 9900
6000 REM " --- File Name Lookup"
6010 LET CHANNEL=SYM03_DEV,RECORD$="",NUMBER=1,TITLE$="ADD+ON System Files"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="File",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=1,LENGTH[0]=6
6050 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6060 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6070 LET V$="NO COMPANY",ROW=V2+2
6080 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

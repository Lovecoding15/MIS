0010 REM "DDX - Rebuild Data Dictionary Cross-Reference File"
0020 REM "Program DDX.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="SYS-01",FILES$[2]="SYM-03",FILES$[3]="DDM-01"
0130 LET FILES$[4]="DDM-03",FILES$[5]="DDM-04"
0160 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1],SYM03_DEV=CHANNELS[2],DDM01_DEV=CHANNELS[3]
0185 LET DDM03_DEV=CHANNELS[4],DDM04_DEV=CHANNELS[5]
0200 REM " --- IOLists
0220 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0230 DDM01A: IOLIST D0$(1),D1$(1),D[ALL]
0240 DDM03A: IOLIST B0$(1),B1$(1),B[ALL]
0250 DDM04A: IOLIST C0$(1),C1$(1),C[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0360 DDM06A: IOLIST X0$(1)
0370 DDM07A: IOLIST X1$(1)
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="DD",N4$=F5$
0500 REM " --- Initializations
0510 DIM A0$(6),A1$(77),A2$(30),A[2],B0$(7),B1$(64),B[2],X0$(57),X1$(18)
0520 DIM C0$(10),C1$(122),C[2],D0$(12),D1$(106),D[2]
0550 LET LAST$=""
0800 REM " --- Options
0810 LET V4$="Do You Wish To Rebuild Data Dictionary Cross-References"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM " --- Clear And Lock Files"
0910 LET FILES$[6]="DDM-06",OPTIONS$[6]="CL"
0915 LET FILES$[7]="DDM-07",OPTIONS$[7]="CL"
0920 CALL "SYC.DA",1,6,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0930 IF STATUS THEN GOTO 9900
0940 LET DDM06_DEV=CHANNELS[6],DDM07_DEV=CHANNELS[7]
0950 REM " --- Position file
0980 CALL "SYC.NB","Processing",6,COLUMN
0990 READ (SYM03_DEV,KEY="",DOM=1000)
1000 REM " --- Read next SYM-03 Record
1010 LET K$=KEY(SYM03_DEV,END=4000)
1020 READ (SYM03_DEV)IOL=SYM03A
1030 PRINT @(COLUMN,11),A0$,
1200 REM " --- Write DDM-07 Alias Xref for file"
1210 IF FNP$(A1$(40,12))="" THEN GOTO 1400
1220 LET X1$(1)=A1$(40,12)+A0$
1230 WRITE (DDM07_DEV,KEY=X1$)IOL=DDM07A
1400 REM " --- Position DDM-03 file"
1490 READ (DDM03_DEV,KEY=A0$,DOM=1500)
1500 REM " --- Read next DDM-03 record"
1510 LET K$=KEY(DDM03_DEV,END=3900)
1520 IF POS(A0$=K$)<>1 THEN GOTO 3900
1530 READ (DDM03_DEV)IOL=DDM03A
1600 REM " --- Write DDM-07 Alias Xref for record"
1610 IF FNP$(B1$(37,12))="" THEN GOTO 1900
1620 LET X1$(1)=B1$(37,12)+B0$
1630 WRITE (DDM07_DEV,KEY=X1$)IOL=DDM07A
1900 REM " --- Position DDM-04 file"
1990 READ (DDM04_DEV,KEY=B0$,DOM=2000)
2000 REM " --- Read next DDM-04 record"
2010 LET K$=KEY(DDM04_DEV,END=2900)
2020 IF POS(B0$=K$)<>1 THEN GOTO 2900
2030 READ (DDM04_DEV)IOL=DDM04A
2200 REM " --- Read dictionary entry
2210 LET D0$(1)=C1$
2220 FIND (DDM01_DEV,KEY=D0$,DOM=2800)IOL=DDM01A
2230 IF FNP$(C1$(17,30))<>"" THEN LET D1$(1,30)=C1$(17,30)
2400 REM " --- Write DDM-06 Xref Record"
2410 LET LENGTH=D[0]
2420 IF C[1]>0 THEN LET LENGTH=LENGTH*C[1]
2430 LET X0$(1)=D0$(1,12)+D1$(1,30)+D1$(37,1)+STR(LENGTH:"0000")+C0$
2440 WRITE (DDM06_DEV,KEY=X0$)IOL=DDM06A
2800 REM " --- Loop back for next DDM-04 record
2890 GOTO 2000
2900 REM " --- Loop back for next DDM-03 record"
2990 GOTO 1500
3900 REM " --- Loop back for next SYM-03 record"
3990 GOTO 1000
4000 REM " --- All done
4090 GOTO 9900
8000 REM " --- Functions
8080 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

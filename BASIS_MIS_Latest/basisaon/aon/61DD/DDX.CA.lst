0010 REM "DDX - Update Data Dictionary Changes"
0020 REM "Program DDX.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=14
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="SYS-01",FILES$[2]="SYM-03"
0130 LET FILES$[3]="DDX-01",FILES$[4]="DDX-03",FILES$[5]="DDX-04"
0135 LET FILES$[6]="DDX-12",FILES$[7]="DDX-13",FILES$[8]="DDX-14"
0140 LET OPTIONS$[3]="F",OPTIONS$[4]="F",OPTIONS$[5]="F"
0145 LET OPTIONS$[6]="F",OPTIONS$[7]="F",OPTIONS$[8]="F"
0150 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SYM03_DEV=CHANNELS[2]
0170 LET DDX01_DEV=CHANNELS[3],DDX03_DEV=CHANNELS[4],DDX04_DEV=CHANNELS[5]
0180 LET DDX12_DEV=CHANNELS[6],DDX13_DEV=CHANNELS[7],DDX14_DEV=CHANNELS[8]
0200 REM " --- IOLists"
0210 DDM01A: IOLIST A0$(1),A1$(1),A[ALL]
0215 DDX01A: IOLIST A0$(1),A1$(1),A[ALL]
0220 SYM03A: IOLIST B0$(1),B1$(1),B2$(1),B[ALL]
0230 DDM03A: IOLIST C0$(1),C1$(1),C[ALL]
0235 DDX03A: IOLIST C0$(1),C1$(1),C[ALL]
0240 DDM04A: IOLIST D0$(1),D1$(1),D[ALL]
0245 DDX04A: IOLIST D0$(1),D1$(1),D[ALL]
0250 DDM12A: IOLIST B10$(1),B11$(1)
0255 DDX12A: IOLIST B10$(1),B11$(1)
0260 DDM13A: IOLIST C10$(1),C11$(1)
0265 DDX13A: IOLIST C10$(1),C11$(1)
0270 DDM14A: IOLIST D10$(1),D11$(1)
0275 DDX14A: IOLIST D10$(1),D11$(1)
0350 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="DD",N4$=F5$
0500 REM " --- Initializations
0510 DIM A0$(12),A1$(105),A[2],B0$(6),B1$(77),B2$(30),B[2],C0$(7),C1$(63),C[2],D0$(10),D1$(122),D[2]
0520 DIM B10$(9),B11$(60),C10$(10),C11$(60),D10$(13),D11$(60)
0540 LET HEADINGS=4,PAGE=0,WIDTH=80,WHEN$=F0$(7,3),L9=59,L=L9+1
0550 DIM HEADINGS$[HEADINGS],DASHES$(WIDTH,"-")
0560 LET VERSION$="6.0.0",M$="##0"
0565 LET VERSION$=STBL("!VERSION",ERR=0570)
0570 LET HEADINGS$[0]="ADD+ON Software - Version "+VERSION$,HEADINGS$[1]=F5$
0600 REM " --- Are There Any Data Dictionary Changes To Update?"
0610 IF DDX01_DEV+DDX03_DEV+DDX12_DEV>0 THEN GOTO 0700
0620 LET NUMBER=1
0630 DIM MESSAGE$[NUMBER]
0640 LET MESSAGE$[0]="There are currently no Data Dictionary changes to update."
0650 LET MESSAGE$[1]="             <Enter> to Continue"
0660 CALL "SYC.XA",0,MESSAGE$[ALL],NUMBER,-1,-1,V$,V3
0690 GOTO 4000
0700 REM " --- Identify Data Dictionary Files With Current Changes"
0710 IF DDX01_DEV THEN LET FILES$[9]="DDM-01",OPTIONS$[9]="L"
0720 IF DDX12_DEV THEN LET FILES$[12]="DDM-12",OPTIONS$[12]="L"
0730 IF DDX03_DEV=0 THEN GOTO 0760
0740 LET FILES$[10]="DDM-03",FILES$[11]="DDM-04",FILES$[13]="DDM-13",FILES$[14]="DDM-14"
0750 LET OPTIONS$[10]="L",OPTIONS$[11]="L",OPTIONS$[13]="L",OPTIONS$[14]="L"
0800 REM " --- Show Data Dictionary Files With Current Changes"
0810 PRINT 'SF',@(5,4),"Changes are available for updating the following Data Dictionary files:",
0820 LET L=5
0830 FOR I=9 TO 14
0840 IF FILES$[I]="" THEN GOTO 0890
0850 LET B0$(1)=FILES$[I],B1$(1)="Unknown File"
0860 READ (SYM03_DEV,KEY=B0$,DOM=0870)IOL=SYM03A
0870 PRINT @(20,L),B0$,"  ",B1$(1,30),
0880 LET L=L+1
0890 NEXT I
1000 REM " --- Print Current Data Dictionary Changes?"
1010 LET V4$="Do You Wish To Print These Data Dictionary Changes"
1020 CALL "SYC.YN",1,V4$,2,V$,V3
1030 ON V3 GOTO 1040,1000,1000,1000,4000
1040 ON POS(V$="YN") GOTO 1000,1100,2000
1100 REM " --- Open Printer"
1110 CALL "SYC.GA",7,1,"","",STATUS
1120 IF STATUS THEN GOTO 9900
1200 REM " --- Print DDM-01(DDX-01) Changes"
1205 IF DDX01_DEV=0 THEN GOTO 1300
1210 LET DLEN=12,LABEL$="Data Dictionary Changes",HEADINGS$[3]=LABEL$+" To DDM-01",PAGE=0,L=L9+1
1215 GOSUB 6900
1220 READ (DDX01_DEV,KEY="",DOM=1230)
1230 READ (DDX01_DEV,END=1300)IOL=DDX01A
1240 PRINT @(COLUMN,11),A0$,
1250 IF L+1>L9 THEN GOSUB 5000; GOSUB 5100
1260 IF FNP$(A1$(31,6))="" THEN LET DATE$="<Delete>" ELSE LET DATE$=FNB6$(A1$(31,6))
1270 PRINT (7)A0$,@(14),A1$(1,30),@(71),DATE$
1280 LET L=L+1
1290 GOTO 1230
1300 REM " --- Print DDM-03(DDX-03) Changes"
1305 IF DDX03_DEV=0 THEN GOTO 1400
1310 LET DLEN=8,LABEL$="Record Layout Changes",HEADINGS$[3]=LABEL$+" To DDM-03, DDM-04, DDM-13 & DDM-14",PAGE=0,L=L9+1
1315 GOSUB 6900
1320 READ (DDX03_DEV,KEY="",DOM=1330)
1330 READ (DDX03_DEV,END=1400)IOL=DDX03A
1335 PRINT @(COLUMN,11),C0$(1,6)," ",C0$(7),
1340 IF L+1>L9 THEN GOSUB 5000; GOSUB 5200
1360 IF FNP$(C1$(31,6))="" THEN LET DATE$="<Delete>" ELSE LET DATE$=FNB6$(C1$(31,6))
1370 PRINT (7)C0$(1,6),@(8),C0$(7,1),@(12),C1$(1,30),@(44),C1$(37,12),@(71),DATE$
1380 LET L=L+1
1390 GOTO 1330
1400 REM " --- Print DDM-12(DDX-12) Changes"
1405 IF DDX12_DEV=0 THEN GOTO 1500
1410 LET DLEN=6,LABEL$="File Comment Changes",HEADINGS$[3]=LABEL$+" To DDM-12",PAGE=0,L=L9+1
1415 GOSUB 6900
1420 LET TRIP$=""
1430 READ (DDX12_DEV,KEY=TRIP$,DOM=1440)
1440 LET DDX12K$=KEY(DDX12_DEV,END=1500),B0$(1)=DDX12K$,TRIP$=B0$+$FFFFFF$
1450 READ (SYM03_DEV,KEY=B0$,DOM=1490)IOL=SYM03A
1455 PRINT @(COLUMN,11),B0$,
1460 IF L+1>L9 THEN GOSUB 5000; GOSUB 5300
1470 PRINT (7)B0$,@(8),B1$(1,30),@(40),B1$(40,12)
1480 LET L=L+1
1490 GOTO 1430
2000 REM " --- Update Current Data Dictionary Changes?"
2010 CLOSE (7,ERR=2020)
2015 PRINT @(0,22),'CE',
2020 LET V4$="Do You Wish To Update These Changes"
2030 CALL "SYC.YN",0,V4$,0,V$,V3
2040 ON V3 GOTO 2050,2000,2000,2000,4000
2050 IF V$<>"YES" THEN GOTO 4000
2100 REM " --- Open And Lock Data Dictionary Files Being Changed"
2110 CALL "SYC.DA",1,9,14,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
2115 IF STATUS THEN GOTO 9900
2120 LET DDM01_DEV=CHANNELS[9],DDM03_DEV=CHANNELS[10],DDM04_DEV=CHANNELS[11]
2130 LET DDM12_DEV=CHANNELS[12],DDM13_DEV=CHANNELS[13],DDM14_DEV=CHANNELS[14]
2190 LET EXIT_CTRL=1; REM "Control abnormal exits
2200 REM " --- Update DDM-01 From DDX-01"
2205 IF DDX01_DEV=0 THEN GOTO 2300
2210 LET DLEN=20,LABEL$="Updating Data Dictionary"
2215 GOSUB 6900
2220 READ (DDX01_DEV,KEY="",DOM=2230)
2230 LET DDX01K$=KEY(DDX01_DEV,END=2300)
2240 READ (DDX01_DEV)IOL=DDX01A
2245 PRINT @(COLUMN,11),"DDM-01: ",A0$,
2270 IF FNP$(A1$(31,6))="" THEN REMOVE (DDM01_DEV,KEY=DDX01K$,DOM=2280)
2280 IF FNP$(A1$(31,6))<>"" THEN WRITE (DDM01_DEV,KEY=DDX01K$)IOL=DDM01A
2290 GOTO 2230
2300 REM " --- Update DDM-03 From DDX-03"
2305 IF DDX03_DEV=0 THEN GOTO 2800
2310 LET DLEN=24,LABEL$="Updating Record Layouts"
2315 GOSUB 6900
2320 READ (DDX03_DEV,KEY="",DOM=2330)
2350 REM " --- Next DDX-03 Record"
2355 NEXTDDX03: 
2360 LET DDX03K$=KEY(DDX03_DEV,END=2800)
2370 READ (DDX03_DEV)IOL=DDX03A
2375 PRINT @(COLUMN,11),"DDM-03: ",C0$(1,6)," ",C0$(7),FILL(8),
2380 GOSUB PURGEOLD
2385 IF FNP$(C1$(31,6))="" THEN GOTO NEXTDDX03
2390 WRITE (DDM03_DEV,KEY=DDX03K$)IOL=DDM03A
2400 REM " --- Update DDM-04 From DDX-04"
2405 IF DDX04_DEV=0 THEN GOTO 2500
2420 READ (DDX04_DEV,KEY=DDX03K$,DOM=2430)
2430 LET DDX04K$=KEY(DDX04_DEV,END=2500)
2440 IF POS(DDX03K$=DDX04K$)<>1 THEN GOTO 2500
2450 READ (DDX04_DEV)IOL=DDX04A
2460 PRINT @(COLUMN,11),"DDM-04: ",D0$(1,6)," ",D0$(7,1)," ",D0$(8),FILL(4),
2480 WRITE (DDM04_DEV,KEY=DDX04K$)IOL=DDM04A
2490 GOTO 2430
2500 REM " --- Update DDM-13 From DDX-13"
2505 IF DDX13_DEV=0 THEN GOTO 2600
2520 READ (DDX13_DEV,KEY=DDX03K$,DOM=2530)
2530 LET DDX13K$=KEY(DDX13_DEV,END=2600)
2540 IF POS(DDX03K$=DDX13K$)<>1 THEN GOTO 2600
2550 READ (DDX13_DEV)IOL=DDX13A
2560 PRINT @(COLUMN,11),"DDM-13: ",C10$(1,6)," ",C10$(7,1)," ",C10$(8),FILL(4),
2580 WRITE (DDM13_DEV,KEY=DDX13K$)IOL=DDM13A
2590 GOTO 2530
2600 REM " --- Update DDM-14 From DDX-14"
2605 IF DDX14_DEV=0 THEN GOTO 2700
2620 READ (DDX14_DEV,KEY=DDX03K$,DOM=2630)
2630 LET DDX14K$=KEY(DDX14_DEV,END=2700)
2640 IF POS(DDX03K$=DDX14K$)<>1 THEN GOTO 2700
2650 READ (DDX14_DEV)IOL=DDX14A
2660 PRINT @(COLUMN,11),"DDM-14: ",D10$(1,6)," ",D10$(7,1)," ",D10$(8,3)," ",D10$(11),
2680 WRITE (DDM14_DEV,KEY=DDX14K$)IOL=DDM14A
2690 GOTO 2630
2700 REM " --- Loop Back For Next DDX-03 Record"
2790 GOTO NEXTDDX03
2800 REM " --- Update DDM-12 From DDX-12"
2805 IF DDX12_DEV=0 THEN GOTO 3000
2810 LET DLEN=14,LABEL$="Updating File Comments",FILE$=""
2815 GOSUB 6900
2820 READ (DDX12_DEV,KEY="",DOM=2830)
2830 LET DDX12K$=KEY(DDX12_DEV,END=3000)
2840 READ (DDX12_DEV)IOL=DDX12A
2850 IF DDX12K$(1,6)=FILE$ THEN GOTO 2950
2860 LET FILE$=DDX12K$(1,6)
2870 PRINT @(COLUMN,11),"DDM-12: ",B10$(1,6),
2900 REM " --- Remove Old DDM-12 Records"
2910 READ (DDM12_DEV,KEY=FILE$,DOM=2920)
2920 LET DDM12K$=KEY(DDM12_DEV,END=2950)
2925 IF POS(FILE$=DDM12K$)<>1 THEN GOTO 2950
2930 REMOVE (DDM12_DEV,KEY=DDM12K$)
2940 GOTO 2920
2950 REM " --- Write New DDM-12 Records"
2960 WRITE (DDM12_DEV,KEY=DDX12K$)IOL=DDM12A
2990 GOTO 2830
3000 REM " --- Erase DDX Update Files"
3010 ERASE "DDX-01",ERR=3020
3015 GOTO 3010
3020 ERASE "DDX-03",ERR=3030
3025 GOTO 3020
3030 ERASE "DDX-04",ERR=3040
3035 GOTO 3030
3040 ERASE "DDX-12",ERR=3050
3045 GOTO 3040
3050 ERASE "DDX-13",ERR=3060
3055 GOTO 3050
3060 ERASE "DDM-14",ERR=3070
3065 GOTO 3060
3500 REM " --- Run Data Dictionary Cross Reference Rebuild?"
3520 RUN "DDX.AA",ERR=3530
4000 REM " --- All done"
4020 GOTO 9900
5000 REM " --- Print Report Header"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS THEN GOTO 9900
5090 RETURN 
5100 REM " --- Print DDM-01(DDX-01) Report Headings"
5120 PRINT (7)"Data Name",@(14),"Description",@(71),"Revised"
5140 PRINT (7)DASHES$
5180 LET L=L+4
5190 RETURN 
5200 REM " --- Print DDM-03(DDX-03) Report Headings"
5210 PRINT (7)"File",@(8),"---------------------Record---------------------"
5220 PRINT (7)"Name",@(8),"ID",@(12),"Description",@(44),"Alias",@(71),"Revised"
5230 PRINT (7)DASHES$
5280 LET L=L+3
5290 RETURN 
5300 REM " --- Print DDM-12(DDX-12) Report Headings"
5310 PRINT (7)"File"
5320 PRINT (7)"Name",@(8),"Description",@(40),"Alias"
5330 PRINT (7)DASHES$
5380 LET L=L+3
5390 RETURN 
6000 REM " --- Purge Old Record Layout Data"
6005 PURGEOLD: 
6010 REMOVE (DDM03_DEV,KEY=DDX03K$,DOM=6020)
6050 READ (DDM04_DEV,KEY=DDX03K$,DOM=6060)
6060 LET DDM04K$=KEY(DDM04_DEV,END=6100)
6070 IF POS(DDX03K$=DDM04K$)<>1 THEN GOTO 6100
6080 REMOVE (DDM04_DEV,KEY=DDM04K$)
6090 GOTO 6060
6100 READ (DDM13_DEV,KEY=DDX03K$,DOM=6110)
6110 LET DDM13K$=KEY(DDM13_DEV,END=6150)
6120 IF POS(DDX03K$=DDM13K$)<>1 THEN GOTO 6150
6130 REMOVE (DDM13_DEV,KEY=DDM13K$)
6140 GOTO 6110
6150 READ (DDM14_DEV,KEY=DDX03K$,DOM=6160)
6160 LET DDM14K$=KEY(DDM14_DEV,END=6195)
6170 IF POS(DDX03K$=DDM14K$)<>1 THEN GOTO 6195
6180 REMOVE (DDM14_DEV,KEY=DDM14K$)
6190 GOTO 6160
6195 RETURN 
6900 REM " --- Update Notification Box"
6920 PRINT @(0,10),'CL',@(0,11),'CL',@(0,12),'CL',
6940 CALL "SYC.NB",LABEL$,DLEN,COLUMN
6990 RETURN 
8000 REM " --- Functions"
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

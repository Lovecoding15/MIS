0010 REM "SYX - File Record Usage"
0020 REM "Program SYX.PA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=4
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="SYM-03",FILES$[3]="SYM-04"
0140 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SYM03_DEV=CHANNELS[2],SYM04_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0220 SYM04A: IOLIST B0$(1),B1$(1)
0270 SYS01T: IOLIST X$,F0$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0450 LET M1$="###,##0",M2$="###,###",M3$="###0.0%"
0460 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$)
0500 REM " --- Initializations"
0510 DIM A0$(6),A1$(77),A2$(30),A[2],B0$(2),B1$(64),H2$(80),HEADINGS$[4],O[7]
0540 LET HEADINGS$[0]="ADD+ON Software File Record Usage Report",OUTPUT$="S"
0550 LET O[7]=70,O[6]=O[7]-M3-1,O[5]=O[6]-M2-1,O[4]=O[5]-M1,O[3]=O[4]-M1
0560 LET O[1]=4,O[2]=11,WHEN$=F0$(7,3),WIDTH=80,L0=7,HEADINGS=4
0570 LET H2$(O[1]+1)="File",H2$(O[2]+1)="Description",H2$(O[3]+M1-6)="Records"
0580 LET H2$(O[4]+M2-5)="In Use",H2$(O[5]+M2-4)="Avail",DW=MIN(O[3]-O[2]-1,30)
0590 LET H2$(O[6]+M3-6)="Percent",H2$(O[7]+1)="Histogram",HEADINGS$[4]=H2$
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,L0-3,0
0720 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0730 PRINT 'SB',@(0,3),"System:",@(65),"Output Mode:",@(0,L0-2),H2$,'SF',
0800 REM " --- Which system?"
0805 PRINT 'CF',
0810 LET V0$="S",V1$="C",V2$="",V3$="",V0=2,V1=8,V2=3
0815 LET V4$="Enter A Valid System ID (<F1>=All/<F3>=Lookup)"
0820 GOSUB 7000
0825 ON V3 GOTO 0855,0830,0800,0850,9900
0830 LET MODULE$=""
0835 PRINT @(V1,V2),"All",FILL(35),
0840 GOTO 0900
0850 CALL "SYC.LM",V$
0855 LET B0$(1)=V$
0860 FIND (SYM04_DEV,KEY=B0$,DOM=0800)IOL=SYM04A
0865 LET MODULE$=B0$
0870 PRINT @(V1,V2),B0$," ",B1$(1,35)
0875 IF MODULE$="OP" THEN LET MODULE$="AR"
0900 REM " --- Output to printer or screen?"
0910 LET V0$="S",V4$="Screen Or Printer Output (S/P)?"
0915 LET V1$="C",V2$=OUTPUT$,V3$="SP",V0=1,V1=78,V2=3
0920 GOSUB 7000
0925 ON V3 GOTO 0930,0900,0800,0900,9900
0930 IF POS(V$="SP")=0 THEN GOTO 0900
0935 LET OUTPUT$=V$
0950 REM " --- Correct?"
0960 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
0965 ON V3 GOTO 0970,0900,0900,0900,9900
0970 ON POS(V$="YN") GOTO 0950,1000,0800
1000 REM " --- Screen Initializations"
1010 LET FILES=0,CHANNEL=0,L9=20,L=L0,PRINTER=0
1020 PRINT @(0,L0),'CE',
1030 IF OUTPUT$="S" THEN GOTO 1900
1200 REM " --- Printer Initializations"
1210 LET L9=59,L=L9+1,CLOCK$="",PAGE=0,PRINTER=UNT
1230 CALL "SYC.GA",PRINTER,1,"","",STATUS
1240 IF STATUS THEN GOTO 9900
1250 CALL "SYC.NB","Printing",6,COLUMN
1900 REM " --- Position system file"
1910 IF MODULE$<>"" THEN GOTO 2900
1990 READ (SYM04_DEV,KEY="",DOM=2000)
2000 REM " --- Next system"
2010 READ (SYM04_DEV,END=4000)IOL=SYM04A
2020 IF B1$(39,1)<>"Y" THEN GOTO 3900
2900 REM " --- Position file definitions"
2990 READ (SYM03_DEV,KEY=B0$,DOM=3000)
3000 REM " --- Print or display file information"
3010 LET K$=KEY(SYM03_DEV,END=3900)
3020 IF POS(B0$=K$)<>1 THEN GOTO 3900
3030 READ (SYM03_DEV)IOL=SYM03A
3040 IF OUTPUT$="P" THEN PRINT @(COLUMN,11),A0$,
3100 REM " --- Does file exist?"
3110 LET FILES$[4]=A0$,OPTIONS$[4]="F"
3120 CALL "SYC.DA",1,4,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
3130 IF STATUS>0 THEN GOTO 3800
3140 LET CHANNEL=CHANNELS[4]
3150 IF CHANNEL=0 THEN GOTO 3800
3200 REM " --- Retrieve file specifications"
3210 LET AVAIL=0,RECORDS=0,NUMREC=0,PERCENT=0,PERCENT$="",HISTOGRAM=0,MARK$=""
3220 CALL "SYC.JA",CHANNEL,FILENAME$,PATHNAME$,FILETYPE$,KEYLEN,RECORDS,RECLEN,NUMREC,STATUS
3300 REM " --- Calculate available and percent used"
3310 IF FILETYPE$="M" THEN GOTO 3390
3320 LET AVAIL=RECORDS-NUMREC,PERCENT=100-100*AVAIL/RECORDS,STATUS=0
3330 LET PERCENT=MIN(PERCENT,100),PERCENT$=STR(PERCENT:M3$)
3340 IF NUMREC=0 THEN LET PERCENT$="  Empty"
3350 IF PERCENT>89.9 THEN LET MARK$="-->"
3360 IF PERCENT>99.9 THEN LET PERCENT$="   Full"
3370 LET HISTOGRAM=INT(PERCENT/10),RECORDS$=STR(RECORDS:M2$)
3380 GOTO 3400
3390 LET RECORDS$="Dynamic",STATUS=0
3400 REM " --- Need header?"
3410 IF L>L9 THEN GOSUB 5000
3420 IF V3=4 THEN GOTO 0800
3500 REM " --- Print detail line"
3510 PRINT (PRINTER)MARK$,@(O[1]),A0$,@(O[2]),A1$(1,DW),@(O[3]),RECORDS$,@(O[4]),NUMREC:M1$,@(O[5]),AVAIL:M2$,@(O[6]),PERCENT$,@(O[7]),FILL(HISTOGRAM,"*")
3520 LET L=L+1,FILES=FILES+1
3800 REM " --- Loop back for next file"
3810 CLOSE (CHANNEL,ERR=3000)
3890 GOTO 3000
3900 REM " --- Loop back for next record"
3990 IF MODULE$="" THEN GOTO 2000
4000 REM " --- End of Screen Display"
4010 IF OUTPUT$="P" THEN GOTO 4100
4020 LET V4$=STR(FILES)+" File(s) Listed (<Enter>=Continue)"
4030 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
4040 GOSUB 7000
4050 IF V3=4 THEN GOTO 9900
4090 GOTO 0800
4100 REM " --- End of Report"
4110 IF L>L9 THEN GOSUB 5000
4120 PRINT (PRINTER)""
4130 PRINT (PRINTER)@(3),FILES," File(s) Listed"
4140 CLOSE (PRINTER,ERR=0800)
4190 GOTO 0800
5000 REM " --- Report Heading"
5010 IF OUTPUT$="S" THEN GOTO 5100
5020 LET L=HEADINGS+1
5030 CALL "SYC.HA",PRINTER,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS>0 THEN GOTO 9900
5090 GOTO 5190
5100 REM " --- Screen Display"
5120 LET L=L0,V0$="S",V1$="CE",V4$="Press <Enter> To Continue"
5130 LET V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5140 GOSUB 7000
5150 PRINT @(0,L0),'CE',
5190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

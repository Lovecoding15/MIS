0010 REM "DDM - Record Layout Maintenance (Additional Options)"
0020 REM "Program DDC.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.1.0 - 01Apr99 |"
0026 REM "|         Copyright (c) 1999 ADD+ON Software, Inc.          |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "WIN_Y     : Window row location (input)
0055 REM "DDM04_1$  : 2nd data string in DDM-04 record (input/returned)
0060 REM "FILES[n]  : Channel numbers (input)
0062 REM "            [0]=not used
0064 REM "            [1]=SYS-01
0066 REM "            [2]=not used
0068 REM "            [3]=DDM-01
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER WIN_Y,DDM04_1$,FILES[ALL]
0100 REM " --- Assign Channel Numbers"
0110 LET SYS01_DEV=FILES[1]
0120 LET DDM01_DEV=FILES[3]
0200 REM " --- IOLists"
0210 DDM01A: IOLIST B0$,B1$(1),B[ALL]
0220 DDM20A: IOLIST X0$,X1$,X2$
0230 DDM30A: IOLIST X0$,X1$,X2$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2)
0500 REM " --- Initializations"
0510 DIM B1$(105),B[2]
0520 LET M$="##0",OFFSET=4,J9=22,WIDTH=80
0530 LET A1$=DDM04_1$
0600 LET MASKSYS$="APARBMGLIVPRWO",VALIDTYPES$="ACHMNOPRUV%"
0605 DIM MASKTYPES$[LEN(MASKSYS$)/2],MASKDESC$[LEN(VALIDTYPES$)]
0610 LET MASKTYPES$[INT((POS("AP"=MASKSYS$,2)+1)/2)]="NAUR"
0615 LET MASKTYPES$[INT((POS("AR"=MASKSYS$,2)+1)/2)]="NAUR"
0620 LET MASKTYPES$[INT((POS("BM"=MASKSYS$,2)+1)/2)]="HMO"
0625 LET MASKTYPES$[INT((POS("GL"=MASKSYS$,2)+1)/2)]="NAU%"
0630 LET MASKTYPES$[INT((POS("IV"=MASKSYS$,2)+1)/2)]="AUCPV"
0635 LET MASKTYPES$[INT((POS("PR"=MASKSYS$,2)+1)/2)]="NAUR"
0640 LET MASKTYPES$[INT((POS("WO"=MASKSYS$,2)+1)/2)]="NAUR"
0645 LET MASKDESC$[POS("A"=VALIDTYPES$)]="Amount"
0650 LET MASKDESC$[POS("C"=VALIDTYPES$)]="Cost"
0655 LET MASKDESC$[POS("H"=VALIDTYPES$)]="Hour"
0660 LET MASKDESC$[POS("M"=VALIDTYPES$)]="Material Factor"
0665 LET MASKDESC$[POS("N"=VALIDTYPES$)]=$22$+"Number"+$22$
0670 LET MASKDESC$[POS("O"=VALIDTYPES$)]="Overhead Factor"
0675 LET MASKDESC$[POS("P"=VALIDTYPES$)]="Price"
0680 LET MASKDESC$[POS("R"=VALIDTYPES$)]="Rate"
0685 LET MASKDESC$[POS("U"=VALIDTYPES$)]="Unit"
0690 LET MASKDESC$[POS("V"=VALIDTYPES$)]="conVersion"
0695 LET MASKDESC$[POS("%"=VALIDTYPES$)]="percent"
0700 REM " --- Background"
0710 LET WIDTH=70,HEIGHT=6,TITLE$="Additional Options"
0720 IF WIN_Y=0 THEN LET WIN_Y=3
0725 IF WIN_Y+HEIGHT>22 THEN LET WIN_Y=22-HEIGHT
0730 LET WIN_X=4
0735 IF WIN_X+WIDTH>79 THEN LET WIN_X=79-WIDTH
0740 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0745 LET OFFSET=0,J9=HEIGHT-2
0750 PRINT 'SB',@(16,OFFSET+0),"Mask",@(6,OFFSET+1),@(1,OFFSET+1),"Read/Write Security",@(30,OFFSET+1),"Password",@(2,OFFSET+2),"Read Only Security",@(30,OFFSET+2),"Password",'SF',
1200 REM " --- Retrieve Defaults From Data Dictionary"
1210 FIND (DDM01_DEV,KEY=A1$(1,12),DOM=1400)IOL=DDM01A
1220 IF FNP$(A1$(55,18))="" THEN LET A1$(55,18)=B1$(38,18)
1230 IF FNP$(A1$(73,1))="" THEN LET A1$(73,1)=B1$(80,1)
1240 IF FNP$(A1$(74,6))="" THEN LET A1$(74,6)=B1$(81,6)
1250 IF FNP$(A1$(80,1))="" THEN LET A1$(80,1)=B1$(87,1)
1260 IF FNP$(A1$(81,6))="" THEN LET A1$(81,6)=B1$(88,6)
1300 REM " --- Display record"
1320 GOSUB 5000
1390 GOTO 4000
1400 REM " --- Bad Data Dictionary Element Name"
1490 GOTO 9900
1800 REM " --- System Source For Mask"
1810 LET V0$="S",V1$="C",V2$=A1$(55,2),V3$="",V0=2,V1=21,V2=OFFSET+0,I0=0
1820 LET V4$="Enter The System ID To Use For Source Of This Mask (<F3>=Lookup)"
1830 GOSUB 7000
1840 ON V3 GOTO 1860,1800,3900,1845,3900
1845 CALL "SYC.LM",V$
1850 IF V$="" THEN GOTO 1800
1855 PRINT @(V1,V2),V$,
1860 LET V$=V$+FILL(2),V$=V$(1,2)
1870 IF POS(V$=FILL(2)+MASKSYS$,2)=0 THEN GOTO 1800
1880 LET A1$(55,2)=V$
1890 GOTO 3900
1900 REM " --- Mask Type"
1905 IF POS(A1$(55,2)=MASKSYS$,2)=0 THEN GOTO 1990
1910 LET V0$="S",V1$="C",V2$=A1$(57,1),V3$=MASKTYPES$[INT((POS(A1$(55,2)=MASKSYS$,2)+1)/2)],V4$="",V0=1,V1=25,V2=OFFSET+0
1915 IF POS(A1$(57,1)=V3$)=0 THEN LET A1$(57,1)="",V2$=""
1920 FOR I=1 TO LEN(V3$)
1925 LET V4$=V4$+V3$(I,1)+"="+MASKDESC$[POS(V3$(I,1)=VALIDTYPES$)]+FILL(2)
1930 NEXT I
1935 GOSUB 7000
1940 ON V3 GOTO 1960,1900,3900,1900,3900
1960 LET A1$(57,1)=V$,A1$(58,15)="",MASK$=FNP$(A1$(58,15))
1970 CALL "DDC.MA",N0$,A1$(55,2),A1$(57,1),MASK$,SYS01_DEV
1980 PRINT @(28,OFFSET+0),MASK$,'CL',
1990 GOTO 3900
2000 REM " --- Special Masks"
2005 IF FNP$(A1$(55,2))<>"" THEN GOTO 2090
2010 LET A1$(57,1)="",V4$="Enter Special Mask If Needed"
2020 LET V0$="S",V1$="C",V2$=A1$(58,15),V3$="",V0=15,V1=28,V2=OFFSET+0
2025 PRINT @(25,OFFSET+0),A1$(57,1),
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 SETERR 2070
2055 PRINT @(28,OFFSET+0),0:V$,@(28,OFFSET+0),'CL',
2060 GOTO 2080
2070 SETERR 9000
2075 GOTO 2000
2080 LET A1$(58,15)=V$
2085 PRINT @(28,OFFSET+0),A1$(58,15),
2090 GOTO 3900
2500 REM " --- READ/WRITE Security"
2505 IF A1$(73,1)=" " THEN LET A1$(73,1)="0"
2510 LET V4$="Enter Minimum Security Level Required For READ/WRITE Permission"
2520 LET V0$="Z",V1$="C",V2$=A1$(73,1),V3$="",V0=1,V1=21,V2=OFFSET+1
2530 GOSUB 7000
2540 ON V3 GOTO 2560,2500,3900,2500,3900
2560 IF V$="" THEN LET V$="0"
2565 IF V$<A1$(80,1) THEN GOTO 2700
2570 LET A1$(73,1)=V$
2580 PRINT @(V1,V2),A1$(73,1),
2590 GOTO 3900
2600 REM " --- READ/WRITE Password"
2610 LET V4$="Enter An Optional Password For READ/WRITE Permission"
2620 LET V0$="S",V1$="C",V2$=A1$(74,6),V3$="",V0=6,V1=39,V2=OFFSET+1
2630 GOSUB 7000
2640 ON V3 GOTO 2660,2600,3900,2600,3900
2660 LET A1$(74,6)=V$
2690 GOTO 3900
2700 REM " --- READ ONLY Security"
2705 IF A1$(80,1)=" " THEN LET A1$(80,1)="0"
2710 LET V4$="Enter Minimum Security Level Required For READ ONLY Permission"
2720 LET V0$="Z",V1$="C",V2$=A1$(80,1),V3$="",V0=1,V1=21,V2=OFFSET+2
2730 GOSUB 7000
2740 ON V3 GOTO 2760,2700,3900,2700,3900
2760 IF V$="" THEN LET V$="0"
2765 IF V$>A1$(73,1) THEN GOTO 2700
2770 LET A1$(80,1)=V$
2780 PRINT @(V1,V2),A1$(80,1),
2790 GOTO 3900
2800 REM " --- READ ONLY Password"
2810 LET V4$="Enter An Optional Password For READ ONLY Permission"
2820 LET V0$="S",V1$="C",V2$=A1$(81,6),V3$="",V0=6,V1=39,V2=OFFSET+2
2830 GOSUB 7000
2840 ON V3 GOTO 2860,2800,3900,2800,3900
2860 LET A1$(81,6)=V$
2890 GOTO 3900
3900 REM " --- Next field"
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1800,1900,2000,2500,2600,2700,2800,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Correct (Y/N)?"
4020 LET V0$="S",V1$="CE^",V2$="Y",V3$="",V0=1,V2=J9,V1=FNV1(V4$,WIDTH)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4090 ON POS(V$="YN") GOTO 4000,4100,1800
4100 REM " --- Keep Non-Default Additional Data"
4110 IF A1$(55,18)=B1$(38,18) THEN LET A1$(55,18)=""
4120 IF A1$(73,1)=B1$(80,1) THEN LET A1$(73,1)=""
4130 IF A1$(74,6)=B1$(81,6) THEN LET A1$(74,6)=""
4140 IF A1$(80,1)=B1$(87,1) THEN LET A1$(80,1)=""
4150 IF A1$(81,6)=B1$(88,6) THEN LET A1$(81,6)=""
4180 LET DDM04_1$=A1$
4190 GOTO 9900
5000 REM " --- Display Additional Data"
5020 LET MASK$=FNP$(A1$(58,15))
5030 CALL "DDC.MA",N0$,A1$(55,2),A1$(57,1),MASK$,SYS01_DEV
5040 PRINT @(21,OFFSET+0),A1$(55,2),@(25,OFFSET+0),A1$(57,1),@(28,OFFSET+0),MASK$,'CL',
5050 PRINT @(21,OFFSET+1),A1$(73,1),@(39,OFFSET+1),A1$(74,6),
5060 PRINT @(21,OFFSET+2),A1$(80,1),@(39,OFFSET+2),A1$(81,6),
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNV1(Q$,Q)=FNC(Q$,Q)+LEN(Q$)
8090 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9205 SETESC 9000
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to Caller"
9940 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9950 EXIT 
9999 END

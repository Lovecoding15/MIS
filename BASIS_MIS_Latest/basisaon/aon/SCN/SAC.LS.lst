0010 REM "SAC - Sales Analysis Lookup"
0020 REM "Program SAC.LS"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Sep98 WGH: Ship-To lookup description not displayed correctly (2435,8060)
0040 REM 
0050 REM "N0$       : Firm ID (Input)
0051 REM "SAM_DEV   : Channel number for Sales Analysis file (Input)
0052 REM "LITERAL$  : Field literal (Input)
0053 REM "TYPE$     : Field type (Input)
0054 REM "IMASK$    : Field input mask (Input)
0056 REM "OMASK$    : Field output mask (Input)
0057 REM "FLDLEN    : Length of field's key segment (Input)
0058 REM "INLEN     : Number of characters in input field (Input)
0060 REM "DESCCHAN  : Channel of file to obtain description (Input)
0062 REM "DESCREC$  : ID of record to obtain description (Input)
0064 REM "DESCSTR   : IOLIST string containing description (Input)
0065 REM "DESCPOS   : Start position of description in DESCSTR (Input)
0066 REM "DESCLEN   : Length of description in DESCSTR (Input)
0070 REM "IV_DESC[n]: Inventory item description parameters (Input)
0071 REM "             [0]        = Item number length
0072 REM "             [1] to [3] = Description segment lengths
0074 REM "PREFIX$   : SAM_DEV key up to input field (Input)
0075 REM "SELECTION$: User selection (Returned)
0080 SETERR 9000
0085 SETESC 9900
0090 ENTER N0$,SAM_DEV,LITERAL$,TYPE$,IMASK$,OMASK$,FLDLEN,INLEN,DESCCHAN,DESCREC$,DESCSTR,DESCPOS,DESCLEN,IV_DESC[ALL],PREFIX$,SELECTION$
0200 REM " --- IOLists
0220 DESCCHANA: IOLIST A$[ALL]
0400 REM " --- Parameters"
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0500 REM " --- Initializations"
0510 LET MAX_FLD=MAX(INLEN,LEN(OMASK$),LEN(LITERAL$))
0520 LET WIDTH=MAX_FLD+MAX(DESCLEN,11)+5,HEIGHT=12,WIN_X=1,WIN_Y=9,MAX_ROW=HEIGHT-5
0530 IF WIDTH<42 THEN LET WIDTH=42
0540 LET TITLE$="Sales Analysis Lookup"
0550 LET OWIDTH=MAX_FLD+4,OHEIGHT=5,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3
0560 LET OTITLE$=""
0570 DIM O[1]
0580 LET O[0]=2,O[1]=O[0]+MAX_FLD+1
0700 REM " --- Background"
0710 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0720 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0730 LET HEADING$(O[0])=LITERAL$,HEADING$(O[1])="Description"
0740 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0750 PRINT @(0,0),'SB','BR',HEADING$,'ER','SF',
0760 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
0800 REM " --- Options"
0810 CALL "SYC.WA",0,OWIDTH,OHEIGHT,OWIN_X,OWIN_Y,OTITLE$,ONAME$
0820 PRINT @(1,0),LITERAL$,
0830 LET V0$=TYPE$,V1$="K",V2$="",V3$=IMASK$,V4$="",V0=INLEN,V1=1,V2=OHEIGHT-2
0840 GOSUB 7000
0850 IF V3=6 THEN GOTO 9900
0860 IF V3=5 THEN GOTO 9900
0870 IF V3=4 THEN GOTO 9900
0880 LET START$=V$
0890 CALL "SYC.WD",ONAME$
0900 REM " --- Detail Window"
0910 CALL "SYC.WB",NAME$,0
1000 REM " --- Position File"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,5]
1020 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$="",LAST$=""
1040 LET PRELEN=LEN(PREFIX$),K$=PREFIX$+START$
1060 READ (SAM_DEV,KEY=K$,DOM=1400)
1090 GOTO 1420
1400 REM " --- Read Next Record"
1410 LET K$=KEY(SAM_DEV,END=3000)
1420 IF LEN(K$)<PRELEN THEN GOTO 3000
1430 IF POS(K$(1,PRELEN)=PREFIX$)<>1 THEN GOTO 3000
1440 READ (SAM_DEV,KEY=K$,ERR=1950)
1450 IF K$(PRELEN+1,FLDLEN)=LAST$ THEN GOTO 2800
1490 GOTO 2000
1950 REM " --- Error Reading Record"
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1990 GOTO 2800
2000 REM " --- Display Page?"
2010 IF L<MAX_ROW THEN GOTO 2200
2020 LET XMODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 0800
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2200 REM " --- Read Description Record"
2210 DIM A$[DESCSTR],A$[DESCSTR](DESCPOS+DESCLEN),DESCRIPTION$(DESCLEN)
2220 IF LEN(DESCRIPTION$)<11 THEN DIM DESCRIPTION$(11)
2230 LET KEYVALUE$=K$(PRELEN+1,FLDLEN)
2240 IF DESCCHAN=0 THEN GOTO 2400
2250 LET A$[DESCSTR](DESCPOS)="Unknown "+LITERAL$
2260 FIND (DESCCHAN,KEY=N0$+DESCREC$+KEYVALUE$,DOM=2400)IOL=DESCCHANA
2400 REM " --- Lookup Window Record"
2410 DIM G1$(WIDTH-2)
2420 LET LAST$=KEYVALUE$,DESCRIPTION$(1)=A$[DESCSTR](DESCPOS,DESCLEN)
2430 IF TYPE$="M" THEN LET KEYVALUE$=FNF$(KEYVALUE$(1,INLEN),OMASK$)
2435 IF FNP$(KEYVALUE$)="" AND LITERAL$="Customer Ship-To" THEN LET DESCRIPTION$(1)="Bill-To Address"
2440 IF LITERAL$="Item" THEN LET DESCRIPTION$(1)=FNITEM$(A$[DESCSTR](DESCPOS,DESCLEN),IV_DESC[1],IV_DESC[2],IV_DESC[3]),KEYVALUE$=KEYVALUE$(1,INLEN)
2470 LET G1$(O[0])=KEYVALUE$,G1$(O[1])=DESCRIPTION$
2480 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=LAST$
2485 LET X0=X0+1,X1=X1+1,L=L+1
2490 GOTO 2900
2800 REM " --- Skip duplicate entries"
2810 READ (SAM_DEV,KEY=K$(1,LEN(K$)-1)+$FF$,DOM=2900)
2900 REM " --- Loop back for next record"
2990 GOTO 1400
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET MESSAGE$[0]="No sales analysis records found (<Enter>=Continue)"
3040 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3050 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page"
3510 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,5
6280 LET L=1,X1=1
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8060 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9925 CALL "SYC.WD",CNAME$
9930 CALL "SYC.WD",ONAME$
9940 CALL "SYC.WD",NAME$
9945 IF SELECTION$="END" THEN LET SELECTION$=""
9950 EXIT 
9999 END

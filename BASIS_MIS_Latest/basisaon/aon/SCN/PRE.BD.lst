0010 REM "PRE - Payroll Entry - Tax Information"
0020 REM "Program PRE.BD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Jul96 WGH: Modify calculations for new Connecticut state tax withholdings (6327,8415,8660-8699)"
0032 REM "29May97 WGH: Supplemental earnings are doubled in tax basis when not using tax table (6480)"
0033 REM "15Jan98 WGH: Earnings taxed on a Supplemental basis are not being taxed at the supplemental rate (2370,2770,3412,5570,5580,5655,5930,6005-6015,6360)"
0034 REM "09Dec98 WGH: Do Maryland standard deduction for both Single, and Married Filing Jointly (8420)
0035 REM "10Dec98 WGH: Update constants used in Oklahoma tax calculations (8210-8240,8330)
0040 REM 
0050 PRECISION P[3]
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 PRE31A: IOLIST W0$,W[ALL]
0220 PRE31A1: IOLIST V$,X,*,Y
0260 PRM10C: IOLIST X0$(1),X[ALL],X1$,X2$
0270 PRM10C1: IOLIST Z0$(1)
0300 PRM10I: IOLIST D0$,D[0],D[1],D[2],I[ALL],J[ALL],K[ALL],L[ALL]
0310 PRM10I1: IOLIST *,LOWL
0380 PRT21A: IOLIST Y0$,Y[ALL],Z[0]
0390 PRT21A1: IOLIST Z1$(1)
0500 REM " --- Init
0510 DIM W0$(32),W[2],Y[17],Z[2],X[2],U[2],D[2],I[10],J[10],K[10],L[10]
0520 LET FI_MC=.0765; REM "FICA + Medicare combined rate"
0530 LET W0$(1)=A0$(1,21)+"C",T2$="DWBSMQA",T1$="365052026024012004001"
0700 REM " --- Background"
0715 CALL "SYC.WC",1,0,80,0,0,9,0
0720 PRINT @(0,L0),'CL','SB',@(4,9),"Taxes",@(0,L0),"Seq",@(4),"Tax Code",@(23),"Table",@(19+M1),"Taxable",@(22+M1+M3),"Rate",@(20+2*M1+M3),"Amount",'SF',
0730 GOSUB 5400
0800 REM " --- Disp
0900 IF TAX$<>"DONE" THEN LET U0=1,T2=0,TAX$="DONE" ELSE LET U0=0
0920 GOSUB 5100
0940 LET U0=0
1000 REM " --- Lines
1020 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L1),'LD',
1040 IF L>L9 THEN LET L=L9
1100 REM " --- Next
1110 DIM U[2]
1120 LET K$=KEY(PRE31_DEV1,END=1300)
1140 IF K$(1,22)<>W0$(1,22) THEN GOTO 1300
1150 READ (PRE31_DEV1)IOL=PRE31A
1160 GOTO 1490
1300 LET W0=NUM(W0$(23,2))+1
1320 IF W0>99 THEN LET W0=1
1340 LET W0$(23,2)=STR(W0:"00")
1350 PRINT @(0,L),W0$(23,2),
1390 GOTO 1480
1400 REM " --- Seq
1420 LET V0$="Z",V1$="KER",V2$=W0$(23,2),V3$="",V4$="",V0=2,V1=0,V2=L,I0=0
1430 GOSUB 7000
1440 IF V3=4 THEN GOTO 4000
1450 IF V$="00" THEN GOTO 1400
1460 LET W0$(23,2)=V$
1480 FIND (PRE31_DEV1,KEY=W0$(1,24),DOM=1600)IOL=PRE31A
1490 IF W0$(1,24)<=K9$ THEN GOSUB 5400
1500 GOSUB 5000
1510 LET U[1]=-W[1],U[2]=-W[2]
1520 LET I0=5
1530 IF W[2]=0 THEN GOTO 2000
1590 GOTO 3300
1600 REM " --- Setup
1620 DIM W[2],U[2]
1630 LET W0$(25)=""
2000 REM " --- Tax
2020 LET V0$="S",V1$="KCRE",V2$=W0$(25,2),V3$="",V0=2,V1=4,V2=L,I0=1,V4$="Enter A Valid Tax Code (<F3>=Lookup)"
2030 GOSUB 7000
2031 IF V3=2 THEN GOTO 1400
2032 IF V3<>3 THEN GOTO 2040
2034 LET V4=1,V6=4,V7=2,V8=6,V9=16,V5$="C"
2036 CALL "SYC.LZ",V1,V2,V4,PRM10_DEV,V6,V7,V8,V9,V$,V5$,N0$
2038 IF V$="" THEN GOTO 2000
2040 LET W0$(25,2)=V$
2042 IF V3=4 THEN GOTO 4000
2050 GOSUB 5000
2060 IF X0$="" OR Y0$="" THEN GOTO 2000
2070 LET W0$(27,3)=Y0$(15,3)
2100 REM " --- Table
2110 IF X0$(24,1)<>"T" THEN GOTO 2200
2120 LET V0$="S",V1$="KCR",V2$=W0$(27,3),V0=3,V1=24,V2=L
2125 LET V4$="Enter A Valid Table ID (<F3>=Lookup)"
2130 GOSUB 7000
2132 IF V3=2 THEN GOTO 2290
2134 IF V3<>3 THEN GOTO 2140
2136 LET V4=1,V6=4,V7=3,V8=7,V9=20,V5$="I"
2137 CALL "SYC.LZ",V1,V2,V4,PRM10_DEV,V6,V7,V8,V9,V$,V5$,N0$
2138 IF V$="" THEN GOTO 2100
2140 LET W0$(27,3)=V$
2150 FIND (PRM10_DEV,KEY=N0$+"I"+W0$(27,3),DOM=2100)IOL=PRM10I
2160 PRINT @(10,22),D0$(7,20),
2200 REM " --- Calc
2220 IF X0$(24,1)="T" THEN GOSUB 6000
2240 IF X0$(24,1)<>"T" THEN GOSUB 6500
2250 PRINT @(28,L),W[0]:M1$,W[1]:M3$,W[2]:M1$,
2290 GOTO 3200
2300 REM " --- Rate
2310 IF X0$(24,1)="F" THEN GOTO 2390
2320 LET V0$="N",V2$=STR(W[1]),V3$=M3$,V4$="",V1=28+M1,V2=L
2340 GOSUB 7000
2360 LET W[1]=V
2370 IF X0$(24,1)="T" THEN GOSUB 6000 ELSE GOSUB 6640
2380 PRINT @(28+M1+M3,L),W[2]:M1$,
2390 GOTO 3200
2600 REM " --- Amt
2610 PRECISION 2; LET W[2]=W[2]*1; PRECISION P[3]
2620 LET V0$="N",V2$=STR(W[2]),V3$=M1$,V4$="",V1=28+M1+M3,V2=L
2640 GOSUB 7000
2650 PRECISION 2
2660 LET W[2]=V*1
2670 PRECISION P[3]
2690 GOTO 3200
2700 REM " --- Taxable
2710 IF X0$(24,1)="F" THEN GOTO 2790
2720 LET V0$="N",V2$=STR(W[0]),V3$=M1$,V4$="",V1=28,V2=L
2740 GOSUB 7000
2760 LET W[0]=V
2770 IF X0$(24,1)="T" THEN GOSUB 6000 ELSE GOSUB 6640
2780 PRINT @(28+M1+M3,L),W[2]:M1$,
2790 GOTO 3200
3200 REM " --- Seq
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,2000,2700,2300,2600,3300
3300 REM " --- Correct?
3320 LET V0$="S",V1$="C",V2$="",V0=1,V1=77,V2=L,K9$=W0$(1,24)
3325 LET V4$="<Enter>=OKay  <F2>=Modify  D=Delete"
3330 GOSUB 7000
3340 IF V3=2 THEN GOTO 3200
3350 IF V$="" THEN GOTO 3400
3360 IF V$="D" THEN GOTO 3700
3370 GOTO 3300
3400 REM " --- Write
3410 PRECISION 2
3412 LET W[2]=W[2]*1,SUPTAX=SUPTAX*1,REGTAX=W[2]-SUPTAX
3414 PRECISION P[3]
3420 WRITE (PRE31_DEV1,KEY=W0$(1,24))IOL=PRE31A
3430 GOSUB 5800
3450 LET L=L+1
3490 GOTO 1000
3700 REM " --- Delete
3720 REMOVE (PRE31_DEV1,KEY=W0$(1,24),DOM=3730)
3730 DIM W[2]
3740 GOSUB 5800
3750 PRINT @(0,L),'CL',
3790 GOTO 1000
4000 REM " --- Options
4010 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Tax Information Correct (Y/N/Review/List/Delete)? ",V0=7,V1=FNV(V4$),V2=22
4015 GOSUB 7000
4020 IF V3=4 THEN RUN "PRE.BA"
4025 IF V$="DELETE" THEN GOTO 4200
4030 IF V$="R" OR V$="Review" THEN LET V$="REVIEW"
4035 IF V$="L" OR V$="List" THEN LET V$="LIST"
4040 IF V$="REVIEW" THEN GOTO 4900
4050 IF V$="LIST" THEN GOTO 4800
4060 IF V$="" THEN GOTO 4100
4070 ON POS(V$="YN") GOTO 4000,4100,1000
4100 IF A0$(12,1)="C" THEN RUN "PRE.BX" ELSE RUN "PRE.BE"
4200 REM " --- Delete
4210 REM " --- Delete Earnings Rec 
4220 READ (PRE11_DEV,KEY=A0$(1,21),DOM=4230)
4230 LET K11$=KEY(PRE11_DEV,END=4290)
4240 IF K11$(1,21)<>A0$(1,21) THEN GOTO 4290
4250 REMOVE (PRE11_DEV,KEY=K11$)
4260 GOTO 4230
4300 REM " --- Delete Deduction Rec 
4320 READ (PRE21_DEV,KEY=A0$(1,21),DOM=4330)
4330 LET K21$=KEY(PRE21_DEV,END=4390)
4340 IF K21$(1,21)<>A0$(1,21) THEN GOTO 4390
4350 REMOVE (PRE21_DEV,KEY=K21$)
4360 GOTO 4330
4400 REM " --- Delete Tax Rec 
4420 READ (PRE31_DEV1,KEY=A0$(1,21),DOM=4430)
4430 LET K31$=KEY(PRE31_DEV1,END=4490)
4440 IF K31$(1,21)<>A0$(1,21) THEN GOTO 4490
4450 REMOVE (PRE31_DEV1,KEY=K31$)
4460 GOTO 4430
4500 REM " --- Delete Contribution Rec
4520 READ (PRE41_DEV,KEY=A0$(1,21),DOM=4530)
4530 LET K41$=KEY(PRE41_DEV,END=4590)
4540 IF K41$(1,21)<>A0$(1,21) THEN GOTO 4590
4550 REMOVE (PRE41_DEV,KEY=K41$)
4560 GOTO 4530
4600 REM " --- Delete Accrual Rec
4620 READ (PRE51_DEV,KEY=A0$(1,21),DOM=4630)
4630 LET K51$=KEY(PRE51_DEV,END=4690)
4640 IF K51$(1,21)<>A0$(1,21) THEN GOTO 4690
4650 REMOVE (PRE51_DEV,KEY=K51$)
4660 GOTO 4630
4700 REM " --- Delete Header Rec
4720 REMOVE (PRE01_DEV,KEY=A0$(1,21),DOM=4730)
4730 LET K$=KEY(PRE01_DEV,END=4790)
4740 IF K$(1,21)<>A0$(1,21) THEN GOTO 4790
4750 REMOVE (PRE01_DEV,KEY=K$)
4760 GOTO 4730
4790 RUN "PRE.BA"
4800 REM " --- List
4810 GOSUB 5100
4820 GOTO 4000
4900 REM " --- Review
4910 READ (PRE31_DEV1,KEY=W0$(1,22),DOM=4920)
4920 GOSUB 5400
4930 GOTO 1000
5000 REM " --- Display
5010 LET X$="",X0$="",Y0$=""
5012 DIM X0$(85)
5015 FIND (PRT21_DEV,KEY=A0$(1,11)+"C"+W0$(25,2),DOM=5020)IOL=PRT21A
5020 FIND (PRM10_DEV,KEY=N0$+"C"+W0$(25,2),DOM=5050)IOL=PRM10C
5030 LET X$=X0$(6,16)
5050 PRINT @(0,L),W0$(23,2),@(4),W0$(25,2)," ",X$,@(24),W0$(27,3),@(28),W[0]:M1$,W[1]:M3$,W[2]:M1$,
5060 IF U0=1 THEN GOSUB 5800
5090 RETURN 
5100 REM " --- List
5110 GOSUB 5400
5120 READ (PRE31_DEV1,KEY=W0$(1,22),DOM=5130)
5130 LET K$=KEY(PRE31_DEV1,END=5300)
5150 IF K$(1,22)<>W0$(1,22) THEN GOTO 5300
5160 READ (PRE31_DEV1)IOL=PRE31A
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Press <Enter> To Continue",V0=1,V1=FNV(V4$),V2=22
5215 GOSUB 7000
5220 IF V3=4 THEN GOTO 5300
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+1,K9$=W0$(1,24)
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen
5420 PRINT @(0,L1),'CE',
5440 LET L=L1
5490 RETURN 
5500 REM " --- Deduct Fed taxes from state taxable earnings"
5505 LET X8=0,Z1=W[2],Z2=U[2],LOWL=0,PER=NUM(T1$(POS(A0$(25,1)=T2$)*3-2,3)),STCHG$="N"
5510 DIM Z0$(33),Z1$(48)
5515 FOR X9=0 TO LEN(FNP$(X0$(70,14)))/2-1
5520 LET X9$=X0$(70+X9*2,2)
5525 FIND (PRM10_DEV,KEY=N0$+"C"+X9$,DOM=5565)IOL=PRM10C1
5530 IF Z0$(31,2)<>X1$(X*2+1,2) THEN GOTO 5565
5535 FIND (PRT21_DEV,KEY=A0$(1,11)+"C"+X9$,DOM=5565)IOL=PRT21A1
5545 FIND (PRM10_DEV,KEY=N0$+"I"+Z1$(15,3),DOM=5565)IOL=PRM10I1
5550 ON POS(X9$="ORALMA",2) GOSUB 5590,OREGON,5590,ALABAMA,5590,MASS
5560 LET STCHG$="Y"
5565 NEXT X9
5570 IF REGTAX+SUPTAX<>0 THEN LET TAXRATIO=SUPTAX/(REGTAX+SUPTAX) ELSE LET TAXRATIO=0
5575 PRECISION 2
5580 LET W[2]=W[2]*1,SUPTAX=W[2]*TAXRATIO,REGTAX=W[2]-SUPTAX,U[2]=U[2]*1
5585 PRECISION P[3]
5590 RETURN 
5600 MASS: REM "Massachusetts State Tax"
5605 LET X8=ABS(T[T,0]*PER)
5610 IF NUM(Z1$(20,2))>0 AND X8<LOWL THEN GOTO 5645
5615 LET MASDED=X8*FI_MC
5620 IF MASDED>2000 THEN LET MASDED=2000
5630 LET MASDED=MASDED/PER
5635 LET W[2]=MASDED*SGN(W[0])
5645 RETURN 
5650 OREGON: REM "Oregon State Tax"
5655 LET W[2]=W[2]-SUPTAX
5675 LET X8=3000/PER
5680 IF ABS(W[2])>X8 THEN LET W[2]=X8*SGN(W[0])
5685 IF ABS(U[2])>X8 THEN LET U[2]=(-X8)*SGN(W[0])
5690 RETURN 
5750 ALABAMA: REM "Alabama State Tax"
5760 LET X8=ABS(T[T,0]*PER)
5765 LET ALDED=X8*.2
5770 IF Z1$(15,3)="AL2" AND ALDED>4000 THEN LET ALDED=4000
5775 IF POS(Z1$(15,3)="AL0AL1")>0 AND ALDED>2000 THEN LET ALDED=2000
5780 LET X8=ALDED/PER
5785 LET W[2]=W[2]+X8*SGN(W[0])
5790 RETURN 
5800 REM " --- Update Totals
5820 FOR X=0 TO LEN(X1$)/2-1
5830 IF X1$(X*2+1,2)="  " THEN GOTO 5950
5840 LET X0=1,Z1=0,Z2=0
5850 IF X2$(X+1,1)="-" THEN LET X0=-1
5855 REM "T(<T>,0)=Total Code; T(T,<0>)=RegPay for TOTCODE T,T(T,<1>)=Units for TOTCODE T, T(T,<2>) = Supplemental Pay for TOTCODE T, T(T,<3>)=Amount for Workers comp"
5860 LET T=NUM(X1$(X*2+1,2))
5862 IF W0$(22,1)="C" AND W0$(25,2)=G7$(1,2) THEN GOSUB 5500
5865 IF W0$(22,1)<>"A" THEN GOTO 5910
5870 IF X3$(X+1,1)=" " THEN LET T[T,0]=T[T,0]+X0*(W[2]+U[2])
5880 IF X[1]=0 THEN LET X[1]=1
5889 REM "          P=Calc on reg portion of premium pay
5890 IF X3$(X+1,1)="P" THEN LET T[T,0]=T[T,0]+X0*(W[2]/X[1]+U[2]/X[1])
5899 REM "          S=Earnings to be taxed on a supplemental basis
5900 IF X3$(X+1,1)="S" THEN LET T[T,2]=T[T,2]+X0*(W[2]+U[2])
5910 LET T[T,1]=T[T,1]+X0*(W[1]+U[1])
5930 IF W0$(22,1)<>"A" THEN LET T[T,0]=T[T,0]+X0*(REGTAX+U[2]),T[T,2]=T[T,2]+X0*SUPTAX
5940 IF STCHG$="Y" THEN LET W[2]=Z1,U[2]=Z2
5950 NEXT X
5970 LET T2=T2+W[2]+U[2]
5980 PRINT @(3*M1,8),T2:M1$,@(4*M1),T0-T1-T2:M1$,'CL',
5990 RETURN 
6000 REM " --- Tax Calc
6005 DIM D[2],I[10],J[10],K[10],L[10]
6010 FIND (PRM10_DEV,KEY=N0$+"I"+W0$(27,3),DOM=6015)IOL=PRM10I
6015 LET W[2]=0,W[1]=0,T=NUM(X0$(31,2)),W[0]=T[T,0]
6020 IF W[0]=0 THEN IF T[T,2]=0 THEN RETURN 
6030 LET X0=ABS(NUM(T1$(POS(A0$(25,1)=T2$)*3-2,3))*W[0]),X2=0
6040 IF POS(" "<>X0$(60,5))=0 THEN LET X0$(60,5)="LIST"
6045 GOSUB STATES
6050 FOR Q0=0 TO 4
6060 IF X0$(60+Q0,1)="T" THEN GOSUB 6200
6070 IF X0$(60+Q0,1)="S" THEN LET X0=X0-D[1]
6080 IF X0$(60+Q0,1)="I" THEN LET X0=X0-NUM(Y0$(20,2))*D[2]
6090 IF X0$(60+Q0,1)="D" THEN LET X0=X0-NUM(Y0$(18,2))*D[1]
6100 IF X0$(60+Q0,1)="L" THEN IF D[0]<>0 THEN IF X0<D[0] THEN LET X0=0
6170 NEXT Q0
6171 IF X0$(3,3)="COK" THEN LET X0=X0+OKSUP
6172 LET X0=X0*SGN(W[0])
6175 IF X0$(3,3)<>"CEI" THEN GOTO 6185
6180 IF W[0]>0 AND X0>0 THEN LET X0=0 ELSE IF W[0]<0 AND X0<0 THEN LET X0=0
6182 GOTO 6190
6185 IF W[0]>0 AND X0<0 THEN LET X0=0 ELSE IF W[0]<0 AND X0>0 THEN LET X0=0
6190 LET W[2]=X0
6195 GOTO 6350
6200 REM " --- Table
6210 LET W[2]=0
6220 IF X0$(3,3)="COK" THEN GOSUB OKLAHOMA2
6270 FOR X=0 TO 10
6280 IF X0<=I[X] THEN EXITTO 6320
6290 IF I[X]=0 THEN EXITTO 6320
6300 NEXT X
6320 LET X=X-1
6325 IF X<0 THEN LET X=0
6327 IF Y0$(13,2)="CT" THEN GOSUB CONN; GOTO 6349
6330 LET W[2]=J[X]+K[X]*(X0-I[X])/100,X2=0
6332 IF Y0$(13,2)="PR" THEN LET W[2]=X0*K[X]/100-J[X]
6340 IF Y0$(13,2)="CA" THEN GOSUB CALTAXCR
6342 IF Y0$(13,2)="IA" THEN GOSUB IOWA
6348 LET X0=W[2]
6349 RETURN 
6350 IF ABS(X2)>ABS(W[2]) THEN LET X2=W[2]
6360 LET REGTAX=(W[2]-X2)/NUM(T1$(POS(A0$(25,1)=T2$)*3-2,3)),SUPTAX=T[T,2]*X[0]/100,W[2]=REGTAX+SUPTAX
6370 IF Y0$(22,1)="N" THEN GOTO 6480
6380 IF Y0$(22,1)="R" AND Y0$(23,1)="A" THEN LET W[2]=Z[0]*SGN(W[0])
6390 IF Y0$(22,1)="R" AND Y0$(23,1)="P" THEN LET W[2]=Z[0]*W[0]/100
6410 IF Y0$(22,1)="A" AND Y0$(23,1)="A" THEN LET W[2]=W[2]+Z[0]*SGN(W[0])
6420 IF Y0$(22,1)="A" AND Y0$(23,1)="P" THEN LET W[2]=W[2]+Z[0]*W[0]/100
6480 IF X0$(24,1)="T" THEN LET W[0]=W[0]+T[T,2]
6490 RETURN 
6500 REM " --- Rate
6510 LET W[0]=0,W[1]=0,W[2]=0,T=NUM(X0$(31,2))
6520 IF X0$(24,1)="N" THEN RETURN 
6530 IF X0$(24,1)<>"F" THEN GOTO 6600
6540 LET W[2]=Z[0]
6550 IF W[2]=0 THEN LET W[2]=X[0]
6560 IF X0$(3,3)="CPO" THEN GOSUB PAOPT
6565 IF SGN(T[T,0])<0 THEN LET W[2]=MIN(W[2],Y[13]+Y[14]+Y[15]+Y[16]+X3),X[2]=0
6570 IF X[2]<>0 THEN IF X[2]<W[2]+Y[13]+Y[14]+Y[15]+Y[16]+X3 THEN LET W[2]=X[2]-Y[13]-Y[14]-Y[15]-Y[16]-X3
6575 LET W[2]=W[2]*SGN(T[T,0])
6595 GOTO 6690
6600 IF X0$(24,1)="D" THEN LET W[0]=T[T,0]+T[T,2]
6610 IF X0$(24,1)="U" THEN LET W[0]=T[T,1]
6612 IF X0$(24,1)="D" THEN IF X0$(30,1)="R" THEN LET W[0]=T[T,3]
6614 IF X0$(24,1)="D" THEN IF X0$(30,1)="P" THEN LET W[0]=T[T,0]-T[T,3]
6615 IF W[0]=0 THEN RETURN 
6620 LET W[1]=Z[0]
6630 IF W[1]=0 THEN LET W[1]=X[0]
6640 GOSUB 6700
6650 PRECISION 2
6660 LET W[2]=W[0]*W[1]
6670 IF X0$(24,1)="D" THEN LET W[2]=W[2]/100
6680 IF X[2]<>0 THEN IF X[0]*X[2]/100<W[2]+Y[13]+Y[14]+Y[15]+Y[16]+X3 THEN LET W[2]=X[0]*X[2]/100-Y[13]-Y[14]-Y[15]-Y[16]-X3
6685 PRECISION P[3]
6690 IF X0$(3,3)="Cny" THEN GOSUB NYSDI
6692 IF Y0$(12,1)="C" THEN GOTO 6370
6695 RETURN 
6700 REM " --- Limit
6710 LET X1=0,X3=0
6720 IF A0$(13,2)>"01" THEN GOSUB 6900
6725 IF X[2]<>0 THEN IF Y[7]+Y[8]+Y[9]+Y[10]+X1=X[2] THEN LET W[0]=0; GOTO 6790; REM "At limit, do no more
6730 IF X[2]<>0 THEN IF Y[7]+Y[8]+Y[9]+Y[10]+W[0]+X1>X[2] THEN LET W[0]=X[2]-Y[7]-Y[8]-Y[9]-Y[10]-X1
6790 RETURN 
6800 CALTAXCR: 
6810 READ (PRM10_DEV,KEY=N0$+"I"+Y0$(17,1)+Y0$(15,2),DOM=6890)IOL=PRM10I
6820 LET X2=0
6830 IF T[T,0]=0 THEN GOTO 6890
6840 LET X2=NUM(Y0$(18,2))
6850 IF X2>10 THEN LET X2=10
6860 LET X2=J[X2]
6880 LET X2=X2*SGN(W[0])
6890 RETURN 
6900 REM " --- Other (Check Seq)
6910 READ (PRE31_DEV2,KEY=A0$(1,12),DOM=6920)
6920 LET K$=KEY(PRE31_DEV2,END=6990)
6930 IF K$(1,12)<>A0$(1,12) THEN GOTO 6990
6940 IF K$(1,14)>=A0$(1,14) THEN GOTO 6990
6945 READ (PRE31_DEV2)IOL=PRE31A1
6960 IF V$(22,1)+V$(25,2)<>Y0$(12,3) THEN GOTO 6920
6970 LET X1=X1+X,X3=X3+Y
6980 GOTO 6920
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8070 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8099 REM " --- States
8100 IOWA: 
8150 IF NUM(Y0$(20,2))<1 THEN GOTO 8190
8160 FOR X=1 TO NUM(Y0$(20,2))
8170 IF X<3 THEN LET X2=X2+20
8180 IF X>=3 THEN LET X2=X2+40
8185 NEXT X
8188 LET X2=X2*SGN(W[0])
8190 RETURN 
8200 OKLAHOMA1: 
8210 LET OKSUP=0,OKTAXBL=0,OKTBLTOP=22560
8220 IF OKTBLTOP>=X0 THEN GOTO 8260
8230 LET OKSUP=(X0-OKTBLTOP)*.0675
8240 LET X0=OKTBLTOP
8260 LET OKTAXBL=X0
8290 RETURN 
8300 OKLAHOMA2: 
8330 LET X0=X0-.15*(OKTAXBL-NUM(Y0$(20,2))*2750-OKSTD)
8390 RETURN 
8400 STATES: REM " --- Special State Area"
8405 IF X0$(3,3)="COK" THEN GOSUB OKLAHOMA1
8410 IF Y0$(13,2)="PR" THEN LET X0=X0-D[0]
8420 IF X0$(3,3)="CMD" THEN LET D[1]=MIN(MAX(D[1]*.75,X0*.15),D[1])
8425 IF Y0$(13,2)="AR" THEN IF X0*.1<D[1] THEN LET D[1]=X0*.1
8430 IF X0$(3,3)="COK" THEN LET OKSTD=D[1],D[1]=MIN(MAX(1000,X0*.15),2000)
8435 IF X0$(3,3)="CNN" OR X0$(3,3)="CYN" THEN GOSUB NEWYORK
8440 IF X0$(3,3)="CMA" THEN IF NUM(Y0$(20,2))=0 THEN LET D[1]=0,D[0]=0 ELSE IF D[0]<X0/(1-FI_MC) THEN LET D[0]=0
8490 RETURN 
8500 NEWYORK: 
8505 LET NYCDED=0
8510 FOR I=3 TO 1 STEP -1
8520 IF X0-1*10000<0 THEN LET NYCDED=NYCDED+1000
8530 NEXT I
8540 LET D[1]=NYCDED
8545 RETURN 
8550 NYSDI: 
8555 LET X8=W[0],PER=NUM(T1$(POS(A0$(25,1)=T2$)*3-2,3))
8560 LET NYSDI=X[0]/100*X[2]
8570 IF ABS(W[2])*PER<=NYSDI THEN GOTO 8590
8575 LET W[2]=NYSDI/PER,W[0]=X[2]/PER
8580 LET W[0]=W[0]*SGN(X8),W[2]=W[2]*SGN(X8)
8590 RETURN 
8600 PAOPT: 
8610 IF X[1]<>0 AND X[1]*SGN(T[T,0])>(Y[1]+Y[2]+Y[3]+Y[4]+T[T,0]+T[T,2])*SGN(T[T,0]) THEN LET W[2]=0,X[2]=0
8645 RETURN 
8650 CONN: 
8655 LET LOWLIMIT=D[0],MAXEXMP=D[1],RATEBREAK=D[2],MINTAX=J[X],CREDIT=K[X],ANNUAL=X0,TAXABLE=ANNUAL,TAXRATE=.03,TABLESTEP=0
8660 IF ANNUAL>LOWLIMIT THEN LET TABLESTEP=1+INT((ANNUAL-LOWLIMIT)/1000)
8665 LET CTEXMP=MAXEXMP-1000*TABLESTEP
8670 IF CTEXMP>0 THEN LET TAXABLE=MAX(0,ANNUAL-CTEXMP)
8675 IF TAXABLE>RATEBREAK THEN LET TAXABLE=TAXABLE-RATEBREAK,TAXRATE=.045
8680 LET W[2]=MINTAX+TAXABLE*TAXRATE
8685 LET W[2]=W[2]*(1-CREDIT/100)
8690 LET X0=W[2],X2=0
8695 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

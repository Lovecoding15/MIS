0010 REM "POE - Purchase Order Invoice Entry (PO Selection)"
0020 REM "Program POE.EB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "15Oct96 JAL: Unable to retrieve 2nd receiver using lookup (2040,2070,2110,2125)"
0032 REM "04Nov96 Caj: Was allowing line items to be invoiced over and over for one receiver (2240,2830-2880,6300-6390,6530,6625-6635)
0033 REM "20Nov96 Caj: With negative invoices, 'This Receiver Has Been Fully Invoiced" message prevents completion of the entry (6630)
0034 REM "02Jan97 Caj: 'O' type receiver lines weren't being made available in invoice entry (6630,6635,6715,6717)
0035 REM "12Aug98 WGH: Allow invoicing more than 99 PO detail lines on one invoice (6760-6780,6910-6990)
0036 REM "01Oct98 WGH: Don't present negative receipts for invoicing once they've already been invoice (6715)
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 POT04A: IOLIST B0$(1),B1$(1),B2$,B3$,B[ALL]
0220 POT14A: IOLIST C0$(1),C1$(1),C2$(1),C3$(1),C4$(1),C5$,C6$,C[ALL]
0230 POE25A: IOLIST D0$(1),D1$(1),D2$,D[ALL]
0300 APM10C: IOLIST S0$(1),S[ALL]
0310 POE15A: IOLIST W0$(1),W1$(1),W2$,W[ALL]
0320 POM02A: IOLIST Y0$(1),Y1$(1)
0500 REM " --- Initializations"
0510 DIM W0$(23),W1$(32),W[4],B0$(22),B1$(160),B[8],S0$(32),S[2]
0520 DIM C0$(25),C1$(48),C2$(32),C3$(22),C4$(40),C[12],Y0$(4),Y1$(32)
0530 DIM D0$(23),D1$(96),D[4]
0550 LET W0$(1)=A0$,L=L0,TOTAL=0,S1=77-M1
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,1,0,L0-2,0
0720 PRINT @(8,L0-2),'SB'," Purchase Order Selection ",@(S1-8)," Total: ",'SF',TOTAL:M1$,@(0,L0-1),'CE','SB',"Seq PO #    Rec #   Ordered  Received Terms",@(71),"Total",'SF',
0900 REM " --- Display any existing lines"
0910 GOSUB 6000
0920 GOSUB 5600
0930 IF NUMBER=0 THEN GOTO 1200
0990 GOTO 4000
1000 REM " --- Overflow"
1010 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L0),'LD',
1020 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1110 LET K$=KEY(POE15_DEV,END=1200)
1120 IF POS(A0$=K$)<>1 THEN GOTO 1200
1130 READ (POE15_DEV)IOL=POE15A
1190 GOTO 1330
1200 REM " --- Sequence Number"
1220 LET NUMBER=NUM(W0$(21))+INCREMENT
1230 LET V4$="Enter A 3 Digit Sequence Number"
1240 LET V0$="Z",V1$="KERC",V2$=STR(NUMBER),V3$="",V0=3,V1=0,V2=L,I0=0
1250 GOSUB 7000
1260 IF V3=4 THEN PRINT @(V1,L),FILL(3),; GOTO 4000
1270 IF V$="000" OR V$="" THEN GOTO 1200
1300 REM " --- Display record"
1310 LET W0$(21)=V$
1320 FIND (POE15_DEV,KEY=W0$,DOM=1400)IOL=POE15A
1330 IF W0$<=K9$ THEN GOSUB 5900
1340 GOSUB 5000
1390 GOTO 3300
1400 REM " --- New Record"
1410 DIM W1$(32),W[4]
1450 PRINT @(0,L),W0$(21,3),@(4),'CL',
1490 GOTO 2000
2000 REM " --- Purchase Order"
2010 LET V4$="Enter A Valid Purchase Order Number (<F3>=Lookup)"
2020 LET V0$="Z",V1$="C",V2$=W1$(1,7),V3$="",V0=7,V1=4,V2=L
2030 GOSUB 7000
2040 ON V3 GOTO 2060,2000,1200,2050,2000
2050 CALL "POC.LA","H",A0$(5,6),V$
2060 IF V$="" THEN GOTO 2000
2070 IF V$(1,7)=W1$(1,7) THEN GOTO 3200
2100 REM " --- Valid PO?"
2110 LET B0$(1)=N0$+A0$(5,6)+V$,K$=B0$
2120 READ (POT04_DEV,KEY=B0$,DOM=2130)
2125 GOTO 2150
2130 LET K$=KEY(POT04_DEV,END=2000)
2140 IF POS(B0$(1,15)=K$)<>1 THEN GOTO 2000
2150 LET W1$(1,7)=K$(9,7),W1$(8,7)=K$(16,7)
2160 PRINT @(V1,V2),W1$(1,7),
2190 GOTO 3200
2200 REM " --- Receiver"
2210 LET V4$="Enter A Valid Receiver Number For This Purchase Order (<F1>=All)"
2220 LET V0$="Z",V1$="C",V2$=W1$(8,7),V3$="",V0=7,V1=12,V2=L
2230 GOSUB 7000
2240 ON V3 GOTO 2300,2400,3200,2200,2200
2300 REM " --- Specific receiver"
2310 LET W1$(8,7)=V$,B0$(1)=N0$+A0$(5,6)+W1$(1,14)
2320 FIND (POT04_DEV,KEY=B0$,DOM=2200)
2390 GOTO 2500
2400 REM " --- All receivers"
2410 LET W1$(8,7)=""
2500 REM " --- Display record
2510 FIND (POE15_DEV,KEY=W0$,DOM=2600)
2520 GOSUB 5000
2590 GOTO 3200
2600 REM " --- Has this PO/Receiver already been selected?"
2610 IF SELECTION$="" THEN GOTO 2800
2620 LET LENGTH=LEN(SELECTION$),STATUS=0
2630 FOR X=1 TO LENGTH STEP 14
2640 IF FNP$(SELECTION$)="" THEN GOTO 2680
2645 LET X$=SELECTION$(X,14)
2650 IF W1$(1,14)=X$ THEN LET STATUS=11
2660 IF FNP$(X$(8))="" THEN IF W1$(1,7)=X$(1,7) THEN LET STATUS=11
2670 IF FNP$(W1$(8,7))="" THEN IF W1$(1,7)=X$(1,7) THEN LET STATUS=11
2680 NEXT X
2690 IF STATUS=0 THEN GOTO 2800
2700 REM " --- Duplicate entry"
2710 LET MESSAGE$[0]="This PO/Receiver Has Already Been Selected (<Enter>=Continue)"
2720 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
2790 GOTO 2200
2800 REM " --- Accumulate total for PO/Receiver and display"
2810 LET EXPLODE=0
2820 GOSUB 6500
2830 IF FOUNDONE THEN GOTO 2880
2840 GOSUB INVOICED
2870 LET I0=0
2875 GOTO 2890
2880 GOSUB 5000
2890 GOTO 3200
3200 REM " --- Sequence Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 2000,2200,3300
3300 REM " --- Line Correct?"
3310 LET V4$="<Enter>=Correct     <F2>=Change     <D>=Delete"
3315 IF FNP$(W1$(1,7))="" THEN LET V4$="<Enter>=Correct     <D>=Delete"
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=W0$,I0=1
3330 GOSUB 7000
3340 IF FNP$(W1$(1,7))="" THEN IF V3=2 THEN LET V3=0
3350 ON V3 GOTO 3360,3300,3200,3300,3360
3360 PRINT @(0,21),'CL',
3370 IF V$="" THEN GOTO 3400
3380 IF V$="D" THEN GOTO 3600
3390 GOTO 3300
3400 REM " --- Explode POT-14 details if necessary"
3410 FIND (POE15_DEV,KEY=W0$,DOM=3450)
3440 GOTO 3500
3450 LET EXPLODE=1
3460 GOSUB 6500
3500 REM " --- Write header"
3510 WRITE (POE15_DEV,KEY=W0$)IOL=POE15A
3520 GOSUB 6000
3530 GOSUB 5000
3540 LET L=L+1
3550 IF V3=4 THEN GOTO 4000
3590 GOTO 1000
3600 REM " --- Remove"
3690 READ (POE25_DEV,KEY=A0$,DOM=3700)
3700 REM " --- Remove supporting detail"
3710 LET K$=KEY(POE25_DEV,END=3800)
3720 IF POS(A0$=K$)<>1 THEN GOTO 3800
3730 READ (POE25_DEV)IOL=POE25A
3740 IF W1$(1,7)<>D1$(1,7) THEN GOTO 3790
3750 IF FNP$(W1$(8,7))<>"" THEN IF W1$(8,7)<>D1$(11,7) THEN GOTO 3790
3760 REMOVE (POE25_DEV,KEY=D0$)
3790 GOTO 3700
3800 REM " --- Remove header"
3810 REMOVE (POE15_DEV,KEY=W0$,DOM=3830)
3820 GOSUB 6000
3830 PRINT @(0,L),'CL',
3840 IF V3=4 THEN GOTO 4000
3890 GOTO 1000
4000 REM " --- Correct?"
4010 PRINT @(3,22),"Is The Above Correct (Y/N/List/Review/Delete/Line #/<F2>=Header)?",
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=69
4030 GOSUB 7000
4035 ON V3 GOTO 4040,4000,4300,4000,4400
4050 IF V$="L" OR V$="LIST" THEN GOTO 4800
4055 IF V$="R" OR V$="REVIEW" THEN GOTO 4900
4060 IF V$="DELETE" THEN GOTO 4200
4065 LET V=NUM(V$,ERR=4090)
4070 IF V<1 OR V>999 THEN GOTO 4090
4075 LET V$=STR(INT(V):"000")
4080 GOTO 1300
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run detail overlay"
4190 RUN "POE.EC"
4200 REM " --- Delete"
4210 PRINT @(0,L0-1),'CE',
4220 LET O0=3
4290 RUN "POE.EA"
4300 REM " --- <F2> Return to header"
4310 PRINT @(0,L0-1),'CE',
4320 LET O0=2
4390 RUN "POE.EA"
4400 REM " --- <F4> Exit"
4410 PRINT @(0,L0-1),'CE',
4420 LET O0=1
4490 RUN "POE.EA"
4800 REM " --- List"
4810 GOSUB 5600
4890 GOTO 4000
4900 REM " --- Review"
4910 READ (POE15_DEV,KEY=A0$,DOM=4920)
4920 GOSUB 5900
4990 GOTO 1000
5000 REM " --- Display"
5010 LET NUMBER=NUMBER+1,B0$(1)=N0$+A0$(5,6)+W1$(1,14),B1$(1)="",S0$(1)=""
5020 LET ORDERED$=FILL(8),RECEIVED$=FILL(8)
5025 IF FNP$(W1$(1,7))="" THEN GOTO 5100
5030 IF FNP$(W1$(8,7))="" THEN LET B0$(16)="All"
5040 FIND (POT04_DEV,KEY=B0$,DOM=5080)IOL=POT04A
5050 LET S0$(1)=N0$+"C"+B1$(38,2)+"(Not On File)"
5060 LET ORDERED$=FNB6$(B1$(5,6)),RECEIVED$=FNB6$(B1$(29,6))
5070 FIND (APM10_DEV,KEY=S0$(1,5),DOM=5080)IOL=APM10C
5080 PRINT @(0,L),W0$(21,3),@(4),B0$(9,7),@(12),B0$(16,7),@(20),ORDERED$,@(29),RECEIVED$,@(38),S0$(4,2)," ",S0$(6,20),@(S1),W[0]:M1$,
5090 RETURN 
5100 REM " --- Non-PO charges"
5110 PRINT @(0,L),W0$(21,3),@(4),"Other Charges",@(S1),W[0]:M1$,
5190 RETURN 
5600 REM " --- List"
5610 LET NUMBER=0
5620 GOSUB 5900
5640 READ (POE15_DEV,KEY=A0$,DOM=5650)
5650 LET K$=KEY(POE15_DEV,END=5790)
5660 IF POS(A0$=K$)<>1 THEN GOTO 5790
5670 READ (POE15_DEV)IOL=POE15A
5690 IF L<=L9 THEN GOTO 5750
5700 REM " --- Full screen"
5710 LET V4$="Press <Enter> To Continue:"
5720 LET V0$="S",V1$="CE",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5730 GOSUB 7000
5740 IF V3=4 THEN GOTO 5790
5745 GOSUB 5900
5750 GOSUB 5000
5760 LET L=L+1,K9$=W0$
5770 GOTO 5650
5790 RETURN 
5900 REM " --- Clear Screen"
5910 PRINT @(0,L0),'CE',
5920 LET L=L0
5990 RETURN 
6000 REM " --- Accumulate total for invoice"
6010 LET TOTAL=0,X$=W0$,Y0$(1)="",SELECTION$=""
6090 READ (POE15_DEV,KEY=A0$,DOM=6100)
6100 REM " --- Read next line item"
6110 LET K$=KEY(POE15_DEV,END=6200)
6120 IF POS(A0$=K$)<>1 THEN GOTO 6200
6130 READ (POE15_DEV)IOL=POE15A
6140 LET TOTAL=TOTAL+W[0],SELECTION$=SELECTION$+W1$(1,14)
6190 GOTO 6100
6200 REM " --- Done with total"
6210 PRINT @(S1,L0-2),TOTAL:M1$,
6280 READ (POE15_DEV,KEY=X$,DOM=6290)IOL=POE15A
6290 RETURN 
6300 REM " --- Tell 'em it's been invoiced
6305 INVOICED: 
6340 DIM MESSAGE$[0]
6350 LET MESSAGE$[0]="This receiver has been fully invoiced"
6360 CALL "SYC.XA",0,MESSAGE$[ALL],0,-1,-1,V$,V3
6390 RETURN 
6500 REM " --- Explode POT-14 detail records into POE-25"
6510 LET B0$(1)=N0$(1,2)+A0$(5,6)+W1$(1,7),B1$(1)=""
6520 IF FNP$(W1$(8,7))<>"" THEN LET B0$(16)=W1$(8,7)
6530 LET X=LEN(FNP$(B0$)),D0$(1)=A0$,W[0]=0,FOUNDONE=0
6590 READ (POT14_DEV,KEY=B0$,DOM=6600)
6600 REM " --- Read next detail"
6610 LET K$=KEY(POT14_DEV,END=6900)
6620 IF POS(B0$(1,X)=K$)<>1 THEN GOTO 6900
6625 READ (POT14_DEV)IOL=POT14A
6640 IF Y0$(1,4)=N0$+C1$(1,2) THEN GOTO 6700
6650 LET Y0$(1)=N0$+C1$(1,2),Y1$(1)="",SEQ=0
6660 FIND (POM02_DEV,KEY=Y0$,DOM=6700)IOL=POM02A
6700 REM " --- Determine next sequence number"
6710 IF POS(Y1$(21,1)="VM") THEN GOTO 6600
6715 IF Y1$(21,1)<>"O" THEN IF (C[7]>=0 AND C[7]<=C[8]) OR (C[7]<0 AND C[7]>=C[8]) THEN GOTO 6890
6717 LET FOUNDONE=1
6720 IF EXPLODE=0 THEN GOTO 6800
6730 READ (POE25_DEV,KEY=D0$(1,20)+$FF$,DOM=6740)
6740 LET K$=KEYP(POE25_DEV,END=6750)
6750 IF POS(A0$=K$)=1 THEN LET SEQ=NUM(K$(21,3))
6760 LET SEQ=SEQ+INCDETAIL
6770 IF SEQ>999 THEN GOTO 6950
6780 LET D0$(21)=STR(SEQ:"000")
6800 REM " --- Write POE-25 detail record"
6810 LET D1$(1)=C0$(9,7)+C0$(23,3)+C0$(16,7),D1$(18,2)=C1$(1,2)
6820 LET D1$(20)=C4$,D[0]=C[1],D[1]=C[7],D[2]=C[1]
6840 IF Y1$(21,1)="O" THEN LET D[1]=1
6850 LET W[0]=W[0]+(D[0]*D[1])
6860 PRECISION I[1]
6870 IF EXPLODE THEN WRITE (POE25_DEV,KEY=D0$)IOL=POE25A
6880 PRECISION 2
6890 GOTO 6600
6900 REM " --- Explosion completed"
6910 RETURN 
6950 REM " --- Too Many PO Detail Lines
6955 DIM MESSAGE$[1]
6960 LET MESSAGE$[0]="There are too many PO detail lines to include on this invoice."
6970 LET MESSAGE$[1]="                   <Enter> = Continue"
6980 CALL "SYC.XA",1,MESSAGE$[ALL],1,-1,-1,V$,V3
6990 EXITTO 3600
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

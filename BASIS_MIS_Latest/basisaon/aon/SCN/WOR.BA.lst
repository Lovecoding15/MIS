0010 REM "WOS - Work Order Pick List Print - Create Work File          "
0020 REM "Program:  WOR.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "05Nov96 JJD: Set mask size to accomodate large numbers. (600) (Needs associated 05Nov96 WOR.BB SCN)"
0032 REM "31Mar98 KJS: Another channel for WOE-04, for WOR.BB 31Mar98 SCN (110,140,150,180)"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=15
0115 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="WOE-04",FILES$[2]="WOE-13",FILES$[3]="WOE-23"
0125 LET FILES$[4]="WOE-01",FILES$[5]="WOW-13",FILES$[6]="IVM-02"
0130 LET FILES$[7]="SYS-01",FILES$[8]="ARM-01",FILES$[9]="IVM-01"
0135 LET FILES$[10]="WOE-15",FILES$[11]="WOE-25",FILES$[12]="WOM-10"
0140 LET FILES$[13]="IVM-10",FILES$[14]="WOE-22",FILES$[15]="WOE-04"
0145 LET OPTIONS$[5]="C",OPTIONS$[8]="F"
0150 CALL "SYC.DA",1,1,15,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE04_DEV=CHANNELS[1],WOE13_DEV=CHANNELS[2],WOE23_DEV=CHANNELS[3]
0165 LET WOE01_DEV=CHANNELS[4],WOW13_DEV=CHANNELS[5],IVM02_DEV=CHANNELS[6]
0170 LET SYS01_DEV=CHANNELS[7],ARM01_DEV=CHANNELS[8],IVM01_DEV=CHANNELS[9]
0175 LET WOE15_DEV=CHANNELS[10],WOE25_DEV=CHANNELS[11],WOM10_DEV=CHANNELS[12]
0180 LET IVM10_DEV=CHANNELS[13],WOE22_DEV=CHANNELS[14],WOE04_DEV2=CHANNELS[15]
0200 REM " --- IOLISTs"
0210 WOE13A: IOLIST W0$,W1$,W[ALL]
0215 WOE23A: IOLIST W0$,W1$,W[ALL]
0220 WOE22B: IOLIST Y0$,Y1$,Y[ALL]
0230 WOW13A: IOLIST PICKSORT$
0240 IVM02A: IOLIST *,I1$
0260 WOS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 ARS01A: IOLIST *,*,C2$,*,*,C3$
0320 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0450 DIM P[4],DESC[3]
0460 LET P[0]=NUM(I2$(1,2)),IVS01_PRECISION=NUM(I2$(5,1))
0470 IF P3$(2,1)<>"Y" THEN GOTO 0500
0480 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=0490)IOL=ARS01A
0490 LET C0=NUM(C2$(1,2))
0500 REM " --- Initialize Data "
0510 LET H0=80,H=0,HEADINGS=2,WHEN$=F0$(7,3),CLOCK$=""
0520 DIM HEADINGS$[HEADINGS],PICKSORT$(27)
0530 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0540 LET L9=59,L=L9+1
0550 DIM J0$(20,"0"),J$(80,"-"),J1$(10,"_"),X0$(30),Y[13]
0560 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$)
0570 FOR X=1 TO 3
0580 LET DESC[X]=NUM(I2$((X-1)*2+9,2))
0590 NEXT X
0600 CALL "SYC.RM",M2$,M2,6,12
0610 DIM A0$(14),A1$(50+P[0]),W[2]
0700 REM " --- Background"
0710 FOR X=4 TO 8 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT @(25,3),'SB',"Sort Sequence:",@(28,5),"Work Order:",'SF',
0800 READ (WOE04_DEV,KEY=N0$+"  "+"C",DOM=0810)
0810 LET K$=KEY(WOE04_DEV,END=0840)
0820 IF K$(1,5)<>N0$+"  "+"C" THEN GOTO 0840
0830 GOTO 1000
0840 DIM MESSAGE$[1]
0850 LET MESSAGE$[0]="There Are No Pick Lists To Print. <Enter> To Continue."
0860 CALL "SYC.XA",0,MESSAGE$[ALL],0,22,-1,V$,V3
0870 GOTO 9900
1000 REM " --- Sort Sequence"
1010 LET V0$="S",V1$="CR",V2$=R1$,V3$="LM",V4$="L = Location Sequence, M = Material Sequence",V0=1,V1=40,V2=3
1020 GOSUB 7000
1030 IF V3=4 THEN GOTO 9900
1040 LET R1$=V$
4000 REM " --- Run Overlay
4010 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct? (Y/N): ",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V3=4 THEN GOTO 9900
4040 IF V$="N" THEN GOTO 1000
4050 IF V$<>"Y" THEN GOTO 4000
4060 CALL "SYC.NB","Now Sorting: ",15,COLUMN
4070 GOSUB 6000
4080 RUN "WOR.BB"
6000 REM " --- Create Cross Reference File "
6010 LET K$=KEY(WOE04_DEV,END=6180)
6020 READ (WOE04_DEV,KEY=K$)
6030 IF K$(1,5)<>N0$+"  "+"C" THEN GOTO 6180
6040 READ (WOE13_DEV,KEY=K$(1,4)+K$(6)+"000",DOM=6000)
6050 READ (WOE23_DEV,KEY=K$(1,4)+K$(6)+"000",DOM=6060)
6060 LET K2$=KEY(WOE23_DEV,END=6000)
6070 IF K2$(1,11)<>K$(1,4)+K$(6,7) THEN GOTO 6000
6080 READ (WOE23_DEV,KEY=K2$)IOL=WOE23A
6090 DIM Y1$(50)
6100 FIND (WOE22_DEV,KEY=W0$(1,11)+"B"+W0$(12,3),DOM=6110)IOL=WOE22B
6110 DIM I1$(10)
6120 IF R1$="L" THEN FIND (IVM02_DEV,KEY=N0$+W1$(19,22),DOM=6130)IOL=IVM02A
6130 LET K4$=W0$(1,11)+Y1$(47,3)+I1$(1,10)+W0$(12,3)
6140 PRINT @(COLUMN,11),W0$(5,7),"  ",W0$(12,3)
6150 LET PICKSORT$=K4$
6160 WRITE (WOW13_DEV,KEY=K4$)IOL=WOW13A
6170 GOTO 6060
6180 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

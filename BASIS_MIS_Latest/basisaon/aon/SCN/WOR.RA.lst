0010 REM "WOR - Closed Recurring Work Order Summary Register"
0020 REM "Program WOR.RA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "20Jan99 WGH: Clear and lock GLW-11 Register Transaction Detail file (670)
0032 REM "18Mar99 KJS: Y2K year processing (440,447)
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=16
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-04",FILES$[2]="WOE-01",FILES$[3]="WOE-02"
0120 LET FILES$[4]="WOE-22",FILES$[5]="WOE-32",FILES$[6]="WOT-01"
0125 LET FILES$[7]="WOT-21",FILES$[8]="WOT-31",FILES$[9]="WOW-07"
0130 LET FILES$[10]="SYS-01",FILES$[11]="IVM-02",FILES$[12]="IVM-01"
0140 LET FILES$[13]="WOM-10"
0145 LET OPTIONS$[9]="C"
0150 CALL "SYC.DA",1,1,13,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE04_DEV=CHANNELS[1],WOE01_DEV=CHANNELS[2],WOE02_DEV=CHANNELS[3]
0165 LET WOE22_DEV=CHANNELS[4],WOE32_DEV=CHANNELS[5],WOT01_DEV=CHANNELS[6]
0170 LET WOT21_DEV=CHANNELS[7],WOT31_DEV=CHANNELS[8],WOW07_DEV=CHANNELS[9]
0180 LET SYS01_DEV=CHANNELS[10],IVM02_DEV=CHANNELS[11],IVM01_DEV=CHANNELS[12]
0185 LET WOM10_DEV=CHANNELS[13]
0200 REM " --- IOLISTs"
0210 WOE01A: IOLIST A0$,A1$,A[ALL]
0240 WOW07A: IOLIST D0$,D[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0265 IVS01A: IOLIST *,*,P9$
0270 WOS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0275 ARS01A: IOLIST *,*,*,R3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0310 WOREQA: IOLIST W0$,W1$,W[ALL]
0315 ARM10D: IOLIST *,I6$(1)
0320 WOTRAN: IOLIST W0$,W1$,W[ALL]
0325 WOM10A: IOLIST X0$,X1$
0380 IVM02A: IOLIST *,I1$
0390 IVM01A: IOLIST *,B1$(1),*,*,*,*,I6$
0400 REM " --- Parameters"
0405 DIM P[2],G[4],INFO$[20],DESC[3]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$,SOURCE$=PGM(-2)
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0435 FOR X=0 TO 4
0440 LET G[X]=NUM(G2$(X*2+1,2),ERR=0441)
0445 NEXT X
0447 LET G[2]=FNYY_YEAR(G2$(5,2))
0450 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P4$(1,2)),P[2]=FNYY_YEAR(P4$(3,2))
0455 LET P1=NUM(P9$(5,1)),R9$="N",IVS01_PRECISION=NUM(P9$(5,1))
0460 CALL "SYC.VA","AR",INFO$[ALL]
0465 IF INFO$[20]<>"Y" THEN GOTO 0480
0470 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=0480)IOL=ARS01A
0475 LET R9$=R3$(11,1)
0480 LET GL$="N",STATUS=0
0485 CALL "GLC.BA",ERR=0490,SOURCE$,N2$,GLW11$,GL$,STATUS
0490 IF STATUS THEN GOTO 9900
0495 FOR X=1 TO 3; LET DESC[X]=NUM(P9$((X-1)*2+9,2)); NEXT X
0500 REM " --- Init Data"
0510 DIM HEADINGS$[3],J$(80,"-"),J0$(10,"0"),J1$(40),A[11],U[7],B1$(60)
0530 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0535 LET H0=132,HEADINGS=3,H=0,WHEN$=F0$(7,3),CLOCK$="",L9=59,L=L9+1
0540 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),G5=LEN(G5$)+2
0545 IF G5<10 THEN LET G5=10
0550 IF H0-FNW(M1,M2,M3)>16 THEN GOTO 0600
0560 LET XM1=M1-1,XM2=M2-1,XM3=M3-1
0565 CALL "SYC.RM",M1$,M1,8,XM1
0570 IF H0-FNW(M1,M2,M3)>16 THEN GOTO 0600
0575 CALL "SYC.RM",M2$,M2,7,XM2
0580 IF H0-FNW(M1,M2,M3)>16 THEN GOTO 0600
0585 CALL "SYC.RM",M3$,M3,6,XM3
0590 IF H0-FNW(M1,M2,M3)>16 THEN GOTO 0600
0595 GOTO 0560
0600 REM " --- Additional headings"
0610 DIM M9$(M1*4,"-"),M8$(G5*3,"-"),J9$(M1*8,"-")
0620 LET M9$(1,1)="+",M9$(LEN(M9$))="+",M8$(1,1)="+",M8$(LEN(M8$))="+"
0630 LET M9$(INT(LEN(M9$)/2)-5,10)=" Variance ",M9=FNW(M1,M2,M3)
0640 LET M8$(INT(LEN(M8$)/2)-5,10)=" Variance "
0650 REM " --- Additional File Opens"
0660 IF R9$="Y" THEN LET FILES$[14]="ARM-10"
0670 IF GL$="Y" THEN LET FILES$[15]=GLW11$,FILES$[16]="GLM-01",OPTIONS$[15]="CL"
0675 CALL "SYC.DA",1,14,16,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0680 IF STATUS THEN GOTO 9900
0690 LET ARM10_DEV=CHANNELS[14],GLW11_DEV=CHANNELS[15],GLM01_DEV=CHANNELS[16]
0800 REM " --- Options"
0810 LET TITLE=0,PROMPT$="Do You Want To Print The "+N3$,PROMPT=2
0820 CALL "SYC.YN",TITLE,PROMPT$,PROMPT,V$,V3
0830 IF V$<>"Y" THEN GOTO 9900
0900 REM " --- Final Initializations"
0910 CALL "SYC.GA",7,1,"","",STATUS
0920 IF STATUS THEN GOTO 9900
0930 CALL "SYC.NB","Printing",7,COLUMN
1000 REM " --- Init Read"
1010 PRECISION IVS01_PRECISION
1020 READ (WOE04_DEV,KEY=N0$+"  B",DOM=1100)
1100 REM " --- Main Read"
1120 LET K$=KEY(WOE04_DEV,END=4000)
1140 IF K$(1,2)<>N0$ THEN GOTO 4000
1150 IF K$(5,1)<>"B" THEN GOTO 4000
1160 READ (WOE04_DEV,KEY=K$)
1200 REM " --- Breaks"
1220 READ (WOE01_DEV,KEY=K$(1,4)+K$(6),DOM=1120)IOL=WOE01A
1230 IF A0$(14,1)<>"R" THEN GOTO 1120
1300 PRINT @(COLUMN,11),A0$(5,7),
1320 DIM B[4],C[4],T[7],W[11]
1400 REM " --- Additional Reads
1405 LET WOREQ_DEV=WOE02_DEV
1410 READ (WOREQ_DEV,KEY=A0$(1,11),DOM=1420)
1420 LET D0$=N0$+A0$(12,2)
1425 DIM D[7],Y0$(40),Y1$(100,"0")
1430 FIND (WOW07_DEV,KEY=D0$,DOM=1440)IOL=WOW07A
1440 FIND (WOM10_DEV,KEY=N0$+"A"+D0$(3),DOM=1500)IOL=WOM10A
1445 LET Y0$=X0$,Y1$=X1$
1500 REM " --- Calculate Standards
1505 READ (WOREQ_DEV,KEY=A0$(1,11),DOM=1510)
1510 LET K3$=KEY(WOREQ_DEV,END=1550)
1515 IF K3$(1,11)<>A0$(1,11) THEN GOTO 1550
1520 READ (WOREQ_DEV,KEY=K3$)IOL=WOREQA
1525 LET X=POS(W0$(12,1)="BC")+1
1530 IF X>1 THEN LET B[X]=B[X]+W[3]; GOTO 1540
1535 LET B[1]=B[1]+W[2]*W[6],B[4]=B[4]+W[3]-W[2]*W[6]; REM "Split Direct & OH
1540 LET B[0]=B[0]+W[3]
1545 GOTO 1510
1550 IF WOREQ_DEV=WOE02_DEV THEN LET WOREQ_DEV=WOE22_DEV; GOTO 1505
1555 IF WOREQ_DEV=WOE22_DEV THEN LET WOREQ_DEV=WOE32_DEV; GOTO 1505
1600 REM " --- Calculate Actuals
1601 LET WOTRAN_DEV=WOT01_DEV
1605 READ (WOTRAN_DEV,KEY=A0$(1,11),DOM=1610)
1610 LET K4$=KEY(WOTRAN_DEV,END=1655)
1615 IF K4$(1,11)<>A0$(1,11) THEN GOTO 1655
1620 DIM W[11]
1625 READ (WOTRAN_DEV,KEY=K4$)IOL=WOTRAN
1630 LET X=POS(W0$(15,1)="MS")+1
1635 IF X>1 THEN LET C[X]=C[X]+W[2]; GOTO 1645
1640 LET C[1]=C[1]+W[0]*W[3],C[4]=C[4]+W[2]-W[0]*W[3]; REM "Split Direct & OH
1645 LET C[0]=C[0]+W[2]
1650 GOTO 1610
1655 IF WOTRAN_DEV=WOT01_DEV THEN LET WOTRAN_DEV=WOT21_DEV; GOTO 1605
1660 IF WOTRAN_DEV=WOT21_DEV THEN LET WOTRAN_DEV=WOT31_DEV; GOTO 1605
1700 REM " --- Calculate Close Value, Variances, etc.
1705 PRECISION 2
1710 LET T[0]=C[0]*1,T[1]=A[3]*1,T[2]=T[0]-T[1],T[3]=A[4]*A[5]
1720 IF A1$(94,1)="Y" THEN LET T0=0 ELSE LET T0=A[0]-(A[2]+A[4])
1730 IF A1$(94,1)="N" THEN GOTO 1900
1740 IF Y0$(27,1)="A" THEN LET T[3]=T[2]; GOTO 1900
1780 IF A[0]=A[2]+A[4] OR B[0]=0 OR A1$(95,1)="N" THEN LET Y0=A[4]*A[5]+A[3]; GOTO 1860
1790 PRECISION IVS01_PRECISION
1800 REM " --- Pro-Rate Standards If Needed
1810 IF A[0]<>0 THEN LET Y0=B[0]*(A[2]+A[4])/A[0] ELSE LET Y0=0
1820 IF Y0=B[0] THEN GOTO 1860
1825 IF B[0]=0 THEN LET B[1]=0,B[2]=0,B[3]=0; GOTO 1850
1830 LET B[1]=B[1]*Y0/B[0]
1835 LET B[2]=B[2]*Y0/B[0]
1840 LET B[3]=B[3]*Y0/B[0]
1850 LET B[4]=Y0-(B[1]+B[2]+B[3])
1860 REM " --- Now Calculate Variances
1865 PRECISION 2
1870 LET T[4]=B[1]*1-C[1]*1,T[5]=B[2]*1-C[2]*1,T[6]=B[3]*1-C[3]*1
1875 LET T[0]=T[0]*1,T[1]=T[1]*1,T[2]=(T[0]-T[1])*1,T[3]=A[4]*A[5]
1878 LET T[7]=Y0*1-T[0]-(T[4]+T[5]+T[6]); REM "All left overs in OH Vnce
1900 DIM X1$(30)
1930 LET X1$=A1$(1,30)
1980 PRECISION IVS01_PRECISION
2000 REM " --- Print"
2020 IF L>L9 THEN GOSUB 5200
2040 PRINT (7)A0$(5,7),@(9),A0$(12,2),@(14),A0$(14,1),@(17),FNB$(A1$(91)),"  ",X1$
2050 PRINT (7)@(132-M9),A[0]:M2$,@(132-M9+M2),A[2]:M2$,@(132-M9+M2*2),A[4]:M2$,@(132-M9+M2*3),T0:M2$," ",A1$(94,1),"  ",A[5]:M3$,
2060 LET OFFSET=0
2070 FOR X=0 TO 6
2080 PRINT (7)@(132-M1*(8-X-OFFSET)),T[X]:M1$,
2085 IF X=4 THEN LET OFFSET=1; PRINT (7)@(132-M1*(8-X-OFFSET)),T[7]:M1$,; LET U[7]=U[7]+T[7]
2090 LET U[X]=U[X]+T[X]
2100 NEXT X
2120 PRINT (7)""
2140 LET L=L+2
2200 REM " --- Sort By Type
2220 LET D0$=N0$+A0$(12,2)
2240 FIND (WOW07_DEV,KEY=D0$,DOM=2260)IOL=WOW07A
2260 FOR X=0 TO 7
2270 LET D[X]=D[X]+T[X]
2280 NEXT X
2290 WRITE (WOW07_DEV,KEY=D0$)IOL=WOW07A
2400 REM " --- Sort By G/L
2410 IF A1$(94,1)<>"Y" THEN LET T[2]=T[3]
2420 FOR X=2 TO 7
2440 IF T[X]=0 THEN GOTO 2600
2450 IF X<4 THEN LET Y=(X-2)*10+1 ELSE IF X=4 THEN LET Y=51 ELSE IF X=7 THEN LET Y=61 ELSE LET Y=X*10+21
2460 IF X<>3 THEN LET T[X]=-T[X]
2470 LET ACCOUNT$=Y1$(Y,10),GLDATE$=A1$(91,3),AMOUNT=T[X],UNITS=0
2480 LET MEMO$=A1$(1,30),REF1$="WO "+A0$(5,7),REF2$="Whse "+A0$(54,2)
2490 LET REF3$="Type "+A0$(12,2)
2500 GOSUB GLRECAP
2600 NEXT X
2900 GOTO 1100
4000 REM " --- End"
4020 IF H=0 THEN GOSUB 5200
4040 IF L+2>L9 THEN GOSUB 5200
4060 PRINT (7)@(132-M1*8),J9$,'LF',@(132-M9),"Report Totals: ",@(132-M1*8),
4065 LET OFFSET=0
4070 FOR X=0 TO 6
4080 PRINT (7)@(132-M1*(8-X-OFFSET)),U[X]:M1$,
4085 IF X=4 THEN LET OFFSET=1; PRINT (7)@(132-M1*(8-X-OFFSET)),U[7]:M1$,
4090 NEXT X
4095 PRINT (7)""
4097 LET L=L+2
4100 REM " --- Now Print By Type
4110 DIM U[7]
4120 READ (WOW07_DEV,KEY=N0$,DOM=4130)
4130 IF L+4>L9 THEN GOSUB 5000
4160 PRINT (7)'LF','LF',@(10),"Summary By Work Order Type"
4180 LET L=L+4
4200 LET K$=KEY(WOW07_DEV,END=4400)
4220 IF POS(N0$=K$)<>1 THEN GOTO 4400
4240 READ (WOW07_DEV,KEY=K$)IOL=WOW07A
4250 DIM X0$(30),X1$(50,"0"); LET X0$(6)="*** Not On File ***"
4260 FIND (WOM10_DEV,KEY=N0$+"A"+D0$(3),DOM=4270)IOL=WOM10A
4270 IF L+2>L9 THEN GOSUB 5000
4280 PRINT (7)@(95-M1*5),D0$(3)," ",X0$(6,20),
4300 LET OFFSET=0
4310 FOR X=2 TO 6
4320 PRINT (7)@(132-M1*(8-X-OFFSET)),D[X]:M1$,
4325 LET U[X]=U[X]+D[X]
4327 IF X=4 THEN LET OFFSET=1; PRINT (7)@(132-M1*(8-X-OFFSET)),D[7]:M1$,; LET U[7]=U[7]+D[7]
4330 NEXT X
4340 PRINT (7)""
4360 LET L=L+2
4380 GOTO 4200
4400 REM " --- Report Totals Again
4410 IF L+2>L9 THEN GOSUB 5000
4420 PRINT (7)@(132-M1*6),J9$(1,M1*6),'LF',@(98-M1*6),"Report Totals: ",
4430 LET OFFSET=0
4440 FOR X=2 TO 6
4450 PRINT (7)@(132-M1*(8-X-OFFSET)),U[X]:M1$,
4455 IF X=4 THEN LET OFFSET=1; PRINT (7)@(132-M1*(8-X-OFFSET)),U[7]:M1$,
4460 NEXT X
4480 PRINT (7)""
4900 REM " --- Run next overlay"
4910 LET UPDATE$="WOU.RA",WIDTH=H0,PAGE=H
4980 IF GL$="Y" THEN RUN "GLR.XA"
4990 RUN UPDATE$
5000 REM " --- Heading"
5010 LET L=0
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS-1,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5090 RETURN 
5200 REM " --- Heading
5210 IF L+2>L9 THEN GOSUB 5000
5220 PRINT (7)"WO #",@(8),"Typ Cat",@(17),"Close Date",@(126-M1*7),"Total",@(124-M1*5),"Current"
5230 PRINT (7)@(127-M9+M2),"Prod",@(126-M9+M2*2),"Prior",@(127-M9+M2*3),"This",@(126-M9+M2*4),"Still",@(127-M1*8),"Unit",@(125-M1*7),"Actual",@(126-M1*6),"Prior",@(124-M1*5),"Work In",@(125-M1*4),"Closed",@(132-M1*4),M9$
5240 PRINT (7)@(128-M9+M2),"Qty",@(126-M9+M2*2),"Close",@(126-M9+M2*3),"Close",@(127-M9+M2*4),"Open",@(132-M9+M2*4),"Cmp?",@(127-M1*8),"Cost",@(127-M1*7),"Cost",@(125-M1*6),"Closed",@(124-M1*5),"Process",@(126-M1*4),"Value",@(125-M1*3),"Direct",@(125-M1*2),"Ovrhd",@(125-M1),"Mat'ls",@(127),"Subs",'LF'
5250 RETURN 
6900 REM " --- Standard G/L Recap Routine"
6910 GLRECAP: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLW11_DEV,GLT05_DEV,ACCOUNT$,GLDATE$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNW(Q1,Q2,Q3)=Q1*8+Q2*4+Q3+5
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

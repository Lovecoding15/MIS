0010 REM "GLM - G/L Account Maintenance (Transaction Inquiry)"
0020 REM "Program GLM.AD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "07Jun00 JAL: Display year in correct format for 2000+ (6320)"
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0200 REM " --- IOLIST's"
0210 GLM01A: IOLIST A0$(1),A1$(1)
0215 GLM02A: IOLIST X0$(1),A[ALL],U[ALL]
0220 GLT06A: IOLIST D0$(1),D1$(1),D2$,D[ALL]
0230 GLM03A: IOLIST C0$(1),C1$,C[ALL]
0400 REM " --- Parameters"
0410 LET A0$(1)=N0$+ACCOUNT$
0420 FIND (GLM01_DEV,KEY=A0$(1,2+P[4]),DOM=4000)IOL=GLM01A
0450 LET WIDTH=80,HEIGHT=18,WIN_X=0,WIN_Y=L0-1,MAXROW=HEIGHT-5,PAGES=20
0500 REM " --- Initializations"
0510 DIM D0$(26+P[4]),D1$(64),D[4],C0$(32),C[2],JT[1]
0520 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),DASH$(WIDTH-2,"-")
0530 DIM AS[(MP+1)*3],US[(MP+1)*3],BALANCES[2]
0550 LET AMOUNT=0,UNITS=0,T0$="",T1$="",DONE=0,L1=0,FKEY=0
0570 LET STARTING$=BEGYEAR$+BEGPER$,ENDING$=ENDYEAR$+ENDPER$
0575 IF BEGPER$="" THEN LET STARTING$=BEGYEAR$+"01"
0580 IF ENDPER$="" THEN LET ENDING$=ENDYEAR$+STR(P[0]:"00")
0600 REM " --- Print positions"
0610 DIM O[6]
0620 LET O[6]=WIDTH-2
0630 IF P4$(3,1)="Y" THEN LET O[6]=O[6]-M2
0640 LET O[5]=O[6]-M1,O[0]=2,O[1]=O[0]+4,O[2]=O[1]+3
0650 LET O[3]=O[2]+9,O[4]=O[3]+3
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,INQUIRY$,NAME$
0720 LET HEADING$(O[0])="Period",HEADING$(O[2]+2)="Date"
0725 LET HEADING$(O[3])="Journal/Memo/Reference"
0730 LET HEADING$(O[4]+22)="Audit #",HEADING$(O[5]+M1-7)="Amount"
0740 IF P4$(3,1)="Y" THEN LET HEADING$(WIDTH-8)="Units"
0750 LET FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0760 PRINT @(0,0),'CS','SB','BR',HEADING$,'ER','SF',
0770 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
0800 REM " --- Determine which years for GLM-02 records"
0810 LET BEGPER=0,ENDPER=P[0],YEAR2=YEAR1+1,YEAR3=YEAR2+1
0820 LET BEGYEAR=FNYY_YEAR(BEGYEAR$),ENDYEAR=FNYY_YEAR(ENDYEAR$)
0825 IF BEGPER$<>"" THEN LET BEGPER=NUM(BEGPER$)-1
0830 IF ENDPER$<>"" THEN LET ENDPER=NUM(ENDPER$)
0835 LET RECORDS$="204",RECORDS=LEN(RECORDS$),BEGOFFSET=0,ENDOFFSET=0
0840 IF BEGYEAR=YEAR2 THEN LET BEGOFFSET=1
0845 IF BEGYEAR=YEAR3 THEN LET BEGOFFSET=2
0850 IF ENDYEAR=YEAR2 THEN LET ENDOFFSET=1
0855 IF ENDYEAR=YEAR3 THEN LET ENDOFFSET=2
0860 LET BEGINDEX=MP*BEGOFFSET+BEGOFFSET,ENDINDEX=MP*ENDOFFSET+ENDOFFSET
0900 REM " --- Position GLT-06"
0910 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,5]
0920 LET LINE=1,L=1,X0=0,X1=1,PAGE=1
0930 GOSUB 6500
0970 LET D0$(1)=A0$+STARTING$
0990 READ (GLT06_DEV,KEY=D0$,DOM=1000)
1000 REM " --- Read next record"
1010 LET K$=KEY(GLT06_DEV,END=3000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 3000
1030 IF K$(3,P[4])<>A0$(3,P[4]) THEN GOTO 3000
1035 IF K$(3+P[4],4)>ENDING$ THEN GOTO 3000
1040 READ (GLT06_DEV)IOL=GLT06A
1090 IF JOURNAL$<>"" THEN IF D0$(13+P[4],2)<>JOURNAL$ THEN GOTO 2900
1100 REM " --- Detail or Summary?"
1105 IF D0$(3+P[4],4)=T1$ THEN GOTO 1120
1110 GOSUB 6200
1115 IF FKEY=4 THEN GOTO 4000
1120 IF D0$(7+P[4],8)=T0$ THEN GOTO 1150
1130 GOSUB 6000
1140 IF FKEY=4 THEN GOTO 4000
1180 LET JT[0]=JT[0]+D[0],JT[1]=JT[1]+D[1]
1190 IF DETAIL$<>"D" THEN GOTO 1700
1200 REM " --- How many lines are needed?"
1210 LET NEEDED=1,L1=L1+1
1220 IF FNP$(D1$(1,30))<>"" THEN LET NEEDED=NEEDED+1
1230 IF FNP$(D1$(31,30))<>"" THEN LET NEEDED=NEEDED+1
1300 REM " --- Display page?"
1310 IF L+NEEDED>MAXROW THEN GOSUB 5400
1320 IF FKEY=4 THEN GOTO 4000
1400 REM " --- Line 1 (Period/Date/Journal and Amounts)"
1410 DIM BUF$(WIDTH-2),C0$(32),C[2]
1420 LET C0$(1,4)=D0$(1,2)+D0$(13+P[4],2)
1430 FIND (GLM03_DEV,KEY=C0$(1,4),DOM=1440)IOL=GLM03A
1440 LET PERIOD=NUM(D0$(5+P[4],2),ERR=1460)
1450 LET BUF$(O[0])=P3$((PERIOD-1)*3+235,3)
1460 LET BUF$(O[1])=FNYY21_YY$(D0$(3+P[4],2)),BUF$(O[2])=FNB6$(D0$(7+P[4],6))
1470 LET BUF$(O[3])=C0$(3,2),BUF$(O[4])=C0$(5,20)
1475 LET BUF$(O[4]+22)=D0$(15+P[4],7),BUF$(O[5])=STR(D[0]:M1$)
1480 IF P4$(3,1)="Y" THEN IF D[1]<>0 THEN LET BUF$(O[6])=STR(D[1]:M2$)
1490 LET LLIST$[PAGE,X1,1]=BUF$,X0=X0+1,X1=X1+1,L=L+1
1500 REM " --- Line 2 Memo"
1510 DIM BUF$(WIDTH-2)
1520 IF FNP$(D1$(31,30))="" THEN GOTO 1600
1530 LET BUF$(O[4])=D1$(31,30)
1590 LET LLIST$[PAGE,X1,1]=BUF$,X1=X1+1,L=L+1
1600 REM " --- Line 3 Ref 1/Ref 2/Ref 3"
1610 DIM BUF$(WIDTH-2)
1620 IF FNP$(D1$(1,30))="" THEN GOTO 1700
1630 LET BUF$(O[4])=D1$(1,10)+" "+D1$(11,10)+" "+D1$(21,10)
1690 LET LLIST$[PAGE,X1,1]=BUF$,X1=X1+1,L=L+1
1700 REM " --- Accumulate totals"
1710 LET AMOUNT=AMOUNT+D[0],UNITS=UNITS+D[1]
1720 LET PT[0]=PT[0]+D[0],PT[1]=PT[1]+D[1]
2900 REM " --- Loop back for next record"
2990 GOTO 1000
3000 REM " --- No more records"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3035 LET MESSAGE$[0]="No transactions found (<Enter>=Continue)"
3040 CALL "SYC.XA",2,MESSAGE$[ALL],0,V2,V1,V$,V3
3090 GOTO 4000
3500 REM " --- Display last page"
3520 LET DONE=1
3530 GOSUB 6200
3540 IF FKEY=4 THEN GOTO 4000
3600 REM " --- Insert totals on last page"
3610 IF L+3>MAXROW THEN GOSUB 5400
3615 IF FKEY=4 THEN GOTO 4000
3635 DIM BUF$(WIDTH-2)
3640 LET BUF$(1)="",BUF$(O[4])="Total Activity",BUF$(O[5])=STR(AMOUNT:M1$)
3650 IF P4$(3,1)="Y" THEN LET BUF$(O[6])=STR(UNITS:M2$)
3660 LET LLIST$[PAGE,X1,1]=BUF$,X1=X1+1,L=L+1
3700 REM " --- Display ending balance"
3710 DIM BUF$(WIDTH-2)
3720 LET AMOUNT=BALANCES[1]+AMOUNT,UNITS=BALANCES[2]+UNITS
3730 LET BUF$(1)="",BUF$(O[4])="Ending Balance",BUF$(O[5])=STR(AMOUNT:M1$)
3740 IF P4$(3,1)="Y" THEN LET BUF$(O[6])=STR(UNITS:M2$)
3750 LET LLIST$[PAGE,X1,1]=BUF$,X1=X1+1,L=L+1,ANSWER$="|EOF"
3790 GOSUB 5400
4000 REM " --- All done"
4010 CALL "SYC.WD",NAME$
4090 RUN "GLM.AA"
5400 REM " --- Middle window processing"
5410 LET XMODE=5,X1=1,L=1
5420 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],ANSWER$,PAGE,MAXROW,NAME$,HEIGHT,WIDTH,FKEY
5460 IF FKEY=4 THEN GOTO 5490
5470 IF FKEY<>-17 THEN LET PAGE=PAGE+1
5480 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAXROW,5,SHEIGHT-2
5490 RETURN
6000 REM " --- Date/Journal break"
6020 IF T0$="" THEN GOTO 6100
6030 IF L+1>MAXROW THEN GOSUB 5400
6040 IF FKEY=4 THEN GOTO 6190
6070 LET JBUF$(O[5])=STR(JT[0]:M1$)
6080 IF P4$(3,1)="Y" THEN IF JT[1]<>0 THEN LET JBUF$(O[6])=STR(JT[1]:M2$)
6090 IF DETAIL$<>"D" THEN LET LLIST$[PAGE,X1,1]=JBUF$,X1=X1+1,L=L+1,L1=L1+1
6100 REM 
6105 IF DONE>0 THEN GOTO 6190
6110 DIM JBUF$(WIDTH-2),JT[2]
6120 LET C0$(1,4)=N0$+D0$(13+P[4],2),C0$(5)="(Not On File)"
6130 FIND (GLM03_DEV,KEY=C0$(1,4),DOM=6140)IOL=GLM03A
6140 LET T0$=D0$(7+P[4],8),PERIOD=NUM(D0$(5+P[4],2),ERR=6160)
6150 LET JBUF$(O[0])=P3$((PERIOD-1)*3+235,3),X0=X0+1
6160 LET JBUF$(O[1])=FNYY21_YY$(D0$(3+P[4],2)),JBUF$(O[2])=FNB6$(D0$(7+P[4],6))
6170 LET JBUF$(O[3])=C0$(3,2),JBUF$(O[4])=C0$(5,20)
6190 RETURN
6200 REM " --- Period break"
6210 IF T1$="" THEN GOTO 6300
6220 GOSUB 6000
6225 IF DETAIL$="D" AND L1<=1 THEN GOTO 6260
6230 IF L+2>MAXROW THEN GOSUB 5400
6235 IF FKEY=4 THEN GOTO 6390
6240 DIM BUF$(WIDTH-2)
6245 LET TOTAL$=PRD_YR$+" Total",BUF$(1,O[0])="",BUF$(O[0],LEN(TOTAL$))=TOTAL$,BUF$(O[5])=STR(PT[0]:M1$)
6250 IF P4$(3,1)="Y" THEN LET BUF$(O[6])=STR(PT[1]:M2$)
6255 LET LLIST$[PAGE,X1,1]=BUF$,X1=X1+1,L=L+1
6260 IF L+1>MAXROW THEN GOSUB 5400
6265 IF FKEY=4 THEN GOTO 6390
6270 DIM BUF$(WIDTH-2)
6275 LET TOTAL$=PRD_YR$+" Ending Balance",BUF$(1,O[0])="",BUF$(O[0],LEN(TOTAL$))=TOTAL$,BUF$(O[5])=STR(BALANCES[1]+AMOUNT:M1$)
6280 IF P4$(3,1)="Y" THEN LET BUF$(O[6])=STR(BALANCES[2]+UNITS:M2$)
6285 LET LLIST$[PAGE,X1,1]=BUF$,X1=X1+1,L=L+1
6290 IF L>0 THEN LET LLIST$[PAGE,X1,1]=DASH$,X1=X1+1,L=L+1
6300 REM 
6305 IF DONE>0 THEN GOTO 6390
6310 DIM PT[2]
6320 LET T1$=D0$(3+P[4],4),PRD_YR$=P3$((NUM(T1$(3))-1)*3+235,3)+" "+FNYY21_YY$(T1$(1,2))
6340 LET L1=0
6390 RETURN
6500 REM " --- Load GLM-02 summary activity for account"
6510 FOR X=1 TO RECORDS
6520 DIM X0$(3+P[4]),A[MP],U[MP]
6530 LET X0$(1)=A0$+RECORDS$(X,1),Y=(X-1)*MP+(X-1)*1
6540 FIND (GLM02_DEV,KEY=X0$,DOM=6580)IOL=GLM02A
6550 FOR P=0 TO MP
6560 LET AS[Y+P]=A[P],US[Y+P]=U[P]
6570 NEXT P
6580 NEXT X
6600 REM " --- Calculate beginning balance"
6610 LET BALANCES[1]=AS[BEGINDEX],BALANCES[2]=US[BEGINDEX]
6630 IF BEGPER=0 THEN GOTO 6670
6640 FOR X=1 TO BEGPER
6650 LET BALANCES[1]=BALANCES[1]+AS[BEGINDEX+X]
6655 LET BALANCES[2]=BALANCES[2]+US[BEGINDEX+X]
6660 NEXT X
6700 REM " --- Store beginning balance line"
6710 DIM BUF$(WIDTH-2)
6720 LET BUF$(O[4])="Beginning Balance",BUF$(O[5])=STR(BALANCES[1]:M1$)
6730 IF P4$(3,1)="Y" THEN LET BUF$(O[6])=STR(BALANCES[2]:M2$)
6740 LET LLIST$[PAGE,X1,1]=BUF$,X0=X0+1,X1=X1+1,L=L+1
6760 LET LLIST$[PAGE,X1,1]=DASH$,X0=X0+1,X1=X1+1,L=L+1
6790 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8070 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 CALL "SYC.WD",NAME$
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 REM " --- Return to Menu"
9910 CALL "SYC.WD",NAME$
9950 RUN "SYS.AA"
9999 END

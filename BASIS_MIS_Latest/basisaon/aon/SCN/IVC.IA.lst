0010 REM "IVC - Inventory Item Entry"
0020 REM "Program IVC.IA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "11Nov96 JWK: Remember default on F2 (595)
0040 REM 
0042 REM "ACTION$    : Action to perform (input)"
0043 REM "             F = Allow <F1>=First"
0044 REM "             L = Allow <F1>=Last"
0045 REM "             R = Use READ instead of FIND"
0046 REM "             V = No field prompt"
0047 REM "             A = Allow <F1>=All"
0048 REM "             N = Return <F3> (No local lookup)"
0050 REM "COLUMN     : Screen column (input)"
0052 REM "ROW        : Screen row (input)"
0054 REM "CHANNELS[N]: Channel Numbers (input)"
0056 REM "              [1] = IVM-01"
0060 REM "             [14] = IVM-14"
0061 REM "FIRM$      : Current Firm (input)"
0062 REM "DEFAULT$   : Default Item Number (input)"
0063 REM "PROMPT$    : Default Screen Prompt (input)"
0064 REM "PARAMS[n]  : Inventory Item Parameters (input)"
0066 REM "             [0]        = Item Number Length"
0068 REM "             [1] to [3] = Description Segment Lengths"
0070 REM "ITEM$[n]   : Inventory Item Information (returned)"
0072 REM "             [0]        = Item Number"
0074 REM "             [1]        = Description formatted by FNITEM$"
0076 REM "             [2] to [6] = IVM-01 second thru sixth strings"
0078 REM "ITEM[n]    : Inventory Item Information (returned)"
0080 REM "             [0] to [12] = IVM-01 Numerics"
0082 REM "FKEY       : Function key value (returned)"
0083 REM 
0085 SETERR 9000
0087 SETESC 9000
0090 ENTER ACTION$,COLUMN,ROW,CHANNELS[ALL],FIRM$,DEFAULT$,PROMPT$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],FKEY
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$,A6$(1),A[ALL]
0220 IVM14A: IOLIST S0$(1)
0400 REM " --- Parameters"
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0420 IF PAGES<3 THEN GOTO 9900
0430 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0500 REM " --- Initializations"
0510 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A6$(50),A[12],S0$(42)
0550 LET FKEY=0,IVM01_DEV=CHANNELS[1],IVM14_DEV=CHANNELS[14]
0560 FOR X=0 TO 12
0570 LET ITEM[X]=0
0580 IF X<7 THEN LET ITEM$[X]=""
0590 NEXT X
0595 LET ITEM$[0]=DEFAULT$
1000 REM " --- Prompt for item number"
1010 LET V0$="S",V1$="C",V2$=DEFAULT$,V3$="",V4$=FNP$(PROMPT$)
1015 LET V0=PARAMS[0],V1=COLUMN,V2=ROW,FKEY=0
1020 IF V4$="" THEN LET V4$="Enter A Valid Item Number"
1030 IF POS("N"=ACTION$)=0 THEN LET V4$=V4$+" ("
1040 IF POS("F"=ACTION$)>0 THEN LET V4$=V4$+"<F1>=First/"
1050 IF POS("L"=ACTION$)>0 THEN LET V4$=V4$+"<F1>=Last/"
1055 IF POS("A"=ACTION$)>0 THEN LET V4$=V4$+"<F1>=All/"
1060 IF POS("N"=ACTION$)=0 THEN LET V4$=V4$+"<F3>=Lookup)"
1065 IF POS("V"=ACTION$)>0 THEN LET V1$="",V4$=""
1070 GOSUB 7000
1080 LET FKEY=V3
1090 ON V3 GOTO 1500,1100,4000,1200,4000
1100 REM " --- <F1> First/Last/All"
1110 IF POS("F"=ACTION$)>0 THEN GOTO 4000
1120 IF POS("L"=ACTION$)>0 THEN GOTO 4000
1130 IF POS("A"=ACTION$)>0 THEN GOTO 4000
1140 IF POS("N"=ACTION$)>0 THEN GOTO 4000
1190 GOTO 1000
1200 REM " --- <F3> Lookup"
1210 IF POS("N"=ACTION$) THEN GOTO 4000
1220 CALL "SYC.LI",V$
1230 IF V$="" THEN GOTO 1000
1240 LET FKEY=0
1500 REM " --- Retrieve item master"
1505 IF FNP$(V$)="" THEN GOTO 1100
1510 LET A0$(1)=FIRM$+V$
1520 IF POS("R"=ACTION$)>0 THEN GOTO 1550
1530 FIND (IVM01_DEV,KEY=A0$,DOM=1900)IOL=IVM01A
1540 GOTO 1600
1550 READ (IVM01_DEV,KEY=A0$,DOM=1900)IOL=IVM01A
1600 REM " --- Return item information"
1610 LET ITEM$[0]=A0$(3),ITEM$[1]=FNITEM$(A1$,PARAMS[1],PARAMS[2],PARAMS[3])
1620 LET ITEM$[2]=A2$,ITEM$[3]=A3$,ITEM$[4]=A4$,ITEM$[5]=A5$,ITEM$[6]=A6$
1630 FOR X=0 TO 12
1640 LET ITEM[X]=A[X]
1650 NEXT X
1690 GOTO 4000
1900 REM " --- Item not found. Check synonyms"
1910 DIM CHECK$(22)
1920 LET S0$(1)=FIRM$+V$,WINDOW=0,L=1,X0=0,X1=1,PAGE=1,CHECK$(1)=FIRM$+V$
1930 LET WIDTH=73,HEIGHT=12,WIN_X=1,WIN_Y=3,MAX_ROW=HEIGHT-5,W=WIDTH-21
1940 LET MATCH=0,SELECTION$=""
1950 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,1]
1990 READ (IVM14_DEV,KEY=S0$,DOM=2000)
2000 REM " --- Read next synonym"
2010 LET K$=KEY(IVM14_DEV,END=3000)
2020 IF POS(FIRM$+V$=K$)<>1 THEN GOTO 3000
2030 IF K$(1,22)=CHECK$ THEN LET MATCH=MATCH+1
2040 IF MATCH=0 THEN GOTO 2100
2050 IF MATCH>1 THEN GOTO 2090
2060 IF K$(1,22)=CHECK$ THEN GOTO 2100
2070 LET SELECTION$=S0$(23)
2080 GOTO 3600
2090 IF K$(1,22)<>CHECK$ THEN GOTO 3000
2100 REM " --- Retrieve item"
2110 READ (IVM14_DEV)IOL=IVM14A
2120 LET A0$(1)=FIRM$+S0$(23)
2130 FIND (IVM01_DEV,KEY=A0$,DOM=2900)IOL=IVM01A
2300 REM " --- Display page?"
2310 IF L<MAX_ROW THEN GOTO 2400
2320 LET MODE=5
2330 GOSUB 6000
2340 IF FKEY=1 THEN GOTO 3900
2350 IF FKEY=4 THEN GOTO 3900
2360 IF SELECTION$<>"" THEN GOTO 3600
2400 REM " --- Lookup window record"
2410 DIM G1$(WIDTH-2),DESCRIPTION$(W-(PARAMS[0]+3))
2430 LET DESCRIPTION$(1)=FNITEM$(A1$,PARAMS[1],PARAMS[2],PARAMS[3])
2440 LET G1$(2)=A0$(3),G1$(3+PARAMS[0])=DESCRIPTION$,G1$(W)=" "+S0$(3,20)
2450 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$(3)
2460 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2990 GOTO 2000
3000 REM " --- End of file"
3010 IF X0=0 THEN GOTO 1000
3020 IF X0>1 THEN GOTO 3500
3030 IF MATCH=0 THEN GOTO 3500
3040 LET V$=S0$(23)
3050 IF NAME$<>"" THEN CALL "SYC.WD",NAME$; LET NAME$=""
3090 GOTO 1500
3500 REM " --- Display last page"
3510 LET MODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6000
3530 IF FKEY=1 THEN GOTO 3900
3540 IF FKEY=4 THEN GOTO 3900
3550 IF SELECTION$="" THEN GOTO 3900
3600 REM " --- Assign SELECTION$ to V$ and go read item"
3610 LET V$=SELECTION$
3620 IF NAME$<>"" THEN CALL "SYC.WD",NAME$; LET NAME$=""
3690 GOTO 1500
3900 REM " --- Try again"
3910 IF NAME$<>"" THEN CALL "SYC.WD",NAME$; LET NAME$=""
3990 GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
6000 REM " --- Synonym lookup initializations"
6010 IF NAME$<>"" THEN GOTO 6100
6020 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
6030 LET HEADING$(2)="Number",HEADING$(3+PARAMS[0])="Name"
6040 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
6050 LET TITLE$="Inventory Synonym Lookup",NAME$="",HEADING$(W+1)="Synonym"
6060 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
6070 PRINT @(0,0),'SB','BR',HEADING$,'ER','SF',
6080 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
6100 REM " --- Display page"
6110 CALL "SYC.SA",MODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6120 IF FKEY=1 THEN GOTO 6190
6130 IF FKEY=-16 THEN LET PAGE=PAGE+1
6150 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6180 LET L=1,X1=1
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9950 EXIT 
9999 END

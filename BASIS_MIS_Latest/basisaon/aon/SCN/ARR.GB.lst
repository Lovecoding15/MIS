0010 REM "ARR - Monthly Cash Receipts Journal (Report)"
0020 REM "Program ARR.GB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Oct96 JJD: Correct error getting default GL account (220,6610,6660)"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLists
0210 ARM01A: IOLIST B0$,B1$
0220 ARM02A: IOLIST *,D1$(1)
0230 ARM10C: IOLIST *,Z0$(1)
0240 ARM10D: IOLIST *,Z0$(1)
0250 ART01A: IOLIST D0$,D[ALL]
0260 ART01A1: IOLIST X$(1)
0290 ART11A: IOLIST S0$,S1$,S[ALL]
0300 ART06A: IOLIST A0$,A[ALL]
0310 ART16A: IOLIST W0$,W[ALL]
0320 ART26A: IOLIST W0$,W[ALL]
0350 ARW07A: IOLIST X1$,X
0380 GLW04A: IOLIST X0$,X
0500 REM " --- Initializations"
0510 DIM HEADINGS$[6],J$(80,"-"),J1$(80,"="),A[1],D[1],T[3],V[3],W[1]
0540 LET L9=59,L=L9+1,H0=132,H=0,HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0550 IF M1$(LEN(M1$),1)="-" THEN LET M1$=M1$(1,LEN(M1$)-1)+"CR"
0555 IF LEN(M1$)>12 THEN LET M1$="######0.00CR"
0560 LET M1=LEN(M1$),CLOCK$="",WHEN$=F0$(7,3)
0600 REM " --- Headings"
0605 DIM X$(H0)
0610 IF R1$="" THEN LET H5$="First" ELSE IF R0$="A" THEN LET H5$=R1$ ELSE LET H5$=FNF$(R1$,M0$)
0615 IF R2$="" THEN LET H6$="Last" ELSE IF R0$="A" THEN LET H6$=R2$ ELSE LET H6$=FNF$(R2$,M0$)
0620 IF R3$="" THEN LET H7$="First" ELSE LET H7$=FNB$(R3$)
0625 IF R4$="" THEN LET H8$="Last" ELSE LET H8$=FNB$(R4$)
0630 LET H9$="Cash Receipts Code: "
0635 IF R5$="" THEN LET H9$=H9$+"All" ELSE LET H9$=H9$+R5$
0640 LET X$(1)="Beginning Customer: "+H5$
0645 LET X$(FNC(H9$,H0),LEN(H9$))=H9$
0650 LET X$(95)="Beginning Receipt Date: "+H7$,HEADINGS$[3]=X$
0655 LET X$(1)="   Ending Customer: "+H6$
0660 LET X$(95)="   Ending Receipt Date: "+H8$,HEADINGS$[4]=X$
0690 LET M9=112-11-5*M1-22,HEADINGS=5
0900 REM " --- Clear work files"
0905 LET X=MIN(M0+G5+12,M0+19)
0910 CALL "SYC.NB","Printing",X,COL
0920 IF GL$<>"Y" THEN GOTO 1000
0925 IF R6$<>"Y" THEN GOTO 1000
0940 CALL "SYC.FA",ARW07_DEV
0960 LOCK (ARW07_DEV)
1000 REM " --- Initial Read
1005 IF R0$<>"N" THEN GOTO 1040
1010 READ (ART06_DEV,KEY=N0$+FILL(3)+R1$,DOM=1020)
1020 LET A0$=KEY(ART06_DEV,END=4000)
1030 READ (ART06_DEV,KEY=A0$(1,11)+R3$,DOM=1120)
1035 REM " --- Init read Alpha
1040 READ (ARM04_DEV,KEY=N0$+"A"+R1$,DOM=1050)
1050 LET S4$=KEY(ARM04_DEV,END=4000)
1055 IF S4$(1,3)<>N0$+"A" THEN GOTO 4000
1060 IF R2$<>"" AND S4$(4,LEN(R2$))>R2$ THEN GOTO 4000
1065 READ (ARM04_DEV)
1070 READ (ART06_DEV,KEY=N0$+FILL(3)+S4$(14,6)+R3$,DOM=1080)
1100 REM " --- Heading Read
1120 IF R0$="A" THEN LET A0$=KEY(ART06_DEV,END=1050) ELSE LET A0$=KEY(ART06_DEV,END=4000)
1130 IF POS(N0$+FILL(3)=A0$)<>1 THEN IF R0$="A" THEN GOTO 1050 ELSE GOTO 4000
1170 IF R0$="A" AND A0$(6,6)<>S4$(14,6) THEN GOTO 1050
1180 IF R0$="N" AND R3$<>"" AND A0$(12,3)<R3$ THEN READ (ART06_DEV,KEY=A0$(1,11)+R3$,DOM=1120)
1200 READ (ART06_DEV,KEY=A0$)IOL=ART06A
1270 IF R2$<>"" AND A0$(6,6)>R2$ AND R0$="N" THEN GOTO 4000
1280 IF R4$<>"" AND A0$(12,3)>R4$ THEN IF R0$="A" THEN GOTO 1050 ELSE GOTO 1100
1290 IF R5$<>"" AND A0$(15,3)<>R5$ THEN GOTO 1100
1300 REM " --- Breaks
1380 IF A0$(6,6)<>T0$ THEN GOSUB 6200
1500 REM " --- Print
1510 PRINT @(COL,11),FNF$(A0$(6,P[0]),M0$)," ",A0$(18,7),
1520 IF L+1>L9 THEN GOSUB 5000
1570 PRINT (7)@(M9),FNB$(A0$(12,3)),@(9+M9),A0$(15,3),@(16+M9),A0$(18,7),@(112-11-5*M1),A[0]:M1$,
1580 LET L1=0
1600 LET T0=T0+A[0],T1=T1+A[0]
2000 REM " --- Detail
2005 REM " --- Invoice Detail (ART-16)
2010 READ (ART16_DEV,KEY=A0$(1,24),DOM=2020)
2020 LET K16$=KEY(ART16_DEV,END=2500)
2040 IF K16$(1,24)<>A0$(1,24) THEN GOTO 2500
2060 READ (ART16_DEV)IOL=ART16A
2130 GOSUB 6700
2200 REM " --- AR
2220 IF L+1>L9 THEN GOSUB 5000
2300 PRINT (7)@(112-9-4*M1),W0$(26,7),@(112-4*M1),Y0:M1$,@(112-3*M1),W[0]:M1$,@(112-2*M1),W[1]:M1$,@(112-M1),W[0]+W[1]:M1$
2320 LET L=L+1,L1=L1+1
2340 FOR X=0 TO 1
2350 LET T[X]=T[X]+W[X],V[X]=V[X]+W[X]
2360 NEXT X
2400 REM " --- Invoice G/L Breakdown
2410 GOSUB 6400
2420 GOSUB 6500
2430 LET X0$=N0$+Z0$,X0=-W[0]
2440 GOSUB 6900
2450 LET X0$=N0$+Z1$,X0=W[0]
2460 GOSUB 6900
2470 LET X0$=N0$+Z2$,X0=W[1]
2475 GOSUB 6900
2480 LET X0$=N0$+Z0$,X0=-W[1]
2485 GOSUB 6900
2490 GOTO 2020
2500 REM " --- GL Detail (ART-26)
2510 READ (ART26_DEV,KEY=A0$(1,24),DOM=2520)
2520 LET K26$=KEY(ART26_DEV,END=3000)
2530 IF K26$(1,24)<>A0$(1,24) THEN GOTO 3000
2540 READ (ART26_DEV)IOL=ART26A
2590 IF L+1>L9 THEN GOSUB 5000
2600 PRINT (7)@(131-M1-G[3]),FNF$(W0$(26,G[3]),G5$),@(132-M1),W[0]:M1$
2620 LET L=L+1,L1=L1+1
2630 LET T[2]=T[2]+W[0],V[2]=V[2]+W[0]
2640 GOSUB 6400
2650 LET X0$=N0$+W0$(26,G[4]),X0=W[0]
2660 GOSUB 6900
2670 LET X0$=N0$+Z1$,X0=-W[0]
2680 GOSUB 6900
2690 GOTO 2520
3000 REM " --- Trans End
3090 GOTO 1100
4000 REM " --- Totals
4010 LET T9$="E"
4020 GOSUB 6200
4030 LET X$="Totals For Report"
4050 IF L+3>L9 THEN GOSUB 5000
4060 PRINT (7)'LF',@(112-11-5*M1),J1$(1,M1),@(112-3*M1),J1$(1,M1),@(112-2*M1),J1$(1,M1),@(112-M1),J1$(1,M1),@(132-M1),J1$(1,M1)
4080 PRINT (7)@(112-11-5*M1-LEN(X$)),X$,@(112-11-5*M1),T1:M1$,@(112-3*M1),V[0]:M1$,@(112-2*M1),V[1]:M1$,@(112-M1),V[0]+V[1]:M1$,@(132-M1),V[2]:M1$
4100 REM " --- Run next overlay"
4110 IF GL$<>"Y" THEN GOTO 9900
4120 IF R6$<>"Y" THEN GOTO 9900
4190 RUN "ARR.GC"
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5070 PRINT (7)"Customer",@(M9),"Receipt",@(9+M9),"Rec",@(16+M9),"Check",@(112-6-5*M1),"Check",@(112-9-4*M1),"Invoice",@(112+2-4*M1),"Invoice",@(112+3-3*M1),"Amount",@(112+3-2*M1),"Discount",@(112-M1+4),"Total",@(112),"--Miscellaneous--"
5080 PRINT (7)"Number And Name",@(M9),"Date",@(9+M9),"Code",@(16+M9),"Number",@(112-6-5*M1),"Amount",@(112-9-4*M1),"Number",@(112+2-4*M1),"Balance",@(112+3-3*M1),"Applied",@(112+3-2*M1),"Amount",@(112-M1+4),"Credit",@(132-M1-G[3]),"Acct",@(132-M1),"Amount"
5085 LET L=L+2
5090 RETURN 
6000 REM " --- Find Customer
6020 DIM B1$(30)
6040 LET B1$(1)="Unknown Customer"
6060 FIND (ARM01_DEV,KEY=N0$+A0$(6,6),DOM=6080)IOL=ARM01A
6070 LET B1$=B1$(1,30)
6090 RETURN 
6200 REM " --- Customer Break
6210 IF T0$="" THEN GOTO 6300
6215 IF L+2>L9 THEN GOSUB 5000
6220 LET X$="Totals For Customer"
6230 PRINT (7)@(112-11-5*M1),J$(1,M1),@(112-3*M1),J$(1,M1),@(112-2*M1),J$(1,M1),@(112-M1),J$(1,M1),@(132-M1),J$(1,M1)
6250 PRINT (7)@(112-11-5*M1-LEN(X$)),X$,@(112-11-5*M1),T0:M1$,@(112-3*M1),T[0]:M1$,@(112-2*M1),T[1]:M1$,@(112-M1),T[0]+T[1]:M1$,@(132-M1),T[2]:M1$
6280 LET L=L+2
6300 IF T9$<>"" THEN RETURN 
6310 GOSUB 6000
6315 IF L+2>L9 THEN GOSUB 5000
6320 PRINT (7)'LF',@(0),FNF$(A0$(6,P[0]),M0$),"  ",B1$
6330 DIM T[3]
6340 LET T0$=A0$(6,6),L=L+2
6350 LET T0=0
6390 RETURN 
6400 REM " --- Get Cash Receipts Code
6420 DIM Z0$(2*G[4])
6440 FIND (ARM10_DEV,KEY=N0$+"C"+A0$(15,3),DOM=6450)IOL=ARM10C
6460 LET Z1$=Z0$(1,G[4]),Z2$=Z0$(G[4]+1,G[4])
6490 RETURN 
6500 REM " --- Get Invoice Distribution Code
6520 DIM Z0$(8*G[4],"0"),X$(40)
6530 IF W0$(26,2)="OA" THEN GOTO 6600
6540 FIND (ART01_DEV,KEY=N0$+"  "+W0$(6,6)+W0$(26,7)+"00",DOM=6580)IOL=ART01A1
6560 FIND (ARM10_DEV,KEY=N0$+"D"+X$(20,2),DOM=6570)IOL=ARM10D
6580 LET Z0$=Z0$(1,G[4]),X$=""
6590 RETURN 
6600 REM " --- Get Customer Default Distribution Code
6610 DIM D1$(64)
6640 FIND (ARM02_DEV,KEY=N0$+W0$(6,6)+"  ",DOM=6680)IOL=ARM02A
6660 FIND (ARM10_DEV,KEY=N0$+"D"+D1$(8,2),DOM=6670)IOL=ARM10D
6680 LET Z0$=Z0$(1,G[4]),X$=""
6690 RETURN 
6700 REM " --- Invoice Balance
6710 DIM D[1],S[1]
6720 LET Y0=0
6730 IF W0$(26,2)="OA" THEN RETURN 
6750 READ (ART01_DEV,KEY=A0$(1,4)+A0$(6,6)+W0$(26,7)+"00",DOM=6890)IOL=ART01A
6760 LET Y0=D[0]
6790 READ (ART11_DEV,KEY=D0$(1,17),DOM=6800)
6800 LET S0$=KEY(ART11_DEV,END=6890)
6810 IF S0$(1,17)<>D0$(1,17) THEN GOTO 6890
6820 READ (ART11_DEV)IOL=ART11A
6840 IF S1$(2,3)>A0$(12,3) OR S1$(5,7)=A0$(18,7) THEN GOTO 6860
6850 LET Y0=Y0+S[0]+S[1]
6880 GOTO 6800
6890 RETURN 
6900 REM " --- Update G/L Summary"
6905 IF GL$<>"Y" THEN GOTO 6990
6910 IF R6$<>"Y" THEN GOTO 6990
6915 IF X0=0 THEN GOTO 6990
6920 LET X=0
6925 FIND (GLW04_DEV,KEY=X0$,DOM=6930)IOL=GLW04A
6930 LET X=X+X0
6940 WRITE (GLW04_DEV,KEY=X0$)IOL=GLW04A
6950 DIM X1$(37)
6955 LET X=0,X1$(1)=A0$(1,4)+A0$(12,3)+X0$(3,10)+A0$(6,6)+A0$(18,7)+W0$(26,7)
6960 FIND (ARW07_DEV,KEY=X1$,DOM=6970)IOL=ARW07A
6970 LET X=X+X0
6980 WRITE (ARW07_DEV,KEY=X1$)IOL=ARW07A
6990 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "OPR - Batch Invoice Printing"
0020 REM "Program OPR.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "20Feb97 WGH: Use IV Price Mask for price fields (280,490)"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES=12
0115 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="ARE-03",FILES$[2]="ARE-04",FILES$[3]="ARE-13"
0125 LET FILES$[4]="ARE-20",FILES$[5]="ARE-23",FILES$[6]="ARE-33"
0130 LET FILES$[7]="ARM-01",FILES$[8]="ARM-03",FILES$[9]="ARM-09"
0135 LET FILES$[10]="ARM-10",FILES$[11]="IVM-01",FILES$[12]="SYS-01"
0150 CALL "SYC.DA",1,1,12,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS>0 THEN GOTO 9900
0160 LET ARE03_DEV=CHANNELS[1],ARE04_DEV=CHANNELS[2],ARE13_DEV=CHANNELS[3]
0165 LET ARE20_DEV=CHANNELS[4],ARE23_DEV=CHANNELS[5],ARE33_DEV=CHANNELS[6]
0170 LET ARM01_DEV=CHANNELS[7],ARM03_DEV=CHANNELS[8],ARM09_DEV=CHANNELS[9]
0175 LET ARM10_DEV=CHANNELS[10],IVM01_DEV=CHANNELS[11],SYS01_DEV=CHANNELS[12]
0200 REM " --- IOLists"
0210 ARM01A: IOLIST *,X0$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 IVS01A: IOLIST X$,X$,I2$,I3$,X$,X$,M1$,M2$,M3$,M4$
0290 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0460 DIM P[2],I[6]
0465 FOR X=0 TO 6
0470 LET I[X]=NUM(I2$(X*2+1,2))
0475 NEXT X
0480 LET DESCLEN=I[4]+I[5]+I[6]
0485 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0490 LET M0=LEN(M0$),M2=LEN(M2$),M3=LEN(M3$),M4=LEN(M4$)
0495 CALL "SYC.RM",M1$,M1,8,12
0500 REM " --- Initialize Data"
0510 LET P5$=""
0700 REM " --- Background"
0710 FOR X=4 TO 8 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(29,3),"Message:",@(23,5),"Restart (Y/N):",@(21,7),"Customer Number:",'SF',
1000 REM " --- Message"
1010 PRINT 'CF',
1020 LET V0$="S",V1$="E",V2$=MESSAGE$,V0=40,V1=38,V2=3,I0=0
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1060 LET MESSAGE$=V$
1090 GOTO 3900
1200 REM " --- Restart"
1220 LET V0$="Y",V1$="R",V2$="N",V0=1,V1=38,V2=5
1240 GOSUB 7000
1260 LET P7$=V$
1290 GOTO 3900
1400 REM " --- Customer"
1410 IF P7$="N" THEN GOTO 3900
1420 LET V0$="M",V1$="K",V2$="",V3$=P1$,V0=P[1],V1=38,V2=7
1425 LET V4$="Enter A Valid Customer Number, <F3>=Lookup"
1430 GOSUB 7000
1440 IF V3=2 THEN GOTO 3900
1450 IF V3=3 THEN CALL "SYC.LC",V$
1460 LET P5$=V$
1470 FIND (ARM01_DEV,KEY=N0$+P5$,DOM=1400)IOL=ARM01A
1480 PRINT @(V1,V2),FNF$(P5$(1,P[0]),M0$)," ",X0$(1,30),
1490 GOTO 3900
3900 REM " --- New Field"
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,4000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct"
4020 CALL "SYC.YN",1,V4$,2,V$,V3
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Open Printer"
4110 CALL "SYC.GA",7,1,"","",STATUS
4120 IF STATUS THEN GOTO 9900
4150 REM " --- Run Overlay"
4160 LET O9$="SYS.AA",O9=1,PRTR_DEV=7
4190 RUN P3$(65,6),ERR=4200
4200 REM " --- Missing Invoice Print Program"
4210 LET NUMBER=1
4220 DIM MESSAGE$[NUMBER]
4230 LET MESSAGE$[0]="Invoice print program "+P3$(65,6)+" not found."
4240 LET MESSAGE$[1]="         <Enter>=Continue"
4250 CALL "SYC.XA",2,MESSAGE$[ALL],NUMBER,-1,-1,V$,V3
4290 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

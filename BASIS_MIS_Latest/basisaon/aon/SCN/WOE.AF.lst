0010 REM "WOS - Work Order Subcontract Requirements - Vendor/PO/Req "
0020 REM "Program:  WOE.AF"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "11Oct96 WGH: Update POE-11 Requisition Detail (2760)"
0032 REM "02Jan97 JWK: Can't find Vendors if Vendor #'s <6 chrs (1580,5020)
0040 REM 
0055 REM "L9=MAX LINES, L1=FIRST LINES, L0=HEADER LINE, L=CURRENT LINE
0080 SETESC 9000
0085 SETERR 9000
0090 PRECISION IVS01_PRECISION
0095 IF PO$<>"Y" THEN GOTO 4100
0098 IF I9$="R" THEN GOTO 4240
0100 REM " --- Open/Lock Files
0105 LET FILES=5
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="APM-01",FILES$[2]="POE-01",FILES$[3]="POE-02"
0120 LET FILES$[4]="POE-11",FILES$[5]="POE-12"
0150 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET APM01_DEV=CHANNELS[1],POE01_DEV=CHANNELS[2],POE02_DEV=CHANNELS[3]
0165 LET POE11_DEV=CHANNELS[4],POE12_DEV=CHANNELS[5]
0200 REM " --- IOLISTs
0210 WOE32A: IOLIST W0$(1),W1$(1),W[ALL]
0220 APM01A: IOLIST R0$,R1$,R[ALL]
0225 APM01A2: IOLIST Y0$,Y1$
0230 POE02A: IOLIST X0$,X1$,X2$,X3$,X[ALL]
0235 POE12A: IOLIST X0$,X1$,X2$,X3$,X4$,X5$,X6$,X[ALL]
0240 POE01A: IOLIST X0$,X1$,X2$,X3$,X[ALL]
0245 POE11A: IOLIST X0$,X1$,X2$,X3$,X4$,X5$,X6$,X[ALL]
0250 WOE03F: IOLIST SUBCSORT$
0500 REM " --- Init Data
0510 LET L0=11,L1=L0+2,L9=19,K9$="",V5$=""
0520 DIM W0$(15),W1$(151),W[13],R[3],O[9]
0525 DIM SUBCSORT$(21)
0530 LET W0$(1)=A0$(1,11)+"C",L=L1
0540 CALL "SYC.RM",M2$,M2,11,11
0550 CALL "SYC.RM",M3$,M3,11,11
0560 LET O[0]=76-M2-M3-29,O[1]=O[0]+8,O[2]=O[1]+4,O[3]=O[2]+4,O[4]=O[3]+3
0570 LET O[5]=76-M3-M2,O[6]=77-M3,O[7]=75-M3*2-M2,O[8]=O[5],O[9]=O[6]
0630 IF V3=4 AND W1$(43,7)<>FILL(7," ") AND W1$(50,3)=FILL(3," ") THEN LET V4$="A Sequence Number Is Required. <Enter> To Continue. "; GOSUB 6000; GOTO 2000
0700 REM " --- Background
0710 PRINT @(0,L0),'CE'
0720 PRINT 'SB',@(2,L0-1),"Subcontract Requirements",
0730 PRINT @(0,L0),"Seq C Vendor",
0740 IF PO$="Y" THEN PRINT @(O[0]),"P.O. #  Seq Ld",
0750 PRINT @(O[3]),"UM  Op",@(63),"Each/Total",
0760 PRINT @(2,L0+1),"Date Req  Description",
0770 IF PO$="Y" THEN PRINT @(O[2]-1),"Time",
0780 PRINT @(O[4]),"Seq",@(O[8]-6),"Rate",@(O[6]-7),"Units",@(72),"Cost",
0790 PRINT 'SF'
0800 REM " --- Initialize "                      
0805 READ (WOE32_DEV,KEY=A0$(1,11)+"C",DOM=0810)
0810 IF O0=0 THEN IF I9$="I" AND BM$="Y" THEN GOTO 0840 ELSE GOSUB 5100; GOTO 4000
0815 GOSUB 5100
0820 IF K9$<>"" THEN GOTO 4000
0825 IF A0$(15,1)="C" OR (O0<>0 AND A0$(14,1)="I" AND BM$="Y") THEN GOTO 4000
0830 GOTO 1000
0840 FIND (APM01_DEV,KEY=A0$(1,2)+A0$(56),DOM=0860)
0860 LET K7$=KEY(APM01_DEV,END=4000)
0870 IF K7$(3,P0)<>A0$(56) THEN GOTO 4000
1000 REM " --- Init Read
1010 IF L<=L9 THEN GOTO 1100
1020 PRINT @(0,21),'CL',@(0,22),'CL',@(0,L1),'LD','LD'
1030 LET L=L-2
1040 GOTO 1010
1100 REM " --- Get Next Key"
1110 LET K$=KEY(WOE32_DEV,END=1200)
1120 IF K$="" THEN GOTO 1200
1130 IF K$(1,2)<>N0$ THEN GOTO 1200
1140 IF K$(1,12)<>A0$(1,11)+"C" THEN GOTO 1200
1150 FIND (WOE32_DEV,KEY=K$)IOL=WOE32A
1160 DIM Y1$(30); LET Y1$(1)="*** Not On File ***"
1170 FIND (APM01_DEV,KEY=N0$+W1$(7,P7),DOM=1180)IOL=APM01A2
1180 GOTO 1270
1200 REM " --- Subcontract Sequence Number
1205 IF A0$(14,1)="I" AND P9$(1,1)="Y" THEN GOTO 4000
1210 PRINT @(0,21),'CL',@(0,22),'CL',
1215 IF L>L9 THEN PRINT @(0,L1),'LD','LD'; LET L=L-2; GOTO 1215
1220 IF V3<>2 THEN LET W0=NUM(W0$(13,3))+5
1225 LET V0$="Z",V1$="ER",V2$=STR(W0),V3$="",V0=3,V1=0,V2=L,I0=0,O9=1
1230 GOSUB 7000
1235 IF V3=4 THEN GOTO 4000
1240 IF V$="000" THEN GOTO 1200
1245 LET W0$(13,3)=V$
1250 IF W0$(1,15)<=K9$ THEN GOSUB 5400
1255 PRINT @(0,L),W0$(13,3)
1260 FIND (WOE32_DEV,KEY=W0$(1,15),DOM=1300)IOL=WOE32A
1265 FIND (APM01_DEV,KEY=N0$+W1$(7,P7),DOM=1270)IOL=APM01A2
1270 GOSUB 5000
1275 LET I0=0
1280 GOTO 3300
1300 REM " --- Setup 
1310 DIM W[13]
1320 REM " --- Set Up Defaults
1330 LET W1$(1,90)="",W1$(92)="",W1$(4,3)=A0$(19,3),I0=0
1340 LET W[4]=1
1350 GOTO 3200
1400 REM " --- Subcontract Line Type  
1410 IF O0<>0 AND A0$(14,1)="I" AND P9$(1,1)="Y" THEN GOTO 1490
1420 FIND (WOE32_DEV,KEY=W0$(1,15),DOM=1430); GOTO 1490
1430 LET V0$="S",V1$="CR",V2$=W1$(91,1),V3$="IMS",V4$="S=Standard, M=Message, I=Internal Documentation",V0=1,V1=4,V2=L
1440 GOSUB 7000
1450 IF V3=2 THEN GOTO 3200
1460 IF V3=4 THEN GOTO 4000
1470 PRINT @(6,L),'CL',@(0,L+1),'CL',
1480 LET W1$(91,1)=V$
1490 GOTO 3200
1500 REM " --- Vendor Number Entry "
1510 IF POS(W1$(91,1)="MI")>0 THEN GOTO 1600
1520 LET V0$="M",V1$="ECK",V2$=W1$(7,P7),V3$=VEND_IN_MASK$,V4$="Enter A Valid Vendor Number, <F3>=Lookup",V0=6,V1=6,V2=L
1530 GOSUB 7000
1540 IF V3=2 THEN GOTO 3200
1550 IF V3=4 THEN GOTO 4000
1560 IF V3=3 THEN CALL "SYC.LV",V$
1570 LET W1$(7,6)=V$
1580 FIND (APM01_DEV,KEY=N0$+W1$(7,6),DOM=1500)IOL=APM01A2
1590 PRINT @(V1,V2),FNF$(V$(1,P7),P5$),@(V1+1+LEN(P5$),V2),Y1$(1,O[0]-8-LEN(P5$)),
1595 GOTO 3200
1600 REM " --- Description "
1610 IF O0<>0 AND A0$(14,1)="I" AND P9$(1,1)="Y" THEN GOTO 1660
1620 IF POS(W1$(91,1)="MI")>0 THEN GOTO 1660
1630 LET V0$="S",V1$="C",V2$=W1$(13,30),V3$="",V4$="Enter Description",V0=30,V1=11,V2=L+1
1640 GOSUB 7000
1650 LET W1$(13,30)=V$
1660 GOTO 3200
1700 REM " --- Date Required "   
1710 IF POS(W1$(91,1)="MI")>0 THEN GOTO 1760
1720 IF POS(" "<>W1$(4,3))=0 THEN LET W1$(4,3)=F0$(7,3)
1730 LET V0$="D",V1$="C",V2$=W1$(4,3),V3$="",V4$="Enter The Date This Process Should Be Completed",V0=8,V1=2,V2=L+1
1740 GOSUB 7000
1750 LET W1$(4,3)=V$
1760 GOTO 3200
1800 REM " --- Unit of Measure " 
1810 IF O0<>0 AND A0$(14,1)="I" AND P9$(1,1)="Y" THEN GOTO 1870
1820 IF POS(W1$(91,1)="MI")>0 THEN GOTO 1870
1830 LET V0$="N",V1$="",V2$=STR(W[0]),V3$=M2$,V4$="",V0=7,V1=O[5],V2=L
1840 GOSUB 7000
1850 LET W[0]=V
1860 GOSUB 5060
1870 GOTO 3200
1900 REM " --- Cost "     
1910 GOTO 1980
1920 IF O0<>0 AND A0$(14,1)="I" AND P9$(1,1)="Y" THEN GOTO 1980
1930 IF POS(W1$(91,1)="MI")>0 THEN GOTO 1980
1940 LET V0$="N",V1$="",V2$=STR(W[1]),V3$=M3$,V4$="",V0=7,V1=O[8],V2=L+1
1950 GOSUB 7000
1960 LET W[1]=V; IF W[0]<>0 THEN LET W[4]=W[1]/W[0]
1970 GOSUB 5060
1980 GOTO 3200
2000 REM " --- Enter Purchase Order/Requisition Number "
2010 IF POS(W1$(91,1)="MI")>0 OR PO$="N" THEN GOTO 2470
2020 IF W1$(56,1)="C" THEN GOTO 2470
2030 LET V0$="Z",V1$="",V2$=W1$(43,7),V3$="",V4$="<F1>=Requisition Lookup, <F3>=P.O. Lookup",V0=7,V1=O[0],V2=L
2040 GOSUB 7000
2050 IF V3=2 THEN GOTO 3200
2060 IF V3<>4 THEN GOTO 2120
2070 IF W1$(43,7)=FILL(7," ") THEN GOTO 2120
2080 IF W1$(50,3)<>FILL(3," ") THEN GOTO 2120
2090 LET V4$="A Sequence Number Is Required. <Enter> To Continue. "
2100 GOSUB 6000
2110 GOTO 2000
2120 IF V3<>1 THEN GOTO 2160
2130 CALL "POC.LA","R",W1$(7,6),V$
2140 IF V$="" THEN GOTO 2000
2150 GOTO 2190
2160 IF V3<>3 THEN GOTO 2190
2170 CALL "POC.LA","P",W1$(7,6),V$
2180 IF V$="" THEN GOTO 2000
2185 REM " --- No Change, Continue To Process "
2190 IF V$=W1$(43,7) THEN GOTO 2350
2200 REM " --- Need To Remove Previous Link?"
2210 IF V$="" AND W1$(43,7)<>FILL(7," ") THEN GOTO 2310
2220 IF V$<>"" AND W1$(43,7)<>FILL(7," ") THEN GOTO 2280
2230 REM " --- Otherwise, Continue "
2240 IF V$="" AND W1$(43,7)=FILL(7," ") THEN GOTO 2460
2250 IF V$<>"" AND W1$(43,7)=FILL(7," ") THEN GOTO 2350
2270 REM " --- PO/Req Number Changed "
2280 GOSUB REMOVE_LINK
2290 GOTO 2350
2300 REM " --- PO/Req Number Removed From WO "
2310 GOSUB REMOVE_LINK
2320 LET W1$(43,10)=""
2330 PRINT @(O[0],V2),FILL(11," ")
2340 GOTO 2460
2350 FIND (POE01_DEV,KEY=N0$+W1$(7,6)+V$,DOM=2380)
2360 LET W1$(56,1)="R"
2370 GOTO 2440
2380 FIND (POE02_DEV,KEY=N0$+W1$(7,6)+V$,DOM=2410)
2390 LET W1$(56,1)="P"
2400 GOTO 2440
2410 LET V4$="No Matching Purchase Req/Order. <Enter> To Continue. "
2420 GOSUB 6000
2430 GOTO 2000
2440 LET W1$(43,7)=V$
2450 PRINT @(V1,V2),W1$(43,7),
2460 GOSUB 5000
2470 GOTO 3200
2500 REM " --- Lead Time
2510 IF POS(W1$(91,1)="MI")>0 OR PO$="N" THEN GOTO 2550
2520 LET V0$="N",V1$="",V2$=STR(W[5]),V3$="##0",V4$="Enter Days Required To Complete Subcontract",V0=0,V1=O[2],V2=L
2530 GOSUB 7000
2540 LET W[5]=V
2550 GOTO 3200
2600 REM " --- P.O. Sequence Number
2610 IF POS(W1$(91,1)="MI")>0 OR PO$="N" THEN GOTO 2850
2620 IF W1$(56,1)="C" THEN GOTO 2850
2630 IF W1$(43,7)=FILL(7," ") THEN LET W1$(50,3)=""; GOTO 2850
2640 LET V0$="Z",V1$="",V2$=W1$(50,3),V3$="",V4$="Enter Sequence Number On The Purchase Req/Order",V0=3,V1=O[1],V2=L
2650 GOSUB 7000
2660 IF V3=2 THEN GOTO 2850
2665 IF V$<>"" THEN GOTO 2680
2670 LET V4$="A Matching Sequence Number Is Required. <Enter> To Continue. "
2675 GOSUB 6000
2677 GOTO 2600
2680 IF POS(W1$(56,1)="PR")=0 THEN LET V3=2; GOTO 2850
2690 DIM X[12]
2700 IF W1$(56,1)="P" THEN LET TYPE$="Order"; GOTO 2780 ELSE LET TYPE$="Req"
2710 REM " --- Update Purchase Requisition Line         
2720 FIND (POE11_DEV,KEY=N0$+W1$(7,6)+W1$(43,7)+V$,DOM=2830)IOL=POE11A
2725 IF X1$(1,2)="N " THEN GOTO 2740
2730 LET V4$="Requisition Line Item Must Be Non-Stock. <Enter> To Continue. "
2735 GOSUB 6000
2737 GOTO 2600
2740 IF FNP$(X2$(1,10))<>"" AND X2$(1,10)<>W0$(5,7)+W0$(13,3) THEN LET V4$="This Requisition Sequence Is Already Assigned To Work Order "+X2$(1,7)+",",V5$="Sequence Number "+X2$(8,3)+". <Enter> To Continue."; GOSUB 6000; GOTO 2600
2750 LET X2$(1,10)=W0$(5,7)+W0$(13,3)
2760 WRITE (POE11_DEV,KEY=N0$+W1$(7,6)+W1$(43,7)+V$)IOL=POE11A; GOTO 2840
2770 REM " --- Update Purchase Order Line        
2780 FIND (POE12_DEV,KEY=N0$+W1$(7,6)+W1$(43,7)+V$,DOM=2830)IOL=POE12A
2785 IF X1$(1,2)="N " THEN GOTO 2800
2790 LET V4$="P.O. Sequence Line Item Must Be Non-Stock. <Enter> To Continue. "
2795 GOSUB 6000
2797 GOTO 2600
2800 IF FNP$(X2$(1,10))<>"" AND X2$(1,10)<>W0$(5,7)+W0$(13,3) THEN LET V4$="This P.O. Sequence Is Already Assigned To Work Order Number "+X2$(1,7)+",",V5$="Sequence Number "+X2$(8,3)+". <Enter> To Continue."; GOSUB 6000; GOTO 2600
2810 LET X2$(1,10)=W0$(5,7)+W0$(13,3)
2820 WRITE (POE12_DEV,KEY=N0$+W1$(7,6)+W1$(43,7)+V$)IOL=POE12A; GOTO 2840
2830 LET V4$="No Matching Purchase "+TYPE$+" Seq # Found. <Enter>=Continue. "; GOSUB 6000; GOTO 2600
2840 LET W1$(50,3)=V$
2850 GOTO 3200
2900 REM " --- Rate
2910 IF O0<>0 AND A0$(14,1)="I" AND P9$(1,1)="Y" THEN GOTO 2970
2920 IF POS(W1$(91,1)="MI")>0 THEN GOTO 2970
2930 LET V0$="N",V1$="C",V2$=STR(W[4]),V3$=M3$,V4$="Enter Rate/Cost Per Unit Of Service/Material (Hour, Each, Etc) ",V0=7,V1=O[7],V2=L+1
2940 GOSUB 7000
2950 LET W[4]=V
2960 GOSUB 5060
2970 GOTO 3200
3000 REM " --- Message
3010 IF O0<>0 AND A0$(14,1)="I" AND P9$(1,1)="Y" THEN GOTO 3190
3020 IF W1$(91,1)="S" THEN GOTO 3060
3030 LET V0$="S",V1$="C",V2$=W1$(92,60),V3$="",V4$="Enter Message",V0=60,V1=11,V2=L+1
3040 GOSUB 7000
3050 LET W1$(92,60)=V$
3060 GOTO 3200
3100 REM " --- Unit Of Measure
3110 IF POS(W1$(91,1)="MI")>0 THEN GOTO 3190
3120 LET V0$="S",V1$="C",V2$=W1$(2,2),V3$="",V4$="Enter P.O. Unit Of Measure",V0=2,V1=O[3],V2=L
3130 GOSUB 7000
3150 LET W1$(2,2)=V$
3190 GOTO 3200
3200 REM " --- Seq Control"
3210 IF V3=4 THEN IF I0=1 THEN GOTO 3390 ELSE GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1200,1400,1500,1700,1600,2000,2600,2500,3100,6100,2900,1800,1900,3000,3300
3300 REM " --- Correct"
3305 IF W1$(91,1)="S" THEN IF POS(" "<>W1$(7,6))=0 THEN LET V$="D"; GOTO 3700
3310 PRINT @(78,L),'CL',@(0,22),'CL'
3320 LET V0$="S",V1$="C",V2$="",V0=1,V1=78,V2=L,K9$=W0$(1,15),V4$="<Enter>=OK, <F2>=Change, D=Delete",I0=14
3330 GOSUB 7000
3335 LET W0=NUM(W0$(13,3))
3340 IF V3=2 THEN LET I0=13; GOTO 3200
3350 IF V$="" THEN GOTO 3390
3360 IF V$="D" THEN GOTO 3700
3370 GOTO 3300
3390 PRINT @(0,21),'LD','LD',
3400 REM " --- Write"
3410 IF V3=4 AND I0=1 AND W1$(91,1)="M" THEN GOTO 3700
3430 WRITE (WOE32_DEV,KEY=W0$(1,15))IOL=WOE32A
3435 LET SUBCSORT$=N0$+"F  "+W1$(7,6)+W0$(5,7)+W0$(13,3)
3440 IF W1$(91,1)="S" THEN WRITE (WOE03_DEV,KEY=SUBCSORT$)IOL=WOE03F
3450 LET L=L+2
3470 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3700 REM " --- Delete"
3720 REMOVE (WOE32_DEV,KEY=W0$(1,15),DOM=3730)
3725 LET SUBCSORT$=N0$+"F  "+W1$(7,6)+W0$(5,7)+W0$(13,3)
3730 REMOVE (WOE03_DEV,KEY=SUBCSORT$,DOM=3740)
3740 IF PO$="N" OR FNP$(W1$(43,9))="" THEN GOTO 3780
3750 GOSUB 3900
3780 PRINT @(0,L),'LD','LD'
3790 GOTO 1000
3900 REM " --- Remove Work Order Link From The PO/Req Item "
3905 REMOVE_LINK: 
3910 DIM X[12]
3915 ON POS(W1$(56,1)="PR") GOTO 3990,REMOVE_PO,REMOVE_REQ
3917 REMOVE_PO: 
3920 FIND (POE12_DEV,KEY=N0$+W1$(7,6)+W1$(43,10),DOM=3990)IOL=POE12A
3930 LET X2$(1,10)=FILL(10," ")
3940 WRITE (POE12_DEV,KEY=N0$+W1$(7,6)+W1$(43,10))IOL=POE12A
3950 GOTO 3990
3955 REMOVE_REQ: 
3960 FIND (POE11_DEV,KEY=N0$+W1$(7,6)+W1$(43,10),DOM=3990)IOL=POE11A
3970 LET X2$(1,10)=FILL(10," ")
3980 WRITE (POE11_DEV,KEY=N0$+W1$(7,6)+W1$(43,10))IOL=POE11A
3990 RETURN 
4000 REM " --- Options"
4005 LET V4$="Is The Detail Information Correct?"
4010 IF A0$(15,1)="C" THEN LET V4$=V4$+" (Y/List): "; GOTO 4025
4015 IF A0$(14,1)="I" AND P9$(1,1)="Y" THEN LET V4$=V4$+" (Y/Review/List): "; GOTO 4025
4020 LET V4$=V4$+" (Y/N/Review/List): "
4025 LET V0$="S",V1$="C",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4035 IF A0$(15,1)="C" THEN IF V$="N" THEN LET V$=""; GOTO 4055 ELSE GOTO 4050
4040 IF A0$(14,1)="I" AND P9$(1,1)="Y" THEN IF V$="N" THEN LET V$=""; GOTO 4055 ELSE IF V$="R" THEN GOTO 4060
4045 IF V$="R" THEN GOTO 4600
4050 IF V$="L" THEN GOTO 4400
4055 ON POS(V$="YN") GOTO 4000,4100,1200
4060 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Standard Work Orders May Not Be Modified!! <Enter> To Adjust Purchase Info",V0=1,V1=FNV(V4$),V2=22; PRINT @(0,0),'RB',
4065 GOSUB 7000
4070 LET V$="R"; GOTO 4600
4100 REM " --- Close & Get Ready
4110 CLOSE (APM01_DEV,ERR=4120); LET APM01_DEV=0
4120 CLOSE (POE01_DEV,ERR=4130); LET POE01_DEV=0
4130 CLOSE (POE11_DEV,ERR=4140); LET POE11_DEV=0
4140 CLOSE (POE12_DEV,ERR=4150); LET POE12_DEV=0
4150 CLOSE (POE02_DEV,ERR=4160); LET POE02_DEV=0
4160 IF O0=0 AND I9$<>"I" THEN RUN "WOE.AK"
4240 LET O9=2
4250 RUN "WOE.AA"
4400 REM " --- List"
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review"
4620 READ (WOE32_DEV,KEY=A0$(1,11)+"C",DOM=4640)
4640 GOSUB 5400
4690 GOTO 1000
5000 REM " --- Display"
5005 IF POS(W1$(91,1)="MI")>0 THEN PRINT @(0,L),'CL',W0$(13,3),@(4),W1$(91,1),@(0,L+1),'CL',@(11),W1$(92,60),; GOTO 5090
5010 DIM Y1$(30); LET Y1$(1)="*** Not On File ***"
5020 FIND (APM01_DEV,KEY=N0$+W1$(7,6),DOM=5030)IOL=APM01A2
5030 PRINT @(0,L),'CL',W0$(13,3),@(4),W1$(91,1),@(6),FNF$(W1$(7,P7),P5$),@(7+LEN(P5$)),Y1$(1,O[0]-8-LEN(P5$)),
5035 IF PO$="Y" THEN PRINT @(O[0]),W1$(43,7),@(O[1]),W1$(50,3),@(O[2]),W[5]:"##0",
5040 PRINT @(O[3]),W1$(2,2),@(O[4]),W1$(53,3),
5050 IF W1$(56,1)="C" THEN LET PSTAT$="*Rcvd*" ELSE IF W1$(56,1)="P" THEN LET PSTAT$="*P.O.*" ELSE IF W1$(56,1)="R" THEN LET PSTAT$="*Req*" ELSE LET PSTAT$="*None*"
5055 PRINT @(O[4]+4),'SB',PSTAT$,'SF',
5060 LET W[1]=W[0]*W[4],W[2]=W[0]*A[0],W[3]=W[1]*A[0]
5070 PRINT @(O[5],L),W[0]:M2$,@(O[6]),W[1]:M3$,
5080 PRINT @(2,L+1),FNB$(W1$(4,3)),@(11),W1$(13,30),@(O[7]),W[4]:M3$,@(O[8]),W[2]:M2$,@(O[9]),W[3]:M3$,
5090 RETURN 
5100 REM " --- List"
5110 GOSUB 5400
5120 READ (WOE32_DEV,KEY=A0$(1,11)+"C",DOM=5130)
5130 LET K$=KEY(WOE32_DEV,END=5300)
5135 IF K$(1,2)<>N0$ THEN GOTO 5300
5140 IF K$(1,12)<>W0$(1,11)+"C" THEN GOTO 5300
5150 FIND (WOE32_DEV,KEY=K$)IOL=WOE32A
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="KCE",V2$="",V3$="",V4$="Continue? (<Enter>/End): ",V0=1,V1=50,V2=22
5215 GOSUB 7000
5220 IF V3=4 THEN GOTO 5300
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+2,K9$=W0$(1,15)
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5420 PRINT @(0,L1),'CE',
5440 LET L=L1
5490 RETURN 
5700 REM " --- Compress Desc
5720 LET D9$=D1$
5730 LET Z=POS(" "<>D9$); IF Z>1 THEN LET D9$=D9$(Z)
5740 LET X8=POS("  "=D9$); IF X8<>0 THEN LET Y8=POS(" "<>D9$(X8+2)); LET D9$=D9$(1,X8)+D9$(X8+Y8+1); IF Y8<>0 THEN GOTO 5740
5750 LET D1$=D9$
5790 RETURN 
6000 REM " --- Generic Warning Routine"
6020 DIM MESSAGE$[4]
6030 LET MESSAGE$[0]=V4$,MESSAGE=0
6040 IF V5$<>"" THEN LET MESSAGE$[1]=V5$,MESSAGE=1
6070 CALL "SYC.XA",2,MESSAGE$[ALL],MESSAGE,22,-1,V$,V3
6080 LET V4$="",V5$=""
6090 RETURN 
6100 REM " --- Operation Sequence Number"
6105 IF POS(W1$(91,1)="MI")>0 OR PO$="N" THEN GOTO 6190
6110 LET V0$="Z",V1$="C",V2$=W1$(53,3),V3$="",V4$="Enter Operation Sequence Number This Subcontract Follows, <F3>=Lookup",V0=3,V1=O[4],V2=L
6120 GOSUB 7000
6125 IF V3=2 THEN GOTO 6190
6135 IF V3=3 THEN LET V$=W0$(5,7); CALL "WOC.DA",V$,SYS01_DEV,OPCODE_DEV,WOE02_DEV; GOTO 6160
6140 IF V$="" THEN GOTO 6180
6160 FIND (WOE02_DEV,KEY=W0$(1,11)+"A"+V$,DOM=6170)
6165 GOTO 6180
6170 DIM MESSAGE$[2]; LET MESSAGE$[0]="The Sequence Number Must Match",MESSAGE$[1]="An Operation Code Sequence. <Enter> To Continue."; CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3; GOTO 6100
6180 LET W1$(53,3)=V$
6185 PRINT @(O[4],L),V$,
6190 GOTO 3200
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9215 LET O9=1
9220 RUN "WOE.AA"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "PRX - Conversion Entry - Taxes
0020 REM "Program PRX.AD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "30Oct96 Caj: Precision wasn't set (510)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0240 PRM10C: IOLIST D0$,D[ALL]
0250 PRM10I: IOLIST X0$
0260 PRT21A: IOLIST E0$(1),E[ALL],X[ALL],Y[ALL],F[ALL]
0500 REM " --- Init Data
0510 PRECISION P[3]
0520 DIM E0$(48),E[5],X[5],Y[5],F[0],D[0]
0540 LET E0$(1)=A0$+"C"
0700 REM " --- Background
0710 PRINT @(0,L0),'CE',
0720 FOR X=7 TO 16 STEP 9
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0760 PRINT 'SB',@(15,5),"Tax Conversion",@(5,6),"Tax Code:",@(8+M1,8),"Gross",@(6+2*M1),"Taxable",@(10+3*M1),"Tax",@(0,9),"Month-To-Date:",@(2,10),"1st Quarter:",@(2,11),"2nd Quarter:",@(2,12),"3rd Quarter:",@(2,13),"4th Quarter:",@(1,14),"Year-To-Date:",@(3,15),"Next Month:",
0770 PRINT @(15,16),"Tax Information",@(4,17),"Tax Table:",@(0,18),"Dependent Ded:",@(1,19),"Itemized Ded:",@(0,20),"Periods Taken:",@(27,17),"Add/Replace:",@(27,18),"Percent/Amt:",@(30,19),"Tax Rate:",
0790 PRINT 'SF',
0900 REM " --- Init Files
0920 READ (PRM10_DEV,KEY=N0$+"C",DOM=0930)
1000 REM " --- Key
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(PRM10_DEV,END=1050)
1030 IF V2$(1,3)<>N0$+"C" THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4,2)
1050 LET V0$="S",V1$="E",V0=2,V1=15,V2=6,O9=0,V4$="Enter Valid Tax Code, <F3>=Lookup"
1060 GOSUB 7000
1062 IF V3<>3 THEN GOTO 1070
1064 LET V4=1,V6=4,V7=2,V8=6,V9=16,V5$="C"
1066 CALL "SYC.LZ",V1,V2,V4,PRM10_DEV,V6,V7,V8,V9,V$,V5$,N0$
1068 IF V$="" THEN GOTO 1000
1070 IF V3=4 THEN RUN "PRX.AA"
1080 IF V$="" THEN GOTO 0900
1090 LET E0$(13)=V$
1200 REM " --- Find Key
1240 FIND (PRM10_DEV,KEY=N0$+"C"+E0$(13,2),DOM=1000)IOL=PRM10C
1250 PRINT @(V1+3,V2),D0$(6,16),
1260 FIND (PRT21_DEV,KEY=E0$(1,14),DOM=1400)IOL=PRT21A
1300 REM " --- Display
1320 GOSUB 5000
1340 GOTO 4000
1400 REM " --- New Record
1420 CALL "SYC.YN",1,"Is This A New Record?",1,V$,V3
1430 IF V$="" THEN GOTO 1500
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup
1510 DIM E[5],F[9],X[5],Y[5]
1520 LET E0$(22,1)="N"
1600 REM " --- Gross
1610 LET I0=0,O9=1
1620 FOR X=0 TO 4
1640 LET V0$="N",V1$="",V2$=STR(E[X]),V3$=M1$,V1=15,V2=9+X
1650 GOSUB 7000
1660 LET E[X]=V
1665 IF X[X]=0 THEN LET X[X]=E[X]
1670 IF V3=4 THEN EXITTO 3000
1680 IF V3<>2 THEN GOTO 1720
1690 IF X>0 THEN LET X=X-1
1700 GOTO 1640
1720 REM " --- Basis
1730 LET V0$="N",V2$=STR(X[X]),V3$=M1$,V1=15+M1,V2=9+X
1740 GOSUB 7000
1750 LET X[X]=V
1760 IF V3=2 THEN GOTO 1640
1800 REM " --- Tax
1820 LET V0$="N",V2$=STR(Y[X]),V3$=M1$,V1=15+2*M1,V2=9+X
1840 GOSUB 7000
1860 LET Y[X]=V
1870 IF V3=2 THEN GOTO 1730
1880 NEXT X
1890 GOTO 3000
2000 REM " --- Table
2010 IF D0$(24,1)<>"T" THEN GOTO 3900
2020 LET V0$="S",V2$=E0$(15,3),V0=3,V1=15,V2=17,V4$="Enter Valid Tax-Table Code or <F3>=Code Lookup"
2030 GOSUB 7000
2035 IF V3<>3 THEN GOTO 2070
2040 LET V4=1,V6=4,V7=3,V8=7,V9=20,V5$="I"
2050 CALL "SYC.LZ",V1,V2,V4,PRM10_DEV,V6,V7,V8,V9,V$,V5$,N0$
2060 IF V$="" THEN GOTO 2000
2070 IF V3=4 THEN LET I0=5; GOTO 3900
2080 LET E0$(15,3)=V$
2085 FIND (PRM10_DEV,KEY=N0$+"I"+E0$(15,3),DOM=2000)IOL=PRM10I
2090 PRINT @(10,22),X0$(7,20),
2095 GOTO 3900
2100 REM " --- Dependant Deduct
2110 IF D0$(24,1)<>"T" THEN GOTO 3900
2120 LET V0$="Z",V2$=E0$(18,2),V0=2,V1=15,V2=18,V4$="If a Dependent Deduction is Allowed, Enter # of Deductions here. "
2140 GOSUB 7000
2160 LET E0$(18,2)=V$
2190 GOTO 3900
2200 REM " --- Item Deduct
2220 LET V0$="Z",V2$=E0$(20,2),V0=2,V1=15,V2=19,V4$="Enter the Number of Itemized Deductions here"
2240 GOSUB 7000
2260 LET E0$(20,2)=V$
2290 GOTO 3900
2300 REM " --- Replace
2320 LET V0$="S",V1$="R",V2$=E0$(22,1),V3$="RAN",V4$="R=Replace A=Add N=None",V0=1,V1=40,V2=17
2340 GOSUB 7000
2360 LET E0$(22,1)=V$
2370 IF E0$(22,1)="N" THEN LET E0$(23,1)="",F[0]=0
2380 PRINT @(40,17),E0$(22,1),@(40,18),E0$(23,1),@(40,19),'CL',F[0]:M3$
2390 GOTO 3900
2400 REM " --- % or Amt
2410 IF E0$(22,1)="N" THEN GOTO 3900
2420 LET V0$="S",V1$="R",V2$=E0$(23,1),V3$="PA",V4$="P=Percent A=Amount",V0=1,V1=40,V2=18
2440 GOSUB 7000
2460 LET E0$(23,1)=V$
2490 GOTO 3900
2500 REM " --- Rate
2510 IF E0$(22,1)="N" THEN GOTO 3900
2520 LET V0$="N",V2$=STR(F[0]),V3$=M1$,V4$=" ",V1=40,V2=19
2530 IF E0$(23,1)<>"A" THEN LET V3$=M3$
2540 GOSUB 7000
2560 LET F[0]=V
2570 IF E0$(23,1)<>"A" THEN PRINT @(40,19),'CL',F[0]:M3$, ELSE PRINT @(40,19),F[0]:M1$,
2590 GOTO 3900
2600 REM " --- Periods Taken
2620 LET V0$="S",V1$="C",V2$=E0$(24,5),V3$="",V4$="Enter OVERRIDE Period Selection, '99999'=Inactive",V0=5,V1=15,V2=20
2630 GOSUB 7000
2640 IF V$="" OR V3=4 THEN GOTO 2690
2660 LET E0$(24,5)=V$
2690 GOTO 3900
3000 REM " --- Display
3020 LET V3=0
3040 GOSUB 5000
3900 REM " --- Next Field
3910 IF V3=4 THEN GOTO 3990
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1600,2000,2100,2200,2600,2300,2400,2500,3990
4000 REM " --- Correct?
4020 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)? ",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF V$="" THEN IF O9=0 THEN GOTO 1000
4050 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write
4110 IF O9=0 THEN GOTO 4190
4120 WRITE (PRT21_DEV,KEY=E0$(1,14))IOL=PRT21A
4190 GOTO 1000
4200 REM " --- Delete
4210 IF E[1]+E[2]+E[3]+E[4]+E[5]<>0 THEN GOTO 4000
4220 REMOVE (PRT21_DEV,KEY=E0$(1,14),DOM=4230)
4290 GOTO 1000
5000 REM " --- Display
5020 LET E0=0,X0=0,Y0=0
5030 FOR X=0 TO 4
5040 PRINT @(15,9+X),E[X]:M1$,X[X]:M1$,Y[X]:M1$,
5050 IF X>0 THEN LET E0=E0+E[X],X0=X0+X[X],Y0=Y0+Y[X]
5060 NEXT X
5070 PRINT @(15,14),E0:M1$,X0:M1$,Y0:M1$,@(15,15),E[5]:M1$,X[5]:M1$,Y[5]:M1$,
5080 PRINT @(15,17),E0$(15,3),@(15,18),E0$(18,2),@(15,19),E0$(20,2),@(15,20),E0$(24,5),@(40,17),E0$(22,1),@(40,18),E0$(23,1),
5090 IF E0$(23,1)<>"A" THEN PRINT @(40,19),'CL',F[0]:M3$, ELSE PRINT @(40,19),F[0]:M1$,
5099 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "POU - Purge Purchase Order Receipt History
0020 REM "Program POU.FA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.6 - 15Aug99 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "20Nov96 Caj: Allow for entry of single character whses (ie '4 ')(2105,2110,2120,2130,2300,2315,2320,2330,2340,8070)
0032 REM "29May97 JWK: Default purge date to end of prior per last year and require YES to purge (550,600-670,1010,4050-4080,4105)
0033 REM "07Apr99 KJS: Avoid error 26 with Y2K year (630,8170-8190)
0034 REM "13Mar00 KJS: Avoid error 46 finding default date in a March of a leap year (660)
0040 REM 
0080 BEGIN
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=10
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APM-01",FILES$[2]="IVM-01",FILES$[3]="IVM-10"
0140 LET FILES$[4]="IVM-14",FILES$[5]="POT-04",FILES$[6]="POT-14"
0145 LET FILES$[7]="POT-24",FILES$[8]="POT-34",FILES$[9]="POT-44"
0150 LET FILES$[10]="SYS-01"
0155 CALL "SYC.DA",1,1,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0165 LET APM01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM10_DEV=CHANNELS[3]
0170 LET IVM14_DEV=CHANNELS[4],POT04_DEV=CHANNELS[5],POT14_DEV=CHANNELS[6]
0175 LET POT24_DEV=CHANNELS[7],POT34_DEV=CHANNELS[8],POT44_DEV=CHANNELS[9]
0180 LET SYS01_DEV=CHANNELS[10]
0200 REM " --- IOLists
0210 APM01A: IOLIST *,X1$(1)
0220 IVM10C: IOLIST X1$(1)
0240 POT04A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0250 POT14A: IOLIST B0$,B1$,B2$,B3$(1),B4$,B5$,B6$,B[ALL]
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 APS01A: IOLIST *,VENDIMASK$,P2$,*,*,VENDOMASK$
0320 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0330 POS01A: IOLIST P0$,P1$,P2$,P3$,P4$
0400 REM " --- Parameters
0405 DIM DESCLEN[3],PARAMS[3]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=POS01A
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 LET VENDLEN=NUM(P2$(1,2))
0435 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET IVPRECISION=NUM(I2$(5,1)),ITEMLEN=NUM(I2$(1,2))
0445 FOR X=1 TO 3
0450 LET DESCLEN[X]=NUM(I2$((X-1)*2+9,2))
0460 LET PARAMS[X]=DESCLEN[X]
0465 NEXT X
0475 LET PARAMS[0]=ITEMLEN
0500 REM " --- Initializations
0510 DIM A0$(22),A1$(160),A[8]
0520 DIM B3$(22),B[12]
0530 DIM VENDFNAME$(30),VENDTNAME$(30)
0540 DIM ITEMFDESC$(30),ITEMTDESC$(30),WHSEFNAME$(30),WHSETNAME$(30)
0560 DIM IV_CHANS[14]
0570 LET IV_CHANS[1]=IVM01_DEV,IV_CHANS[14]=IVM14_DEV
0600 REM " --- Set-up default purge through date:
0610 REM  "    End up with end of prior period, 1 year ago
0620 CALL "SYC.PB",N0$,SYS01_DEV,F0$(18,6),F_PERIOD$,F_YEAR$; REM "Fiscal Period for today
0630 CALL "SYC.PA",SYS01_DEV,NUM(F_PERIOD$),FNYY_YEAR(F_YEAR$),PER_BEG$,PER_END$,STATUS; REM "Figure beginning of fiscal period
0640 IF STATUS THEN LET PER_BEG$=F0$(18,6); REM "If can't figure it out, use today's date
0660 CALL "SYC.CA",PER_BEG$,PURGEDATE$,-366; REM "One year and one day ago
0670 LET PURGEDATE$=FNM$(FNC$(PURGEDATE$))
0700 REM " --- Background 
0710 FOR X=4 TO 16 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(12,3),"Purge Through Receipt Date:",@(22,5),"Beginning Vendor: ",@(25,7),"Ending Vendor:",@(24,9),"Beginning Item:",@(27,11),"Ending Item:",@(19,13),"Beginning Warehouse:",@(22,15),"Ending Warehouse:",'SF',@(0,22),'CL',
0760 GOSUB REFRESH
1000 REM " --- Purge Through Date
1040 LET V0$="D",V1$="6N",V2$=PURGEDATE$,V3$="",V4$="Enter Invoice Date To Purge Through",V0=6,V1=40,V2=3,I0=0
1050 GOSUB 7000
1060 IF V3=4 THEN GOTO 9900
1070 IF V$="" THEN GOTO 1000
1080 LET PURGEDATE$=V$
1090 GOTO 3900
1200 REM " --- Beginning Vendor
1210 LET V0$="M",V1$="KCE",V2$=VENDFROM$,V3$=VENDIMASK$,V4$="Enter A Valid Vendor Number (<F1>=First/<F3>=Lookup)",V0=6,V1=40,V2=5
1220 GOSUB 7000
1230 ON V3 GOTO 1290,1240,1340,1270,4000
1240 LET VENDFROM$=""
1250 PRINT @(V1,V2),'CL',@(V1,V2),"First",
1260 GOTO 1340
1270 CALL "SYC.LV",V$
1280 IF V$="" THEN GOTO 1200
1290 IF V$="" THEN GOTO 1240
1300 DIM X1$(195)
1310 FIND (APM01_DEV,KEY=N0$+V$,DOM=1200)IOL=APM01A
1320 LET VENDFROM$=V$,VENDFNAME$(1)=X1$(1,30)
1330 PRINT @(V1,V2),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
1340 GOTO 3900
1400 REM " --- Ending Vendor
1410 LET V0$="M",V1$="KCE",V2$=VENDTHRU$,V3$=VENDIMASK$,V4$="Enter A Valid Vendor Number (<F1>=Last/<F3>=Lookup)",V0=6,V1=40,V2=7
1420 GOSUB 7000
1430 ON V3 GOTO 1490,1440,1550,1470,4000
1440 LET VENDTHRU$=""
1450 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
1460 GOTO 1550
1470 CALL "SYC.LV",V$
1480 IF V$="" THEN GOTO 1400
1490 IF V$="" THEN GOTO 1440
1500 IF VENDFROM$<>"" THEN IF V$<VENDFROM$ THEN GOTO 1400
1510 DIM X1$(195)
1520 FIND (APM01_DEV,KEY=N0$+V$,DOM=1400)IOL=APM01A
1530 LET VENDTHRU$=V$,VENDTNAME$(1)=X1$(1,30)
1540 PRINT @(V1,V2),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
1550 GOTO 3900
1600 REM " --- Starting Item
1605 ITEMFROM: 
1620 DIM ITEM$[6],ITEM[12]
1630 LET ACTION$="F",DEFAULT$=ITEMFROM$,V1=40,V2=9
1640 LET V4$="Enter A Valid Item Number"
1650 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1660 IF V3=2 THEN GOTO NEXT_ITEMFROM
1670 IF V3=4 THEN GOTO 4000
1680 IF V3=1 OR ITEM$[0]="" THEN PRINT @(V1,V2),'CL',"First",; LET ITEM$[0]=""; GOTO NEXT_ITEMFROM
1780 LET ITEMFROM$=ITEM$[0],ITEMFDESC$(1)=ITEM$[1]+FILL(28)
1785 PRINT @(V1,V2),ITEMFROM$(1,ITEMLEN)," ",ITEMFDESC$(1,28),
1790 NEXT_ITEMFROM: 
1795 GOTO 3900
1800 REM " --- Ending Item
1805 ITEMTHRU: 
1810 DIM ITEM$[6],ITEM[12]
1820 LET ACTION$="L",DEFAULT$=ITEMTHRU$,V1=40,V2=11,V$=""
1830 LET V4$="Enter A Valid Item Number"
1840 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1850 IF V3=2 THEN GOTO NEXT_ITEMTHRU
1860 IF V3=4 THEN GOTO 4000
1880 IF V3=1 OR ITEM$[0]="" THEN PRINT @(V1,V2),'CL',"Last",; LET ITEM$[0]=""; GOTO NEXT_ITEMTHRU
1970 IF ITEM$[0]<ITEMFROM$ THEN GOTO 1800
1975 LET ITEMTHRU$=ITEM$[0],ITEMTDESC$(1)=ITEM$[1]+FILL(28)
1980 PRINT @(V1,V2),ITEMTHRU$(1,ITEMLEN)," ",ITEMTDESC$(1,28),
1985 NEXT_ITEMTHRU: 
1990 GOTO 3900
2000 REM " --- Warehouse From
2010 LET V0$="S",V1$="",V2$=WHSEFROM$,V3$="",V4$="Enter A Valid Warehouse ID  (<F1>=First/<F3>=Lookup)",V0=2,V1=40,V2=13
2020 GOSUB 7000
2030 ON V3 GOTO 2090,2040,2190,2070,4000
2040 LET WHSEFROM$=""
2050 PRINT @(V1,V2),'CL',@(V1,V2),"First",
2060 GOTO 2190
2070 CALL "IVC.LK",3,V1,V2,V$
2080 IF V$="" THEN GOTO 2000
2090 IF V$="" THEN GOTO 2040
2100 DIM X1$(160)
2105 LET X1$(1)=N0$+"C"+V$
2110 FIND (IVM10_DEV,KEY=X1$(1,5),DOM=2000)IOL=IVM10C
2120 LET WHSEFROM$=X1$(4,2),WHSEFNAME$(1)=X1$(6,24)
2130 PRINT @(V1,V2),WHSEFROM$," ",WHSEFNAME$(1,24),'CL',
2190 GOTO 3900
2200 REM " --- Warehouse Thru
2210 LET V0$="S",V1$="",V2$=WHSETHRU$,V3$="",V4$="Enter A Valid Warehouse ID  (<F1>=Last/<F3>=Lookup)",V0=2,V1=40,V2=15
2220 GOSUB 7000
2230 ON V3 GOTO 2290,2240,2390,2270,4000
2240 LET WHSETHRU$=""
2250 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
2260 GOTO 2390
2270 CALL "IVC.LK",3,V1,V2,V$
2280 IF V$="" THEN GOTO 2200
2290 IF V$="" THEN GOTO 2240
2300 IF WHSEFROM$<>"" AND V$<FNP$(WHSEFROM$) THEN GOTO 2200
2310 DIM X1$(160)
2315 LET X1$(1)=N0$+"C"+V$
2320 FIND (IVM10_DEV,KEY=X1$(1,5),DOM=2200)IOL=IVM10C
2330 LET WHSETHRU$=X1$(4,2),WHSETNAME$(1)=X1$(6,24)
2340 PRINT @(V1,V2),WHSETHRU$," ",WHSETNAME$(1,24),'CL',
2390 GOTO 3900
3900 REM " --- Next Field
3910 IF V3=4 THEN GOTO 7000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,2200,4000
4000 REM " --- Correct
4010 GOSUB REFRESH
4020 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4051,1000
4070 CALL "SYC.YN",0,"Are You Ready To Begin Purging",0,V$,V3
4080 IF V$<>"YES" THEN GOTO 9900
4100 REM " --- Purge Receipt Hist files
4120 LET EXIT_CTRL=1
4130 READ (POT04_DEV,KEY=N0$+VENDFROM$,DOM=READHEAD)
4200 REM " --- Loop through POT-04 headers
4205 READHEAD: 
4220 LET K$=KEY(POT04_DEV,END=DONE)
4230 IF K$(1,2)<>N0$ THEN GOTO DONE
4240 READ (POT04_DEV)IOL=POT04A
4250 LET VENDOR$=A0$(3,6),PONUM$=A0$(9,7),RECEIVER$=A0$(16,7)
4260 LET RCPTDATE$=A1$(29,6)
4300 REM " --- Valid Vendor/Receipt Date?
4310 IF VENDTHRU$<>"" THEN IF VENDOR$>VENDTHRU$ THEN GOTO DONE
4360 IF RCPTDATE$>PURGEDATE$ THEN GOTO NEXTHEAD
4500 REM " --- Remove all passing detail
4510 READ (POT14_DEV,KEY=K$,DOM=READDETAIL)
4530 READDETAIL: 
4540 LET K14$=KEY(POT14_DEV,END=DONEDETAIL)
4550 IF K14$(1,22)<>K$ THEN GOTO DONEDETAIL
4560 READ (POT14_DEV)IOL=POT14A
4580 LET WHSE$=B3$(1,2),ITEM$=B3$(3,20),LINENUM$=B0$(23,3)
4590 REM " --- Valid Whse/Item?
4600 IF WHSEFROM$<>"" THEN IF WHSE$<WHSEFROM$ THEN GOTO NEXTDETAIL
4610 IF WHSETHRU$<>"" THEN IF WHSE$>WHSETHRU$ THEN GOTO NEXTDETAIL
4620 IF ITEMFROM$<>"" THEN IF ITEM$<ITEMFROM$ THEN GOTO NEXTDETAIL
4630 IF ITEMTHRU$<>"" THEN IF ITEM$>ITEMTHRU$ THEN GOTO NEXTDETAIL
4700 REM " --- Valid, so purge
4720 REMOVE (POT44_DEV,KEY=N0$+VENDOR$+RCPTDATE$+PONUM$+RECEIVER$+LINENUM$,DOM=4730)
4730 REMOVE (POT34_DEV,KEY=N0$+ITEM$+WHSE$+RCPTDATE$+VENDOR$+PONUM$+RECEIVER$+LINENUM$,DOM=4740)
4740 REMOVE (POT24_DEV,KEY=N0$+ITEM$+WHSE$+VENDOR$+RCPTDATE$+PONUM$+RECEIVER$+LINENUM$,DOM=4750)
4750 REMOVE (POT14_DEV,KEY=N0$+VENDOR$+PONUM$+RECEIVER$+LINENUM$,DOM=NEXTDETAIL)
4800 REM " --- Loop up for next detail
4805 NEXTDETAIL: 
4810 GOTO READDETAIL
4850 REM " --- Done detail for header; Remove orphaned headers
4855 DONEDETAIL: 
4860 READ (POT14_DEV,KEY=K$,DOM=4870)
4870 LET K14$=KEY(POT14_DEV,END=NEXTHEAD)
4880 IF K14$(1,22)=K$ THEN GOTO NEXTHEAD; REM "Leave hdr if there's detail
4890 REMOVE (POT04_DEV,KEY=K$,DOM=NEXTHEAD)
4900 REM " --- Loop up for next header
4905 NEXTHEAD: 
4920 GOTO READHEAD
4950 REM " --- All done
4955 DONE: 
4970 GOTO 9900
5000 REM " --- Refresh screen data
5005 REFRESH: 
5010 PRINT 'CF',
5020 PRINT @(40,3),FNB6$(PURGEDATE$),'CL',
5030 IF VENDFROM$="" THEN PRINT @(40,5),"First",'CL', ELSE PRINT @(40,5),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5040 IF VENDTHRU$="" THEN PRINT @(40,7),"Last",'CL', ELSE PRINT @(40,7),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5050 IF ITEMFROM$="" THEN PRINT @(40,9),"First" ELSE PRINT @(40,9),ITEMFROM$(1,ITEMLEN)," ",ITEMFDESC$(1,MIN(79-V1-ITEMLEN-1,30)),'CL',
5060 IF ITEMTHRU$="" THEN PRINT @(40,11),"Last" ELSE PRINT @(40,11),ITEMTHRU$(1,ITEMLEN)," ",ITEMTDESC$(1,MIN(79-V1-ITEMLEN-1,30)),'CL',
5070 IF WHSEFROM$="" THEN PRINT @(40,13),"First",'CL', ELSE PRINT @(40,13),WHSEFROM$," ",WHSEFNAME$,'CL',
5080 IF WHSETHRU$="" THEN PRINT @(40,15),"Last",'CL', ELSE PRINT @(40,15),WHSETHRU$," ",WHSETNAME$,'CL',
5190 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "POU - Purchase Requisition Register Update
0020 REM "Program POU.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "09Oct96 WGH: Write POE-42 x-ref only for 'S' Line Types (2070,2415)"
0040 REM 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0170 CLOSE (7,ERR=0180)
0200 REM " --- IOLIST's"
0210 POE02A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0220 POE01A: IOLIST B0$(1),B1$(1),B2$,B3$,B[ALL]
0310 POE22A: IOLIST X2$(1)
0315 POE31A: IOLIST X3$(1)
0320 POE32A: IOLIST X3$(1)
0330 POE42A: IOLIST X4$(1)
0350 POE52A: IOLIST X5$(1)
0360 POM02A: IOLIST Y0$(1),Y1$(1)
0380 POE11A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0390 POE12A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0500 REM " --- Initializations"
0510 DIM A0$(15),A1$(160),A[8],B0$(15),B1$(160),B[8],Y0$(4),Y1$(32)
0520 DIM W0$(18),W1$(48),W2$(32),W3$(22),W4$(40),W[12],FILES[20]
0530 DIM ITEM$[3],CHANNELS[99],PARAMS[10],PARAMS$[10],REFS[20],REFS$[20]
0550 LET FILES[1]=POS10_DEV,FILES[2]=POM02_DEV,FILES[3]=POE01_DEV
0560 LET FILES[4]=POE11_DEV,FILES[5]=POE21_DEV,FILES[6]=POE31_DEV
0570 LET FILES[7]=POE41_DEV,CHANNELS[1]=IVM01_DEV,CHANNELS[2]=IVM02_DEV
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0800 REM " --- Run Update?"
0810 LET V4$="Are You Ready To Update The "+F5$
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM " --- Position file"
0910 CALL "SYC.NB","Updating",18,COLUMN
0920 LET EXIT_CTRL=1
0990 READ (POW07_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Get next sort record"
1010 LET K$=KEY(POW07_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 READ (POW07_DEV)
1090 ON POS(SEQUENCE$="AVR") GOTO 3900,1100,1200,1300
1100 REM " --- Alpha sequence"
1110 LET B0$(1)=K$(1,2)+K$(13),X$=K$(3,10)+" "+K$(19,7)
1190 GOTO 1500
1200 REM " --- Vendor sequence"
1210 LET B0$(1)=K$(1,2)+K$(13),X$=FNF$(K$(13,VENDLEN),M0$)+" "+K$(19,7)
1290 GOTO 1500
1300 REM " --- Requisition sequence"
1310 LET B0$(1)=K$(1,2)+K$(20,6)+K$(13,7),X$=K$(19,7)
1500 REM " --- Read requisition header"
1510 PRINT @(COLUMN,11),X$,
1520 FIND (POE01_DEV,KEY=B0$,DOM=3900)IOL=POE01A
1600 REM " --- Get next PO Number"
1610 CALL "POC.NA","A","P",POS10_DEV,N0$,V$
1620 LET A0$(1)=B0$,A0$(9)=V$
1800 REM " --- Create purchase order from requisition"
1810 DIM X2$(15),X3$(15),X5$(22)
1820 LET A1$(1)=B1$,A1$(112,7)=B0$(9),A[ALL]=B[ALL],X2$(1)=A0$
1830 LET X3$(1)=N0$+A0$(9)+A0$(3),X5$(1)=N0$+A1$(112,7)+A0$(3)
1850 WRITE (POE02_DEV,KEY=A0$)IOL=POE02A
1860 WRITE (POE22_DEV,KEY=X2$)IOL=POE22A
1870 WRITE (POE32_DEV,KEY=X3$)IOL=POE32A
1880 WRITE (POE52_DEV,KEY=X5$)IOL=POE52A
1900 REM " --- Position detail file"
1990 READ (POE11_DEV,KEY=B0$,DOM=2000)
2000 REM " --- Read next detail line"
2010 LET K$=KEY(POE11_DEV,END=3000)
2020 IF POS(B0$=K$)<>1 THEN GOTO 3000
2030 READ (POE11_DEV)IOL=POE11A
2040 LET REQ$=W0$(9),W0$(1,15)=A0$,W[3]=W[2]
2050 WRITE (POE12_DEV,KEY=W0$)IOL=POE12A
2060 LET X4$=W0$(1,2)+W3$(3,20)+W0$(9,10)+W0$(3,6)
2200 REM " --- Read line code"
2210 IF N0$+W1$(1,2)=Y0$ THEN GOTO 2400
2220 LET Y0$(1)=N0$+W1$(1,2),Y1$(1)=""
2230 FIND (POM02_DEV,KEY=Y0$,DOM=2400)IOL=POM02A
2400 REM " --- Update inventory?"
2410 IF Y1$(21,1)<>"S" THEN GOTO 2500
2415 WRITE (POE42_DEV,KEY=X4$)IOL=POE42A
2420 LET ITEM$[0]=N0$,ITEM$[1]=W3$(1,2),ITEM$[2]=W3$(3),REFS[0]=W[3]
2430 LET ACTION$="OO",PARAMS$[2]=I2$,PARAMS$[3]=I3$,PARAMS$[4]=I4$
2440 CALL "IVC.UA",ACTION$,CHANNELS[ALL],PARAMS[ALL],PARAMS$[ALL],ITEM$[ALL],REFS$[ALL],REFS[ALL],STATUS
2500 REM " --- Update work order?"
2510 IF SF$="N" THEN GOTO 2900
2520 IF POS(Y1$(21,1)="NS")=0 THEN GOTO 2900
2530 IF FNP$(W2$(1,7))="" THEN GOTO 2900
2540 LET OLDWO$=W2$(1,10),NEWWO$=OLDWO$,PO$=W0$(9)
2550 CALL "POC.WA",WOE22_DEV,WOE32_DEV,N0$,PO$,"P",Y1$,OLDWO$,NEWWO$,I[1],STATUS
2900 REM " --- Loop back for next detail"
2990 GOTO 2000
3000 REM " --- Delete requisition source"
3010 LET PARAMS$[0]=N0$,PARAMS$[1]="N",PARAMS$[2]=I2$
3020 LET PARAMS$[3]=I3$,PARAMS$[4]=I4$,PARAMS[0]=I[1]
3030 CALL "POC.EA","R",FILES[ALL],PARAMS[ALL],PARAMS$[ALL],B0$,B1$,STATUS
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9910 RUN "SYS.AA"
9999 END

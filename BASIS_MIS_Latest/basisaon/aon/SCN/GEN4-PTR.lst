0001 SETESC 8800; SETERR 8900
0010 REM "GEN4-PTR - DEFINE AND/OR SELECT PRINTER
0020 REM "COPYRIGHT (C) 1989 BY ALLEN D. MIGLORE.  ALL RIGHTS RESERVED.
0030 REM 
0031 REM "09Aug96 Caj: ERROR 12 @ 1635 when running report to PF(1635)
0100 REM ^100 - CHECK FOR CALL OPTIONS
0110 SETERR 0300
0120 REM "P$=PARAM, OPTION$=SELECT/DESELECT, WIDTH=REPORT WIDTH, UNIT=PTR OPEN CHANNEL, PTR$=RETURNED PRINTER, DESC$=PTR DESC, DEV$=PTR BBX NAME, DEVHEX$=BLOCKED INIT/RESET/ETC STRINGS
0130 ENTER P$,OPTION$,WIDTH,UNIT,PTR$,DESC$,DEV$,DEVHEX$
0140 LET WPG$=PGM(-2); CALL "GEN4-LAN",P$,WPG$,GBL$[ALL],MSG$[ALL],ERRMSG$; IF ERRMSG$>"" THEN GOTO 8940
0150 SETERR 8900
0160 LET GEN4MST=NUM(STBL("*GEN4MST"))
0170 LET OVERWRITE=0; IF STBL("*GENOW",ERR=0180)="Y" THEN LET OVERWRITE=1
0180 LET CTLMAP$=STBL("*GEN_CTLMAP"),CHRMAP$=STBL("!TERMS")
0190 LET CALLED=1; GOSUB 8010
0200 IF OPTION$="SELECT" THEN GOSUB 1400; GOTO 9000
0210 IF OPTION$="OPEN" OR OPTION$="OPEN_TEST" OR OPTION$="OPEN_AUTO" THEN GOSUB 1500; GOTO 9000
0220 IF OPTION$="CLOSE" THEN GOSUB 1900; GOTO 9000
0230 IF OPTION$="DESC" THEN GOSUB 1500; GOTO 9000
0240 GOTO 9000
0300 REM ^100 - INIT ON RUN
0310 BEGIN 
0320 SETESC 8800; SETERR 8900
0330 LET GEN4MST=1; OPEN (GEN4MST)"GEN4MST"
0340 GOSUB 20000
1000 REM ^1000 - GET LIST OF PRINTERS, FIRST IN FMMST, THEN USE TSK(X)
1010 LET I=0,CFGDEV$="",CFGDESC$="",CFGPTR$="",CFGWL$="",CFGBBX$="",COMPRESS=0,BBXPRINTER=0; DIM Z1$(6),Z2$(40),Z3$(6),Z4$(6)
1020 READ (GEN4MST,KEY="D",DOM=1030)
1030 READ (GEN4MST,ERR=1070)PKY$,PREC$; IF POS("D"=PKY$)<>1 THEN GOTO 1070
1040 LET Z1$(1)=PKY$(2),Z2$(1)=PREC$(1,40),Z3$(1)=PREC$(41,6),Z4$(1)=PREC$(47,6)
1050 LET CFGPTR$=CFGPTR$+Z1$,CFGDESC$=CFGDESC$+Z2$,CFGDEV$=CFGDEV$+Z3$,CFGWL$=CFGWL$+Z4$,CFGBBX$=CFGBBX$+"0"
1060 GOTO 1030
1070 REM "FILIX PRINTERS?
1080 LET TESTFL$=STBL("*GENFILIX",ERR=1140)+"FMMST",FMMST=UNT; OPEN (FMMST,ERR=1140)TESTFL$
1090 READ (FMMST,KEY="D",DOM=1100)
1100 READ (FMMST,END=1140)PKY$,PREC$; IF POS("D"=PKY$)<>1 THEN GOTO 1140
1110 LET Z1$(1)=PKY$(2),Z2$(1)=PREC$(1,40),Z3$(1)=PREC$(41,6),Z4$(1)=PREC$(47,6)
1120 IF POS(Z1$=CFGPTR$,LEN(Z1$))=0 THEN LET CFGPTR$=CFGPTR$+Z1$,CFGDESC$=CFGDESC$+Z2$,CFGDEV$=CFGDEV$+Z3$,CFGWL$=CFGWL$+Z4$,CFGBBX$=CFGBBX$+"1"; REM "ADD FILIX PRINTERS NOT FOUND IN GEN PRINTERS
1130 GOTO 1100
1140 REM "HOST PRINTER DEVICES
1150 ON NUM(STBL("*GEN_HOST"))-1 GOTO 1160,1200,1230
1160 LET X$=TSK(I,ERR=1260),I=I+1
1170 IF X$(1,1)<>$01$ THEN GOTO 1160
1180 LET WDEV$=X$(3),WDESC$=WDEV$(POS($00$=WDEV$)+1),WDEV$=WDEV$(1,POS($00$=WDEV$)-1),WDESC$=WDESC$(1,POS($00$=WDESC$)-1),WWL$="132066"; GOSUB 10000; IF BBXW>0 THEN LET WWL$(1,3)=STR(BBXW:"000")
1190 GOTO 1240
1200 LET I=I+1,X$=TSK(0); IF I*6>LEN(X$) THEN GOTO 1260
1210 LET WDEV$=X$(I*6-5,2); IF POS(WDEV$(1,1)="LP")=0 THEN GOTO 1200
1220 LET WDESC$=MSG$[19]+" "+WDEV$,WWL$="132066"; GOTO 1240
1230 REM 
1240 LET Z1$(1)=WDEV$,Z2$(1)=WDESC$,Z3$(1)=WDEV$,Z4$(1)=WWL$; IF POS(Z3$=CFGDEV$,LEN(Z3$))=0 THEN LET CFGPTR$=CFGPTR$+Z1$,CFGDESC$=CFGDESC$+Z2$,CFGDEV$=CFGDEV$+Z3$,CFGWL$=CFGWL$+Z4$,CFGBBX$=CFGBBX$+"2"
1250 GOTO 1150
1260 REM "ADD VDT/FILE OPTIONS
1270 IF WIDTH>80 THEN LET VDTLEN$=STR(NUM(P$(255,3))-6:"000") ELSE CALL "GEN4-FST",P$,0,"",0,0,VDTROWS,""; LET VDTLEN$=STR(VDTROWS:"000")
1280 LET Z1$(1)="VDT",Z2$(1)=MSG$[1],Z3$(1)=FID(0),Z4$(1)="255"+VDTLEN$,CFGPTR$=CFGPTR$+Z1$,CFGDESC$=CFGDESC$+Z2$,CFGDEV$=CFGDEV$+Z3$,CFGWL$=CFGWL$+Z4$,CFGBBX$=CFGBBX$+"0"
1290 LET Z1$(1)="FILE",Z2$(1)=MSG$[2],Z3$(1)="",Z4$(1)="255066",CFGPTR$=CFGPTR$+Z1$,CFGDESC$=CFGDESC$+Z2$,CFGDEV$=CFGDEV$+Z3$,CFGWL$=CFGWL$+Z4$,CFGBBX$=CFGBBX$+"2"
1295 LET Z1$(1)="ADDON",Z2$(1)="Print To ADD+ON Default Printer",Z3$(1)="",Z4$(1)="255066",CFGPTR$=CFGPTR$+Z1$,CFGDESC$=CFGDESC$+Z2$,CFGDEV$=CFGDEV$+Z3$,CFGWL$=CFGWL$+Z4$,CFGBBX$=CFGBBX$+"2"
1297 LET Z1$(1)="PF",Z2$(1)="Print To ADD+ON Print-To-File",Z3$(1)="",Z4$(1)="255066",CFGPTR$=CFGPTR$+Z1$,CFGDESC$=CFGDESC$+Z2$,CFGDEV$=CFGDEV$+Z3$,CFGWL$=CFGWL$+Z4$,CFGBBX$=CFGBBX$+"2"
1300 RETURN 
1400 REM ^100 - SELECT PRINTER
1410 GOSUB 1000
1420 GOSUB 8020
1430 IF X=0 THEN EXITTO 8940 ELSE LET PTR$=X$(X*48-47,6),X=POS(PTR$=CFGPTR$,6),X=1+INT(X/6),DESC$=CVS(CFGDESC$(X*40-39,40),3),PTR$=CVS(PTR$,3),BBXPRINTER=NUM(CFGBBX$(X,1))
1440 LET DEV$=""; IF PTR$<>"FILE" THEN LET DEV$=CVS(CFGDEV$(X*6-5,6),3)
1450 IF PTR$="FILE" THEN LET DEV$="GEN_"+FID(0)+".PTR"; GOSUB 2000; LET DESC$=DEV$
1455 IF PTR$="ADDON" OR PTR$="PF" THEN GOTO 1470
1460 IF DEV$="" THEN GOTO 1420
1470 LET P$(200,6)=PTR$,P$(206,40)=DESC$; GOSUB 20100
1480 RETURN 
1500 REM ^100 - OPEN PRINTER
1510 GOSUB 1000
1520 DIM DEVHEX$(10+500)
1530 LET U=UNT; IF LEN(PTR$)>6 THEN GOSUB 2200; GOTO 1550
1540 LET PTR$=PTR$+"      ",PTR$=PTR$(1,6),X=POS(PTR$=CFGPTR$,6); IF X=0 THEN LET BBXPRINTER=2; GOSUB 2200 ELSE LET X=1+INT(X/6),DESC$=CVS(CFGDESC$(X*40-39,40),3),DEV$=CVS(CFGDEV$(X*6-5,6),3),DEVHEX$(1,6)=CFGWL$(X*6-5,6),DEVHEX$(7,1)="2",PTR$=CVS(PTR$,3),BBXPRINTER=NUM(CFGBBX$(X,1))
1550 IF OPTION$="DESC" THEN RETURN 
1560 IF PTR$="FILE" THEN LET DEV$=FNTRIM$(P$(206,40)),DEVHEX$(1,3)=STR(WIDTH:"000"); GOSUB 2000; LET DESC$=DEV$
1570 LET STDWIDTH=NUM(DEVHEX$(1,3)),STDLENGTH=NUM(DEVHEX$(4,3)); GOSUB 4000
1580 IF MODELIST$="" THEN GOTO 8940 ELSE IF MODE$>"" THEN LET DEVHEX$(1,6)=MODE$(11,6)
1590 LET PTR$=CVS(PTR$,3),P$(200,6)=PTR$,P$(206,40)=DESC$; GOSUB 20100
1600 IF OPTION$="OPEN_TEST" THEN GOTO 9000
1610 IF BBXPRINTER=2 THEN LET DEVHEX$(7,1)=STBL("*GEN_FF",ERR=1630)
1620 LET XXWCH=GEN4MST; IF BBXPRINTER=1 THEN LET XXWCH=FMMST
1630 IF PTR$<>"FILE" AND PTR$<>"VDT" AND PTR$<>"ADDON" AND PTR$<>"PF" THEN FIND (XXWCH,KEY="D"+P$(200,6),ERR=1640)*,PREC$; GOSUB 3000; LET DEVHEX$(7,1)=PREC$(53,1),DEVHEX$(8)=PREC$(54,310)
1632 IF PTR$="PF" THEN LET ADDON_PTR$="PF" ELSE LET ADDON_PTR$=""
1635 IF PTR$="ADDON" OR PTR$="PF" THEN CALL "SYC.GA",U,2,"",ADDON_PTR$,ASTATUS; IF ASTATUS=0 THEN PRINT (U)'FF'; LET XXTYPE$="PTR"; GOTO 1700
1640 OPEN (U,ERR=8110)DEV$
1650 CALL "GEN4-FST",P$,U,XXTYPE$,0,0,0,""; IF XXTYPE$<>"TXT" THEN GOTO 1700
1660 ON NUM(STBL("*GEN_HOST"))-1 GOTO 1700,1670,1690
1670 CLOSE(U); OPEN(U,OPT="TEXT")DEV$; REM "TBRED
1680 GOTO 1700
1690 CLOSE (U); OPEN (U,ISZ=1)DEV$; GOTO 1700
1700 IF PTR$="FILE" THEN LOCK (U,ERR=8140)
1710 LET UNIT=U
1720 GOSUB 3100
1730 IF PTR$="VDT" OR PTR$="FILE" OR PTR$="ADDON" OR PTR$="PF" THEN GOTO 9000
1740 IF BBXPRINTER<>2 THEN GOTO 1790 ELSE LET NOT_COMPRESSED=1
1750 CALL "GEN4-FST",P$,U,"",0,WCOLS,WROWS,WDIM$; IF WDIM$="" THEN LET WMAXCOLS=WCOLS ELSE LET WMAXCOLS=ASC(WDIM$(3,1))
1760 IF WMAXCOLS<WIDTH AND NOT_COMPRESSED AND STBL("*GEN_HOST")<>"2" THEN PRINT (U,ERR=8120,TIM=5)'CP',; LET NOT_COMPRESSED=0; GOTO 1750
1770 IF WMAXCOLS<WIDTH THEN GOTO 8130
1780 LET DEVHEX$(1,3)=STR(WMAXCOLS:"000"),DEVHEX$(4,3)=STR(WROWS:"000")
1790 IF POS(DEVHEX$(7,1)="13")>0 THEN PRINT (U,ERR=8120,TIM=5)CHR(12),
1800 IF BBXPRINTER<>2 THEN PRINT (U,ERR=8120,TIM=5)'BO',FNSTRIP$(DEVHEX$(8,40)),$0D$,
1810 IF BBXPRINTER<>2 THEN IF MODE$>"" THEN PRINT (U,ERR=8120,TIM=5)FNSTRIP$(MODE$(17)),$0D$,
1820 GOTO 9000
1900 REM ^100 - CLOSE PRINTER
1910 LET PTR$=CVS(P$(200,6),3),U=UNIT
1920 IF PTR$="FILE" THEN CLOSE (U,ERR=9000); GOTO 9000
1930 IF PTR$="VDT" THEN GOTO 9000; REM "UNLIKE FILIX, VDT OUTPUT DOESN'T GO TO FILE
1940 IF DEVHEX$>"" THEN IF POS(DEVHEX$(7,1)="23")>0 THEN PRINT (U,ERR=1940)CHR(12),
1945 IF PTR$="ADDON" OR PTR$="PF" THEN GOTO 1960
1950 IF DEVHEX$>"" THEN PRINT (U,ERR=1960)FNSTRIP$(DEVHEX$(48,40)),$0D$,'EO',
1960 CLOSE (U,ERR=9000); GOTO 9000
2000 REM ^100 - ENTER FILE NAME
2010 IF FID(0)="IO" OR OPTION$="OPEN_AUTO" OR OPTION$="OPEN_TEST" THEN LET DEV$=CVS(P$(206,40),3); GOTO 2170
2020 LET WINDOW=0; CALL "GEN4-WDW",P$,"OPEN",18,10,44,5,"*",WINDOW,X$,5
2030 IF P$(200,6)="FILE  " THEN LET DEV$=P$(206,40)
2040 SETERR 8900; GOSUB 8150; IF C=10 THEN LET DEV$=""; EXITTO 8940 ELSE LET DEV$=CVS(DEV$,3)
2050 LET TESTNM$=DEV$; GOSUB 22200; IF NOT_OKAY THEN GOTO 2040
2060 LET U=UNT; OPEN (U,ERR=2090)DEV$; CALL "GEN4-FST",P$,U,X$,0,0,0,""; CLOSE (U); IF X$<>"TXT" THEN GOTO 2040
2070 LET TEXTFL$=DEV$; GOSUB 8160; IF X<2 THEN GOTO 2040
2080 ERASE DEV$,ERR=2040
2090 ON NUM(STBL("*GEN_HOST"))-1 GOTO 2100,2110,2100
2100 STRING DEV$,ERR=2040; GOTO 2150
2110 SETERR 2040
2120 TEXT DEV$,NUM(STBL("*GENDISK")),0
2130 SETERR 8900; GOTO 2150
2140 REM "
2150 CALL "GEN4-WDW",P$,"CLOSE",0,0,0,0,"",WINDOW,"",0
2160 REM LET DEVHEX$(1,3)=STR(WIDTH:"000"); REM "SET TO WIDTH REQUIRED
2170 RETURN 
2200 REM ^100 - TRY TO OPEN FILENAME DIRECTLY ENTERED
2210 LET PTR$=FNTRIM$(PTR$); IF LEN(PTR$)>40 THEN GOTO 8220
2220 LET TESTNM$=PTR$; GOSUB 22200; IF NOT_OKAY THEN GOTO 8200
2230 LET TEST=UNT; OPEN (TEST,ERR=2270)PTR$; CALL "GEN4-FST",P$,TEST,X$,0,0,0,""; CLOSE (TEST)
2240 IF X$<>"TXT" THEN GOTO 8200
2250 LET TEXTFL$=PTR$; GOSUB 8160; IF X<2 THEN GOTO 8940
2260 ERASE PTR$,ERR=2270
2270 ON NUM(STBL("*GEN_HOST"))-1 GOTO 2280,2300,2280
2280 STRING PTR$,0,ERR=8210
2290 GOTO 2340
2300 SETERR 8210
2310 TEXT PTR$,NUM(STBL("*GENDISK")),0
2320 SETERR 8900; GOTO 2340
2330 REM "
2340 LET P$(200,6)="FILE",P$(206,40)=PTR$,DESC$=PTR$,PTR$="FILE"
2350 RETURN 
3000 REM ^1000 - CONVERT PREC$ STRINGS TO PRINTABLE VALUES
3010 CALL "GEN4-CNV",P$,6,PREC$(54,40),HEX$,X$; LET PREC$(54,40)=HEX$
3020 CALL "GEN4-CNV",P$,6,PREC$(94,40),HEX$,X$; LET PREC$(94,40)=HEX$
3030 FOR I=134 TO 234 STEP 20
3040 CALL "GEN4-CNV",P$,6,PREC$(I,20),HEX$,X$; LET PREC$(I,20)=HEX$
3050 NEXT I
3060 FOR I=254 TO 354 STEP 10
3070 CALL "GEN4-CNV",P$,6,PREC$(I,10),HEX$,X$; LET PREC$(I,10)=HEX$
3080 NEXT I
3090 RETURN 
3100 REM ^100 - SET DEFAULT LINE DRAW
3110 LET DEFDRAW$="-|********+"
3120 FOR I=1 TO 11
3130 IF POS(" "<>DEVHEX$(198+I*10,10))=0 THEN LET DEVHEX$(198+I*10,1)=DEFDRAW$(I,1)
3140 NEXT I
3150 RETURN 
4000 REM ^1000 - IF STANDARD DOESN'T FIT, SEE IF ADDITIONAL MODE WILL
4010 LET MODE$="",MODELIST$=""; IF STDWIDTH>=WIDTH THEN LET MODE$=FILL(76),MODE$(1,10)=STANDARD$,MODE$(11,3)=STR(STDWIDTH),MODE$(14,3)=STR(STDLENGTH),MODELIST$=MODE$(1,10)
4020 LET PTR$=PTR$+FILL(6),PTR$=PTR$(1,6); IF BBXPRINTER=0 THEN LET MODECH=GEN4MST FI; IF BBXPRINTER=1 THEN LET MODECH=FMMST FI; FIND (MODECH,KEY="F"+PTR$,ERR=4150)*,MODES$
4030 FOR MODE=1 TO 4
4040 LET W=NUM(MODES$(MODE*76-65,3),ERR=4060)
4050 IF W>=WIDTH THEN LET MODELIST$=MODELIST$+MODES$(MODE*76-75,10),MODE$=MODE$+MODES$(MODE*76-75,76)
4060 NEXT MODE
4070 IF OPTION$="SELECT" THEN RETURN 
4080 IF MODELIST$="" THEN EXITTO 8130 ELSE IF LEN(MODELIST$)=10 THEN GOTO 4130
4090 LET DEFAULT=POS(FNUCASE$(P$(259,10))=FNUCASE$(MODELIST$),10),DEFAULT=1+INT(DEFAULT/10)
4100 IF FID(0)<>"IO" AND OPTION$<>"OPEN_AUTO" THEN GOSUB 8170 ELSE LET X=DEFAULT
4110 IF X=0 THEN LET MODELIST$=""; RETURN 
4120 LET MODE$=MODE$(X*76-75,76); REM "CHOOSE THE SELECTED ONE
4130 IF CVS(MODE$(17,60),3)>"" THEN CALL "GEN4-CNV",P$,6,MODE$(17,60),HEX$,ERROR$; LET MODE$(17,60)=HEX$
4140 LET P$(259,10)=MODE$(1,10); GOSUB 20100
4150 RETURN 
7000 REM 7000 - INPUT CONTROL
7010 LET EXITS$=CHR(10),SAVEEXITS$=CHR(23)+CHR(24)+CHR(9)+SPECIALEXITS$
7020 IF FLDNUM=1 THEN LET SAVEEXITS$=SAVEEXITS$(2); REM "NO UP ARROW AT FIELD 1
7030 LET STARTPOS=0,INITFLG=0,HELPKEY$=PGM(-2)+$FF$+HELP$
7040 CALL "GEN4-ENT",P$,TP$,REC$,XXCOL,XXROW,DISPLEN,DISPLNS,XXPOS,XXLEN,XXDEC,MSK$,CS$,JS$,SHELP$,HELPKEY$,C,EXITS$,SAVEEXITS$,STARTPOS,INITFLG
7050 IF C=10 THEN EXITTO 0300
7060 LET DISPLNS=1,XXDEC=0,MSK$="",CS$="",JS$="",SHELP$="",HELPKEY$="",SPECIALEXITS$=""
7070 RETURN 
8000 REM 8000 - MESSAGES/SCREEN IOLISTS
8010 LET STANDARD$=MSG$[3]; RETURN 
8020 DIM Z$(48),X$(0)
8030 FOR I=1 TO LEN(CFGPTR$)/6
8040 LET PTR$=CFGPTR$(I*6-5,6),STDWIDTH=NUM(CFGWL$(I*6-5,3)),STDLENGTH=NUM(CFGWL$(I*6-2,3)),BBXPRINTER=NUM(CFGBBX$(I,1)); GOSUB 4000
8050 IF MODELIST$>"" THEN LET Z$(1)=CFGPTR$(I*6-5,6)+" "+CFGDESC$(I*40-39,40),X$=X$+Z$
8060 NEXT I
8070 IF X$="" THEN CALL "GEN4-MSG",P$,PGM(-2)+$FF$+"NO DEVICES",0,MSG$[18],0; GOTO 8940
8080 LET DEFAULT=1+INT(POS(P$(200,6)=X$,48)/48)
8090 CALL "GEN4-SEL",P$,MSG$[4],X$,48,0,X,PGM(-2),"SELECT DEVICE",DEFAULT,C,CHR(10)
8100 RETURN 
8110 CALL "GEN4-CNV",P$,25,MSG$[5]+$FF80$+"@PRINTER"+$FF81$+DEV$,EMSG$,""; CALL "GEN4-MSG",P$,PGM(-2)+$FF$+"OPEN ERROR",1,EMSG$,X; IF X=1 THEN RETRY ELSE LET PTR$="ERROR"; GOTO 9000
8120 CALL "GEN4-CNV",P$,25,MSG$[6]+$FF80$+"@PRINTER"+$FF81$+DEV$,EMSG$,""; CALL "GEN4-MSG",P$,PGM(-2)+$FF$+"INIT ERROR",1,EMSG$,X; IF X=0 OR X=2 THEN CLOSE (U); LET PTR$="ERROR"; GOTO 9000 ELSE IF X=1 THEN RETRY
8130 CALL "GEN4-MSG",P$,PGM(-2)+$FF$+"NO CP",0,MSG$[7],0; GOTO 8940
8140 CALL "GEN4-CNV",P$,25,MSG$[8]+$FF80$+"@FILE"+$FF81$+DEV$,EMSG$,""; CALL "GEN4-MSG",P$,PGM(-2)+$FF$+"LOCK FILE ERROR",1,EMSG$,X; IF X=1 THEN RETRY ELSE CLOSE (U); LET PTR$="ERROR"; GOTO 9000
8150 CALL "GEN4-ENT",P$,"T",DEV$,1,0,40,1,1,40,0,"","","",MSG$[9],PGM(-2)+$FF$+"FILE NAME",C,CHR(10),"",0,0; RETURN 
8160 IF OVERWRITE THEN LET X=2; RETURN ELSE CALL "GEN4-CNV",P$,25,MSG$[10]+$FF80$+"@FILE"+$FF81$+CVS(TEXTFL$,3),EMSG$,""; CALL "GEN4-MSG",P$,PGM(-2)+$FF$+"OVERWRITE",2,EMSG$,X; RETURN 
8170 LET X$="",Z$=FILL(30); FOR I=1 TO LEN(MODELIST$)/10; LET Z$(1)=MODELIST$(I*10-9,10),Z$(16)=STR(NUM(MODE$(I*76-65,3)):"##0"),Z$(25)=STR(NUM(MODE$(I*76-62,3)):"##0"),X$=X$+Z$; NEXT I
8180 LET SHEAD$=FILL(30),SHEAD$(1)=MSG$[11],SHEAD$(12)="| "+MSG$[12],SHEAD$(22)="| "+MSG$[13]
8190 CALL "GEN4-CNV",P$,25,MSG$[14]+$FF80$+"@PRINTER"+$FF81$+CVS(PTR$,3),WTTL$,""; CALL "GEN4-SEL",P$,WTTL$+$FF$+SHEAD$,X$,LEN(Z$),0,X,PGM(-2),"SELECT MODE",DEFAULT,C,CHR(10); RETURN 
8200 CALL "GEN4-CNV",P$,25,MSG$[15]+$FF80$+"@PRINTER"+$FF81$+CVS(PTR$,3),EMSG$,""; CALL "GEN4-MSG",P$,PGM(-2)+$FF$+"INVALID FILE",0,EMSG$,0; GOTO 8940
8210 SETERR 8900; CALL "GEN4-CNV",P$,25,MSG$[16]+$FF80$+"@FILE"+$FF81$+CVS(PTR$,3),EMSG$,""; CALL "GEN4-MSG",P$,PGM(-2)+$FF$+"CAN'T CREATE FILE",0,EMSG$,0; GOTO 8940
8220 CALL "GEN4-MSG",P$,PGM(-2)+$FF$+"FILE NAME LENGTH",0,MSG$[17],0; GOTO 8940
8800 REM 8800 - SETESC TRAP
8810 PRINT 'BE',
8820 GOTO 8940
8830 RETURN 
8900 REM 8900 - ERROR TRAP
8910 LET E0$=PGM(-2),E1$=STR(ERR),E2$=STR(TCB(5)),E3$=""; IF TCB(13)=0 THEN LET E3$=LST(PGM(TCB(5)),ERR=8920)
8920 CALL "GEN4-ERR",P$,E0$,E1$,E2$,E3$,C
8930 IF C=0 THEN RETRY ELSE IF C=98 THEN ESCAPE
8940 LET PTR$="ERROR"; GOTO 9000
9000 REM 9000 - EXIT
9010 PRINT 'BE',
9020 IF COMPRESS THEN PRINT 'CS','SP',
9030 IF WINDOW>0 THEN CALL "GEN4-WDW",P$,"CLOSE",0,0,0,0,"",WINDOW,"",0
9040 IF FMMST THEN CLOSE (FMMST)
9050 IF CALLED THEN SETERR 9060; EXIT 
9060 RUN "FMMAIN",ERR=9900
9100 REM ^100 - ERASE WORK FILE BEFORE EXIT
9110 CALL "GEN4-CNV",P$,101,STR(U),NAME$,""
9120 CLOSE (U); ERASE NAME$,ERR=9000; GOTO 9000
9900 REM 9900 - BAIL OUT
9910 END
10000 REM ^1000 - CHECK FOR BBX TSK WIDTH INFO
10010 LET BBXW=0,X=POS("CPCOLS="=X$); IF X=0 THEN GOTO 10100
10020 LET CPCOLS$="",XX$=X$(X+7)
10030 WHILE XX$>""
10040 IF POS(XX$(1,1)="0123456789")=0 THEN EXITTO 10070
10050 LET CPCOLS$=CPCOLS$+XX$(1,1); IF LEN(XX$)>1 THEN LET XX$=XX$(2) ELSE LET XX$=""
10060 WEND
10070 LET BBXW=NUM(CPCOLS$,ERR=10080)
10080 RETURN 
10100 REM ^100 - CHECK FOR SPCOLS
10110 LET X=POS("SPCOLS="=X$); IF X=0 THEN RETURN 
10120 GOTO 10020
15000 REM 15000 - FUNCTIONS
15010 DEF FNSTRIP$(WX$)=CVS(WX$,3)
17000 REM 17000 - FUNCTIONS
17010 DEF FNTRIM$(WX$)=CVS(WX$,3)
17020 DEF FNUCASE$(WX$)=CVS(WX$,4)
20000 REM 20000 - GET P$ PARAMETER VARIABLE
20010 LET P$=STBL("*GENPARAM",ERR=20020)
20020 RETURN 
20100 REM ^100 - SET P$ BACK INTO GLOBAL VARIABLE
20110 LET P$=STBL("*GENPARAM",P$)
20120 RETURN 
21000 REM 21000 - RETURN CHARACTER (C$), OR CONTROL VALUE (C)
21010 INPUT (0,SIZ=1)'EE',C$,'BE',
21020 IF C$>=$20$ AND C$<=$FE$ OR (C$="" AND CTL=0) THEN LET C=0; RETURN ELSE IF C$>"" AND CTL=5 THEN GOTO 21010
21030 LET C=POS(BIN(CTL,2)=CTLMAP$,2); IF C>0 THEN LET C=1+INT(C/2); GOTO 21060
21040 LET C=POS(C$=CHRMAP$,2); IF C>0 THEN LET C=ASC(CHRMAP$(C+1,1)); GOTO 21060
21050 GOTO 21010
21060 IF C<36 THEN RETURN 
21070 IF C=36 THEN CALL "GEN4-CAL",P$,TYPE$,FILLVAL,FILLFLAG$ ELSE IF C=37 THEN PRINT 'RS',; GOTO 21010 ELSE IF C=39 THEN LET ESC2=0; GOTO 21110
21080 IF PGM(-2)="GEN4-ENT" THEN IF C=36 AND FILLFLAG$="Y" THEN LET REC$(XXPOS,XXLEN)=STR(FILLVAL),STARTPOS=0; EXITTO 0300
21090 IF PGM(-2)="GEN4-ENT" THEN IF C=38 THEN LET REC$(XXPOS,XXLEN)=SAVEDATA$,STARTPOS=0; EXITTO 0300
21100 GOTO 21010
21110 INPUT (0,SIZ=1,TIM=2,ERR=21130)'EE',C$,'BE',; LET C=POS(BIN(CTL,2)=CTLMAP$,2),C=1+INT(C/2); IF C=39 THEN LET ESC2=1; GOTO 21110
21120 LET C=POS(C$=STBL("*GEN_ESC")),C$=""; IF C>0 THEN LET C=C+ESC2*10; RETURN 
21130 PRINT 'RB','CI',; GOTO 21010
22000 REM ^1000 - INIT ATTRIB
22010 IF INT(XXATT/4)=0 THEN PRINT 'SB', ELSE PRINT 'SF',
22020 IF MOD(XXATT,4)=1 THEN PRINT 'BU', ELSE IF MOD(XXATT,4)=2 THEN PRINT 'BR', ELSE IF MOD(XXATT,4)=3 THEN PRINT 'BU','BR',
22030 RETURN 
22100 REM ^100 - RESET ATTRIB
22110 PRINT 'SF','EU','ER',
22120 RETURN 
22200 REM ^100 - TEST FILE VALIDITY
22210 LET NOT_OKAY=1,TESTNM$=FNTRIM$(TESTNM$); IF POS(" "=TESTNM$)>0 THEN RETURN 
22220 LET X=POS("\"=TESTNM$); IF X>0 THEN LET TESTNM$(X,1)="/"; GOTO 22220
22230 IF POS("DOS"=INFO(0,0)) OR POS("WIN"=INFO(0,0)) THEN LET TESTNM$=FNUCASE$(TESTNM$)
22240 IF POS("DOS"=INFO(0,0)) OR POS("WIN"=INFO(0,0)) THEN IF POS(".BAT"=TESTNM$)>0 OR POS(".SYS"=TESTNM$)>0 THEN RETURN ELSE GOTO 22300
22250 REM "UNIX NAMES
22260 IF POS("/dev/"=TESTNM$)=1 OR POS("/etc/"=TESTNM$)=1 OR POS("/bin/"=TESTNM$)=1 OR POS("/usr/bin/"=TESTNM$)=1 OR TESTNM$="/" THEN RETURN 
22270 IF POS("unix"=TESTNM$)>0 OR POS("xenix"=TESTNM$)>0 THEN RETURN 
22300 REM ^100 - CHECK FOR PARAMETER STRING
22310 LET TEST$=STBL("*GEN_PROTECT",ERR=22380); IF TEST$="" THEN GOTO 22380
22320 FOR WX=1 TO LEN(TEST$) STEP 50
22330 LET CHECKSTR$=FNTRIM$(TEST$(WX,50))
22340 LET X=POS("\"=CHECKSTR$); IF X>0 THEN LET CHECKSTR$(X,1)="/"; GOTO 22340
22350 IF CHECKSTR$="/" THEN IF LEN(TESTNM$)>1 THEN IF (TESTNM$(1,1)="/" AND POS("/"=TESTNM$(2))=0) THEN EXITTO 22390; REM "IF ROOT, MAKE SURE FILE ISNT' IN ROOT
22360 IF CHECKSTR$<>"/" THEN IF POS(CHECKSTR$=TESTNM$)=1 THEN EXITTO 22390
22370 NEXT WX
22380 LET NOT_OKAY=0; RETURN 
22390 LET NOT_OKAY=1; RETURN 

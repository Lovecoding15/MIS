0010 REM "WOR - Work Order Committed Materials Report - Transaction Print"
0020 REM "Program:  WOR.MB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "31Mar97 JJD: Clean up detail header line (5100)"
0032 REM "31Oct98 WGH: Correct line count and hyphenation (5010,5100-5180,5620)
0033 REM "26Feb99: Fix lazer pagination (2020)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0200 REM " --- IOLISTs"
0210 WOE23A: IOLIST A0$,A1$,A[ALL]
0220 WOE01A: IOLIST B0$,B1$
0230 IVM10A: IOLIST H4$
0240 IVM01A1: IOLIST D0$,D1$(1),D2$
0250 IVM01A2: IOLIST X0$,X1$,X2$
0320 IVM02A: IOLIST C0$,C1$,C2$,C[ALL]
0500 REM " --- Init Data"
0520 DIM A[4],C[16],D1$(DESCLEN)
0600 REM " --- Additional File Opens "        
0610 CALL "SYC.GA",7,1,"","",STATUS
0620 IF STATUS THEN GOTO 9900
0700 REM " --- Background"
0720 CALL "SYC.NB","Printing:",12,COLUMN
0800 REM " --- Options"
1000 REM " --- Init Read"
1020 READ (WOW10_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Main Read"
1120 LET K$=KEY(WOW10_DEV,END=4000)
1140 IF K$(1,2)<>N0$ THEN GOTO 4000
1160 READ (WOW10_DEV)
1200 REM " --- Breaks"
1220 IF K$(3,2)<>T0$ THEN GOSUB 6000
1225 FIND (IVM01_DEV,KEY=N0$+K$(5,20),DOM=1120)IOL=IVM01A2
1230 IF P8$<>"" AND X2$(1,3)<>P8$ THEN READ (WOW10_DEV,KEY=K$(1,24)+$FF$,DOM=1120)
1240 IF K$(5,20)<>T1$ THEN GOSUB 6200
1260 IF T1$="" THEN GOTO 1100
1300 PRINT @(40,11),K$(3,2),"-",K$(5,P0),
1400 REM " --- Additional Reads
1440 FIND (WOE01_DEV,KEY=K$(1,2)+"  "+K$(25,7),DOM=1100)IOL=WOE01A
1460 IF P7$<>"" THEN IF POS(" "<>B0$(19,3))>0 AND B0$(19,3)>P6$ THEN GOTO 1100
1480 IF P7$<>"" THEN IF POS(" "<>B0$(22,3))>0 AND B0$(22,3)<P7$ THEN GOTO 1100
1490 DIM A[4]
1500 FIND (WOE23_DEV,KEY=B0$(1,11)+K$(32,3),DOM=1520)IOL=WOE23A
1520 LET A[2]=A[0]-A[1]
2000 REM " --- Print"
2020 IF L+1>L9 THEN GOSUB 5600
2040 PRINT (7)@(100-M2*5-30),B0$(5,7),"  ",B0$(12,2)," ",FNB$(B0$(19,3))," ",FNB$(B0$(22,3)),@(100-M2*5),A[2]:M2$,@(100),
2050 IF B0$(14,1)="I" THEN PRINT (7)@(100),B0$(56),
2060 IF B0$(14,1)<>"I" THEN PRINT (7)@(100),B1$(1,30),
2100 PRINT (7)""
2120 LET L=L+1,T0=T0+A[2]
2900 GOTO 1100
4000 REM " --- End"
4005 LET T9$="U"
4010 GOSUB 6000
4100 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 LET HEADINGS$[2]=H3$,HEADINGS$[3]=H6$
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5040 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)"Item ",@(100-M2*5-27),"WO",@(100-M2*5-21),"WO",@(100-M2*5-17),"Start",@(100-M2*5-8),"Complete",@(100-M2*4-6),"Req'd",@(100-M2*3-4),"On-",@(100-M2*2-5),"Com-",@(100-M2-7),"Avail-",@(100-5),"On"
5110 PRINT (7)"Code",@(P0+3),"Type  UM",@(100-M2*5-29),"Number",@(100-M2*5-22),"Type",@(100-M2*5-16),"Date",@(100-M2*5-6),"Date",@(100-M2*4-4),"Qty",@(100-M2*3-5),"Hand",@(100-M2*2-7),"mitted",@(100-M2-5),"able",@(100-6),"Order",@(101),"Mfg'd Item",'LF'
5180 LET L=L+3
5190 RETURN 
5600 REM " --- Item Heading
5620 IF L+3>L9 THEN GOSUB 5000
5640 PRINT (7)@(0),D0$(3),@(P0+3),D2$(1,3),"  ",D2$(4,2),'LF',@(3),D1$
5660 LET L=L+2
5690 RETURN 
6000 REM " --- First Sub Total
6010 IF T0$="" THEN GOTO 6100
6020 IF T9$="" THEN LET T9$="T"
6030 GOSUB 6200
6040 IF T9$="T" THEN LET T9$="",T1$=""
6100 IF T9$<>"" THEN RETURN 
6120 DIM H4$(30)
6130 LET H4$(6,20)="*** Not On File ***"
6140 LET T0$=K$(3,2),T1$=""
6160 FIND (IVM10_DEV,KEY=N0$+"C"+T0$,DOM=6180)IOL=IVM10A
6180 LET H6$="Warehouse: "+T0$+"  "+H4$(6,20)
6185 LET L=L9+5
6190 RETURN 
6200 REM " --- Item Subtotals
6210 IF T1$="" THEN GOTO 6300
6220 IF L+2*SGN(L1)+2>L9 THEN GOSUB 5600
6240 PRINT (7)@(100-M2*5-15),"Item Total: ",@(100-M2*5),T0:M2$,
6270 PRINT (7)@(100-M2*4),C[0]:M2$,C[2]:M2$,C[0]-C[2]:M2$,C[1]:M2$,'LF'
6280 LET L=L+2
6290 LET L1=0
6300 IF T9$<>"" THEN RETURN 
6320 LET T1$=K$(5,20),T0=0
6330 DIM D0$(22),D1$(DESCLEN),D2$(10)
6340 FIND (IVM01_DEV,KEY=N0$+T1$,DOM=6360)IOL=IVM01A1
6350 LET D1$(1)=FNITEM$(D1$,DESC[1],DESC[2],DESC[3])
6360 FIND (IVM02_DEV,KEY=N0$+T0$+T1$,DOM=6500)IOL=IVM02A
6380 GOSUB 5600
6390 RETURN 
6500 REM " --- No Warehouse
6520 READ (WOW10_DEV,KEY=K$(1,24)+$FF$,DOM=6540)
6540 LET T1$=""
6590 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

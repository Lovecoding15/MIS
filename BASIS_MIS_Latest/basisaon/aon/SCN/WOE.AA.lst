0010 REM "WOE - Work Order Entry Initial Overlay"
0020 REM "Program WOE.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "12Jul96 JJD: Allow item synonym entry (102,126,640,695).  See related SCNs in WOE.AB & WOE.AE dated 12Jul96.
0032 REM "20Jan99 KJS: Typo, POE01_DEC (670)
0033 REM "11Mar99 WGH: Possible Error 41 on Line 2580 after hardcopy print (530,2520)
0040 REM 
0050 SETERR 9000
0060 SETESC 9000
0065 ON O9 GOTO 0070,1000,2500
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files
0102 LET FILES=54
0104 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0106 LET FILES$[1]="WOE-01",FILES$[2]="WOS-10",FILES$[3]="WOM-07"
0108 LET FILES$[4]="WOE-03",FILES$[5]="IVM-01",FILES$[6]="IVM-03"
0110 LET FILES$[7]="WOE-04",FILES$[8]="SYS-01",FILES$[9]="IVM-10"
0112 LET FILES$[10]="IVM-02",FILES$[11]="WOM-10",FILES$[12]="WOE-02"
0114 LET FILES$[13]="WOE-22",FILES$[14]="WOE-32",FILES$[15]="WOE-07"
0116 LET FILES$[16]="IVM-07",FILES$[17]="WOE-15",FILES$[18]="WOE-25"
0118 LET FILES$[19]="WOE-13",FILES$[20]="WOE-01",FILES$[21]="WOE-06"
0120 LET FILES$[22]="WOM-05",FILES$[23]="WOM-06",FILES$[24]="WOE-23"
0122 LET FILES$[25]="WOT-01",FILES$[26]="WOT-21",FILES$[27]="WOT-31"
0124 LET FILES$[28]="WOT-03",FILES$[29]="WOT-23",FILES$[30]="WOT-33"
0126 LET FILES$[31]="WOM-01",FILES$[32]="WOM-02",FILES$[54]="IVM-14"
0150 CALL "SYC.DA",1,1,32,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE01_DEV=CHANNELS[1],WOS10_DEV=CHANNELS[2],WOM07_DEV=CHANNELS[3]
0162 LET WOE03_DEV=CHANNELS[4],IVM01_DEV=CHANNELS[5],IVM03_DEV=CHANNELS[6]
0164 LET WOE04_DEV=CHANNELS[7],SYS01_DEV=CHANNELS[8],IVM10_DEV=CHANNELS[9]
0166 LET IVM02_DEV=CHANNELS[10],WOM10_DEV=CHANNELS[11],WOE02_DEV=CHANNELS[12]
0168 LET WOE22_DEV=CHANNELS[13],WOE32_DEV=CHANNELS[14],WOE07_DEV=CHANNELS[15]
0170 LET IVM07_DEV=CHANNELS[16],WOE15_DEV=CHANNELS[17],WOE25_DEV=CHANNELS[18]
0172 LET WOE13_DEV=CHANNELS[19],WOE01_DEV2=CHANNELS[20],WOE06_DEV=CHANNELS[21]
0174 LET WOM05_DEV=CHANNELS[22],WOM06_DEV=CHANNELS[23],WOE23_DEV=CHANNELS[24]
0176 LET WOT01_DEV=CHANNELS[25],WOT21_DEV=CHANNELS[26],WOT31_DEV=CHANNELS[27]
0178 LET WOT03_DEV=CHANNELS[28],WOT23_DEV=CHANNELS[29],WOT33_DEV=CHANNELS[30]
0180 LET WOM01_DEV=CHANNELS[31],WOM02_DEV=CHANNELS[32]
0200 REM " --- IOLists
0210 WOE01A: IOLIST A0$(1),A1$(1),A[ALL]
0215 GLS01A: IOLIST *,*,G2$,G3$
0220 IVM01A: IOLIST D0$,D1$(1)
0225 BMS01A: IOLIST *,*,M4$,M5$,M4,M5
0230 WOS10A: IOLIST WN0$,WONUM
0235 ARS01A: IOLIST *,*,X$,*,*,P8$
0240 WOM10A: IOLIST X1$
0260 WOS01A: IOLIST X$,P1$,P2$,P9$,P4$,M0$,M1$,M2$,M3$
0265 APS01A: IOLIST *,VEND_IN_MASK$,X$,*,*,P5$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0275 PRS01A: IOLIST *,*,X$,*,*,P6$
0280 IVS01A: IOLIST X$,X$,P2$,P3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0435 LET P6=NUM(P2$(1,2)),P6$=M0$,BM$=P9$(1,1)
0436 LET AR$=P9$(2,1),PO$=P9$(3,1),PR$=P9$(4,1)
0437 FIND (SYS01_DEV,KEY=N0$+"GL"+"00",DOM=9800)IOL=GLS01A
0440 IF BM$<>"Y" THEN GOTO 0450
0445 FIND (SYS01_DEV,KEY=N0$+"BM00",DOM=0450)IOL=BMS01A
0450 IF AR$<>"Y" THEN GOTO 0460
0455 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=0460)IOL=ARS01A
0457 LET P8=NUM(X$(1,2))
0460 IF PO$<>"Y" THEN GOTO 0470
0465 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=0470)IOL=APS01A
0467 LET P7=NUM(X$(1,2))
0470 IF PR$<>"Y" THEN GOTO 0480
0475 FIND (SYS01_DEV,KEY=N0$+"PR00",DOM=0480)IOL=PRS01A
0477 LET P6=NUM(X$(1,2))
0480 IF PR$<>"Y" AND P9$(12,1)="P" THEN LET M5=NUM(P9$(19,6))
0485 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0487 DIM P[1],DESC[3],INFO2$[20]
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(5,1)),P1=P[1]
0495 LET IVS01_PRECISION=P1,M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$)
0497 CALL "SYC.RM",M3$,M3,8,10
0500 REM " --- Initialize Data "
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40),A0$(75),A1$(141),A[11],D[8],B[16]
0520 DIM SYSINFO$[20]
0525 GOSUB VERIFY_INSTALLED_MODULES
0530 LET K0=1,K9=11,NUMBER=1,A0$(1)=N0$
0550 FOR X=1 TO 3
0555 LET DESC[X]=NUM(P2$((X-1)*2+9,2)),DESCLEN=DESCLEN+DESC[X]
0560 NEXT X
0565 LET P0=NUM(P4$(1,2)),P1=FNYY_YEAR(P4$(3,2)),P0=P0-1
0570 IF P0=0 THEN LET P0=NUM(G2$(1,2)),P1=P1-1
0575 CALL "SYC.PA",SYS01_DEV,P0,P1,V0$,G9$,STATUS
0580 LET P0=P[0],P1=P[1],ITEM_SOURCE$="Bill Number:"
0590 IF BM$<>"Y" THEN LET ITEM_SOURCE$="Item Number:"
0600 REM " --- Open Additional Files
0605 IF BM$<>"Y" THEN GOTO 0620
0610 LET FILES$[33]="BMM-08",FILES$[34]="BMM-01",FILES$[35]="BMM-09"
0615 LET FILES$[36]="BMM-02",FILES$[37]="BMM-03",FILES$[38]="BMM-05"
0620 IF PR$="Y" THEN LET FILES$[39]="PRM-01"
0625 IF MP$="Y" THEN LET FILES$[50]="MPM-01"
0630 IF POS(P3$(17,1)="LS")=0 THEN GOTO 0640
0635 LET FILES$[51]="IVM-17",FILES$[52]="WOT-11",FILES$[53]="WOT-12"
0640 CALL "SYC.DA",1,33,54,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0645 IF STATUS THEN GOTO 9900
0650 LET BMM08_DEV=CHANNELS[33],BMM01_DEV=CHANNELS[34],BMM09_DEV=CHANNELS[35]
0655 LET BMM02_DEV=CHANNELS[36],BMM03_DEV=CHANNELS[37],BMM05_DEV=CHANNELS[38]
0660 LET PRM01_DEV=CHANNELS[39],ARM10_DEV=CHANNELS[40],ARE03_DEV=CHANNELS[41]
0665 LET ARM01_DEV=CHANNELS[42],ARE13_DEV=CHANNELS[43],ARE23_DEV=CHANNELS[44]
0670 LET APM01_DEV=CHANNELS[45],POE02_DEV=CHANNELS[46],POE01_DEV=CHANNELS[47]
0675 LET POE11_DEV=CHANNELS[48],POE12_DEV=CHANNELS[49],MPM01_DEV=CHANNELS[50]
0680 LET IVM17_DEV=CHANNELS[51],WOT11_DEV=CHANNELS[52],WOT12_DEV=CHANNELS[53]
0685 IF BM$="Y" THEN LET OPCODE_DEV=BMM08_DEV ELSE LET OPCODE_DEV=WOM02_DEV
0690 IF PR$="Y" THEN LET EMPCODE_DEV=PRM01_DEV ELSE LET EMPCODE_DEV=WOM01_DEV
0695 LET IVM14_DEV=CHANNELS[54]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,10,0
0720 PRINT 'SB',@(0,3),"WO Number:",@(20,3),"Type:",@(57,3),"Status:",@(0,5),ITEM_SOURCE$,@(0,6),"Description:",@(55,4),"Category:",@(54,5),"Open Date:",@(55,6),"Revision:",@(68),"U/M:",@(52,7),"Closed Date:",@(0,8),"Drawing #:",@(37),"Rev:",@(51),"Closed Value:",@(0,9),"Sched Production:",@(33),"Yield %:",@(53,9),"Closed Qty:",'SF',
0900 REM " --- Init File"
0990 READ (WOE01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Key"
1010 PRINT 'SF','CF'
1020 LET V2$="",V2$=KEY(WOE01_DEV,END=1050)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(5)
1050 LET V0$="Z",V1$="CER",V3$="",V0=7,V1=11,V2=3,V4$="Enter A Valid Work Order Number (<F1>=Next Work Order Number/<F3>=Lookup)"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 9900
1080 IF V3=3 THEN GOSUB 6300
1090 IF V3=1 THEN GOSUB 6100
1100 IF V$="" THEN GOTO 1050
1110 IF V$="" OR POS("0"<>V$)=0 THEN GOTO 0900
1120 LET A0$(5)=V$
1130 DIM A[11]
1200 REM " --- Find Key"
1210 FIND (WOE01_DEV,KEY=A0$(1,K9),DOM=1400)IOL=WOE01A
1220 LET S9$=A0$(12,3)+A0$(34,16)+A0$(54),S9=A[0],O0=1
1230 IF A[6]=0 THEN LET A[6]=100; REM "Default yield to 100%
1300 REM " --- Display"
1310 GOSUB 5000
1390 GOTO 2500
1400 REM " --- New Record"
1410 IF V3=3 THEN GOTO 1500
1415 LET NEW_WONUM=NUM(V$)
1420 LET V4$="Is This A New Record"
1430 CALL "SYC.YN",1,V4$,1,V$,V3
1440 IF V$="Y" THEN GOTO 1500
1450 IF V$<>"N" THEN GOTO 1400
1455 FIND (WOS10_DEV,KEY=N0$+"N",DOM=1495)IOL=WOS10A
1460 IF WONUM<>NEW_WONUM+1 THEN GOTO 1495
1465 LET WONUM=WONUM-1
1470 WRITE (WOS10_DEV,KEY=N0$+"N")IOL=WOS10A
1480 LET NEW_WONUM=0
1495 GOTO 1000
1500 REM " --- Set Up
1510 DIM S9$(21+P[0])
1520 LET A0$(12)="",A0$(16,3)=F0$(7,3),A0$(22,3)="",A0$(15,1)="P",A[0]=1,A0=0
1530 LET A0$(54,2)=P3$(6,2),A0$(19,3)=F0$(7,3),A1$(1)="",A1$(97,1)="5",O0=0
1550 PRINT @(65,3),A0$(15,1),@(67,3),"*Planned*",@(65,5),FNB$(A0$(16,3)),@(18,9),A[0]:M2$
1600 REM " --- Run header overlay"
1610 LET O9=0
1690 RUN "WOE.AB"
2500 REM " --- Menu
2510 DIM MENUS$[3]
2520 LET MENUS=1,MENUS$[MENUS]="WOE.AA",WIN_X=0,WIN_Y=22,O0=1,O9=2,L0=11
2530 IF PO$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="WOE.AF"
2540 IF I9$<>"I" OR A1$(96,1)<>"Y" OR POS(P3$(17,1)="SL")=0 THEN GOTO 2560
2550 LET MENUS=MENUS+1,MENUS$[MENUS]="WOE.AJ"
2560 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
2570 CALL "SYC.WC",1,0,80,0,0,10,0
2580 PRINT @(0,L0),'CE',
2590 IF SELECTION$="" THEN GOTO 1000
2600 IF SELECTION$="SYC.KA" THEN GOTO 2630
2610 IF SELECTION$="WOE.AH" THEN LET O9=0
2620 RUN SELECTION$,ERR=2500
2630 REM " --- Comments"
2640 DIM INFO$[5],INFO[5]
2650 LET X$="Work Order "+A0$(5,7)
2660 LET INFO$[0]="WOE-07",INFO$[1]=A0$(1,K9),INFO$[2]=X$,ACTION=0
2670 LET INFO[1]=L0,INFO[3]=3,INFO[4]=60
2680 CALL "SYC.KA",ACTION,INFO$[ALL],INFO[ALL]
2690 GOTO 2500
5000 REM " --- Display"
5010 DIM D1$(60),DESC$(60)
5020 FIND (IVM01_DEV,KEY=A0$(1,2)+A0$(56),DOM=5030)IOL=IVM01A
5030 DIM X1$(30); LET X1$(6,20)="** Not On File **",I9$="N"
5040 LET DESC$(1)=FNITEM$(D1$(1),DESC[1],DESC[2],DESC[3])
5050 FIND (WOM10_DEV,KEY=N0$+"A"+A0$(12,2),DOM=5060)IOL=WOM10A
5055 LET I9$=A0$(14,1)
5060 PRINT @(11,3),A0$(5,7),@(27,3),A0$(12,2),"  ",X1$(6,20),@(65),A0$(15,1),@(65,4),A0$(14,1),@(65,5),FNB$(A0$(16,3)),@(65,6),A0$(52,2),@(73,6),A0$(50,2)
5065 IF A0$(15,1)="O" THEN PRINT @(67,3),"**Open**",
5070 IF A0$(15,1)="P" THEN PRINT @(67,3),"*Planned*",
5075 IF A0$(15,1)="Q" THEN PRINT @(67,3),"*Quoted*",
5080 IF POS(A0$(15,1)="OPQ")=0 THEN PRINT @(67,3),"**Closed**",
5090 IF I9$="I" THEN PRINT @(67,4),"Inventory",@(13,5),A0$(56),
5095 IF I9$="N" THEN PRINT @(67,4),"Non-Stocking",
5100 IF I9$="R" THEN PRINT @(67,4),"Recurring",
5105 IF A0$(15,1)="C" OR POS(" "<>A0$(31,3))>0 THEN PRINT @(65,7),FNB$(A0$(31,3))
5110 IF I9$<>"I" THEN PRINT @(13,6),A1$(1,30),@(13,7),A1$(31,30),
5120 IF I9$="I" THEN IF DESC[3]>0 THEN PRINT @(13,6),DESC$(1,30),@(13,7),DESC$(31,30) ELSE PRINT @(13,6),D1$(1,MIN(DESC[1],42)),@(13,7),D1$(DESC[1]+1,MIN(DESC[2],37))
5130 PRINT @(11,8),A1$(61,25),@(42),A1$(86,5),@(65),A[3]:M1$,@(18,9),A[0]:M2$,@(42),A[6]:"##0.0",@(65),A[2]:M2$,
5190 RETURN 
6100 REM " --- Next W/O #
6110 EXTRACT (WOS10_DEV,KEY=N0$+"N",DOM=6130)IOL=WOS10A
6120 GOTO 6140
6130 LET WONUM=1000
6140 IF WONUM>9999999 THEN LET WONUM=1000
6150 LET V$=STR(WONUM:"0000000"),WN0$=N0$+"N"
6160 FIND (WOE01_DEV,KEY=N0$+"  "+V$,DOM=6190)
6170 LET WONUM=WONUM+1
6180 GOTO 6150
6190 LET WONUM=WONUM+1
6200 WRITE (WOS10_DEV,KEY=WN0$)IOL=WOS10A
6210 PRINT @(V1,V2),V$
6220 RETURN 
6300 REM " --- W/O Lookup
6310 IF V3=4 THEN GOTO 6350
6320 LET V3$=P3$(6,2),V$=P9$(2,1),V7$=P7$,V6$="?",V8=P[0]
6330 LET V4=1,V5=2,V6=12,V7=21
6340 CALL "SYC.LW",V$,V6$
6350 RETURN 
6500 REM " --- Verify Installed Modules "
6501 VERIFY_INSTALLED_MODULES: 
6505 IF BM$="N" THEN GOTO 6520
6510 CALL "SYC.VA","BM",SYSINFO$[ALL]
6515 LET BM$=SYSINFO$[20]
6520 IF AR$="N" THEN GOTO 6535
6525 CALL "SYC.VA","AR",SYSINFO$[ALL]
6530 LET AR$=SYSINFO$[20]
6535 IF PO$="N" THEN GOTO 6550
6540 CALL "SYC.VA","PO",SYSINFO$[ALL]
6545 LET PO$=SYSINFO$[20]
6550 IF PR$="N" THEN GOTO 6565
6555 CALL "SYC.VA","PR",SYSINFO$[ALL]
6560 LET PR$=SYSINFO$[20]
6565 CALL "SYC.VA","MP",SYSINFO$[ALL]
6570 LET MP$=SYSINFO$[20]
6595 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "SHU - Manifest Update Program"
0020 REM "Program SHU.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "28May96 JAL: Eliminate x[ALL]=0 construct for P/5 compatibility (2530,2535,3050)"
0032 REM "09Apr97 JAL: Headers without detail may not be removed (3260)"
0033 REM "03Nov97 JWK: Leaving orphan SHE-04 records (1410,2020)
0034 REM "11Mar98 WGH: Removing SHE-01 header before all SHE-02 detail is gone (2730)"
0035 REM "18Mar99 KJS: Avoid error 47, J1$ not defined (1930)
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files
0110 CLOSE (7,ERR=0120)
0200 REM " --- IOLIST's"
0210 SHE01A: IOLIST A0$,A1$,A2$,A[ALL]
0220 SHE02A: IOLIST W0$,W1$,W[ALL]
0240 SHT01A: IOLIST S0$,S1$,S2$,S[ALL]
0250 SHT02A: IOLIST T0$,T1$,T[ALL]
0290 SHT05A: IOLIST X0$(1),X1$(1),X[0]
0300 SHM10N: IOLIST C0$(1),C1$(1)
0310 SHM10T: IOLIST H0$(1),H[0],H[1]
0320 SHM10A: IOLIST *,I7$
0330 SHT04A: IOLIST XA$
0340 SHT04B: IOLIST XB$
0350 SHT04C: IOLIST XC$
0360 SHT04D: IOLIST XD$
0500 REM " --- Initializations"
0510 DIM A[8],W[20],S[8],T[20],X0$(10),X1$(44),X[1]
0520 DIM C0$(6),C1$(48),H0$(6),H[1]
0700 REM " --- Background"
0710 PRINT @(0,10),'CE',
0800 REM " --- Options
0810 LET V4$="Do You Wish To Update The Manifest Information"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM " --- Additional background"
0910 CALL "SYC.NB","Updating",8,COLUMN
0930 REM " --- Disallow 'M'enu option in Error Routine
0940 LET EXIT_CTRL=1
0950 REM " --- Position files"
0960 LET BEGKEY$=N0$+"A"+C$+WHSE$
0970 READ (SHE01_DEV,KEY=N0$,DOM=0980)
0980 READ (SHE02_DEV,KEY=N0$,DOM=0990)
0990 READ (SHE04_DEV,KEY=BEGKEY$,DOM=1000)
1000 REM " --- Process next header"
1010 LET K$=KEY(SHE04_DEV,END=3000)
1020 IF POS(BEGKEY$=K$)<>1 THEN GOTO 3000
1030 READ (SHE04_DEV)
1040 FIND (SHE01_DEV,KEY=K$(1,2)+K$(17,15)+"000",DOM=2900)IOL=SHE01A
1400 REM " --- Position detail file"
1410 LET FREIGHT=0,OTHER=0,PACKAGES=0
1490 READ (SHE02_DEV,KEY=A0$,DOM=1500)
1500 REM " --- Process next detail"
1510 LET K$=KEY(SHE02_DEV,END=2500)
1520 IF POS(A0$(1,17)=K$)<>1 THEN GOTO 2500
1530 READ (SHE02_DEV)IOL=SHE02A
1540 FIND (SHM10_DEV,KEY=W0$(1,2)+"A"+W1$(4,7),DOM=2000)IOL=SHM10A
1550 REM " --- Milestone count"
1560 LET X$=W1$(12,4)
1570 IF I7$(26,1)<>"Y" THEN LET X$=W0$(3,7)
1580 PRINT @(COLUMN,11),X$,
1600 REM " --- Write SHT-02 Package Detail History Record"
1610 IF W1$(11,1)<>C$ THEN GOTO 2400
1620 IF W1$(28,1)="Y" THEN GOTO 2000
1630 IF A2$(23,1)="Y" THEN GOTO 2000
1640 LET T0$=W0$+A2$(1,6),T1$=W1$,T[ALL]=W[ALL]
1650 WRITE (SHT02_DEV,KEY=T0$)IOL=SHT02A
1700 REM " --- Write SHT-04 Transaction Xref Records"
1710 LET XC$=N0$+"C"+A2$(1,6)+W1$(11,1)+W1$(62,12)+W1$(12,4)+A1$(1,6)+A0$(3,15)+W0$(18,3)
1720 LET XD$=N0$+"D"+A2$(1,6)+W1$(11,1)+W1$(59,2)+A0$(3,15)+W0$(18,3)
1730 WRITE (SHT04_DEV,KEY=XC$)IOL=SHT04C
1740 WRITE (SHT04_DEV,KEY=XD$)IOL=SHT04D
1800 REM " --- Accumulate Shipment Totals"
1810 LET PACKAGES=PACKAGES+1,FREIGHT=FREIGHT+W[4]
1820 LET OTHER=OTHER+W[3]+W[5]+W[6]+W[7]+W[8]+W[9]+W[11]+W[12]+W[13]+W[15]+W[16]
1900 REM " --- Write SHT-05 COD History Record"
1910 IF FNP$(W1$(20,7))="" THEN GOTO 2000
1920 LET X0$(1)=W0$(1,2)+"A"+W1$(20,7),X[0]=W[14]
1930 LET X1$(1)=A2$(1,6)+W0$(3,18)+"O"+FILL(6)+A1$(1,6)+W1$(4,7)
1940 WRITE (SHT05_DEV,KEY=X0$)IOL=SHT05A
2000 REM " --- Remove SHE-02 Package Detail & SHE-04 Xref"
2010 REMOVE (SHE02_DEV,KEY=W0$,DOM=2020)
2020 REMOVE (SHE04_DEV,KEY=N0$+"A"+W1$(11,1)+W1$(59,2)+A2$(1,6)+W1$(12,4)+W0$(3,18),DOM=2400)
2400 REM " --- Loop back for next detail"
2490 GOTO 1500
2500 REM " --- Write SHT-01 Shipment History Header"
2510 IF PACKAGES=0 THEN GOTO 2700
2520 IF A2$(23,1)="Y" THEN GOTO 2700
2530 LET S0$=A0$+A2$(1,6),S1$=A1$,S2$=A2$
2535 FOR X=0 TO 8; LET S[X]=0; NEXT X
2540 FIND (SHT01_DEV,KEY=S0$,DOM=2550)IOL=SHT01A
2550 LET S[0]=S[0]+FREIGHT,S[1]=S[1]+OTHER,S[2]=S[2]+PACKAGES
2560 WRITE (SHT01_DEV,KEY=S0$)IOL=SHT01A
2600 REM " --- Write SHT-04 Shipment History Xref"
2610 LET XA$=N0$+"A"+A1$(1,6)+A0$(3,18)+A2$(1,6)
2620 LET XB$=N0$+"B"+A2$(1,6)+A1$(1,6)+A0$(3,18)
2630 WRITE (SHT04_DEV,KEY=XA$)IOL=SHT04A
2640 WRITE (SHT04_DEV,KEY=XB$)IOL=SHT04B
2700 REM " --- Remove SHE-01 header if all SHE-02 detail is gone"
2710 READ (SHE02_DEV,KEY=A0$,DOM=2720)
2720 LET K$=KEY(SHE02_DEV,END=2790)
2730 IF POS(A0$(1,17)=K$)=1 THEN GOTO 2900
2790 REMOVE (SHE01_DEV,KEY=A0$,DOM=2900)
2900 REM " --- Loop back for next SHE-01 header record"
2990 GOTO 1000
3000 REM " --- Manifest Completed. Update Carrier & Transaction Number"
3010 LET C0$(1)=N0$+"N"+C$+WHSE$
3020 FIND (SHM10_DEV,KEY=C0$,DOM=3030)IOL=SHM10N
3030 LET C1$(11,12)=""
3040 WRITE (SHM10_DEV,KEY=C0$)IOL=SHM10N
3050 LET H0$(1)=N0$+"T"+C$+WHSE$,H[0]=0,H[1]=0
3060 WRITE (SHM10_DEV,KEY=H0$)IOL=SHM10T
3100 REM " --- Scan SHE-01/SHE-02 for unlinked records and remove"
3110 PRINT 'SB',@(27,9)," Examining Manifest File ",'SF',
3120 CALL "SYC.NB","Scanning",11,COLUMN
3190 READ (SHE01_DEV,KEY=N0$,DOM=3200)
3200 REM " --- Remove unlinked headers from SHE-01"
3210 LET K$=KEY(SHE01_DEV,END=3300)
3220 IF POS(N0$=K$)<>1 THEN GOTO 3300
3230 READ (SHE01_DEV)
3240 PRINT @(COLUMN,11),K$(3,7),
3250 READ (SHE02_DEV,KEY=K$,DOM=3260)
3260 LET X$=KEY(SHE02_DEV,END=3280)
3270 IF POS(K$(1,17)=X$)=1 THEN GOTO 3200
3280 REMOVE (SHE01_DEV,KEY=K$,DOM=3200)
3290 GOTO 3200
3300 REM " --- Position detail file"
3390 READ (SHE02_DEV,KEY=N0$,DOM=3400)
3400 REM " --- Remove unlinked details from SHE-02"
3410 LET K$=KEY(SHE02_DEV,END=4000)
3420 IF POS(N0$=K$)<>1 THEN GOTO 4000
3430 READ (SHE02_DEV)
3440 PRINT @(COLUMN,11),K$(3,7)," ",K$(18,3),
3450 FIND (SHE01_DEV,KEY=K$(1,17)+"000",DOM=3480)
3460 GOTO 4300
3480 REMOVE (SHE02_DEV,KEY=K$,DOM=3400)
3490 GOTO 3400
4900 REM " --- All done"
4990 GOTO 9900
8000 REM " --- Function Definitions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

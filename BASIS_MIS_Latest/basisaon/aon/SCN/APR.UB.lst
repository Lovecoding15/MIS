0010 REM "APR ACCOUNTS PAYABLE - CHECK PRINTING/BLANK STUB 2"
0020 REM "Program: <APR.UB>"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "23Sep99 WGH: Prevent possible Error 41 on Line 5810 (5810)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0200 REM " --- IOLists"
0210 APE04A1: IOLIST A0$,A[ALL]
0220 APM01A: IOLIST B0$,B1$(1)
0230 APM03A: IOLIST B0$,B1$(1)
0240 APW01A: IOLIST D0$
0250 APT02A: IOLIST E0$,E[ALL]
0260 APE04A2: IOLIST W0$(1),W[ALL]
0270 APM10A: IOLIST X$
0500 REM " --- Init Data"
0700 REM " --- Background"
0800 REM " --- Test Pattern"
0805 CALL "SYC.YN",0,"Do You Wish To Print Test Pattern",4,V$,V3
0820 IF V3=4 THEN GOTO 9900
0840 ON POS(V$="YN") GOTO 800,850,1000
0850 DIM B1$(111,"X"),A0$(60,"X"),T[2]
0860 LET A[0]=9999.99,A[1]=A[0],T[0]=A[0],T[1]=A[1],A0$(3,2)="",A0$(21,3)=P9$
0900 GOSUB 5000
0910 FOR X=1 TO 3
0920 GOSUB 5200
0930 NEXT X
0950 GOSUB 5800
0990 GOTO 0800
1000 REM " --- Init Read"
1010 IF P7$="Y" THEN READ (APE04_DEV1,KEY=N0$+P6$+P5$,DOM=1100)
1020 READ (APE04_DEV1,KEY=N0$,DOM=1030)
1100 REM " --- Main Read"
1120 LET K$=KEY(APE04_DEV1,END=4000)
1130 IF K$(1,2)<>N0$ THEN GOTO 4000
1140 READ (APE04_DEV1)IOL=APE04A1
1200 REM " --- Breaks"
1220 IF K$(3,2)<>T0$ THEN GOSUB 6000
1240 IF K$(3,8)<>T1$ THEN GOSUB 6200
2540 GOSUB 5200
2600 REM " --- Accumulate"
2620 FOR X=0 TO 2
2630 LET T[X]=T[X]+A[X]
2640 NEXT X
2900 GOTO 1100
4000 REM " --- Totals"
4020 LET T9$="E"
4040 GOSUB 6200
4050 PRINT (7)'LF',
4100 GOTO 9900
5000 REM " --- Check Stub Header"
5010 LET L=0
5020 PRINT (7)@(H0),"Vendor: ",X1$," ",B1$(1,30),@(H0+50),"CHECK NO: ",P8$,'LF'
5030 PRINT (7)@(H0+3),"Date",@(H0+11),"Invoice #",@(H0+14+M1),"Invoice",@(H0+25+M1),"Discount",@(H0+40+M1),"Payment",@(H0+49+M1),"Memo/Distribution",'LF'
5040 LET H1=0
5050 IF P3$(7,1)="N" THEN GOTO 5100
5070 LET H1=17
5190 RETURN
5200 REM " --- Invoice Detail"
5210 IF L>L9 THEN GOSUB 5700
5220 PRINT (7)@(H0+1),FNB$(A0$(21,3)),"  ",A0$(11,10),@(H0+23),A[0]:M1$,@(H0+35),A[1]:M1$,@(H0+49),A[0]-A[2]-A[1]:M1$,
5230 IF P3$(7,1)="Y" THEN PRINT (7)@(H0+62),A0$(40,16),
5240 LET L1=0
5250 IF P3$(8,1)="N" THEN GOTO 5380
5252 IF P3$(7,1)="Y" THEN PRINT (7)'LF',
5254 IF P3$(7,1)="Y" THEN LET L=L+1,L1=L1+1
5260 READ (APT02_DEV,KEY=A0$(1,10)+"I"+A0$(11,10),DOM=5270)
5270 LET K1$=KEY(APT02_DEV,END=5380)
5280 IF K1$(1,21)<>A0$(1,10)+"I"+A0$(11,10) THEN GOTO 5380
5290 READ (APT02_DEV)IOL=APT02A
5300 IF L>L9 THEN GOSUB 5700
5302 LET X0=0
5304 IF A[3]<>0 THEN LET X0=E[0]*(A[0]+A[2])/A[3]
5310 PRINT (7)@(H0+62),FNF$(E0$(22,G[3]),G5$),X0:M1$
5320 LET L1=L1+1,L=L+1
5330 GOTO 5270
5380 IF L1=0 THEN PRINT (7)'LF',
5385 IF L1=0 THEN LET L=L+1
5390 RETURN
5400 REM " --- Stub Total"
5420 PRINT (7)H9$(1,L9-L)
5430 PRINT (7)@(H0),"Totals:",@(H0+23),T[0]:M1$,@(H0+35),T[1]:M1$,@(H0+49),T[0]-T[2]-T[1]:M1$
5490 RETURN
5600 REM " --- Print Check"
5610 PRINT (7)H9$(1,3)
5620 PRINT (7)@(H0+67),P8$,'LF','LF','LF'
5640 LET X$=STR(FPT(T[0]-T[2]-T[1])*100:"00")
5650 IF X$="00" THEN LET X$="NO"
5660 PRINT (7)H9$(1,1),@(H0+6),"Exactly*",INT(T[0]-T[2]-T[1]):M8$,"*Dollars*And*",X$,"*Cents",'LF'
5665 PRINT (7)@(H0+51),FNB$(P9$)
5670 PRINT (7)@(H0+68),T[0]-T[2]-T[1]:M9$
5675 PRINT (7)H9$(1,1)
5680 PRINT (7)@(H0+7),B1$(1,30),'LF',@(H0+7),B1$(31,24),'LF',@(H0+7),B1$(55,24),'LF',@(H0+7),B1$(79,24)," ",B9$,H9$(1,7)
5690 RETURN
5700 REM " --- Overflow"
5720 GOSUB 5800
5740 GOSUB 5000
5790 RETURN
5800 REM " --- Void Check"
5810 IF L9>L THEN PRINT (7)H9$(1,L9-L)
5822 PRINT (7)@(H0+49),"Continued"
5830 PRINT (7)H9$(1,4)
5840 PRINT (7)@(H0+67),P8$,'LF','LF'
5860 PRINT (7)H9$(1,1),@(H0+6),"***Void***Void***Void***Void***",@(H0+51),FNB$(P9$),@(H0+68),"**Void**"
5880 PRINT (7)H9$(1,13)
5900 REM " --- Write Void"
5920 LET D0$=N0$+A0$(3,2)+P8$+"V"+P9$+A0$(5,16),D$=D0$(1,11)
5930 IF P3$(1,1)<>"Y" THEN LET D$(3,2)=""
5940 WRITE (APW01_DEV,KEY=D$,END=9200)IOL=APW01A
5960 GOSUB 6800
5990 RETURN
6000 REM " --- AP Type Break"
6010 LET T9$="A"
6020 GOSUB 6200
6030 LET T0$=A0$(3,2),T1$="",X$="",T9$=""
6040 IF P3$(1,1)<>"Y" THEN RETURN
6100 REM " --- New Checks"
6130 FIND (APM10_DEV,KEY=N0$+"A"+T0$,DOM=6150)IOL=APM10A
6140 LET X$=X$(6,20)+" "
6150 LET MESSAGE$[0]="Please Mount The "+X$+"Checks <Enter>: "
6155 CALL "SYC.XA",0,MESSAGE$[ALL],0,-1,-1,V$,V3
6190 RETURN
6200 REM " --- New Vendor"
6210 IF T1$="" THEN GOTO 6500
6220 IF T[0]-T[2]-T[1]<=0 THEN GOTO 6490
6230 GOSUB 5400
6240 GOSUB 5600
6250 DIM W0$(65)
6260 LET W0$(1)=T2$,W$=W0$(1,20)
6270 IF P3$(1,1)<>"Y" THEN LET W$(3,2)=""
6280 FIND (APE04_DEV2,KEY=W$,DOM=6490)IOL=APE04A2
6285 IF POS(" "<>W0$(56,7))=0 THEN GOTO 6400
6290 LET D$=W$(1,4)+W0$(56,7)
6300 FIND (APW01_DEV,KEY=D$,DOM=6400)IOL=APW01A
6320 IF D0$(1,4)+D0$(16,16)<>W$ THEN GOTO 6400
6330 LET D0$=D0$(1,11)+"V"+P9$+T2$(5,16)
6340 WRITE (APW01_DEV,KEY=D$)IOL=APW01A
6400 LET D0$=T2$(1,4)+P8$+"C"+P9$+T2$(5,16),D$=D0$(1,11)
6410 IF P3$(1,1)<>"Y" THEN LET D$(3,2)=""
6420 WRITE (APW01_DEV,KEY=D$,END=9200)IOL=APW01A
6440 LET W0$(56,7)=P8$
6450 WRITE (APE04_DEV2,KEY=W$)IOL=APE04A2
6470 GOSUB 6800
6480 GOTO 6500
6490 GOSUB 5400
6495 GOSUB 5830
6500 IF T9$<>"" THEN RETURN
6510 DIM X1$(M0),B1$(121),T[2]
6520 LET T2$=A0$
6530 IF A0$(5,4)="TEMP" THEN GOTO 6600
6550 FIND (APM01_DEV,KEY=N0$+A0$(5,6),DOM=6570)IOL=APM01A
6560 LET B9$=FNZ$(B1$(103))
6570 IF POS(" "<>B1$(79,24))=0 THEN LET B1$(79,24)=B9$,B9$=""
6580 LET X1$(1)=FNF$(A0$(5,P[0]),M0$),T1$=K$(3,8)
6590 GOTO 6700
6600 REM " --- Temp Name"
6610 LET X1$(1)="",T1$="TEMP",B9$=""
6620 FIND (APM03_DEV,KEY=A0$(1,4)+A0$(11,10),DOM=6640)IOL=APM03A
6700 GOSUB 5000
6790 RETURN
6800 REM " --- Increment Check #"
6820 LET P8=NUM(P8$)+1
6830 IF P8>9999999 THEN LET P8=1
6840 LET P8$=STR(P8:J0$(1,7))
6860 PRINT @(40,5),P8$,
6890 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
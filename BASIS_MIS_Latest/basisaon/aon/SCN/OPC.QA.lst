0010 REM "OPC - Order Processing SpeedSearch (Invoice History Header)"
0020 REM "Program OPC.QA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "24Apr97 WGH: Display invoice number on return when selected from <F3> lookup (1235)"
0040 REM 
0050 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0060 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ARM-01",FILES$[2]="ART-03"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0170 LET ARM01_DEV=CHANNELS[1],ART03_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 ARM01A: IOLIST A0$(1),A1$(1)
0220 ART03A: IOLIST B0$(1),B[ALL]
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,I5$,M1$,M2$,M3$
0400 REM " --- Parameters"
0405 DIM P[2]
0410 LET X$=STBL("!SPEEDSEARCH"),N0$=X$(1,2),MAXPAGE=NUM(X$(66,2))
0415 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0420 LET SNAME$=FNP$(X$(19,8)),SWIDTH=NUM(X$(43,2)),SHEIGHT=NUM(X$(45,2))
0425 LET MAXOPT=NUM(X$(57,2)),SYS01_DEV=NUM(STBL("!SYS01")),ONAME$=FNP$(X$(27,8))
0430 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0440 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0450 LET PAGES=INT((DSZ-5000)/4000),TNAME$=FNP$(X$(3,8))
0460 IF PAGES<3 THEN GOTO 9900
0470 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0480 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9900)IOL=ARS01A
0485 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9900)IOL=IVS01A
0490 LET P[0]=NUM(P2$(1,2))
0500 REM " --- Initializations"
0510 DIM A0$(8),A1$(30),B0$(101),B[10]
0520 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),OPTLIST$[2,MAXOPT,6]
0550 CALL "SYC.RM",M1$,M1,10,14
0560 LET D_STATUS=0,MAXROW=HEIGHT-5,PAGE=1,PGM$=PGM(-2)
0570 LET A0$(1)=N0$+OPTION$[3],M1=LEN(M1$),H0=WIDTH-2,W=H0-M1-13
0580 LET B0$(1)=N0$+"  "+OPTION$[3],B0$(18,3)="000",BEGDATE$=""
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,ONAME$
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0720 LET HEADING$(2)="Invoice  Type  Ord Date  Inv Date  By   PO Number"
0730 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0740 PRINT @(0,0),'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",SNAME$,0
0920 PRINT 'CF',
0930 CALL "SYC.WB",TNAME$,0
0940 PRINT @(0,1),'CL',@(0,2),'CL',@(0,3),'CL',
0990 GOTO 1100
1000 REM " --- Process key field"
1010 GOSUB 6000
1015 CALL "SYC.WB",TNAME$,0
1030 PRINT @(1,0),'CL','SB',"Customer:",'SF',@(0,1),'CL',@(0,2),'CL',
1035 LET V2$="",V2$=KEY(ARM01_DEV,END=1050)
1040 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1045 IF V2$<>"" THEN LET V2$=V2$(3)
1050 LET V0$="M",V1$="KE",V3$=M0$,V0=6,V1=11,V2=0,V4$=""
1055 PRINT @(V0+V1+1,V2),'CL',
1060 GOSUB 7000
1065 ON V3 GOTO 1080,1000,1000,1070,3800,3900,3800
1070 CALL "SYC.LC",V$
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(3)=V$,B0$(3)=V$
1100 REM " --- Retrieve records for display"
1110 DIM X$(WIDTH-2)
1120 FIND (ARM01_DEV,KEY=A0$,DOM=1000)IOL=ARM01A
1130 LET X$(1)='SB'+" Customer: "+'SF'+FNF$(A0$(3,P[0]),M0$)+" "+FNP$(A1$)
1140 LET X1$=A0$(3,6),X2$=PARAM$
1150 CALL "SYC.QB",X$,X1$,X2$
1180 READ (ART03_DEV,KEY=B0$(1,20),DOM=1200)IOL=ART03A
1190 GOTO 1300
1200 REM " --- Invoice Number"
1210 PRINT @(1,1),'CL','SB'," Invoice:",'SF',
1215 LET V0=7,V1=11,V2=1,V0$="Z",V1$="KE",V2$="",V3$="",V4$=""
1220 GOSUB 7000
1225 ON V3 GOTO 1240,1200,1000,1230,3800,3900,3800
1230 GOSUB 6100
1235 PRINT @(V1,V2),V$,
1240 LET B0$(1)=A0$(1,2)+"  "+A0$(3)+V$,B0$(18,3)="000"
1250 REM " --- Starting Date"
1255 PRINT @(0,2),'CL','SB'," Starting Date:",'SF',
1260 LET V0=3,V1=16,V2=2,V0$="D",V1$="E",V2$=BEGDATE$,V3$="",V4$=""
1270 GOSUB 7000
1280 ON V3 GOTO 1290,1250,1200,1250,3900,3800,3900
1290 LET BEGDATE$=V$
1300 REM " --- Retrieve records for display"
1310 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,SHEIGHT-2],X$(WIDTH-2)
1320 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,SELECTION$=""
1330 READ (ART03_DEV,KEY=B0$(1,20),DOM=1400)IOL=ART03A
1340 LET X$(1)='SB'+"  Invoice: "+'SF'+B0$(11,7)
1350 LET X1$=B0$(11,7),X2$=SUBPARAM$
1360 CALL "SYC.QB",X$,X1$,X2$
1390 GOTO 1500
1400 REM " --- Read next invoice"
1410 LET K$=KEY(ART03_DEV,END=1900)
1420 IF POS(N0$=K$)<>1 THEN GOTO 1900
1430 IF K$(5,6)<>A0$(3) THEN GOTO 1900
1440 READ (ART03_DEV)IOL=ART03A
1450 IF BEGDATE$<>"" THEN IF B0$(24,3)<BEGDATE$ THEN GOTO 1800
1500 REM " --- Display page?"
1510 IF L<MAXROW THEN GOTO 1600
1520 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
1530 GOSUB 5000
1540 IF DONE THEN GOTO 4000
1570 IF ANSWER$<>"" THEN GOTO 3000
1590 LET L=1,X1=1
1600 REM " --- Middle window record"
1610 DIM G1$(WIDTH-2)
1620 LET TYPE$="Inv"
1630 IF B0$(21,1)="V" THEN LET TYPE$="Void"
1640 LET G1$(2)=B0$(11,7),G1$(11)=TYPE$,G1$(17)=FNB$(B0$(77,3))
1650 LET G1$(27)=FNB$(B0$(24,3)),G1$(37)=B0$(96,3),G1$(42)=B0$(49,10)
1660 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=B0$(5,13)
1700 REM " --- Bottom window record"
1710 DIM G2$(SWIDTH-2)
1720 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2)
1730 LET G2$(1)=" {Territory}"+B0$(72,3),G2$(17)="{Salesman}"+B0$(59,3)
1740 LET G2$(W+5)="{Order #}"+B0$(42,7),MORE$[PAGE,X1,1]=G2$
1750 LET G2$(1)="{Price Code}"+B0$(82,4)+"{Discount}"+B0$(64,2)
1760 LET G2$(W+4)="{Ship Via}"+B0$(32,10),MORE$[PAGE,X1,2]=G2$
1770 LET G2$(1)="  {Tax Code}"+B0$(80,2)+" {Dist Code}"+B0$(66,2)
1780 LET G2$(W)="{Sales Amount}"+STR(B[6]:M1$),MORE$[PAGE,X1,3]=G2$
1790 LET LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
1800 REM " --- Loop back for next invoice"
1890 GOTO 1400
1900 REM " --- No more invoices"
1910 IF X0>0 THEN GOTO 2000
1920 DIM MESSAGE$[1]
1930 LET MESSAGE$[0]="No Invoices Found (<Enter>=Continue)"
1940 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
1960 IF V3=5 THEN GOTO 4000
1970 IF V3=6 THEN GOTO 4000
1990 GOTO 3000
2000 REM " --- Display last page"
2010 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2020 GOSUB 5000
2030 IF DONE THEN GOTO 4000
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$=ANSWER$,CHOICE2$=""
3030 GOSUB 5000
3040 IF CONTROL=1 THEN GOTO 1000
3090 GOTO 4000
3800 REM " --- Exit"
3810 LET OPTION$[1]="EXIT"
3890 GOTO 4000
3900 REM " --- Startover"
3910 LET OPTION$[1]="STARTOVER"
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CS',
6090 RETURN 
6100 REM " --- Order lookup"
6110 DIM G1$(10)
6120 LET V$=A0$(3)
6130 CALL "SYC.LO",V$,G1$
6140 IF G1$(1,1)<>"I" THEN LET V$=""
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9950 EXIT 
9999 END

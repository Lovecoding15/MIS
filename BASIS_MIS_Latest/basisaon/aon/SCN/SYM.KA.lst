0010 REM "SYM -  Fiscal Kalendar Maintenance"
0020 REM "Program SYM.KA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "25Mar97 WGH: Fix test for duplicate period end dates (4120)"
0040 REM 
0070 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="GLM-02",FILES$[3]="GLT-04"
0135 LET FILES$[4]="GLT-06"
0140 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1]
0200 REM " --- IOLIST's"
0210 GLS01A: IOLIST A0$,A1$,A2$(1),A3$(1),A4$(1),M0$,M1$,M2$,M3$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N3$=F5$
0430 LET P7=32,P8=10,P9=20,MP=13,O0=1,A3=MP*2+MP*16+MP*3+MP*1+MP*3
0440 CALL "SYC.VA",N2$,INFO$[ALL]
0450 LET GL$=INFO$[20]
0500 REM " --- Initializations"
0510 DIM A2$(10),A3$(A3),A4$(32)
0550 LET I0=0,W=3,L0=5,A0$=N0$+N2$+"00"
0555 LET MONTHS$="January         February        March           "
0560 LET MONTHS$=MONTHS$+"April           May             June            "
0565 LET MONTHS$=MONTHS$+"July            August          September       "
0570 LET MONTHS$=MONTHS$+"October         November        December        "
0575 LET ABBREV$="JanFebMarAprMayJunJulAugSepOctNovDec"
0600 REM " --- Additional file opens"
0610 IF GL$<>"Y" THEN GOTO 0700
0620 CALL "SYC.DA",1,2,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS>0 THEN GOTO 9900
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0750 PRINT 'SB',@(25,3),"Total Periods:",@(11,L0),"Period",@(19),"Ends",@(26),"Period Name",@(44),"Abbr",@(50),"Locked?",@(61),"Date",'SF',
1200 REM " --- Retrieve record"
1210 FIND (SYS01_DEV,KEY=A0$,DOM=1500)IOL=GLS01A
1220 LET NUMBER=0,NUMBER=NUM(A2$(1,2),ERR=1500)
1230 IF NUMBER=0 THEN GOTO 1500
1240 IF POS("  "=A3$(1,2*NUMBER))<>0 THEN GOTO 1500
1250 LET LOCKED$=A3$(FNP4(1,MP),MP),ORIGINAL_A2$=A2$(1,2)
1260 LET ORIGINAL_A3$=A3$(1,26)
1300 REM " --- Display"
1310 GOSUB 5000
1390 GOTO 4000
1500 REM " --- New Record"
1510 LET A3$(1)=FNI$(1)+FNI$(31)+FNI$(2)+FNI$(28)+FNI$(3)+FNI$(31)+FNI$(4)+FNI$(30)+FNI$(5)+FNI$(31)+FNI$(6)+FNI$(30)+FNI$(7)+FNI$(31)+FNI$(8)+FNI$(31)+FNI$(9)+FNI$(30)+FNI$(10)+FNI$(31)+FNI$(11)+FNI$(30)+FNI$(12)+FNI$(31)+FNI$(12)+FNI$(31)
1520 LET A3$(FNP2(1,MP))=MONTHS$,A3$(FNP3(1,MP))=ABBREV$
1530 LET A3$(FNP4(1,MP))="NNNNNNNNNNNN",LOCKED$=A3$(FNP4(1,MP),MP)
1540 LET NUMBER=12,A2$(1,2)="12",A2$(3,2)=F0$(20,2),A2$(5,2)=F0$(18,2)
1550 LET A2$(7,2)="00",A2$(9,2)="10",A4$(1,6)="YYYYYY"
1560 LET ORIGINAL_A2$="",ORIGINAL_A3$=""
1590 GOSUB 5000
2200 REM " --- Total Number of Periods"
2210 LET V4$="Enter The Total Number of Periods in The Fiscal Calendar (Up to 13)"
2220 LET V0$="Z",V1$="CR",V2$=A2$(1,2),V3$="",V0=2,V1=40,V2=3,I0=0
2230 GOSUB 7000
2235 ON V3 GOTO 2240,2200,3900,2200,3900
2240 LET X=0,V=NUM(V$,ERR=2200)
2250 IF V<1 OR V>MP THEN GOTO 2200
2260 IF V<>NUMBER THEN LET X=1
2270 LET A2$(1,2)=V$,NUMBER=V
2280 IF X>0 THEN GOSUB 5000
2290 GOTO 3900
2400 REM " --- Period Ending Date"
2405 LET X=INT(I0/W)+1,P1=FNP1(X,MP)
2410 IF X>NUMBER THEN GOTO 3900
2415 LET V4$="Enter Period Ending Date For Period "+STR(X)+" As "+$22$+"MMDD"+$22$
2420 LET V0$="S",V1$="C",V3$="",V0=4,V1=19,V2=X+L0
2423 LET V2$=FNB1$(A3$(P1,2)),V2$=V2$(1,2)+V2$(4,2)
2425 GOSUB 7000
2430 ON V3 GOTO 2440,2400,2480,2400,2480
2440 IF V$="" THEN LET V$=FNA$(A3$(P1),$FFFF$)+FNA$(A3$(P1+1),$FFFF$)
2445 IF LEN(V$)<>4 THEN GOTO 2400
2450 LET MM=NUM(V$(1,2),ERR=2400),DD=NUM(V$(3,2),ERR=2400)
2455 LET X$="312931303130313130313031"
2460 IF MM<1 OR MM>12 THEN GOTO 2400
2465 IF DD<1 OR DD>NUM(X$(MM*2-1,2)) THEN GOTO 2400
2470 LET A3$(P1,2)=FNI$(MM)+FNI$(DD)
2480 PRINT @(V1,V2),FNB1$(A3$(P1,2)),
2490 GOTO 3900
2600 REM " --- Period Name"
2610 LET X=INT((I0-1)/W)+1,P2=FNP2(X,MP)
2620 IF X>NUMBER THEN GOTO 3900
2625 GOSUB 6000
2630 LET V4$="Enter The Name For Period "+STR(X)
2635 IF M$<>"" THEN LET V4$=V4$+" (<F1>="+FNP$(M$)+")"
2640 LET V0$="S",V1$="CK",V2$=A3$(P2,16),V3$="",V0=16,V1=26,V2=L0+X
2645 IF FNP$(V2$)="" THEN LET V2$=M$
2650 GOSUB 7000
2660 ON V3 GOTO 2680,2670,3900,2600,3900
2670 IF M$<>"" THEN LET V$=M$
2680 LET A3$(P2,16)=V$
2685 PRINT @(V1,V2),A3$(P2,16),
2690 GOTO 3900
2800 REM " --- Abbreviated Name"
2810 LET X=INT((I0-2)/W)+1,P3=FNP3(X,MP)
2820 IF X>NUMBER THEN GOTO 3900
2825 GOSUB 6000
2830 LET V4$="Enter The Abbreviated Name For Period "+STR(X)
2835 IF A$<>"" THEN LET V4$=V4$+" (<F1>="+FNP$(A$)+")"
2840 LET V0$="S",V1$="CK",V2$=A3$(P3,3),V3$="",V0=3,V1=44,V2=L0+X
2845 IF FNP$(V2$)="" THEN LET V2$=A$
2850 GOSUB 7000
2860 ON V3 GOTO 2880,2870,3900,2800,3900
2870 IF A$<>"" THEN LET V$=A$
2880 LET A3$(P3,3)=V$
2885 PRINT @(V1,V2),A3$(P3,3),
2890 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2200,2400,2600,2800,2400,2600,2800,2400,2600,2800,2400,2600,2800,2400,2600,2800,2400,2600,2800,2400,2600,2800,2400,2600,2800,2400,2600,2800,2400,2600,2800,2400,2600,2800,2400,2600,2800,2400,2600,2800,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Check for Duplicate Period Ending Dates"
4110 FOR X=1 TO NUMBER
4115 LET P1=(X-1)*2+1
4120 IF POS(A3$(P1,2)=A3$(1,NUMBER*2),2)<>P1 THEN EXITTO 4150
4130 NEXT X
4140 GOTO 4200
4150 LET V4$="Duplicate Period Ending Dates Not Allowed (<Enter>=Continue)"
4160 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
4170 GOSUB 7000
4190 GOTO 2000
4200 REM " --- Calendar changed?"
4210 IF GL$<>"Y" THEN GOTO 4900
4220 IF ORIGINAL_A2$<>"" THEN IF ORIGINAL_A2$<>A2$(1,2) THEN GOTO 4300
4230 IF ORIGINAL_A3$<>"" THEN IF ORIGINAL_A3$<>A3$(1,26) THEN GOTO 4300
4290 GOTO 4900
4300 REM " --- Check G/L files for activity"
4310 FOR J=2 TO 4
4320 READ (CHANNELS[J],KEY=N0$,DOM=4330)
4330 LET K$=KEY(CHANNELS[J],END=4380)
4340 IF POS(N0$=K$)=1 THEN EXITTO 4400
4380 NEXT J
4390 GOTO 4900
4400 REM " --- G/L activity found. Must use G/L utility to change calendar"
4410 DIM MESSAGE$[7]
4420 LET MESSAGE$[0]="General Ledger Daily Detail, Transaction History and/or Summary Activity"
4425 LET MESSAGE$[1]="already exists. This information will not be automatically moved to a"
4430 LET MESSAGE$[2]="new period as a result of your changes to the fiscal calendar. Your General"
4440 LET MESSAGE$[3]="Ledger screens and reports may become inaccurate. The Fiscal Calendar"
4450 LET MESSAGE$[4]="changes you have made will NOT be saved. Try using the Change Fiscal"
4460 LET MESSAGE$[5]="Calendar task on the G/L Utilities Menu to make the desired modifications."
4470 LET MESSAGE$[7]="                  Press <Enter> To Continue"
4480 CALL "SYC.XA",2,MESSAGE$[ALL],7,-1,-1,V$,V3
4485 IF V$="SPECIAL" THEN GOTO 4900
4490 GOTO 9900
4900 REM " --- Write"
4910 WRITE (SYS01_DEV,KEY=A0$)IOL=GLS01A
4990 GOTO 9900
5000 REM " --- Display"
5010 PRINT @(40,L0-2),A2$(1,2),
5020 FOR X=1 TO MP
5025 IF X>NUMBER THEN GOTO 5070
5030 LET X$="No",Y$=""
5035 IF A3$(FNP4(X,MP),1)="Y" THEN LET X$="Yes"
5040 IF FNP$(A3$(FNP5(X,MP),3))<>"" THEN LET Y$=FNB$(A3$(FNP5(X,MP),3))
5045 PRINT @(13,X+L0),X:"##",@(19),FNB1$(A3$(FNP1(X,MP),2)),@(26),A3$(FNP2(X,MP),16),@(44),A3$(FNP3(X,MP),3),@(52),X$,@(59),Y$,
5065 GOTO 5080
5070 PRINT @(13,X+L0),'CL',
5080 NEXT X
5090 RETURN 
6000 REM " --- Return default month name and abbrev based on ending date"
6010 LET M$="",A$="",P1=FNP1(X,MP)
6020 LET X$=FNB1$(A3$(P1,2)),MM=NUM(X$(1,2),ERR=6090)
6030 IF MM<1 OR MM>12 THEN GOTO 6090
6040 LET M$=MONTHS$((MM-1)*16+1,16),A$=ABBREV$((MM-1)*3+1,3)
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNB1$(Q1$)=FNA$(Q1$(1),"XXX")+"/"+FNA$(Q1$(2),"XXX")
8050 DEF FNI$(Q)=CHR(Q+32)
8060 DEF FNP$(Q$)=CVS(Q$,2)
8065 DEF FNP1(Q1,Q2)=(Q1-1)*2+1
8070 DEF FNP2(Q1,Q2)=Q2*2+((Q1-1)*16+1)
8075 DEF FNP3(Q1,Q2)=Q2*2+Q2*16+((Q1-1)*3+1)
8080 DEF FNP4(Q1,Q2)=Q2*2+Q2*16+Q2*3+Q1
8085 DEF FNP5(Q1,Q2)=Q2*2+Q2*16+Q2*3+Q2*1+((Q1-1)*3+1)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

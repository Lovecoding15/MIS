0010 REM "SHX - RPS Generate Sample Manifest Data"
0020 REM "Program SHXDMA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 08Feb99 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Feb97 CAJ: Modified for 1997 SpeedShip Update"
0032 REM "01Feb98 JAL: Modified for 1998 SpeedShip Update"
0033 REM "01Feb99 JAL: Modified for 1999 SpeedShip Update"
0034 REM "03Feb99 JAL: Correct assignment of zone elements (680-685)"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 SHE01A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0220 SHE02A: IOLIST W0$,W1$(1),W[ALL]
0240 SHM43A: IOLIST X0$,X[ALL]
0250 SHM10A: IOLIST S0$(1),S1$(1)
0260 SHM10C: IOLIST R0$(1),R[ALL]
0270 SHM10G: IOLIST G0$(1),G1$(1)
0500 REM " --- Initializations"
0510 DIM S0$(10),S1$(128)
0550 LET COD=10000000,COD=FNYY_YEAR(FNN$(FNC$(F0$(7,3))),ERR=0550)*1000
0560 LET ZONES=30,CODAMOUNT=100.01,VALUE=100.01,N=0,TRACKING=COD
0570 LET DW$="Y",AH$="Y",HZ$="Y",CT$="Y",OVERSIZE$="N",COD$="Y"
0580 LET PICKUP$=FNP$(I8$(37,10)),PREFIX$=N0$+P8$+P9$
0585 LET PICKUP$=PICKUP$+"-"+FILL(11-LEN(PICKUP$),"0")
0590 LET PICKUP$(LEN(PICKUP$),1)="X",LASTFILE$=""
0600 REM " --- Zone Initializations"
0610 DIM Z[ZONES],Y[ZONES]
0615 LET Z[1]=2,Z[2]=3,Z[3]=4,Z[4]=5,Z[5]=6,Z[6]=7,Z[7]=8,Z[8]=9
0620 LET Z[9]=10,Z[10]=17,Z[11]=32,Z[12]=33,Z[13]=34,Z[14]=35,Z[15]=36
0625 LET Z[16]=37,Z[17]=38,Z[18]=72,Z[19]=73,Z[20]=74,Z[21]=75,Z[22]=76
0630 LET Z[23]=77,Z[24]=78,Z[25]=51,Z[26]=54,Z[27]=61,Z[28]=41
0635 LET Z[29]=42,Z[30]=43
0660 REM " --- Define which numeric element to use for each zone"
0665 LET Y[1]=0,Y[2]=0,Y[3]=0,Y[4]=0,Y[5]=0,Y[6]=0,Y[7]=0,Y[8]=2
0670 LET Y[9]=2,Y[10]=2,Y[11]=3,Y[12]=3,Y[13]=3,Y[14]=3,Y[15]=3
0675 LET Y[16]=3,Y[17]=3,Y[18]=1,Y[19]=1,Y[20]=1,Y[21]=1,Y[22]=1
0680 LET Y[23]=1,Y[24]=0,Y[25]=0,Y[26]=0,Y[27]=0,Y[28]=0,Y[29]=0
0685 LET Y[30]=0
1000 REM " --- Ground Zones (2 through 8)"
1010 LET Z$=""
1020 PRINT @(COLUMN,11),FILL(11),
1030 FOR X=2 TO 8
1040 LET Z$=Z$+"/RPS GRD"
1050 NEXT X
1060 LET Z$=Z$(2)
1200 REM " --- RPS Deferred Puerto Rico, Alaska and Hawaii (9, 10 and 17)"
1210 FOR X=1 TO 3
1220 LET Z$=Z$+"/RPS DEFRPS DAK"
1230 NEXT X
1300 REM " --- RPS 3rd Day (32 through 38)"
1310 FOR X=32 TO 38
1320 LET Z$=Z$+"/RPS 3DA"
1330 NEXT X
1400 REM " --- RPSAIR Domestic (72 through 78)"
1410 FOR X=72 TO 78
1420 LET Z$=Z$+"/RPS AIR"
1430 NEXT X
1500 REM " --- Canadian Ground Zones (51 and 54)"
1510 FOR X=1 TO 2
1520 LET Z$=Z$+"/RPS CAN"
1530 NEXT X
1600 REM " --- Mexican Ground (61)"
1610 LET Z$=Z$+"/RPS MGD"
1700 REM " --- International Air Service (41 through 43)"
1710 FOR X=41 TO 43
1720 LET Z$=Z$+"/RPS INT"
1730 NEXT X
1900 REM 
1910 LET Z$=Z$+"/"
2000 REM " --- Top of Zone Loop"
2010 FOR J=1 TO ZONES
2020 IF LEN(Z$)<2 THEN GOTO 3800
2030 LET Z1$=Z$(1,POS("/"=Z$)-1),Z$=Z$(POS("/"=Z$)+1),ZONE=Z[J]
2040 IF LEN(Z1$)=0 THEN GOTO 3800
2045 IF ZONE=19 THEN GOTO 3800
2050 PRINT @(COLUMN,11),ZONE:"##0",
2060 LET AK=0
2065 IF ZONE=17 THEN LET AK=1
2100 REM " --- Top of Service Loop"
2110 FOR K=1 TO LEN(Z1$) STEP 7
2120 PRINT @(COLUMN+4,11),Z1$(K,7),
2130 FIND (SHM10_DEV,KEY=N0$+"A"+Z1$(K,7),DOM=3500)IOL=SHM10A
2200 REM " --- Initialize SHE-01 Header"
2210 LET MANUAL=MANUAL+1,A0$(1)=N0$+"M"+STR(MANUAL:"000000")
2220 LET A0$(18,3)="000",A1$(1)="000000"+S1$(2,24)
2230 LET A1$(31,96)=C1$(31,96),A2$(1)=F0$(18,6)
2240 FOR X=7 TO 103 STEP 24
2250 IF FNP$(A1$(X,24))<>"" THEN GOTO 2270
2255 LET X9=X-24
2260 IF X9<7 THEN LET X9=7
2265 EXITTO 2400
2270 NEXT X
2280 LET X9=103
2290 IF A1$(144,24)<>"" THEN LET X9=144
2400 REM " --- Initialize SHE-02 Detail"
2410 DIM W1$(128),W[20]
2420 LET W[0]=W+N,W=W[0]
2430 IF W[0]>150 THEN EXITTO 3900
2440 LET W0$(1)=A0$(1,9),W0$(18,3)="001",W1$(59,2)=P8$
2450 LET W1$(1,2)=STR(Z[J]:"00"),W1$(4,7)=Z1$(K,7),W1$(11,1)=S1$(1,1)
2455 IF FNP$(W1$(29,24))="" THEN LET W1$(29,24)=S1$(2,24)
2480 IF W[0]<51 THEN GOTO 2500
2490 IF MOD(W[0],2)<>0 THEN LET W[0]=INT(W[0]/2)*2
2500 REM " --- Set defaults"
2510 DIM G0$(10),G1$(128),R0$(10),R[20]
2520 FIND (SHM10_DEV,KEY=N0$+"C"+W1$(4,7),DOM=2530)IOL=SHM10C
2530 FIND (SHM10_DEV,KEY=N0$+"G"+W1$(4,7),DOM=2600)IOL=SHM10G
2540 LET G1$=FNP$(G1$),L=LEN(G1$)
2550 IF L=0 THEN GOTO 2600
2560 FOR X=1 TO L
2580 IF G1$(X,1)<>" " THEN IF W1$(X,1)=" " THEN LET W1$(X,1)=G1$(X,1)
2590 NEXT X
2600 REM " --- Special fields based on service"
2610 IF W[0]=0 THEN LET W[0]=1
2620 LET W1$(16,1)=OVERSIZE$,W1$(17,1)=COD$
2630 IF W1$(16,1)<>"Y" THEN GOTO 2650
2640 LET COD=COD+1,W1$(20,7)=STR(COD:"0000000"),W[2]=CODAMOUNT
2650 IF OVERSIZE$="Y" THEN LET OVERSIZE$="N",W[1]=VALUE
2655 IF AH$="Y" THEN LET W1$(126,1)="Y",AH$="N"
2660 IF COD$="Y" THEN LET COD$="N",OVERSIZE$="Y"
2670 LET TRACKING=TRACKING+1,W1$(108,18)="1"+STR(TRACKING:FILL(18,"0"))
2680 IF POS(W1$(61,1)="CMI")=0 THEN GOSUB 6000
2700 REM " --- Dimensional weight"
2710 IF N=0 THEN GOTO 2800
2720 IF DW$<>"Y" THEN GOTO 2800
2730 IF R[20]=0 THEN GOTO 2800
2740 IF W1$(4,7)<>"RPS AIR" THEN GOTO 2800
2750 LET W1$(88,1)="D",DW$="N",X=INT(SQR(W[0]))+1
2760 LET W[17]=X,W[18]=X+1,W[19]=X-1
2765 LET W[20]=W[17]*W[18]*W[19]/R[20]
2770 IF W[20]>W[0] THEN GOTO 2790
2775 LET X=X+2
2780 GOTO 2760
2790 IF INT(W[20])<>W[20] THEN LET W[20]=INT(W[20])+1
2800 REM " --- Hazardous and call tag"
2810 IF S1$(79,1)<>"G" THEN GOTO 2900
2820 IF W1$(126,1)="Y" THEN GOTO 2900
2830 IF W1$(88,1)="D" THEN GOTO 2900
2840 IF W1$(16,1)="Y" THEN GOTO 2900
2850 IF W1$(17,1)="Y" THEN GOTO 2900
2860 IF CT$="Y" THEN LET W1$(18,1)="Y",CT$="N"
2870 IF W1$(18,1)="Y" THEN GOTO 2900
2880 IF HZ$="Y" THEN LET W1$(19,1)="Y",HZ$="N"
2900 REM " --- Generate zip code/destination for this package"
2910 GOSUB 6200
2920 IF FNP$(A1$(127,3))<>"" THEN GOTO 3000
2930 IF W1$(61,1)<>"I" THEN GOTO 3500
3000 REM " --- Write sample records"
3020 WRITE (SHE01_DEV,KEY=A0$)IOL=SHE01A
3030 WRITE (SHE02_DEV,KEY=W0$)IOL=SHE02A
3500 REM " --- End of Service Loop"
3590 NEXT K
3800 REM " --- End of Zone Loop"
3890 NEXT J
3900 REM " --- Check for completion"
3910 IF N>0 THEN GOTO 4000
3920 LET N=1
3990 GOTO 1000
4000 REM " --- Run generation overlay"
4010 CLOSE (SHM43_DEV,ERR=4020)
4090 RUN "SHX.MB"
6000 REM " --- Extract destination state code from address"
6010 LET X$=FNP$(A1$(144,24))
6020 IF LEN(X$)>1 THEN GOTO 6080
6030 FOR X=103 TO 31 STEP -24
6040 LET X$=FNP$(A1$(X,24))
6050 IF LEN(X$)>1 THEN EXITTO 6080
6060 NEXT X
6070 LET X$=FILL(2)
6080 LET A1$(142,2)=X$(LEN(X$)-1,2)
6090 RETURN 
6200 REM " --- Generate pseudo zip code for this zone"
6210 DIM X[3]
6220 LET P=Y[J],ZONEFILE$=S1$(42,6)
6225 LET CITY$=A1$(X9,24),STATE$=A1$(142,2),ZIPCODE$=""
6230 IF ZONEFILE$=LASTFILE$ THEN GOTO 6290
6260 GOSUB 6900
6270 IF STATUS THEN GOTO 6700
6285 LET LASTFILE$=ZONEFILE$
6290 READ (SHM43_DEV,KEY=PREFIX$,DOM=6300)
6300 REM " --- Read next zone chart record"
6310 READ (SHM43_DEV,END=6700)IOL=SHM43A
6320 IF POS(PREFIX$=X0$)<>1 THEN GOTO 6700
6340 IF ZONE<>X[P] THEN GOTO 6300
6350 IF AK>0 THEN GOTO 6500
6390 IF W1$(61,1)="I" THEN GOTO 6600
6400 REM " --- Domestic/Canadian/Mexican by postal code"
6410 IF POS(W1$(61,1)="CM")>0 THEN GOTO 6450
6420 LET ZIPCODE$=X0$(6,3)+"00"
6430 IF ZONE=10 THEN LET CITY$="San Juan",STATE$="PR"
6435 IF ZONE=9 THEN LET CITY$="Honolulu",STATE$="HI"
6440 GOTO 6700
6450 IF W1$(61,1)="M" THEN LET CITY$="Mexico",ZIPCODE$=X0$(6,5)
6460 IF W1$(61,1)="C" THEN LET CITY$="Canada",ZIPCODE$=X0$(6,6)
6470 LET STATE$=""
6490 GOTO 6700
6500 REM " --- Alaskan by zip code"
6510 LET STATE$="AK",ZIPCODE$=X0$(6,5)
6590 GOTO 6700
6600 REM " --- International by destination"
6610 LET CITY$=FNP$(X0$(6)),STATE$="",ZIPCODE$=""
6700 REM " --- Pseudo zip assigned"
6710 LET A1$(X9,24)=CITY$,A1$(142,2)=STATE$,A1$(127,9)=ZIPCODE$
6790 RETURN 
6900 REM " --- Open a zone file"
6910 LET FILES$[FILES]=ZONEFILE$,LASTFILE$="",X=FILES
6920 CLOSE (SHM43_DEV,ERR=6930)
6930 CALL "SYC.DA",1,X,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
6940 IF STATUS=0 THEN LET LASTFILE$=ZONEFILE$
6950 LET SHM43_DEV=CHANNELS[FILES]
6990 RETURN 
8000 REM " --- Function Definitions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8070 DEF FNN$(Q$)=Q$(3,4)+Q$(1,2)
8095 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

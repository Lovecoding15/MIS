0010 REM "BMM - Bill Maintenance - Operation Info
0020 REM "Program BMM.MD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "26Aug98 WGH: Provide prompts for input fields ()
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 BMM03A: IOLIST R0$,R1$,R[ALL]
0220 BMM06A: IOLIST KEY6$
0230 BMM08A: IOLIST D0$,D[ALL]
0500 REM " --- Init Data
0510 LET L9=20,MESS$="",FIRST_PASS$="Y"
0520 DIM R0$(25),R[11],D[13]
0530 LET R0$(1)=A0$(1,22)
0540 PRECISION BOMP6
0700 REM " --- Background
0710 PRINT @(0,10),'CL',@(0,11),'CL'
0715 CALL "SYC.WC",1,0,80,0,0,9,0
0720 PRINT 'SB',@(10,9),"Operation Information",
0730 PRINT @(4,10),"Op Code",@(30),"Setup",@(36),"Hrs/Piece",@(47),"Piece/Hr",@(68),"Net Hrs"
0740 IF WO$="Y" THEN PRINT @(20,10),"Queue",@(61),"Move",
0750 PRINT @(0,11),"Seq",@(4),"Description",@(41),"Dir Rate",@(51),"Dir Cost",@(61),"O/H Cost",@(71),"Tot Cost",
0780 PRINT 'SF',
0790 GOSUB 5100
0800 IF FIRST_PASS$="Y" THEN LET FIRST_PASS$="N"; GOTO 4000
1000 REM " --- Check Line
1040 IF L>L9 THEN PRINT @(0,L2),'LD','LD'; LET L=L9
1100 REM " --- Get Next Key"
1120 LET K$=KEY(BMM03_DEV,END=1400)
1140 IF POS(A0$(1,22)=K$)<>1 THEN GOTO 1400
1150 READ (BMM03_DEV,KEY=K$)IOL=BMM03A
1160 GOTO 1490
1400 REM " --- Seq
1405 IF L>L9 THEN PRINT @(0,L2),'LD','LD',; LET L=L9
1410 IF V3<>2 THEN LET R0=NUM(R0$(23,3))+5
1420 LET V0$="Z",V1$="KER",V2$=STR(R0),V3$="",V4$="",V0=3,V1=0,V2=L,I0=0
1430 GOSUB 7000
1440 IF V3=4 THEN PRINT @(V1,V2),"    "; GOTO 4000
1450 IF V$="000" OR V$="" THEN GOTO 1400
1460 LET R0$(23,3)=V$
1470 DIM R[11]
1475 DIM R1$(110)
1480 FIND (BMM03_DEV,KEY=R0$(1,25),DOM=1600)IOL=BMM03A
1485 PRINT @(0,L),'CL',@(0,L+1),'CL',
1490 IF R0$(1,25)<=SAVER0$ THEN GOSUB 5400
1500 GOSUB 5000
1520 LET I0=5
1530 GOTO 3000
1600 REM " --- Setup
1610 IF MESS$="Y" THEN LET R1$(4,1)="M" ELSE LET R1$(4,1)="S"
1620 PRINT @(0,L),'CL',@(0,L+1),'CL',
1630 PRINT @(0,L),R0$(23,3),
2000 REM " --- Operation Code
2010 REMOVE (BMM06_DEV,KEY=R0$(1,2)+R1$(1,3)+R0$(3),DOM=2020)
2020 IF R1$(4,1)="M" THEN LET MESS$="Y"; GOTO 2800
2030 LET V0$="S",V1$="KCR",V2$=R1$(1,3),V0=3,V1=4,V2=L,I0=1,V4$="Enter A Valid Operation Code, "
2040 FIND (BMM03_DEV,KEY=R0$(1,25),DOM=2050)IOL=BMM03A; LET V4$=V4$+"<F3>=Code Lookup",EXIST$="Y"; GOTO 2060
2050 LET V4$=V4$+"<F1>=Message Line, <F3>=Code Lookup",EXIST$="N"
2060 GOSUB 7000
2070 IF EXIST$="N" THEN IF V3=1 THEN LET MESS$="Y",R1$(4,1)="M"; GOTO 2800
2080 IF V3<>3 THEN GOTO 2120
2090 LET V4=1,V6=3,V7=3,V8=6,V9=20,V5$=""
2100 CALL "SYC.LZ",V1,V2,V4,BMM08_DEV,V6,V7,V8,V9,V$,V5$,N0$
2110 IF V$="" THEN GOTO 2000
2120 IF V3=2 THEN GOTO 2240
2130 IF V3=4 THEN GOTO 4000
2140 IF LEN(V$)<3 THEN LET V$=V$+FILL(3-LEN(V$))
2150 READ (BMM08_DEV,KEY=R0$(1,2)+V$,DOM=2000)IOL=BMM08A
2160 IF D[1]=0 THEN LET D[1]=1
2170 IF R[1]=0 THEN LET R[1]=D[1]
2180 IF R[0]=0 THEN LET R[0]=1
2190 IF R[4]=0 THEN LET R[4]=D[4]
2200 IF R[5]=0 THEN LET R[5]=D[5]
2210 LET OVERHEAD=D[2]
2220 LET R1$(1,3)=V$
2230 GOSUB 5000
2240 GOTO 2900
2300 REM " --- Hours Per Piece
2310 IF R1$(4,1)="M" THEN GOTO 2900
2320 LET V0$="N",V1$="C",V2$=STR(R[0]),V3$=P1$,V4$="Enter hours needed to create the piece.",V0=0,V1=37,V2=L
2330 GOSUB 7000
2340 LET R[0]=V
2350 GOTO 2900
2400 REM " --- Setup Time
2410 IF R1$(4,1)="M" THEN GOTO 2450
2420 LET V0$="N",V1$="C",V2$=STR(R[4]),V3$=M6$,V4$="Enter hours needed to set up for assembly of entire lot.",V0=0,V1=29,V2=L
2430 GOSUB 7000
2440 LET R[4]=V
2450 GOTO 2900
2500 REM " --- Pieces Per Hour
2510 IF R1$(4,1)="M" THEN GOTO 2650
2520 LET V0$="N",V1$="C",V2$=STR(R[1]),V3$=P2$,V4$="Enter how many pieces per hour can be created.",V0=0,V1=47,V2=L
2530 GOSUB 7000
2540 LET R[1]=V
2550 IF R[1]<>0 THEN GOTO 2600
2560 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Pieces/Hr May Not Be Zero, <Enter> To Correct: ",V0=1,V1=FNV(V4$),V2=22
2570 PRINT @(V1,V2),'RB',
2580 GOSUB 7000
2590 GOTO 2520
2600 LET DIRECTCOST=0
2610 IF R[1]<>0 THEN LET DIRECTCOST=FNOP1(R[0],D[0],R[1],A[4],R[4],A[5])
2620 LET OHCOST=DIRECTCOST*OVERHEAD
2630 GOSUB 5000; GOTO 2650
2640 PRINT @(75-3*P9),DIRECTCOST:P9$,@(76-2*P9),OHCOST:P9$,@(77-P9,V2),DIRECTCOST+OHCOST:P9$,
2650 GOTO 2900
2700 REM " --- Move Time
2710 IF R1$(4,1)="M" OR WO$="N" THEN GOTO 2750
2720 LET V0$="N",V1$="C",V2$=STR(R[5]),V3$=M6$,V4$="Enter hours needed to move to next process.",V0=0,V1=59,V2=L
2730 GOSUB 7000
2740 LET R[5]=V
2750 GOTO 2900
2800 REM " --- Message
2810 LET V0$="S",V1$="C",V2$=R1$(5,60),V3$="",V4$="Enter Message",V0=60,V1=4,V2=L+1,I0=1
2820 FIND (BMM03_DEV,KEY=R0$(1,25),DOM=2830)IOL=BMM03A; LET EXIST$="Y"; GOTO 2840
2830 LET V4$=V4$+", <F1>=Standard Line",EXIST$="N"
2840 GOSUB 7000
2850 IF EXIST$="N" THEN IF V3=1 THEN LET MESS$="N",R1$(4,1)="S"; GOTO 2000
2860 LET R1$(5,60)=V$
2870 GOTO 2900
2900 REM " --- Seq Control
2910 IF V3=4 THEN IF I0>1 THEN GOTO 3000
2920 IF V3<>2 THEN GOTO 2950
2930 IF I0>0 THEN LET I0=I0-1
2940 GOTO 2960
2950 LET I0=I0+1
2960 ON I0 GOTO 1400,2000,2400,2300,2500,2700,3000
3000 REM " --- Correct?
3010 LET R0=NUM(R0$(23,3))
3020 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,SAVER0$=R0$(1,25)
3025 LET V4$="<Enter>=OK, <F1>=Additional Options, <F2>=Change, D=Delete"
3030 GOSUB 7000
3040 IF V3=1 THEN LET X0$=R1$(65,3),X1$=R1$(68,3); CALL "BMC.BA",X0$,X1$; LET R1$(65,3)=X0$,R1$(68,3)=X1$; GOTO 3000
3050 IF V3=2 THEN LET I0=6; GOTO 2900
3060 IF V$="" THEN GOTO 3100
3080 IF V$="D" THEN GOTO 3200
3100 REM " --- Write
3120 WRITE (BMM03_DEV,KEY=R0$(1,25))IOL=BMM03A
3125 IF R1$(4,1)<>"S" THEN GOTO 3140
3130 LET KEY6$=R0$(1,2)+R1$(1,3)+R0$(3,23)
3135 IF R1$(4,1)="S" THEN WRITE (BMM06_DEV,KEY=KEY6$)IOL=BMM06A
3140 LET L=L+2,SAVER0$=R0$(1,25)
3160 IF V3=4 THEN GOTO 4000
3190 GOTO 1000
3200 REM " --- Delete
3220 REMOVE (BMM03_DEV,KEY=R0$(1,25),DOM=3230)
3230 REMOVE (BMM06_DEV,KEY=R0$(1,2)+R1$(1,3)+R0$(3,23),DOM=3240)
3240 PRINT @(0,L),'CL',@(0,L+1),'CL'
3290 GOTO 1000
4000 REM " --- Options
4010 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct? (Y/N/#/Review/List): ",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4025 LET I=NUM(V$,ERR=4030); LET V$=STR(I:"000")
4026 IF V$="000" THEN GOTO 4030
4027 IF L>L9 THEN PRINT @(0,L2),'LD','LD',; LET L=L9
4028 GOTO 1450
4030 IF V$="R" OR V$="Review" THEN LET V$="REVIEW" ELSE IF V$="L" OR V$="List" THEN LET V$="LIST"
4040 IF V$="REVIEW" THEN GOTO 4300
4050 IF V$="LIST" THEN GOTO 4400
4060 ON POS(V$="YN") GOTO 4000,4100,1400
4100 GOSUB 6000
4180 IF O0=0 THEN PRINT @(0,10),'CE',; RUN "BMM.MC"
4190 RUN "BMM.MA"
4300 REM " --- Review
4320 READ (BMM03_DEV,KEY=R0$(1,22),DOM=4340)
4340 GOSUB 5400
4390 GOTO 1000
4400 REM " --- List
4420 GOSUB 5100
4490 GOTO 4000
5000 REM " --- Display
5002 DIM D0$(30)
5005 LET D[0]=0,D[1]=0
5010 IF R1$(4,1)="M" THEN PRINT @(0,L),R0$(23,3),@(4,L+1),R1$(5,60),; GOTO 5090
5020 READ (BMM08_DEV,KEY=R0$(1,2)+R1$(1,3),DOM=5050)IOL=BMM08A
5050 IF R[1]=0 THEN LET R[1]=1
5055 IF A[5]=0 THEN LET A[5]=1
5057 LET OVERHEAD=D[2]
5060 LET DIRECTCOST=FNOP1(R[0],D[0],R[1],A[4],R[4],A[5])
5062 LET OHCOST=DIRECTCOST*OVERHEAD
5080 PRINT @(0,L),R0$(23,3),@(4),R1$(1,3),@(29),R[4]:M6$,@(37),R[0]:P1$,@(47),R[1]:P2$,@(67),100*(R[0]/R[1])/A[4]+R[4]/A[5]:P1$,
5082 IF WO$="Y" THEN PRINT @(19),D[3]:M6$,@(59),R[5]:M6$,
5085 PRINT @(4,L+1),D0$(6,20),@(41),D[0]:P1$,@(51),DIRECTCOST:P1$,@(61),OHCOST:P1$,@(71),DIRECTCOST+OHCOST:P1$
5090 RETURN 
5100 REM " --- List
5110 GOSUB 5400
5130 READ (BMM03_DEV,KEY=A0$(1,22),DOM=5140)
5140 LET K$=KEY(BMM03_DEV,END=5300)
5150 IF POS(A0$(1,22)=K$)<>1 THEN GOTO 5300
5160 READ (BMM03_DEV)IOL=BMM03A
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="C",V2$="",V3$="",V4$="<Enter>=Continue, <F4>=End>: ",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5225 IF V3=4 THEN EXITTO 4000
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+2,SAVER0$=R0$(1,25)
5290 GOTO 5140
5300 RETURN 
5400 REM " --- Clear Screen
5420 PRINT @(0,L2),'CE',
5440 LET L=L2
5490 RETURN 
6000 REM " --- Recalculate Direct & Overhead
6020 READ (BMM03_DEV,KEY=A0$(1,22),DOM=6040)
6040 LET K$=KEY(BMM03_DEV,END=6190)
6060 IF POS(A0$(1,22)=K$)<>1 THEN GOTO 6190
6070 READ (BMM03_DEV,KEY=K$)IOL=BMM03A
6080 DIM D0$(30); LET D[0]=0
6090 READ (BMM08_DEV,KEY=R0$(1,2)+R1$(1,3),DOM=6110)IOL=BMM08A
6110 LET DIRECTCOST=0,OVERHEAD=D[2]
6120 IF R[1]<>0 THEN LET DIRECTCOST=FNOP1(R[0],D[0],R[1],A[4],R[4],A[5])
6130 LET OHCOST=DIRECTCOST*OVERHEAD
6150 GOTO 6040
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNOP1(Q1,Q2,Q3,Q4,Q5,Q6)=100*(Q1*Q2/Q3)/Q4+Q5*Q2/Q6
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

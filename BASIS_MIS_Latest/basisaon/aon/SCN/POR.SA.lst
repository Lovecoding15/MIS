0010 REM "POR - Quality Assurance Rejection History Report (Input Overlay)
0020 REM "Program POR.SA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "20Nov96 Caj: Allow for entry of single character whses (ie '4 ')(1105,1110,1120,1130)
0040 REM 
0070 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="APM-01",FILES$[2]="IVM-01",FILES$[3]="IVM-10"
0130 LET FILES$[4]="POM-03",FILES$[5]="POT-03",FILES$[6]="POT-04"
0135 LET FILES$[7]="POT-14",FILES$[8]="POW-16",FILES$[9]="SYS-01"
0155 LET OPTIONS$[8]="C"
0160 CALL "SYC.DA",1,1,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET APM01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM10_DEV=CHANNELS[3]
0180 LET POM03_DEV=CHANNELS[4],POT03_DEV=CHANNELS[5],POT04_DEV=CHANNELS[6]
0185 LET POT14_DEV=CHANNELS[7],POW16_DEV=CHANNELS[8],SYS01_DEV=CHANNELS[9]
0200 REM " --- IOLists
0240 APM01A: IOLIST X0$,X1$(1)
0280 IVM10C: IOLIST X1$(1)
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 APS01A: IOLIST U0$,VENDIMASK$,U2$,U3$,U4$,VENDOMASK$
0330 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0400 REM " --- Parameters
0405 DIM INFO$[20],DESCLEN[3],PARAMS[3]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO",WHEN$=F0$(7,3)
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 LET VENDLEN=NUM(U2$(1,2))
0435 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0440 LET IVPRECISION=NUM(I2$(5,1)),ITEMLEN=NUM(I2$(1,2))
0445 FOR X=1 TO 3
0450 LET DESCLEN[X]=NUM(I2$((X-1)*2+9,2)),DESCLEN=DESCLEN+DESCLEN[X]
0460 LET PARAMS[X]=DESCLEN[X]
0465 NEXT X
0475 LET PARAMS[0]=ITEMLEN
0480 IF DESCLEN>40 THEN LET DESCLEN=40
0500 REM " --- Init Data
0510 PRECISION IVPRECISION
0520 DIM IV_CHANS[14],VENDFNAME$(30),VENDTNAME$(30),WHSEFNAME$(24)
0530 LET IV_CHANS[1]=IVM01_DEV,IV_CHANS[14]=IVM14_DEV
0540 LET EXTMASK$=M1$,QTYMASK$=M2$
0550 CALL "SYC.RM",M1$,M1,9,12
0560 CALL "SYC.RM",M2$,M2,8,10
0570 CALL "SYC.RM",M3$,M3,4,10
0700 REM " --- Background
0710 FOR X=4 TO 12 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(29,3),"Warehouse:",@(22,5),"Beginning Vendor:",@(25,7),"Ending Vendor:",@(16,9),"Beginning Receipt Date:",@(19,11),"Ending Receipt Date:"
0760 GOSUB REFRESH
1000 REM " --- Warehouse
1010 LET V0$="S",V1$="",V2$=WHSEFOR$,V3$="",V4$="Enter A Valid Warehouse ID  (<F1>=All/<F3>=Lookup)",V0=2,V1=40,V2=3,I0=0
1020 GOSUB 7000
1030 ON V3 GOTO 1090,1040,1190,1070,4000
1040 LET WHSEFOR$=""
1050 PRINT @(V1,V2),'CL',@(V1,V2),"All",
1060 GOTO 1190
1070 CALL "IVC.LK",3,V1,V2,V$
1080 IF V$="" THEN GOTO 1000
1090 IF V$="" THEN GOTO 1040
1100 DIM X1$(160)
1105 LET X1$(1)=N0$+"C"+V$
1110 FIND (IVM10_DEV,KEY=X1$(1,5),DOM=1000)IOL=IVM10C
1120 LET WHSEFOR$=X1$(4,2),WHSEFNAME$(1)=X1$(6,24)
1130 PRINT @(V1,V2),WHSEFOR$," ",WHSEFNAME$(1,24),'CL',
1190 GOTO 3900
1200 REM " --- Beginning Vendor
1210 LET V0$="M",V1$="KCE",V2$=VENDFROM$,V3$=VENDIMASK$,V4$="Enter A Valid Vendor Number (<F1>=First/<F3>=Lookup)",V0=6,V1=40,V2=5
1220 GOSUB 7000
1230 ON V3 GOTO 1290,1240,1340,1270,4000
1240 LET VENDFROM$=""
1250 PRINT @(V1,V2),'CL',@(V1,V2),"First",
1260 GOTO 1340
1270 CALL "SYC.LV",V$
1280 IF V$="" THEN GOTO 1200
1290 IF V$="" THEN GOTO 1240
1300 DIM X1$(195)
1310 FIND (APM01_DEV,KEY=N0$+V$,DOM=1200)IOL=APM01A
1320 LET VENDFROM$=V$,VENDFNAME$(1)=X1$(1,30)
1330 PRINT @(V1,V2),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
1340 GOTO 3900
1400 REM " --- Ending Vendor
1410 LET V0$="M",V1$="KCE",V2$=VENDTHRU$,V3$=VENDIMASK$,V4$="Enter A Valid Vendor Number (<F1>=Last/<F3>=Lookup)",V0=6,V1=40,V2=7
1420 GOSUB 7000
1430 ON V3 GOTO 1490,1440,1550,1470,4000
1440 LET VENDTHRU$=""
1450 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
1460 GOTO 1550
1470 CALL "SYC.LV",V$
1480 IF V$="" THEN GOTO 1400
1490 IF V$="" THEN GOTO 1440
1500 IF VENDFROM$<>"" THEN IF V$<VENDFROM$ THEN GOTO 1400
1510 DIM X1$(195)
1520 FIND (APM01_DEV,KEY=N0$+V$,DOM=1400)IOL=APM01A
1530 LET VENDTHRU$=V$,VENDTNAME$(1)=X1$(1,30)
1540 PRINT @(V1,V2),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
1550 GOTO 3900
1600 REM " --- Beginning Receipt Date
1610 LET V0$="D",V1$="6N",V2$=DATEFROM$,V3$="",V4$="",V0=6,V1=40,V2=9
1620 GOSUB 7000
1630 ON V3 GOTO 1650,1640,1670,1600,4000
1640 LET V$=""
1650 IF V$="" THEN PRINT @(V1,V2),"First   ",
1660 LET DATEFROM$=V$
1670 GOTO 3900
1800 REM " --- Ending Receipt Date
1810 LET V0$="D",V1$="6N",V2$=DATETHRU$,V3$="",V4$="",V0=6,V1=40,V2=11
1820 GOSUB 7000
1830 ON V3 GOTO 1850,1840,1880,1850,4000
1840 LET V$=""
1850 IF V$="" THEN PRINT @(V1,V2),"Last    ",
1860 IF V$<>"" THEN IF V$<DATEFROM$ THEN GOTO 1800
1870 LET DATETHRU$=V$
1880 GOTO 3900
3900 REM " --- Next Field
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2599,4000
4000 REM " --- Correct
4010 GOSUB REFRESH
4020 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run print overlay
4170 IF VENDFROM$="" THEN LET VENDFROM$=FILL(6),VENDFNAME$=FILL(30)
4180 IF VENDTHRU$="" THEN LET VENDTHRU$=FILL(6),VENDTNAME$=FILL(30)
4190 RUN "POR.SB"
5000 REM " --- Refresh screen data
5005 REFRESH: 
5020 PRINT 'CF',
5030 IF WHSEFOR$="" THEN PRINT @(40,3),"All",'CL', ELSE PRINT @(40,3),WHSEFOR$," ",WHSEFNAME$,'CL',
5090 IF VENDFROM$="" THEN PRINT @(40,5),"First",'CL', ELSE PRINT @(40,5),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5100 IF VENDTHRU$="" THEN PRINT @(40,7),"Last",'CL', ELSE PRINT @(40,7),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5120 IF DATEFROM$="" THEN PRINT @(40,9),"First   ", ELSE PRINT @(40,9),FNB6$(DATEFROM$)
5130 IF DATETHRU$="" THEN PRINT @(40,11),"Last    ", ELSE PRINT @(40,11),FNB6$(DATETHRU$)
5190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

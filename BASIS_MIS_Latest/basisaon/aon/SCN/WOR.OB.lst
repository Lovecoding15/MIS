0010 REM "WOR - Closed WO Variance Report - Detail Print               "
0020 REM "Program:  WOR.OB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "29May99 WGH: Prevent Error 1 when masks overflow (2030,4055,4350,4570,4840,4940,6080,8400-8445)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0200 REM " --- IOLISTs"
0210 WOE01A: IOLIST A0$,A1$(1)
0220 WOW02A: IOLIST B0$,B[ALL]
0230 IVM01A: IOLIST D0$,D1$(1)
0240 WOREQA: IOLIST W0$,W1$,W[ALL]
0250 WOTRAN: IOLIST W0$,W1$,W[ALL]
0260 WOM10A: IOLIST X0$,X1$
0270 WOM10P: IOLIST H5$
0280 ARM01P: IOLIST *,H5$(1)
0400 REM " --- Check & Adjust Masks
0420 LET M9=M1*11+M2*2+8
0430 IF M9<=120 THEN GOTO 0500
0440 IF POS(","=M1$)=0 THEN GOTO 0460
0450 LET M1$=M1$(1,POS(","=M1$)-1)+M1$(POS(","=M1$)+1),M1=LEN(M1$)
0455 GOTO 0400
0460 IF POS(","=M2$)=0 THEN GOTO 0490
0470 LET M2$=M2$(1,POS(","=M2$)-1)+M2$(POS(","=M2$)+1),M2=LEN(M2$)
0480 GOTO 0400
0490 IF M9>132 THEN IF M1>9 THEN LET M1$(2,1)=","; GOTO 0400 ELSE IF M2>8 THEN LET M2$(2,1)=","; GOTO 0400
0500 REM " --- Init Data"
0520 DIM J$(M9,"-"),H9$(M1*5+M2,"-"),H7$(M1*5+M2),T[10],U[10],A[10]
0530 LET H9$(1,1)="+",H9$(LEN(H9$),1)="+",H8$=H9$
0540 LET H8$(INT(LEN(H8$)/2)-6,12)=" Standards ",H9$(INT(LEN(H9$)/2)-4,8)=" Actual "
0550 LET H6$=H7$,H7$(3)="Operations",H7$(M2+M1*3-7)="Mat'ls",H7$(M2+M1*4-6)="Sub's",H7$(LEN(H7$)-5)="W.O."
0560 LET H6$(3)="Hours",H6$(M2+M1-7)="Direct",H6$(M2+M1*2-7)="Ovrhd",H6$(M2+M1*3-6)="Total",H6$(M2+M1*4-6)="Total",H6$(LEN(H6$)-6)="Total"
0600 REM " --- Additional File Opens "
0605 CALL "SYC.GA",7,1,"","",STATUS
0610 IF STATUS THEN GOTO 9900
0615 IF PRINT_PD<CURRENT_PD THEN GOTO 0640
0620 LET TRAN01_DEV=WOT01_DEV
0625 LET TRAN02_DEV=WOT21_DEV
0630 LET TRAN03_DEV=WOT31_DEV
0635 GOTO 0700
0640 LET TRAN01_DEV=WOT03_DEV
0645 LET TRAN02_DEV=WOT23_DEV
0650 LET TRAN03_DEV=WOT33_DEV
0700 REM " --- Background"
0720 CALL "SYC.NB","Printing:",28,COLUMN
0800 REM " --- Options"
1000 REM " --- Init Read"
1010 LET K$=N0$+REC$+P8$
1020 READ (CHAN_DEV,KEY=N0$+REC$+P8$,DOM=1100)
1030 GOTO 1240
1100 REM " --- Main Read"
1110 DIM A[10]
1120 LET K$=KEY(CHAN_DEV,END=4000)
1130 READ (CHAN_DEV)
1140 IF POS(N0$=K$)<>1 THEN GOTO 4000
1160 IF K$(3,LEN(REC$))<>REC$ THEN GOTO 4000
1180 IF P7$<>"" THEN IF K$(1,LEN(N0$+REC$+P7$))>N0$+REC$+P7$ THEN GOTO 4000
1190 IF P9$="W" THEN GOTO 1240
1200 REM " --- Breaks"
1210 IF K$(4,X8)<>T0$ THEN GOSUB 6000
1220 LET K$=K$(1,2)+K$(X8+4)
1230 IF P9$="C" THEN LET K$=K$(1,2)+K$(13)
1240 PRINT @(COLUMN,11),K$(5),
1250 READ (WOE01_DEV,KEY=K$,DOM=1120)IOL=WOE01A
1260 IF A0$(15,1)<>"C" THEN GOTO 1100
1270 IF A0$(31,3)<P$ THEN GOTO 1100
1280 IF A0$(31,3)>P4$ THEN GOTO 1100
1290 IF A0$(14,1)<>"I" THEN GOTO 1500
1300 DIM D1$(60)
1310 FIND (IVM01_DEV,KEY=N0$+A0$(56),DOM=1500)IOL=IVM01A
1320 LET A1$(1,30)=D1$(1,MIN(DESC[1],30))
1500 REM " --- Calculate Standards
1510 LET WOREQ_DEV=WOE02_DEV
1520 DIM W[7]
1530 READ (WOREQ_DEV,KEY=A0$(1,11),DOM=1540)
1540 PRINT @(50,11),'CL',"Standards",
1550 LET K3$=KEY(WOREQ_DEV,END=1620)
1560 IF K3$(1,11)<>A0$(1,11) THEN GOTO 1620
1570 READ (WOREQ_DEV,KEY=K3$)IOL=WOREQA
1580 ON POS(W0$(12,1)="ABC") GOTO 1550,1590,1600,1610
1590 LET A[0]=A[0]+W[2],A[1]=A[1]+W[2]*W[6],A[9]=A[9]+W[3]-W[2]*W[6]; GOTO 1550
1600 LET A[2]=A[2]+W[3]; GOTO 1550
1610 LET A[3]=A[3]+W[3]; GOTO 1550
1620 IF WOREQ_DEV=WOE02_DEV THEN LET WOREQ_DEV=WOE22_DEV; GOTO 1530
1630 IF WOREQ_DEV=WOE22_DEV THEN LET WOREQ_DEV=WOE32_DEV; GOTO 1530
1700 REM " --- Calculate Actuals
1710 LET WOTRAN_DEV=TRAN01_DEV
1720 DIM W[11]
1730 READ (WOTRAN_DEV,KEY=A0$(1,11),DOM=1740)
1740 PRINT @(50,11),'CL',"Actuals",
1750 LET K4$=KEY(WOTRAN_DEV,END=1830)
1760 IF K4$(1,11)<>A0$(1,11) THEN GOTO 1830
1770 IF P4$<>"" THEN IF K4$(12,3)>P4$ THEN GOTO 1830
1780 READ (WOTRAN_DEV,KEY=K4$)IOL=WOTRAN
1790 ON POS(W0$(15,1)="OMS") GOTO 1750,1800,1810,1820
1800 LET A[4]=A[4]+W[0]+W[6],A[5]=A[5]+W[0]*W[3],A[10]=A[10]+W[2]-W[0]*W[3]
1805 GOTO 1750
1810 LET A[6]=A[6]+W[2]
1815 GOTO 1750
1820 LET A[7]=A[7]+W[2]
1825 GOTO 1750
1830 IF WOTRAN_DEV=TRAN01_DEV THEN LET WOTRAN_DEV=TRAN02_DEV; GOTO 1730
1840 IF WOTRAN_DEV=TRAN02_DEV THEN LET WOTRAN_DEV=TRAN03_DEV; GOTO 1730
1850 REM " --- Calculate Totals, Variance, etc.
1860 LET A0=A[1]+A[2]+A[3]+A[9],A1=A[5]+A[6]+A[7]+A[10],A2=A0-A1
1870 IF A0<>0 THEN LET A3=(A0-A1)*100/A0 ELSE LET A3=0
1880 IF A0$(14,1)="I" THEN LET X$=A0$(56),X$=X$(1,POS("   "=X$+"   ")-1),A1$(1,30)=X$+" "+A1$(1,30)
2000 REM " --- Print"
2010 IF L>L9 THEN GOSUB 5200
2020 IF M9<120 THEN PRINT (7)A0$(5,7),"  ",A0$(12,2), ELSE PRINT (7)A0$(5,7),"  ",A0$(12,2),"  ",A1$(1,30)
2030 PRINT (7)@(132-M9),FNMASK$(A[0],M2$),FNMASK$(A[1],M1$),FNMASK$(A[9],M1$),FNMASK$(A[2],M1$),FNMASK$(A[3],M1$),FNMASK$(A0,M1$),FNMASK$(A[4],M2$),FNMASK$(A[5],M1$),FNMASK$(A[10],M1$),FNMASK$(A[6],M1$),FNMASK$(A[7],M1$),FNMASK$(A1,M1$),FNMASK$(A2,M1$),FNMASK$(A3,"####.00-")
2040 IF M9<120 THEN PRINT (7)@(8),A1$(1,30),
2050 LET L=L+2,L0=L0+1
2060 DIM B0$(20),B[10]
2070 LET B0$(1)=A0$(1,4)+A0$(12,3)
2080 FIND (WOW02_DEV,KEY=B0$(1,6),DOM=2090)IOL=WOW02A
2090 FOR X=0 TO 10
2100 LET T[X]=T[X]+A[X],U[X]=U[X]+A[X],B[X]=B[X]+A[X]
2110 NEXT X
2120 WRITE (WOW02_DEV,KEY=B0$(1,6))IOL=WOW02A
2130 GOTO 1100
4000 REM " --- End"
4005 LET T9$="U"
4010 IF P9$<>"W" THEN GOSUB 6000
4015 IF H=0 THEN GOSUB 5000
4020 IF L+1>L9 THEN GOSUB 5000
4025 LET U0=U[1]+U[2]+U[3]+U[9],U1=U[5]+U[6]+U[7]+U[10],U2=U0-U1
4030 IF U0<>0 THEN LET U3=(U0-U1)*100/U0 ELSE LET U3=0
4035 IF U[8]=0 THEN GOTO 4050
4040 LET U5=U0-(U1-U[8])
4045 IF U0<>0 THEN LET U6=(U0-(U1-U[8]))*100/U0 ELSE LET U6=0
4050 PRINT (7)@(132-M9),J$(1,M9-1),'LF',
4055 PRINT (7)@(0),"Report Total: ",'LF',@(132-M9),FNMASK$(U[0],M2$),FNMASK$(U[1],M1$),FNMASK$(U[9],M1$),FNMASK$(U[2],M1$),FNMASK$(U[3],M1$),FNMASK$(U0,M1$),FNMASK$(U[4],M2$),FNMASK$(U[5],M1$),FNMASK$(U[10],M1$),FNMASK$(U[6],M1$),FNMASK$(U[7],M1$),FNMASK$(U1,M1$),FNMASK$(U2,M1$),FNMASK$(U3,"####.00-")
4060 IF U[8]<>0 THEN PRINT (7)@(132-M1*2-18),"Closed: ",@(132-M1*2-8),-U[8]:M1$,'LF',@(132-M1*2-20),"Net Open: ",@(132-M1*2-8),U1-U[8]:M1$; LET L=L+2
4100 REM " --- Subtotals By Type
4120 DIM C[2,10],T[10],U[10]
4140 GOSUB 5000
4150 PRINT (7)@(0),"Totals By Type: ",'LF'; LET L=2
4160 PRINT @(40,11),'CL',"Totals By Type ",
4180 READ (WOW02_DEV,KEY=N0$,DOM=4200)
4200 LET K5$=KEY(WOW02_DEV,END=4500)
4220 IF POS(N0$=K5$)<>1 THEN GOTO 4500
4240 READ (WOW02_DEV)IOL=WOW02A
4250 DIM X0$(30),X1$(G0,"0"); LET X0$(6)="*** Not On File ***"
4260 FIND (WOM10_DEV,KEY=N0$+"A"+B0$(5,2),DOM=4300)IOL=WOM10A
4300 IF L+2>L9 THEN GOSUB 5000
4320 PRINT (7)B0$(5,2),"  ",X0$(6,20),"  ",FNF$(X1$(1,G0),G5$),"  ",FNF$(X1$(1+G1,G0),G5$)
4330 LET B0=B[1]+B[2]+B[3]+B[9],B1=B[5]+B[6]+B[7]+B[10],B2=B0-B1
4340 IF B0<>0 THEN LET B3=(B0-B1)*100/B0 ELSE LET B3=0
4350 PRINT (7)@(132-M9),FNMASK$(B[0],M2$),FNMASK$(B[1],M1$),FNMASK$(B[9],M1$),FNMASK$(B[2],M1$),FNMASK$(B[3],M1$),FNMASK$(B0,M1$),FNMASK$(B[4],M2$),FNMASK$(B[5],M1$),FNMASK$(B[10],M1$),FNMASK$(B[6],M1$),FNMASK$(B[7],M1$),FNMASK$(B1,M1$),FNMASK$(B2,M1$),FNMASK$(B3,"####.00-")
4360 IF B[8]<>0 THEN PRINT (7)@(132-M1*2-18),"Closed: ",@(132-M1*2-8),-B[8]:M1$,'LF',@(132-M1*2-20),"Net Open: ",@(132-M1*2-8),B1-B[8]:M1$; LET L=L+2
4390 LET L=L+2
4400 LET Y=POS(X0$(26,1)="INR")-1
4420 FOR X=0 TO 10
4440 LET C[Y,X]=C[Y,X]+B[X],T[X]=T[X]+B[X]
4460 NEXT X
4480 GOTO 4200
4500 REM " --- Report Totals
4540 LET T0=T[1]+T[2]+T[3]+T[9],T1=T[5]+T[6]+T[7]+T[10],T2=T0-T1
4550 IF T0<>0 THEN LET T3=(T0-T1)*100/T0 ELSE LET T3=0
4560 PRINT (7)@(132-M9),J$(1,M9-1),'LF',
4570 PRINT (7)@(0),"Report Totals:",'LF',@(132-M9),FNMASK$(T[0],M2$),FNMASK$(T[1],M1$),FNMASK$(T[9],M1$),FNMASK$(T[2],M1$),FNMASK$(T[3],M1$),FNMASK$(T0,M1$),FNMASK$(T[4],M2$),FNMASK$(T[5],M1$),FNMASK$(T[10],M1$),FNMASK$(T[6],M1$),FNMASK$(T[7],M1$),FNMASK$(T1,M1$),FNMASK$(T2,M1$),FNMASK$(T3,"####.00-")
4575 IF T[8]<>0 THEN PRINT (7)@(132-M1*2-18),"Closed: ",@(132-M1*2-8),-T[8]:M1$,'LF',@(132-M1*2-20),"Net Open: ",@(132-M1*2-8),T1-T[8]:M1$; LET L=L+1
4580 LET L=L+3
4700 REM " --- Now Subtotal By Category
4710 IF L+13>L9 THEN GOSUB 5000
4720 PRINT (7)'LF','LF','LF',"Totals By Category: ",'LF'
4800 FOR Y=0 TO 2
4810 IF Y=0 THEN PRINT (7)"Inventory: " ELSE IF Y=1 THEN PRINT (7)"Specials:" ELSE IF Y=2 THEN PRINT (7)"Recurring:"
4820 LET C0=C[Y,1]+C[Y,2]+C[Y,3]+C[Y,9],C1=C[Y,5]+C[Y,6]+C[Y,7]+C[Y,10],C2=C0-C1
4830 IF C0<>0 THEN LET C3=(C0-C1)*100/C0 ELSE LET C3=0
4840 PRINT (7)@(132-M9),FNMASK$(C[Y,0],M2$),FNMASK$(C[Y,1],M1$),FNMASK$(C[Y,9],M1$),FNMASK$(C[Y,2],M1$),FNMASK$(C[Y,3],M1$),FNMASK$(C0,M1$),FNMASK$(C[Y,4],M2$),FNMASK$(C[Y,5],M1$),FNMASK$(C[Y,10],M1$),FNMASK$(C[Y,6],M1$),FNMASK$(C[Y,7],M1$),FNMASK$(C1,M1$),FNMASK$(C2,M1$),FNMASK$(C3,"####.00-")
4845 IF C[Y,8]<>0 THEN PRINT (7)@(132-M1*2-18),"Closed: ",@(132-M1*2-8),-C[Y,8]:M1$,'LF',@(132-M1*2-20),"Net Open: ",@(132-M1*2-8),C1-C[Y,8]:M1$; LET L=L+2
4850 FOR X=0 TO 10
4860 LET U[X]=U[X]+C[Y,X]
4870 NEXT X
4880 NEXT Y
4900 REM " --- Report Totals
4910 LET U0=U[1]+U[2]+U[3]+U[9],U1=U[5]+U[6]+U[7]+U[10],U2=U0-U1
4920 IF U0<>0 THEN LET U3=(U0-U1)*100/U0 ELSE LET U3=0
4930 PRINT (7)@(132-M9),J$(1,M9-1),'LF',
4940 PRINT (7)@(0),"Report Total: ",'LF',@(132-M9),FNMASK$(U[0],M2$),FNMASK$(U[1],M1$),FNMASK$(U[9],M1$),FNMASK$(U[2],M1$),FNMASK$(U[3],M1$),FNMASK$(U0,M1$),FNMASK$(U[4],M2$),FNMASK$(U[5],M1$),FNMASK$(U[10],M1$),FNMASK$(U[6],M1$),FNMASK$(U[7],M1$),FNMASK$(U1,M1$),FNMASK$(U2,M1$),FNMASK$(U3,"####.00-")
4950 IF U[8]<>0 THEN PRINT (7)@(132-M1*2-18),"Closed: ",@(132-M1*2-8),-U[8]:M1$,'LF',@(132-M1*2-20),"Net Open: ",@(132-M1*2-8),U1-U[8]:M1$; LET L=L+1
4990 GOTO 9900
5000 REM " --- Heading"
5010 LET L=0
5020 LET HEADINGS$[2]=H3$,HEADINGS$[3]=H4$
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5040 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)@(132-M9),H8$,H9$
5120 PRINT (7)@(132-M9),H7$,H7$
5130 PRINT (7)"WO #",@(7),"Type",@(132-M9),H6$,H6$,@(116),"Variance",@(130),"%"
5140 PRINT (7)""
5190 RETURN 
5200 REM " --- Sub Heading
5210 IF L>L9 THEN GOSUB 5000
5220 ON POS(P9$="TC") GOTO 5290,5240,5270
5240 PRINT (7)'LF',T0$,"  ",H5$,'LF'
5250 LET L=L+3
5260 GOTO 5290
5270 PRINT (7)FNF$(T0$(1,C0),C1$)," ",H5$
5280 LET L=L+1
5290 RETURN 
6000 REM " --- First Sub Total
6010 IF T0$="" THEN GOTO 6100
6020 IF L0<=1 THEN GOTO 6100
6030 IF L+2>L9 THEN GOSUB 5200
6040 LET T0=T[1]+T[2]+T[3]+T[9],T1=T[5]+T[6]+T[7]+T[10],T2=T0-T1
6050 IF T0<>0 THEN LET T3=(T0-T1)*100/T0 ELSE LET T3=0
6060 PRINT (7)@(132-M9),J$(1,M9-1),'LF',
6070 IF P9$="T" THEN PRINT (7)@(0),"Type ",T0$,"Totals: ",
6075 IF P9$="C" THEN PRINT (7)@(0),"Customer ",FNF$(T0$(1,C0),C1$)," Totals:",
6077 IF P9$="B" THEN PRINT (7)@(0),"Bill Number Totals:",
6080 PRINT (7)'LF',@(132-M9),FNMASK$(T[0],M2$),FNMASK$(T[1],M1$),FNMASK$(T[9],M1$),FNMASK$(T[2],M1$),FNMASK$(T[3],M1$),FNMASK$(T0,M1$),FNMASK$(T[4],M2$),FNMASK$(T[5],M1$),FNMASK$(T[10],M1$),FNMASK$(T[6],M1$),FNMASK$(T[7],M1$),FNMASK$(T1,M1$),FNMASK$(T2,M1$),FNMASK$(T3,"####.00-"),'LF'
6090 LET L=L+3
6100 IF T9$<>"" THEN RETURN 
6120 DIM T[10]
6140 LET T0$=K$(4,X8),L0=0
6150 ON POS(P9$="TC") GOTO 6290,6160,6200
6160 DIM H5$(20); LET H5$(1)="*** Not On File ***"
6170 FIND (WOM10_DEV,KEY=N0$+"A"+T0$,DOM=6190)IOL=WOM10P
6180 LET H5$=H5$(6,20)
6185 GOSUB 5200
6190 GOTO 6290
6200 REM " --- Customer
6220 DIM H5$(30); LET H5$(1)="*** Not On File ***"
6240 FIND (ARM01_DEV,KEY=N0$+T0$,DOM=6120)IOL=ARM01P
6260 GOSUB 5200
6290 RETURN 
6500 IF T9$<>"" THEN RETURN 
6510 IF L0<=1 THEN GOTO 6520
6512 PRINT (7)""
6514 LET L=L+1
6520 DIM R[1]
6530 LET T2$=A0$(11,11)
6680 GOSUB 5200
6700 IF L>L9 THEN GOSUB 5200
6720 PRINT (7)@(32),H5$,
6790 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
8400 REM " --- Numeric Mask with Overflow"
8410 DEF FNMASK$(Q,Q1$)
8420 LET Q2$=STR(Q:Q1$)
8430 IF LEN(Q2$)>LEN(Q1$) THEN LET Q2$=FILL(LEN(Q1$),"*")
8440 RETURN Q2$
8445 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

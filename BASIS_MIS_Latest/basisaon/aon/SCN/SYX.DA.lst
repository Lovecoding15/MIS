0010 REM "SYX - Rebuild Process Cross References"
0020 REM "Program SYX.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.6 - 15Aug99 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "14Apr98 JWK: Was checking wrong parameter record (420)
0032 REM "03Dec98 WGH: Don't check for parameter record (420)
0033 REM "02May00 JAL: Prevent Error 47/1140 when file is in use (860,880-890)"
0040 REM 
0060 BEGIN
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYM-19",FILES$[2]="SYM-49",FILES$[3]="SYS-01"
0140 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYM19_DEV=CHANNELS[1],SYM49_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLISTS"
0210 SYM29A: IOLIST SYM29_K$
0220 SYM49A: IOLIST *,A1$(1)
0230 SYM59A: IOLIST SYM59_K$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters "
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="SY",N4$=F5$
0500 REM " --- Initialize Data"
0510 DIM A1$(50)
0800 REM " --- Options
0810 LET V4$="Do You Wish To Rebuild The Process Cross Reference Files"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0850 REM " --- Clear And Lock SYM-29 & SYM-59"
0860 LET FILES$[4]="SYM-29",FILES$[5]="SYM-59",OPTIONS$[4]="C",OPTIONS$[5]="C"
0865 CALL "SYC.DA",1,4,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0870 IF STATUS THEN GOTO 9900
0875 LET SYM29_DEV=CHANNELS[4],SYM59_DEV=CHANNELS[5]
0950 REM " --- Initialize SYM-19 "
0960 CALL "SYC.NB","Updating",19,COLUMN
0970 READ (SYM19_DEV,KEY="",DOM=1000)
1000 REM " --- Read Process Definition Records"
1020 LET K$=KEY(SYM19_DEV,END=1900)
1040 READ (SYM19_DEV)
1060 PRINT @(COLUMN,11),K$(1,10)," ",K$(11,8),
1100 REM " --- Write SYM-29 Cross Reference"
1120 LET SYM29_K$=K$(11,8)+K$(1,10)
1140 WRITE (SYM29_DEV,KEY=SYM29_K$)IOL=SYM29A
1190 GOTO 1000
1950 REM " --- Initialize SYM-49 "
1960 PRINT @(0,10),'CL',@(0,11),'CL',@(0,12),'CL',
1970 CALL "SYC.NB","Updating",3,COLUMN
1980 READ (SYM49_DEV,KEY="",DOM=2000)
2000 REM " --- Read Process Batch Control Records"
2020 LET K$=KEY(SYM49_DEV,END=4000)
2040 READ (SYM49_DEV)IOL=SYM49A
2060 PRINT @(COLUMN,11),K$,
2100 REM " --- Write SYM-59 Cross Reference"
2120 LET SYM59_K$=A1$(1,12)+K$
2140 WRITE (SYM59_DEV,KEY=SYM59_K$)IOL=SYM59A
2190 GOTO 2000
4000 REM " --- Done"
4090 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
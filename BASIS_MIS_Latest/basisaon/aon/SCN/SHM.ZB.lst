0010 REM "SHM - Zone Maintenance"
0020 REM "Program SHM.ZB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 07Feb98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Feb97 CAJ: Modified for 1997 SpeedShip Update"
0032 REM "19Dec97 JAL: Prevent overwriting of low prefix (5020)"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 SHM24A: IOLIST A0$(1),A[ALL]
0220 SHM10D: IOLIST *,I8$(1)
0400 REM " --- Parameters"
0410 DIM I8$(30)
0420 FIND (SHM10_DEV,KEY=N0$+"D"+C$,DOM=0430)IOL=SHM10D
0430 LET I8$=FNP$(I8$)
0500 REM " --- Initializations"
0510 DIM A[3]
0540 LET OFFSET=9,R=5,H9$="Carrier: "+C$+" "+FNP$(I8$)
0550 IF X1$="" THEN LET OFFSET=8
0560 LET H0$="Zone Chart: "+F$+" "+FNP$(D1$)
0700 REM " --- Background"
0710 PRINT @(0,3),'CE','SB',@(31,3),"Carrier: ",C$," ",I8$,@(28,4),"Zone Chart: ",F$," ",D1$,'SF',
0740 FOR X=1 TO LEN(D2$) STEP 24
0750 LET H0$=FNP$(D2$(X,24)),R=R+1
0760 IF H0$<>"" THEN PRINT 'SB',@(38-LEN(H0$),R),H0$,":",'SF',
0770 IF X=1 THEN PRINT 'SB',@(40,R),H1$,'SF',
0790 NEXT X
1000 REM " --- Low Zip Prefix"
1005 PRINT @(0,0),'CF',
1010 LET V0$="S",V1$="CE",V2$="",V3$="",V0=X[1],V1=40,V2=7,I0=0
1015 LET V4$="Enter The Beginning "+REGION$+" From The Zone Chart (<Enter>=Next Record)"
1020 GOSUB 7000
1030 ON V3 GOTO 1035,1000,1000,1000,4500
1040 LET A0$(X[0],X[1])=V$
1050 IF FNP$(V$)="" THEN GOTO 1100
1055 REM " --- High Zip Prefix"
1057 IF X1$="" THEN GOTO 1085
1060 LET V0$="S",V1$="CE",V2$="",V3$="",V0=X[3],V1=40,V2=8,I0=1
1065 LET V4$="Enter The Ending "+REGION$+" From The Zone Chart (<Enter>=Next Record)"
1070 GOSUB 7000
1075 ON V3 GOTO 1080,1060,1000,1060,4500
1080 LET A0$(X[2],X[3])=V$
1085 IF FNP$(V$)="" THEN READ (SHM24_DEV,KEY=T$+A0$(X[0],X[1]),DOM=1100)
1090 GOTO 1200
1100 REM " --- Get next record"
1110 LET K$=KEY(SHM24_DEV,END=1190)
1115 READ (SHM24_DEV)
1120 IF POS(T$=K$)<>1 THEN GOTO 1150
1130 LET A0$(1)=K$
1140 GOTO 1200
1150 READ (SHM24_DEV,KEY=A0$(1,T),DOM=1190)
1190 GOTO 1000
1200 REM " --- Retrieve record"
1210 READ (SHM24_DEV,KEY=A0$(1,K9),DOM=1400)IOL=SHM24A
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1490,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record"
1510 DIM A[3]
1700 REM " --- Zone(s)"
1703 IF Z=0 THEN GOTO 4000
1705 FOR X=0 TO Z-1
1710 LET V0$="N",V1$="C",V2$=STR(A[X]),V3$=FILL(W,"0"),V0=W,V1=40
1715 LET V4$="Enter A Zone Number For This "+REGION$,V2=X+OFFSET
1720 GOSUB 7000
1730 ON V3 GOTO 1740,1700,1750,1700,1780
1740 LET A[X]=V
1745 GOTO 1790
1750 LET X=X-1
1755 IF X<0 THEN EXITTO 1055
1760 GOTO 1710
1780 EXITTO 4000
1790 NEXT X
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CR",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4035 IF V3=12 THEN LET V$="DELETE",V3=0
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1700
4100 REM " --- Write record"
4110 WRITE (SHM24_DEV,KEY=A0$)IOL=SHM24A
4190 GOTO 1000
4200 REM " --- Remove record"
4210 REMOVE (SHM24_DEV,KEY=A0$,DOM=4220)
4290 GOTO 1000
4500 REM " --- Print"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4520 ON V3 GOTO 4590,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4900,9900
4900 REM " --- Run report overlay"
4910 LET V5$=T$
4990 RUN "SHR.ZA"
5000 REM " --- Display record"
5010 PRINT @(40,7),A0$(X[0],X[1]),
5020 IF X1$<>"" THEN PRINT @(40,8),A0$(X[2],X[3]),
5030 IF Z=0 THEN GOTO 5090
5040 FOR X=0 TO Z-1
5050 PRINT @(40,X+OFFSET),A[X]:"00",
5060 NEXT X
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "IVU - Inventory Transfer Update"
0020 REM "Program IVU.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "9Jun97 JWK: Close Printer, use Correct TO Whse (100,110,1710)
0040 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 PRECISION P[2]
0100 REM " --- Close Printer Before Asking Update Question
0110 CLOSE (7,ERR=0111)
0200 REM " --- IOLIST's"
0210 IVE02A: IOLIST A0$,A1$,A[ALL]
0220 IVM01A: IOLIST B0$,B1$(1),B2$,B3$,B4$,B5$,B6$(1)
0230 IVM02A: IOLIST C0$,C1$,C2$
0240 IVM10C: IOLIST X0$(1)
0290 ARM10D: IOLIST *,X$(1)
0500 REM " --- Initializations"
0505 PRECISION P[2]
0510 DIM FILES[44],PARAMS[10],PARAMS$[10],ITEM$[3]
0550 LET FILES$[11]="IVM-12",FILES$[12]="IVT-04"
0590 IF A9$<>"Y" THEN LET GL$="N"
0600 REM " --- Additional File Opens"
0620 IF GL$="Y" THEN LET FILES$[13]="GLT-04",FILES$[14]="GLT-05"
0630 IF LS$="Y" THEN LET FILES$[15]="IVM-07",FILES$[16]="IVM-08",FILES$[17]="IVM-17",FILES$[18]="IVT-01"
0640 CALL "SYC.DA",1,11,18,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0650 IF STATUS>0 THEN GOTO 9900
0660 LET IVM12_DEV=CHANNELS[11],IVT04_DEV=CHANNELS[12],GLT04_DEV=CHANNELS[13]
0670 LET GLT05_DEV=CHANNELS[14],IVM07_DEV=CHANNELS[15],IVM08_DEV=CHANNELS[16]
0680 LET IVM17_DEV=CHANNELS[17],IVT01_DEV=CHANNELS[18]
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0800 REM " --- Run Update?"
0810 LET V4$="Are You Ready To Update The "+N3$
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0850 REM " --- Initialize update arrays"
0855 LET FILES[0]=SYS01_DEV,FILES[1]=IVM01_DEV,FILES[2]=IVM02_DEV
0865 LET FILES[7]=IVM07_DEV,FILES[8]=IVM08_DEV,FILES[12]=IVM12_DEV
0870 LET FILES[17]=IVM17_DEV,FILES[41]=IVT01_DEV,FILES[44]=IVT04_DEV
0880 LET PARAMS[0]=G[0],PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3)
0890 LET PARAMS$[2]=P2$,PARAMS$[3]=P3$,PARAMS$[4]=P4$,FILES[4]=IVM04_DEV
0910 CALL "SYC.NB","Updating",4+P[0]+P[1],COLUMN
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
0970 REM " --- Position file"
0990 READ (IVE02_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next IVE-02 record"
1010 LET K$=KEY(IVE02_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 READ (IVE02_DEV)IOL=IVE02A
1040 PRINT @(COLUMN,11),A0$(3,2)," ",A0$(12,P[0])," ",
1050 IF LS$="Y" THEN PRINT A0$(12+P[0],P[1]),
1090 IF A[0]=0 THEN GOTO 3900
1200 REM " --- Retrieve item"
1210 DIM B0$(22),B1$(60),B6$(G[4]*2)
1220 LET B0$(1)=N0$+A0$(12,20)
1230 FIND (IVM01_DEV,KEY=B0$,DOM=3900)IOL=IVM01A
1400 REM " --- Inventory initializations"
1410 DIM ITEM$[3],REFS$[20],REFS[20]
1420 LET ITEM$[0]=A0$(1,2),ITEM$[2]=A0$(12,20)
1430 IF LS$="Y" THEN LET ITEM$[3]=A0$(32,20)
1440 LET REFS$[0]=A0$(5,3),REFS$[2]=A1$(1,6),ACCOUNT$="",REFS[1]=A[2]
1500 REM " --- GL initializations"
1510 IF GL$<>"Y" THEN GOTO 1600
1520 LET WHEN$=A0$(5,3),MEMO$=A0$(12,P[0])+" "+FNITEM$(B1$,P[3],P[4],P[5])
1530 LET REF1$="Xfr "+A1$(1,6)
1600 REM " --- From warehouse GL update"
1610 IF GL$<>"Y" THEN GOTO 1700
1620 DIM C0$(24)
1630 LET C0$(1)=A0$(1,4)+A0$(12,20)
1640 FIND (IVM02_DEV,KEY=C0$,DOM=1800)IOL=IVM02A
1650 LET A[1]=A[0]*A[2],AMOUNT=-A[1],UNITS=-A[0]
1660 LET REF2$="Fr Whse "+A0$(3,2),REF3$="To Whse "+A0$(10,2)
1670 GOSUB 6000
1700 REM " --- From warehouse inventory update"
1710 LET ITEM$[1]=A0$(3,2),REFS$[4]=A0$(10,2),REFS$[7]=ACCOUNT$
1720 LET ACTION$="TO",REFS[0]=-A[0]
1750 GOSUB 6200
1800 REM " --- To warehouse GL update"
1810 IF GL$<>"Y" THEN GOTO 1900
1820 DIM C0$(24)
1830 LET C0$(1)=A0$(8,4)+A0$(12,20)
1840 FIND (IVM02_DEV,KEY=C0$,DOM=1900)IOL=IVM02A
1850 LET AMOUNT=A[1],UNITS=A[0]
1860 LET REF3$="Fr Whse "+A0$(3,2),REF2$="To Whse "+A0$(10,2)
1870 GOSUB 6000
1900 REM " --- To warehouse inventory update"
1910 LET ITEM$[1]=A0$(10,2),REFS$[4]=A0$(3,2),REFS$[7]=ACCOUNT$
1920 LET ACTION$="TI",REFS[0]=A[0]
1950 GOSUB 6200
3900 REM " --- Remove record"
3910 REMOVE (IVE02_DEV,KEY=K$,DOM=3920)
3990 GOTO 1000
4000 REM " --- All done"
4010 IF GL$="Y" THEN CALL "GLC.CA",STATUS
4020 CALL "SYC.BB",STATUS
4090 GOTO 9900
6000 REM " --- Update GL"
6010 DIM X$(8*G[4],"0")
6020 LET ACCOUNT$=B6$(1,G[4])
6030 FIND (ARM10_DEV,KEY=N0$+"D"+C1$(13,2),DOM=6050)IOL=ARM10D
6040 LET ACCOUNT$=X$(6*G[4]+1,G[4])
6050 PRECISION 2
6060 LET AMOUNT=AMOUNT*1
6070 PRECISION P[2]
6080 GOSUB GLPOST
6090 RETURN 
6200 REM " --- Update Inventory"
6210 CALL "IVC.UA",ACTION$,FILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEM$[ALL],REFS$[ALL],REFS[ALL],STATUS
6290 RETURN 
6900 REM " --- Standard G/L Posting Routine"
6910 GLPOST: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,WHEN$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions"
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

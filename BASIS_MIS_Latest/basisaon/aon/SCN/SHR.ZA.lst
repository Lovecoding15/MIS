0010 REM "SHR - Zone Chart Listing"
0020 REM "Program SHR.ZA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Feb97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Feb97 CAJ: Modified for 1997 SpeedShip Update"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SHM-10",FILES$[3]=F$
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SHM10_DEV=CHANNELS[2],SHM24_DEV=CHANNELS[3]
0170 CLOSE (7,ERR=0180)
0180 CALL "SYC.GA",7,1,"","",STATUS
0190 IF STATUS THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 SHM24A: IOLIST A0$,A[ALL]
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0280 SHM10D: IOLIST *,I8$
0290 SHM10F: IOLIST *,C1$
0300 SHM10O: IOLIST W0$,W1$
0400 REM " --- Parameters"
0405 DIM C1$(192)
0415 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="SH",N4$=F5$,W0$="",W1$=""
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)
0440 FIND (SHM10_DEV,KEY=N0$+"D"+C$,DOM=9900)IOL=SHM10D
0450 FIND (SHM10_DEV,KEY=N0$+"F"+W$,DOM=0500)IOL=SHM10F
0460 FIND (SHM10_DEV,KEY=N0$+"O"+F$+W$,DOM=0500)IOL=SHM10O
0500 REM " --- Initializations"
0510 DIM J$(80,"-"),A[3],O[5],HEADINGS$[8]
0550 LET H0=80,H=0,L9=55,L=L9+1,WHEN$=F0$(7,3),CLOCK$="",W=X[1]
0560 IF W>7 THEN LET W=7
0565 LET HEADINGS$[0]=N1$,HEADINGS$[1]="Carrier: "+C$+" "+FNP$(I8$(1,30))
0570 LET HEADINGS$[2]="Zone Chart: "+F$+" "+FNP$(D1$),HEADINGS=3
0575 IF LEN(D2$)>0 THEN LET HEADINGS$[3]=FNP$(D2$(1,24))+": "+FNP$(H1$),HEADINGS=4
0580 IF H5$<>"" THEN LET HEADINGS$[4]=H5$,HEADINGS=5
0600 REM " --- Initialize print positions"
0610 LET O[0]=0,O[1]=O[0]+LEN(X0$)+2,O[2]=O[1]+LEN(X1$)+2
0620 IF X[3]>LEN(X1$) THEN LET O[2]=O[1]+X[3]+2
0630 IF X[3]>0 THEN GOTO 0690
0650 LET O[2]=O[1]
0660 IF X[1]>LEN(X0$) THEN LET O[2]=O[0]+X[1]+2
0690 LET O[3]=O[2]+LEN(X2$)+2,O[4]=O[3]+LEN(X3$)+2,O[5]=O[4]+LEN(X4$)+2
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",W,COLUMN
0800 REM " --- Remaining headings"
0810 DIM X$(H0)
0820 LET X$(O[0]+1)=X0$
0825 IF X[2]>0 THEN LET X$(O[1]+1)=X1$
0830 LET X$(O[2]+1)=X2$
0835 IF Z>1 THEN LET X$(O[3]+1)=X3$
0840 IF Z>2 THEN LET X$(O[4]+1)=X4$
0845 IF Z>3 THEN LET X$(O[5]+1)=X5$
0850 LET HEADINGS=HEADINGS+1,HEADINGS$[HEADINGS]=J$
0855 LET HEADINGS=HEADINGS+1,HEADINGS$[HEADINGS]=X$
0860 LET HEADINGS=HEADINGS+1,HEADINGS$[HEADINGS]=J$
0900 REM " --- Position file"
0910 LET V5=LEN(V5$),A0$(1,V5)=V5$
0990 READ (SHM24_DEV,KEY=A0$(1,V5),DOM=1000)
1000 REM " --- Top of report loop"
1010 LET K$=KEY(SHM24_DEV,END=4000)
1020 IF POS(A0$(1,V5)=K$)<>1 THEN GOTO 4000
1040 READ (SHM24_DEV)IOL=SHM24A
1050 PRINT @(COLUMN,11),A0$(X[0],W),
1500 REM " --- Print record for current set of rates"
1510 IF L>L9 THEN GOSUB 5000
1520 PRINT (7)@(O[0]),A0$(X[0],X[1]),
1530 IF X[2]>0 THEN PRINT (7)@(O[1]),A0$(X[2],X[3]),
1540 IF Z>0 THEN PRINT (7)@(O[2]-1),A[0]:"##0",
1550 IF Z>1 THEN PRINT (7)@(O[3]-1),A[1]:"##0",
1560 IF Z>2 THEN PRINT (7)@(O[4]-1),A[2]:"##0",
1570 IF Z>3 THEN PRINT (7)@(O[5]-1),A[3]:"##0",
1580 PRINT (7)""
1590 LET L=L+1
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- Report completed"
4010 CLOSE (7,ERR=4020)
4020 CLOSE (SHM24_DEV,ERR=4030)
4030 CLOSE (SHM10_DEV,ERR=4040)
4040 CLOSE (SYS01_DEV,ERR=4050)
4090 RUN O0$,ERR=9900
5000 REM " --- Report Heading"
5010 LET L=10
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5090 RETURN 
8000 REM " --- Function Definitions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

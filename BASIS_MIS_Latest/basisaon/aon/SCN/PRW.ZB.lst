0010 REM "PRR - Federal Form W-2 (Printing)"
0020 REM "Program PRW.ZB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 15Dec97 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "11Mar97 WGH: Print company zip code correctly (560-590,710,5315,5330,8085)"
0032 REM "03Dec97 WGH: Use correct print positions for Boxes a thru f (5160,5170,5180,5190,5205,5230,5238,5267,5280,5315,5330)"
0033 REM "04Dec97 WGH: Allow codes Q, R, S and T for Box 13 (550)"
0034 REM "29Dec97 WGH: Include Box 13 code S in Subtotal and mark Deferred Compensation box (505,530,550,960,1307,2530,2555,4205,5037,5435,5640,6460)"
0035 REM "01Dec98 WGH: Don't use Household Employee and Subtotal in Box 15 (520,910,920,5402,4220-4224)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS=1 THEN GOTO 9900
0200 REM " --- IOLists"
0210 PRM01A: IOLIST A0$,A1$
0220 PRM03W2: IOLIST K0$
0230 PRM10C2: IOLIST X2$
0240 PRM10C3: IOLIST X3$
0250 PRM10W: IOLIST H0$
0260 PRW06A: IOLIST B0$,B1
0500 REM " --- Init Data"
0505 LET M4$="#######.00",W$="ABCDEFGHJKLMNPQRST",LEN_W=LEN(W$)
0510 DIM T[15],U[15]
0520 DIM A[13],B[9],C[9],D[6],F[LEN_W-1],H[5],K[5],M[5]
0530 DIM B1[5],C1[5],F1[LEN_W-1],H1[5],K1[5],M1[5]
0540 LET H9=NUM(P8$),L=0,L1=0,L9=40,L9$="Y",Q0=0
0560 LET R$=R0$(79,POS("   "=R0$(79,22)+"   ")-1)+", "+R0$(101,2)+" ",ZIP$=FNP$(FNZ$(R0$(103)))
0565 IF LEN(R$)+LEN(ZIP$)<=33 THEN GOTO 0580
0570 IF LEN(ZIP$)=10 AND POS("-"=ZIP$)=6 THEN LET ZIP$=ZIP$(1,5); REM "USA 9-digit zip
0575 IF LEN(ZIP$)=7 AND POS(" "=ZIP$)=4 THEN LET ZIP$=ZIP$(1,3)+ZIP$(5,3); REM "Canadian zip
0580 LET R$=R$+ZIP$
0590 LET HEADINGS=0; DIM HEADINGS$[HEADINGS]
0700 REM " --- Create Test Print Data"
0705 DIM A0$(12,"9"),X1$(150,"X"),A1$(240),H1$(3),H2$(3),H3$(3)
0710 LET ZIP$=FILL(P[3],"9")+FILL(9),X1$(103,9)=ZIP$(1,9),X1$(136,9)="999999999",A=0,B=0,C=0,D=0,F=0,H=0,K=0,M=0,N=0,R3$=R2$,ZIP$=FNZ$(ZIP$)
0715 READ (PRM10_DEV,KEY=N0$+"W",DOM=0720,END=8500)
0720 LET K$=KEY(PRM10_DEV,END=0800)
0725 IF K$(1,3)<>N0$+"W" THEN GOTO 0800
0730 READ (PRM10_DEV,KEY=K$)IOL=PRM10W
0735 IF K$(4,2)>="a " AND K$(4,2)<="f " THEN LET A[A]=NUM(H0$(87,2)); LET A=A+1
0740 IF K$(4,2)>="01" AND K$(4,2)<="12" THEN IF FPT(NUM(K$(4,2))/2)=0 THEN LET C[C]=NUM(H0$(87,2)); LET C=C+1 ELSE LET B[B]=NUM(H0$(87,2)); LET B=B+1
0750 IF K$(4,2)="13" THEN LET F[F]=NUM(H0$(87,2)),F=F+1
0755 IF K$(4,2)="14" AND K$(6,1)<>" " THEN LET H[H]=NUM(H0$(87,2)),H=H+1
0760 IF K$(4,2)="15" THEN LET D[D]=NUM(H0$(87,2)); LET D=D+1
0770 IF K$(4,2)="16" THEN LET K[0]=NUM(H0$(87,2))
0775 IF K$(4,2)="17" THEN LET K[1]=NUM(H0$(87,2))
0780 IF K$(4,2)="18" THEN LET K[2]=NUM(H0$(87,2))
0785 IF K$(4,2)="19" THEN LET M[0]=NUM(H0$(87,2))
0790 IF K$(4,2)="20" THEN LET M[1]=NUM(H0$(87,2))
0795 IF K$(4,2)="21" THEN LET M[2]=NUM(H0$(87,2))
0799 GOTO 0720
0800 REM " --- Test Pattern Query"
0820 CALL "SYC.YN",1,"Do You Wish To Print A Test Pattern",2,V$,V3
0840 IF V3=4 THEN GOTO 9900
0860 ON POS(V$="YN") GOTO 0800,0900,0990
0900 REM " --- Print Test Pattern"
0910 LET C0$="X",C1$="X",C2$="X",C3$="X",C4$="",C5$="",C6$="X",C7$="X"
0915 FOR X=0 TO 5; LET B1[X]=99999999.99,C1[X]=99999999.99,K1[X]=999.99,M1[X]=999.99; NEXT X
0920 FOR X=0 TO 2; LET F1[X]=999.99; NEXT X
0930 LET H1[0]=999.99,X2$="XX"
0940 GOSUB 5005
0950 LET C0$="",C1$="",C2$="",C3$="",C4$="",C5$="",C6$="",C7$=""
0960 DIM F1[LEN_W-1],B1[5],C1[5],K1[5],M1[5]
0970 LET H1[0]=0
0980 GOTO 0700
0990 PRINT (7)'FF',
0995 CALL "SYC.NB","Printing",M0+33,COLUMN
1000 REM " --- Init Read"
1020 READ (PRM03_DEV2,KEY=N0$+"A",DOM=1030)
1060 IF R9$="Y" THEN LET L9$="N"
1100 REM " --- Main Read"
1105 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS; REM "Check printing interrupt
1110 IF STATUS>0 THEN GOTO 9900
1115 IF L>L9 THEN GOSUB 5400
1120 LET K$=KEY(PRM03_DEV2,END=4000)
1140 IF K$(1,3)<>N0$+"A" THEN GOTO 4000
1150 READ (PRM03_DEV2)
1160 FIND (PRM01_DEV,KEY=N0$+K$(20,9),DOM=1100)IOL=PRM01A
1180 PRINT @(COLUMN,11),FILL(M0+33),@(COLUMN,11),FNF$(A0$(3,P[0]),M0$)," ",FNP$(A1$(1,16)),", ",FNP$(A1$(17,14)),
1190 LET STATE_CODE$="  ",LOCAL_CODE$="  "
1200 REM " --- Check if selected state"
1210 IF T2$="  " AND T8$="" THEN GOTO 1290; REM "All states selected
1220 READ (PRW06_DEV,KEY=A0$+"17",DOM=1230)
1230 LET K2$=""; LET K2$=KEY(PRW06_DEV,END=1240)
1240 IF K2$="" THEN IF T8$="NONE" THEN GOTO 1290 ELSE GOTO 1100; REM "No state records found
1260 IF T8$="NONE" THEN IF K2$(1,13)<>A0$+"17" THEN GOTO 1290 ELSE GOTO 1100; REM "No state records found
1270 IF K2$(1,13)<>A0$+"17" THEN GOTO 1100; REM "No match
1280 IF K2$(15,2)<>T2$ THEN READ (PRW06_DEV,END=1100); GOTO 1230; REM "Try again
1290 IF T5$="  " AND T9$="" THEN GOTO 1300 ELSE GOSUB 6200; IF L$="NO" THEN GOTO 1100; REM "Check on local
1300 REM " --- Get Employee $ Amts From Employee Box Detail PRW-06"
1302 READ (PRW06_DEV,KEY=A0$+"01",DOM=1305)
1305 DIM H1$(3),H2$(3),H3$(3)
1307 DIM B1[5],C1[5],F1[LEN_W-1],H1[5],K1[5],M1[5]
1310 LET D1=0,F1=0,H1=0,K1=0,M1=0,R3$=R2$
1320 LET K2$=KEY(PRW06_DEV,END=2200)
1340 IF K2$(1,11)<>A0$ THEN GOTO 2200
1360 READ (PRW06_DEV,KEY=K2$)IOL=PRW06A
1400 REM " --- Set Boxes"
1410 IF B1<0 THEN LET B1=0
1420 IF B0$(12,2)="01" THEN IF B1=0 THEN GOTO 1100
1430 IF B0$(12,2)<"13" THEN IF FPT(NUM(B0$(12,2))/2)=0 THEN LET C1[NUM(B0$(12,2))/2-1]=B1; GOTO 1320 ELSE LET B1[(NUM(B0$(12,2))+1)/2-1]=B1; GOTO 1320
1500 REM " --- Switch For Boxes >= 13"
1510 ON POS(B0$(12,2)="1314",2) GOSUB 1700,1520,1600
1515 GOTO 1320
1520 REM " --- Box 13 Loop"
1525 LET F1=POS(B0$(14,1)=W$)-1
1530 LET F1[F1]=B1
1535 RETURN 
1600 REM " --- Box 14 Loop"
1610 LET H1=NUM(B0$(14,1)); IF H1>0 THEN LET H1=H1-1
1630 LET H1[H1]=B1
1650 READ (PRM10_DEV,KEY=B0$(1,2)+"W"+B0$(12,3))IOL=PRM10W
1652 IF POS("-"=H0$(47,40))=0 THEN GOTO 1680
1655 ON H1 GOTO 1660,1665,1670
1660 LET H1$=H0$(47,POS("-"=H0$(47))-1),H1$=H1$+"   ",H1$=H1$(1,3); GOTO 1680
1665 LET H2$=H0$(47,POS("-"=H0$(47))-1),H2$=H2$+"   ",H2$=H2$(1,3); GOTO 1680
1670 LET H3$=H0$(47,POS("-"=H0$(47))-1),H3$=H3$+"   ",H3$=H3$(1,3)
1680 LET H1=H1+1
1690 RETURN 
1700 REM " --- Boxes 16 Thru 18 - State Taxes"
1705 IF B0$(12,2)<>"17" THEN GOTO 1740
1710 IF T2$="  " THEN IF T8$="NONE" THEN RETURN ELSE GOTO 1716
1715 IF B0$(15,2)<>T2$ THEN RETURN 
1716 DIM X2$(90)
1717 READ (PRM10_DEV,KEY=N0$+"C"+B0$(15,2),DOM=1719)IOL=PRM10C2
1718 LET R3$=X2$(45,15)
1719 LET X2$=B0$(15,2),STATE_CODE$=X2$
1720 LET K1[1]=B1
1730 RETURN 
1740 REM " --- State Wage Basis"
1745 IF B0$(12,2)<>"18" THEN GOTO 1800
1750 IF T2$="  " THEN IF T8$="NONE" THEN RETURN ELSE GOTO 1765
1760 IF B0$(15,2)<>T2$ THEN RETURN 
1780 LET K1[2]=B1
1790 RETURN 
1800 REM " --- Boxes 19 Thru 21 - Local Taxes"
1805 IF B0$(12,2)<>"20" THEN GOTO 1840
1807 IF T2$<>"  " AND B0$(15,2)<>"  " AND B0$(15,2)<>T2$ THEN GOTO 1830
1810 IF T5$="  " THEN IF T9$="NONE" THEN RETURN ELSE GOTO 1816
1815 IF B0$(17,2)<>T5$ THEN RETURN 
1816 DIM X3$(90); LET X3$(34)="Missing"
1817 READ (PRM10_DEV,KEY=N0$+"C"+B0$(17,2),DOM=1818)IOL=PRM10C3
1818 LET X3$=X3$(34,10),LOCAL_CODE$=B0$(17,2)
1820 LET M1[1]=B1
1830 RETURN 
1840 REM " --- Local Wage Basis"
1845 IF B0$(12,2)<>"21" THEN GOTO 1890
1847 IF T2$<>"  " AND B0$(15,2)<>"  " AND B0$(15,2)<>T2$ THEN GOTO 1890
1850 IF T5$="  " THEN IF T9$="NONE" THEN RETURN ELSE GOTO 1865
1860 IF B0$(17,2)<>T5$ THEN RETURN 
1880 LET M1[2]=B1
1890 RETURN 
2200 REM " --- Employee Info"
2210 LET C0$="",C1$="",C2$="",C3$="",C4$="",C5$="",C6$="",C7$="",C8$="",C9$=""
2220 LET X1$=A1$,V0$=A1$(1,16),V1$=A1$(17,14),V2$="",W1$=""
2240 GOSUB 6000
2260 LET X1$(1,30)=V$
2280 IF POS(" "<>X1$(79,24))=0 THEN LET X1$(79,33)=X1$(103,P[3])
2400 REM " --- Print"
2410 GOSUB 6300
2420 IF X0$="" THEN GOTO 2900
2425 GOSUB 6400
2430 GOSUB 5000
2460 FOR X=0 TO 5
2470 LET T[X]=T[X]+B1[X],U[X]=U[X]+B1[X]
2480 NEXT X
2500 FOR X=6 TO 10
2510 LET T[X]=T[X]+C1[X-6],U[X]=U[X]+C1[X-6]
2520 NEXT X
2530 FOR X=3 TO 7; REM "Box 13 codes D, E, F and H
2540 LET T[11]=T[11]+F1[X],U[11]=U[11]+F1[X]
2550 NEXT X
2555 LET T[11]=T[11]+F1[16],U[11]=U[11]+F1[16]; REM "Box 13 code S
2560 FOR X=12 TO 13
2565 LET T[X]=T[X]+K1[X-11],U[X]=U[X]+K1[X-11]
2570 NEXT X
2580 FOR X=14 TO 15
2585 LET T[X]=T[X]+M1[X-13],U[X]=U[X]+M1[X-13]
2590 NEXT X
2700 IF L>L9 THEN GOSUB 5400
2900 GOTO 1100
4000 REM " --- Totals"
4020 IF L=0 THEN GOTO 4200
4100 GOSUB 5400
4110 LET X0=MOD(L1,2)
4120 IF X0=0 THEN GOSUB 5600
4200 REM " --- Grand Total"
4205 DIM F1[LEN_W-1],H1[5]
4210 LET C0$="",C1$="",C2$="",C3$="",C4$="",C5$="",C6$="",C7$=""
4215 LET C1[5]=0
4220 LET C5$=""; REM "Subtotal Box eliminated for 1998 W-2's
4221 LET X1$(1)="W3 transmittal information."
4222 LET X1$(31,24)="Do NOT submit this W2"
4223 LET X1$(55,24)="form to the Social"
4224 LET X1$(79,24)="Security Admin."
4225 FOR X=0 TO 5
4230 LET B1[X]=U[X]; NEXT X
4235 FOR X=6 TO 10
4240 LET C1[X-6]=U[X]; NEXT X
4242 LET F1[0]=U[11]
4245 FOR X=12 TO 13
4250 LET K1[X-11]=U[X]; NEXT X
4255 FOR X=14 TO 15
4260 LET M1[X-13]=U[X]; NEXT X
4300 GOSUB 5000
4390 GOTO 9900
5000 REM " --- Heading"
5002 LET C2$="",C6$=""
5003 LET C1$="",Q0=0
5005 IF L9$<>"N" THEN GOTO 5020; REM "If printing test, L9$="Y"
5010 IF L9$="N" THEN IF R9$="Y" THEN IF R8$=A0$(3) THEN LET L9$="Y"
5015 IF L9$="N" THEN GOTO 5390
5019 REM " --- Prints 1 Line Feed So Print Begins On 2nd Line Of Form"
5020 IF POS("X"<>X1$(1,30))<>0 THEN LET L=L+1,L0=L0+1
5025 PRINT (7)'LF','LF',
5029 REM " --- Box 1 - Control Number"
5030 PRINT (7)@(H0+A[0]),FNF$(A0$(3,P[0]),M0$),@(H0+A[1]),C7$,; IF H9>0 THEN PRINT (7)@(H0+H9+A[0]),FNF$(A0$(3,P[0]),M0$),@(H0+H9+A[1]),C7$ ELSE PRINT (7)'LF',
5033 IF A1$(173,3)="DDD" OR A1$(173,3)="ddd" THEN LET C1$="X"
5035 IF A1$(226,1)="Y" THEN IF C5$<>"X" THEN IF C7$<>"X" THEN LET C2$="X" ELSE LET C2$=""
5037 IF F1[3]<>0 OR F1[4]<>0 OR F1[5]<>0 OR F1[6]<>0 OR F1[7]<>0 OR F1[16]<>0 THEN LET C6$="X"; REM "Box 13 code D, E, F, G, H or S
5040 IF POS("X"<>X1$(1,30))<>0 THEN LET L1=L1+1
5042 IF B1[0]<=0 THEN LET B$="" ELSE LET B$=STR(B1[0]:M2$)
5043 IF C1[0]<=0 THEN LET C$="" ELSE LET C$=STR(C1[0]:M2$)
5045 PRINT (7)'LF',@(H0+A[2]),R1$,; IF Q0=0 THEN PRINT (7)@(H0+B[0]),B$,@(H0+C[0]),C$,
5050 IF H9>0 THEN PRINT (7)@(H0+H9+A[2]),R1$,; IF Q0=0 THEN PRINT (7)@(H0+H9+B[0]),B$,@(H0+H9+C[0]),C$,
5054 IF B1[1]<=0 THEN LET B$="" ELSE LET B$=STR(B1[1]:M2$)
5056 IF C1[1]<=0 THEN LET C$="" ELSE LET C$=STR(C1[1]:M2$)
5060 PRINT (7)'LF','LF',; IF Q0=0 THEN PRINT (7)@(H0+B[1]),B$,@(H0+C[1]),C$,; IF H9>0 THEN PRINT (7)@(H0+H9+B[1]),B$,@(H0+H9+C[1]),C$,
5070 PRINT (7)'LF',@(H0+A[3]),R0$(1,30),
5080 IF H9>0 THEN PRINT (7)@(H0+H9+A[3]),R0$(1,30),
5084 IF B1[2]<=0 THEN LET B$="" ELSE LET B$=STR(B1[2]:M2$)
5086 IF C1[2]<=0 THEN LET C$="" ELSE LET C$=STR(C1[2]:M2$)
5090 PRINT (7)'LF',@(H0+A[4]),R0$(31,24),; IF Q0=0 THEN PRINT (7)@(H0+B[2]),B$,@(H0+C[2]),C$,
5100 IF H9>0 THEN PRINT (7)@(H0+H9+A[4]),R0$(31,24),; IF Q0=0 THEN PRINT (7)@(H0+H9+B[2]),B$,@(H0+H9+C[2]),C$,
5110 PRINT (7)'LF',@(H0+A[5]),R0$(55,24),
5120 IF H9>0 THEN PRINT (7)@(H0+H9+A[5]),R0$(55,24),
5134 IF B1[3]<=0 THEN LET B$="" ELSE LET B$=STR(B1[3]:M2$)
5136 IF C1[3]<=0 THEN LET C$="" ELSE LET C$=STR(C1[3]:M2$)
5140 PRINT (7)'LF',@(H0+A[6]),R$,; IF Q0=0 THEN PRINT (7)@(H0+B[3]),B$,@(H0+C[3]),C$,
5150 IF H9>0 THEN PRINT (7)@(H0+H9+A[6]),R$,; IF Q0=0 THEN PRINT (7)@(H0+H9+B[3]),B$,@(H0+H9+C[3]),C$,
5160 PRINT (7)'LF','LF',; IF C5$<>"X" THEN PRINT (7)@(H0+A[8]),FNF$(X1$(136,9),M9$),
5162 IF B1[4]<=0 THEN LET B$="" ELSE LET B$=STR(B1[4]:M2$)
5163 IF C1[4]<=0 THEN LET C$="" ELSE LET C$=STR(C1[4]:M2$)
5165 IF Q0=0 THEN PRINT (7)@(H0+B[4]),B$,@(H0+C[4]),C$,
5170 IF H9>0 THEN IF C5$<>"X" THEN PRINT (7)@(H0+H9+A[8]),FNF$(X1$(136,9),M9$),
5175 IF H9>0 THEN IF Q0=0 THEN PRINT (7)@(H0+H9+B[4]),B$,@(H0+H9+C[4]),C$,
5177 IF B1[5]<=0 THEN LET B$="" ELSE LET B$=STR(B1[5]:M2$)
5178 IF C1[5]<=0 THEN LET C$="" ELSE LET C$=STR(C1[5]:M2$)
5180 PRINT (7)'LF','LF',@(H0+A[9]),X1$(1,30),; IF Q0=0 THEN PRINT (7)@(H0+B[5]),B$,@(H0+C[5]),C$,
5190 IF H9>0 THEN PRINT (7)@(H0+H9+A[9]),X1$(1,30),; IF Q0=0 THEN PRINT (7)@(H0+H9+B[5]),B$,@(H0+H9+C[5]),C$,
5205 PRINT (7)'LF','LF',@(H0+A[10]),X1$(31,24),; IF H9>0 THEN PRINT (7)@(H0+H9+A[10]),X1$(31,24),
5210 IF Q0=1 THEN GOTO 5230
5215 FOR I=0 TO LEN_W-1; IF F1[I]>0 THEN EXITTO 5225 ELSE NEXT I
5217 IF I=LEN_W-1 THEN LET I=LEN_W
5220 IF I=LEN_W THEN GOTO 5230
5225 IF C5$="" THEN LET W1$=W$(I+1,1) ELSE LET W1$=" "
5230 PRINT (7)'LF',@(H0+A[11]),X1$(55,24),
5235 IF I<LEN_W THEN PRINT (7)@(H0+F[I]),W1$+"  ",F1[I]:M1$,
5237 IF Q0=0 THEN IF H1[0]<>0 THEN PRINT (7)@(H0+H[0]),H1$(1,3),H1[0]:M1$,
5238 IF H9>0 THEN PRINT (7)@(H0+H9+A[11]),X1$(55,24), ELSE GOTO 5250
5240 IF I<LEN_W THEN PRINT (7)@(H0+H9+F[I]),W1$+"  ",F1[I]:M1$,
5245 IF Q0=0 THEN IF H1[0]<>0 THEN PRINT (7)@(H0+H9+H[0]),H1$(1,3),H1[0]:M1$,
5250 IF I<LEN_W-1 THEN FOR I=I+1 TO LEN_W-1; IF F1[I]>0 THEN EXITTO 5260 ELSE NEXT I
5252 IF I=LEN_W-1 THEN LET I=LEN_W
5255 IF I=LEN_W THEN GOTO 5265
5260 LET W1$=W$(I+1,1)
5264 REM " --- Box f Address 3"
5265 LET X$=X1$(79,POS("   "=X1$(79,22)+"   ")-1)+", "+X1$(101,2)
5266 IF POS(","=X$)=1 THEN LET X$(1)=""
5267 PRINT (7)'LF',@(H0+A[12]),X$,
5270 IF I<LEN_W THEN PRINT (7)@(H0+F[I]),W1$+"  ",F1[I]:M1$,
5275 IF Q0=0 THEN IF H1[1]<>0 THEN PRINT (7)@(H0+H[1]),H2$(1,3),H1[1]:M1$,
5280 IF H9>0 THEN PRINT (7)@(H0+H9+A[12]),X$, ELSE GOTO 5300
5285 IF I<LEN_W THEN PRINT (7)@(H0+H9+F[I]),W1$+"  ",F1[I]:M1$,
5290 IF Q0=0 THEN IF H1[1]<>0 THEN PRINT (7)@(H0+H9+H[1]),H2$(1,3),H1[1]:M1$,
5300 IF I<LEN_W-1 THEN FOR I=I+1 TO LEN_W-1; IF F1[I]>0 THEN EXITTO 5310 ELSE NEXT I
5302 IF I=LEN_W-1 THEN LET I=LEN_W
5305 IF I=LEN_W THEN GOTO 5315
5310 LET W1$=W$(I+1,1)
5315 PRINT (7)'LF',@(H0+A[13]),FNZ$(X1$(103,9)),
5320 IF I<LEN_W THEN PRINT (7)@(H0+F[I]),W1$+"  ",F1[I]:M1$,
5325 IF Q0=0 THEN IF H1[2]<>0 THEN PRINT (7)@(H0+H[2]),H3$(1,3),H1[2]:M1$,
5330 IF H9>0 THEN PRINT (7)@(H0+H9+A[13]),FNZ$(X1$(103,9)), ELSE GOTO 5342
5335 IF I<LEN_W THEN PRINT (7)@(H0+H9+F[I]),W1$+"  ",F1[I]:M1$,
5340 IF Q0=0 THEN IF H1[2]<>0 THEN PRINT (7)@(H0+H9+H[2]),H3$(1,3),H1[2]:M1$,
5342 PRINT (7)'LF','LF',
5343 PRINT (7)@(H0+D[0]),C0$,@(H0+D[1]),C1$,@(H0+D[2]),C2$,@(H0+D[3]),C3$,@(H0+D[4]),C4$,@(H0+D[5]),C5$,@(H0+D[6]),C6$,; IF H9>0 THEN PRINT (7)@(H0+H9+D[0]),C0$,@(H0+H9+D[1]),C1$,@(H0+H9+D[2]),C2$,@(H0+H9+D[3]),C3$,@(H0+H9+D[4]),C4$,@(H0+H9+D[5]),C5$,@(H0+H9+D[6]),C6$,
5344 PRINT (7)'LF','LF',
5345 IF K1[1]<>0 OR K1[2]<>0 THEN PRINT (7)@(H0+K[0]),X2$," ",R3$,@(H0+K[1]),K1[1]:M4$,@(H0+K[2]),K1[2]:M3$,
5346 IF H9>0 THEN IF K1[1]<>0 OR K1[2]<>0 THEN PRINT (7)@(H0+H9+K[0]),X2$," ",R3$,@(H0+H9+K[1]),K1[1]:M4$,@(H0+H9+K[2]),K1[2]:M3$,
5347 IF M1[1]<>0 OR M1[2]<>0 THEN PRINT (7)@(H0+M[0]),X3$,@(H0+M[1]),M1[1]:M4$,@(H0+M[2]),M1[2]:M3$,
5348 IF H9>0 THEN IF M1[1]<>0 OR M1[2]<>0 THEN PRINT (7)@(H0+H9+M[0]),X3$,@(H0+H9+M[1]),M1[1]:M4$,@(H0+H9+M[2]),M1[2]:M3$,
5350 PRINT (7)'LF','LF',
5360 PRINT (7)'LF','LF','LF','LF','LF','LF','LF','LF',
5362 IF I=LEN_W-1 THEN LET I=LEN_W
5365 IF I=LEN_W THEN GOTO 5375
5370 FOR I=I+1 TO LEN_W-1; IF F1[I]>0 THEN EXITTO 5380 ELSE NEXT I
5375 IF I=LEN_W THEN LET Q0=0; GOTO 5390
5380 LET W1$=W$(I+1,1),Q0=1
5385 GOTO 5025
5390 RETURN 
5400 REM " --- Subtotal"
5402 GOTO 5580; REM "Subtotal eliminated for 1998 W-2's
5405 IF L0<=L9 THEN GOTO 5590
5410 LET X0=MOD(L1,2)
5420 IF X0=0 THEN GOSUB 5600
5430 LET L1=0
5435 DIM F1[LEN_W-1],H1[5]
5440 LET C0$="",C1$="",C2$="",C3$="",C4$="",C5$="",C6$="",C7$=""
5445 LET C1[5]=0
5450 LET X1$(1)="",X1$(136,9)="999999999",C5$="X"
5460 FOR X=0 TO 5
5470 LET B1[X]=T[X]; NEXT X
5480 FOR X=6 TO 10
5490 LET C1[X-6]=T[X]; NEXT X
5500 LET F1[0]=T[11]
5510 FOR X=12 TO 13
5520 LET K1[X-11]=T[X]; NEXT X
5530 FOR X=14 TO 15
5540 LET M1[X-13]=T[X]; NEXT X
5570 GOSUB 5000
5580 DIM T[15]
5585 LET L=0,L1=0
5590 RETURN 
5600 REM " --- Void"
5610 LET C0$="",C1$="",C2$="",C3$="",C4$="",C5$="",C6$="",C7$=""
5620 LET X1$(1)="VOID",X1$(136,9)="9999999999",C7$="X",C5$=""
5640 DIM B1[6],C1[8],F1[LEN_W-1],H1[5],K1[5],M1[5]
5660 GOSUB 5000
5690 RETURN 
6000 REM " --- Rev"
6020 LET V3$=V1$
6030 GOSUB 6100
6040 LET V$=V3$,V3$=V0$
6050 GOSUB 6100
6060 IF LEN(V$)>0 THEN LET V$=V$+" "
6070 LET V$=V$+V3$
6080 IF V2$<>"" THEN LET V$=V$+" "+V2$
6090 RETURN 
6100 LET V=POS("  "=V3$)
6110 IF V=0 THEN IF LEN(V3$)>1 THEN IF V3$(LEN(V3$),1)=" " THEN LET V=LEN(V3$)
6120 IF V=0 THEN LET V=LEN(V3$)+1
6130 IF V>1 THEN LET V3$=V3$(1,V-1)
6140 IF V=1 THEN LET V3$=""
6190 RETURN 
6200 REM " --- Check if selected state"
6210 LET L$=""
6220 READ (PRW06_DEV,KEY=A0$+"20",DOM=6230)
6230 LET K2$=""; LET K2$=KEY(PRW06_DEV,END=6240)
6240 IF K2$="" THEN IF T9$="NONE" THEN GOTO 6290 ELSE GOTO 6285; REM "No state records found
6260 IF T9$="NONE" THEN IF K2$(1,13)<>A0$+"20" THEN GOTO 6290 ELSE GOTO 6285; REM "No state records found
6270 IF K2$(1,13)<>A0$+"20" THEN GOTO 6285; REM "No match
6280 IF K2$(17,2)<>T5$ THEN READ (PRW06_DEV,END=6285); GOTO 6230; REM "Try again
6282 GOTO 6290
6285 LET L$="NO"
6290 RETURN 
6300 REM " --- Make sure W-2 should be printed"
6310 LET X0$=""
6320 FOR X=0 TO 5
6330 IF B1[X]>0 OR C1[X]>0 OR H1[X]>0 OR K1[X]>0 OR M1[X]>0 THEN LET X0$="Y"; EXITTO 6390
6340 NEXT X
6350 FOR X=0 TO 15
6360 IF F1[X]>0 THEN LET X0$="Y"; EXITTO 6390
6370 NEXT X
6390 RETURN 
6400 REM " --- Check if first W-2 for employee"
6410 LET K4$=N0$+"W"+A0$(3,9)+FNYY_YY21$(YR$)
6420 READ (PRM03_DEV,KEY=K4$,DOM=6421)
6430 LET K4$=KEY(PRM03_DEV,END=6470)
6440 READ (PRM03_DEV)
6450 IF K4$(1,14)<>N0$+"W"+A0$(3,9)+FNYY_YY21$(YR$) THEN GOTO 6470
6455 IF K4$(15,4)=STATE_CODE$+LOCAL_CODE$ THEN GOTO 6490
6460 DIM B1[5],C1[5],F1[LEN_W-1],H1[5]
6465 GOTO 6490
6470 LET K0$=N0$+"W"+A0$(3,9)+FNYY_YY21$(YR$)+STATE_CODE$+LOCAL_CODE$
6475 WRITE (PRM03_DEV,KEY=K0$)IOL=PRM03W2
6480 READ (PRM03_DEV2,KEY=K$)
6490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

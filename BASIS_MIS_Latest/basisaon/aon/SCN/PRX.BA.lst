0010 REM "PRX - Rebuild Employee Cross-References"
0020 REM "Program PRX.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "23Oct96 JWK: Clear x-ref file AFTER asking (130-160,185,900-930)
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="PRM-01",FILES$[2]="SYS-01",FILES$[3]="PRM-03"
0140 LET OPTIONS$[3]="C"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0185 LET PRM01_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 PRM01A: IOLIST A0$,A1$
0230 PRM03A: IOLIST B0$(1)
0240 PRM03B: IOLIST C0$(1)
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 PRS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0500 REM " --- Init Data
0510 DIM B0$(28),C0$(22)
0800 REM " --- Options
0810 LET V4$="Do You Wish To Rebuild Employee Cross-References"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM " --- Clear Files
0910 CALL "SYC.DA",1,3,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0920 IF STATUS THEN GOTO 9900
0930 LET PRM03_DEV=CHANNELS[3]
1000 REM " --- Employee X-refs
1050 CALL "SYC.NB","Updating",12,COLUMN
1090 READ (PRM01_DEV,KEY="",DOM=1100)
1100 REM " --- Main Read
1120 READ (PRM01_DEV,END=4000)IOL=PRM01A
1140 IF A1$="" THEN GOTO 1100
1160 PRINT @(COLUMN,11),A0$(1,2)," ",A0$(3,9),
1200 LET B0$=A0$(1,2)+"A"+A1$(1,16)+A0$(3,9)
1220 WRITE (PRM03_DEV,KEY=B0$)IOL=PRM03A
1230 LET C0$=A0$(1,2)+"B"+A1$(145,10)+A0$(3,9)
1240 WRITE (PRM03_DEV,KEY=C0$)IOL=PRM03B
1900 GOTO 1100
4000 REM " --- End
4090 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "IVX - Rebuild Unit Costs"
0020 REM "Program IVX.IA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "24May00 WGH: Rebuild Unit Cost in IVM-07 Lot/Serial Number record (110,130,220,420,520-530,600-660,920-930,1000-1020,1900-2090)
0040 REM 
0070 BEGIN
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="IVM-02",FILES$[2]="SYS-01",FILES$[3]="IVM-07"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET IVM02_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 IVM02A: IOLIST A0$,A1$,A2$,A[ALL]
0220 IVM07A: IOLIST B0$,B1$,B[ALL]
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N4$=F5$,LS$="N"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 IF POS(P3$(17,1)="LS") THEN LET LS$="Y"
0500 REM " --- Initializations"
0510 PRECISION 4
0520 DIM A[20],B[12]
0600 REM " --- Additional file opens"
0610 IF LS$="N" THEN GOTO 0700
0640 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0650 IF STATUS>0 THEN GOTO 9900
0660 LET IVM07_DEV=CHANNELS[3]
0800 REM " --- Options
0810 LET V4$="Do You Wish To Rebuild Inventory Unit Costs"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM " --- Position warehouse file"
0920 CALL "SYC.NB","Updating IVM-02",47,COLUMN
0930 LET UNIT_COST=11,COST_METHOD=UNIT_COST+POS(P3$(8,1)="ASR")
0990 READ (IVM02_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Reset IVM-02 Unit Cost"
1010 LET K$=KEY(IVM02_DEV,END=1900)
1020 IF POS(N0$=K$)<>1 THEN GOTO 1900
1040 PRINT @(COLUMN,11),K$(1,2)," ",K$(3,2)," ",K$(5),
1050 EXTRACT (IVM02_DEV,KEY=K$)IOL=IVM02A
1060 LET A[UNIT_COST]=A[COST_METHOD]
1080 WRITE (IVM02_DEV,KEY=K$)IOL=IVM02A
1090 GOTO 1000
1900 REM " --- Position Lot/Serial Number file"
1910 IF LS$="N" THEN GOTO 4000
1920 CALL "SYC.NB","Updating IVM-07",47,COLUMN
1930 LET UNIT_COST=3,COST_METHOD=UNIT_COST+POS(P3$(8,1)="ASR")
1990 READ (IVM07_DEV,KEY=N0$,DOM=2000)
2000 REM " --- Reset IVM-07 Unit Cost"
2010 LET K$=KEY(IVM07_DEV,END=4000)
2020 IF POS(N0$=K$)<>1 THEN GOTO 4000
2040 PRINT @(COLUMN,11),K$(1,2)," ",K$(3,2)," ",K$(5,20)," ",K$(25),
2050 EXTRACT (IVM07_DEV,KEY=K$)IOL=IVM07A
2060 LET B[UNIT_COST]=B[COST_METHOD]
2080 WRITE (IVM07_DEV,KEY=K$)IOL=IVM07A
2090 GOTO 2000
4000 REM " --- All done"
4090 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
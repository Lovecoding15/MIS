0010 REM "IVR - Inventory Synonyms Listing (Input Overlay)"
0020 REM "Program IVR.SA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "20Dec96 JWK: Error 13 after Item Type lookup in IVC.IB (550)
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=6
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-10"
0130 LET FILES$[4]="IVM-14",FILES$[5]="IVM-24",FILES$[6]="IVM-15"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM10_DEV=CHANNELS[3]
0180 LET IVM14_DEV=CHANNELS[4],IVM24_DEV=CHANNELS[5],IVM15_DEV=CHANNELS[6]
0200 REM " --- IOLIST's"
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0400 REM " --- Parameters"
0405 DIM P[5]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="IV"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0450 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1))
0460 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0500 REM " --- Initializations"
0510 DIM FILES[15],PARAMS$[3],OPTIONS$[7]
0550 LET FILES[1]=IVM01_DEV,FILES[10]=IVM10_DEV,FILES[14]=IVM14_DEV,FILES[15]=IVM15_DEV
0560 LET PARAMS$[0]=N0$,PARAMS$[1]=I1$,PARAMS$[2]=I2$,PARAMS$[3]=I3$
0570 LET SEQUENCE$="S",BEGSYN$="",ENDSYN$=""
0700 REM " --- Background"
0710 PRINT 'SB',@(30,3),"Sequence:",'SF',
0720 CALL "SYC.WC",1,0,80,0,0,4,0
0730 PRINT 'SB',@(21,5),"Beginning Synonym:",'SF',
0740 CALL "SYC.WC",1,0,80,0,0,6,0
0750 PRINT 'SB',@(24,7),"Ending Synonym:",'SF',
0760 CALL "SYC.WC",1,0,80,0,0,8,0
0770 CALL "IVC.IB","BL","DW",9,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0780 PRINT @(40,3),SEQUENCE$,@(40,5),"First",@(40,7),"Last",
0790 CALL "IVC.IB","DL","DW",9,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
1000 REM " --- Sequence"
1010 LET V4$="List Items By Synonym Or Item Number (S=Synonym/I=Item Number)"
1020 LET V0$="S",V1$="CE",V2$=SEQUENCE$,V3$="SI",V0=1,V1=40,V2=3,I0=0
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1000,3900
1050 LET SEQUENCE$=V$
1090 GOTO 3900
1200 REM " --- Beginning Synonym"
1210 LET V4$="Enter The Beginning Synonym To Include On The Report (<F1>=First)"
1220 LET V0$="S",V1$="C",V2$=BEGSYN$,V3$="",V0=20,V1=40,V2=5
1230 GOSUB 7000
1240 ON V3 GOTO 1260,1250,3900,1200,3900
1250 LET V$=""
1260 LET BEGSYN$=V$
1270 IF BEGSYN$="" THEN PRINT @(V1,V2),"First",'CL', ELSE PRINT @(V1,V2),BEGSYN$,'CL',
1290 GOTO 3900
1400 REM " --- Ending Synonym"
1410 LET V4$="Enter The Ending Synonym To Include On The Report (<F1>=Last)"
1420 LET V0$="S",V1$="C",V2$=ENDSYN$,V3$="",V0=20,V1=40,V2=7
1430 GOSUB 7000
1440 ON V3 GOTO 1460,1450,3900,1400,3900
1450 LET V$=""
1460 LET ENDSYN$=V$
1470 IF ENDSYN$="" THEN PRINT @(V1,V2),"Last",'CL', ELSE PRINT @(V1,V2),ENDSYN$,'CL',
1490 GOTO 3900
1600 REM " --- Remaining selection criteria"
1610 CALL "IVC.IB","IL","DW",9,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
1690 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 GOSUB 5000
4020 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4030 ON V3 GOTO 4040,4000,4000,4000,9900
4040 ON POS(V$="YN") GOTO 4000,4050,1000
4050 LET BEGPROD$=OPTIONS$[3],ENDPROD$=OPTIONS$[4],BEGITEM$=OPTIONS$[5]
4060 LET ENDITEM$=OPTIONS$[6],TYPE$=OPTIONS$[7]
4090 RUN "IVR.SB"
5000 REM " --- Display selection criteria"
5010 LET X1$="First",X2$="Last"
5020 IF BEGSYN$<>"" THEN LET X1$=BEGSYN$
5030 IF ENDSYN$<>"" THEN LET X2$=ENDSYN$
5040 PRINT @(40,3),SEQUENCE$,@(40,5),X1$,'CL',@(40,7),X2$,'CL',
5080 CALL "IVC.IB","DL","DW",9,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

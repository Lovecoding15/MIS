0010 REM "WOS - OP Code Calendar Purge                                 "
0020 REM "Program WOU.HA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Jun97 WGH: Display 21st century dates correctly (4225)"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="WOM-04",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET WOM04_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 WOM04A: IOLIST A0$(1),A1$,A2$,A[ALL]
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)
0500 REM " --- Init Data"
0520 DIM A0$(9),A[31]
0700 REM " --- Background"
0710 FOR X=4 TO 6 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(15,3),"Date To Purge Through:",@(30,5),"Purging:",
0750 CALL "SYC.WC",0,0,76,4,2,8,0
0760 PRINT @(4,9),'SB',"This program will purge all hours for all Operation Codes through the",@(4,10),"purge date specified.",'SF'
1000 REM " --- Date To Purge Through
1010 PRINT 'CF'
1015 IF LEN(R0$)=0 THEN LET R0$=F0$(18,6)
1020 LET V0$="D",V1$="KEC6N",V2$=R0$,V3$="",V4$="Enter Date To Purge Through",V0=8,V1=39,V2=3,I0=0
1040 GOSUB 7000
1045 IF V3=2 THEN GOTO 1090
1050 IF V3=4 THEN GOTO 9900
1080 LET R0$=V$
1090 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,3990
3990 LET I0=0
4000 REM " --- Options"
4020 LET V0$="Y",V1$="KCE",V2$="",V3$="",V4$="Is The Above Information Correct? (Y/N): ",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Disallow 'M'enu option in Error Routine
4110 LET EXIT_CTRL=1
4120 REM " --- Purge Records
4130 READ (WOM04_DEV,KEY=N0$,DOM=4131)
4200 LET K$=KEY(WOM04_DEV,END=9900)
4210 IF POS(N0$=K$)<>1 THEN GOTO 9900
4220 IF K$(6,4)>R0$(1,4) THEN READ (WOM04_DEV,KEY=K$(1,5)+$FF$,DOM=4200)
4225 PRINT @(39,5),K$(8,2),"/",FNYY21_YY$(K$(6,2)),
4230 IF K$(6,4)<R0$(1,4) THEN REMOVE (WOM04_DEV,KEY=K$); GOTO 4200
4240 READ (WOM04_DEV,KEY=K$)IOL=WOM04A
4250 FOR X=1 TO NUM(R0$(5,2))
4260 LET A[X]=-1
4270 NEXT X
4280 WRITE (WOM04_DEV,KEY=K$)IOL=WOM04A
4290 FOR X=1 TO A[0]
4300 IF A[X]>=0 THEN EXITTO 4390
4310 NEXT X
4320 REMOVE (WOM04_DEV,KEY=K$)
4390 GOTO 4200
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

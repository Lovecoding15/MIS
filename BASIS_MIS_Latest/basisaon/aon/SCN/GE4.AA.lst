0010 REM "GEN - GENERAL Report Writer Lead Overlay"
0020 REM "Program GE4.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "10Mar97 JWK: Prevent Error 47 trying to do WO/PR interface with no PR (2525)
0040 REM 
0045 REM "This program is run by SYS.AA going into GEN and by GEN4 on"
0050 REM "the way out. The presence of the SYS-01 G record controls the"
0055 REM "flow of processing"
0060 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GEN4MST",FILES$[2]="SYS-01"
0140 LET OPTIONS$[1]="F"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0170 LET GEN4MST_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0180 IF GEN4MST_DEV=0 THEN GOTO 4800
0200 REM " --- IOLists
0210 GEN4MST: IOLIST A0$,A1$
0220 SYS01G: IOLIST B0$,B1$
0230 APS01A: IOLIST AP0$,AP1$,AP2$,AP3$,AP4$,AP5$,AP6$,AP7$,AP8$
0240 ARS01A: IOLIST AR0$,AR1$,AR2$,AR3$,AR4$,AR5$,AR6$,AR7$,AR8$
0250 GLS01A: IOLIST GL0$,GL1$,GL2$,GL3$,GL4$,GL5$,GL6$,GL7$,GL8$
0260 IVS01A: IOLIST IV0$,IV1$,IV2$,IV3$,IV4$,IV5$,IV6$,IV7$,IV8$,IV9$,IV10$
0270 PRS01A: IOLIST PR0$,PR1$,PR2$,PR3$,PR4$,PR5$,PR6$,PR7$,PR8$
0280 WOS01A: IOLIST WO0$,WO1$,WO2$,WO3$,WO4$,WO5$,WO6$,WO7$,WO8$
0290 SHS01A: IOLIST SH0$,SH1$
0300 SYS01T: IOLIST TERMINAL$,F0$,F1$,F2$,F3$,F4$,F5$
0400 REM " --- Parameters
0410 DIM INFO$[20]
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),MAXVARS=48,KEYSEQ=0,VARCOUNT=1,COUNT=1
0440 LET X0$="NAME["+STR(MAXVARS)+"]:C(30),DESC["+STR(MAXVARS)+"]:C(40),VALUE["+STR(MAXVARS)+"]:C(50)"
0450 LET X1$="SEQ[8]:C(120)"
0500 REM " --- Initializations
0510 DIM VAR$:X0$,A1$:X1$,WOSOURCE$[3],GENMASK$[9]
0550 LET B0$="G"+FID(0)
0600 REM " --- Going to or returning from GEN4?"
0610 FIND (SYS01_DEV,KEY=B0$,DOM=BACKGROUND)IOL=SYS01G
0690 GOTO EXIT_GENERAL
0700 REM " --- Background
0710 BACKGROUND: 
0720 CALL "SYC.WC",0,0,25,3,27,10,0
0730 PRINT @(29,11),"Loading Parameters...",
0800 REM " --- Which applications are installed?"
0810 CALL "SYC.VA","AP",INFO$[ALL]
0815 LET AP$=INFO$[20]
0820 CALL "SYC.VA","AR",INFO$[ALL]
0825 LET AR$=INFO$[20]
0830 CALL "SYC.VA","GL",INFO$[ALL]
0835 LET GL$=INFO$[20]
0840 CALL "SYC.VA","IV",INFO$[ALL]
0845 LET IV$=INFO$[20]
0850 CALL "SYC.VA","PR",INFO$[ALL]
0855 LET PR$=INFO$[20]
0860 CALL "SYC.VA","WO",INFO$[ALL]
0865 LET WO$=INFO$[20]
0870 CALL "SYC.VA","SH",INFO$[ALL]
0875 LET SH$=INFO$[20]
0900 REM " --- Additional Parameter Reads
0910 IF AP$="Y" THEN FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0920 IF AR$="Y" THEN FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0930 IF GL$="Y" THEN FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0940 IF IV$="Y" THEN FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0950 IF PR$="Y" THEN FIND (SYS01_DEV,KEY=N0$+"PR00",DOM=9800)IOL=PRS01A
0960 IF WO$="Y" THEN FIND (SYS01_DEV,KEY=N0$+"WO00",DOM=9800)IOL=WOS01A
0970 IF SH$="Y" THEN FIND (SYS01_DEV,KEY=N0$+"SH00",DOM=9800)IOL=SHS01A
1000 REM " --- Administrator Variables
1005 ADMINISTRATOR: 
1010 LET V0$="FIRM",V4$="Firm ID",V2$=N0$
1015 GOSUB 6000
1020 LET V0$="FIRMNAME",V4$="Firm Name",V2$=F4$
1025 GOSUB 6000
1030 LET V0$="TIMEMASK",V4$="Default Time Mask",V2$="00:00"
1035 GOSUB 6000
1040 LET V0$="PHONEMASK",V4$="Default Phone Mask",V2$="(XXX) XXX-XXXX"
1045 GOSUB 6000
1200 REM " --- Accounts Payable Variables
1205 ACCOUNTS_PAYABLE: 
1210 LET V0$="APVENDLEN",V4$="Vendor Number Length",V2$="6"
1215 GOSUB 6000
1220 LET V0$="APVENDMASK",V4$="Vendor Mask",V2$="000000"
1225 GOSUB 6000
1230 LET V0$="APVENDDISP",V4$="Vendor Display Length",V2$="6"
1235 GOSUB 6000
1240 IF AP$<>"Y" THEN GOTO ACCOUNTS_RECEIVABLE
1250 LET VARCOUNT=VARCOUNT-3
1260 LET V0$="APVENDLEN",V4$="Vendor Number Length",V2$=AP2$(1,2)
1265 GOSUB 6000
1270 LET V0$="APVENDMASK",V4$="Vendor Mask",V2$=AP5$
1275 GOSUB 6000
1280 LET V0$="APVENDDISP",V4$="Vendor Display Length",V2$=STR(LEN(AP5$))
1285 GOSUB 6000
1400 REM " --- Accounts Receivable Variables
1405 ACCOUNTS_RECEIVABLE: 
1410 LET V0$="ARCUSTLEN",V4$="AR Customer Number Length",V2$="6"
1415 GOSUB 6000
1420 LET V0$="ARCUSTMASK",V4$="AR Customer Mask",V2$="000000"
1425 GOSUB 6000
1430 LET V0$="ARCUSTDISP",V4$="AR Customer Display Length",V2$="6"
1435 GOSUB 6000
1440 LET GENMASK$[9]="#,###,##0.00-"
1450 IF AR$<>"Y" THEN GOTO GENERAL_LEDGER
1460 LET VARCOUNT=VARCOUNT-3
1470 LET V0$="ARCUSTLEN",V4$="AR Customer Number Length",V2$=AR2$(1,2)
1475 GOSUB 6000
1480 LET V0$="ARCUSTMASK",V4$="AR Customer Mask",V2$=AR5$
1485 GOSUB 6000
1490 LET V0$="ARCUSTDISP",V4$="AR Customer Display Length",V2$=STR(LEN(AR5$))
1495 GOSUB 6000
1500 LET GENMASK$[9]=AR6$
1600 REM " --- General Ledger Variables
1605 GENERAL_LEDGER: 
1610 LET V0$="GLACCTLEN",V4$="GL Account Number Length",V2$="10"
1615 GOSUB 6000
1620 LET V0$="GLACCTMASK",V4$="GL Account Mask",V2$="0000000000"
1625 GOSUB 6000
1630 LET V0$="GLACCTDISP",V4$="GL Account Display Length",V2$="10"
1635 GOSUB 6000
1640 LET GENMASK$[8]="##,###,##0.00-"
1650 IF GL$<>"Y" THEN GOTO INVENTORY
1660 LET VARCOUNT=VARCOUNT-3
1670 LET V0$="GLACCTLEN",V4$="GL Account Number Length",V2$=GL2$(7,2)
1675 GOSUB 6000
1680 LET V0$="GLACCTMASK",V4$="GL Account Mask",V2$=GL5$
1685 GOSUB 6000
1690 LET V0$="GLACCTDISP",V4$="GL Account Display Length",V2$=STR(LEN(GL5$))
1695 GOSUB 6000
1700 LET GENMASK$[8]=GL6$
1800 REM " --- Inventory Variables
1805 INVENTORY: 
1810 LET V0$="IVITEMLEN",V4$="Inventory Item Number Length",V2$="10"
1815 GOSUB 6000
1820 LET V0$="IVLOTSLEN",V4$="Inventory Lot/Serial Number Length",V2$="10"
1825 GOSUB 6000
1830 LET V0$="IVDESCLEN",V4$="Inventory Description Length",V2$="30"
1835 GOSUB 6000
1840 LET V0$="IVDESC1LEN",V4$="Inventory Description 1 Length",V2$="30"
1845 GOSUB 6000
1850 LET V0$="IVDESC2LEN",V4$="Inventory Description 2 Length",V2$="0"
1855 GOSUB 6000
1860 LET V0$="IVDESC3LEN",V4$="Inventory Description 3 Length",V2$="0"
1865 GOSUB 6000
1870 LET V0$="IVMFGLEN",V4$="Inventory Manufacturer Part length",V2$="10"
1875 GOSUB 6000
1880 LET GENMASK$[1]="##,###,##0.00-",GENMASK$[2]="##,##0-"
1885 LET GENMASK$[3]="#,##0.00-",GENMASK$[4]="##,##0.00-"
1890 IF IV$<>"Y" THEN GOTO PAYROLL
1910 LET VARCOUNT=VARCOUNT-7
1920 LET V0$="IVITEMLEN",V4$="Inventory Item Number Length",V2$=IV2$(1,2)
1925 GOSUB 6000
1930 LET V0$="IVLOTSLEN",V4$="Inventory Lot/Serial Number Length",V2$=IV2$(7,2)
1935 GOSUB 6000
1940 LET V0$="IVDESCLEN",V4$="Inventory Description Length"
1950 LET V2$=STR(NUM(IV2$(9,2))+NUM(IV2$(11,2))+NUM(IV2$(13,2))+2)
1955 GOSUB 6000
1960 LET V0$="IVDESC1LEN",V4$="Inventory Description 1 Length",V2$=IV2$(9,2)
1965 GOSUB 6000
1970 LET V0$="IVDESC2LEN",V4$="Inventory Description 2 Length",V2$=IV2$(11,2)
1975 GOSUB 6000
1980 LET V0$="IVDESC3LEN",V4$="Inventory Description 3 Length",V2$=IV2$(13,2)
1985 GOSUB 6000
1990 LET V0$="IVMFGLEN",V4$="Inventory Manufacturer Part length",V2$=IV2$(3,2)
1995 GOSUB 6000
2000 LET GENMASK$[1]=IV6$,GENMASK$[2]=IV7$,GENMASK$[3]=IV8$,GENMASK$[4]=IV9$
2200 REM " --- Payroll Variables
2205 PAYROLL: 
2210 LET V0$="PREMPLLEN",V4$="PR Employee Number Length",V2$="6"
2215 GOSUB 6000
2220 LET V0$="PREMPLMASK",V4$="PR Employee Mask",V2$="000000"
2225 GOSUB 6000
2230 LET V0$="PREMPLDISP",V4$="PR Employee Display Length",V2$="6"
2235 GOSUB 6000
2240 LET GENMASK$[5]="##,##0.00-",GENMASK$[6]="#,##0.00-"
2245 LET GENMASK$[7]="##,##0.00-"
2250 IF PR$<>"Y" THEN GOTO SHOPFLOOR
2260 LET VARCOUNT=VARCOUNT-3
2270 LET V0$="PREMPLLEN",V4$="PR Employee Number Length",V2$=PR2$(1,2)
2275 GOSUB 6000
2280 LET V0$="PREMPLMASK",V4$="PR Employee Mask",V2$=PR5$
2285 GOSUB 6000
2290 LET V0$="PREMPLDISP",V4$="PR Employee Display Length",V2$=STR(LEN(PR5$))
2295 GOSUB 6000
2300 LET GENMASK$[5]=PR6$,GENMASK$[6]=PR7$,GENMASK$[7]=PR8$
2400 REM " --- Shop Floor Variables
2405 SHOPFLOOR: 
2410 LET V0$="WOEMPLLEN",V4$="WO Employee Number Length",V2$="6"
2415 GOSUB 6000
2420 LET V0$="WOEMPLMASK",V4$="WO Employee Mask",V2$="000000"
2425 GOSUB 6000
2430 LET V0$="WOEMPLDISP",V4$="WO Employee Display Length",V2$="6"
2435 GOSUB 6000
2440 LET V0$="WOSOURCE",V4$="WO Employee Source WO/PR",V2$="WO"
2445 GOSUB 6000
2450 IF WO$<>"Y" THEN GOTO SPEEDSHIP
2460 LET VARCOUNT=VARCOUNT-4
2500 REM " --- Employee records from Payroll or Shop Floor?"
2510 LET WOSOURCE$[0]=WO2$(1,2),WOSOURCE$[1]=WO5$
2520 LET WOSOURCE$[2]=STR(LEN(W05$)),WOSOURCE$[3]="WO"
2525 IF WO3$(4,1)<>"Y" OR PR$<>"Y" THEN GOTO 2540
2530 LET WOSOURCE$[0]=PR2$(1,2),WOSOURCE$[1]=PR5$
2535 LET WOSOURCE$[2]=STR(LEN(PR5$)),WOSOURCE$[3]="PR"
2540 LET V0$="WOEMPLLEN",V4$="WO Employee Number Length",V2$=WOSOURCE$[0]
2545 GOSUB 6000
2550 LET V0$="WOEMPLMASK",V4$="WO Employee Mask",V2$=WOSOURCE$[1]
2555 GOSUB 6000
2560 LET V0$="WOEMPLDISP",V4$="WO Employee Display Length",V2$=WOSOURCE$[2]
2565 GOSUB 6000
2570 LET V0$="WOSOURCE",V4$="WO Employee Source WO/PR",V2$=WOSOURCE$[3]
2575 GOSUB 6000
2600 REM " --- SpeedShip Variables
2605 SPEEDSHIP: 
2610 LET V0$="SHCUSTLEN",V4$="SH Customer Number Length",V2$="6"
2615 GOSUB 6000
2620 LET V0$="SHCUSTMASK",V4$="SH Customer Mask",V2$="000000"
2625 GOSUB 6000
2630 LET V0$="SHCUSTDISP",V4$="SH Customer Display Length",V2$="6"
2635 GOSUB 6000
2640 LET V0$="SHSOURCE",V4$="SH Customer Source AR/SH",V2$="SH"
2645 GOSUB 6000
2650 IF SH$<>"Y" THEN GOTO PREPRECORD
2660 IF SH1$(1,1)<>"Y" THEN GOTO PREPRECORD
2670 LET VARCOUNT=VARCOUNT-4
2680 LET V0$="SHCUSTLEN",V4$="SH Customer Number Length",V2$=AR2$(1,2)
2685 GOSUB 6000
2690 LET V0$="SHCUSTMASK",V4$="SH Customer Mask",V2$=AR5$
2695 GOSUB 6000
2700 LET V0$="SHCUSTDISP",V4$="SH Customer Display Length",V2$=STR(LEN(AR5$))
2705 GOSUB 6000
2710 LET V0$="SHSOURCE",V4$="SH Customer Source AR/SH",V2$="AR"
2715 GOSUB 6000
3000 REM " --- Prep variable records
3010 PREPRECORD: 
3020 WHILE COUNT<=VARCOUNT
3030 FOR X=1 TO 8
3040 LET A1.SEQ$[X]=VAR.NAME$[COUNT]+VAR.DESC$[COUNT]+VAR.VALUE$[COUNT]
3050 LET COUNT=COUNT+1
3060 NEXT X
3200 REM " --- Retrieve next available key
3210 NEXTSEQ: 
3220 READ (GEN4MST_DEV,KEY="Y"+$FFFFFF$,DOM=3230)
3230 LET KEYSEQ=1,A0$=KEYP(GEN4MST_DEV,END=3250)
3240 IF POS(A0$(1,1)="Y")=1 THEN LET KEYSEQ=NUM(A0$(2,3))+1
3250 LET A0$="Y"+STR(KEYSEQ:"000"),B1$=B1$+A0$
3400 REM " --- Write record to GEN4MST
3410 WRITERECORD: 
3420 WRITE (GEN4MST_DEV,KEY=A0$)IOL=GEN4MST
3430 DIM A1$:FATTR(A1$)
3500 REM " --- End of GEN4MST update"
3590 WEND
4000 REM " --- Store masks, reset function keys and run GEN4"
4005 RUN_GENERAL: 
4010 FOR X=1 TO 9
4020 LET X$=STBL("GENMASK"+STR(X:"0"),GENMASK$[X],ERR=4030)
4030 NEXT X
4040 WRITE (SYS01_DEV,KEY=B0$)IOL=SYS01G
4050 CALL "SYC.FK",STATUS
4060 PRINT @(0,3),'CE','CURSOR'("ON"),
4090 RUN "GEN4"
4500 REM " --- Perform cleanup when returning from GEN4"
4510 EXIT_GENERAL: 
4520 CALL "SYC.FK",STATUS
4530 IF LEN(B1$)<4 THEN GOTO 4580
4540 FOR X=1 TO LEN(B1$)/4
4550 LET K$=B1$((X-1)*4+1,4)
4560 REMOVE (GEN4MST_DEV,KEY=K$,DOM=4570)
4570 NEXT X
4580 REMOVE (SYS01_DEV,KEY=B0$,DOM=4600)
4600 REM " --- Clean GENMASK STBL's
4610 FOR X=1 TO 9
4620 LET X$=STBL("!CLEAR","GENMASK"+STR(X:"0"),ERR=4630)
4630 NEXT X
4690 GOTO 9900
4800 REM " --- Missing GEN4MST"
4810 DIM MESSAGE$[1]
4820 LET MESSAGE$[0]="Unable To Locate Or Open GEN4MST (<Enter>=Continue)"
4830 CALL "SYC.XA",1,MESSAGE$[ALL],0,-1,-1,V$,V3
4900 REM " --- All done"
4990 GOTO 9900
6000 REM " --- Store ADD+ON information in GEN format"
6010 LET VAR.NAME$[VARCOUNT]=V0$+FID(0)
6020 LET VAR.DESC$[VARCOUNT]=V4$
6030 LET VAR.VALUE$[VARCOUNT]=V2$
6040 LET VARCOUNT=VARCOUNT+1
6090 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

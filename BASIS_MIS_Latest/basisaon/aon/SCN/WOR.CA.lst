0010 REM "WOR - Work Order Time Sheet Register"
0020 REM "Program WOR.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "04Dec96 JJD: Register financial totals must match subsequent GL journal entries. (2130,2240,2410,2435,2440-70,2800,3045-80,6500-6580)"
0032 REM "18Mar99 KJS: Y2K year processing (455,463,8145-8190)
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=13
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-01",FILES$[2]="WOE-02",FILES$[3]="WOW-11"
0120 LET FILES$[4]="WOM-10",FILES$[5]="SYS-01",FILES$[6]="IVM-01"
0130 LET FILES$[7]="WOE-11",FILES$[8]="WOE-21",FILES$[9]="WOE-31"; REM "Must open batch before GLC.BA
0140 LET OPTIONS$[3]="C",OPTIONS$[7]="L",OPTIONS$[8]="L",OPTIONS$[9]="L"
0150 CALL "SYC.DA",1,1,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE01_DEV=CHANNELS[1],WOE02_DEV=CHANNELS[2],WOW11_DEV=CHANNELS[3]
0165 LET WOM10_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5],IVM01_DEV=CHANNELS[6]
0170 LET WOE11_DEV=CHANNELS[7],WOE21_DEV=CHANNELS[8],WOE31_DEV=CHANNELS[9]
0200 REM " --- IOLISTs"
0210 TIMESHEET: IOLIST A0$,A1$(1),A[ALL]
0220 WOE01A: IOLIST D$,D1$
0230 IVM01A: IOLIST C0$,C1$(1)
0240 WOW11A: IOLIST W0$,W[ALL]
0250 EMPCODE: IOLIST E$,E1$
0260 WOS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0265 IVS01A: IOLIST *,*,I9$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0285 BMS01A: IOLIST *,*,*,M4$,*,M4
0290 PRS01A: IOLIST X$,I1$,I2$,X$,X$,I3$
0295 WOM10A: IOLIST X0$,X1$
0400 REM " --- Parameters"
0405 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0410 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$,SOURCE$=PGM(-2)
0415 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0420 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0425 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0430 IF P3$(4,1)="Y" THEN FIND (SYS01_DEV,KEY=N0$+"PR00",DOM=9800)IOL=PRS01A
0435 IF P3$(4,1)="Y" THEN LET P1$=I1$,P2$=I2$,M0$=I3$
0440 IF P3$(1,1)="Y" THEN FIND (SYS01_DEV,KEY=N0$+"BM00",DOM=9800)IOL=BMS01A
0445 DIM P[4],G[4],DESC[3]
0450 FOR X=0 TO 4
0455 LET G[X]=NUM(G2$(X*2+1,2),ERR=0456)
0460 NEXT X
0463 LET G[2]=FNYY_YEAR(G2$(5,2))
0465 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(19,6)),P[3]=NUM(I9$(5,1)),IVS01_PRECISION=P[3]
0466 IF GL$="N" THEN GOTO 0485
0470 LET GL$="N",STATUS=0
0475 CALL "GLC.BA",ERR=0480,SOURCE$,N2$,GLW11$,GL$,STATUS
0480 IF STATUS<>0 THEN GOTO 9900
0485 FOR X=1 TO 3; LET DESC[X]=NUM(I9$((X-1)*2+9,2)); NEXT X
0500 REM " --- Init Data"
0505 LET H0=132,H=0,HEADINGS=2,WHEN$=F0$(7,3),CLOCK$="",L9=59,L=L9+1
0510 DIM J$(132,"-")
0515 DIM A[11],A1$(32),U[6],HEADINGS$[HEADINGS+1]
0520 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,M0=LEN(M0$),M4=LEN(M4$)
0525 LET M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),M9=130-M1*3-M2-M3*2-2
0530 LET X=POS("#"=M1$); IF X=0 THEN LET M9$=M1$ ELSE IF X=1 THEN LET M9$="##"+M1$ ELSE LET M9$=M1$(1,X-1)+"##"+M1$(X)
0535 LET P9$=P3$(25,1),ERRORS=0
0540 ON POS(P9$="DEW") GOTO 9900,0545,0550,0555
0545 LET WIDTH=10+M0,GLDATE=3,P0=3,P2=16,H3$="Date",H4$="Employee"; GOTO 0560
0550 LET WIDTH=M0,GLDATE=12,P0=9,P2=16,H3$="Employee",H4$="Date"; GOTO 0560
0555 LET WIDTH=17,GLDATE=10,P0=7,P2=14,H3$="Work Order",H4$="Date"
0560 DIM A0$(P2),A1$(30)
0600 REM " --- Additional File Opens"
0605 IF P3$(4,1)="Y" THEN LET FILES$[10]="PRM-01"
0610 IF P3$(4,1)<>"Y" THEN LET FILES$[10]="WOM-01"
0615 IF P3$(1,1)="Y" THEN LET FILES$[11]="BMM-08"
0620 IF P3$(1,1)<>"Y" THEN LET FILES$[11]="WOM-02"
0630 LET FILES$[12]="GLM-01",FILES$[13]=GLW11$,OPTIONS$[12]="F",OPTIONS$[13]="CL"
0650 CALL "SYC.DA",1,10,13,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0655 IF STATUS THEN GOTO 9900
0660 LET EMPCODE_DEV=CHANNELS[10],OPCODE_DEV=CHANNELS[11]
0665 LET GLM01_DEV=CHANNELS[12],GLW11_DEV=CHANNELS[13]
0670 IF P9$="D" THEN LET TIMESHEET_DEV=WOE11_DEV
0675 IF P9$="E" THEN LET TIMESHEET_DEV=WOE21_DEV
0680 IF P9$="W" THEN LET TIMESHEET_DEV=WOE31_DEV
0800 REM " --- Options"
0810 LET TITLE=1,PROMPT$="Do You Want To Print The "+N3$,PROMPT=2
0820 CALL "SYC.YN",TITLE,PROMPT$,PROMPT,V$,V3
0830 IF V$<>"Y" THEN GOTO 9900
0900 REM " --- Final Initializations"
0910 CALL "SYC.NB","Printing",WIDTH,COLUMN
0920 CALL "SYC.GA",7,1,"","",STATUS
0930 IF STATUS THEN GOTO 9900
1000 REM " --- Init Read"
1020 READ (TIMESHEET_DEV,KEY=N0$,DOM=1100)
1100 REM " --- Main Read"
1120 READ (TIMESHEET_DEV,END=4000)IOL=TIMESHEET
1140 IF A0$(1,2)<>N0$ THEN GOTO 4000
1160 IF A0$(P2-1)="00" THEN GOTO 1100
1200 REM " --- Breaks"
1220 IF A0$(1,P0+2)<>T0$ THEN GOSUB 6000
1240 IF A0$(1,P2-2)<>T1$ THEN GOSUB 6200
1300 IF P9$="D" THEN PRINT @(COLUMN,11),FNB$(A0$(3,3)),"  ",FNF$(A0$(6,P[0]),M0$),
1305 IF P9$="E" THEN PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$),
1310 IF P9$="W" THEN PRINT @(COLUMN,11),FNB$(A0$(10,3)),"  ",A0$(3,7),
1320 ON POS(P9$="DEW")-1 GOTO 1340,1360,1380
1340 LET E0$=A0$(6,9),D0$=A1$(7,7),D9$=A0$(3,3); GOTO 1400
1360 LET E0$=A0$(3,9),D0$=A1$(7,7),D9$=A0$(12,3); GOTO 1400
1380 LET E0$=A1$(7,9),D0$=A0$(3,7),D9$=A0$(10,3)
1400 REM " --- Additional Reads
1500 REM " --- Get Employee
1520 IF POS(E0$=E$)<>3 THEN GOSUB 6600
1540 IF POS(D0$=D$)<>5 THEN GOSUB 6700
1600 REM " --- Calculate Overhead
1690 PRECISION IVS01_PRECISION
2000 REM " --- Print"
2020 IF L+2>L9 THEN GOSUB 5200
2040 PRINT (7)A0$(P2-1),
2060 ON POS(P9$="DEW")-1 GOTO 2100,2200,2300
2100 REM " --- Dates
2120 PRINT (7)@(4),FNF$(A0$(6,P[0]),M0$),@(17),A1$(7,7),
2130 GOTO 2400
2200 REM " --- Employee
2220 PRINT (7)@(4),A1$(7,7),@(17),FNB$(A0$(12,3)),
2240 GOTO 2400
2300 REM " --- Work Order
2320 PRINT (7)@(4),FNF$(A1$(7,P[0]),M0$),@(17),FNB$(A0$(10)),
2400 PRINT (7)@(27),A1$(1,3),"  ",A1$(4,3),"  ",
2405 IF P3$(4,1)="Y" THEN PRINT (7)@(36),A1$(24,2),"  ",A1$(26,2),
2410 IF P3$(13,1)<>"Y" THEN GOTO 2435
2420 IF NUM(A1$(16,4),ERR=2430)>0 THEN PRINT (7)@(M9-12),FNG$(A1$(16)),
2430 IF NUM(A1$(20,4),ERR=2440)>0 THEN PRINT (7)@(M9-6),FNG$(A1$(20)),
2435 GOSUB 6500
2440 PRINT (7)@(M9),A[0]:M2$,A[1]:M3$,DIRECT_TOTAL:M1$,A[2]:M3$,OH_TOTAL:M1$,TRANS_TOTAL:M9$
2450 LET S[0]=S[0]+A[0]+A[4],S[2]=S[2]+DIRECT_TOTAL,S[4]=S[4]+OH_TOTAL,S[5]=S[5]+TRANS_TOTAL,S[6]=S[6]+A[5]
2460 LET T[0]=T[0]+A[0]+A[4],T[2]=T[2]+DIRECT_TOTAL,T[4]=T[4]+OH_TOTAL,T[5]=T[5]+TRANS_TOTAL,T[6]=T[6]+A[5]
2470 LET U[0]=U[0]+A[0]+A[4],U[2]=U[2]+DIRECT_TOTAL,U[4]=U[4]+OH_TOTAL,U[5]=U[5]+TRANS_TOTAL,U[6]=U[6]+A[5]
2500 ON POS(P9$="DEW")-1 GOTO 2520,2540,2560
2520 PRINT (7)@(4),E1$,@(30),D1$,
2530 GOTO 2580
2540 PRINT (7)@(4),D1$,
2550 GOTO 2580
2560 PRINT (7)@(4),E1$,
2580 PRINT (7)@(M9-M2),A[5]:M2$,A[4]:M2$
2600 LET L=L+2,L1=L1+1,L2=L2+1
2670 PRECISION IVS01_PRECISION
2700 REM " --- Post To Summary
2720 DIM W0$(30),W[5]
2740 LET W0$(1)=N0$+"  "+D0$+A1$(1,3)+D9$
2760 FIND (WOW11_DEV,KEY=W0$(1,17),DOM=2800)IOL=WOW11A
2800 LET W[0]=W[0]+A[0],W[1]=W[1]+DIRECT_TOTAL,W[2]=W[2]+OH_TOTAL,W[3]=W[3]+TRANS_TOTAL,W[4]=W[4]+A[4],W[5]=W[5]+A[5]
2820 WRITE (WOW11_DEV,KEY=W0$(1,17))IOL=WOW11A
3000 REM " --- Generate G/L summary"
3010 IF GL$<>"Y" THEN GOTO 3900
3020 FIND (WOM10_DEV,KEY=N0$+"A"+D$(12,2))IOL=WOM10A
3030 LET GLDATE$=A0$(GLDATE,3)
3040 IF P9$<>"D" THEN GOSUB 6800
3100 REM " --- Post to General Ledger "
3105 PRECISION 2
3110 LET ACCOUNT$=X1$(1,G[4]),MEMO$=E1$,REF1$=FNF$(E0$(1,P[0]),M0$)
3120 LET TOTAL=TRANS_TOTAL,REF2$="WO "+D$(5,7)
3130 LET REF3$="Step "+A1$(1,3),AMOUNT=TOTAL,UNITS=0
3140 GOSUB GLRECAP
3150 LET ACCOUNT$=X1$(2*G[4]+1,G[4]),AMOUNT=-DIRECT_TOTAL,UNITS=A[0]
3160 GOSUB GLRECAP
3170 LET ACCOUNT$=X1$(3*G[4]+1,G[4]),AMOUNT=-(TOTAL+AMOUNT),UNITS=A[0]
3180 GOSUB GLRECAP
3190 PRECISION IVS01_PRECISION
3900 REM " --- Loop back for next record"
3990 GOTO 1100
4000 REM " --- End"
4005 LET T9$="U"
4010 GOSUB 6000
4020 IF L+4>L9 THEN GOSUB 5000
4040 PRINT (7)@(M9),J$(1,132-M9),'LF',@(0),"Grand Totals: ",@(M9),U[0]:M2$,@(M9-2+M2+M3),U[2]:M9$,@(M9-2+M1+M2+M3*2),U[4]:M9$,U[5]:M9$
4050 DIM J2$(132-M9,"=")
4060 PRINT (7)@(M9),J2$
4100 RUN "WOR.CB"
5000 REM " --- Heading"
5010 LET L=HEADINGS+5
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 IF P3$(4,1)="Y" THEN PRINT (7)@(36),"Pay Ttl",
5105 IF P3$(13,1)="Y" THEN PRINT (7)@(M9-12),"Start Stop",
5110 PRINT (7)@(27),"Step Op",@(M9+M2-8),"Run Hrs",@(M9+M2+M3-6),"Direct",@(M9+M2+M3+M1-7),"Direct",@(M9+M2+M3*2+M1-6),"Ov'hd",@(M9+M1*2+M2+M3*2-6),"Ov'hd",@(123),"Expense"
5112 IF P3$(4,1)="Y" THEN PRINT (7)@(36),"Cde Cde",
5120 PRINT (7)@(0),"Seq",@(27),"Code Cd",@(M9-11),"Cmplt Qty",@(M9+M2-6),"Setup",@(M9+M2+M3-5),"Rate",@(M9+M2+M3+M1-6),"Total",@(M9+M2+M3*2+M1-5),"Rate",@(M9+M1*2+M2+M3*2-6),"Total",@(124),"Total",
5130 ON POS(P9$="DEW")-1 GOTO 5140,5150,5160
5140 PRINT (7)@(4),"Employee",@(17),"W.O.",; GOTO 5190
5150 PRINT (7)@(4),"Work Order",@(19),"Date",; GOTO 5190
5160 PRINT (7)@(4),"Employee",@(19),"Date",; GOTO 5190
5190 PRINT (7)'LF'
5195 RETURN 
5200 REM " --- Sub Heading
5210 IF L+2>L9 THEN GOSUB 5000
5220 ON POS(P9$="DEW")-1 GOTO 5230,5250,5270
5230 PRINT (7)"Date: ",FNB$(T0$(3,3))
5235 LET GLDATE$=T0$(3,3)
5240 GOSUB 6800
5245 GOTO 5280
5250 PRINT (7)"Employee: ",FNF$(T0$(3,P[0]),M0$),"  ",E1$; GOTO 5280
5270 PRINT (7)"Work Order: ",T0$(3,7),"  ",D1$
5280 PRINT (7)'LF',
5285 LET L=L+2
5290 RETURN 
6000 REM " --- First Sub Total
6010 IF T0$="" THEN GOTO 6100
6020 IF T9$="" THEN LET T9$="T"
6030 GOSUB 6200
6040 IF T9$="T" THEN LET T9$="",T1$=""
6050 IF L2<=1 THEN GOTO 6100
6060 IF L+3>L9 THEN GOSUB 5200
6080 PRINT (7)@(M9-M2),J$(1,M2),@(M9),J$(1,132-M9),'LF',@(10),H3$," Totals: ",@(M9-M2),T[6]:M2$,@(M9),T[0]:M2$,@(M9-2+M2+M3),T[2]:M9$,@(M9-2+M1+M2+M3*2),T[4]:M9$,T[5]:M9$,'LF'
6085 LET L=L+3
6100 IF T9$<>"" THEN RETURN 
6120 DIM T[6]
6140 LET T0$=A0$(1,P0+2),L2=0
6160 IF P9$="E" THEN LET E0$=T0$(3,P0); GOSUB 6600
6170 IF P9$="W" THEN LET D0$=T0$(3,P0); GOSUB 6700
6180 GOSUB 5200
6190 RETURN 
6200 REM " --- Second Sub Total
6210 IF T1$="" THEN GOTO 6300
6220 IF L1<=1 THEN GOTO 6280
6240 IF L+2>L9 THEN GOSUB 5200
6260 PRINT (7)@(M9-M2),J$(1,M2),@(M9),J$(1,132-M9),'LF',@(20),H4$," Totals: ",@(M9-M2),S[6]:M2$,@(M9),S[0]:M2$,@(M9-2+M2+M3),S[2]:M9$,@(M9-2+M1+M2+M3*2),S[4]:M9$,S[5]:M9$
6270 LET L=L+2
6280 PRINT (7)""
6290 LET L=L+1
6300 IF T9$<>"" THEN RETURN 
6320 DIM S[6]
6340 LET T1$=A0$(1,P2-2),L1=0
6350 IF P9$="D" THEN LET E0$=T1$(6,9); GOSUB 6600
6390 RETURN 
6500 REM " --- Calculate Transaction Direct & Overhead Totals "
6510 LET DIRECT_HRS=ROUND(A[1]*A[0],2)
6520 LET DIRECT_SET=ROUND(A[1]*A[4],2)
6530 LET DIRECT_TOTAL=DIRECT_HRS+DIRECT_SET
6540 LET OH_HRS=ROUND(A[2]*A[0],2)
6550 LET OH_SET=ROUND(A[2]*A[4],2)
6560 LET OH_TOTAL=OH_HRS+OH_SET
6570 LET TRANS_TOTAL=DIRECT_TOTAL+OH_TOTAL
6580 RETURN 
6600 REM " --- Get Employee
6605 DIM E1$(30); LET E1$(1)="*** Not On File ***"
6610 FIND (EMPCODE_DEV,KEY=N0$+E0$,DOM=6620)IOL=EMPCODE
6620 DIM E9$(30)
6640 LET E9$(1)=FNP$(E1$(1,16))+", "+E1$(17,14)
6660 LET E1$=E9$
6680 RETURN 
6700 REM " --- Get Work Order
6710 DIM D1$(30); LET D1$(1)="*** Not On File ***"
6720 FIND (WOE01_DEV,KEY=N0$+"  "+D0$,DOM=6730)IOL=WOE01A
6730 LET D1$=D1$(1,30)
6740 IF D$(14,1)<>"I" THEN GOTO 6790
6750 DIM C1$(60)
6760 FIND (IVM01_DEV,KEY=N0$+D$(56),DOM=6790)IOL=IVM01A
6770 LET D1$(1)=C1$(1,MIN(DESC[1],30))
6790 RETURN 
6800 REM " --- Check transaction date"
6810 IF GL$<>"Y" THEN GOTO 6890
6820 CALL "GLC.EA",GLDATE$,"N",PERIOD$,YEAR$,GLSTATUS
6830 IF GLSTATUS>0 THEN CALL "GLC.GA",7,GLDATE$,GLSTATUS,L,ERRORS,STATUS
6890 RETURN 
6900 REM " --- Standard G/L Recap Routine"
6910 GLRECAP: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLW11_DEV,GLT05_DEV,ACCOUNT$,GLDATE$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8070 DEF FNG$(Q$)=Q$(1,2)+":"+Q$(3,2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

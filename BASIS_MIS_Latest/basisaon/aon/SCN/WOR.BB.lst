0010 REM "WOS - Work Order Pick Slip - Printing                        "
0020 REM "Program:  WOR.BB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 15Dec97 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "05Nov96 JJD: Accomodate larger mask size. (5090,5250) (Needs associated 05Nov96 WOR.BA SCN)"
0032 REM "05Nov96 JJD: Correct step number increment.(1110,5010)"
0033 REM "05Nov96 JJD: Laser pagination. (5010,5110,5120,5130,5150,5170,5340)"
0034 REM "13Nov96 JJD: Correct progress display & "Clearing..." display. (710,2060,4060)"
0035 REM "11Jun97 WGH: Remove duplicate WOE-25 Material Issues records (4130,4325,6000-6090)
0036 REM "31Mar98 KJS: Never reprint Pick List when there are Issues against the Work Order.  Requires 6.0.5 WOE.BA SCN. (1230,1240,1430,4300,4325,6000-6090)"
0037 REM "31Oct98 WGH: Correct line count (525-530,5150-5175,5240-5255,5340)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0095 PRECISION IVS01_PRECISION
0200 REM " --- IOLISTs"
0210 WOE13A: IOLIST A0$,A1$
0220 WOE23B: IOLIST W0$,W1$,W[ALL]
0230 WOE01A: IOLIST B0$,B1$,B[ALL]
0240 ARM01A: IOLIST *,C1$(1)
0250 IVM01A1: IOLIST D0$,D1$(1),D2$
0260 IVM01A2: IOLIST *,DESCRIPTION$(1)
0270 IVM02A: IOLIST I0$,I1$,I2$,I[ALL]
0280 WOE15A: IOLIST A0$,A1$
0290 WOE22B: IOLIST WO0$,WO1$,WO[ALL]
0300 IVM10C: IOLIST Y0$(1)
0310 WOM10A: IOLIST Z0$(1)
0330 WOE25A: IOLIST R0$,W1$,W[ALL]
0500 REM " --- Init Data"
0510 DIM W[4],B[11],WO[13]
0520 LET M9=80-M2*3
0525 IF M2+23+P[0]>M9 THEN LET SPLITLINE=1
0530 IF SPLITLINE OR M2+23+P[0]<M9-M2 THEN LET M9=M9-M2,M9$="Y"
0540 LET STEPNUM=1
0600 REM " --- Additional File Opens "       
0610 CALL "SYC.GA",7,1,"","",STATUS
0615 IF STATUS THEN GOTO 9900
0620 LET V=5
0700 REM " --- Background"
0705 PRINT @(1,10),'CL',@(1,11),'CL',@(1,12),'CL'
0710 CALL "SYC.NB","Printing: ",23,COLUMN
1000 REM " --- Init Read"
1020 READ (WOE04_DEV,KEY=N0$+"  "+"C",DOM=1100)
1100 REM " --- Main Read"
1110 LET OLD_OP_STEP$="",STEPNUM=1
1120 LET K$=KEY(WOE04_DEV,END=4000)
1130 IF K$(1,5)<>N0$+"  "+"C" THEN GOTO 4000
1140 READ (WOE04_DEV)
1200 REM " --- Get Work Order
1210 READ (WOE13_DEV,KEY=K$(1,4)+K$(6)+"000",DOM=1120)IOL=WOE13A
1220 READ (WOE01_DEV,KEY=A0$(1,11),DOM=1120)IOL=WOE01A
1230 FIND (WOE04_DEV2,KEY=A0$(1,4)+"D"+A0$(5,7),DOM=1300)
1240 GOTO 1120
1300 REM " --- Find Customer
1310 DIM C1$(30)
1315 LET C1$(1)="*** Not On File ***"
1320 IF POS(" "<>B0$(34,6))=0 THEN GOTO 1400
1330 FIND (ARM01_DEV,KEY=N0$+B0$(34,6),DOM=1400)IOL=ARM01A
1400 REM " --- Find Warehouse
1410 IF Y1$=A1$(49,2) THEN GOTO 1460
1420 DIM Y0$(25)
1430 LET Y0$(6)="*** Not On File ***"
1440 FIND (IVM10_DEV,KEY=N0$+"C"+A1$(49,2),DOM=1460)IOL=IVM10C
1450 LET Y1$=A1$(49,2)
1460 REM " --- Type
1470 IF Z1$=A1$(1,2) THEN GOTO 1600
1480 DIM Z0$(25)
1490 LET Z0$(6)="**** Not On File ****"
1500 FIND (WOM10_DEV,KEY=N0$+"A"+A1$(1,2),DOM=1600)IOL=WOM10A
1600 REM " --- Category
1610 IF A1$(3,1)="I" THEN LET Z2$="Inventory"
1620 IF A1$(3,1)="N" THEN LET Z2$="Non-Stock"
1630 IF A1$(3,1)="R" THEN LET Z2$="Recurring"
1640 LET H=0
1650 PRINT @(40,5),A0$(5,7),
2000 REM " --- Details Here
2010 READ (WOW13_DEV,KEY=A0$(1,11),DOM=2020)
2020 LET HEAD_COMM$=""
2030 GOSUB 5700
2040 LET K$=KEY(WOW13_DEV,END=1100)
2050 IF K$(1,11)<>A0$(1,11) THEN GOTO 1100
2060 PRINT @(COLUMN,11),K$(7),
2070 LET OP_STEP$=K$(12,3)
2080 LET K$=K$(1,11)+K$(25)
2090 READ (WOW13_DEV)
2100 READ (WOE23_DEV,KEY=K$,DOM=2040)IOL=WOE23B
2110 DIM D1$(60)
2120 LET D1$(1)="*** Not On File ***"
2130 FIND (IVM01_DEV,KEY=N0$+W1$(21,20),DOM=2140)IOL=IVM01A1
2140 LET DX$=D1$
2150 DIM I[4]
2160 FIND (IVM02_DEV,KEY=N0$+W1$(19,22),DOM=2170)IOL=IVM02A
2170 LET I[2]=I[2]-(W[0]-W[1]); REM "ADD BACK IN THIS ORDER...
2180 GOSUB 5200
2190 LET STEPNUM=STEPNUM+1
2200 GOTO 2040
4000 REM " --- End"
4010 CLOSE (7,ERR=4020)
4015 FOR I=10 TO 12; PRINT @(0,I),'CL'; NEXT I
4020 LET V4$="Did The Print Lists Pick Correctly "
4030 CALL "SYC.YN",1,V4$,1,V$,V3
4040 IF V$="N" THEN GOTO 9900
4050 IF V$<>"Y" THEN GOTO 4020
4060 PRINT @(27,7),"Now Clearing Print File...",'CL',
4070 READ (WOE04_DEV,KEY=N0$+"  "+"C",DOM=4080)
4080 LET K$=KEY(WOE04_DEV,END=9900)
4090 IF K$(1,5)<>N0$+"  "+"C" THEN GOTO 9900
4100 READ (WOE04_DEV)
4110 READ (WOE13_DEV,KEY=K$(1,4)+K$(6)+"000",DOM=4080)IOL=WOE13A
4120 WRITE (WOE15_DEV,KEY=A0$)IOL=WOE15A
4200 REM " --- Remove Committed and Create Issue
4210 LET X0=0
4220 READ (WOW13_DEV,KEY=A0$(1,11),DOM=4240)
4240 LET K1$=KEY(WOW13_DEV,END=4400)
4250 IF K1$(1,11)<>A0$(1,11) THEN GOTO 4400
4260 LET K1$=K1$(1,11)+K1$(25)
4265 READ (WOW13_DEV)
4270 READ (WOE23_DEV,KEY=K1$,DOM=4240)IOL=WOE23B
4300 REM " --- Renumber Issue Sequence to Match Pick List"
4310 LET X0=X0+1
4320 LET R0$=W0$(1,11)+STR(X0:"000"),W1$(7,3)=W0$(12,3)
4330 WRITE (WOE25_DEV,KEY=R0$)IOL=WOE25A
4390 GOTO 4240
4400 REMOVE (WOE04_DEV,KEY=K$)
4490 GOTO 4080
5000 REM " --- Headings "
5010 LET L=HEADINGS+3
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 DIM DESCRIPTION$(60)
5050 FIND (IVM01_DEV,KEY=N0$+A1$(51),DOM=5060)IOL=IVM01A2
5060 IF A1$(3,1)="I" THEN LET DESC1$=DESCRIPTION$(1,MIN(DESC[1],41)),DESC2$=DESCRIPTION$(DESC[1]+1,MIN(DESC[2],42))
5070 IF A1$(3,1)<>"I" THEN LET DESC1$=B1$(1,30),DESC2$=B1$(31,30)
5080 LET OLD_OP_STEP$=OP_STEP$
5090 PRINT (7)@(0),"WO Number: ",A0$(5,7),@(21),"Type: ",A1$(1,2)," ",Z0$(6),@(53),"Category: ",A1$(3,1)," ",Z2$,'LF',@(2),"Op Step: ",OP_STEP$; LET L=L+2
5100 IF A1$(3,1)="I" THEN PRINT (7)@(0),"Bill Number:  ",A1$(51); LET L=L+1
5110 PRINT (7)@(0),"Description:",@(14),DESC1$,@(56),"Open Date: ",@(67),FNB$(B0$(16,3)),'LF',@(14),DESC2$,@(55),"Start Date: ",@(67),FNB$(B0$(19,3)),'LF',@(0),"Drawing #: ",B1$(61,25),@(37),"Rev: ",B1$(86,5),@(50),"Completion Date:",@(67),FNB$(B0$(22,3)),'LF'; LET L=L+3
5120 IF POS(" "<>B0$(34,6))>0 THEN PRINT (7)@(0),"Customer: ",FNF$(B0$(34,C0),C3$),"  ",C1$,@(62),"Order: ",B0$(40,7),"-",B0$(47,3),'LF'; LET L=L+1
5130 PRINT (7)@(0),"Warehouse: ",A1$(49,2)," ",Y0$(6),@(62-M2),"Production Qty: ",B[0]-B[2]:M2$,'LF',J$; LET L=L+2
5140 PRINT (7)@(M9+M2-4),"On",
5150 IF M9$="Y" THEN PRINT (7)@(M9+M2*4-4),"On" ELSE PRINT (7)""
5160 PRINT (7)"Step Location",@(14+M2-6),"Req'd",@(23+M2-8),"Picked",@(22+M2),"Item",@(M9+M2-5),"Hand",@(M9+M2*2-6),"Com'd",@(M9+M2*3-6),"Avail",
5170 IF M9$="Y" THEN PRINT (7)@(M9+M2*4-6),"Order",'LF' ELSE PRINT (7)'LF'
5175 LET L=L+3
5180 GOSUB 5800
5190 RETURN 
5200 REM " --- Details "
5210 LET X1=0
5220 IF I3$(17,1)="S" AND D2$(19,2)="YY" THEN LET X1=W[0]-W[1],X9$="Serial #:"
5230 IF I3$(17,1)="L" AND D2$(19,2)="YY" THEN LET X1=5,X9$="Lot #:"
5240 IF L+2+SPLITLINE>L9 OR OP_STEP$<>OLD_OP_STEP$ THEN GOSUB 5000
5250 PRINT (7)@(0),STEPNUM:"000"," ",I1$(1,10),@(14),W[0]-W[1]:M2$,J1$(1,7),@(22+M2),W1$(21),
5255 IF SPLITLINE THEN PRINT (7)""
5260 PRINT (7)@(M9),I[0]:M2$,I[2]:M2$,I[0]-I[2]:M2$,
5270 IF M9$="Y" THEN PRINT (7)I[1]:M2$,
5280 PRINT (7)""
5290 DIM WO1$(151)
5300 READ (WOE22_DEV,KEY=W0$(1,11)+"B"+W0$(12,3),DOM=5310)IOL=WOE22B
5310 DIM DESC$(60)
5320 LET DESC$(1)=FNITEM$(DX$,DESC[1],DESC[2],DESC[3])
5330 PRINT (7)@(5),"WO Seq: ",W0$(12,3),@(25+M2),DESC$(1,MIN(DESC[1]+DESC[2]+DESC[3]+2,79-(25+M2)))
5340 LET L=L+2+SPLITLINE
5350 GOSUB 5600
5360 IF POS(I3$(17,1)="LS")=0 THEN GOTO 5430
5370 IF D2$(19,2)<>"YY" THEN GOTO 5430
5380 FOR X=1 TO X1
5390 IF L>L9 THEN GOSUB 5000
5400 PRINT (7)@(14),X9$,"_____________________________"
5410 LET L=L+1
5420 NEXT X
5430 RETURN 
5600 REM " --- Get comments for this material
5610 LET K13$=KEY(WOE22_DEV,END=5690)
5620 IF POS(A0$(1,11)+"B"=K13$)<>1 THEN GOTO 5690
5630 READ (WOE22_DEV)IOL=WOE22B
5640 IF WO1$(91,1)<>"M" THEN GOTO 5690
5650 IF L+1>L9 THEN GOSUB 5000
5660 PRINT (7)@(14),WO1$(92,60)
5670 LET L=L+1
5680 GOTO 5610
5690 RETURN 
5700 REM " --- Get header comments for this Work Order"
5710 READ (WOE22_DEV,KEY=A0$(1,11)+"B",DOM=5720)
5720 LET K13$=KEY(WOE22_DEV,END=5790)
5730 IF POS(A0$(1,11)+"B"=K13$)<>1 THEN GOTO 5790
5740 READ (WOE22_DEV)IOL=WOE22B
5750 IF WO1$(91,1)<>"M" THEN GOTO 5790
5760 LET HEAD_COMM$=HEAD_COMM$+WO1$(92,60)
5780 GOTO 5720
5790 RETURN 
5800 REM " --- Print Header Comments"
5810 IF LEN(HEAD_COMM$)=0 THEN GOTO 5890
5820 FOR X=1 TO LEN(HEAD_COMM$) STEP 60
5830 PRINT (7)@(14),HEAD_COMM$(X,60)
5840 LET L=L+1
5850 NEXT X
5860 IF L>L9 THEN LET L=L9-1
5890 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "POE - Purchase Order Receipt Entry (Line Items)"
0020 REM "Program POE.DC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 15Dec97 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "07Jun96 JAL: Allow input of order info for special orders and drop shipments (2240,3510,6830)"
0032 REM "12Jul96 JAL: Allow negative order/receipt quantities (2735,2740,2850,2885,2985)
0033 REM "13Aug96 WGH: <F3>=Lot/Serial # infinite loop when received quantity zero (3730)"
0034 REM "21Aug96 WGH: For V line types, initialize vendor part number based on last entered item (250,515,2095,2425)"
0035 REM "28Aug96 WGH: Reset Unit Cost when changed during lot/serial entry (120)"
0036 REM "05Sep96WGH: On-Order qnty is only maintainable via PO Entry (2705,6640)"
0037 REM "18Sep96 JAL: Redisplay contact, phone and fax on <F2> to ribbon (4220,4260)"
0038 REM "29Jan96 Caj: Assign full Line Code to key variable for POM-02, Line Codes file (5010)
0039 REM "19Feb97 Caj: Check for missing lot/serial numbers (360,550,4100-4230,6200-6490)
0040 REM "5May97 JWK: Prevent Error 47 on Line 6280 if 1st line Non-Stock (6270)
0041 REM "02Jun97 WGH: Line code, warehouse, item, unit of measure and conversion factor are only maintainable via PO Entry (1505,1805,2005,2305,2405,2505,2605)"
0042 REM "23Sep98 KJS: On-Order qty not correct when POE-12 line missing or new line deleted (1140-1160,1275-1295,1360,1415,1430,1440,1505,1805,2005,2305,2405,2505,2605,2705,3650,3665,3930,4080,6610-6640)
0050 REM 
0070 SETERR 9000
0080 SETESC 9000
0090 PRECISION I[1]
0100 REM " --- Return From Lot/Serial Number Entry?"
0105 IF O0<>4 THEN GOTO 0150
0110 LET STRT_W7=W[7],L=L-2,X=W[1]/W[0]
0120 IF UCOST<>X THEN LET W[1]=UCOST*W[0]
0130 GOSUB 5000
0140 GOTO 3600
0200 REM " --- IOLIST's"
0210 POE14A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1),W5$,W6$,W[ALL]
0220 IVM01A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1),B4$(1),B5$,B6$(1),B[ALL]
0230 IVM02A: IOLIST C0$(1),C1$(1),C2$,C[ALL]
0240 POE12A: IOLIST D0$(1),D1$,D2$,D3$,D4$,D5$,D6$,D[ALL]
0250 IVM05A: IOLIST *,*,U2$(1)
0310 POM02A: IOLIST Y0$(1),Y1$(1)
0320 IVM10C: IOLIST X0$(1)
0340 POE44A: IOLIST X4$(1)
0360 POE24A: IOLIST *,*,*,LSQTYRCVD
0500 REM " --- Initializations"
0510 DIM DESCRIPTION$(48),REFS[20],REFS$[20],C0$(24),C1$(64),C[20]
0515 DIM U2$(20),Y0$(4),Y1$(32),X0$(160),FILES[99],PARAMS[10],PARAMS$[10]
0520 DIM W0$(25),W1$(48),W2$(32),W3$(22),W4$(40),W[12],ITEM$[6],ITEM[12]
0550 LET PO=0,W0$(1)=A0$,L=LINE,L9=19,TOTAL=0,CHECKLS=1
0700 REM " --- Background"
0710 PRINT @(O[5]-8,L0-3),'SB'," Total: ",'SF',TOTAL:M1$,@(0,L0-2),'CE','SB',"Seq Cd Wh Item",@(O[0]),"UM",@(O[1]+M5-5),"Conv",@(O[2]+M2-8),"Qty Ord",@(O[3]+M2-6),"Rec'd",@(O[4]+M3-5),"Cost",@(O[5]+M1-10),"Extension",'SF',
0900 REM " --- Display any existing lines"
0910 GOSUB 6000
0920 GOSUB 5600
0930 IF NUMBER=0 THEN GOTO 1200
0990 GOTO 4000
1000 REM " --- Overflow"
1010 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L0-1),'LD','LD',
1020 IF L>L9 THEN LET L=L9
1030 LET LOTSER$="N"
1100 REM " --- Get Next Key"
1110 LET K$=KEY(POE14_DEV,END=1200)
1120 IF POS(A0$=K$)<>1 THEN GOTO 1200
1130 READ (POE14_DEV)IOL=POE14A
1140 LET POE12_EXISTS=0
1150 FIND (POE12_DEV,KEY=W0$(1,8)+W0$(16,10),DOM=1190)
1160 LET POE12_EXISTS=1
1190 GOTO 1330
1200 REM " --- Sequence Number"
1210 PRINT @(0,21),'CL',
1220 LET NUMBER=NUM(W0$(23))+INCREMENT
1230 LET V4$="Enter A 3 Digit Sequence Number"
1240 LET V0$="Z",V1$="KERC",V2$=STR(NUMBER),V3$="",V0=3,V1=0,V2=L,I0=0
1250 GOSUB 7000
1260 IF V3=4 THEN PRINT @(V1,L),FILL(3),; GOTO 4000
1270 IF V$="000" OR V$="" THEN GOTO 1200
1275 REM " --- Does a corresponding P/O Entry Detail record exist?
1280 DIM D0$(18),D[12]
1285 LET D0$(1)=W0$(1,8)+W0$(16,7)+V$,POE12_EXISTS=0
1290 FIND (POE12_DEV,KEY=D0$,DOM=1300)IOL=POE12A
1295 LET POE12_EXISTS=1
1300 REM " --- Display record"
1310 LET W0$(23)=V$,C1$(1)="",NEWLINE=0
1320 FIND (POE14_DEV,KEY=W0$,DOM=1400)IOL=POE14A
1330 IF W0$<=K9$ THEN GOSUB 5900
1340 GOSUB 5000
1350 GOSUB 5800
1360 LET STRT_W7=W[7],STRT_W3=W[3]
1390 GOTO 3300
1400 REM " --- New Record"
1410 DIM W1$(48),W2$(32),W3$(22),W4$(40),W[12]
1415 IF POE12_EXISTS THEN LET W1$=D1$,W2$=D2$,W3$=D3$,W4$=D4$,W5$=D5$,W6$=D6$,W[0]=D[0],W[1]=D[1],W[2]=D[2],W[3]=D[3],W[4]=D[7]; GOTO 1440; REM "Re-enter a deleted line
1420 LET W1$(1,2)=P3$(56,2),W1$(3,6)=A1$(23,6),W1$(9,6)=A1$(11,6)
1430 LET W1$(15,6)=A1$(17,6),W3$(1,2)=A1$(1,2)
1440 LET Y0$(1)=N0$+W1$(1,2),X0$(1)=N0$+"C"+W3$(1,2),LOTSER$="N",STRT_W7=W[7],STRT_W3=W[3],NEWLINE=1
1450 PRINT @(4,L),'CL',@(4,L+1),'CL',
1460 FIND (POM02_DEV,KEY=Y0$,DOM=1600)IOL=POM02A
1470 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=1800)IOL=IVM10C
1480 GOSUB 5000
1490 GOTO 2000
1500 REM " --- Line Code"
1505 IF POE12_EXISTS THEN LET V3=0; GOTO 3200
1510 PRINT @(0,21),'CL',
1520 LET V4$="Enter A Valid Line Code (<F3>=Lookup)",I0=1
1530 LET V0$="S",V1$="C",V2$=W1$(1,2),V3$="",V0=2,V1=4,V2=L
1540 GOSUB 7000
1550 ON V3 GOTO 1600,1500,3200,1560,1200
1560 CALL "POC.LK",1,V1,V2,V$
1570 IF V$="" THEN GOTO 1500
1600 REM " --- Valid line code?"
1610 LET Y0$(1)=N0$+V$
1620 FIND (POM02_DEV,KEY=Y0$,DOM=1500)IOL=POM02A
1630 PRINT @(0,21),'CL','SB',"Line Code: ",'SF',Y0$(3)," ",Y1$(1,20),@(38),'SB',"Dropship: ",'SF',Y1$(22,1),@(54),'SB',"Lead Time: ",'SF',Y1$(23,1),
1640 IF Y1$(22,1)<>"Y" THEN GOTO 1700
1650 LET ACTION$="I"
1660 GOSUB 6800
1670 IF STATUS THEN GOTO 1500
1700 REM " --- Changed line code?"
1710 IF Y0$(3,2)=W1$(1,2) THEN GOTO 1750
1720 DIM W4$(40),W[14]
1730 LET W1$(22)="",W3$(3)=""
1735 IF Y1$(22,1)<>"Y" THEN LET W2$(11)=""
1740 PRINT @(10,L),'CL',@(10,L+1),'CL',
1750 LET W1$(1,2)=Y0$(3),W1$(21,1)=Y1$(23,1)
1760 PRINT @(V1,V2),W1$(1,2),
1770 GOSUB 5000
1790 GOTO 3200
1800 REM " --- Warehouse ID (All Lines)"
1805 IF POE12_EXISTS THEN GOTO 3200
1810 LET V4$="Enter A Valid Warehouse ID (<F3>=Lookup)",I0=2
1820 LET V0$="S",V1$="CR",V2$=W3$(1,2),V3$="",V0=2,V1=7,V2=L,V$=V2$
1830 IF I3$(1,1)="N" THEN GOTO 1900
1840 GOSUB 7000
1850 ON V3 GOTO 1900,1800,3200,1860,1800
1860 CALL "IVC.LK",3,V1,V2,V$
1870 IF V$="" THEN GOTO 1800
1900 REM " --- Display warehouse"
1910 LET X0$(1)=N0$+"C"+V$
1920 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=1800)IOL=IVM10C
1930 LET W3$(1,2)=X0$(4,2)
1940 PRINT @(0,21),'CL','SB',"Warehouse: ",'SF',X0$(4,2)," ",X0$(6,24),
1950 IF W3$(1,2)=A1$(1,2) THEN GOTO 3200
1960 LET MESSAGE$[0]="This Warehouse Does Not Match Header Ship-to (<Enter>=Continue):"
1970 CALL "SYC.XA",2,MESSAGE$[ALL],0,6,-1,V$,V3
1990 GOTO 3200
2000 REM " --- Item Number (S Lines)"
2005 IF POE12_EXISTS THEN GOTO 3200
2010 IF Y1$(21,1)<>"S" THEN GOTO 3200
2020 LET FILES[1]=IVM01_DEV,FILES[14]=IVM14_DEV,PARAMS[0]=I[0]
2030 LET PARAMS[1]=I[3],PARAMS[2]=I[4],PARAMS[3]=I[5]
2035 LET V0$="",V2$=W3$(3),V4$="",V1=10,V2=L,I0=3
2040 CALL "IVC.IA",V0$,V1,V2,FILES[ALL],N0$,V2$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
2045 ON V3 GOTO 2050,2000,3200,2000,2000
2050 REM " --- Retrieve warehouse record"
2060 DIM C0$(24),C1$(64),C[20]
2070 LET C0$(1)=N0$+W3$(1,2)+ITEM$[0]
2080 IF C0$(3)=W3$ THEN GOTO 2200
2090 FIND (IVM02_DEV,KEY=C0$,DOM=2000)IOL=IVM02A
2095 LET U2$(1)=""; FIND (IVM05_DEV,KEY=A0$(1,8)+C0$(5),DOM=2100)IOL=IVM05A
2100 REM " --- Set defaults if warehouse/item has changed"
2110 DIM W[12]
2120 LET W1$(22,2)=ITEM$[2](6,2),W3$(1)=C0$(3),W[0]=ITEM[0]
2130 IF FNP$(W1$(24,10))="" THEN LET W1$(24,10)="Stock"
2140 IF W[0]=0 THEN LET W[0]=1
2150 LET W[1]=W[0]*C[11]
2200 REM " --- Display line"
2210 GOSUB 5000
2220 GOSUB 5800
2230 IF C1$(60,1)<>"Y" THEN GOTO 3200
2240 LET ACTION$=""
2250 GOSUB 6800
2260 GOSUB 5800
2290 GOTO 3200
2300 REM " --- Non-Stock Item Number (N Lines)"
2305 IF POE12_EXISTS THEN GOTO 3200
2310 IF Y1$(21,1)<>"N" THEN GOTO 3200
2320 LET V4$="Enter The Non-Stock Item Number For This Line"
2330 LET V0$="S",V1$="CR",V2$=W3$(3),V3$="",V0=I[0],V1=10,V2=L
2340 GOSUB 7000
2350 ON V3 GOTO 2360,2300,3200,2300,2300
2360 LET W3$(3)=V$
2390 GOTO 3200
2400 REM " --- Vendor's Item Number (V Lines)/Message (M/N/O Lines)"
2405 IF POE12_EXISTS THEN GOTO 3200
2410 IF POS(Y1$(21,1)="VMNO")=0 THEN GOTO 3200
2415 LET V0$="S",V1$="C",V2$=W4$,V3$="",V0=40,V1=10,V2=L+1
2420 LET V4$="Enter Up To "+STR(V0)+" Characters"
2425 IF Y1$(21,1)="V" THEN LET V4$="Enter The Vendor's Item Number",V0=I[6]; IF FNP$(V2$)="" THEN LET V2$=U2$
2430 IF Y1$(21,1)="N" THEN LET V4$="Enter The Description For This Item"
2440 GOSUB 7000
2450 ON V3 GOTO 2460,2400,3200,2400,3200
2460 LET W4$(1)=V$
2470 PRINT @(V1,V2),W4$,
2480 IF Y1$(21,1)="N" THEN PRINT @(46,V2),'SB',"Loc: ",'SF',
2490 GOTO 3200
2500 REM " --- Unit of Measure (S/N Lines)"
2505 IF POE12_EXISTS THEN GOTO 3200
2510 IF POS(Y1$(21,1)="SN")=0 THEN GOTO 3200
2520 LET V4$="Enter The Unit Of Measure For This Item"
2530 LET V0$="S",V1$="C",V2$=W1$(22,2),V3$="",V0=2,V1=O[0],V2=L
2540 GOSUB 7000
2550 ON V3 GOTO 2560,2500,3200,2500,3200
2560 LET W1$(22,2)=V$
2590 GOTO 3200
2600 REM " --- Conversion factor (S Lines)"
2605 IF POE12_EXISTS THEN GOTO 3200
2610 IF Y1$(21,1)<>"S" THEN GOTO 3200
2620 LET V4$="Enter The Conversion Factor For This Item"
2630 LET V0$="N",V1$="C",V2$=STR(W[0]),V3$=M5$,V0=M5,V1=O[1],V2=L
2640 GOSUB 7000
2650 ON V3 GOTO 2660,2600,2680,2600,2680
2660 IF V<=0 THEN GOTO 2600
2665 IF W[0]=V THEN GOTO 2680
2670 LET W[1]=W[1]/W[0],W[0]=V,W[1]=W[1]*W[0]
2680 GOSUB 6900
2685 PRINT @(V1,V2),W[0]:M5$,@(O[4]),W[1]:M3$,@(O[5]),EXTENSION:M1$,
2690 GOTO 3200
2700 REM " --- Quantity ordered (S/N Lines)"
2705 IF POE12_EXISTS THEN GOTO 3200
2710 IF POS(Y1$(21,1)="SN")=0 THEN GOTO 3200
2715 LET V4$="Enter The Quantity Of This Item To Order"
2720 LET V0$="N",V1$="C",V2$=STR(W[3]),V3$=M2$,V0=M2,V1=O[2],V2=L
2725 GOSUB 7000
2730 ON V3 GOTO 2735,2700,2780,2700,2780
2735 IF V=0 THEN GOTO 2700
2740 IF V>0 THEN IF V<W[7] THEN GOTO 2700
2745 LET W[3]=V
2750 IF NEWLINE=0 THEN GOTO 2780
2755 IF Y1$(21,1)<>"S" THEN GOTO 2780
2760 CALL "POC.UA","R",TYPE$,IVM05_DEV,A0$,A1$,W3$,W[ALL],I[1],STATUS
2780 GOSUB 6900
2785 PRINT @(V1,V2),W[3]:M2$,@(O[5]),EXTENSION:M1$,
2790 GOTO 3200
2800 REM " --- Quantity received (S/N Lines)"
2810 IF POS(Y1$(21,1)="SN")=0 THEN GOTO 3200
2820 LET V4$="Enter The Quantity Of This Item Received"
2830 LET V0$="N",V1$="C",V2$=STR(W[7]),V3$=M2$,V0=M2,V1=O[3],V2=L
2835 GOSUB 7000
2840 ON V3 GOTO 2850,2800,2880,2800,2880
2850 IF W[3]>0 THEN IF V<0 THEN GOTO 2800
2860 IF V>W[3]-W[4] THEN GOSUB 6100
2870 LET W[7]=V
2880 GOSUB 6900
2885 PRINT @(O[2],V2),W[3]:M2$,@(O[3]),W[7]:M2$,@(O[5]),EXTENSION:M1$,
2890 GOTO 3200
2900 REM " --- Unit Cost (S/N/O Lines)"
2910 IF POS(Y1$(21,1)="SNO")=0 THEN GOTO 3200
2920 LET V4$="Enter The Unit Cost For This Item"
2930 LET V0$="N",V1$="C",V2$=STR(W[1]),V3$=M3$,V0=M3,V1=O[4],V2=L
2940 GOSUB 7000
2950 ON V3 GOTO 2960,2900,2980,2900,2980
2960 IF V<0 THEN GOTO 2900
2970 LET W[1]=V
2980 GOSUB 6900
2985 PRINT @(O[3],V2),W[7]:M2$,@(O[4]),W[1]:M3$,@(O[5]),EXTENSION:M1$,
2990 GOTO 3200
3000 REM " --- Receiving Location (S/N Lines)"
3010 IF POS(Y1$(21,1)="SN")=0 THEN GOTO 3200
3020 LET V4$="Enter The Receiving Location For This Item"
3030 LET V0$="S",V1$="C",V2$=W1$(24,10),V3$="",V0=10,V1=51,V2=L+1
3040 GOSUB 7000
3050 ON V3 GOTO 3060,3000,3200,3000,3200
3060 LET W1$(24,10)=V$
3090 GOTO 3200
3100 REM " --- Date Required (S/N/O Lines)"
3110 IF POS(Y1$(21,1)="SNO")=0 THEN GOTO 3200
3115 LET V4$="Enter The Date That This Line Item Is Required"
3120 LET V0$="D",V1$="C6RN",V2$=W1$(3,6),V3$="",V0=6,V1=69,V2=L+1
3125 GOSUB 7000
3130 ON V3 GOTO 3135,3100,3200,3100,3200
3135 LET W1$(3,6)=V$
3140 GOTO 3200
3150 REM " --- Additional options?"
3160 IF Y1$(25,1)<>"Y" THEN GOTO 3200
3170 GOSUB 6500
3190 GOTO 3200
3200 REM " --- Sequence Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1200,1500,1800,2000,2300,2400,2500,2600,2700,2800,2900,3000,3100,3150,3300
3300 REM " --- Line Correct?"
3305 LET V4$="<Enter>=Correct  <F1>=Add'l Options  <F2>=Change| ",V0=0
3310 IF LOTSER$="Y" THEN LET V4$=V4$+"<F3>=Lot/Serial #  "
3315 IF Y1$(22,1)="Y" OR C1$(60,1)="Y" THEN LET V4$=V4$+"<S>=Sales Order  "
3320 LET V4$=V4$+"<D>=Delete",V0=POS("|"=V4$)
3325 IF LEN(V4$)<81 THEN GOTO 3340
3330 LET V$=V4$(1,V0-1),V4$=V4$(V0+2),V0=0
3335 PRINT @(0,21),'CL',@(FNC(V$,80),21),V$,
3340 IF V0 THEN LET V4$(V0,1)=" "
3345 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L+1,K9$=W0$,I0=12
3350 IF POS(Y1$(21,1)="VM") THEN LET I0=6
3355 GOSUB 7000
3360 PRINT @(0,21),'CL',
3365 ON V3 GOTO 3380,3400,3200,3370,3380
3370 IF LOTSER$<>"Y" THEN GOTO 3300
3380 IF V$="" THEN GOTO 3600
3385 IF V$="D" THEN GOTO 3800
3390 IF V$="S" AND (Y1$(22,1)="Y" OR C1$(60,1)="Y") THEN GOTO 3500
3395 GOTO 3300
3400 REM " --- Additional Options"
3410 GOSUB 6500
3490 GOTO 3300
3500 REM " --- Drop ship information"
3510 LET ACTION$=""
3520 GOSUB 6800
3590 GOTO 3300
3600 REM " --- Write"
3610 WRITE (POE14_DEV,KEY=W0$)IOL=POE14A
3620 LET X4$(1)=N0$+W3$(3)+W0$(16)+W0$(3)
3630 IF Y1$(21,1)="S" THEN WRITE (POE44_DEV,KEY=X4$)IOL=POE44A
3640 GOSUB 6000
3650 LET L=L+2
3660 GOSUB 6600
3665 IF POE12_EXISTS=0 AND NEWLINE THEN CALL "POC.UA","W",TYPE$,IVM05_DEV,A0$,A1$,W3$,W[ALL],I[1],STATUS
3670 IF LOTSER$="Y" THEN IF V3=3 OR W[7]<>STRT_W7 THEN GOTO 3700
3680 IF V3=4 THEN GOTO 4000
3690 GOTO 1000
3700 REM " --- Lot/Serial Number Entry"
3710 LET L1=L0-1,L2=L1+2,Q_COL=O[3],C_COL=O[4]
3720 LET QNTY=W[7]*W[0],UCOST=W[1]/W[0]
3730 LET V3=0,O0=4,PROGRAM$=PGM(-2)
3740 RUN "POE.DD"
3800 REM " --- Remove"
3805 IF LS$<>"Y" THEN GOTO 3850
3810 READ (POE24_DEV,KEY=W0$(1,25),DOM=3820)
3820 LET K$=KEY(POE24_DEV,END=3850)
3825 IF POS(W0$(1,25)=K$)<>1 THEN GOTO 3850
3830 REMOVE (POE24_DEV,KEY=K$)
3840 GOTO 3820
3900 REMOVE (POE14_DEV,KEY=W0$,DOM=3910)
3910 LET X4$(1)=N0$+W3$(3)+W0$(16)+W0$(3)
3920 REMOVE (POE44_DEV,KEY=X4$,DOM=3930)
3930 LET OLDWO$=W2$(1,10),NEWWO$="",W[3]=0
3935 GOSUB 6600
3940 IF FNP$(OLDWO$)="" THEN GOTO 3960
3950 IF SF$="Y" THEN CALL "POC.WA",WOE22_DEV,WOE32_DEV,N0$,W0$(16),"P",Y1$,OLDWO$,NEWWO$,I[1],STATUS
3960 GOSUB 6000
3970 PRINT @(0,L),'CL',@(0,L+1),'CL',
3980 IF V3=4 THEN GOTO 4000
3990 GOTO 1000
4000 REM " --- Correct?"
4010 PRINT @(2,22),"Is The Above Correct (Y/N/List/Review/Delete/Line #/<F2>=Header)?",
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V4$="",V0=6,V2=22,V1=69
4030 GOSUB 7000
4035 ON V3 GOTO 4040,4000,4200,4000,4100
4040 IF V$="L" OR V$="LIST" THEN GOTO 4800
4050 IF V$="R" OR V$="REVIEW" THEN GOTO 4900
4060 IF V$="DELETE" THEN GOTO 4200
4065 LET V=NUM(V$,ERR=4090)
4070 IF V<1 OR V>999 THEN GOTO 4090
4075 LET V$=STR(INT(V):"000")
4080 GOTO 1275
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Go check for missing L/S numbers
4110 IF LS$="N" OR CHECKLS=0 THEN GOTO 4200
4120 GOSUB CHECK4LS
4130 IF FIXIT THEN LET V$=W0$(23,3); GOTO 4065
4200 REM " --- Return to header overlay"
4210 PRINT @(0,L0-2),'CE',
4220 LET O0=1,ACTION$="B",VENDOR$(1)=N0$+A0$(3,6)
4230 IF V3=2 THEN LET O0=2,ACTION$="BD"
4240 IF V$="DELETE" THEN LET O0=3
4250 CALL "POC.RA",TYPE$,ACTION$,RIBBON$[ALL],RIBBON[ALL],RFILES[ALL],A$[ALL],A[ALL],V3
4260 IF V$<>"DELETE" THEN CALL "POC.VA","",VENDOR$,VENDLEN,M0$,APM01_DEV,APM09_DEV,P3$,L0,STATUS
4290 RUN "POE.DA"
4800 REM " --- List"
4810 GOSUB 5600
4890 GOTO 4000
4900 REM " --- Review"
4910 READ (POE14_DEV,KEY=A0$,DOM=4920)
4920 GOSUB 5900
4990 GOTO 1000
5000 REM " --- Display"
5010 LET NUMBER=NUMBER+1,Y0$(1)=N0$+W1$(1,2),LOTSER$="N",DESCRIPTION$(1)=W4$
5030 FIND (POM02_DEV,KEY=Y0$,DOM=5590)IOL=POM02A
5040 GOSUB 6900
5080 PRINT @(0,L),'CL',W0$(23),@(4),W1$(1,2),@(7),W3$(1,2),@(0,L+1),'CL',
5090 ON POS(Y1$(21,1)="SNVMO") GOTO 5590,5100,5200,5300,5400,5500
5100 REM " --- Display Standard Line"
5110 DIM B0$(22),B1$(60),B2$(64),B3$(40),B4$(21),B6$(50),B[12],C1$(64),C[20]
5120 LET B0$(1)=N0$+W3$(3),C0$(1)=N0$+W3$
5130 FIND (IVM01_DEV,KEY=B0$,DOM=5140)IOL=IVM01A
5135 FIND (IVM02_DEV,KEY=C0$,DOM=5150)IOL=IVM02A
5140 LET DESCRIPTION$(1)=FNITEM$(B1$,I[3],I[4],I[5])
5150 PRINT @(10,L),W3$(3,I[0]),@(O[0]),W1$(22,2),@(O[1]),W[0]:M5$,@(O[2]),W[3]:M2$,@(O[3]),W[7]:M2$,@(O[4]),W[1]:M3$,@(O[5]),EXTENSION:M1$,@(10,L+1),DESCRIPTION$,@(46),'SB',"Loc: ",'SF',W1$(24,10),@(62),'SB',"Req'd: ",'SF',FNB6$(W1$(3,6)),
5170 IF LS$="Y" AND B2$(19,2)="YY" THEN LET LOTSER$="Y"
5190 RETURN 
5200 REM " --- Display Non-Stock Line"
5210 PRINT @(10,L),W3$(3,I[0]),@(O[0]),W1$(22,2),@(O[2]),W[3]:M2$,@(O[3]),W[7]:M2$,@(O[4]),W[1]:M3$,@(O[5]),EXTENSION:M1$,@(10,L+1),DESCRIPTION$,@(46),'SB',"Loc: ",'SF',W1$(24,10),@(62),'SB',"Req'd: ",'SF',FNB6$(W1$(3,6)),
5290 RETURN 
5300 REM " --- Display Vendor Part Number Line"
5310 PRINT @(10,L+1),W4$(1,I[6]),
5390 RETURN 
5400 REM " --- Display Message Line"
5410 PRINT @(10,L+1),W4$,
5490 RETURN 
5500 REM " --- Display Other Line"
5510 PRINT @(O[4],L),W[1]:M3$,@(O[5]),EXTENSION:M1$,@(10,L+1),DESCRIPTION$,@(62),'SB',"Req'd: ",'SF',FNB6$(W1$(3,6)),
5590 RETURN 
5600 REM " --- List"
5610 LET NUMBER=0
5620 GOSUB 5900
5640 READ (POE14_DEV,KEY=A0$,DOM=5650)
5650 LET K$=KEY(POE14_DEV,END=5790)
5660 IF POS(A0$=K$)<>1 THEN GOTO 5790
5670 READ (POE14_DEV)IOL=POE14A
5690 IF L<=L9 THEN GOTO 5750
5700 REM " --- Full screen"
5710 LET V4$="Press <Enter> To Continue:"
5720 LET V0$="S",V1$="CE",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5730 GOSUB 7000
5740 IF V3=4 THEN GOTO 5790
5745 GOSUB 5900
5750 GOSUB 5000
5760 LET L=L+2,K9$=W0$
5770 GOTO 5650
5790 RETURN 
5800 REM " --- Display line 21"
5810 LET X=10+LEN(P7$)
5820 IF FNP$(W2$(11,6))="" THEN GOTO 5850
5825 PRINT @(0,21),'SB',"Customer ",'SF',FNF$(W2$(11,CUSTLEN),P7$),
5830 IF FNP$(W2$(17,7))="" THEN GOTO 5850
5835 PRINT @(X,21),'SB',"SO # ",'SF',W2$(17,7),
5840 IF FNP$(W2$(24,3))<>"" THEN PRINT @(X+12,21),"-",W2$(24,3),
5850 IF FNP$(W2$(1,7))="" THEN GOTO 5880
5860 PRINT @(X+17,21),'SB',"WO # ",'SF',W2$(1,7),
5870 IF FNP$(W2$(8,3))<>"" THEN PRINT @(X+28,21),"-",W2$(8,3),
5880 IF W[4]<>0 THEN PRINT @(79-M2-16,21),'SB'," Qty Prev Rec'd ",'SF',W[4]:M2$,
5890 RETURN 
5900 REM " --- Clear Screen"
5910 LET L=L0-1
5920 PRINT @(0,L),'CE',
5990 RETURN 
6000 REM " --- Accumulate/display total for receiver"
6010 LET V1=O[5],V2=L0-3
6050 CALL "POC.TA",TYPE$,POE14_DEV,POM02_DEV,A0$,W0$,V1,V2,M1$,I[1],TOTAL,STATUS
6090 RETURN 
6100 REM " --- Quantity received greater than quantity ordered"
6110 LET MESSAGE$[0]="Quantity Received Exceeds Quantity Ordered Plus Quantity"
6120 LET MESSAGE$[1]="Previously Received (<Enter>=Continue)"
6130 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
6190 RETURN 
6200 REM " --- Check for missing Lot/Serial numbers
6205 CHECK4LS: 
6210 LET FIXIT=0
6220 READ (POE14_DEV,KEY=A0$,DOM=READPOE14)
6230 READPOE14: LET QTYRCVD=0
6240 LET K$=KEY(POE14_DEV,END=DONECHECK)
6250 IF POS(A0$=K$)<>1 THEN GOTO DONECHECK
6260 READ (POE14_DEV)IOL=POE14A
6270 FIND (IVM01_DEV,KEY=N0$+W3$(3,20),DOM=NEXTPOE14)IOL=IVM01A
6280 IF B2$(19,2)<>"YY" THEN GOTO NEXTPOE14
6290 LET QTYRCVD=W[7]*W[0],TOTLSQTY=0,LSQTYRCVD=0
6300 READ (POE24_DEV,KEY=W0$,DOM=6310)
6310 LET K$=KEY(POE24_DEV,END=TEST4MSG)
6320 IF POS(W0$=K$)<>1 THEN GOTO TEST4MSG
6330 READ (POE24_DEV)IOL=POE24A
6340 LET TOTLSQTY=TOTLSQTY+LSQTYRCVD
6350 GOTO 6310
6360 TEST4MSG: IF TOTLSQTY=QTYRCVD THEN GOTO NEXTPOE14
6370 IF TOTLSQTY<QTYRCVD THEN LET MESSAGE$[0]=FILL(4)+"Missing "
6380 IF TOTLSQTY>QTYRCVD THEN LET MESSAGE$[0]=FILL(4)+"Too Many "
6390 LET MESSAGE$[0]=MESSAGE$[0]+"Lot/Serial numbers in Line "+W0$(23,3)+". Register Will Not Update. "
6400 LET MESSAGE$[1]="F=Fix Now   S=Skip Further L/S Checking   <Enter>=Continue Checking"
6410 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
6420 IF V$="" THEN GOTO NEXTPOE14
6430 ON POS(V$="FS") GOTO 6390,FIX,SKIP
6440 FIX: LET FIXIT=1
6450 GOTO DONECHECK
6460 SKIP: LET CHECKLS=0
6470 GOTO DONECHECK
6480 NEXTPOE14: GOTO READPOE14
6490 DONECHECK: RETURN 
6500 REM " --- Process additional options window"
6510 LET PARAMS$[0]=W0$,PARAMS$[1]=W1$,PARAMS$[2]=W2$,PARAMS$[3]=Y1$
6520 LET PARAMS$[4]=SF$,PARAMS$[5]=C1$(60,1),FILES[1]=POM04_DEV
6530 LET FILES[2]=WOE01_DEV,FILES[3]=WOE22_DEV,FILES[4]=WOE32_DEV
6540 CALL "POC.AO",TYPE$,FILES[ALL],PARAMS$[ALL],L,STATUS
6550 IF FNP$(W2$(1,10))="" THEN IF FNP$(PARAMS$[2](1,10))="" THEN GOTO 6580
6560 LET OLDWO$=W2$(1,10),NEWWO$=PARAMS$[2](1,10)
6570 IF SF$="Y" THEN CALL "POC.WA",WOE22_DEV,WOE32_DEV,N0$,W0$(9),TYPE$,Y1$,OLDWO$,NEWWO$,I[1],STATUS
6580 LET W1$(1)=PARAMS$[1],W2$(1)=PARAMS$[2]
6590 RETURN 
6600 REM " --- Check to see if on order quantity needs updating"
6610 IF POE12_EXISTS THEN GOTO 6790
6640 LET QTY=(W[3]-STRT_W3)*W[0]
6650 IF QTY=0 THEN GOTO 6790
6700 REM " --- Update on order quantity"
6710 LET ITEM$[0]=N0$,ITEM$[1]=W3$(1,2),ITEM$[2]=W3$(3),REFS[0]=QTY
6720 LET FILES[1]=IVM01_DEV,FILES[2]=IVM02_DEV,ACTION$="OO"
6730 LET PARAMS$[2]=I2$,PARAMS$[3]=I3$,PARAMS$[4]=I4$
6750 CALL "IVC.UA",ACTION$,FILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEM$[ALL],REFS$[ALL],REFS[ALL],STATUS
6790 RETURN 
6800 REM " --- Process drop ship window"
6810 LET FILES[1]=ARM01_DEV,FILES[2]=ARM03_DEV,FILES[3]=ARE03_DEV
6820 LET FILES[4]=ARE13_DEV,FILES[5]=ARE33_DEV,PARAMS[0]=CUSTLEN,PARAMS$[0]=N0$
6830 LET PARAMS$[1]=P7$,PARAMS$[2]=W2$,PARAMS$[3]=AR$,PARAMS$[4]=OP$,PARAMS$[5]=Y1$
6840 CALL "POC.DS",TYPE$,ACTION$,FILES[ALL],PARAMS[ALL],PARAMS$[ALL],L,STATUS
6850 IF STATUS=0 THEN LET W2$(1)=PARAMS$[2]
6890 RETURN 
6900 REM " --- Calculate EXTENSION"
6910 LET QTY=W[7]
6920 IF Y1$(21,1)="O" THEN LET QTY=1
6930 PRECISION 2
6940 LET EXTENSION=W[1]*QTY
6950 PRECISION I[1]
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8070 DEF FNP(M1,M2,M3,M5,P)=(80-(M1+M2+M2+M3+M5))-P
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "IVM - Item Maintenance (Transaction History)"
0020 REM "Program IVM.ML"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "12Jul96 JAL: Correct heading alignment (570-575)"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0220 IVT04A: IOLIST C0$(1),C1$(1),C2$(1),B3$,C[ALL]
0230 IVM10C: IOLIST X0$(1)
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM B0$(24),X0$(32),FILES[5],PARAMS[13],PARAMS$[13],O[3]
0520 DIM C0$(32),C1$(96),C2$(16),C[4]
0565 LET HEADING$(1)="   Date   Type Reference"
0570 LET HEADING$(H0-M1-M2-M3-9)="Quantity",HEADING$(H0-M1-M2-5)="Cost"
0575 LET HEADING$(H0-M2-10)="Extension",HEADING$(H0-8)="Balance"
0580 LET FOOTING$(1)=" <F1>=Next Warehouse",DW=((H0-M1-(M2*2)-M3)-16)+1
0585 LET FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0590 LET O[3]=H0-M2,O[2]=O[3]-M1,O[1]=O[2]-M3,O[0]=O[1]-M2
0600 REM " --- Additional parameters"
0610 IF AR$="Y" THEN LET PARAMS[1]=CUSTLEN,PARAMS$[1]=CUSTMASK$,FILES[2]=ARM01_DEV
0620 IF AP$="Y" THEN LET PARAMS[2]=VENDLEN,PARAMS$[2]=VENDMASK$,FILES[3]=APM01_DEV
0630 IF GL$="Y" THEN LET PARAMS[3]=G[3],PARAMS$[3]=G5$,FILES[4]=GLM01_DEV
0640 LET FILES[0]=IVM01_DEV,FILES[1]=IVM10_DEV,FILES[5]=IVT04_DEV
0645 LET PARAMS[0]=P[0],PARAMS[4]=P[3],PARAMS[5]=P[4],PARAMS[6]=P[5]
0650 LET PARAMS[7]=31,PARAMS[8]=MAXROW,PARAMS[9]=WIDTH,PARAMS[10]=HEIGHT
0660 LET PARAMS[11]=32,PARAMS[12]=P[1],PARAMS$[0]=N0$,PARAMS$[9]=M1$
0670 LET PARAMS$[10]=M2$,PARAMS$[11]=M3$
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT 'CS','SB',@(1,0),"Warehouse ID:",@(48),"From:",@(65),"To:",'SF',
0730 CALL "SYC.WC",1,0,WIDTH-2,1,0,1,0
0740 PRINT @(0,2),'SB','BR',HEADING$,'ER','SF',
0750 PRINT @(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 IF PAGES<3 THEN GOTO 4000
0990 READ (IVM03_DEV,KEY=A0$(1,2)+"G"+A0$(3),DOM=1000)
1000 REM " --- Warehouse ID"
1005 PRINT 'CF',
1010 LET V2$="",V2$=KEY(IVM03_DEV,END=1040)
1015 READ (IVM03_DEV,KEY=V2$)
1020 IF POS(A0$(1,2)+"G"+A0$(3)=V2$)<>1 THEN LET V2$=""
1030 IF V2$<>"" THEN LET V2$=V2$(24)
1040 LET V0$="S",V1$="KE",V3$="",V4$="",V0=2,V1=15,V2=0
1050 GOSUB 7000
1055 ON V3 GOTO 1080,1000,1000,1060,4000
1060 CALL "IVC.LK",3,V1,V2,V$
1070 IF V$="" THEN GOTO 0900
1080 LET WHSE$=V$+"  ",WHSE$=WHSE$(1,2)
1200 REM " --- Retrieve record"
1210 FIND (IVM10_DEV,KEY=N0$+"C"+WHSE$,DOM=1000)IOL=IVM10C
1220 LET K$=A0$(1,2)+"G"+A0$(3)+WHSE$,B0$(1)=N0$+WHSE$+A0$(3)
1230 FIND (IVM02_DEV,KEY=B0$,DOM=1000)
1290 READ (IVM03_DEV,KEY=K$,DOM=1300)
1300 REM " --- Display record"
1310 PRINT @(15,0),X0$(4,2)," ",X0$(6,24),
1400 REM " --- Beginning Date"
1410 LET V0$="D",V1$="E",V2$=BEGDATE$,V3$="",V4$="",V0=3,V1=54,V2=0
1420 GOSUB 7000
1430 ON V3 GOTO 1450,1440,1000,1400,4000
1440 LET V$=""
1450 IF V$="" THEN PRINT @(V1,V2),"First   ",
1490 LET BEGDATE$=V$
1500 REM " --- Ending Date"
1510 LET V0$="D",V1$="E",V2$=ENDDATE$,V3$="",V4$="",V0=3,V1=69,V2=0
1520 GOSUB 7000
1530 ON V3 GOTO 1550,1540,1400,1500,4000
1540 LET V$=""
1550 IF V$="" THEN PRINT @(V1,V2),"Last    ",
1560 IF V$<>"" THEN IF V$<BEGDATE$ THEN GOTO 1500
1590 LET ENDDATE$=V$
1900 REM " --- Determine starting beginning balance"
1910 LET BALANCE=0,ITEM$=N0$+WHSE$+A0$(3)
1920 READ (IVT04_DEV,KEY=ITEM$+BEGDATE$,DOM=1930)
1930 LET K$=KEY(IVT04_DEV,END=2000)
1990 GOTO 2030
2000 REM " --- Read backwards thru file until BB record is found"
2010 LET K$=KEYP(IVT04_DEV,END=3000)
2020 IF POS(ITEM$=K$)<>1 THEN GOTO 3000
2030 EXTRACT (IVT04_DEV,KEY=K$)IOL=IVT04A
2040 IF C1$(1,2)<>"BB" THEN GOTO 2000
2050 LET BALANCE=C[0]
2060 READ (IVT04_DEV)
2100 REM " --- Read forward until first record for BEGDATE$"
2110 LET K$=KEY(IVT04_DEV,END=3000)
2120 IF POS(ITEM$=K$)<>1 THEN GOTO 3000
2130 IF K$(25,3)>=BEGDATE$ THEN GOTO 3000
2140 READ (IVT04_DEV,KEY=K$)IOL=IVT04A
2150 LET BALANCE=BALANCE+C[0]
2190 GOTO 2100
3000 REM " --- Call display routine"
3010 LET ACTION$="B",PARAMS[13]=BALANCE,PARAMS$[4]=B0$
3020 LET PARAMS$[5]=BEGDATE$,PARAMS$[6]=ENDDATE$,PARAMS$[7]=NAME$
3030 CALL "SYC.WB",NAME$,0
3040 CALL "IVC.TA",ACTION$,FILES[ALL],PARAMS[ALL],PARAMS$[ALL],SELECTION$,FKEY
3050 IF FKEY=4 THEN GOTO 4000
3090 GOTO 1000
4000 REM " --- Run menu overlay"
4010 CALL "SYC.WD",NAME$
4090 RUN "IVM.MA"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9910 CALL "SYC.WD",NAME$
9950 RUN "SYS.AA"
9999 END

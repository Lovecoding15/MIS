0010 REM "MPR - Pegging Report (Report Overlay)"
0020 REM "Program MPR.FB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "07Jun96 JJD: Hard error, NUM of alphanumeric Customer and Vendor #s (1717,1860,1917)"
0032 REM "02Aug96 WGH: Fix laser pagination ()"
0033 REM "25Oct96 JJD: Assign 'Available' correctly. (1660,1670)"
0034 REM "20Oct97 KJS: Use F0$(18,6) instead of DAY function (598,6650)"
0035 REM "09Oct98 WGH: Report correct Stocking Level (item or warehouse) information (5160,5205,5265,5360,5380,6140-6142)
0036 REM "31Dec98 WGH: Use Inventory ABC Analysis Factors, and current Period and Year (595,6660)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 CALL "MPC.FA",MPE05_DEV,"MPE-05",N0$,FORECAST$,FILENAME$,STATUS
0115 IF STATUS>0 THEN GOTO 9900
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 MPE05A: IOLIST A0$(1),A1$(1),A2$(1),A3$,A[ALL]
0230 APM01A: IOLIST C0$(1),C1$(1)
0250 BMM01A: IOLIST B0$(1),B1$,B2$(1),B[ALL]
0280 IVM01A: IOLIST D0$(1),D1$(1),D2$(1),D3$,D4$,D5$,D6$,D[ALL]
0290 IVM02A: IOLIST W0$(1),W1$(1),W2$,W[ALL]
0300 IVM05A: IOLIST U0$(1),U1$(1),U2$,U[ALL]
0310 IVM03H: IOLIST V0$(1)
0320 ARM01A: IOLIST C0$(1),C1$(1)
0330 WOE01A: IOLIST X0$(1)
0500 REM " --- Initializations"
0505 PRECISION IVS01_PRECISION
0510 DIM A0$(37),A1$(20),A2$(63),A[12],TYPES$[12],V0$(29)
0515 LET HEADINGS$[1]=HEADINGS$[1]+" For "+H5$
0520 DIM C0$(8),C1$(30),U0$(28),U1$(32),U[12],J2$(M2*6,"-")
0540 LET L1=0,H3$="",T9$="",PREFIX$=FNP$(N0$+FORECAST$+WHSE$)
0545 LET TYPES$[0]="Unknown",TYPES$[1]="On Hand",TYPES$[2]="PO On Order"
0550 LET TYPES$[3]="WO On Order",TYPES$[4]="Forecast",ALL$="ABCDEFGHIJKL"
0555 LET TYPES$[5]="Dependent",TYPES$[6]="Sales Demand",CONTINUED=0
0560 LET TYPES$[7]="WO Demand",TYPES$[8]="WO Release"
0565 LET TYPES$[9]="PO Release",M0=MAX(LEN(VM0$),LEN(CM0$)),NUMBER=0
0567 LET TYPES$[10]="WO Receipt",TYPES$[11]="PO Receipt"
0569 LET TYPES$[12]="PO Req On Order"
0570 IF SELECTION$="" THEN GOTO 0593
0575 LET H9$="Selected Transactions: ",NW=LEN(SELECTION$)
0580 FOR X=1 TO NW
0583 LET TRANS=POS(SELECTION$(X,1)=ALL$),H9$=H9$+TYPES$[TRANS]
0585 IF X=NW-1 THEN LET H9$=H9$+" And "; GOTO 0590
0587 IF X<NW-1 THEN LET H9$=H9$+", "
0590 NEXT X
0592 GOTO 0595
0593 LET H3$="All Transaction Types",SELECTION$=ALL$
0595 DIM IVFILES[12],ITEM$[2]
0596 LET IVFILES[1]=IVM01_DEV,IVFILES[2]=IVM02_DEV,IVFILES[3]=IVM03_DEV
0597 LET IVFILES[12]=IVM12_DEV
0598 LET DAY_OF_MONTH=NUM(F0$(22,2))
0600 REM " --- Print positions"
0610 DIM O[10],H1[5],H2[10]
0615 LET O[10]=WIDTH-M2,O[9]=O[10]-M2,O[8]=O[9]-M2,O[7]=O[8]-M2,O[6]=O[7]-M2,O[5]=O[6]-M2
0620 LET O[0]=0,O[1]=9,O[2]=O[1]+13,O[3]=O[2]+P[0],O[4]=O[3]+11
0625 LET X$=" No Bill Of Materials Defined",NW=O[5]-(O[4]+M0+1)
0630 LET H2[10]=WIDTH-8,H2[9]=H2[10]-10,H2[8]=H2[9]-10,H2[7]=H2[8]-11
0635 LET H2[6]=H2[7]-9,H2[5]=H2[6]-M2-1,H2[4]=H2[5]-M2-1,H2[3]=H2[4]-4
0640 LET H2[2]=H2[3]-M5-1,H2[1]=P[0],DW2=H2[2]-H2[1]+1
0645 LET H1[4]=O[7]-M3,H1[3]=H1[4]-21,H1[2]=H1[3]-M5-1,H1[1]=P[0]
0650 LET H1[5]=H1[3]-(VM0+31),VW=30
0655 IF H1[5]<20 THEN LET H1[5]=20,VW=H1[3]-(H1[5]+VM0+1)
0660 DIM NB$(WIDTH-O[5],"-")
0665 LET DW1=H1[2]-H1[1]+1
0670 LET HR=WIDTH-MAX(LEN(H7$),LEN(H8$)),NB$(FNC(X$,LEN(NB$)),LEN(X$))=X$
0680 LET S2$=TODAY$+S2$; IF POS(TODAY$>=S2$(7,S[0]*6),6)>0 THEN LET S2$(1,6)=""
0690 IF BEGPER=1 AND ENDPER=S[0] THEN LET BEGPER=BEGPER-1
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",P[0]+9,COLUMN
0900 REM " --- Position file"
0990 READ (MPE05_DEV,KEY=PREFIX$+BEGITEM$,DOM=1000)
1000 REM " --- Read next MPE-05 record"
1010 READ (MPE05_DEV,END=4000)IOL=MPE05A
1020 IF POS(PREFIX$=A0$)<>1 THEN GOTO 4000
1030 IF FNP$(ENDITEM$)<>"" THEN IF A0$(8,20)>ENDITEM$ THEN GOTO 4000
1040 LET TRANSDATE$=A0$(28,6),PROD$="N"
1070 IF OPTION$<>"" THEN IF A2$(37,1)<>OPTION$ THEN GOTO 3900
1075 IF POS(A0$(34,1)=SELECTION$)=0 THEN GOTO 3900
1080 IF A2$(37,1)="B" THEN LET PROD$="Y"
1090 PRINT @(COLUMN,11),A0$(8,P[0])," ",FNB6$(TRANSDATE$),
1200 REM " --- Level breaks"
1210 IF A0$(8,20)<>T0$ THEN GOSUB 6000
1220 IF TRANSDATE$>T1$ THEN GOSUB 6200
1230 IF PERIOD<BEGPER THEN GOTO 3900
1240 IF PERIOD>ENDPER THEN GOTO 3900
1500 REM " --- Format detail line"
1510 LET TRANS$=A0$(34,1),TRANS=POS(TRANS$=ALL$)
1520 LET ITEM$="",REF$="",WHO$="",WHO2$=""
1530 IF POS(TRANS$="CEGHIJK") THEN LET ITEM$=A1$
1540 IF POS(TRANS$="F")>0 THEN LET REF$=A2$(1,7)
1550 IF POS(TRANS$="BIL")>0 THEN LET REF$=A2$(20,7)
1560 IF POS(TRANS$="CGH")>0 THEN LET REF$=A2$(27,7)+" "+A2$(34,2)+"  "+A2$(36,1)
1600 REM " --- Amounts"
1610 DIM AMOUNTS$[5]
1615 IF TRANS$="A" THEN LET AVAIL=A[2]
1620 FOR X=0 TO 5
1630 IF A[X]=0 OR X=2 THEN GOTO 1650
1640 LET AMOUNTS$[X]=STR(A[X]:M2$)
1645 LET S[X]=S[X]+A[X],T[X]=T[X]+A[X]
1650 NEXT X
1660 LET AVAIL=AVAIL+A[1]-A[0]+A[5],AMOUNTS$[2]=STR(AVAIL:M2$)
1670 LET T[2]=AVAIL,S[2]=T[2]; REM "Available is a running total
1690 ON TRANS GOTO 2000,2000,1700,1900,2000,2000,1900,1800,2000,1700,2000,1700
1700 REM " --- Reference Vendor"
1710 DIM C0$(8),C1$(30)
1715 IF FNP$(A2$(14,6))="" THEN GOTO 2000
1720 LET C0$(1)=N0$+A2$(14,6),C1$(1)="Unknown Vendor"
1730 FIND (APM01_DEV,KEY=C0$,DOM=1740)IOL=APM01A
1740 LET WHO$=FNF$(C0$(3,AP),VM0$)+" "+C1$
1790 GOTO 2000
1800 REM " --- Work Order Demand"
1810 IF WO$<>"Y" THEN GOTO 2000
1815 DIM X0$(76)
1820 LET X0$(1)=N0$+"  "+A2$(27,7)
1830 FIND (WOE01_DEV,KEY=X0$(1,11),DOM=2000)IOL=WOE01A
1840 LET A2$(8,6)=X0$(34,6)
1850 IF FNP$(A2$(8,6))="" THEN GOTO 2000
1900 REM " --- Reference Customer"
1910 IF AR$<>"Y" THEN GOTO 2000
1915 DIM C0$(8),C1$(30),WHO$(0)
1917 IF FNP$(A2$(8,6))="" THEN GOTO 1950
1920 LET C0$(1)=N0$+A2$(8,6),C1$(1)="Unknown Customer"
1930 FIND (ARM01_DEV,KEY=C0$,DOM=1940)IOL=ARM01A
1940 LET WHO$=FNF$(C0$(3,AR),CM0$)
1950 IF TRANS$<>"F" THEN LET WHO$=WHO$+" "+C1$ ELSE LET WHO2$=C1$
2000 REM " --- Print detail line"
2010 IF L+3>L9 THEN GOSUB 5000
2020 IF L1>0 THEN GOTO 2050
2030 PRINT (7)""
2040 LET L=L+1
2050 IF TRANS$<>"A" THEN PRINT (7)@(O[0]),FNB6$(TRANSDATE$),
2060 IF TRANS$="A" THEN PRINT (7)@(O[0]),"Begin:",
2070 PRINT (7)@(O[1]),TYPES$[TRANS],@(O[2]),ITEM$,@(O[3]),REF$,@(O[4]),WHO$,@(O[5]),AMOUNTS$[0],@(O[6]),AMOUNTS$[1],@(O[7]),AMOUNTS$[2],@(O[8]),AMOUNTS$[3],@(O[9]),AMOUNTS$[4],@(O[10]),AMOUNTS$[5]
2080 IF WHO2$<>"" THEN PRINT (7)@(O[3]),WHO2$; LET L=L+1,L1=L1+1
2090 LET L=L+1,L1=L1+1,CONTINUED=1
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 GOSUB 6000
4020 IF NUMBER>0 THEN GOTO 9900
4040 LET T9$="END"
4050 GOSUB 5000
4060 PRINT (7)"No Items Found For Selected Report Option(s)"
4090 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+5
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5035 PRINT (7)H6$,@(FNC(H4$,WIDTH)),H4$,@(HR),H7$
5040 PRINT (7)P9$,@(FNC(H3$,WIDTH)),H3$,@(HR),H8$
5045 PRINT (7)""
5050 IF H9$="" THEN GOTO 5070
5055 PRINT (7)@(FNC(H9$,WIDTH)),H9$
5060 PRINT (7)""
5070 IF T9$<>"" THEN GOTO 5490
5080 IF CONTINUED>0 THEN GOTO 5400
5090 IF PROD$="Y" THEN GOTO 5300
5100 REM " --- Purchase Heading"
5110 DIM DESC$(DW1)
5120 LET DESC$(1)=FNZ$(D1$,P[2],P[3],P[4])
5130 PRINT (7)"Purchase",@(H1[2]),"Lead",@(H1[4]+M3-5),"Unit",@(O[8]+M2-8),"Avg Mth",@(O[9]+M2-7),"Safety",@(O[10]+M2-9),"Quantity"
5140 PRINT (7)"Item",@(H1[1]),"Description",@(H1[2]),"Time",@(H1[3]),"Vendor Item Number",@(H1[4]+M3-5),"Cost",@(O[7]+M2-4),"EOQ",@(O[8]+M2-6),"Usage",@(O[9]+M2-6),"Stock",@(O[10]+M2-8),"On Hand"
5150 PRINT (7)""
5160 PRINT (7)D0$(3),@(H1[1]),DESC$,@(H1[2]),LT:M5$,@(H1[4]),W[11]:M3$,@(O[7]),EOQ:M2$,@(O[8]),AVG_USAGE:M2$,@(O[9]),SS:M2$,@(O[10]),W[0]:M2$
5190 LET L=L+4,NUMBER=NUMBER+1
5200 REM " --- Vendor Information"
5205 LET VENDOR$=STOCKVENDOR$
5210 GOSUB 6500
5215 PRINT (7)""
5220 PRINT (7)@(H1[5]-16),"Stocking Vendor ",V$," ",C1$,@(H1[3]),P$,@(H1[4]),C$
5225 LET L=L+1,NUMBER=NUMBER+1
5230 IF INCLUDE$<>"Y" THEN GOTO 5400
5235 READ (IVM03_DEV,KEY=N0$+"H"+W0$(5,20),DOM=5240)
5240 LET K3$=KEY(IVM03_DEV,END=5290)
5245 IF POS(K3$(1,3)=N0$+"H")<>1 THEN GOTO 5290
5250 IF K3$(4,20)<>W0$(5,20) THEN GOTO 5290
5255 READ (IVM03_DEV,KEY=K3$)IOL=IVM03H
5260 LET VENDOR$=V0$(24,6)
5265 IF VENDOR$=STOCKVENDOR$ THEN GOTO 5240
5270 GOSUB 6500
5275 PRINT (7)@(H1[5]-LEN(T$)),T$,V$," ",C1$,@(H1[3]),P$,@(H1[4]),C$
5280 LET L=L+1
5285 GOTO 5240
5290 GOTO 5400
5300 REM " --- Production Heading"
5310 DIM DESC$(DW2)
5320 LET DESC$(1)=FNZ$(D1$,P[2],P[3],P[4])
5330 PRINT (7)"Production",@(H2[2]),"Lead",@(H2[5]+M2-4),"Lot",@(H2[6]),"Phantom",@(H2[7]),"Estimated",@(H2[8]+2),"Bill",@(H2[9]+2),"Last",@(H2[10]+2),"Last"
5340 PRINT (7)"Item",@(H2[1]),"Description",@(H2[2]),"Time",@(H2[3]),"Rev",@(H2[4]+M2-4),"EOQ",@(H2[5]+M2-5),"Size",@(H2[6])," Bill?",@(H2[7]+2),"Yield",@(H2[8]),"Created",@(H2[9]),"Revision",@(H2[10]+2),"Used"
5350 PRINT (7)""
5355 IF BILL$="" THEN GOTO 5380
5360 PRINT (7)D0$(3),@(H2[1]),DESC$,@(H2[2]),LT:M5$,@(H2[3]),REVCODE$,@(H2[4]),EOQ:M2$,@(H2[5]),LOT$,@(H2[6]+3),PHANTOM$,@(H2[7]),YIELD$,@(H2[8]),CREATED$,@(H2[9]),REVISION$,@(H2[10]),USED$
5370 GOTO 5390
5380 PRINT (7)D0$(3),@(H2[1]),DESC$,@(H2[2]),LT:M5$,@(H2[4]),EOQ:M2$,@(H2[5]),NB$
5390 LET L=L+4,NUMBER=NUMBER+1
5400 REM " --- Detail Heading"
5410 IF CONTINUED=0 THEN PRINT (7)""
5420 PRINT (7)@(O[5]+M2-8),"Gross",@(O[6]+M2-9),"Schedule",@(O[7]+M2-10),"Projected",@(O[8]+M2-7),"Net",@(O[9]+M2-8),"Planned",@(O[10]+M2-8),"Planned"
5430 PRINT (7)@(O[0]+2),"Date",@(O[1]),"Transaction",@(O[2]),"Item",@(O[3]),"Reference",@(O[4]),"Customer/Vendor",@(O[5]+M2-9),"Required",@(O[6]+M2-8),"Receipt",@(O[7]+M2-10),"Available",@(O[8]+M2-9),"Required",@(O[9]+M2-8),"Release",@(O[10]+M2-8),"Receipt"
5450 LET L=L+3
5490 RETURN 
6000 REM " --- Item Break"
6010 IF T0$="" THEN GOTO 6100
6020 GOSUB 6200
6030 LET T0=T[0],T1=T[1],T2=T[2],T3=T[3],T4=T[4],T5=T[5],T2=0
6040 LET T$="Totals For Item "+D0$(3)+" "+FNP$(FNZ$(D1$,P[2],P[3],P[4]))
6045 IF LEN(T$)>O[5] THEN LET T$=T$(1,O[5])
6050 GOSUB 6400
6100 REM 
6110 DIM D0$(22),D1$(P[2]+P[3]+P[4]),D2$(62),D[12]
6115 DIM W0$(24),W1$(64),W2$(1),W[20],B0$(52),B2$(57),B[11],T[5]
6120 LET D0$(1,22)=N0$+A0$(8),W0$(1,24)=N0$+WHSE$+A0$(8),T0$=A0$(8,20),T1$=""
6125 LET REVCODE$="",LOT$="",PHANTOM$="",YIELD$="",CREATED$=""
6130 LET REVISION$="",USED$="",BILL$="",B0$(1)=D0$,CONTINUED=0
6135 FIND (IVM01_DEV,KEY=D0$,DOM=6140)IOL=IVM01A
6140 FIND (IVM02_DEV,KEY=W0$,DOM=6142)IOL=IVM02A
6142 IF D2$(24,1)="I" THEN LET STOCKVENDOR$=D2$(12,6),SS=D[5],EOQ=D[6],LT=D[7] ELSE LET STOCKVENDOR$=W1$(22,6),SS=W[5],EOQ=W[6],LT=W[7]
6143 IF PROD$<>"Y" THEN GOTO 6180
6145 FIND (BMM01_DEV,KEY=B0$(1,22),DOM=6180)IOL=BMM01A
6150 LET REVCODE$=B2$(38,2),PHANTOM$="No",CREATED$=FNB$(B2$(45,3))
6155 LET REVISION$=FNB$(B2$(35,3)),USED$=FNB$(B2$(41,3)),BILL$="Y"
6160 IF B2$(40,1)="Y" THEN LET PHANTOM$="Yes"
6165 IF B[4]>999.99 THEN LET B[4]=999.99
6170 IF B[4]<-999.99 THEN LET B[4]=-999.99
6175 LET YIELD$=STR(B[4]:"-##0.00")+"%",LOT$=STR(B[5]:M2$)
6180 LET L=L9+1
6182 GOSUB CALCULATE_USAGE
6185 LET AVAIL=0
6190 RETURN 
6200 REM " --- Period Break"
6210 IF LEN(CVS(T1$,1))=0 THEN GOTO 6300
6220 IF L1=0 THEN GOTO 6300
6230 LET T0=S[0],T1=S[1],T2=S[2],T3=S[3],T4=S[4],T5=S[5],T2=0
6240 LET T$="Totals For Period "+STR(PERIOD)+" "+FNB6$(T1$)
6245 IF T1$=TODAY$ THEN LET T$="Totals For Prior Periods"
6247 IF LEN(T$)>O[5] THEN LET T$=T$(1,O[5])
6250 GOSUB 6400
6300 REM 
6310 DIM S[5]
6320 LET PERIOD$=A0$(28,6),PERIOD=POS(PERIOD$<=S2$,6)
6330 IF PERIOD=0 THEN LET PERIOD=LEN(S2$)-5
6340 LET PERIOD=INT(PERIOD/6),L1=0,T1$=S2$(PERIOD*6+1,6)
6390 RETURN 
6400 REM " --- Print totals"
6405 IF L+2>L9 THEN GOSUB 5000
6410 PRINT (7)@(O[5]),J2$
6420 PRINT (7)@(O[5]-LEN(T$)),T$,@(O[5]),T0:M2$,@(O[6]),T1:M2$,@(O[8]),T3:M2$,@(O[9]),T4:M2$,@(O[10]),T5:M2$
6430 LET L=L+2
6490 RETURN 
6500 REM " --- Retrieve vendor information"
6510 DIM C0$(8),C1$(VW),V$(VM0),U0$(28),U1$(6),U[6]
6520 LET V$(1)="None",P$="",C$="",T$="Other Vendor "
6530 IF FNP$(VENDOR$)="" THEN GOTO 6590
6540 LET V$(1)=FNF$(VENDOR$(1,AP),VM0$),U0$(1)=N0$+VENDOR$+A0$(8)
6550 LET C0$(1)=N0$+VENDOR$,C1$(1)="Unknown Vendor"
6560 FIND (APM01_DEV,KEY=C0$,DOM=6570)IOL=APM01A
6570 FIND (IVM05_DEV,KEY=U0$,DOM=6590)IOL=IVM05A
6575 LET P$=U2$,C$=STR(U[6]:M3$)
6580 IF U1$(1,1)="P" THEN LET T$="Primary Vendor "
6585 IF U1$(1,1)="S" THEN LET T$="Secondary Vendor "
6590 RETURN 
6600 REM " --- Calculate Monthly Average Usage "
6605 CALCULATE_USAGE: 
6610 LET AVG_USAGE=0,BASE_PRYR$=CURR_PERIOD$+CURR_YEAR$
6620 LET ACTION$="W",PERIODS=NUM(MAX_PERIOD$)
6630 LET CURR_PRYR$=CURR_PERIOD$+CURR_YEAR$
6640 LET ITEM$[0]=N0$,ITEM$[1]=W0$(3,2),ITEM$[2]=W0$(5,20)
6650 CALL "IVC.AU",ACTION$,IVFILES[ALL],ITEM$[ALL],FACTORS[ALL],DAY_OF_MONTH,BASE_PRYR$,CURR_PRYR$,PERIODS,IVS01_PRECISION,ACTUAL,ESTIMATED,FLAG$,STATUS
6660 IF FLAG$="" THEN GOTO 6680
6670 LET ACTUAL=ESTIMATED
6680 LET AVG_USAGE=ACTUAL/PERIODS
6690 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8060 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNS$(Q$)=FNP$(Q$(POS(" "<>Q$)))
8090 DEF FNZ$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

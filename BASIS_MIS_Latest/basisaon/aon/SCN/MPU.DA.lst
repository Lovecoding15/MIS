0010 REM "MPU - Planned Work Order Update"
0020 REM "Program MPU.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "25Oct96 JJD: Force page break at startup. (590)"
0032 REM "16Apr97 JAL: Write material records to WOE-22, not WOE-02 (2875,2885)"
0033 REM "02Oct98 WGH: Not re-setting PRECISION to Inventory Control precision (2710)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0200 REM " --- IOLISTs
0205 MPW05A: IOLIST A0$(1),A1$(1),A[ALL]
0210 MPM01A: IOLIST FOR0$,FOR1$(1),FOR2$(1),FOR0,FOR1
0215 IVM01A: IOLIST D0$,D1$(1),D2$(1),D3$,D4$,D5$,D6$,D[ALL]
0220 IVM02A: IOLIST B0$,B1$,B2$,B[ALL]
0225 BMM01A: IOLIST C0$,C1$,C2$,C[ALL]
0230 BMM02A: IOLIST R0$,R1$,R2$,R[ALL]
0235 BMM03A: IOLIST L0$,L1$,L[ALL]
0240 BMM05A: IOLIST S0$,S1$,S2$,S3$,S4$,S[ALL]
0245 BMM08A: IOLIST OP0$,OP0,OP1,OP2
0250 BMM09A: IOLIST COM0$,COM1$
0255 WOE01A: IOLIST W0$(1),W1$(1),W[ALL]
0260 WOE02A: IOLIST AW0$(1),AW1$(1),AW[ALL]
0265 WOE22A: IOLIST BW0$(1),BW1$(1),BW[ALL]
0270 WOE22A1: IOLIST XW0$(1),XW1$(1),XW[ALL]
0275 WOE32A: IOLIST CW0$(1),CW1$(1),CW[ALL]
0280 WOE07A: IOLIST COM0$,COM1$
0285 WOS10A: IOLIST WS0$,WS0
0290 WOE03X: IOLIST CROSS_REF$
0500 REM " --- Initializations"
0505 PRECISION IVS01_PRECISION
0510 DIM A0$(33),A1$(38),A[6]
0515 DIM B0$(24),B1$(64),B2$(1),B[20]
0520 DIM C0$(22),C[11]
0525 DIM D0$(22),D1$(60),D2$(62),D3$(40),D4$(20),D5$(1),D6$(50),D[12]
0530 DIM W0$(75),W1$(141),W[11]
0535 DIM AW0$(15),AW1$(151),AW[13]
0540 DIM BW0$(15),BW1$(151),BW[13]
0545 DIM CW0$(15),CW1$(151),CW[13]
0550 DIM XW0$(15),XW1$(151),XW[13]
0555 DIM R[10],L[11],S[9],XW[13]
0560 LET HEADINGS$[2]="Scheduling Report"
0565 DIM PARAMS[0],PARAMS$[4],ITEMS$[3],REFS$[11],REFS[5],IVFILES[44]
0570 LET PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3),PARAMS$[2]=I2$,PARAMS$[3]=I3$
0575 LET PARAMS$[4]=I4$
0580 LET IVFILES[0]=SYS01_DEV,IVFILES[1]=IVM01_DEV,IVFILES[2]=IVM02_DEV
0590 LET L=L9+1
0800 REM " --- Options "
0810 LET V4$="Are You Ready to Update the "+N3$
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0840 REM " --- Disallow 'M'enu option in Error Routine
0850 LET EXIT_CTRL=1
0900 REM " --- Position File"
0910 LOCK (MPW05_DEV)
1100 REM " --- Main Read Here"
1110 PRINT @(COLUMN-9,11),'SB',"Updating",'SF',J1$(1,P[0]+10),
1120 LET LAST$="",ITEM$=""
1190 READ (MPW05_DEV,KEY=N0$,DOM=1200)
1200 REM " --- Read next MPW-05 record"
1210 LET K$=KEY(MPW05_DEV,END=4000)
1220 IF POS(PREFIX$=K$)<>1 THEN GOTO 4000
1230 READ (MPW05_DEV)IOL=MPW05A
1250 IF K$(8,20)<>LAST$ THEN PRINT @(COLUMN,11),K$(8,P[0]),
1260 LET LAST$=K$(8,20)
1270 IF K$(3,3)<>TYPE$ THEN GOSUB 6200
1280 IF WOTYPE$="ERR" THEN GOTO 9900
1300 REM " --- Generate WOE-01 Planned Work Order"
1305 DIM W0$(75),W1$(141),W[11]
1310 GOSUB 6100
1315 LET W0$(1)=N0$+"  "+WONUM$
1320 FIND (WOE01_DEV,KEY=W0$(1,11),DOM=1330)
1325 GOTO 1310
1330 LET W0$(12,4)=WOTYPE$+"IP",W0$(16,3)=TO_DAY$
1340 LET W0$(19,3)=FND$(FNN$(A1$(3,6))),W0$(22,3)=FND$(FNN$(A0$(28,6)))
1345 LET W1$(97,2)="5B",W1$(99,3)=TYPE$
1350 LET WHS$=A0$(6,2),ITEM$=A0$(8,20),W0$(54)=WHS$+ITEM$
1355 DIM D0$(22),D1$(60),D2$(62)
1360 FIND (IVM01_DEV,KEY=N0$+ITEM$,DOM=1100)IOL=IVM01A
1365 LET W0$(50,2)=D2$(4,2)
1370 IF POS(I3$(17,1)="SL")>0 THEN IF D2$(19,2)="YY" THEN LET W1$(96,1)="Y"
1375 EXTRACT (BMM01_DEV,KEY=N0$+ITEM$,DOM=1100)IOL=BMM01A
1380 LET W0$(52,2)=C2$(38,2),W1$(61,30)=C0$(23,30)
1385 LET W[0]=A[2],W[6]=C[4]
1390 IF C2$(41,3)>=W0$(16,3) THEN GOTO 1500
1395 LET C2$(41,4)=W0$(16,3)+"W"
1400 WRITE (BMM01_DEV,KEY=C0$(1,22))IOL=BMM01A
1500 REM " --- Initialize Here "
1510 DIM T0$(0),T[10,1]
1520 LET X=0,T[X,0]=1,T[X,1]=1,T=1
1600 REM " --- Write"
1610 WRITE (WOE01_DEV,KEY=W0$(1,11))IOL=WOE01A
1620 DIM CROSS_REF$(14)
1630 LET CROSS_REF$=N0$+"A"+W0$(12,2)+W0$(3,9)
1640 WRITE (WOE03_DEV,KEY=CROSS_REF$)IOL=WOE03X
1650 DIM CROSS_REF$(34)
1660 LET CROSS_REF$=N0$+"C"+W0$(54,22)+W0$(3,9)
1670 WRITE (WOE03_DEV,KEY=CROSS_REF$)IOL=WOE03X
1800 REM " --- Comments here
1810 READ (BMM09_DEV,KEY=N0$+ITEM$,DOM=1820)
1820 LET K9$=KEY(BMM09_DEV,END=2000)
1830 IF POS(N0$+ITEM$=K9$)<>1 THEN GOTO 2000
1840 READ (BMM09_DEV,KEY=K9$)IOL=BMM09A
1850 LET COM0$=W0$(1,11)+STR(NUM(K9$(23)):"000")
1860 WRITE (WOE07_DEV,KEY=COM0$)IOL=WOE07A
1870 GOTO 1820
2000 REM " --- Explode Bills
2010 DIM YLD[99]
2020 LET T0$=ITEM$,T9$=T0$,O$=W0$(56),O=W[0],Y=W[6],SUBS$="",MATS$=""
2030 LET YLD[0]=C[4],YLD=C[4]
2040 READ (BMM02_DEV,KEY=N0$+T9$,DOM=2060)
2060 REM " --- Do it Here!
2065 LET K7$=KEY(BMM02_DEV,END=2900)
2070 IF K7$(1,22)<>N0$+T9$ THEN GOTO 2900
2075 PRINT @(COLUMN+9,11),K7$(23,3)
2080 READ (BMM02_DEV,KEY=K7$)IOL=BMM02A
2090 LET EDATE$=R2$(5,3),ODATE$=R2$(8,3); GOSUB 4700; IF OK$="N" THEN GOTO 2060
2100 DIM BW0$(15),BW1$(151),BW[13],B[20]
2110 LET BW0$(1)=W0$(1,11)+"B"
2120 LET BW0$(13,3)=STR(NUM(BW0$(13,3))+5:"000")
2130 IF R2$(4,1)="M" THEN LET BW1$(91,61)="M"+R2$(35,60); GOTO 2400
2140 DIM D1$(60),D2$(46); LET D1$(1)="*** Not On File ***"
2145 FIND (IVM01_DEV,KEY=N0$+R1$,DOM=2150)IOL=IVM01A
2147 FIND (IVM02_DEV,KEY=N0$+WHS$+R1$,DOM=2150)IOL=IVM02A
2150 LET BW1$(1,2)=D2$(4,2),COST=B[11],BW1$(4,3)=W0$(19,3),BW1$(25,2)=W0$(54,2),BW1$(27,20)=R1$,T[X,1]=NUM(K7$(20)),BW1$(91)="S"
2155 IF R[0]=0 THEN LET R[0]=1
2160 IF R[1]=0 THEN LET R[1]=1
2165 IF R[3]=0 THEN LET R[3]=1
2170 LET BW[4]=R[3],BW[5]=R[0]*T,BW[6]=R[1],BW[7]=COST,BW[8]=R[4]
2172 LET YLD=YLD[X]
2176 IF YLD=0 THEN LET YLD=100
2180 LET BW[0]=FNQTY(X)*T,BW[1]=FNQTY(X)*COST*T
2190 LET BW[2]=O*FNQTY(X)*T,BW[3]=O*FNQTY(X)*COST*T
2195 PRECISION 2; LET BW[3]=BW[3]*1; PRECISION IVS01_PRECISION
2200 REM " --- Phantom?
2210 FIND (BMM01_DEV,KEY=N0$+R1$,DOM=2400)IOL=BMM01A
2220 IF C2$(40,1)="N" THEN GOTO 2400
2300 REM " --- Down One Level Here
2310 LET T0$=T0$+R1$,T9$=R1$,BMM$=R1$,X=LEN(T0$)/20-1,T[X,0]=BW[0],T[X,1]=NUM(K7$(3+20)),T=1
2320 LET T=T[X,0],YLD[X]=C[4]
2350 EXTRACT (BMM01_DEV,KEY=N0$+BMM$,DOM=2390)IOL=BMM01A
2360 IF W0$(16,3)<C2$(41,3) THEN GOTO 2390
2370 LET C2$(41,4)=W0$(16,3)+"W"
2380 WRITE (BMM01_DEV,KEY=N0$+BMM$)IOL=BMM01A
2390 GOTO 2040
2400 REM " --- Write Materials Here
2405 READ (WOE22_DEV,KEY=BW0$(1,12)+$FF$,DOM=2410)
2410 LET BW0$(13,3)="000"
2415 LET Z$=KEYP(WOE22_DEV,END=2430)
2420 IF POS(BW0$(1,12)=Z$)=1 THEN LET BW0$(13,3)=Z$(13,3)
2425 IF BW0$(13,3)="999" THEN LET MODULE$="Materials"; GOSUB 4500; GOTO 2900
2430 LET BW0$(13,3)=STR(NUM(BW0$(13,3))+1:"000")
2435 WRITE (WOE22_DEV,KEY=BW0$)IOL=WOE22A
2440 IF BW1$(91,1)<>"S" THEN GOTO 2460
2445 DIM CROSS_REF$(37)
2450 LET CROSS_REF$=N0$+"E  "+BW1$(25,22)+BW0$(5,7)+BW0$(13,3)
2455 WRITE (WOE03_DEV,KEY=CROSS_REF$)IOL=WOE03X
2460 IF R2$(1,3)<>"   " THEN LET MATS$=MATS$+R0$(3,20)+R2$(1,3)+BW0$(13,3)
2465 GOTO 2060
2500 REM " --- Do Labor Here
2520 READ (BMM03_DEV,KEY=N0$+T9$,DOM=2540)
2540 DIM AW0$(15),AW1$(151),AW[13],L[11]
2550 LET AW0$(1)=W0$(1,11)+"A"
2600 REM " --- Read 3
2610 LET K8$=KEY(BMM03_DEV,END=2895)
2620 IF K8$(1,22)<>N0$+T9$ THEN GOTO 2895
2625 READ (BMM03_DEV,KEY=K8$)IOL=BMM03A
2635 LET EDATE$=L1$(65,3),ODATE$=L1$(68,3); GOSUB 4700; IF OK$="N" THEN GOTO 2600
2640 IF L1$(4,1)="M" THEN DIM OP0$(90); GOTO 2660
2650 FIND (BMM08_DEV,KEY=N0$+L1$(1,3),DOM=2600)IOL=BMM08A
2660 LET AW1$(1,3)=L1$(1,3),AW1$(4,3)=W0$(19,3),AW1$(7,20)=OP0$(6,20),AW1$(91)=L1$(4,1)
2670 IF L1$(4,1)="M" THEN LET AW1$(92,60)=L1$(5,60); GOTO 2800
2680 IF L[1]=0 THEN IF OP1<>0 THEN LET L[1]=OP1 ELSE LET L[1]=1
2690 LET AW[4]=L[0]*T/YLD*100,AW[5]=L[1],AW[6]=OP0,AW[7]=AW[6]*OP2,AW[8]=L[4],AW[9]=L[5]
2700 LET AW[0]=FNOPNOSET(X)*T,AW[1]=FNOPNOSETDOL(X)*T,AW[2]=FNOP(X),AW[3]=FNOPDOL(X)
2710 LET AW[3]=ROUND(AW[3],2)
2800 REM " --- Write Operation Records"
2810 READ (WOE02_DEV,KEY=AW0$(1,12)+$FF$,DOM=2820)
2820 LET AW0$(13,3)="000"
2825 LET Z$=KEYP(WOE02_DEV,END=2840)
2830 IF POS(AW0$(1,12)=Z$)=1 THEN LET AW0$(13,3)=Z$(13,3)
2835 IF AW0$(13,3)="999" THEN LET MODULE$="Operations"; GOSUB 4500; GOTO 2900
2840 LET AW0$(13,3)=STR(NUM(AW0$(13,3))+1:"000")
2845 WRITE (WOE02_DEV,KEY=AW0$)IOL=WOE02A
2850 LET SUBS$=SUBS$+T9$+L0$(23,3)+AW0$(13,3)
2855 DIM AW[13]
2860 IF AW1$(91,1)<>"S" THEN GOTO 2865
2861 DIM CROSS_REF$(21)
2862 LET CROSS_REF$=N0$+"D  "+AW1$(1,6)+AW0$(5,7)+AW0$(13,3)
2863 WRITE (WOE03_DEV,KEY=CROSS_REF$)IOL=WOE03X
2865 LET Y0=POS(L0$(3,23)=MATS$,26)
2870 IF Y0=0 THEN GOTO 2890
2875 EXTRACT (WOE22_DEV,KEY=AW0$(1,11)+"B"+MATS$(Y0+23,3),DOM=2890)IOL=WOE22A1
2880 LET XW1$(47,3)=AW0$(13,3)
2885 WRITE (WOE22_DEV,KEY=AW0$(1,11)+"B"+MATS$(Y0+23,3))IOL=WOE22A1
2890 GOTO 2600
2895 RETURN 
2900 REM " --- Back Up The Levels
2910 IF T0$=O$ THEN GOTO 2955
2912 GOSUB 2500; REM "Do component labor
2913 GOSUB 3000; REM "Do component subs
2915 LET T[X,0]=0,T[X,1]=0
2920 LET T0$=T0$(1,X*20),X=X-1,T9$=T0$(X*20+1,20),T=1
2925 IF X=0 THEN GOTO 2940
2930 LET T=T[X,0]
2940 READ (BMM02_DEV,KEY=N0$+T9$+STR(T[X,1]:"000"),DOM=2920)
2950 GOTO 2060
2955 LET T9$=T0$; GOSUB 2500; REM "Now do final assembly labor
2960 GOSUB 3000; REM "Now do final assembly subs
2970 GOSUB 6300; REM "Go Schedule it!
2980 WRITE (WOE01_DEV,KEY=W0$(1,11))IOL=WOE01A
2990 GOTO 1200
3000 REM " --- Explode Subcontracts here"
3010 READ (BMM05_DEV,KEY=N0$+T9$,DOM=3020)
3020 DIM CW0$(15),CW1$(151),CW[13],S[9],X0$(31)
3030 LET CW0$(1)=W0$(1,11)+"C"
3050 REM " --- Read 4
3060 LET K8$=KEY(BMM05_DEV,END=3190)
3070 IF K8$(1,22)<>N0$+T9$ THEN GOTO 3190
3080 READ (BMM05_DEV,KEY=K8$)IOL=BMM05A
3085 LET EDATE$=S2$(1,3),ODATE$=S2$(4,3); GOSUB 4700; IF OK$="N" THEN GOTO 3050
3095 LET CW1$(4,3)=W0$(19,3),CW1$(7,6)=S1$(4,6),CW1$(91,1)=S1$(1,1)
3100 IF S1$(1,1)="S" THEN LET CW1$(2,2)="",CW1$(13,30)=S1$(10,30),CW1$(53,3)="",CW[0]=FNSUB(X),CW[1]=FNSUB(X)*S[2],CW[2]=O*CW[0],CW[3]=O*CW[1],CW[4]=S[2],CW[5]=S[4] ELSE LET CW1$(2,2)="",CW1$(13,30)=""
3105 LET CW0=POS(S0$(3,20)+S3$(1,3)=SUBS$,26); IF CW0>0 THEN LET CW1$(53,3)=SUBS$(CW0+23,3)
3110 IF S1$(1,1)<>"S" THEN LET CW1$(92,60)=S1$(10,60)
3120 READ (WOE32_DEV,KEY=CW0$(1,12)+$FF$,DOM=3130)
3130 LET CW0$(13,3)="000"
3140 LET Z$=KEYP(WOE32_DEV,END=3170)
3150 IF POS(CW0$(1,12)=Z$)=1 THEN LET CW0$(13,3)=Z$(13,3)
3160 IF CW0$(13,3)="999" THEN LET MODULE$="Subcontracts"; GOSUB 4500; GOTO 3190
3170 LET CW0$(13,3)=STR(NUM(CW0$(13,3))+1:"000")
3180 WRITE (WOE32_DEV,KEY=CW0$)IOL=WOE32A; GOTO 3050
3185 IF CW1$(91,1)<>"S" THEN GOTO 3190
3186 DIM CROSS_REF$(21)
3187 LET CROSS_REF$=N0$+"F  "+CW1$(7,6)+CW0$(5,7)+CW0$(13,3)
3188 WRITE (WOE03_DEV,KEY=CROSS_REF$)IOL=WOE03X
3190 RETURN 
3900 REM " --- Loop back for next record"
3990 GOTO 2000
4000 REM " --- All done"
4090 GOTO 9900
4500 REM " --- Issue warning for subs"
4505 DIM MESSAGE$[1]
4510 LET MESSAGE$[0]="Not enough free sequence numbers for "+MODULE$+".",MESSAGE$[1]="<Enter> to continue ",V0=1,V1=40+INT(LEN(V2$)/2),V2=22
4520 PRINT 'RB',
4530 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
4590 RETURN 
4700 REM " --- Verify date within Effective and Obsolete dates"
4710 LET OK$="Y"
4720 IF EDATE$<>"   " THEN IF W0$(16,3)<EDATE$ THEN LET OK$="N"; GOTO 4790
4730 IF ODATE$<>"   " THEN IF W0$(16,3)>=ODATE$ THEN LET OK$="N"
4790 RETURN 
5000 REM " --- Heading"
5010 LET L=0
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)"Work",@(42),"--- Scheduled ---"
5050 PRINT (7)"Order",@(8),"Item",@(29),"Pri Mthd Sts",@(44),"Start",@(51),"Complete",@(61),"Cat",@(66),"Scheduling Status",'LF'
5090 RETURN 
6100 REM " --- Next W/O #
6105 DIM WS0$(3)
6110 EXTRACT (WOS10_DEV,KEY=N0$+"N",DOM=6130)IOL=WOS10A
6120 GOTO 6140
6130 LET WS0=1000
6140 LET WS0=WS0+1
6150 IF WS0>9999999 THEN LET WS0[0]=1000
6160 LET WONUM$=STR(WS0:"0000000")
6170 WRITE (WOS10_DEV,KEY=N0$+"N")IOL=WOS10A
6190 RETURN 
6200 REM " --- GET WO Type code
6210 DIM FOR1$(83),FOR2$(155),FOR[1]
6220 LET TYPE$=K$(3,3)
6230 LET FOR0$=N0$+TYPE$
6240 FIND (MPM01_DEV,KEY=FOR0$)IOL=MPM01A
6250 IF FOR1$(62,2)<>"  " THEN LET WOTYPE$=FOR1$(62,2); GOTO 6290
6260 PRINT 'RB'
6270 DIM MESSAGE$[1]
6275 LET TITLE=2,MESSAGE$[0]="No Work Order Type Code Assigned to Forecast Type "+TYPE$+".",MESSAGE$[1]="Press <Enter> and Create One"
6280 CALL "SYC.XA",TITLE,MESSAGE$[ALL],1,22,-1,V$,V3
6285 LET WOTYPE$="ERR"
6290 RETURN 
6300 REM " --- Schedule it!"
6320 IF POS(W1$(98,1)="BF")=0 THEN GOTO 6600
6330 IF W1$(98,1)="F" THEN LET SDATE$=W0$(19,3) ELSE LET SDATE$=W0$(22,3)
6340 LET EDATE$="",STATUS$="00"
6350 CALL "WOC.AA",W0$(5,7),SDATE$,EDATE$,W1$(98,1),BMM08_DEV,STATUS$
6380 DIM LINEREC$(132)
6383 LET LINEREC$(1)=W0$(5,7),LINEREC$(9)=W0$(56),LINEREC$(30)=W1$(97,1)
6385 LET LINEREC$(34)=W1$(98,1),LINEREC$(39)=W0$(15,1),M$=""
6390 ON NUM(STATUS$(2,1)) GOTO 6400,6500,6550,6900,6600,6650,6700
6400 REM " --- Successful Schedule"
6410 IF W1$(98,1)="B" THEN LET W0$(19,6)=EDATE$+SDATE$ ELSE LET W0$(19,6)=SDATE$+EDATE$
6420 LET LINEREC$(43)=FNB$(W0$(19,3)),LINEREC$(52)=FNB$(W0$(22,3))
6430 LET LINEREC$(62)=W0$(14,1),M$="Scheduled"
6490 GOTO 6800
6500 REM " --- Unscheduled Date in Calendar"
6510 LET M$="Unscheduled Date Found in Operations Calendar"
6540 GOTO 6800
6550 REM " --- Invalid 'From' Date"
6560 IF W1$(98,1)="B" THEN LET MESS$="Completion" ELSE LET MESS$="Start"
6570 LET M$="Invalid Estimated "+MESS$+" Date"
6590 GOTO 6800
6600 REM " --- Invalid Scheduling Method"
6620 IF W1$(98,1)="M" THEN LET LINEREC$(13)=W1$(97,1),LINEREC$(17)=W1$(98,1),LINEREC$(22)=W0$(15,1),LINEREC$(25)="Manual Method. No Rescheduling Performed"; GOTO 6800
6630 LET LINEREC$(9)="***",LINEREC$(13)=W1$(97,1),LINEREC$(17)=W1$(98,1),LINEREC$(22)=W0$(15,1),LINEREC$(25)="Invalid Scheduling Method"
6640 GOTO 6800
6650 REM " --- Unscheduled Date in Purchase Calendar"
6660 LET M$="Unscheduled Date Found In Purchase Order Calendar"
6690 GOTO 6800
6700 REM " --- Undetermined error"
6710 LET M$="An Undetermined Error Has Occurred"
6800 REM " --- Now print the line!"
6810 IF L+2>L9 THEN GOSUB 5000
6815 LET LINEREC$(67)=M$
6820 PRINT (7)@(0),LINEREC$
6830 LET L=L+1
6860 IF STATUS$(1,1)="0" THEN GOTO 6890
6870 PRINT (7)@(8),"A Required Date Changed For A Subcontract For This Work Order"
6880 LET L=L+1
6890 RETURN 
6900 REM " --- File Missing"
6905 DIM MESSAGE$[1]
6910 LET MESSAGE$[0]="A necessary Shop Floor Control file is missing. Make sure the"
6920 LET MESSAGE$[1]="Calendar File and Work Orders exist, then rerun this task."
6930 CALL "SYC.XA",1,MESSAGE$[ALL],1,22,-1,V$,V3
6990 GOTO 4000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNN$(Q$)=Q$(3,4)+Q$(1,2)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8050 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8065 DEF FNSUB(Q)=S[0]*S[1]/S[3]
8070 DEF FNOPNOSET(Q)=100*(L[0]/L[1])/YLD
8075 DEF FNOPNOSETDOL(Q)=100*(L[0]*(AW[6]+AW[7])/L[1])/YLD
8080 DEF FNOP(Q)=T*A[2]*100*(L[0]/L[1])/YLD+L[4]
8085 DEF FNOPDOL(Q)=T*A[2]*100*(L[0]*(AW[6]+AW[7])/L[1])/YLD+L[4]*(AW[6]+AW[7])
8090 DEF FNQTY(Q)=10000*R[0]*R[1]/(R[3]*YLD*(100-R[4]))
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

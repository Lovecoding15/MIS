0010 REM "POC - Calendar Printing Selection Criteria"
0020 REM "Program POC.CC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "07May99 WGH: Display selected Months correctly (1085,1485)"
0032 REM "07May99 WGH: Validate selected date range (4005,6000-6190)"
0040 REM 
0045 REM "SYS01_DEV: SYS-01 channel number (input)"
0050 REM "POM01_DEV: POM-01 channel number (input)"
0055 REM "PARAMS[n]: Beginning/ending month/year (returned)"
0065 REM "STATUS   : 0=No error (returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER SYS01_DEV,POM01_DEV,PARAMS[ALL],STATUS
0200 REM " --- IOLISTs"
0210 POM01A: IOLIST A0$(1),A1$(1),A0
0260 IVS01A: IOLIST X$,X$,I2$,I3$,I4$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2)
0430 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0500 REM " --- Init Data"
0510 DIM A0$(6),A1$(31)
0550 LET PARAMS[0]=NUM(I4$(1,2)),PARAMS[1]=FNYY_YEAR(I4$(3,2))
0560 LET PARAMS[2]=PARAMS[0],PARAMS[3]=PARAMS[1],M6$="#0"
0700 REM " --- Background"
0710 FOR X=4 TO 14 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(9,3),"First Day Currently Scheduled:",@(10,5),"Last Day Currently Scheduled:",@(23,7),"Beginning Month:",@(24,9),"Beginning Year:",@(26,11),"Ending Month:",@(27,13),"Ending Year:",'SF',
0800 REM " --- Get First Day Scheduled"
0810 CALL "POC.CE",POM01_DEV,N0$,BEGDATE$,ENDDATE$,STATUS
0820 LET V1$="None",V2$="None"
0830 IF BEGDATE$<>"" THEN LET V1$=FNB$(BEGDATE$),V2$=FNB$(ENDDATE$)
0840 PRINT @(40,3),V1$,@(40,5),V2$,
0850 GOSUB 5000
1000 REM " --- First Month To Print"
1010 LET V4$="Enter The First Month To Print (<F1>=First)",I0=0
1020 LET V0$="N",V1$="KEC",V2$=STR(PARAMS[0]),V3$=M6$,V0=2,V1=40,V2=7
1030 GOSUB 7000
1040 ON V3 GOTO 1070,1050,3900,1000,3900
1050 LET PARAMS[0]=0
1055 PRINT @(V1,V2),"First",'CL',
1060 GOTO 1090
1070 IF V<1 OR V>12 THEN GOTO 1000
1080 LET PARAMS[0]=V
1085 PRINT @(V1,V2),FNMONTH$(V),'CL',
1090 GOTO 3900
1200 REM " --- First Year To Print"
1210 LET V4$="Enter The First Year To Print (<F1>=First)"
1220 LET V0$="D",V1$="KECY",V2$=FNYEAR_YY21$(PARAMS[1]),V3$="",V0=2,V1=40,V2=9
1230 GOSUB 7000
1240 ON V3 GOTO 1270,1250,3900,1200,3900
1250 LET PARAMS[1]=0
1255 PRINT @(V1,V2),"First",
1260 GOTO 1290
1270 LET PARAMS[1]=FNYY_YEAR(V$)
1280 PRINT @(V1,V2),FNYEAR_YY$(PARAMS[1]),'CL',
1290 GOTO 3900
1400 REM " --- Last Month To Print"
1410 LET V4$="Enter The Last Month To Print (<F1>=Last)"
1420 LET V0$="N",V1$="KEC",V2$=STR(PARAMS[2]),V3$=M6$,V0=2,V1=40,V2=11
1430 GOSUB 7000
1440 ON V3 GOTO 1470,1450,3900,1400,3900
1450 LET PARAMS[2]=0
1455 PRINT @(V1,V2),"Last",'CL',
1460 GOTO 1490
1470 IF V<1 OR V>12 THEN GOTO 1400
1480 LET PARAMS[2]=V
1485 PRINT @(V1,V2),FNMONTH$(V),'CL',
1490 GOTO 3900
1600 REM " --- Last Year To Print"
1610 LET V4$="Enter The Last Year To Print (<F1>=Last)"
1620 LET V0$="D",V1$="KECY",V2$=FNYEAR_YY21$(PARAMS[3]),V3$="",V0=2,V1=40,V2=13
1630 GOSUB 7000
1640 ON V3 GOTO 1670,1650,3900,1600,3900
1650 LET PARAMS[3]=0
1655 PRINT @(V1,V2),"Last",
1660 GOTO 1690
1670 LET PARAMS[3]=FNYY_YEAR(V$)
1680 PRINT @(V1,V2),FNYEAR_YY$(PARAMS[3]),'CL',
1690 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4005 GOSUB CHECK_DATE_RANGE; IF BAD_RANGE THEN GOTO 1000
4010 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
4020 ON V3 GOTO 4030,4000,4000,4000,9900
4030 ON POS(V$="YN") GOTO 4000,4050,1000
4090 GOTO 9900
5000 REM " --- Display"
5010 LET V1$="First",V2$="First",V3$="Last",V4$="Last"
5020 IF PARAMS[0]>0 THEN LET V1$=FNMONTH$(PARAMS[0])
5030 IF PARAMS[1]>0 THEN LET V2$=FNYEAR_YY$(PARAMS[1])
5040 IF PARAMS[2]>0 THEN LET V3$=FNMONTH$(PARAMS[2])
5050 IF PARAMS[3]>0 THEN LET V4$=FNYEAR_YY$(PARAMS[3])
5060 PRINT @(40,7),V1$,'CL',@(40,9),V2$,'CL',@(40,11),V3$,'CL',@(40,13),V4$,'CL',
5090 RETURN 
6000 REM " --- Validate Selected Date Range"
6005 CHECK_DATE_RANGE: 
6010 LET BAD_RANGE=0
6020 LET FIRSTMTH$=STR(PARAMS[0]:"00")
6030 IF PARAMS[0]=0 THEN IF BEGDATE$<>"" THEN LET FIRSTMTH$=FNA$(BEGDATE$(2),BEGDATE$) ELSE GOTO 6100
6040 LET FIRSTYR$=STR(PARAMS[1]:"000")
6050 IF PARAMS[1]=0 THEN IF BEGDATE$<>"" THEN LET FIRSTYR$=STR(FNYY_YEAR(FNA$(BEGDATE$(1),BEGDATE$)):"000") ELSE GOTO 6100
6060 LET LASTMTH$=STR(PARAMS[2]:"00")
6070 IF PARAMS[2]=0 THEN IF ENDDATE$<>"" THEN LET LASTMTH$=FNA$(ENDDATE$(2),BEGDATE$) ELSE GOTO 6100
6080 LET LASTYR$=STR(PARAMS[3]:"000")
6090 IF PARAMS[3]=0 THEN IF ENDDATE$<>"" THEN LET LASTYR$=STR(FNYY_YEAR(FNA$(ENDDATE$(1),BEGDATE$)):"000") ELSE GOTO 6100
6100 REM " --- Warn About Bad Date Range"
6105 IF LASTYR$+LASTMTH$>=FIRSTYR$+FIRSTMTH$ THEN GOTO 6190
6110 LET BAD_RANGE=1,NUMBER=1
6120 DIM MESSAGE$[NUMBER]
6130 LET MESSAGE$[0]=" Invalid date range! Please re-enter."
6140 LET MESSAGE$[1]="<Enter>=Continue    <F4>=Exit"
6150 CALL "SYC.XA",2,MESSAGE$[ALL],NUMBER,-1,-1,V$,V3
6160 ON V3 GOTO 6190,6100,6100,6100,6170
6170 EXITTO 9900
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8065 DEF FNMONTH$(Q)=STR(Q:"#0")+" "+DATE(JUL(1,Q,1):"%Ml")
8100 REM " --- FNYEAR_YY$ Un-Convert 21st Century Numeric Year to 2-Char Year"
8105 DEF FNYEAR_YY$(Q)=STR(MOD(Q,100):"00")
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 IF V3=4 THEN LET STATUS=V3
9950 EXIT 
9999 END

0010 REM "OPR - Open Order Detail Report"
0020 REM "Program OPR.IB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Jul96 JAL: Pagination problem during product subtotals (4145-4170,4255,4270,5300-5390)"
0032 REM "12Aug96 WGH: Using IVM-01 Maximum Quantity instead of IVM-02 Unit Cost (270,515,2134-2135)"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS>0 THEN GOTO 9900
0200 REM " --- IOLISTS"
0210 ARE03A: IOLIST A0$(1),A[ALL]
0220 ARM10E: IOLIST X0$
0230 ARM01A: IOLIST B0$,B1$
0240 ARM02A: IOLIST C0$,C1$,C[ALL]
0250 ARE23A: IOLIST R0$,R1$,R2$,R0,R1,R2
0260 IVM01A: IOLIST D0$,D1$(1),D2$,D3$,D4$,D5$,D6$,D[ALL]
0270 IVM02A: IOLIST Y0$,Y1$,Y2$,Y[ALL]
0290 IVM10A: IOLIST X0$(1)
0310 ARE13A: IOLIST W0$,W1$(1),W[ALL]
0320 ARM10A: IOLIST W$(1)
0400 REM " --- Parameters"
0450 LET H0=132,HEADINGS=6
0500 REM " --- Initializations"
0505 PRECISION NUM(I2$(5,1))
0510 DIM J$(H0,"-"),J1$(30),J2$(H0,"="),T9[999,3],R[3,4],HEADINGS$[HEADINGS]
0515 DIM A0$(101),W1$(64),A[10],W[14],D[10],S[2],T[2],U[2],Z[2],H$(H0),Y[20]
0520 DIM H[14],O[14],LINES$[2]
0540 LET H0=132,H=0,L9=59,L=L9+1,WHEN$=F0$(7,3),CLOCK$=""
0545 LET M9$="-###0.00%",T9$="",R9$="SPN",X2$=""
0550 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),M4=LEN(M4$),M9=LEN(M9$)
0555 LET LINES$[0]="Standard Items",LINES$[1]="Promotional Items"
0560 LET LINES$[2]="Non-Stock Items"
0570 IF I3$(17,1)="S" THEN LET X2$="Ser#"
0575 IF I3$(17,1)="L" THEN LET X2$="Lot#"
0580 IF S0$="" THEN LET H4$="First"
0585 IF S1$="" THEN LET H5$="Last"
0590 IF S2$="" THEN LET H6$="First"
0595 IF S3$="" THEN LET H7$="Last"
0600 REM " --- Headings"
0605 LET H4$="Beginning Customer: "+H4$,H5$="   Ending Customer: "+H5$
0610 LET H6$="Beginning Order Date: "+H6$,H7$="   Ending Order Date: "+H7$
0615 LET H8$=X1$+X3$,H9$="Entered By: "+H9$,X=H0-MAX(LEN(H6$),LEN(H7$))
0620 IF POS(" "<>X1$)>0 AND POS("All "=X3$)=1 THEN LET H8$=X3$(1,4)+X1$+X3$(5)
0625 IF S7$="" THEN LET H10$="All" ELSE LET H10$=S7$
0630 LET H10$="Salesperson: "+H10$,HEADINGS$[0]=N1$,HEADINGS$[1]=N4$
0635 LET H$(1)=H4$,H$(FNC(H8$,H0))=H8$,H$(X)=H6$,HEADINGS$[3]=H$
0640 LET H$(1)=H5$,H$(FNC(H10$,H0))=H10$,H$(X)=H7$,HEADINGS$[4]=H$
0645 LET HEADINGS$[5]=H9$,XM1=M1,XM2=M2,XM3=M3
0680 IF M1<9 THEN CALL "SYC.RM",M1$,M1,9,15
0685 IF M2<8 THEN CALL "SYC.RM",M2$,M2,8,12
0690 IF M3<8 THEN CALL "SYC.RM",M3$,M3,8,12
0695 LET XM1$=M1$,XM2$=M2$,XM3$=M3$
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,20,0
0720 CALL "SYC.NB","Printing:",LEN(M0$)+8,COLUMN
0800 REM " --- Print positions"
0810 LET X=14+I[0]+M1*3+M2*3+M3+M4+M9*2
0820 IF X<=H0 THEN GOTO 0850
0825 LET XM1=XM1-1,XM2=XM2-1,XM3=XM3-1
0830 CALL "SYC.RM",M1$,M1,9,XM1
0835 CALL "SYC.RM",M2$,M2,8,XM2
0840 CALL "SYC.RM",M3$,M3,8,XM3
0845 GOTO 0800
0850 LET O[14]=H0-M9,O[13]=O[14]-M1,O[12]=O[13]-M1,O[11]=O[12]-M3
0855 LET O[10]=O[11]-M1,O[9]=O[10]-M4,O[8]=O[9]-M9,O[7]=O[8]-M2
0860 LET O[6]=O[7]-M2,O[5]=O[6]-M2,O[4]=O[5]-I[0],O[3]=O[4]-4
0865 LET O[2]=O[3]-4,O[1]=O[2]-2,O[0]=O[1]-4
0870 IF O[0]<=M0+1 THEN GOTO 0880
0875 LET O[0]=M0+1,O[1]=O[0]+4,O[2]=O[1]+2,O[3]=O[2]+4,O[4]=O[3]+4
0880 FOR X=0 TO 14
0885 LET H[X]=O[X]
0890 NEXT X
0900 LET H[5]=H[5]+M2-6,H[6]=H[6]+M2-6,H[7]=H[7]+M2-5,H[8]=H[8]+M9-4
0905 LET H[9]=H[9]+M3-6,H[10]=H[10]+M1-9,H[11]=H[11]+M3-5
0910 LET H[12]=H[12]+M1-9,H[13]=H[13]+M1-7,H[14]=H[14]+M9-4
1000 REM " ---Initial File Read "
1020 READ (ARE03_DEV,KEY=N0$+"  "+S0$,DOM=1030)
1100 REM " --- Main Read "
1120 LET K1$=KEY(ARE03_DEV,END=4000)
1125 IF POS(N0$+"  "=K1$)<>1 THEN GOTO 4000
1130 IF S1$<>"" AND K1$(1,10)>N0$+"  "+S1$ THEN GOTO 4000
1140 IF K1$(18,3)<>"000" THEN READ (ARE03_DEV,KEY=K1$(1,17)+$FF$,DOM=1100)
1150 READ (ARE03_DEV,KEY=K1$)IOL=ARE03A
1155 PRINT @(COLUMN,11),FNF$(A0$(5,6),M0$)," ",A0$(11,7)
1160 IF S2$<>"" AND A0$(77,3)<S2$ THEN GOTO 1300
1170 IF S3$<>"" AND A0$(77,3)>S3$ THEN GOTO 1300
1190 IF S5$<>"" AND A0$(96,3)<>S5$ THEN GOTO 1300
1200 IF S7$<>"" AND A0$(59,3)<>S7$ THEN GOTO 1300
1205 IF A0$(22,1)="I" THEN GOTO 1300
1210 LET STAT$="Order"; IF A0$(21,1)="P" THEN LET STAT$="Quote" ELSE IF A0$(101,1)="C" THEN LET STAT$="Credit" ELSE IF A0$(101,1)="R" THEN LET STAT$="Release" ELSE IF A0$(23,1)="B" THEN LET STAT$="B/O"
1220 IF S6$="A" THEN GOTO 1260
1230 FIND (ARE04_DEV,KEY=N0$+"O"+"  "+A0$(5,13),DOM=1250)
1240 IF S6$="P" THEN GOTO 1300
1245 GOTO 1260
1250 IF S6$="U" THEN GOTO 1300
1260 IF POS("O"=S4$)>0 AND A0$(23,1)<>"B" AND A0$(101,1)<>"C" AND A0$(21,1)<>"P" THEN GOTO 1400
1270 IF POS("B"=S4$)<>0 AND A0$(23,1)="B" THEN GOTO 1400
1280 IF POS("C"=S4$)<>0 AND A0$(101,1)="C" THEN GOTO 1400
1290 IF POS("Q"=S4$)>0 AND A0$(21,1)="P" THEN GOTO 1400
1300 REM " --- Next Record "
1320 READ (ARE03_DEV,KEY=K1$(1,17)+$FF$,DOM=1100)
1400 REM " --- Heading "
1405 GOSUB 6000
1410 IF A0$(21,1)<>"V" THEN GOTO 1900
1420 REM " --- Void "
1430 IF L+2>L9 THEN GOSUB 5000
1440 PRINT (7)'LF',@(0),FNF$(A0$(5,P[0]),M0$)," ",B1$(1,30)," ",A0$(11,7)," ",A0$(42,7)," ",FNB$(A0$(24,3))," ",A0$(96,3)," ",A0$(59,3)," *** VOID ***"
1460 LET L=L+2
1490 GOTO 1100
1900 REM " --- Print "
1920 GOSUB 5200
2000 REM " --- Read Order Detail "
2005 REM " --- Position ARE-13 File Pointer "
2010 READ (ARE13_DEV,KEY=A0$(1,17)+"000",DOM=2020)
2020 LET K$=KEY(ARE13_DEV,END=3000)
2030 IF LEN(K$)<17 THEN GOTO 3000
2040 IF K$(1,17)<>A0$(1,17) THEN GOTO 3000
2060 READ (ARE13_DEV)IOL=ARE13A
2070 LET DESC$=W1$(1,40)
2080 DIM X0$(32); FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=2081)IOL=ARM10E
2120 IF POS(X0$(25,1)="SP")=0 THEN GOTO 2150
2130 DIM D[9],D1$(I[3]+I[4]+I[5])
2131 FIND (IVM01_DEV,KEY=N0$+W0$(33),DOM=2150)IOL=IVM01A
2132 IF X0$(27,1)="Y" AND P3$(28,1)="Y" THEN GOTO 2140
2134 FIND (IVM02_DEV,KEY=N0$+W0$(31),DOM=2140)IOL=IVM02A
2135 LET W[0]=Y[11]
2140 IF FNP$(DESC$)="" THEN LET DESC$=FNZ$(D1$,I[3],I[4],I[5])
2150 PRECISION 2
2160 LET COST=W[0]*W[2],PRICE=W[1]*W[2]
2170 PRECISION NUM(I2$(5,1))
2180 IF X0$(25,1)="O" THEN IF W1$(44,1)="Y" THEN LET COST=W[0],PRICE=W[6] ELSE LET COST=W[0],PRICE=W[1]
2190 GOSUB 5700
2200 LET S[0]=S[0]+PRICE,S[1]=S[1]+COST,S[2]=S[2]+W[9],S0=S0+1
2210 LET U[0]=U[0]+PRICE,U[1]=U[1]+COST,U[2]=U[2]+W[9]
2220 LET R9=0,R9=POS(X0$(25,1)=R9$)
2230 IF R9<>0 THEN LET R[R9-1,0]=R[R9-1,0]+PRICE,R[R9-1,1]=R[R9-1,1]+COST,R[R9-1,2]=R[R9-1,2]+W[2],R[R9-1,3]=R[R9-1,3]+W[3],R[R9-1,4]=R[R9-1,4]+W[4]
2240 IF X0$(25,1)="O" THEN LET R[3,0]=R[3,0]+PRICE
2290 IF X0$(32,1)="N" THEN GOTO 2900
2300 IF POS(W0$(23,3)=T9$,3)=0 THEN LET T9$=T9$+W0$(23,3)
2320 LET X=POS(W0$(23,3)=T9$,3),X=INT(X/3)
2340 LET T9[X,0]=T9[X,0]+PRICE,T9[X,1]=T9[X,1]+COST,T9[X,2]=T9[X,2]+W[9],T9[X,3]=T9[X,3]+W[2]
2900 GOTO 2020
3000 REM " --- Invoice Totals "
3005 IF S0<2 THEN GOTO 3040
3007 IF L+2>L9 THEN GOSUB 5200
3010 PRINT (7)@(O[10]),J$(1,H0-O[10])
3020 PRINT (7)@(O[10]-15),"Total For Order",@(O[10]),S[0]:M1$,@(O[12]),S[1]:M1$,@(O[13]),S[0]-S[1]:M1$,@(O[14]),FND(S[0]-S[1],S[0]):M9$
3030 LET L=L+2
3060 LET T2=T2+1,U2=U2+1
3100 DIM S[2]; LET S0=0
3900 GOTO 1100
4000 REM " --- Order Totals"
4010 IF H=0 THEN GOSUB 5000
4020 LET E9$="E"
4030 IF L+2>L9 THEN GOSUB 5000
4040 PRINT (7)J2$
4050 PRINT (7)STR(U2)," Orders Listed",@(O[10]-17),"Totals For Report",@(O[10]),U[0]:M1$,@(O[12]),U[1]:M1$,@(O[13]),U[0]-U[1]:M1$,@(O[14]),FND(U[0]-U[1],U[0]):M9$
4100 REM " --- Product Totals"
4110 IF T9$="" THEN GOTO 4500
4120 DIM T[3]
4125 LET M1$=XM1$,M1=LEN(M1$),M2$=XM2$,M2=LEN(M2$),M3$=XM3$,M3=LEN(M3$)
4130 LET L=L+LEN(T9$)/3+7
4140 IF L>L9 THEN GOSUB 5000
4150 GOSUB 5300
4200 REM 
4205 FOR X=1 TO LEN(T9$) STEP 3
4210 LET X0=INT(X/3)
4220 DIM X0$(32)
4230 LET X0$(7)="Unknown Product Type"
4240 FIND (IVM10_DEV,KEY=N0$+"A"+T9$(X,3),DOM=4250)IOL=IVM10A
4250 IF T9[X0,0]=0 AND T9[X0,1]=0 AND T9[X0,2]=0 THEN GOTO 4290
4255 IF L+1>L9 THEN GOSUB 5000; GOSUB 5300
4260 PRINT (7)T9$(X,3)," ",X0$(7,20),@(30),T9[X0,3]:M2$,T9[X0,0]:M1$,T9[X0,1]:M1$,T9[X0,0]-T9[X0,1]:M1$,FND(T9[X0,0]-T9[X0,1],T9[X0,0]):M9$
4270 LET T[0]=T[0]+T9[X0,0],T[1]=T[1]+T9[X0,1],T[2]=T[2]+T9[X0,2],T[3]=T[3]+T9[X0,3],L=L+1
4290 NEXT X
4400 REM 
4410 PRINT (7)@(30),J$(1,M2+M1*3+M9)
4420 PRINT (7)@(4),"Totals For Report",@(30),T[3]:M2$,T[0]:M1$,T[1]:M1$,T[0]-T[1]:M1$,FND(T[0]-T[1],T[0]):M9$
4500 REM " --- Line Totals"
4510 DIM T[1]
4515 LET L=L+12,O[0]=30,O[1]=O[0]+M2,O[2]=O[1]+M2
4520 LET O[3]=O[2]+M2,O[4]=O[3]+M1
4525 IF L>L9 THEN GOTO 4550
4530 PRINT (7)""
4535 PRINT (7)""
4545 GOTO 4560
4550 GOSUB 5000
4560 PRINT (7)""
4570 PRINT (7)"Line Type Totals"
4575 PRINT (7)""
4580 PRINT (7)"Line Type",@(O[0]+M2-6),"Order",@(O[1]+M2-4),"B/O",@(O[2]+M2-5),"Ship",@(O[3]+M1-6),"Sales",@(O[4]+M1-5),"Cost"
4590 PRINT (7)""
4600 REM 
4610 FOR X=0 TO 2
4620 IF R[X,0]=0 AND R[X,1]=0 AND R[X,2]=0 AND R[X,3]=0 AND R[X,4]=0 THEN GOTO 4650
4630 PRINT (7)LINES$[X],@(O[0]),R[X,2]:M2$,@(O[1]),R[X,3]:M2$,@(O[2]),R[X,4]:M2$,@(O[3]),R[X,0]:M1$,@(O[4]),R[X,1]:M1$
4640 LET T[0]=T[0]+R[X,0],T[1]=T[1]+R[X,1]
4650 NEXT X
4660 IF R[3,0]=0 THEN GOTO 4680
4670 PRINT (7)"Other Charges",@(O[3]),R[3,0]:M1$
4675 LET T[0]=T[0]+R[3,0]
4680 PRINT (7)@(O[0]),J$(1,M2*3+M1*2)
4690 PRINT (7)"Totals For Report",@(O[3]),T[0]:M1$,@(O[4]),T[1]:M1$
4990 GOTO 9900
5000 REM "HEADING"
5010 LET L=HEADINGS
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)"-- Customer --",@(41),"Order",@(50),"Order",@(59),"Ship",@(75),"P.O.",@(97),"Dist",@(120),"Tax"
5110 PRINT (7)"Number",@(8),"Name",@(40),"Number",@(50),"Date",@(59),"Date",@(66),"By",@(70),"Sls",@(74),"Number",@(85),"Ship Via",@(97),"Code",@(103),"Terms",@(120),"Code",@(126),"Prntd?"
5180 LET L=L+4
5190 RETURN 
5200 REM "ORDER HEADER HERE
5210 IF L+7>L9 THEN GOSUB 5000
5215 GOSUB 6900
5217 LET WIDTH=39-LEN(STAT$)-M0-2
5218 IF WIDTH>30 THEN LET WIDTH=30
5220 PRINT (7)'LF',@(0),FNF$(A0$(5,P[0]),M0$),@(M0+1),B1$(1,WIDTH),@(39-LEN(STAT$)),STAT$,@(40),A0$(11,7),@(48),FNB$(A0$(77,3)),@(57),FNB$(A0$(27,3)),@(66),A0$(96,3),@(70),A0$(59,3),@(74),A0$(49,10),@(85),A0$(32,10),@(98),A0$(66,2),@(103),W$,@(120),A0$(80,2),@(127),A0$(68,1)
5221 IF A0$(69,1)<>"N" THEN PRINT (7)@(40),"**Locked Flag="+A0$(69,1)+"**",
5225 PRINT (7)""
5230 PRINT (7)@(H[6]),"Back-",@(H[8]),"Disc",@(H[10]),"Extended",@(H[12]),"Extended"
5235 PRINT (7)@(H[0]),"Ln Cd",@(H[2]),"Prd Whs Item",@(H[5]),"Order",@(H[6]),"Order",@(H[7]),"Ship",@(H[8]),"Pct",@(H[9]),"Price",@(H[10])," Price",@(H[11]),"Cost",@(H[12]),"  Cost",@(H[13]),"Profit",@(H[14]),"GP %"
5280 LET L=L+5
5290 RETURN 
5300 REM " --- Product subtotals heading"
5310 PRINT (7)""
5320 PRINT (7)"Product Totals"
5330 PRINT (7)""
5340 PRINT (7)"Product",@(30+M2-6),"Units",@(30+M2+M1-6),"Sales",@(30+M2+M1*2-5),"Cost",@(30+M2+M1*3-7),"Profit",@(30+M2+M1*3+M9-4),"GP %"
5350 PRINT (7)""
5360 LET L=L+5
5390 RETURN 
5500 REM "SERIALIZED/LOTTED INVENTORY
5520 READ (ARE23_DEV,KEY=K$(1,20),DOM=5530)
5530 LET K2$=KEY(ARE23_DEV,END=5590)
5540 IF K2$(1,20)<>K$(1,20) THEN GOTO 5590
5550 READ (ARE23_DEV,KEY=K2$)IOL=ARE23A
5560 PRINT (7)@(12),X2$," ",K2$(21,3)," ",R1$,
5570 PRINT (7)@(132-M1*3-M3*3-M2*3-M9),R0:M2$,@(132-M1*3-M3*3-M2-M9),R1:M2$,@(132-M1*2-M3*2),R2:M3$
5575 LET L=L+1
5580 GOTO 5530
5590 RETURN 
5700 REM " --- Print Detail "
5720 IF L>L9 THEN GOSUB 5000; GOSUB 5200
5730 PRINT (7)@(O[0]),W0$(18,3)," ",W0$(21,1),@(O[3]),W0$(31,2),
5740 IF POS(X0$(25,1)="M")>0 THEN GOTO 5900
5760 IF POS(X0$(25,1)="O")>0 THEN GOTO 5820
5770 PRINT (7)@(O[2]),W0$(23,3),
5780 IF POS(X0$(25,1)="SP")>0 THEN PRINT (7)@(O[4]),W0$(33),
5810 PRINT (7)@(O[5]),W[2]:M2$,@(O[6]),W[3]:M2$,@(O[7]),W[4]:M2$,
5820 PRINT (7)@(O[8]),W[8]:M9$,@(O[9]),W[1]:M4$,@(O[10]),PRICE:M1$,@(O[11]),W[0]:M3$,@(O[12]),COST:M1$,@(O[13]),PRICE-COST:M1$,@(O[14]),FND(PRICE-COST,PRICE):M9$,
5840 IF X0$(25,1)="O" THEN GOTO 5900
5870 PRINT (7)""
5880 LET L=L+1
5900 PRINT (7)@(O[4]+3),DESC$,
5905 IF W1$(41,3)<>"   " AND W1$(41,3)<>A0$(27,3) AND X0$(25,1)<>"M" THEN PRINT (7)@(O[4]+I[3]+I[4]+I[5]+3),"Sched Ship Date: ",FNB$(W1$(41)),
5910 PRINT (7)'LF',
5920 LET L=L+1
5930 IF POS(X0$(25,1)="SP")<>0 AND POS(I3$(17,1)="SL")<>0 AND D2$(19,1)="Y" THEN GOSUB 5500
5990 RETURN 
6000 REM " --- Find Customer "
6020 DIM B1$(30),C[6]
6040 LET B1$(1)="Not On File"
6060 FIND (ARM01_DEV,KEY=N0$+A0$(5,6),DOM=6070)IOL=ARM01A
6070 FIND (ARM02_DEV,KEY=N0$+A0$(5,6)+"  ",DOM=6080)IOL=ARM02A
6090 RETURN 
6900 REM " --- Terms Code "
6910 DIM W$(30)
6920 LET W$(1)="* Not On File *"
6930 FIND (ARM10_DEV,KEY=N0$+"A"+A0$(62,2),DOM=6940)IOL=ARM10A
6940 LET W$=W$(6,15)
6950 RETURN 
8000 REM " --- Functions "
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8035 DEF FND(Q1,Q2)=MAX(MIN(SGN(ABS(Q2))*Q1*100/(Q2+ABS(SGN(Q2))-1),999.99),-999.99)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNZ$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

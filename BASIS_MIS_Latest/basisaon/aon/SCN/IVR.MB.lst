0010 REM "IVR - Inventory Requirements Report (Report Overlay)"
0020 REM "Program IVR.MB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 15Dec97 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "20Aug96 WGH: Committed and On-Hand quantities are reversed (6994)
0032 REM "03Sep96 WGH: When printing in Buyer sequence, stop after Ending Buyer (1110)"
0033 REM "24Oct96 JAL: Not clearing totals, use stocking level for order point and EOQ and calculate usage (520,555,820-850,1070-1090,5210-5250,6255,6645,6710,6920,6960-6980)"
0034 REM "07Feb97 JAL: Move last purchase date/cost to item line (645,660,1530,2015,2530,6620,6645,6650)"
0035 REM "18Feb97 WGH: Items with Stocking Level I don't print correctly by Buyer (1180-1190,1600-1790,1910)"
0036 REM "20May97 WGH: Eliminate extra blank lines/pages when printed by Vendor for a single Product Type (1520-1540,2520-2540,6620-6640,6650-6670)"
0037 REM "15Jun98 WGH: Item description may run into next field (645,660)"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 CALL "SYC.GA",7,1,"","",STATUS
0120 IF STATUS THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$,A5$,A6$,A[ALL]
0220 IVM02A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0230 IVM05A: IOLIST C0$(1),C1$(1),C2$(1),C[ALL]
0240 APM01A: IOLIST D0$(1),D1$(1)
0330 IVM10A: IOLIST X0$(1)
0340 IVM10C: IOLIST X0$(1)
0350 IVM10F: IOLIST X0$(1)
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A[12],C0$(28),C1$(32)
0520 DIM C2$(20),C[12],HEADINGS$[7],O[14],TOTAL[6],CHANNELS[20],ITEMS$[2]
0545 LET HEADINGS$[0]=F4$,WHEN$=F0$(7,3),CLOCK$="",H9$="",NUMBER=0,LAST$=""
0550 LET WIDTH=132,PAGE=0,L9=59,L=L9+1,NF$="(Not On File)",M0=LEN(M0$)
0555 LET HEADINGS=6,LASTDATE$="",LASTCOST$="",FLAG$=""
0560 LET BEGWHSE$=OPTIONS$[1],ENDWHSE$=OPTIONS$[2],BEGPROD$=OPTIONS$[3]
0565 LET ENDPROD$=OPTIONS$[4],BEGITEM$=OPTIONS$[5],ENDITEM$=OPTIONS$[6]
0570 LET TYPE$=OPTIONS$[7]
0590 IF SEQUENCE$="B" THEN LET HEADINGS=7
0600 REM " --- Print positions"
0610 CALL "SYC.RM",M2$,M2,10,12
0615 CALL "SYC.RM",M3$,M3,12,12
0620 LET O[12]=WIDTH-M3,O[11]=O[12]-9,O[10]=O[11]-M2
0625 FOR X=9 TO 4 STEP -1
0630 LET O[X]=O[X+1]-M2
0635 NEXT X
0640 LET O[3]=O[4]-11,O[2]=O[3]-3,O[1]=O[0]+P[0]+1,W=M0+4
0645 LET O[14]=O[11]-P[2]-12,O[13]=O[14]-36,DW=O[13]-O[1]-1
0650 IF SEQUENCE$="V" THEN LET W=M0+P[0]+1
0660 IF SEQUENCE$="B" THEN LET DW=O[14]-O[1]-1
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",W,COLUMN
0800 REM " --- Report headings"
0810 GOSUB 5800
0820 CALL "SYC.PB",N0$,SYS01_DEV,WHEN$,PERIOD$,YEAR$
0830 LET X$=FNB$(WHEN$),DAY_OF_MONTH=NUM(X$(4,2)),BASE$=PERIOD$+YEAR$
0840 LET CHANNELS[1]=IVM01_DEV,CHANNELS[2]=IVM02_DEV,CHANNELS[3]=IVM03_DEV
0850 LET CHANNELS[12]=IVM12_DEV,ITEMS$[0]=N0$
0900 REM " --- Position files"
0960 LET FIRST$=N0$+"F",K$=FIRST$+BEGBUYER$
0970 READ (IVM05_DEV,KEY=N0$+BEGVENDOR$,DOM=0980)
0980 READ (IVM03_DEV,KEY=K$,DOM=0990)
0990 ON POS(SEQUENCE$="BV") GOTO 4000,1000,2000
1000 REM " --- Get next sort record"
1010 LET K$=KEY(IVM03_DEV,END=4000)
1020 IF POS(FIRST$=K$)<>1 THEN GOTO 4000
1030 READ (IVM03_DEV)
1040 LET BUYER$=K$(4,3),VENDOR$=K$(7,6),PRODUCT$=K$(13,3)
1050 LET ITEM$=K$(16,20),WHSE$=K$(36,2)
1060 IF K$(4,10)=LAST$ THEN GOTO 1100
1070 LET LAST$=K$(4,10),X$=FILL(M0)
1080 IF AP$="Y" THEN IF FNP$(VENDOR$)<>"" THEN LET X$=FNF$(VENDOR$(1,I[0]),M0$)
1090 PRINT @(COLUMN,11),BUYER$," ",X$,
1100 REM " --- Outside range?"
1110 IF ENDBUYER$<>"" THEN IF BUYER$>ENDBUYER$ THEN GOTO 4000
1120 IF BEGVENDOR$<>"" THEN IF VENDOR$<BEGVENDOR$ THEN GOTO 1900
1130 IF ENDVENDOR$<>"" THEN IF VENDOR$>ENDVENDOR$ THEN GOTO 1900
1140 IF BEGPROD$<>"" THEN IF PRODUCT$<BEGPROD$ THEN GOTO 1900
1150 IF ENDPROD$<>"" THEN IF PRODUCT$>ENDPROD$ THEN GOTO 1900
1160 IF BEGITEM$<>"" THEN IF ITEM$<BEGITEM$ THEN GOTO 1900
1170 IF ENDITEM$<>"" THEN IF ITEM$>ENDITEM$ THEN GOTO 1900
1180 IF BEGWHSE$<>"" AND WHSE$<>"" THEN IF WHSE$<BEGWHSE$ THEN GOTO 1900
1190 IF ENDWHSE$<>"" AND WHSE$<>"" THEN IF WHSE$>ENDWHSE$ THEN GOTO 1900
1200 REM " --- Level breaks"
1210 IF BUYER$<>T0$ THEN GOSUB 6000
1220 IF AP$="Y" AND VENDOR$<>T1$ THEN GOSUB 6200
1230 IF PRODUCT$<>T2$ THEN GOSUB 6400
1240 IF ITEM$<>T3$ THEN GOSUB 6600
1250 IF TYPE$<>"" THEN IF A2$(53,3)<>TYPE$ THEN GOTO 1900
1300 REM " --- Additional reads"
1310 DIM C0$(28),C1$(32),C2$(20),C[12]
1320 LET C0$(1)=N0$+VENDOR$+ITEM$,LASTDATE$="",LASTCOST$="",VENDITEM$=""
1330 FIND (IVM05_DEV,KEY=C0$,DOM=1340)IOL=IVM05A
1340 IF FNP$(C2$)<>"" THEN LET VENDITEM$="Vendor Item "+C2$(1,P[2])
1350 IF FNP$(C1$(2,3))="" THEN GOTO 1400
1360 LET LASTDATE$=FNB$(C1$(2,3)),LASTCOST$=STR(C[6]:M3$)
1400 REM " --- New product?"
1410 IF H9$="" THEN GOTO 1500
1420 IF L+2>L9 THEN GOSUB 5000
1430 PRINT (7)@(O[0]),H9$
1440 PRINT (7)""
1450 LET L=L+2,H9$=""
1500 REM " --- New item?"
1510 IF DESCRIPTION$="" THEN GOTO 1600
1520 IF L+3>L9 THEN GOSUB 5000
1530 PRINT (7)'LF',@(O[0]),A0$(3,P[0]),@(O[1]),DESCRIPTION$,@(O[14]),VENDITEM$,@(O[11]),LASTDATE$,@(O[12]),LASTCOST$
1540 LET L=L+2,DESCRIPTION$=""
1600 REM " --- Stocking Level By Item?"
1610 IF A2$(24,1)<>"I" THEN GOTO 1700
1620 READ (IVM03_DEV,KEY=N0$+"G"+ITEM$,DOM=1650)
1650 REM " --- Get Warehouse For Stocking Level By Item"
1660 LET K2$=KEY(IVM03_DEV,END=1900)
1670 IF POS(N0$+"G"+ITEM$=K2$)<>1 THEN GOTO 1900
1680 READ (IVM03_DEV)
1690 LET WHSE$=K2$(24,2)
1700 REM " --- Retrieve detail line information"
1710 GOSUB 6900
1750 REM " --- Print detail line"
1760 GOSUB 5200
1790 IF A2$(24,1)="I" THEN GOTO 1650
1900 REM " --- Done with record"
1910 IF A2$(24,1)="I" THEN READ (IVM03_DEV,KEY=K$)
1990 GOTO 1000
2000 REM " --- Get next vendor/item record"
2010 LET K$=KEY(IVM05_DEV,END=4000)
2015 IF POS(N0$=K$)<>1 THEN GOTO 4000
2020 IF ENDVENDOR$<>"" THEN IF K$(3,6)>ENDVENDOR$ THEN GOTO 4000
2030 READ (IVM05_DEV)IOL=IVM05A
2040 LET VENDOR$=K$(3,6),ITEM$=K$(9),LASTDATE$="",LASTCOST$="",VENDITEM$=""
2050 IF BEGITEM$<>"" THEN IF ITEM$<BEGITEM$ THEN GOTO 3900
2060 IF ENDITEM$<>"" THEN IF ITEM$>ENDITEM$ THEN GOTO 3900
2065 PRINT @(COLUMN,11),FNF$(K$(3,I[0]),M0$)," ",ITEM$(1,P[0]),
2070 IF FNP$(C2$)<>"" THEN LET VENDITEM$="Vendor Item "+C2$(1,P[2])
2080 IF FNP$(C1$(2,3))="" THEN GOTO 2200
2090 LET LASTDATE$=FNB$(C1$(2,3)),LASTCOST$=STR(C[6]:M3$)
2200 REM " --- Additional reads
2210 IF AP$="Y" AND VENDOR$<>T1$ THEN GOSUB 6200
2220 IF ITEM$<>T3$ THEN GOSUB 6600
2225 IF TYPE$<>"" THEN IF A2$(53,3)<>TYPE$ THEN GOTO 3900
2230 LET PRODUCT$=A2$(1,3)
2240 IF BEGPROD$<>"" THEN IF PRODUCT$<BEGPROD$ THEN GOTO 3900
2250 IF ENDPROD$<>"" THEN IF PRODUCT$>ENDPROD$ THEN GOTO 3900
2260 IF PRODUCT$<>T2$ THEN GOSUB 6400
2270 IF A2$(24,1)="W" THEN GOTO 2300
2280 IF BEGBUYER$<>"" THEN IF A2$(9,3)<BEGBUYER$ THEN GOTO 3900
2290 IF ENDBUYER$<>"" THEN IF A2$(9,3)>ENDBUYER$ THEN GOTO 3900
2300 REM " --- For each warehouse for this item"
2380 LET FIRST$=N0$+"G"+ITEM$
2390 READ (IVM03_DEV,KEY=FIRST$,DOM=2400)
2400 REM " --- Next warehouse"
2410 LET K$=KEY(IVM03_DEV,END=3000)
2420 IF POS(FIRST$=K$)<>1 THEN GOTO 3000
2430 READ (IVM03_DEV)
2440 LET WHSE$=K$(24,2)
2450 IF BEGWHSE$<>"" THEN IF WHSE$<BEGWHSE$ THEN GOTO 2900
2460 IF ENDWHSE$<>"" THEN IF WHSE$>ENDWHSE$ THEN GOTO 3000
2470 GOSUB 6900
2475 IF A2$(24,1)="I" THEN GOTO 2500
2480 IF BEGBUYER$<>"" THEN IF B1$(19,3)<BEGBUYER$ THEN GOTO 2900
2490 IF BEGBUYER$<>"" THEN IF B1$(19,3)<BEGBUYER$ THEN GOTO 2900
2500 REM " --- Print item heading"
2510 IF DESCRIPTION$="" THEN GOTO 2600
2520 IF L+3>L9 THEN GOSUB 5000
2530 PRINT (7)'LF',@(O[0]),A0$(3,P[0]),@(O[1]),DESCRIPTION$,@(O[13]),H9$,@(O[14]),VENDITEM$,@(O[11]),LASTDATE$,@(O[12]),LASTCOST$
2540 LET L=L+2,DESCRIPTION$=""
2600 REM " --- Print detail line"
2610 GOSUB 5200
2900 REM " --- Loop back for next warehouse"
2990 GOTO 2400
3900 REM " --- Loop back for next record"
3990 GOTO 2000
4000 REM " --- All done"
4010 LET DONE=1
4020 GOSUB 6600
4030 IF NUMBER>0 THEN GOTO 9900
4040 GOSUB 5000
4050 PRINT (7)"No Items Found"
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+4
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)@(O[4]+M2-9),"Quantity",@(O[5]+M2-10),"Committed",@(O[6]+M2-10),"Quantity",@(O[7]+M2-9),"Quantity",@(O[8]+M2-6),"Order",@(O[10]+M2-7),"Annual",@(O[11]),"-- Last Purchase --"
5050 PRINT (7)@(O[0]),"Item",@(O[2]),"Wh",@(O[3]),"Location",@(O[4]+M2-9),"On Hand",@(O[5]+M2-10),"Quantity",@(O[6]+M2-10),"Available",@(O[7]+M2-9),"On Order",@(O[8]+M2-6),"Point",@(O[9]+M2-4),"EOQ",@(O[10]+M2-7),"Usage",@(O[11]+2),"Date",@(O[12]+M3-5),"Cost"
5060 PRINT (7)""
5090 RETURN 
5200 REM " --- Print warehouse line"
5210 LET POINT$="",EOQ$=""
5220 IF A2$(24,1)="W" THEN LET POINT$=STR(POINT:M2$),EOQ$=STR(EOQ:M2$)
5230 IF L+1>L9 THEN GOSUB 5000
5240 PRINT (7)@(O[2]),WHSE$,@(O[3]),LOCATION$,@(O[4]),ONHAND:M2$,@(O[5]),COMMITTED:M2$,@(O[6]),AVAILABLE:M2$,@(O[7]),ORDERED:M2$,@(O[8]),POINT$,@(O[9]),EOQ$,@(O[10]),USAGE:M2$,FLAG$
5250 LET L=L+1,L1=L1+1,NUMBER=NUMBER+1
5400 REM " --- Accumulate totals"
5410 LET TOTAL[0]=TOTAL[0]+ONHAND,TOTAL[1]=TOTAL[1]+COMMITTED
5420 LET TOTAL[2]=TOTAL[2]+AVAILABLE,TOTAL[3]=TOTAL[3]+ORDERED
5430 LET TOTAL[4]=TOTAL[4]+POINT,TOTAL[5]=TOTAL[5]+EOQ,TOTAL[6]=TOTAL[6]+USAGE
5490 RETURN 
5800 REM " --- Initialize HEADINGS$"
5810 DIM H1$(WIDTH),H2$(WIDTH),H3$(WIDTH)
5815 LET X3$="All Item Types"
5820 IF TYPE$<>"" THEN LET X3$="Item Type "+FNP$(TYPE$)
5825 IF SEQUENCE$="B" THEN LET HEADINGS$[1]=F5$+" By Buyer Code For "+X3$
5830 IF SEQUENCE$="V" THEN LET HEADINGS$[1]=F5$+" By Vendor For "+X3$
5835 LET X1$="First",X2$=X1$,X3$=X1$,X4$=X1$,X5$=X1$,X2=5
5840 IF BEGITEM$<>"" THEN LET X1$=BEGITEM$
5845 IF BEGPROD$<>"" THEN LET X2$=BEGPROD$,X2=3
5850 IF BEGWHSE$<>"" THEN LET X3$=BEGWHSE$
5855 IF BEGBUYER$<>"" THEN LET X4$=BEGBUYER$
5860 IF BEGVENDOR$<>"" THEN LET X5$=FNF$(BEGVENDOR$(1,I[0]),M0$)
5865 IF ENDPROD$="" THEN IF X2=3 THEN LET X2=4
5870 LET H1$(1)="From Item: "+X1$,H1$(37)="From Warehouse: "+X3$
5875 LET H1$(65)="From Buyer: "+X4$,H1$(89)="From Vendor: "+X5$
5880 LET H1$(WIDTH-X2-13)="From Product: "+X2$
5885 LET X1$="Last",X2$=X1$,X3$=X1$,X4$=X1$,X5$=X1$
5890 IF ENDITEM$<>"" THEN LET X1$=ENDITEM$
5895 IF ENDPROD$<>"" THEN LET X2$=ENDPROD$
5900 IF ENDWHSE$<>"" THEN LET X3$=ENDWHSE$
5905 IF ENDBUYER$<>"" THEN LET X4$=ENDBUYER$
5910 IF ENDVENDOR$<>"" THEN LET X5$=FNF$(ENDVENDOR$(1,I[0]),M0$)
5915 LET H2$(1)="  To Item: "+X1$,H2$(39)="To Warehouse: "+X3$
5920 LET H2$(67)="To Buyer: "+X4$,H2$(91)="To Vendor: "+X5$
5925 LET H2$(WIDTH-X2-13)="  To Product: "+X2$
5930 LET HEADINGS$[HEADINGS-2]=H1$,HEADINGS$[HEADINGS-1]=H2$
5935 IF SEQUENCE$="B" THEN LET HEADINGS$[3]=H3$
5990 RETURN 
6000 REM " --- Buyer break"
6010 IF AP$="Y" THEN GOSUB 6200
6050 DIM X0$(32)
6060 LET T0$=BUYER$,X0$(7,20)=NF$,L=L9+1
6070 FIND (IVM10_DEV,KEY=N0$+"F"+T0$,DOM=6080)IOL=IVM10F
6080 LET X$="Buyer: "+T0$+" "+X0$(6,24),HEADINGS$[3](1,LEN(X$))=X$
6090 RETURN 
6200 REM " --- Vendor break"
6210 DIM D0$(8),D1$(30)
6220 IF SEQUENCE$="B" THEN GOSUB 6400
6225 IF SEQUENCE$="V" THEN GOSUB 6600
6230 LET T1$=VENDOR$,D0$(1)=N0$+VENDOR$,D1$(1)=NF$,L=L9+1
6240 FIND (APM01_DEV,KEY=D0$,DOM=6250)IOL=APM01A
6250 LET X$="Vendor: "+FNF$(D0$(3,I[0]),M0$)+" "+FNP$(D1$)
6255 IF FNP$(VENDOR$)="" THEN LET X$="Vendor: None"
6260 IF SEQUENCE$="B" THEN LET HEADINGS$[3](78)="",HEADINGS$[3](WIDTH-LEN(X$)+1)=X$
6270 IF SEQUENCE$="V" THEN LET HEADINGS$[2]=X$
6290 RETURN 
6400 REM " --- Product break"
6410 IF SEQUENCE$="B" THEN GOSUB 6600
6420 DIM X0$(32)
6430 LET T2$=PRODUCT$,X0$(7,24)=NF$
6440 FIND (IVM10_DEV,KEY=N0$+"A"+T2$,DOM=6450)IOL=IVM10A
6450 LET H9$="Product Type "+T2$+" "+X0$(7,20)
6490 RETURN 
6600 REM " --- Item break"
6610 IF T3$="" THEN GOTO 6700
6620 LET POINT$="",EOQ$=""
6630 IF L1<=1 THEN GOTO 6700
6640 IF L+1>L9 THEN GOSUB 5000
6645 IF A2$(24,1)="I" THEN LET POINT$=STR(A[4]:M2$),EOQ$=STR(A[6]:M2$)
6650 PRINT (7)@(O[4]-15),"Total For Item",@(O[4]),TOTAL[0]:M2$,@(O[5]),TOTAL[1]:M2$,@(O[6]),TOTAL[2]:M2$,@(O[7]),TOTAL[3]:M2$,@(O[8]),POINT$,@(O[9]),EOQ$,@(O[10]),TOTAL[6]:M2$
6670 LET L=L+1
6700 IF DONE THEN GOTO 6790
6710 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A[12],DESCRIPTION$(DW),TOTAL[6]
6720 LET T3$=ITEM$,A0$(1)=N0$+T3$,A1$(1)=NF$,A2$(1)="",L1=0
6730 FIND (IVM01_DEV,KEY=A0$,DOM=6740)IOL=IVM01A
6740 LET DESCRIPTION$(1)=FNITEM$(A1$,P[3],P[4],P[5])
6750 IF SEQUENCE$="V" THEN GOSUB 6400
6790 RETURN 
6900 REM " --- Retrieve warehouse record"
6910 DIM B0$(24),B1$(64),B[20]
6920 LET B0$(1)=N0$+WHSE$+ITEM$,ITEMS$[1]=WHSE$,ITEMS$[2]=ITEM$
6930 FIND (IVM02_DEV,KEY=B0$,DOM=6940)IOL=IVM02A
6940 LET LOCATION$=B1$(1,10),ONHAND=B[0],COMMITTED=B[2],ORDERED=B[1]
6950 LET AVAILABLE=ONHAND-COMMITTED,POINT=B[4],EOQ=B[6]
6960 CALL "IVC.AU",ACTION$,CHANNELS[ALL],ITEMS$[ALL],FACTORS[ALL],DAY_OF_MONTH,BASE$,I4$,G[0],P[1],USAGE,ESTIMATED,FLAG$,STATUS
6970 IF FLAG$<>"" THEN LET USAGE=ESTIMATED
6980 IF A2$(24,1)<>"W" THEN LET POINT=0,EOQ=0
6990 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

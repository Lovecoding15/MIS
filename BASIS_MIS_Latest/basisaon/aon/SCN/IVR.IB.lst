0010 REM "IVR - Inventory Vendor Listing (Report Overlay)"
0020 REM "Program IVR.IB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "25Sep96 JJD: Correct error 47 when Product Types are specified (565)"
0032 REM "01Jan97 JWK: Ignores Prod Type and has vendors with 0 Items (1200-1280,1310-1390)
0033 REM "20Feb97 JAL: Can includes wrong item(s) for vendor (1170-1190,1200-1210)"
0040 REM 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLists
0210 APM01A: IOLIST X0$,X1$
0220 IVM01A: IOLIST B0$,B1$(1),B2$,B3$,B4$,B5$,B6$,B[ALL]
0230 IVM05A: IOLIST D0$,D1$,D2$,D[ALL]
0400 REM " --- Parameters"
0410 LET WIDTH=132,HEADINGS=5
0500 REM " --- Init Data"
0505 PRECISION P[1]
0510 DIM B1$(60),B[12],D[12],O[13],HEADINGS$[HEADINGS]
0520 LET H=0,L9=59,L=L9+1,WHEN$=F0$(7,3),CLOCK$="",X$=FNB$(WHEN$)
0540 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,DAY_OF_MTH=NUM(X$(4,2))
0550 LET HEADINGS3$=FILL(WIDTH),HEADINGS$[4]=FILL(WIDTH),M9=LEN(M9$)
0560 IF BEG_ITEM$="" THEN LET HEADINGS3$(1)="From Item: First" ELSE LET HEADINGS3$(1)="From Item: "+BEG_ITEM$
0565 IF BEG_PROD$="" THEN LET HEADINGS3$(113)="From Product: First" ELSE LET HEADINGS3$(113)="From Product: "+BEG_PROD$
0570 IF END_ITEM$="" THEN LET HEADINGS$[4](3)="To Item: Last" ELSE LET HEADINGS$[4](3)="To Item: "+END_ITEM$
0575 IF END_PROD$="" THEN LET HEADINGS$[4](115)="To Product: Last" ELSE LET HEADINGS$[4](115)="To Product: "+END_PROD$
0580 IF ITEM_TYPE$="" THEN LET HEADINGS$[4](59,14)="Item Type: All" ELSE LET HEADINGS$[4](60,14)="Item Type: "+ITEM_TYPE$
0590 LET BASE_PERYR$=P4$(1,4)
0600 REM " --- Print positions"
0610 CALL "SYC.RM",M2$,M2,8,12
0620 CALL "SYC.RM",M3$,M3,8,12
0630 LET O[0]=0,O[1]=O[0]+P[0]+1,O[2]=O[1]+5,O[3]=O[2]+4,O[4]=O[3]+3
0640 LET O[5]=O[4]+2,O[6]=WIDTH-M2*3-M3*2-17,O[7]=O[6]+9,O[8]=O[7]+4
0650 LET O[9]=O[8]+M3,O[10]=O[9]+M2+1,O[11]=O[10]+2,O[12]=O[11]+M2+1
0660 LET O[13]=O[12]+M2
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",M9+1+P[0],COLUMN
1000 REM " --- Initial Read"
1090 READ (IVM05_DEV,KEY=N0$+BEG_VEND$,DOM=1100)
1100 REM " --- Heading Read"
1120 LET K$=KEY(IVM05_DEV,END=4000)
1130 IF POS(N0$=K$)<>1 THEN GOTO 4000
1150 IF END_VEND$<>"" THEN IF K$(3,6)>END_VEND$ THEN GOTO 4000
1170 IF BEG_ITEM$<>"" THEN IF K$(9,20)<BEG_ITEM$ THEN LET K$=K$(1,8)+BEG_ITEM$; GOTO 1200
1180 IF END_ITEM$<>"" THEN IF K$(9,20)>END_ITEM$ THEN LET K$=K$(1,8)+$FF$; GOTO 1200
1200 REM " --- Position IVM-05"
1210 READ (IVM05_DEV,KEY=K$,DOM=1100)IOL=IVM05A
1300 REM " --- Additional Reads"
1310 FIND (IVM01_DEV,KEY=N0$+K$(9,20),DOM=1100)IOL=IVM01A
1320 IF BEG_PROD$<>"" THEN IF B2$(1,3)<BEG_PROD$ THEN GOTO 1100
1330 IF END_PROD$<>"" THEN IF B2$(1,3)>END_PROD$ THEN GOTO 1100
1350 IF ITEM_TYPE$<>"" THEN IF B2$(53,3)<>ITEM_TYPE$ THEN GOTO 1100
1360 REM " --- Breaks"
1370 IF D0$(1,2)<>N0$ THEN GOTO 4000
1380 IF D0$(3,6)<>T0$ THEN GOSUB 6000
1390 PRINT @(COLUMN,11),FNF$(D0$(3,VENDLEN),M9$)," ",D0$(9,P[0]),
1400 REM " --- Get Actual Annual Usage & EOQ"
1410 DIM CHANNS[12],ITEM$[2],WHSE$[2],WHSE[20]
1420 LET CHANNS[1]=IVM01_DEV,CHANNS[2]=IVM02_DEV,CHANNS[3]=IVM03_DEV,CHANNS[12]=IVM12_DEV
1430 LET ITEM$[0]=N0$,ITEM$[1]="",ITEM$[2]=B0$(3,20)
1460 CALL "IVC.AU","I",CHANNS[ALL],ITEM$[ALL],ABCF[ALL],DAY_OF_MTH,BASE_PERYR$,P4$(1,4),G[0],P[1],W0,ESTIMATED,FLAG$,STATUS
1465 IF FLAG$<>"" THEN LET W0=ESTIMATED
1470 IF STATUS<>0 THEN LET USAGE$=FILL(M2,"*") ELSE LET USAGE$=STR(W0:M2$)
1480 IF B2$(24,1)="I" THEN LET EOQ_CODE$=B2$(26,1),EOQ_QTY$=STR(B[6]:M2$); GOTO 1500
1485 CALL "IVC.WA","A",CHANNS[ALL],ITEM$[ALL],WHSE$[ALL],WHSE[ALL],VALUE,STATUS
1490 IF STATUS<>0 THEN LET EOQ_CODE$="*",EOQ_QTY$=FILL(M2,"*") ELSE LET EOQ_CODE$=WHSE$[1](57,1),EOQ_QTY$=STR(WHSE[6]:M2$)
1500 REM " --- Print"
1520 IF L+2>L9 THEN GOSUB 5000
1540 PRINT (7)@(O[0]),B0$(3,P[0]),@(O[1]),B2$(1,3),@(O[2]),B2$(6,2),@(O[3]),B2$(24,1),@(O[4]),D1$(1,1),@(O[5]),D2$(1,20),@(O[6]),FNB$(D1$(2,3)),@(O[7]),D[7]:"##0",@(O[8]),D[6]:M3$,@(O[9]),EOQ_QTY$,@(O[10]),EOQ_CODE$,@(O[11]),USAGE$,FLAG$,
1550 IF D[3]<>0 THEN PRINT (7)@(O[12]),D[0]:M2$,@(O[13]),D[3]:M3$,
1560 PRINT (7)""
1565 LET D9$=FNITEM$(B1$,P[3],P[4],P[5])
1570 PRINT (7)@(O[0]+3),D9$,
1580 IF D[4]<>0 THEN PRINT (7)@(O[12]),D[1]:M2$,@(O[13]),D[4]:M3$,
1590 PRINT (7)""
1600 LET L=L+2,T0=T0+1
1610 IF D[5]=0 THEN GOTO 1700
1620 IF L+1>L9 THEN GOSUB 5000
1630 PRINT (7)@(O[12]),D[2]:M2$,@(O[13]),D[5]:M3$
1640 LET L=L+1
1990 GOTO 1100
4000 REM " --- All done"
4010 LET T9$="E"
4020 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS<>0 THEN EXITTO 9900
5040 PRINT (7)@(O[1]),"Prod",@(O[3]-1),"Stk",@(O[4]),"P",@(O[5]),"Vendor",@(O[6]),FILL(M3+13,"-"),@(O[6]+FNC("Last Purchase",M3+13)),"Last Purchase",@(O[12]-7),"Annual",@(O[12]),FILL(M2+M3,"-"),@(O[12]+FNC("Price Breaks",M2+M3)),"Price Breaks"
5050 PRINT (7)@(O[0]),"Item",@(O[1]),"Type",@(O[2]),"UM",@(O[3]-1),"Lev",@(O[4]),"S",@(O[5]),"Item Number",@(O[6]+2),"Date",@(O[7]-1),"Lead",@(O[9]-10),"Unit Cost",@(O[10]-5),"EOQ",@(O[12]-7),"Usage",@(O[13]-9),"Quantity",@(WIDTH-10),"Unit Cost",'LF'
5080 LET L=L+3
5090 RETURN 
6000 REM " --- Vendor"
6010 IF T0$="" THEN GOTO 6100
6020 IF L+2>L9 THEN GOSUB 5000
6030 PRINT (7)'LF',@(0),T0:"###,##0"," Items For ",H3$
6080 LET L=L9+1,T1$=""
6100 IF T9$<>"" THEN RETURN 
6110 LET HEADINGS$[3]=HEADINGS3$
6120 LET T0$=D0$(3,6),H3$="Vendor: Not On File",T0=0
6130 FIND (APM01_DEV,KEY=N0$+D0$(3,6),DOM=6160)IOL=APM01A
6140 LET H3$=FNP$("Vendor: "+FNF$(X0$(3,VENDLEN),M9$)+" "+X1$(1,30))
6160 LET HEADINGS$[3](FNC(H3$,WIDTH),LEN(H3$))=H3$
6190 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

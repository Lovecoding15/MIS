0010 REM "WOU - Work Order Time Sheet Update"
0020 REM "Program WOU.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "23Jul96 JJD: Calculate Overhead total (6070)"
0032 REM "12Nov96 JJD: Send Payroll entries to PRE-02 instead of PRE-01 & PRE-11 (230,250,510,645,647,660,680,685,830,865,1705,1710,1715,1725,1740-1755,1910-1990)"
0033 REM "14Nov96 JJD: Generate payroll entries correctly (210,1505,1565-80)"
0034 REM "14Nov96 JJD: Correct precision rounding problems (6010-60)"
0035 REM "15Nov96 JJD: Prevent error 47 @2750 (2710)"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=16
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOT-01",FILES$[2]="WOE-01",FILES$[3]="WOM-10"
0120 LET FILES$[4]="WOM-01",FILES$[5]="SYS-01",FILES$[6]="WOM-07"
0150 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOT01_DEV=CHANNELS[1],WOE01_DEV=CHANNELS[2],WOM10_DEV=CHANNELS[3]
0165 LET WOM01_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5],WOM07_DEV=CHANNELS[6]
0200 REM " --- IOLIST's"
0210 PRM02A: IOLIST C0$,C[ALL]
0220 PRM01A: IOLIST B0$,B1$
0230 PRE02A: IOLIST D0$,D1$,D[ALL]
0240 TIMESHEET: IOLIST W0$,W1$,W[ALL]
0245 WOM10A: IOLIST T0$,T1$(1)
0255 WOM01A: IOLIST B0$,B1$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0265 IVS01A: IOLIST *,*,I9$
0270 WOS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0285 PRS01A: IOLIST X$,I1$,I2$,I3$,I4$,I5$
0290 PRS01B: IOLIST *,P0$
0300 PRE01A: IOLIST R0$
0360 WOT01A: IOLIST Y0$,Y1$,Y[ALL]
0370 WOE01A: IOLIST H0$,H1$,H[ALL]
0380 WOM07A: IOLIST CROSSREF$
0390 PRM10A: IOLIST *,X[ALL]
0400 REM " --- Parameters"
0405 DIM P[4],A[2],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$,PR$="N"
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0435 LET PR$=P3$(4,1)
0440 IF PR$<>"Y" THEN GOTO 0470
0445 CALL "SYC.VA","PR",INFO$[ALL]
0450 LET PR$=INFO$[20]
0455 IF PR$<>"Y" THEN GOTO 0470
0460 FIND (SYS01_DEV,KEY=N0$+"PR00",DOM=9800)IOL=PRS01A
0463 FIND (SYS01_DEV,KEY=N0$+"PR01",DOM=4900)IOL=PRS01B
0465 LET P1$=I1$,P2$=I2$,P9$=I3$,M0$=I5$
0470 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(19,6))
0475 LET P[3]=NUM(I9$(5,1)),G[4]=NUM(G2$(9,2))
0480 CALL "SYC.VA",N2$,INFO$[ALL]
0485 LET GL$=INFO$[9]
0490 LET IVS01_PRECISION=P[3]
0495 IF PR$="Y" THEN LET PRS01_PRECISION=NUM(P9$(7,1))
0500 REM " --- Initializations"
0510 DIM J$(80,"-"),W[11],C[1],B[5],D[2]
0530 LET T$=P3$(25,1),M0=LEN(M0$)
0600 REM " --- Additional File Opens"
0605 ON POS(P3$(25,1)="DEW") GOTO 9900,0610,0610,0610
0610 IF P3$(25,1)="D" THEN LET FILES$[7]="WOE-11"
0615 IF P3$(25,1)="E" THEN LET FILES$[7]="WOE-21"
0620 IF P3$(25,1)="W" THEN LET FILES$[7]="WOE-31"
0625 LET OPTIONS$[7]="L"
0630 IF GL$<>"Y" THEN GOTO 0640
0635 LET FILES$[8]="GLM-01",FILES$[9]="GLT-04",FILES$[10]="GLT-05"
0640 IF PR$<>"Y" THEN GOTO 0650
0645 LET FILES$[11]="PRE-02",FILES$[12]="PRM-02",FILES$[13]="PRM-10"
0647 LET FILES$[14]="PRM-01"
0650 IF P3$(3,1)<>"Y" THEN GOTO 0660
0655 LET FILES$[15]="BMM-08"
0660 CALL "SYC.DA",1,7,15,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0665 IF STATUS THEN GOTO 9900
0670 LET TIMESHEET_DEV=CHANNELS[7]
0675 LET GLM01_DEV=CHANNELS[8],GLT04_DEV=CHANNELS[9],GLT05_DEV=CHANNELS[10]
0680 LET PRE02_DEV=CHANNELS[11],PRM02_DEV=CHANNELS[12],PRM10_DEV=CHANNELS[13]
0685 LET PRM01_DEV=CHANNELS[14],BMM08_DEV=CHANNELS[15]
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0800 REM " --- Options"
0810 LET V4$="Are You Ready To Update The Time Sheet Register?"
0815 CALL "SYC.YN",0,V4$,0,V$,V3
0820 IF V$<>"YES" THEN GOTO 9900
0825 IF PR$="N" THEN GOTO 0900
0830 PRINT @(22,3),"Current Pay Period Ending Date: ",FNB$(P0$(1,3)),
0835 CALL "SYC.WC",1,0,80,0,0,4,0
0840 LET V4$="Generate Payroll Entries From These Time Sheets?",X$="Not "
0845 CALL "SYC.YN",0,V4$,0,V$,V3
0850 ON V3 GOTO 0855,0855,0800,0855,9900
0855 IF V$="YES" THEN LET X$=""
0860 LET V4$=X$+"Generating Payroll Entries"
0865 PRINT @(INT(40-(LEN(V4$)/2)),5),V4$
0870 CALL "SYC.WC",1,0,80,0,0,6,0
0875 LET P8$=V$
0900 REM 
0910 CALL "SYC.NB","Updating",9+M0+8,COLUMN
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
1000 REM " --- Initial Read "
1010 PRECISION IVS01_PRECISION
1020 READ (TIMESHEET_DEV,KEY=N0$,DOM=1100)
1100 REM " --- Main Read "
1120 LET K0$=KEY(TIMESHEET_DEV,END=4000)
1160 IF POS(N0$=K0$)<>1 THEN GOTO 4000
1180 READ (TIMESHEET_DEV,KEY=K0$)IOL=TIMESHEET
1190 IF W0$(LEN(W0$)-1,2)="00" THEN GOTO 2900
1200 ON POS(T$="DEW")-1 GOTO 1210,1240,1280
1210 LET W9$=W0$(3,12)+W1$(7,7)
1220 GOTO 1300
1240 LET W9$=W0$(12,3)+W0$(3,9)+W1$(7,7)
1260 GOTO 1300
1280 LET W9$=W0$(10,3)+W1$(7,9)+W0$(3,7)
1300 PRINT @(COLUMN,11),FNB$(W9$(1,3))," ",FNF$(W9$(4,P[0]),M0$)," ",W9$(13,7),
1500 REM " --- Post To Payroll "
1505 IF P8$<>"YES" THEN GOTO 2000
1510 PRECISION PRS01_PRECISION
1515 FIND (PRM01_DEV,KEY=N0$+W9$(4,9),DOM=2000)IOL=PRM01A
1520 DIM X[2]
1525 FIND (PRM10_DEV,KEY=N0$+"A"+W1$(24,2),DOM=1530)IOL=PRM10A
1530 IF X[1]=0 THEN LET X[1]=1
1535 IF P3$(11,1)<>"A" THEN GOTO 1550
1540 LET X[2]=W[1]
1545 GOTO 1700; REM "Actual pay rate
1550 LET X[2]=X[0]*X[1]; REM "Pay code rate
1555 FIND (PRM02_DEV,KEY=B0$+W1$(26,2),DOM=2000)IOL=PRM02A
1565 IF B1$(225,1)="S" AND C[1]<>0 THEN LET C[0]=C[0]/C[1]
1570 IF X[0]<>0 THEN LET C[0]=X[0]; REM " Override by pay code
1575 IF X[1]<>0 THEN LET C[0]=C[0]*X[1]; REM "Premium rate
1580 LET X[2]=C[0]; REM "Employee rate
1700 REM " --- Build It "
1705 DIM D0$(34)
1710 LET D0$(1)=N0$+W9$(4,9),D9=1
1715 IF D9>999 THEN GOTO 1725 ELSE GOTO 1740
1720 DIM MESSAGE$[1]
1725 LET MESSAGE$[0]="More Than 999 Entries For Employee "+FNF$(W0$(6,P[0]),M0$)+" (<Enter>=Continue)"
1730 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
1735 GOTO 2000
1740 LET D0$(12,3)=STR(D9:"000")
1745 DIM D1$(12),D[2]
1750 FIND (PRE02_DEV,KEY=D0$(1,14),DOM=1900)IOL=PRE02A
1760 LET D9=D9+1
1765 GOTO 1715
1900 REM " --- Build It & Check It "
1910 LET D0$(15,3)=W9$(1,3),D0$(18,6)=W1$(24,4)+B1$(221,2),D1$=W9$(13,7)
1920 LET D[0]=X[2],D[1]=W[0]+W[4],D[2]=ROUND(D[0]*D[1],2)
1990 WRITE (PRE02_DEV,KEY=D0$(1,14))IOL=PRE02A
2000 REM " --- Work Order Transaction "
2005 PRECISION IVS01_PRECISION
2010 LET W9=1
2015 DIM Y0$(18),Y1$(30),Y[11]
2020 LET Y0$=N0$+"  "+W9$(13,7)+W9$(1,3)+"O"+STR(W9:"000")
2025 LET Y1$(1)=W1$(1,6)+W9$(4,9)
2030 FIND (WOT01_DEV,KEY=Y0$,DOM=2040)
2035 LET W9=W9+1,Y0$(16,3)=STR(W9:"000"); GOTO 2030
2040 LET Y[0]=W[0],Y[1]=W[1]+W[2],Y[2]=W[3],Y[3]=W[1],Y[4]=W[2],Y[5]=W[5],Y[6]=W[4]
2045 PRECISION 2
2050 LET Y[2]=Y[2]*1
2055 PRECISION IVS01_PRECISION
2060 WRITE (WOT01_DEV,KEY=Y0$)IOL=WOT01A
2100 REM " Transaction Cross Reference "
2110 DIM CROSSREF$(18)
2120 LET CROSSREF$(1,18)=Y0$(1,18)
2130 WRITE (WOM07_DEV,KEY=CROSSREF$)IOL=WOM07A
2500 REM " --- Update Header "
2505 DIM H[11]
2510 EXTRACT (WOE01_DEV,KEY=N0$+"  "+W9$(13,7))IOL=WOE01A
2520 IF POS(" "<>H0$(25,3))=0 THEN LET H0$(25,3)=W9$(1,3)
2530 IF H0$(28,3)<W9$(1,3) THEN LET H0$(28,3)=W9$(1,3)
2540 WRITE (WOE01_DEV,KEY=H0$(1,11))IOL=WOE01A
2600 REM " --- Find WO Type "
2610 IF GL$<>"Y" THEN GOTO 2900
2620 IF T0$=H0$(12,2) THEN GOTO 2700
2630 DIM T1$(G[4]*7,"0")
2640 FIND (WOM10_DEV,KEY=N0$+"A"+H0$(12,2))IOL=WOM10A
2650 LET T0$=T0$(4,2)
2700 REM " --- Retrieve employee name"
2710 IF P8$="YES" THEN GOTO 2740
2720 DIM B1$(30)
2730 FIND (WOM01_DEV,KEY=N0$+W9$(4,9),DOM=2740)IOL=WOM01A
2735 GOTO 2750
2740 FIND (PRM01_DEV,KEY=N0$+W9$(4,9),DOM=2750)IOL=PRM01A
2750 LET EMP$=FNP$(B1$(1,16))+", "+FNP$(B1$(17,14))
2760 GOSUB 6000
2800 REM " --- Generate G/L Postings"
2805 PRECISION 2
2810 LET ACCOUNT$=T1$(1,G[4]),MEMO$=EMP$,REF1$=FNF$(W9$(4,P[0]),M0$)
2820 LET TOTAL=TRANS_TOTAL,REF2$="WO "+W9$(13,7)
2830 LET REF3$="Step "+W1$(1,3),AMOUNT=TOTAL,UNITS=0,WHEN$=W9$(1,3)
2840 GOSUB GLPOST
2850 LET ACCOUNT$=T1$(2*G[4]+1,G[4]),AMOUNT=-DIRECT_TOTAL,UNITS=W[0]
2860 GOSUB GLPOST
2870 LET ACCOUNT$=T1$(3*G[4]+1,G[4]),AMOUNT=-(TOTAL+AMOUNT),UNITS=W[0]
2880 GOSUB GLPOST
2890 PRECISION IVS01_PRECISION
2900 REM " --- Remove Timesheet Record "
2920 REMOVE (TIMESHEET_DEV,KEY=K0$)
2990 GOTO 1100
4000 REM " --- Finished "
4010 IF GL$="Y" THEN CALL "GLC.CA",STATUS
4020 CALL "SYC.BB",STATUS
4100 GOTO 9900
4900 REM " --- Payroll Period Must Be Defined"
4910 DIM MESSAGE$[1]
4920 LET MESSAGE$[0]="PR Period Must Be Defined For Time Sheet Update (<Enter>=Continue)"
4930 CALL "SYC.XA",1,MESSAGE$[ALL],0,22,-1,V$,V3
4990 GOTO 9900
6000 REM " --- Calculate Transaction Direct & Overhead Totals "
6020 LET DIRECT_HRS=ROUND(W[1]*W[0],2)
6030 LET DIRECT_SET=ROUND(W[1]*W[4],2)
6040 LET DIRECT_TOTAL=DIRECT_HRS+DIRECT_SET
6050 LET OH_HRS=ROUND(W[2]*W[0],2)
6060 LET OH_SET=ROUND(W[2]*W[4],2)
6070 LET OH_TOTAL=OH_HRS+OH_SET
6080 LET TRANS_TOTAL=DIRECT_TOTAL+OH_TOTAL
6090 RETURN 
6900 REM " --- Standard G/L Posting Routine"
6910 GLPOST: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,WHEN$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

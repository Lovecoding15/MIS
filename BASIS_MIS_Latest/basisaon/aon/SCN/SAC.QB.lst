0010 REM "SAC - Sales Analysis SpeedSearch (Item Sales History)"
0020 REM "Program SAC.QB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "20Aug96 WGH: Re-initialize Totals for each item (1235)"
0032 REM "26Aug96 WGH: Open IVM-14 for item synonym lookup (110,130,170)"
0033 REM "12Feb97 WGH: Year incorrectly labeled when non-calendar fiscal year (555)"
0034 REM "08Dec99 KJS: Display 21st century dates correctly (790)
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open/Lock Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="IVM-01",FILES$[2]="IVM-10"
0130 LET FILES$[3]="IVM-15",FILES$[4]="SAM-07",FILES$[5]="IVM-14"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET IVM01_DEV=CHANNELS[1],IVM10_DEV=CHANNELS[2]
0170 LET IVM15_DEV=CHANNELS[3],SAM07_DEV=CHANNELS[4],IVM14_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1)
0220 SAM07A: IOLIST B0$(1),U[ALL],C[ALL],S[ALL]
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SAS01A: IOLIST X$,U1$,U2$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$(1)
0290 SYS01T: IOLIST X$,F0$
0330 IVM10A: IOLIST X0$(1)
0350 IVM15A: IOLIST Y0$(1),Y1$(1)
0400 REM " --- Parameters"
0405 DIM I[5],G[1],X$(128),G3$(325)
0410 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2),MAXPAGE=NUM(X$(66,2))
0415 LET TNAME$=FNP$(X$(3,8)),ONAME$=FNP$(X$(27,8)),SNAME$=FNP$(X$(19,8))
0420 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0425 LET SHEIGHT=NUM(X$(45,2)),MAXOPT=NUM(X$(57,2))
0430 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0435 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0440 LET PAGES=INT((DSZ-5000)/4000),SYS01_DEV=NUM(STBL("!SYS01"))
0445 IF PAGES<3 THEN GOTO 9900
0450 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0455 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0460 FIND (SYS01_DEV,KEY=N0$+"SA00",DOM=9900)IOL=SAS01A
0465 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9900)IOL=IVS01A
0470 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9900)IOL=GLS01A
0475 IF U1$(7,1)<>"Y" THEN GOTO 9900
0480 IF U2$(7,1)<>"I" THEN GOTO 9900
0485 LET I[0]=NUM(P2$(1,2)),I[3]=NUM(P2$(9,2)),I[4]=NUM(P2$(11,2))
0490 LET I[5]=NUM(P2$(13,2)),G[0]=NUM(G2$(1,2)),WHEN$=F0$(18,6)
0495 CALL "SYC.PB",N0$,SYS01_DEV,WHEN$,PERIOD$,YEAR$
0500 REM " --- Initializations"
0510 DIM A0$(22),A1$(60),A2$(64),OPTLIST$[2,MAXOPT,6]
0520 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),X0$(32),Y0$(5),Y1$(32)
0530 DIM FILES[14],ITEM$[6],ITEM[12],PARAMS[3],O[5],PERIODS$[12]
0540 LET D_STATUS=0,MAXROW=HEIGHT-5,A0$(1)=N0$+OPTION$[3],DONE=0
0545 LET FILES[1]=IVM01_DEV,FILES[14]=IVM14_DEV,PGM$=PGM(-2),OPTION=0
0550 LET PARAMS[0]=I[0],PARAMS[1]=I[3],PARAMS[2]=I[4],PARAMS[3]=I[5]
0555 LET PERIOD=NUM(PERIOD$),YEAR=FNYY_YEAR(YEAR$),NF$="(Not On File)"
0560 LET CURYEAR$=YEAR$,PRIYEAR$=FNYEAR_YY21$(YEAR-1)
0575 FOR X=12 TO 1 STEP -1
0580 LET PERIODS$[X]=G3$((PERIOD-1)*3+235,3)+" "+FNYEAR_YY$(YEAR),PERIOD=PERIOD-1
0585 IF PERIOD<1 THEN LET PERIOD=G[0],YEAR=YEAR-1
0590 NEXT X
0600 REM " --- Resize masks"
0610 CALL "SYC.RM",M1$,M1,7,11
0620 CALL "SYC.RM",M2$,M2,6,10
0630 LET O[0]=1,O[1]=7,O[2]=O[1]+M2,O[3]=29,O[4]=35,O[5]=O[4]+M2,W=M1+M2
0650 REM " --- Option Window"
0660 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,""
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0715 LET HEADING$(O[0]+1)="Period",HEADING$(O[1]+M2-5)="Units"
0720 LET HEADING$(O[2]+M1-5)="Sales",HEADING$(O[3]+1)="Period"
0725 LET HEADING$(O[4]+M2-5)="Units",HEADING$(O[5]+M1-5)="Sales"
0730 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-10)="F4=End"
0735 PRINT @(0,0),'CS','SB','BR',HEADING$,'ER','SF',@(0,HEIGHT-3),'SB','BR',FOOTING$,'ER','SF',
0740 FOR X=1 TO 6
0745 PRINT 'SB',@(O[0],X+1),PERIODS$[X],@(O[3]),PERIODS$[X+6],'SF',
0750 NEXT X
0755 CALL "SYC.WC",1,0,M1+M2+6,1,O[3],8,0
0760 PRINT @(O[3],9),'SB',"Total",'SF',
0780 CALL "SYC.WB",SNAME$,0
0790 PRINT 'SB',@(3,0),"UM",@(O[4]+M2-6),"Units",@(O[5]+M1-6),"Sales",@(1,1),"Prod",@(O[4]-9),"Fiscal ",FNYY21_YY$(CURYEAR$),@(1,2),"Type",@(O[4]-9),"Fiscal ",FNYY21_YY$(PRIYEAR$),'SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",TNAME$,0
0920 PRINT @(0,0),'CL',@(0,1),'CL',@(0,2),'CL',
0930 CALL "SYC.WB",SNAME$,0
0940 PRINT 'CF',
0980 READ (IVM01_DEV,KEY=A0$,DOM=1000)IOL=IVM01A
0990 GOTO 1100
1000 REM " --- Process key field"
1010 GOSUB 6000
1015 CALL "SYC.WB",TNAME$,0
1020 PRINT @(0,0),'CS',@(0,2),'SB'," Item:",'SF',
1025 LET V2$="",V2$=KEY(IVM01_DEV,END=1040)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1035 IF V2$<>"" THEN LET V2$=V2$(3,I[0])
1040 LET ACTION$="V",V1=7,V2=2,V4$=""
1045 CALL "IVC.IA",ACTION$,V1,V2,FILES[ALL],N0$,V2$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1050 ON V3 GOTO 1080,1000,1000,1000,1060,1070,1060
1060 LET OPTION$[1]="EXIT"
1065 GOTO 4000
1070 LET OPTION$[1]="STARTOVER"
1075 GOTO 4000
1080 LET V$=FNP$(ITEM$[0])
1090 IF V$="" THEN GOTO 0900
1095 LET A0$(3)=V$
1200 REM " --- Process top window display"
1210 DIM LLIST$[PAGES,MAXROW,5],X0$(32),DESCRIPTION$(WIDTH-10-I[0])
1220 DIM MORE$[PAGES,MAXROW,SHEIGHT-2],UNITS[12],SALES[12]
1230 LET LINE=1,L=1,X0=0,X1=1,PAGE=1
1235 LET CURUNITS=0,CURSALES=0,PRIUNITS=0,PRISALES=0,TOTUNITS=0,TOTSALES=0
1240 READ (IVM01_DEV,KEY=A0$,DOM=1000)IOL=IVM01A
1250 LET DESCRIPTION$(1)=FNITEM$(A1$,I[3],I[4],I[5])
1260 LET X$='SB'+" Item: "+'SF'+A0$(3,I[0])+" "+DESCRIPTION$
1265 LET X1$=A0$(3,I[0]),X2$=PARAM$
1270 CALL "SYC.QB",X$,X1$,X2$
1280 PRINT @(0,2),X$,
2000 REM " --- Retrieve code descriptions"
2010 LET X0$(1)=N0$+"A"+A2$(1,3)+NF$,Y0$(1)=N0$+A2$(53,3),Y1$(1)=NF$
2020 FIND (IVM10_DEV,KEY=X0$(1,6),DOM=2030)IOL=IVM10A
2030 FIND (IVM15_DEV,KEY=Y0$,DOM=2040)IOL=IVM15A
2100 REM " --- Retrieve record for current fiscal year"
2110 DIM B0$(27),U[12],C[12],S[12]
2120 LET B0$(1)=N0$+CURYEAR$+A2$(1,3)+A0$(3),PERIOD=NUM(PERIOD$),P=12
2130 FIND (SAM07_DEV,KEY=B0$,DOM=2140)IOL=SAM07A
2140 FOR X=0 TO G[0]-1
2150 LET CURUNITS=CURUNITS+U[X],CURSALES=CURSALES+S[X]
2160 NEXT X
2200 REM " --- Load rolling 12 month arrays"
2210 LET UNITS[P]=U[PERIOD-1],SALES[P]=S[PERIOD-1]
2220 LET PERIOD=PERIOD-1,P=P-1
2230 IF PERIOD>0 THEN GOTO 2200
2300 REM " --- Retrieve record for prior fiscal year"
2310 DIM B0$(27),U[12],C[12],S[12]
2320 LET B0$(1)=N0$+PRIYEAR$+A2$(1,3)+A0$(3),PERIOD=G[0]
2330 FIND (SAM07_DEV,KEY=B0$,DOM=2340)IOL=SAM07A
2340 FOR X=0 TO G[0]-1
2350 LET PRIUNITS=PRIUNITS+U[X],PRISALES=PRISALES+S[X]
2360 NEXT X
2390 IF P=0 THEN GOTO 2500
2400 REM " --- Load rolling 12 month arrays"
2410 LET UNITS[P]=U[PERIOD-1],SALES[P]=S[PERIOD-1]
2420 LET PERIOD=PERIOD-1,P=P-1
2430 IF P>0 THEN GOTO 2400
2500 REM " --- Display middle window"
2510 CALL "SYC.WB",NAME$,0
2520 FOR X=1 TO 12
2530 LET TOTUNITS=TOTUNITS+UNITS[X],TOTSALES=TOTSALES+SALES[X]
2540 NEXT X
2550 FOR X=1 TO 6
2560 PRINT @(O[1],X+1),UNITS[X]:M2$,@(O[2]),SALES[X]:M1$,@(O[4]),UNITS[X+6]:M2$,@(O[5]),SALES[X+6]:M1$,
2570 NEXT X
2580 PRINT @(O[4],9),TOTUNITS:M2$,@(O[5]),TOTSALES:M1$,
2600 REM " --- Display bottom window"
2610 CALL "SYC.WB",SNAME$,0
2620 PRINT @(6,0),A2$(4,2),@(6,1),X0$(4,3)," ",X0$(7,15),@(O[4]),CURUNITS:M2$,@(O[5]),CURSALES:M1$,@(6,2),Y0$(3,3)," ",Y1$(1,15),@(O[4]),PRIUNITS:M2$,@(O[5]),PRISALES:M1$,
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$=A0$(3),CHOICE2$=""
3030 GOSUB 5000
3040 IF CONTROL=1 THEN GOTO 1000
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=(OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR")
5090 RETURN
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CF',
6090 RETURN
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8060 DEF FNM$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+Q$(1,2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8100 REM " --- FNYEAR_YY$ Un-Convert 21st Century Numeric Year to 2-Char Year"
8105 DEF FNYEAR_YY$(Q)=STR(MOD(Q,100):"00")
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9950 EXIT
9999 END

0010 REM "ARM - Cash Receipts Code Maintenance"
0020 REM "Program ARM.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "28Apr97 JWK: Blank out G/L account if not used (2260)
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="ARM-10",FILES$[3]="GLM-01"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],ARM10_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 ARM10C: IOLIST A0$(1),A1$(1)
0220 GLM01A: IOLIST B0$(1),B1$(1)
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0405 DIM G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0460 FOR X=0 TO 4
0470 LET G[X]=NUM(G2$(X*2+1,2),ERR=0480)
0480 NEXT X
0485 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 CALL "SYC.VA","OP",INFO$[ALL]
0495 LET OP$=INFO$[20]
0500 REM " --- Initializations"
0510 DIM A0$(30),A1$(2*G[4]+1),B0$(2+G[4]),B1$(40),INFO$[20],MESSAGE$[1]
0550 LET CHANNEL=ARM10_DEV,RECORD$="C",TITLE$="Cash Receipt Codes"
0590 LET K0=1,K9=6,G5=LEN(G5$),POS_ROW=7,A0$(1,3)=N0$+RECORD$
0600 REM " --- Additional File Opens"
0610 CALL "SYC.VA",N2$,INFO$[ALL]
0620 LET GL$=INFO$[9]
0630 IF GL$<>"Y" THEN GOTO 0700
0640 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0650 IF STATUS>0 THEN GOTO 9900
0660 LET GLM01_DEV=CHANNELS[3],POS_ROW=10
0700 REM " --- Background"
0710 PRINT 'SB',@(9,3),"Code:",@(2,4),"Description:",@(9,5),"Cash:",@(5,6),"Discount:",'SF',
0720 IF GL$="Y" THEN PRINT 'SB',@(8,7),"AR/GL:",@(1,8),"Cash Account:",@(1,9),"Disc Account:",'SF',
0730 IF OP$="Y" THEN PRINT 'SB',@(0,POS_ROW),"POS Cash Type:",'SF',
0900 REM " --- Position file"
0990 READ (ARM10_DEV,KEY=A0$(1,3),DOM=1000)
1000 REM " --- Cash Receipt Code"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(ARM10_DEV,END=1050)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4)
1050 LET V0$="S",V1$="KEC",V3$="",V0=3,V1=15,V2=3,V4$="Enter A Valid Cash Receipts Code (<F3>=Lookup)"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN GOSUB 6000
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(4)=V$
1200 REM " --- Get record"
1210 READ (ARM10_DEV,KEY=A0$(1,K9),DOM=1400)IOL=ARM10C
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Initialize new record"
1510 LET A0$(7)="",A1$(1)=""
1520 LET A0$(27,1)="Y",A0$(28,1)="Y",A0$(29,1)="A",A0$(30,1)="$"
2000 REM " --- Description"
2020 LET V0$="S",V1$="",V2$=A0$(7,20),V3$="",V0=20,V1=15,V2=4,I0=0
2030 LET V4$="Enter The Description For This Cash Receipts Code"
2040 GOSUB 7000
2050 ON V3 GOTO 2060,2000,3900,2000,3900
2060 LET A0$(7,20)=V$
2090 GOTO 3900
2100 REM " --- Cash code"
2110 LET V4$="Y=Deposit Amount And Adjustments     N=Adjustments Only"
2120 LET V0$="Y",V1$="RC",V2$=A0$(27,1),V3$="YN",V0=1,V1=15,V2=5
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2150 LET A0$(27,1)=V$
2190 GOTO 3900
2200 REM " --- Discounts?"
2210 LET V4$="Y=Discounts May Be Taken     N=No Discounts Permitted"
2220 LET V0$="Y",V1$="RC",V2$=A0$(28,1),V3$="YN",V0=1,V1=15,V2=6
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 LET A0$(28,1)=V$
2260 IF A0$(28,1)="N" THEN LET A1$(G[4]+1,G[3])=""; PRINT @(15,9),'CL',
2290 GOTO 3900
2300 REM " --- AR, GL or Both?"
2310 IF GL$<>"Y" THEN GOTO 3900
2320 LET V4$="A=Post To A/R Only   G=Post To G/L Only   B=Post To Both A/R And G/L"
2330 LET V0$="S",V1$="RC",V2$=A0$(29,1),V3$="AGB",V0=1,V1=15,V2=7
2340 GOSUB 7000
2350 ON V3 GOTO 2360,2300,3900,2300,3900
2360 LET A0$(29,1)=V$
2390 GOTO 3900
3100 REM " --- GL Account(s)"
3110 IF GL$<>"Y" THEN GOTO 3900
3120 LET X0=I0-4
3130 IF X0=1 THEN IF A0$(28,1)<>"Y" THEN GOTO 3900
3140 LET V0$="M",V1$="CR",V2$=A1$(G[4]*X0+1,G[3]),V3$=G1$,V0=G[4],V1=15,V2=X0+8
3150 LET V4$="Enter A Valid G/L Account (<F3>=Lookup)"
3160 GOSUB 7000
3170 ON V3 GOTO 3200,3100,3900,3180,3900
3180 CALL "SYC.LG",V$
3190 IF V$="" THEN GOTO 3100
3200 REM " --- Retrieve account"
3210 FIND (GLM01_DEV,KEY=N0$+V$,DOM=3100)IOL=GLM01A
3220 PRINT @(V1,V2),FNF$(B0$(3,G[3]),G5$)," ",B1$(1,30),
3230 LET A1$(X0*G[4]+1,G[4])=B0$(3)
3290 GOTO 3900
3300 REM " --- Point of Sale (POS) Cash Type"
3310 IF OP$<>"Y" THEN GOTO 3900
3315 LET V4$="Enter The Point Of Sale Cash Type ($=Cash/C=Check/P=Credit Card)"
3320 LET V0$="S",V1$="RC",V2$=A1$(21,1),V3$="$CP",V0=1,V1=15,V2=POS_ROW
3330 GOSUB 7000
3335 ON V3 GOTO 3340,3300,3900,3300,3900
3340 IF POS(V$="$CP")=0 THEN GOTO 3300
3350 LET A1$(21,1)=V$
3390 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,2300,3100,3100,3300,3990
3990 LET I0=K0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="KC",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4110 WRITE (ARM10_DEV,KEY=A0$(1,K9))IOL=ARM10C
4190 GOTO 1000
4200 REM " --- Delete"
4210 REMOVE (ARM10_DEV,KEY=A0$(1,K9),DOM=4220)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4600,9900
4600 REM " --- Report Initializations"
4610 LET PROGRAM$="",TOTAL$=TITLE$,NUMBER=7,HEADINGS=3,WIDTH=80
4620 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4630 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4640 DIM HEADINGS$[HEADINGS]
4650 LET FIELD$[0]="Code",FIELD[0]=1,POSITION[0]=4
4660 LET LENGTH[0]=3,TYPE$[0]="S",MASK$[0]=""
4670 LET FIELD$[1]="Description",FIELD[1]=1,POSITION[1]=7
4680 LET LENGTH[1]=20,TYPE$[1]="S",MASK$[1]=""
4690 LET FIELD$[2]="Cash?",FIELD[2]=1,POSITION[2]=27
4700 LET LENGTH[2]=1,TYPE$[2]="Y",MASK$[2]=""
4710 LET FIELD$[3]="Disc?",FIELD[3]=1,POSITION[3]=28
4720 LET LENGTH[3]=1,TYPE$[3]="Y",MASK$[3]=""
4730 LET FIELD$[4]="POS",FIELD[4]=2,POSITION[4]=G[4]*2+1
4740 LET LENGTH[4]=1,TYPE$[4]="S",MASK$[4]=""
4745 IF GL$<>"Y" THEN LET NUMBER=3; GOTO 4900
4750 LET FIELD$[5]="AR/GL",FIELD[5]=1,POSITION[5]=29
4760 LET LENGTH[5]=1,TYPE$[5]="Y",MASK$[5]=""
4770 LET FIELD$[6]="Cash Acct",FIELD[6]=2,POSITION[6]=1
4780 LET LENGTH[6]=G[3],TYPE$[6]="M",MASK$[6]=G5$
4790 LET FIELD$[7]="Disc Acct",FIELD[7]=2,POSITION[7]=1+G[4]
4800 LET LENGTH[7]=G[3],TYPE$[7]="M",MASK$[7]=G5$
4980 LET HEADINGS$[0]=N1$,HEADINGS$[1]=TOTAL$+" Listing"
4990 RUN "SYR.AA"
5000 REM " --- Display record"
5010 PRINT @(15,3),A0$(4,3),@(15,4),A0$(7,20),@(15,5),A0$(27,1),@(15,6),A0$(28,1),
5020 IF GL$<>"Y" THEN GOTO 5080
5040 PRINT @(15,7),A0$(29,1),@(15,8),FNF$(A1$(1,G[3]),G5$)
5050 IF A0$(28,1)="Y" THEN PRINT @(15,9),FNF$(A1$(G[4]+1,G[3]),G5$)
5080 IF OP$="Y" THEN PRINT @(15,POS_ROW),A1$(21,1)
5090 RETURN 
6000 REM " --- Code Lookup"
6010 LET NUMBER=1
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Code",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3
6050 LET FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
6060 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6070 LET V$="SKIP",ROW=V2+2
6080 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

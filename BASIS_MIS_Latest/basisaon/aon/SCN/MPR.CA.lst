0010 REM "MPR - Production Forecast Worksheet (Input Overlay)"
0020 REM "Program MPR.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "25Sep96 JAL: Product Lookup not performed on <F3> (1435)"
0040 REM 
0080 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=6
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="IVM-01",FILES$[2]="IVM-12",FILES$[3]="IVM-03"
0120 LET FILES$[4]="MPM-01",FILES$[5]="IVM-10",FILES$[6]="SYS-01"
0150 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET IVM01_DEV=CHANNELS[1],IVM12_DEV=CHANNELS[2],IVM03_DEV=CHANNELS[3]
0165 LET MPM01_DEV=CHANNELS[4],IVM10_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0200 REM " --- IOLIST's"
0210 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0220 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,X$,M1$,M2$,M3$
0230 IVM10A: IOLIST X0$(1)
0240 IVM10C: IOLIST X0$(1)
0250 MPM01A: IOLIST S0$(1),S1$(1),S2$,S[ALL]
0260 GLS01A: IOLIST G0$,G1$,G2$
0400 REM " --- Parameters"
0410 DIM P[6]
0420 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N3$=F5$
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0450 LET P[0]=NUM(P2$(1,2)),IVS01_PRECISION=NUM(P2$(5,1)),NUMBER=1,MP=26
0460 LET P[2]=NUM(P2$(9,2)),P[3]=NUM(P2$(11,2)),P[4]=NUM(P2$(13,2))
0480 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0490 LET TOTAL_PD$=G2$(1,2),CURR_PD$=G2$(3,2),CURR_YR$=G2$(5,2)
0500 REM " --- Initializations"
0510 PRECISION IVS01_PRECISION
0520 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
0530 DIM FORECAST$(3),WHSE$(2),PRODUCT$(3)
0540 LET WHSE$(1)=P3$(6,2),BEGDATE$=F0$(18,6)
0550 LET ALLPRODUCTS=1
0560 LET HEADINGS=4
0570 DIM HEADINGS$[HEADINGS]
0580 LET HEADINGS$[O]=N1$,HEADINGS$[1]=N3$
0590 LET L9=59,L=L9+1
0700 REM " --- Background"
0710 FOR X=4 TO 10 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(25,3),"Forecast Type:",@(24,5),"Warehouse Code:",@(31,7),"Product:",@(24,9),"Beginning Date:",'SF',
0900 REM 
0910 READ (MPM01_DEV,KEY=N0$,DOM=0920)
0920 LET K$=KEY(MPM01_DEV,END=0990)
0930 IF POS(N0$=K$)<>1 THEN GOTO 0990
0940 LET FORECAST$(1)=K$(3,3)
0990 GOSUB 5000
1000 REM " --- Forecast Type"
1010 LET V0$="S",V1$="CE",V2$=FORECAST$,V0=3,V1=40,V2=3,I0=0
1020 LET V4$="Enter A Valid Forecast Type (<F3>=Lookup)"
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1045,3900
1045 GOSUB 6000
1050 GOSUB 5200
1060 IF STATUS<>0 THEN GOTO 1000
1070 LET FORECAST$(1)=V$
1090 GOTO 3900
1200 REM " --- Warehouse"
1210 LET V0$="S",V1$="C",V2$=WHSE$,V3$="",V0=2,V1=40,V2=5
1220 LET V4$="Enter A Valid Warehouse Code (<F3>=Lookup)"
1230 GOSUB 7000
1235 ON V3 GOTO 1250,1200,3900,1240,3900
1240 GOSUB 6100
1250 GOSUB 5300
1260 IF STATUS<>0 THEN GOTO 1200
1280 LET WHSE$(1)=V$
1290 GOTO 3900
1400 REM " --- Product Type"
1410 LET V0$="S",V1$="C",V2$=PRODUCT$,V3$="",V0=3,V1=40,V2=7
1420 LET V4$="Enter A Valid Product Type (<F1>=All/<F3>=Lookup)"
1430 GOSUB 7000
1435 IF V$="   " THEN IF V3=0 THEN GOTO 1445
1440 ON V3 GOTO 1460,1445,3900,1455,3900
1445 LET V$="",ALLPRODUCTS=1
1447 GOSUB 5400
1450 GOTO 3900
1455 GOSUB 6200
1457 IF V$="" THEN GOTO 1400
1460 LET ALLPRODUCTS=0
1470 GOSUB 5400
1475 IF STATUS<>0 THEN GOTO 1400
1480 LET PRODUCT$(1)=V$
1490 GOTO 3900
1600 REM " --- Beginning Date"
1610 LET V0$="D",V1$="C6N",V2$=BEGDATE$,V3$="",V0=6,V1=40,V2=9
1620 LET V4$="Enter The Beginning Date To Use For The Worksheet"
1630 GOSUB 7000
1640 ON V3 GOTO 1650,1600,3900,1600,3900
1650 LET BEGDATE$=V$
1690 GOTO 3900
3900 REM " --- Next Field "
3905 IF V3=4 THEN GOSUB 5000; GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,4000
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4060,4000,4000,4000,9900
4060 ON POS(V$="YN") GOTO 4000,4070,1000
4070 GOSUB 5000
4090 RUN "MPR.CB"
5000 REM " --- Display"
5010 LET V1=40,V2=3,V$=FORECAST$
5015 GOSUB 5200
5020 LET H4$=" Forecast: "+X$,V2=5,V$=WHSE$
5030 GOSUB 5300
5040 LET H5$="Warehouse: "+X$,V2=7,V$=PRODUCT$
5050 GOSUB 5400
5060 LET H6$="Product Type: "+X$
5070 PRINT @(40,9),FNB6$(BEGDATE$)
5080 LET H7$="Beginning Date: "+FNB6$(BEGDATE$)
5090 RETURN 
5200 REM " --- Forecast"
5210 DIM S0$(5),S1$(30),S[1]
5220 LET STATUS=1,S0$(1)=N0$+V$
5230 FIND (MPM01_DEV,KEY=S0$,DOM=5250)IOL=MPM01A
5240 LET STATUS=0
5250 LET X$=S0$(3,3)+" "+FNP$(S1$)
5260 PRINT @(V1,V2),X$,'CL',
5290 RETURN 
5300 REM " --- Warehouse"
5310 DIM X0$(32)
5320 LET STATUS=1,X0$(1)=N0$+"C"+V$
5330 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=5350)IOL=IVM10C
5340 LET STATUS=0
5350 LET X$=X0$(4,2)+" "+FNP$(X0$(6,24))
5360 PRINT @(V1,V2),X$,'CL',
5390 RETURN 
5400 REM " --- Buyer Code"
5410 DIM X0$(32)
5415 IF ALLPRODUCTS>0 THEN GOTO 5470
5420 LET STATUS=1,X0$(1)=N0$+"A"+V$
5430 FIND (IVM10_DEV,KEY=X0$(1,6),DOM=5450)IOL=IVM10A
5440 LET STATUS=0
5450 LET X$=X0$(4,3)+" "+FNP$(X0$(7,20))
5460 PRINT @(V1,V2),X$,'CL',
5465 GOTO 5490
5470 LET STATUS=0,X$="All"
5480 PRINT @(V1,V2),"All",'CL',
5490 RETURN 
6000 REM " --- Forecast Lookup"
6010 LET RECORD$="",TITLE$="MRP Forecast Types"
6030 LET DESCRIPTION$[0]="Type",CHANNEL=MPM01_DEV,FIELD[1]=2
6040 LET POSITION[0]=3,LENGTH[0]=3,POSITION[1]=1,LENGTH[1]=30
6090 GOTO 6400
6100 REM " --- Warehouse Lookup"
6110 LET RECORD$="C",TITLE$="Warehouse Lookup"
6130 LET DESCRIPTION$[0]="ID",CHANNEL=IVM10_DEV,FIELD[1]=1
6140 LET POSITION[0]=4,LENGTH[0]=2,POSITION[1]=6,LENGTH[1]=20
6190 GOTO 6400
6200 REM " --- Product Lookup"
6210 LET RECORD$="A",TITLE$="Product Type Lookup"
6230 LET DESCRIPTION$[0]="Type",CHANNEL=IVM10_DEV,FIELD[1]=1
6240 LET POSITION[0]=4,LENGTH[0]=3,POSITION[1]=7,LENGTH[1]=20
6290 GOTO 6400
6400 REM " --- Call Windowed Lookup"
6410 LET FIELD[0]=1,DESCRIPTION$[1]="Description"
6440 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,ROW=V2+2,V$="SKIP"
6450 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNB6$(Q1$)=Q1$(3,2)+"/"+Q1$(5,2)+"/"+FNYY21_YY$(Q1$(1,2))
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

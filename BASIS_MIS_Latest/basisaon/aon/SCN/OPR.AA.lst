0010 REM "OPR - ORDER STATUS REPORT BY ITEM & SHIP DATE"
0020 REM "Program: OPR.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "11Nov96 JWK: Fix short Prod Code entry and selection by Prod Code (1065,5430)
0032 REM "11Nov96 JWK: Look for more lines after non-inventory line (5420)
0033 REM "04Mar97 WGH: Use correct ARE-13 variable to write Committed flag to ARW-03 (5550)"
0034 REM "20Apr98 WGH: Eliminate hard coded 12/31/99 period ending date (6150)"
0035 REM "29May99 WGH: Can't enter single character warehouse code (965)"
0040 REM 
0050 BEGIN 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files "
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARE-03",FILES$[2]="ARE-13",FILES$[3]="ARM-01"
0130 LET FILES$[4]="IVM-02",FILES$[5]="IVM-01",FILES$[6]="ARW-03"
0135 LET FILES$[7]="SYS-01",FILES$[8]="IVM-10",FILES$[9]="ARM-10"
0140 CALL "SYC.DA",1,1,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET ARE03_DEV=CHANNELS[1],ARE13_DEV=CHANNELS[2],ARM01_DEV=CHANNELS[3]
0165 LET IVM02_DEV=CHANNELS[4],IVM01_DEV=CHANNELS[5],ARW03_DEV=CHANNELS[6]
0170 LET SYS01_DEV=CHANNELS[7],IVM10_DEV=CHANNELS[8],ARM10_DEV=CHANNELS[9]
0200 REM " --- IOLISTS"
0210 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0220 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,P5$,P6$,P7$,P8$
0230 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,X$,M1$,M2$,M3$,M4$
0240 IVM10C: IOLIST W0$(1)
0250 IVM10A: IOLIST C0$(1)
0260 ARE03A: IOLIST A0$(1),A[ALL]
0270 ARE13A: IOLIST E0$,E1$(1),E[ALL]
0280 ARM10E: IOLIST Y0$
0290 ARW03A: IOLIST X$,X1$,E[2],E[6]
0400 REM " --- Parameters "
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0450 DIM P[2],I[4]
0460 FOR X=0 TO 3
0470 LET I[X]=NUM(I2$(X*2+1,2))
0480 NEXT X
0490 LET I[0]=NUM(I2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1)),P[0]=NUM(P2$(1,2))
0500 REM " --- Initialize Data "
0510 LET H0=0,NUMBER=1
0515 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
0520 LET L9=30,L=0
0530 LET OPTN$="OQBC",P7$="OB"
0540 DIM A[5],W[7]
0550 LET M0=LEN(P5$),M1=LEN(M1$),M2=LEN(M2$)
0570 IF M2<9 THEN CALL "SYC.RM",M2$,M2,9,9
0580 DIM J$(M1+M2*7,"-"),J0$(10,"0"),J1$(50," ")
0700 REM " --- Background "
0720 FOR X=4 TO 16 STEP 2
0725 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(24,3),"Warehouse Code:",@(26,5),"Product Type:",@(32,7),"Option:",@(20,9),"Summary or Detail?:",@(28,11),"Aging Date:",@(34,13),"Days:",
0790 PRINT 'SF'
0800 REM " --- Options "
0900 REM " --- Selected Warehouse
0920 LET V0$="S",V1$="EC",V2$=WH$,V0=2,V1=40,V2=3,I0=0,V4$="Enter Selected Warehouse, <F1>=All, <F3>=Lookup"
0930 GOSUB 7000
0935 IF V3<>3 THEN GOTO 0950
0940 LET DESCRIPTION$[0]="ID",DESCRIPTION$[1]="Warehouse",TITLE$="Warehouse Lookup"
0946 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
0947 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=24
0948 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,RECORD$="C",ROW=3
0949 GOSUB 6200
0950 IF V3=4 THEN GOTO 9900
0955 IF V3=1 OR V$="" THEN LET WH$=""; PRINT @(V1,V2),"All",'CL'; GOTO 0990
0960 DIM W0$(165)
0965 LET V$=V$+FILL(2),V$=V$(1,2)
0970 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=0900)IOL=IVM10C
0980 PRINT @(V1,V2),W0$(4,2),"  ",W0$(6,24)
0985 LET WH$=W0$(4,2)
0990 GOTO 3900
1000 REM " --- Product "
1020 LET V0$="S",V1$="EC",V2$="",V0=3,V1=40,V2=5,V4$="Select A Valid Product Type, <F1>=All Product Types, <F3>=Lookup"
1040 GOSUB 7000
1041 IF V3=2 THEN GOTO 1090 ELSE IF V3=4 THEN GOTO 9900
1042 IF V3<>3 THEN GOTO 1050
1043 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITOIN[NUMBER],LENGTH[NUMBER]
1044 LET DESCRIPTION$[0]="ID",DESCRIPTION$[1]="Description",TITLE$="Product Type Lookup"
1045 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=3
1046 LET FIELD[1]=1,POSITION[1]=7,LENGTH[1]=20
1047 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,RECORD$="A",ROW=5
1049 GOSUB 6200
1050 IF V3=4 THEN GOTO 9900
1052 IF V3=1 OR V$="" THEN LET P8$="",V3=0; PRINT @(V1,V2),'CL',"All"; GOTO 1090
1060 DIM C0$(35)
1065 LET V$=V$+FILL(3),V$=V$(1,3)
1070 FIND (IVM10_DEV,KEY=N0$+"A"+V$,DOM=1000)IOL=IVM10A
1080 PRINT @(V1,V2),C0$(4,3),"  ",C0$(7,20),
1085 LET P8$=C0$(4,3)
1090 GOTO 3900
1200 REM " --- Option "
1210 LET V0$="S",V1$="^C",V2$=P7$,V3$="",V4$="O=Open Orders, Q=Quotes, B=Backorders, C=Credit Held Orders, <F1>=All Orders",V0=4,V1=40,V2=7,X$="",H3$=""
1220 PRINT @(V1,V2),'CL',
1230 GOSUB 7000
1235 IF V3=2 OR V3=4 THEN LET V$=P7$
1240 IF V$="" OR V3=1 THEN LET V$=OPTN$; GOTO 1270
1242 FOR X=1 TO LEN(OPTN$)
1244 IF POS(OPTN$(X,1)=V$)>0 THEN LET X$=X$+OPTN$(X,1)
1246 NEXT X
1248 IF LEN(X$)>0 THEN LET V$=X$ ELSE GOTO 1200
1250 FOR X=1 TO LEN(V$)
1255 IF X=LEN(V$) AND X<>1 THEN LET H3$=H3$+" and " ELSE IF H3$<>"" THEN LET H3$=H3$+", "
1260 IF V$(X,1)="O" THEN LET H3$=H3$+"Open" ELSE IF V$(X,1)="Q" THEN LET H3$=H3$+"Quoted" ELSE IF V$(X,1)="B" THEN LET H3$=H3$+"Backordered" ELSE IF V$(X,1)="C" THEN LET H3$=H3$+"Credit Held" ELSE EXITTO 1200
1265 NEXT X
1270 LET P7$=V$
1275 IF LEN(P7$)<4 THEN LET H3$=H3$+" Orders Only" ELSE LET H3$="All Orders"
1280 IF LEN(H3$)>80-V1-LEN(P7$)-1 THEN LET X$=H3$(1,80-V1-LEN(P7$)-1) ELSE LET X$=H3$
1285 PRINT @(V1,V2),'CL',P7$," ",X$,
1290 GOTO 3900
1300 REM " --- Summary Or Detail ? "
1305 IF P9$="" THEN LET P9$="D"
1310 LET V0$="S",V1$="C",V2$=P9$,V3$="SD",V4$="S=Summary Of Items D=Item Detail By Order",V0=1,V2=40,V2=9
1320 GOSUB 7000
1330 LET P9$=V$
1340 IF P9$="D" THEN PRINT @(V1,V2),'CL',"Detail" ELSE PRINT @(V1,V2),'CL',"Summary",
1350 GOTO 3900
1400 REM " --- Aging Date "
1420 LET V0$="D",V1$="CER",V2$=F0$(7,3),V0=3,V1=40,V2=11,V4$="Enter Date to Schedule Shipments From"
1430 GOSUB 7000
1440 IF V3=4 THEN GOTO 9900
1450 LET P6$=V$
1460 GOTO 3900
1500 REM " --- Number of Days "
1510 IF P8=0 THEN LET P8=15
1520 LET V0$="N",V1$="CR",V2$=STR(P8),V3$="###",V1=40,V2=13,V4$="Enter # of Aging Days per Column"
1530 GOSUB 7000
1540 IF V3=4 THEN GOTO 9900
1555 LET P8=NUM(V$)
1590 GOTO 3900
3900 REM " --- New Field "
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 0900,1000,1200,1300,1400,1500,4000
4000 REM " - Correct ? "
4020 LET V4$="Is The Above Information Correct",V0=1,V1=63,V2=22
4025 CALL "SYC.YN",1,V4$,2,V$,V3
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,0900
5000 REM " --- Create Data ? "
5005 GOTO 5050; REM "Delete this line if user wants option to NOT rebuild ARW-03
5010 CALL "SYC.YN",1,"Resort Work File? <Y/N=Use File From Last Report>",2,V$,V3
5020 IF V$="N" THEN GOTO 5900
5050 CALL "SYC.NB","Sorting:",(LEN(P5$)+1)+7,COLUMN
5100 CALL "SYC.FA",ARW03_DEV
5190 READ (ARE03_DEV,KEY=N0$+"  ",DOM=5200)
5200 REM " --- Read Order Header Information"
5205 DIM A0$(117),E0$(52),E1$(64),E[15]
5210 LET K$=KEY(ARE03_DEV,END=5900)
5212 IF K$(1,2)<>N0$ THEN GOTO 5900
5230 READ (ARE03_DEV,KEY=K$)IOL=ARE03A
5240 PRINT @(COLUMN,11),FNF$(A0$(5,P[0]),P5$),"  ",A0$(11,7)
5315 IF A0$(21,1)="V" THEN GOTO 5200
5316 IF A0$(22,1)="I" THEN GOTO 5200
5340 LET ORDDATE$=A0$(77,3),SHIPDATE$=A0$(27,3)
5345 LET STAT$=A0$(22,1)
5350 IF A0$(101,1)="C" THEN LET STAT$="C"
5352 IF A0$(101,1)="R" THEN LET STAT$="R"
5354 IF A0$(23,1)="B" THEN LET STAT$="B"
5360 IF A0$(21,1)="P" THEN LET STAT$="Q"
5400 DIM Y0$(30)
5401 REM " --- Read Order Detail "
5402 READ (ARE13_DEV,KEY=A0$(1,20),DOM=5403)
5403 LET K$=KEY(ARE13_DEV,END=5200)
5404 IF K$(1,17)<>A0$(1,17) THEN GOTO 5200
5405 FIND (ARE13_DEV,KEY=K$)IOL=ARE13A
5410 FIND (ARM10_DEV,KEY=N0$+"E"+E0$(21,1),DOM=5420)IOL=ARM10E
5420 IF POS(Y0$(25,1)="SNP")=0 THEN GOTO 5403
5430 IF P8$<>"" AND E0$(23,3)<>P8$ THEN GOTO 5403
5440 IF E1$(41,3)<>"   " THEN LET SHIPDATE$=E1$(41,3)
5500 REM " --- Write to ARW-03 "
5510 DIM X$(54),X1$(28),X9$(40)
5530 IF POS(Y0$(25,1)="SP")<>0 THEN LET X9$(1)=E0$(33,I[0]) ELSE LET X9$(1)=E1$
5535 LET X$(1)=N0$+X9$(1)+E0$(31,2)+E0$(11,10)
5550 LET X1$(1)=E0$(3,8)+E0$(23,3)+SHIPDATE$+Y0$(25,1)+STAT$+ORDDATE$+E1$(44,1)+Y0$(27,1)
5560 WRITE (ARW03_DEV,KEY=X$)IOL=ARW03A
5590 GOTO 5403
5900 GOSUB 6000
5910 CALL "SYC.NB","Printing:",20,COLUMN
5990 RUN "OPR.AB"
6000 REM " --- Days Heading "
6010 LET H4$="",H5$=""
6012 LET V0$=P6$,V1$="",V0=-1
6014 CALL "SYC.CA",V0$,V1$,V0
6020 FOR X=4 TO 1 STEP -1
6040 LET V$=V1$,V=P8*X,V0$=""
6060 CALL "SYC.CA",V$,V0$,V
6070 IF V$="N" OR V0$="N" THEN GOTO 6140
6080 LET H5$=V0$+H5$,V=P8*X+1,V0$=""
6100 CALL "SYC.CA",V$,V0$,V
6110 IF V$="N" OR V0$="N" THEN GOTO 6140
6120 LET H4$=V0$+H4$
6140 NEXT X
6150 LET H4$=P6$+H4$
6190 RETURN 
6200 REM " --- Code Lookup "
6210 CALL "SYC.LK",IVM10_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6220 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

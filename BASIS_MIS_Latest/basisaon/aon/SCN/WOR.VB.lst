0010 REM "WOS - Work Order Job Status Summery - Printing Overlay "
0020 REM "Program:  WOR.VB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "19Mar97 JJD: Check WO close date vs current period to determine source file for WO transaction records. (1610,1612)"
0032 REM "24Aug98 WGH: Prevent possible Error 40 on Line 2644 (1610-1612)
0040 REM 
0090 SETERR 9000
0095 SETESC 9000
0100 REM " --- Open/Lock Files"
0200 REM " --- IOLISTs"
0210 WOE01A: IOLIST A0$(1),A1$(1),A[ALL]
0220 IVM01A: IOLIST D0$,D1$(1)
0230 WOTRAN: IOLIST R0$,R1$,R[ALL]
0250 WOM10A: IOLIST H5$
0260 ARM01A: IOLIST *,H5$(1)
0270 WOE02A: IOLIST W0$(1),W1$(1),W[ALL]
0280 WOE22A: IOLIST W0$(1),W1$(1),W[ALL]
0500 REM " --- Init Data"
0510 DIM U[1],T[1],W[11],W0$(15),W1$(151),W[13]
0520 LET M6$="###0.00-",M9$="##,##0.00-"
0540 LET HEADINGS$[2]=H3$
0550 LET HEADINGS$[3]=H4$
0560 LET HEADINGS$[4]=H6$
0570 LET HEADINGS$[5]="In "+HOW$
0600 REM " --- Additional File Opens "
0610 CALL "SYC.GA",7,1,"","",STATUS
0620 IF STATUS THEN GOTO 9900
0700 REM " --- Background"
0720 CALL "SYC.NB","Printing:",8,COLUMN
0800 REM " --- Options"
1000 REM " --- Init Read"
1010 LET K$=N0$+X9$+P8$
1020 READ (FILEX9_DEV,KEY=N0$+X9$+P8$,DOM=1100)
1030 GOTO 1250
1100 REM " --- Main Read"
1110 LET K$=KEY(FILEX9_DEV,END=4000)
1120 IF POS(N0$=K$)<>1 THEN GOTO 4000
1130 IF K$(3,LEN(X9$))<>X9$ THEN GOTO 4000
1140 IF P7$<>"" THEN IF K$(1,LEN(N0$+X9$+P7$))>N0$+X9$+P7$ THEN GOTO 4000
1150 IF P9$="W" THEN GOTO 1250
1200 REM " --- Breaks"
1210 IF K$(4,X8)<>T0$ THEN GOSUB 6000
1220 LET K$=K$(1,2)+K$(X8+4)
1230 IF P9$="C" THEN LET K$=K$(1,2)+K$(13,9)
1240 READ (FILEX9_DEV)
1250 PRINT @(COLUMN,11),K$(5),
1260 READ (WOE01_DEV,KEY=K$(1,11),DOM=1110)IOL=WOE01A
1265 LET OQTY=A[0],CQTY=A[2]
1270 IF P4$<>"" AND A0$(16,3)>P4$ THEN GOTO 1100
1280 IF POS("P"=P6$)>0 AND A0$(15,1)="P" THEN GOTO 1400
1290 IF POS("Q"=P6$)>0 AND A0$(15,1)="Q" THEN GOTO 1400
1300 IF POS("O"=P6$)>0 AND A0$(15,1)="O" THEN GOTO 1400
1310 IF POS("P"=P6$)=0 AND A0$(15,1)="P" THEN GOTO 1100
1320 IF POS("Q"=P6$)=0 AND A0$(15,1)="Q" THEN GOTO 1100
1330 IF POS("O"=P6$)=0 AND A0$(15,1)="O" THEN GOTO 1100
1340 IF POS("C"=P6$)>0 AND A0$(15,1)<>"C" THEN GOTO 1100
1350 IF POS("C"=P6$)=0 AND A0$(15,1)="C" AND P4$="" THEN GOTO 1100
1360 IF POS("O"=P6$)>0 AND POS("C"=P6$)=0 AND A0$(15,1)="C" AND A0$(31,3)<=P4$ THEN GOTO 1100
1370 IF POS("P"=P6$)>0 AND POS("C"=P6$)=0 AND A0$(15,1)="C" AND A0$(31,3)<=P4$ THEN GOTO 1100
1380 IF POS("Q"=P6$)>0 AND POS("C"=P6$)=0 AND A0$(15,1)="C" AND A0$(31,3)<=P4$ THEN GOTO 1100
1390 IF POS("C"=P6$)>0 AND LEN(P4$)>0 AND A0$(31,3)>P4$ THEN GOTO 1100
1400 IF A0$(14,1)<>"I" THEN LET D1$=A1$(1,30)+" "+A1$(31,30); GOTO 1450
1410 DIM D1$(60)
1420 LET D1$(1)="**Invalid Item**"
1430 FIND (IVM01_DEV,KEY=N0$+A0$(56,20),DOM=1440)IOL=IVM01A
1440 LET D1$(1)=FNITEM$(D1$,DESC[1],DESC[2],DESC[3])
1450 REM " --- Print Work Order Header Information "
1452 IF L+1>L9 THEN GOSUB 5200
1454 PRINT (7)A0$(5,7),@(9),A0$(15,1),@(13),A0$(12,2),@(18),D1$(1,DESCLEN)
1456 PRINT (7)@(18),"Sched.Qty:",A[0]:M6$+" Closed Qty:",A[2]:M6$
1458 LET L=L+2
1460 GOSUB 1600
1470 GOTO 1100
1600 REM " --- Process Work Order "
1610 IF A0$(15,1)="O" OR (A0$(15,1)="C" AND A0$(31,3)>G9$) THEN LET WO_STATUS$="1" ELSE LET WO_STATUS$="0"
1620 LET X0$="",TOTSLAB=0,TOTALAB=0,T2=0,T3=0,T4=0,TOTAMAT=0,T6=0,T7=0,S0=0,S1=0,S2=0,S3=0,Z0=0,Z1=0,X1$="",X2$="",LAST_DATE$=""
1630 DIM STDLAB[I9],STDRUN[I9],ACTLAB[I9],ACTRUN[I9],STDDIR[I9],STDOVH[I9],ACTDIR[I9],ACTOVH[I9],STDSET[I9],ACTSET[I9],QTYCOM[I9]
1700 REM " --- Calc Standards - Operations
1710 READ (WOE02_DEV,KEY=A0$(1,11)+"A",DOM=1720)
1720 LET K$=KEY(WOE02_DEV,END=1900)
1730 IF POS(K$(1,11)=A0$(1,11))<>1 THEN GOTO 1900
1740 READ (WOE02_DEV,KEY=K$)IOL=WOE02A
1750 IF W1$(91,1)<>"S" THEN GOTO 1720
1760 LET Y=POS(W0$(13,3)=X0$,3)
1770 IF Y=0 THEN LET X0$=X0$+W0$(13,3),X1$=X1$+W1$(7,20),X2$=X2$+W1$(1,3),X3$=X3$+W1$(4,3); GOTO 1760
1780 LET X=INT(Y/3)
1790 LET STDLAB[X]=STDLAB[X]+W[3],STDRUN[X]=STDRUN[X]+W[2]-W[8]
1800 LET STDDIR[X]=STDDIR[X]+W[2]*W[6],STDOVH[X]=STDOVH[X]+W[3]-W[2]*W[6]
1810 LET STDSET[X]=STDSET[X]+W[8]
1820 GOTO 1720
1900 REM " --- Calc Standards - Materials "
1905 LET TOTSAMT=0
1910 READ (WOE22_DEV,KEY=A0$(1,11),DOM=1920)
1920 LET K$=KEY(WOE22_DEV,END=2000)
1930 IF POS(K$(1,11)=A0$(1,11))<>1 THEN GOTO 2000
1940 READ (WOE22_DEV,KEY=K$)IOL=WOE22A
1950 IF W1$(91,1)<>"S" THEN GOTO 1920
1980 LET TOTSMAT=TOTSMAT+W[3]
1990 GOTO 1920
2000 REM " --- Get Actuals "        
2010 DIM LAST_DATE$(LEN(X0$))
2020 GOSUB 3200
2100 REM " --- Print "
2110 IF X0$="" THEN GOTO 2800
2120 FOR Y=1 TO LEN(X0$)/3
2130 IF L+1>L9 THEN GOSUB 5000
2140 PRINT (7)@(29),X2$(3*Y-2,3)," ",X1$(20*Y-19,20),
2150 LET X=INT((3*Y-2)/3)
2160 IF HOW$="Units" THEN GOSUB 2300
2170 IF HOW$="Dollars" THEN GOSUB 2400
2180 LET TOTSLAB=TOTSLAB+STDLAB[X],TOTALAB=TOTALAB+ACTLAB[X],T2=T2+STDRUN[X],T3=T3+ACTRUN[X]
2210 LET L=L+1
2220 NEXT Y
2230 IF HOW$="Units" THEN GOTO 2700
2240 IF HOW$="Dollars" THEN GOTO 2500
2300 REM " --- Print Unit Information "
2310 PRINT (7)@(54),FNB$(X3$(X*3+1,3)),@(64),FNB$(LAST_DATE$(X*3+1,3)),@(74),STDSET[X]:M6$,@(81),STDRUN[X]:M6$,@(88),ACTSET[X]:M6$,@(95),ACTRUN[X]:M6$,
2320 LET TOTSTD=STDSET[X]+STDRUN[X],TOTACT=ACTSET[X]+ACTRUN[X]
2330 IF TOTSTD<>0 THEN LET PCTVAR=(TOTSTD-TOTACT)*100/TOTSTD ELSE LET PCTVAR=0
2340 PRINT (7)@(103),PCTVAR:M6$,@(111),QTYCOM[X]:M6$
2350 LET TOTSSET=TOTSSET+STDSET[X],TOTSRUN=TOTSRUN+STDRUN[X]
2360 LET TOTASET=TOTASET+ACTSET[X],TOTARUN=TOTARUN+ACTRUN[X]
2370 RETURN 
2400 REM " --- Print Dollar Information "
2410 PRINT (7)@(54),FNB$(X3$(X*3+1,3)),@(64),FNB$(LAST_DATE$(X*3+1,3)),@(74),STDDIR[X]:M6$,@(82),STDOVH[X]:M6$,@(90),ACTDIR[X]:M6$,@(98),ACTOVH[X]:M6$,
2420 LET TOTSTD=STDDIR[X]+STDOVH[X],TOTACT=ACTDIR[X]+ACTOVH[X]
2430 IF TOTSTD<>0 THEN LET PCTVAR=(TOTSTD-TOTACT)*100/TOTSTD ELSE LET PCTVAR=0
2440 PRINT (7)@(106),(TOTSTD-TOTACT):M6$,@(114),PCTVAR:M6$
2450 LET TOTSDIR=TOTSDIR+STDDIR[X],TOTSOVH=TOTSOVH+STDOVH[X]
2460 LET TOTADIR=TOTADIR+ACTDIR[X],TOTAOVH=TOTAOVH+ACTOVH[X]
2470 RETURN 
2500 REM " --- Dollar Totals "
2510 IF L+4>L9 THEN GOSUB 5000
2520 PRINT (7)@(74),FILL(53,"-")
2525 IF TOTSDIR<>0 THEN LET PCTVAR=(TOTSDIR-TOTADIR)*100/TOTSDIR ELSE LET PCTVAR=0
2530 PRINT (7)@(9),"*** Total For Work Order "+A0$(5,7)+" ***",@(56),"Direct Labor:",@(72),TOTSDIR:M9$,@(90),TOTADIR:M6$,@(106),TOTSDIR-TOTADIR:M6$,@(114),PCTVAR:M6$
2540 IF TOTSOVH<>0 THEN LET PCTVAR=(TOTSOVH-TOTAOVH)*100/TOTSOVH ELSE LET PCTVAR=0
2550 PRINT (7)@(54),"Overhead Labor:",@(80),TOTSOVH:M9$,@(96),TOTAOVH:M9$,@(106),TOTSOVH-TOTAOVH:M6$,@(114),PCTVAR:M6$
2580 LET TOTADIR=0,TOTAOVH=0,TOTSDIR=0,TOTSOVH=0
2590 LET L=L+3
2595 REM " --- Summary Totals "
2600 IF L+8>L9 THEN GOSUB 5000
2605 IF WO_STATUS$="0" THEN LET CQTY=A[0]
2606 IF WO_STATUS$="1" AND CQTY=0 THEN LET CQTY=A[0]
2610 PRINT (7)@(74),FILL(53,"-")
2630 PRINT (7)@(56),"Labor Totals:",@(77),TOTSLAB:M9$,@(93),TOTALAB:M9$,@(106),TOTSLAB-TOTALAB:M6$,
2640 IF TOTSLAB<>0 THEN PRINT (7)@(114),(TOTSLAB-TOTALAB)*100/TOTSLAB:M6$ ELSE PRINT (7)@(114),0:M6$
2644 PRINT (7)@(60),"Per Unit:",@(77),TOTSLAB/CQTY:M9$,@(93),TOTALAB/CQTY:M9$,@(106),(TOTSLAB-TOTALAB)/CQTY:M6$
2650 PRINT (7)@(53),"Material Totals:",@(77),TOTSMAT:M9$,@(93),TOTAMAT:M9$,@(106),TOTSMAT-TOTAMAT:M6$,
2660 IF TOTSMAT<>0 THEN PRINT (7)@(114),(TOTSMAT-TOTAMAT)*100/TOTSMAT:M6$ ELSE PRINT (7)@(114),0:M6$
2665 PRINT (7)@(60),"Per Unit:",@(77),TOTSMAT/CQTY:M9$,@(93),TOTAMAT/CQTY:M9$,@(106),(TOTSMAT-TOTAMAT)/CQTY:M6$
2670 PRINT (7)@(74),FILL(53,"-")
2675 PRINT (7)@(59),"WO Totals:",@(77),TOTSLAB+TOTSMAT:M9$,@(93),TOTALAB+TOTAMAT:M9$,@(106),(TOTSLAB+TOTSMAT)-(TOTALAB+TOTAMAT):M6$,
2676 PRINT (7)@(114),((TOTSLAB+TOTSMAT)-(TOTALAB+TOTAMAT))*100/(TOTSLAB+TOTSMAT):M6$
2680 PRINT (7)@(53),"Per Unit Totals:",@(77),(TOTSLAB+TOTSMAT)/CQTY:M9$,@(93),(TOTALAB+TOTAMAT)/CQTY:M9$,@(106),((TOTSLAB+TOTSMAT)-(TOTALAB+TOTAMAT))/CQTY:M6$,
2685 PRINT (7)@(114),(((TOTSLAB+TOTSMAT)/CQTY)-((TOTALAB+TOTAMAT)/CQTY))*100/((TOTSLAB+TOTSMAT)/CQTY):M6$
2690 LET TOTSLAB=0,TOTALAB=0,TOTSMAT=0,TOTAMAT=0
2698 LET L=L+8
2699 GOTO 2800
2700 REM " --- Unit Totals "
2710 IF L+4>L9 THEN GOSUB 5000
2725 PRINT (7)@(74),FILL(53,"-")
2726 LET TOTSTD=TOTSSET+TOTSRUN,TOTACT=TOTASET+TOTARUN
2729 IF TOTSTD<>0 THEN LET PCTVAR=(TOTSTD-TOTACT)*100/TOTSTD ELSE LET PCTVAR=0
2730 PRINT (7)@(9),"*** Total For Work Order "+A0$(5,7)+" ***",@(72),TOTSSET:M9$,@(86),TOTASET:M9$
2745 PRINT (7)@(79),TOTSRUN:M9$,@(93),TOTARUN:M9$,@(103),PCTVAR:M6$
2750 PRINT (7)""
2755 LET TOTSSET=0,TOTASET=0,TOTSRUN=0,TOTARUN=0
2760 LET L=L+4
2800 REM " --- All Done With Work Order "
2830 RETURN 
2900 REM " --- All Done!
3200 REM " --- Calculate Actuals "
3210 LET TRAN_PASS$="0"
3220 GOSUB SELECT_TRANSACTION_FILE
3230 IF TRAN_PASS$="4" THEN GOTO 3480
3240 READ (WOTRAN_DEV,KEY=A0$(1,11),DOM=3250)
3250 DIM R[11]
3260 LET K7$=KEY(WOTRAN_DEV,END=3220)
3270 IF K7$(1,11)<>A0$(1,11) THEN GOTO 3220
3280 READ (WOTRAN_DEV,KEY=K7$)IOL=WOTRAN
3290 ON POS(R0$(15,1)="OMS") GOTO 3260,3400,3440,3460
3400 REM " --- Actual Operations
3410 LET Y=POS(R1$(1,3)=X0$,3),Y=INT(Y/3)
3420 LET ACTLAB[Y]=ACTLAB[Y]+R[2],ACTRUN[Y]=ACTRUN[Y]+R[0],ACTDIR[Y]=ACTDIR[Y]+R[0]*R[3],ACTOVH[Y]=ACTOVH[Y]+R[2]-R[0]*R[3],ACTSET[Y]=ACTSET[Y]+R[6],QTYCOM[Y]=QTYCOM[Y]+R[5]
3425 IF R0$(12,3)>LAST_DATE$(Y*3+1,3) THEN LET LAST_DATE$(Y*3+1,3)=R0$(12,3)
3430 GOTO 3260
3440 LET TOTAMAT=TOTAMAT+R[2]
3450 GOTO 3260
3460 LET T7=T7+R[2]
3470 GOTO 3260
3490 RETURN 
3500 REM " --- Select Transaction File "
3510 SELECT_TRANSACTION_FILE: 
3520 ON POS(WO_STATUS$="01") GOTO DONE,CLOSED_WO,OPEN_WO
3530 CLOSED_WO: 
3540 ON POS(TRAN_PASS$="0123") GOTO DONE,FILE_03,FILE_23,FILE_33,DONE
3550 FILE_03: 
3560 LET WOTRAN_DEV=WOT03_DEV
3570 LET TRAN_PASS$="1"
3580 GOTO 3660
3590 FILE_23: 
3600 LET WOTRAN_DEV=WOT23_DEV
3610 LET TRAN_PASS$="2"
3620 GOTO 3660
3630 FILE_33: 
3640 LET WOTRAN_DEV=WOT33_DEV
3650 LET TRAN_PASS$="3"
3660 RETURN 
3670 OPEN_WO: 
3680 ON POS(TRAN_PASS$="0123") GOTO DONE,FILE_01,FILE_21,FILE_31,DONE
3690 FILE_01: 
3700 LET WOTRAN_DEV=WOT01_DEV
3710 LET TRAN_PASS$="1"
3720 GOTO 3800
3730 FILE_21: 
3740 LET WOTRAN_DEV=WOT21_DEV
3750 LET TRAN_PASS$="2"
3760 GOTO 3800
3770 FILE_31: 
3780 LET WOTRAN_DEV=WOT31_DEV
3790 LET TRAN_PASS$="3"
3800 RETURN 
3810 DONE: 
3820 LET TRAN_PASS$="4"
3830 RETURN 
4000 REM " --- End"
4100 RUN "SYS.AA"
5000 REM " --- Heading"
5010 LET L=HEADINGS+2
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 IF HOW$="Dollars" THEN GOTO 5120
5100 PRINT (7)"W.O. #",@(8),"Sts Type",@(29),"Operations",@(55),"Date Req",@(66),"Last",@(78),"Standard",@(94),"Actual",@(104),"% Vnce  Cmp Qty"
5106 PRINT (7)@(64),"Activity",@(76),"Setup",@(85),"Run  Setup",@(99),"Run"
5115 GOTO 5150
5120 PRINT (7)"W.O. #",@(8),"Sts Type",@(29),"Operations",@(55),"Date Req",@(66),"Last",@(80),"Standard",@(96),"Actual",@(107),"$ Vnce",@(115),"% Vnce"
5125 PRINT (7)@(64),"Activity",@(75),"Direct",@(86),"Ovh  Direct",@(102),"Ovh"
5150 LET L=L+3
5190 RETURN 
5200 REM " --- Sub Heading
5210 IF L>L9 THEN GOSUB 5000
5220 ON POS(P9$="TC") GOTO 5290,5240,5270
5240 PRINT (7)'LF',FNH$(T0$+"   "),H5$,'LF'
5250 LET L=L+3
5260 GOTO 5290
5270 PRINT (7)FNF$(T0$(1,C0),C1$)," ",H5$
5280 LET L=L+1
5290 RETURN 
6000 REM " --- First Sub Total
6010 IF T0$="" THEN GOTO 6100
6020 IF L0<=1 THEN PRINT (7)""; LET L=L+1; GOTO 6100
6030 IF L+2>L9 THEN GOSUB 5200
6040 IF P9$="B" THEN PRINT (7)@(45),J$(1,M2-1),
6050 PRINT (7)@(45+M2),J$(1,M1-1),'LF',
6080 IF P9$="B" THEN PRINT (7)@(45),T[0]:M2$,
6090 PRINT (7)@(10),H9$," ",FNH$(T0$+"   "),"Total: ",@(45+M2),T[1]:M1$,'LF'
6095 LET L=L+3
6100 IF T9$<>"" THEN RETURN 
6120 DIM T[1]
6140 LET T0$=K$(4,X8),L0=0
6150 ON POS(P9$="TC") GOTO 6290,6160,6200
6160 DIM H5$(39); LET H5$(1)="*** Not On File ***"
6170 FIND (WOM10_DEV,KEY=N0$+"A"+T0$,DOM=6190)IOL=WOM10A
6180 LET H5$=H5$(6,20)
6185 GOSUB 5200
6190 GOTO 6290
6200 REM " --- Customer
6220 DIM H5$(30); LET H5$(1)="*** Not On File ***"
6240 FIND (ARM01_DEV,KEY=N0$+T0$,DOM=6120)IOL=ARM01A
6260 GOSUB 5200
6290 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8080 DEF FNH$(Q$)=Q$(1,2)+CHR(32*POS("C"=X9$))+Q$(3)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

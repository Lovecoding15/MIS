0010 REM "WOE - Work Order Entry (WO Release & Materials Commitment)"
0011 REM 0
0020 REM "Program WOE.AM"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "10Feb97 JJD: Make sure all components have a warehouse record (1050,5415,5515,5610,6405)"
0032 REM "30Mar98 KJS: Don't allow reprinting of a Pick List if Issues have been entered (660-690,1120,5030,5145)"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0095 PRECISION IVS01_PRECISION
0200 REM " --- IOLISTs "
0210 WOE01A: IOLIST A0$(1),A1$(1),A[ALL]
0215 WOE22A: IOLIST W0$(1),W1$(1),W[ALL]
0220 WOE13A: IOLIST Y0$,Y1$
0230 WOE23A: IOLIST U0$,U1$,U[ALL]
0240 IVM01A: IOLIST D0$,D1$(1),D2$,D3$,D4$,D5$,D6$,D[ALL]
0250 IVM02A: IOLIST B0$,B1$,B2$,B[ALL]
0255 WOE04A: IOLIST OPEN_TRANS$
0260 WOE04C: IOLIST COMIT_TRANS$
0400 REM "--- If Work Order is Closed Exit "
0420 IF A0$(15,1)="C" THEN GOTO 4000
0500 REM " --- Initialize Data "
0510 DIM W0$(15),W1$(151),W[13],OPEN_TRANS$(12),COMIT_TRANS$(12)
0520 DIM HEAD$[3],ANSWER$[3],PROMPT$[3]
0530 LET ANSWER$[0]="Y"
0540 FOR X=1 TO 3
0550 LET ANSWER$[X]="N"
0560 NEXT X
0570 IF A0$(15,1)="O" THEN LET ANSWER$[1]="Y"
0580 LET PROMPT$[0]="Do You Wish to Preview any Material Shortages?"
0590 LET PROMPT$[1]="Release This Order and Commit Materials Now?"
0600 LET PROMPT$[3]="Print This Order with Pick Lists?"
0610 LET PROMPT$[2]="Print This Order with Travelers?"
0620 LET HEAD$[0]="Display Component Shortages?"
0630 LET HEAD$[1]="Release This Work Order?"
0640 LET HEAD$[2]="Print Work Order Traveler?"
0650 LET HEAD$[3]="Print Pick List?"
0660 REM " --- Have Issues Been Entered For This Work Order?"
0670 LET ISSUES=0
0680 FIND (WOE04_DEV,KEY=A0$(1,4)+"D"+A0$(5,7),DOM=0700)
0690 LET ISSUES=1,HEAD$[3]="   Can Not Reprint Pick List While Issues In Process"
0700 REM " --- Background "
0710 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0720 PRINT 'SB',@(2,L0-1),"Release Work Order",'SF'
0740 GOSUB BACKGRD
0900 GOSUB ANSWRS
1000 REM "Display Shortages?
1010 LET V0$="Y",V1$="C",V2$=ANSWER$[0],V3$="YN",V4$=PROMPT$[0]+" (Y=Yes,N=No)",V0=1,V1=FND(HEAD$[0],80),V2=L0
1020 GOSUB 7000
1030 ON V3 GOTO 1040,1000,1000,1000,1500
1040 LET ANSWER$[0]=V$
1060 GOSUB PREVIEW
1070 IF ERR1$<>"" THEN GOTO 4000
1080 IF RESTART$="END" THEN GOTO 1500
1100 REM " --- Prompt for answers?
1110 FOR COUNT=1 TO 3
1120 IF COUNT=3 AND (ANSWER$[1]="N" OR A0$(15,1)="C" OR ISSUES) THEN GOTO 1190
1130 IF COUNT=1 AND A0$(15,1)="O" THEN GOTO 1190
1140 LET V0$="Y",V1$="C",V2$=ANSWER$[COUNT],V3$="YN",V4$=PROMPT$[COUNT]+" (Y=Yes,N=No)",V0=1,V1=FND(HEAD$[COUNT],80),V2=L0+COUNT*2
1150 GOSUB 7000
1160 ON V3 GOTO 1170,1140,1200,1140,1240
1170 LET ANSWER$[COUNT]=V$
1180 IF COUNT=1 THEN IF ANSWER$[1]="N" THEN LET ANSWER$[3]="N"; GOSUB ANSWRS
1190 GOTO 1250
1200 IF COUNT>0 THEN LET COUNT=COUNT-1
1210 IF COUNT=0 THEN EXITTO 1000
1220 IF COUNT=1 AND A0$(15,1)="O" THEN LET COUNT=COUNT-1; EXITTO 1000
1230 GOTO 1130
1240 LET COUNT=4
1250 NEXT COUNT
1500 REM "All done
1510 LET RESTART$=""
1520 LET V0$="Y",V4$="Is The Above Information Correct (Y/N)?"
1530 LET V1$="C",V2$="Y",V3$="YN",V0=1,V1=FNV(V4$),V2=22
1540 GOSUB 7000
1550 IF V3=4 THEN GOTO 4000
1560 IF V$="N" THEN GOTO 1000
1570 IF ANSWER$[1]="Y" THEN GOSUB RLEASE
1580 IF ANSWER$[2]="Y" THEN GOSUB TRAVEL
1590 IF ANSWER$[3]="Y" THEN GOSUB PICK
4000 REM " --- Exit Process "  
4010 LET O9=2
4020 RUN "WOE.AA"
4100 REM " --- Release Here"
4110 RLEASE: 
4120 IF POS(A0$(15,1)="PQ")=0 THEN GOTO 4880
4130 REM " --- Update On Order Quantity "    
4140 DIM PARAMS[0],PARAMS$[4],ITEMS$[3],REFS$[11],REFS[5],IVFILES[44]
4150 LET PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3),PARAMS$[2]=P2$,PARAMS$[3]=P3$,PARAMS$[4]=P4$
4160 LET ACTION$="OO",ITEMS$[0]=N0$,ITEMS$[1]=A0$(54,2),ITEMS$[2]=A0$(56,20)
4170 LET REFS[0]=A[0],IVFILES[0]=SYS01_DEV,IVFILES[1]=IVM01_DEV
4180 LET IVFILES[2]=IVM02_DEV,IVFILES[7]=IVM07_DEV,IVFILES[17]=IVM17_DEV
4190 GOSUB UPDATE_INVENTORY
4200 READ (WOE13_DEV,KEY=A0$(1,11),DOM=4210)
4210 LET K8$=KEY(WOE13_DEV,END=4230)
4220 IF POS(A0$(1,11)=K8$)=1 THEN GOTO 4850
4230 DIM Y1$(70)
4240 LET Y1$(1)=A0$(12,3),Y1$(49)=A0$(54,22),Y0$=A0$(1,11)+"000",O9$="0"
4250 READ (WOE22_DEV,KEY=A0$(1,11)+"B",DOM=4260)
4260 LET K6$=KEY(WOE22_DEV,END=4830)
4270 IF K6$(1,11)<>Y0$(1,11) THEN GOTO 4830
4280 READ (WOE22_DEV,KEY=K6$)IOL=WOE22A
4290 IF W1$(91,1)="M" THEN GOTO 4260
4300 DIM U1$(40),U[4]
4310 LET U0$=W0$(1,11)+W0$(13),U1$(1,6)=W1$(1,6),U1$(19)=Y1$(49,2)+W1$(27,20)
4320 FIND (WOE23_DEV,KEY=U0$,DOM=4340)
4330 GOTO 4260
4340 LET U[0]=W[2],U[2]=W[7],U[4]=U[2]
4350 PRINT @(0,22),'CL',@(10,22),"Creating Step: ",U0$(12,3),"..."
4400 REM " --- Update Inventory Committed Quantity "
4410 DIM PARAMS[0],PARAMS$[4],ITEMS$[3],REFS$[11],REFS[5],IVFILES[44]
4420 LET PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3),PARAMS$[2]=P2$,PARAMS$[3]=P3$,PARAMS$[4]=P4$
4430 LET ACTION$="CO",ITEMS$[0]=N0$,ITEMS$[1]=U1$(19,2),ITEMS$[2]=U1$(21,20)
4440 LET REFS[0]=U[0],IVFILES[0]=SYS01_DEV,IVFILES[1]=IVM01_DEV
4445 LET IVFILES[2]=IVM02_DEV,IVFILES[7]=IVM07_DEV,IVFILES[17]=IVM17_DEV
4450 GOSUB UPDATE_INVENTORY
4460 GOTO 4800
4700 REM " --- No Warehouse Record
4710 PRINT 'RB',@(0,22),"Missing Warehouse ",Y1$(49,2)," For Item: ",W1$(27,20)," Record Not Written, <Enter>:",
4720 LET V0$="S",V1$="C",V2$="",V3$="",V4$="",V0=1,V1=78,V2=22
4730 GOSUB 7000
4740 GOTO 4260
4800 REM " --- Write It "
4810 WRITE (WOE23_DEV,KEY=U0$)IOL=WOE23A
4820 GOTO 4260
4830 REM " --- Write Header
4840 WRITE (WOE13_DEV,KEY=Y0$)IOL=WOE13A
4850 LET A0$(15,1)="O"
4860 PRINT @(65,3),A0$(15,1),@(67,3),"**Open**"
4870 WRITE (WOE01_DEV,KEY=A0$(1,11))IOL=WOE01A
4880 RETURN 
5000 REM " --- Display answers here
5010 ANSWRS: 
5020 FOR X=0 TO 3
5030 IF (X=1 AND A0$(15,1)="O") OR (X=3 AND ISSUES) THEN GOTO 5050
5040 PRINT @(FND(HEAD$[X],80),L0+X*2),ANSWER$[X],'CL'
5050 NEXT X
5060 RETURN 
5100 REM " --- Display Background stuff
5110 BACKGRD: 
5120 PRINT @(0,L0),'CE','SB'
5130 FOR X=0 TO 3
5140 IF X=1 AND A0$(15,1)="O" THEN PRINT @(FNC(HEAD$[X],80),L0+X*2),"Work Order Has Been Released"; GOTO 5160
5145 IF X=3 AND ISSUES THEN PRINT @(FNC(HEAD$[3],80),L0+X*2),HEAD$[3]; GOTO 5160
5150 PRINT @(FNC(HEAD$[X],80),L0+X*2),HEAD$[X]
5160 NEXT X
5170 PRINT 'SF'
5180 RETURN 
5200 REM " --- Generate Pick Sort here
5210 PICK: 
5220 LET COMIT_TRANS$=A0$(1,4)+"C"+A0$(5,7)
5230 WRITE (WOE04_DEV,KEY=COMIT_TRANS$)IOL=WOE04C
5240 RETURN 
5300 REM " --- Generate Traveler Here
5310 TRAVEL: 
5320 LET OPEN_TRANS$=A0$(1,4)+"A"+A0$(5,7)
5330 WRITE (WOE04_DEV,KEY=OPEN_TRANS$)IOL=WOE04A
5340 RETURN 
5400 REM " --- Preview Shortages Here"
5410 PREVIEW: 
5415 IF ANSWER$[0]="N" THEN GOTO 5450
5420 PRINT @(0,L0),'CE'
5430 PRINT 'SB',@(0,L0),"Item/Description",@(78-M2*4-9),"Qty Req'd",@(78-M2*3-8),"On Hand",@(78-M2*2-7),"Commit",@(78-M2-6),"Avail",@(78-9),"On Order",'SF'
5440 LET L=1,L9=0,ERR1$=""
5450 READ (WOE22_DEV,KEY=A0$(1,11)+"B",DOM=5460)
5460 LET K6$=KEY(WOE22_DEV,END=6400)
5470 IF K6$(1,11)<>A0$(1,11) THEN GOTO 6400
5480 READ (WOE22_DEV,KEY=K6$)IOL=WOE22A
5490 IF W1$(91,1)="M" THEN GOTO 5460
5500 DIM B[16]
5510 FIND (IVM02_DEV,KEY=N0$+W1$(25,22),DOM=5600)IOL=IVM02A
5515 IF ANSWER$[0]="N" THEN GOTO 5460
5520 IF B[0]-B[2]<W[2] THEN GOSUB SHOWIT; IF RESTART$="END" THEN GOTO 6440
5530 GOTO 5460
5600 REM " --- No Warehouse Record
5610 PRINT 'RB',@(0,21),"Item ",W1$(27,P[0])," Missing From Warehouse ",A0$(54,2),
5620 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Order Will Not Be Released",V0=1,V1=FNV(V4$),V2=22
5630 GOSUB 7000
5640 LET ERR1$="Y",L9=L9+1
5650 PRINT @(0,21),'CL'
5660 GOTO 5460
5700 REM " --- Showit here "
5710 SHOWIT: 
5720 IF L0+L+1>21 THEN GOSUB 6500; IF RESTART$="Y" THEN GOTO 5400 ELSE IF RESTART$="END" THEN GOTO 5780
5730 DIM D1$(DESC[1]+DESC[2]+DESC[3])
5740 FIND (IVM01_DEV,KEY=B0$(1,2)+B0$(5))IOL=IVM01A
5750 PRINT @(0,L0+L),B0$(5)," ",@(78-M2*5,L0+L),W[2]:M2$,B[0]:M2$,B[2]:M2$,B[0]-B[2]:M2$,B[1]:M2$
5760 PRINT @(2,L0+L+1),FNITEM$(D1$,DESC[1],DESC[2],DESC[3])
5770 LET L=L+2,L9=L9+1
5780 RETURN 
6400 REM " --- All Done "
6405 IF ANSWER$[0]="N" THEN GOTO 6490
6410 IF L9=0 THEN LET V4$="No Component Shortages Found,"
6420 GOSUB 6510
6430 IF RESTART$="Y" THEN GOTO 5400
6440 GOSUB BACKGRD; GOSUB ANSWRS
6490 RETURN 
6500 REM " --- Full screen here
6505 LET V4$="<F1>=Restart,"
6510 LET RESTART$="N",L=1
6520 LET V0$="S",V4$=V4$+" <Enter> to Continue. "
6540 LET V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
6560 GOSUB 7000
6570 PRINT @(0,L0+1),'CE'
6580 IF V3=1 THEN LET RESTART$="Y"
6585 IF V3=4 THEN LET RESTART$="END"
6590 RETURN 
6600 REM " --- Update Inventory "
6605 UPDATE_INVENTORY: 
6610 CALL "IVC.UA",ACTION$,IVFILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEMS$[ALL],REFS$[ALL],REFS[ALL],STATUS
6615 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8030 DEF FND(Q$,Q)=INT((Q+LEN(Q$))/2)+2
8070 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8095 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "SHM - Terminal Parameters"
0020 REM "Program SHP.AD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "16Oct96 WGH: Display Shipping Label Printer ID (5040)"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 SHM10E: IOLIST B0$(1),B1$(1)
0220 SHM10D: IOLIST C0$(1),C1$(1)
0230 SHM10S: IOLIST *,SCALE$(1)
0240 SHM10F: IOLIST *,WAREHOUSE$(1)
0500 REM " --- Initializations"
0510 DIM B0$(8),B1$(48),C0$(4),C1$(128),SCALE$(30),WAREHOUSE$(30)
0590 LET R$="E",B0$(1)=N0$+R$,L9=13
0700 REM " --- Background"
0750 PRINT 'SB',@(25,4),"Carrier/Terminal Parameters",@(31,6),"Carrier:",@(27,7),"Terminal ID:",@(17,8),"Is a scale installed?:",@(27,9),"Scale model:",@(16,10),"Print Shipping Labels?:",@(21,11),"Print COD Labels?:",@(29,13),"Warehouse:",'SF',
0900 REM " --- Position file"
0990 READ (SHM10_DEV,KEY=N0$+R$,DOM=1000)
1000 REM " --- Carrier ID"
1005 PRINT 'CF',
1010 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=40,V2=6,I0=0
1015 LET V4$="Enter a Carrier Reference (<Enter>=Next/<F3>=Lookup)"
1020 GOSUB 7000
1030 ON V3 GOTO 1060,1000,1000,1040,9900
1040 CALL "SHC.LK",V1,V2,"D",0,V$
1050 IF V$="" THEN GOTO 1000
1060 IF V$="" THEN GOTO 1100
1070 LET B0$(4,1)=V$,C1$(1)=""
1080 FIND (SHM10_DEV2,KEY=N0$+"D"+V$,DOM=1000)IOL=SHM10D
1085 PRINT @(V1,V2),V$," ",C1$(1,30),
1090 GOTO 1200
1100 REM " --- Retrieve next record"
1110 LET K$=KEY(SHM10_DEV,END=1150)
1115 READ (SHM10_DEV)
1120 IF POS(N0$+R$=K$)<>1 THEN GOTO 1150
1130 LET B0$=K$
1140 GOTO 1300
1150 READ (SHM10_DEV,KEY=N0$+R$,DOM=1000)
1190 GOTO 1000
1200 REM " --- Terminal ID"
1210 LET V0$="S",V1$="CR",V2$=B0$(5,4),V3$="",V0=4,V1=40,V2=7
1220 LET V4$="Enter a Terminal ID"
1230 IF V2$="  " THEN LET V2$=FID(0)
1240 GOSUB 7000
1250 ON V3 GOTO 1260,1200,1000,1200,9900
1260 LET B0$(5,4)=V$
1300 REM " --- Retrieve record"
1310 READ (SHM10_DEV,KEY=B0$,DOM=1400)IOL=SHM10E
1380 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record"
1410 LET V4$="Is this a new record (Y/N)?"
1420 LET V0$="Y",V1$="C",V2$="N",V3$="",V0=1,V1=FNV(V4$),V2=22
1430 GOSUB 7000
1440 ON V3 GOTO 1490,1400,1400,1400,9900
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record"
1510 DIM B1$(48)
1520 LET B1$(1,3)="NNN"
2000 REM " --- Is there a scale?"
2010 LET V0$="Y",V1$="CR",V2$=B1$(1,1),V3$="",V0=1,V1=40,V2=8,I0=0
2015 LET V4$="Does this system have a scale connected to the computer?"
2020 GOSUB 7000
2030 ON V3 GOTO 2040,2000,3900,2000,3900
2040 LET B1$(1,1)=V$
2090 GOTO 3900
2500 REM " --- Which port is the scale on?"
2505 LET V0$="S",V1$="C",V2$=B1$(4,2),V3$="",V0=2,V1=42,V2=8
2510 IF B1$(1,1)<>"Y" THEN LET B1$(4,2)="  "; GOTO 2580
2515 LET V4$="Enter the BASIC port name that the scale is connected to"
2520 GOSUB 7000
2530 ON V3 GOTO 2540,2500,3900,2500,3900
2540 LET B1$(4,2)=V$
2580 PRINT @(V1,V2),B1$(4,2),
2590 GOTO 3900
2700 REM " --- Scale Program"
2705 LET V0$="S",V1$="C^",V2$=B1$(30,6),V3$="",V0=6,V1=40,V2=9
2710 IF B1$(1,1)<>"Y" THEN LET B1$(30,6)="",SCALE$(1)=""; GOTO 2780
2715 LET V4$="Enter the scale interface program name (<F3>=Lookup)"
2720 GOSUB 7000
2725 ON V3 GOTO 2760,2700,3900,2730,3900
2730 CALL "SHC.LK",V1,V2,"S",0,V$
2740 IF V$="" THEN GOTO 2700
2760 LET B1$(30,6)=V$,SCALE$(1)=""
2770 FIND (SHM10_DEV2,KEY=N0$+"S"+B1$(30,6),DOM=2700)IOL=SHM10S
2780 PRINT @(V1,V2),B1$(30,6)," ",SCALE$
2790 GOTO 3900
3000 REM " --- Print shipping labels?"
3010 LET V0$="Y",V1$="C",V2$=B1$(2,1),V3$="",V4$="",V0=1,V1=40,V2=10
3015 LET V4$="Print shipping labels (Y/N)?"
3020 GOSUB 7000
3030 ON V3 GOTO 3040,3000,3900,3000,3900
3040 LET B1$(2,1)=V$
3090 GOTO 3900
3100 REM " --- Shipping label printer"
3105 IF B1$(2,1)<>"Y" THEN GOTO 3190
3110 LET V0$="S",V1$="C",V2$=B1$(36,2),V3$="",V0=2,V1=42,V2=10
3115 LET V4$="Enter the name of the shipping label printer"
3120 GOSUB 7000
3140 LET B1$(36,2)=V$
3190 GOTO 3900
3200 REM " --- Print COD labels?"
3210 LET V0$="Y",V1$="C",V2$=B1$(3,1),V3$="",V0=1,V1=40,V2=11
3215 LET V4$="Print COD labels (Y/N)?"
3220 GOSUB 7000
3230 ON V3 GOTO 3240,3200,3900,3200,3900
3240 LET B1$(3,1)=V$
3290 GOTO 3900
3300 REM " --- COD label printer"
3305 IF B1$(3,1)<>"Y" THEN GOTO 3390
3310 LET V0$="S",V1$="CK",V2$=B1$(38,2),V3$="",V0=2,V1=42,V2=11
3315 LET V4$="Enter the name of the COD label printer"
3320 GOSUB 7000
3330 ON V3 GOTO 3340,3300,3900,3300,3900
3340 LET B1$(38,2)=V$
3390 GOTO 3900
3400 REM " --- Warehouse ID of this terminal"
3410 LET V0$="S",V1$="CKR",V2$=B1$(40,2),V3$="",V0=2,V1=40,V2=13
3415 LET V4$="Enter the Warehouse ID code for this terminal (<F3>=Lookup)"
3420 GOSUB 7000
3430 ON V3 GOTO 3450,3400,3900,3435,3900
3435 CALL "SHC.LK",V1,V2,"F",0,V$
3445 IF V$="" THEN GOTO 3400
3450 LET B1$(40,2)=V$,WAREHOUSE$(1)=""
3455 FIND (SHM10_DEV2,KEY=N0$+"F"+B1$(40,2),DOM=3400)IOL=SHM10F
3460 PRINT @(V1,V2),B1$(40,2)," ",WAREHOUSE$(1)
3490 GOTO 3900
3900 REM " --- Next field"
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2000,2500,2700,3000,3100,3200,3300,3400,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CR",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write record"
4110 IF B1$(1,1)<>"Y" THEN LET B1$(4,2)="",B1$(30,6)=""
4120 IF B1$(2,1)<>"Y" THEN LET B1$(36,2)=""
4130 IF B1$(3,1)<>"Y" THEN LET B1$(38,2)=""
4150 WRITE (SHM10_DEV,KEY=B0$)IOL=SHM10E
4190 GOTO 0900
4200 REM " --- Remove record"
4210 REMOVE (SHM10_DEV,KEY=B0$,DOM=4230)
4290 GOTO 0900
5000 REM " --- Display record"
5010 LET SCALE$(1)="",WAREHOUSE$(1)=""
5020 FIND (SHM10_DEV2,KEY=N0$+"S"+B1$(30,6),DOM=5030)IOL=SHM10S
5030 FIND (SHM10_DEV2,KEY=N0$+"F"+B1$(40,2),DOM=5040)IOL=SHM10F
5040 PRINT @(40,6),B0$(4,1),@(40,7),B0$(5,4),@(40,8),B1$(1,1)," ",B1$(4,2),@(40,9),B1$(30,6)," ",SCALE$(1),@(40,10),B1$(2,1),@(42,10),B1$(36,2),@(40,11),B1$(3,1),@(42,11),B1$(38,2),@(40,13),B1$(40,2)," ",WAREHOUSE$(1)
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SHP.AA"
9999 END

0010 REM "WOE - WO Time Sheet Data Entry"
0020 REM "Program WOE.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "17Sep96 JJD: Establish proper precision (595,597)"
0032 REM "17Apr97 JWK: Non-<CR> creates bad date entry (6747)
0033 REM "19Nov98 WGH: Delete obsolete code (220,1240,1385,6000-6040)
0034 REM "18Mar99 KJS: Y2K year processing (460,467,8145-8190)
0040 REM 
0050 SETERR 9000
0060 SETESC 9000
0070 ON O0 GOTO 0080,1300,4200
0080 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=14
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-01",FILES$[2]="WOE-02",FILES$[3]="SYS-01"
0120 LET FILES$[4]="IVM-10",FILES$[5]="IVM-01"
0130 LET FILES$[6]="WOE-11",FILES$[7]="WOE-21",FILES$[8]="WOE-31"; REM "Must open batch before GLC.BA
0150 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE01_DEV=CHANNELS[1],WOE02_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0165 LET IVM10_DEV=CHANNELS[4],IVM01_DEV=CHANNELS[5]
0170 LET WOE11_DEV=CHANNELS[6],WOE21_DEV=CHANNELS[7],WOE31_DEV=CHANNELS[8]
0200 REM " --- IOLISTs"
0210 TIMESHEETA: IOLIST A0$,A1$,A[ALL]
0230 WOS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0240 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0250 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0260 PRS01A: IOLIST X$,I1$,I2$,X1$,X$,I3$,X$,I4$,I5$
0270 IVS01A: IOLIST *,*,I9$,I8$
0280 ARS01A: IOLIST *,*,*,*,*,P7$
0290 EMPCODE: IOLIST *,X0$
0300 WOE01A: IOLIST *,X0$
0400 REM " --- Parameters"
0405 DIM P[4],G[4],A[11],DESC[3]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N4$=F5$
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0425 FIND (SYS01_DEV,KEY=N0$+"GL"+"00",DOM=9800)IOL=GLS01A
0430 FIND (SYS01_DEV,KEY=N0$+"IV"+"00",DOM=9800)IOL=IVS01A
0435 FIND (SYS01_DEV,KEY=N0$+"AR"+"00",DOM=0440)IOL=ARS01A
0440 IF P3$(4,1)="Y" THEN FIND (SYS01_DEV,KEY=N0$+"PR00",DOM=9800)IOL=PRS01A
0445 IF P3$(4,1)="Y" THEN LET P1$=I1$,P2$=I2$,M0$=I3$,M2$=I4$,M3$=I5$,R$=X1$(1,2),P[3]=NUM(X1$(7,1)) ELSE LET P[3]=NUM(I9$(5,1))
0450 LET IVS01_PRECISION=P[3]
0455 FOR X=0 TO 4
0460 LET G[X]=NUM(G2$(X*2+1,2),ERR=0461)
0465 NEXT X
0467 LET G[2]=FNYY_YEAR(G2$(5,2))
0470 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(19,6)),P[4]=NUM(I9$(1,2))
0475 LET GL$="N",STATUS=0,SOURCE$=PGM(-2)
0480 CALL "GLC.BA",ERR=0485,SOURCE$,N2$,GLM11$,GL$,STATUS
0485 IF STATUS THEN GOTO 9900
0500 REM " --- Init Data"
0510 DIM X0$(30),Q[8]
0513 IF P3$(13,1)=" " THEN LET P3$(13,1)="N"
0515 LET M0=LEN(M0$),TS_SEQ$=P3$(25,1); REM "DATA ENTRY SEQUENCE
0520 FOR X=1 TO 3; LET DESC[X]=NUM(I9$((X-1)*2+9,2)); NEXT X
0523 CALL "SYC.RM",M1$,M1,8,12
0525 CALL "SYC.RM",M2$,M2,8,12
0527 CALL "SYC.RM",M3$,M3,8,12
0530 IF TS_SEQ$="W" THEN LET Q[0]=MAX(8,M0)+4 ELSE LET Q[0]=11
0532 LET Q[1]=Q[0]+4
0534 LET Q[6]=77-M2*3,Q[7]=77-M2*2,Q[8]=77-M2
0536 IF P3$(13,1)="Y" THEN LET Q[4]=Q[6]-11,Q[5]=Q[6]-5
0538 IF P3$(4,1)="Y" THEN IF P3$(13,1)="N" THEN LET Q[2]=Q[6]-6,Q[3]=Q[6]-3 ELSE LET Q[2]=Q[4]-6,Q[3]=Q[4]-3
0550 ON POS(TS_SEQ$="DEW") GOTO 9900,0560,0570,0580
0560 LET P0=3,P1=9,P2=16; GOTO 0590
0570 LET P0=9,P1=3,P2=16; GOTO 0590
0580 LET P0=7,P1=3,P2=14
0590 DIM A0$(P2),A1$(30); LET A0$(1)=N0$,P9=P2-2,P9=P2-2
0600 REM " --- Additional File Opens "
0601 IF TS_SEQ$="D" THEN LET TIMESHEET_DEV=WOE11_DEV
0602 IF TS_SEQ$="E" THEN LET TIMESHEET_DEV=WOE21_DEV
0603 IF TS_SEQ$="W" THEN LET TIMESHEET_DEV=WOE31_DEV
0610 IF P3$(4,1)="Y" THEN GOTO 0625
0615 LET FILES$[9]="WOM-01",FILES$[10]="WOM-03"
0620 GOTO 0640
0625 LET FILES$[9]="PRM-01",FILES$[10]="PRM-03",FILES$[11]="PRM-10"
0630 LET FILES$[12]="PRT-01"
0635 IF P3$(11,1)="A" THEN LET FILES$[13]="PRM-02"
0640 IF P3$(1,1)="Y" THEN LET FILES$[14]="BMM-08"
0645 IF P3$(1,1)<>"Y" THEN LET FILES$[14]="WOM-02"
0650 CALL "SYC.DA",1,9,14,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0655 IF STATUS THEN GOTO 9900
0660 LET EMPCODE_DEV=CHANNELS[9],EMPXREF_DEV=CHANNELS[10]
0665 IF CHANNELS[11] THEN LET PRM10_DEV=CHANNELS[11],PRT01_DEV=CHANNELS[12]
0670 IF CHANNELS[13] THEN LET PRM02_DEV=CHANNELS[13]
0675 LET OPCODE_DEV=CHANNELS[14]
0700 REM " --- Background"
0705 CALL "SYC.WC",1,0,80,0,0,5,0
0710 PRINT 'SB',
0715 ON POS(TS_SEQ$="DEW") GOTO 9900,0720,0740,0760
0720 PRINT @(6,3),"Date:",@(33,3),"Employee:",@(3,6),"W.O."
0730 GOTO 0780
0740 PRINT @(2,3),"Employee:",@(37,3),"Date:",@(3,6),"W.O."
0750 GOTO 0780
0760 PRINT @(0,3),"Work Order:",@(37,3),"Date:",@(3,6),"Emp #"
0780 PRINT @(70-M2,3),"Total:",@(69-M2,4),"Entered:",@(0,6),"Sq",@(Q[0],6),"Stp Op",@(Q[7]-6,6),"Setup",@(Q[8]-6,6),"Hours",@(77-7,6),"Complete",
0785 IF P3$(13,1)="Y" THEN PRINT @(Q[4],6),"Start",@(Q[5],6),"Stop"
0788 IF P3$(4,1)="Y" THEN PRINT @(Q[2],6),"PC TC"
0790 PRINT 'SF',
0900 REM " --- Init File"
0920 READ (TIMESHEET_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Key"
1005 PRINT 'CF',
1010 LET V2$="",V2$=KEY(TIMESHEET_DEV,END=1045),A0$(3)=""
1015 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1020 IF LEN(V2$)<P2 THEN LET V2$=""
1025 IF V2$="" THEN GOTO 1040
1030 IF V2$(P2-1,2)="00" THEN GOTO 1040
1035 READ (TIMESHEET_DEV,KEY=V2$(1,P2-1)+$FF$,DOM=1000)
1040 IF V2$<>"" THEN LET V2$=V2$(3,P0)
1045 LET V1$="CER",V3$="",V0=P0,V1=12,V2=3
1050 IF TS_SEQ$<>"D" THEN GOTO 1065
1055 LET V4$="Enter Time Sheet Date",V0$="D"
1060 IF POS(" "<>V2$)=0 THEN LET V2$=FNC$(F0$(7,3))
1065 IF TS_SEQ$<>"E" THEN GOTO 1080
1070 LET V0$="M",V3$=M0$,V4$="Enter A Valid Employee Number (<F3>=Lookup)"
1075 IF V2$<>"" THEN LET V2$=V2$(1,P[0])
1080 IF TS_SEQ$="W" THEN LET V0$="Z",V4$="Enter A Valid Work Order # (<F3>=Lookup)"
1085 GOSUB 7000
1090 IF V3=4 THEN GOTO 9900
1095 IF TS_SEQ$="E" AND V3=3 THEN GOSUB 6500
1100 IF TS_SEQ$="W" AND V3=3 THEN GOSUB 6600
1105 IF V$="" THEN GOTO 0900
1110 IF V0$<>"D" THEN GOTO 1125
1115 GOSUB 6900
1120 IF STATUS>99 THEN GOTO 1045
1125 LET A0$(3,P0)=V$
1130 IF TS_SEQ$="E" THEN LET A0$(3,9)=V$
1135 IF TS_SEQ$="W" THEN LET W9$=V$
1200 REM " --- Find Key"
1210 READ (TIMESHEET_DEV,KEY=A0$(1,P2),DOM=1220)
1220 IF TS_SEQ$<>"E" THEN GOTO 1260
1230 FIND (EMPCODE_DEV,KEY=A0$(1,11),DOM=1000)IOL=EMPCODE
1240 GOSUB 6200
1250 IF P3$(4,1)="Y" THEN LET R1$=X0$(223,2),R2$=X0$(225,1)
1260 IF TS_SEQ$="W" THEN FIND (WOE01_DEV,KEY=A0$(1,2)+"  "+A0$(3,7),DOM=1000)IOL=WOE01A
1300 REM " --- 2nd Field
1305 PRINT 'CF'
1307 GOSUB 5100
1310 LET V1$="C",V2$="",V3$="",V0=P1,V1=43,V2=3
1315 DIM A[11]
1320 ON POS(TS_SEQ$="DEW") GOTO 9900,1330,1340
1330 LET V0$="M",V3$=M0$,V0=9,V4$="Enter A Valid Employee Number (<F3>=Lookup/"
1335 GOTO 1345
1340 LET V0$="D",V4$="Enter Time Sheet Date (<F1>=Menu Date"
1345 LET V4$=V4$+"/<Enter>=Next Entry)"
1350 GOSUB 7000
1355 IF V3=2 OR V3=4 THEN GOTO 1000
1357 IF POS(TS_SEQ$="EW")<>0 AND V3=1 THEN LET V$=F0$(7,3); PRINT @(V1,V2),FNB$(V$)
1360 IF TS_SEQ$="D" AND V3=3 THEN GOSUB 6500; IF V3=4 THEN GOTO 1300
1370 IF V$<>"" THEN GOTO 1375
1372 GOSUB 6700
1375 IF V$="" THEN GOTO 1000
1377 IF V0$="D" THEN GOSUB 6900; IF STATUS>99 THEN GOTO 1310
1380 LET A0$(P0+3,P1+2)=V$+"00"
1385 IF TS_SEQ$="D" THEN FIND (EMPCODE_DEV,KEY=A0$(1,2)+A0$(6,9),DOM=1300)IOL=EMPCODE; IF P3$(4,1)="Y" THEN LET R1$=X0$(223,2),R2$=X0$(225,1); GOSUB 6200
1390 EXTRACT (TIMESHEET_DEV,KEY=A0$(1,P2),DOM=1500)IOL=TIMESHEETA
1400 REM " --- Correct Entry?
1410 GOSUB 5000
1420 CALL "SYC.YN",1,"Is This The Correct Entry",2,V$,V3
1450 IF V$="N" THEN GOTO 1300
1460 IF V3=4 THEN GOTO 1300
1490 GOTO 4000
1500 REM " --- Set-Up"
1505 GOSUB 5000
1510 CALL "SYC.YN",1,"Is This A New Entry",2,V$,V3
1530 IF V$="N" THEN GOTO 1300
1540 IF V3=4 THEN GOTO 1300
1550 DIM A[11]
2000 REM " --- First Field
2020 LET V0$="N",V1$="",V2$=STR(A[0]),V3$=M2$,V0=0,V1=77-M2,V2=3
2040 GOSUB 7000
2060 LET A[0]=V
2090 GOTO 3900
2200 REM " --- Second Field
2240 GOSUB 7000
2290 GOTO 3900
2400 REM " --- Third Field
2440 GOSUB 7000
2490 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,3990
3990 LET I0=0
4000 REM " --- Options"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)?",V0=6,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF V3=4 THEN GOTO 1300
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 WRITE (TIMESHEET_DEV,KEY=A0$(1,P2))IOL=TIMESHEETA
4190 RUN "WOE.BB"
4200 REM " --- Delete"
4220 READ (TIMESHEET_DEV,KEY=A0$(1,P9),DOM=4300)
4300 LET K$=KEY(TIMESHEET_DEV,END=4400)
4320 IF K$(1,P9)<>A0$(1,P9) THEN GOTO 4400
4340 REMOVE (TIMESHEET_DEV,KEY=K$,DOM=4360)
4360 GOTO 4300
4400 REMOVE (TIMESHEET_DEV,KEY=A0$(1,P9),DOM=4490)
4490 GOTO 1000
5000 REM " --- Display"
5010 ON POS(TS_SEQ$="DEW") GOTO 9900,5020,5040,5060
5020 PRINT @(43,3),FNF$(A0$(P0+3,P[0]),M0$),@(44,4),X0$(1,30)
5030 GOTO 5080
5040 PRINT @(43,3),FNB$(A0$(P0+3,P1))
5050 GOTO 5080
5060 PRINT @(43,3),FNB$(A0$(P0+3,P1))
5080 PRINT @(77-M2,3),A[0]:M2$
5100 REM " --- Control Display
5110 ON POS(TS_SEQ$="DEW") GOTO 9900,5120,5140,5160
5120 PRINT @(12,3),FNB$(A0$(P0,3))
5130 GOTO 5190
5140 PRINT @(12,3),FNF$(A0$(3,P[0]),M0$),@(13,4),X0$(1,30)
5150 GOTO 5190
5160 PRINT @(12,3),A0$(3,P0),@(13,4),X0$(1,30)
5190 RETURN 
6200 REM " --- Condense Name
6210 DIM D9$(30)
6220 LET D9$(1)=X0$(1,POS("   "=X0$(1,16)+"   ")-1)+", "+X0$(17,14)
6240 LET X0$(1,30)=D9$
6290 RETURN 
6500 REM " --- Employee Lookup Here
6560 CALL "WOC.LP",V$
6580 RETURN 
6600 REM " --- W/O Lookup
6630 LET V$=P3$(2,1),V6$="O"
6640 CALL "SYC.LW",V$,V6$
6690 RETURN 
6700 REM " --- Next Ones..
6720 READ (TIMESHEET_DEV,KEY=A0$(1,P9)+$FF$,DOM=6730)
6730 LET K$=KEY(TIMESHEET_DEV,END=6742)
6735 IF LEN(K$)<>P2 THEN READ (TIMESHEET_DEV); GOTO 6730
6737 IF K$(P2-1,2)<>"00" THEN GOTO 6730
6740 IF A0$(1,2+P0)=K$(1,2+P0) THEN GOTO 6750
6742 PRINT @(V1,V2),FILL(6),
6744 LET V0$="S",V1$="C",V2$="",V3$="",V4$="No More Entries Made (<Enter>=Continue)",V0=1,V1=FNV(V4$),V2=22
6745 PRINT @(V1,V2),'RB',
6746 GOSUB 7000
6747 LET V$=""
6748 GOTO 6880
6750 READ (TIMESHEET_DEV,KEY=K$,DOM=6730)IOL=TIMESHEETA
6780 LET V$=A0$(P0+3,P1)
6790 IF TS_SEQ$<>"D" THEN GOTO 6890
6800 FIND (EMPCODE_DEV,KEY=A0$(1,2)+A0$(6,9),DOM=6700)IOL=EMPCODE
6810 IF P3$(4,1)="Y" THEN LET R1$=X0$(223,2),R2$=X0$(225,1)
6820 GOTO 6890
6880 LET A0$(3+P0)=""
6890 RETURN 
6900 REM " --- Date verification"
6910 LET STATUS=0
6920 IF GL$="Y" THEN CALL "GLC.EA",V$,"Y",PERIOD$,YEAR$,STATUS
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "WOS - Generate Requisitions (Update Overlay)"
0020 REM "Program WOU.IB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "14Oct98 KJS: Creating P/O Reqs without a date and with the wrong cost (1580,1610)
0032 REM "16Jan99 WGH: Use Vendor's item discount pricing when available for Special Order Items (250,510,1516-1540,1560-1570,1610-1810)
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLISTs"
0210 WOREQA: IOLIST A0$,A1$,A[ALL]
0220 WOE01A: IOLIST B0$,B1$,B[ALL]
0230 IVM01A: IOLIST C0$,C1$,C2$,C3$,C4$,C5$,C6$,C[ALL]
0240 IVM02A: IOLIST D0$,D1$,D2$,D[ALL]
0250 IVM05A: IOLIST L0$,L1$,L2$,L[ALL]
0500 REM " --- Init Data"
0510 DIM A[13],B[11],C[12],D[20],L[12],T0$(13),CHANS[11]
0520 LET CHANS[1]=APM02_DEV,CHANS[2]=POE01_DEV,CHANS[3]=POE02_DEV
0530 LET CHANS[4]=POE11_DEV,CHANS[5]=POE52_DEV,CHANS[6]=POM02_DEV
0550 LET CHANS[7]=POS10_DEV,CHANS[8]=POE21_DEV,CHANS[9]=POE31_DEV
0560 LET CHANS[10]=POE41_DEV,CHANS[11]=SYS01_DEV
0600 REM " --- Additional File Opens "
0610 LET WOREQ_DEV=WOE22_DEV
0700 REM " --- Background"
0710 CALL "SYC.NB","Updating",7,COLUMN
0800 REM " --- Disallow 'M'enu option in error routine
0810 LET EXIT_CTRL=1
0900 REM " --- Init Read"
0910 READ (WOREQ_DEV,KEY=N0$+"  "+R0$,DOM=1000)
1000 REM " --- Main Read"
1010 LET K$=KEY(WOREQ_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 IF LEN(R1$)>0 THEN IF N0$+"  "+R1$<K$(1,11) THEN GOTO 4000
1040 PRINT @(COLUMN,11),K$(5,7),
1050 READ (WOREQ_DEV,KEY=K$)IOL=WOREQA
1060 IF WOREQ_DEV=WOE22_DEV THEN GOTO PROCESS_MATERIALS
1070 IF A1$(91,1)="I" THEN GOTO 1000
1080 FIND (WOE01_DEV,KEY=K$(1,11),DOM=1000)IOL=WOE01A
1085 IF POS(B0$(15,1)=P6$)=0 THEN GOTO 1000
1090 IF B0$(14,1)="R" THEN GOTO 1000
1095 IF T0$(5,7)<>A0$(5,7) AND A1$(91,1)<>"S" THEN GOTO 1000
1100 REM " --- Breaks
1110 IF LEN(MESSAGE$)>0 AND (A1$(91,1)<>"M" OR T0$<>A0$(5,7)) THEN GOSUB 6100
1120 IF T0$(5,7)<>A0$(5,7) THEN LET T0$=A0$,PROCESS$="N"
1200 REM " --- Process Subcontracts"
1210 IF A1$(91,1)="M" AND PROCESS$="N" THEN GOTO 1000
1220 IF A1$(91,1)="S" THEN LET T1$=A1$
1230 IF A1$(91,1)="S" AND NUM(A1$(43,7))>0 THEN LET PROCESS$="N"; GOTO 1000
1240 IF A1$(56,1)="C" THEN LET PROCESS$="N"; GOTO 1000
1250 LET PROCESS$="Y",LAST_TYPE$=A1$(91,1)
1260 IF A1$(91,1)="M" THEN LET MESSAGE$=MESSAGE$+FNP$(A1$(92,60)); GOTO 1000
1270 LET DATE_REQD$=FNYY_YY21$(FNM$(FNC$(A1$(4,3))))
1300 REM " --- Process Non-Stock Items "
1310 DIM INFO$[15],INFO[2]
1320 LET INFO$[0]=N0$,INFO$[1]=A1$(7,6),INFO$[2]="N",INFO$[3]=B0$(54,2)
1330 LET INFO$[4]=F0$(18,6),INFO$[5]=DATE_REQD$,INFO$[6]=A1$(2,2),INFO$[7]="W"
1340 LET INFO$[9]=A0$(5,7)+A0$(13,3),INFO$[10]=B0$(34,6),INFO$[14]=A1$(13,30)
1350 LET INFO[1]=A[4],INFO[2]=A[2]
1360 GOSUB 6000
1370 LET A1$(43,10)=DETAIL_KEY$(9,10)
1380 LET A1$(56,1)="R"
1390 WRITE (WOREQ_DEV,KEY=K$)IOL=WOREQA
1400 GOTO 1000
1410 LET VENDOR$=D1$(22,6)
1500 REM " --- Process Special Order Material Requirements "
1510 PROCESS_MATERIALS: 
1515 DIM INFO$[15],INFO[2]
1516 IF A1$(50,10)<>"          " THEN GOTO 1800
1520 FIND (IVM02_DEV,KEY=N0$+A1$(25,22),DOM=1800)IOL=IVM02A
1530 IF D1$(60,1)<>"Y" THEN GOTO 1800
1535 LET VENDOR$=D1$(22,6),COST=D[11]
1540 FIND (IVM01_DEV,KEY=N0$+A1$(27,20),DOM=1800)IOL=IVM01A
1550 IF C2$(24,1)="I" THEN LET VENDOR$=C2$(12,6)
1560 IF VENDOR$="      " THEN GOTO 1800
1570 IF A[2]=0 THEN GOTO 1800
1580 LET INFO$[0]=N0$,INFO$[1]=VENDOR$,INFO$[2]="S",INFO$[3]=A1$(25,2),INFO$[4]=F0$(18,6)
1590 LET INFO$[5]=FNYY_YY21$(FNM$(FNC$(A1$(4,3)))),INFO$[6]=A1$(1,2),INFO$[7]="W"
1600 LET INFO$[9]=A0$(5,7)+A0$(13,3),INFO$[13]=A1$(27,20)
1610 LET INFO[0]=C[0],INFO[2]=A[2]
1650 REM " --- Use Vendor's Item Discount Pricing When Available
1660 FIND (IVM05_DEV,KEY=INFO$[0]+INFO$[1]+INFO$[13],DOM=1700)IOL=IVM05A
1670 IF L[3]>0 AND INFO[2]>L[0] THEN LET COST=L[3]
1680 IF L[4]>0 AND INFO[2]>L[1] THEN LET COST=L[4]
1690 IF L[5]>0 AND INFO[2]>L[2] THEN LET COST=L[5]
1700 REM " --- Create PO Requisition And Log It In Work Order Detail Record
1710 LET INFO[1]=COST
1720 GOSUB 6000
1730 LET A1$(50,10)=DETAIL_KEY$(9,10)
1740 LET A1$(60,1)="R"
1750 WRITE (WOREQ_DEV,KEY=A0$)IOL=WOREQA
1800 REM " --- Loop Back For Next Work Order Detail Record
1810 GOTO 1000
4000 REM " --- End"
4010 IF LEN(MESSAGE$)>0 THEN GOSUB 6100
4020 IF WOREQ_DEV=WOE22_DEV THEN LET WOREQ_DEV=WOE32_DEV; GOTO 0900
4100 GOTO 9900
6000 REM " --- Create Requisition Record "     
6050 CALL "POC.BA",CHANS[ALL],INFO$[ALL],INFO[ALL],DETAIL_KEY$,STATUS
6090 RETURN 
6100 REM " --- Write The Message Record"
6110 DIM INFO$[15],INFO[2]
6120 LET INFO$[0]=N0$,INFO$[1]=T1$(7,6),INFO$[2]="M",INFO$[3]=B0$(54,2),INFO$[7]="W"
6125 LET INFO$[4]=F0$(18,6),INFO$[5]=DATE_REQD$
6130 LET INFO$[9]=T0$(5,7)+T0$(13,3),INFO$[14]=MESSAGE$,INFO$[8]="W"
6150 GOSUB 6000
6180 LET MESSAGE$=""
6190 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8050 DEF FNP$(Q$)=CVS(Q$,2)
8060 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "GLM - Financial Report Maintenance (Heading)"
0020 REM "Program GLM.DB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.6 - 15Aug99 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "27Jan99 WGH: Clarify Accounting Period prompt (3210)
0032 REM "21Oct99 WGH: Don't display GLM-32 keys when deleting a report (4445)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0210 GLM12A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1)
0220 GLM32A: IOLIST C0$(1),C1$(1)
0500 REM " --- Initializations"
0520 DIM C0$(7),C1$(7)
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,8,0
0720 PRINT 'SB',@(3,6),"Heading No:",@(22),"Dollar Mask:",@(53),"Ratio Mask:",@(24,7),"Unit Mask:",@(51),"Percent Mask:",@(0,9),"Column",@(12),"Acctg",@(27),"Actual/",@(43),"Unit",@(57),"Ratio/",@(72),"Default",@(0,10),"Number",@(12),"Period",@(27),"Budget",@(43),"Type",@(56),"Percent",@(72),"Position",
0730 FOR X=1 TO 8
0740 PRINT @(2,X+10),X:"#.",
0750 NEXT X
0760 PRINT 'SF',
0800 REM " --- Options"
0820 ON O0 GOTO 1200,1200,2000
1200 REM " --- Display"
1220 GOSUB 5000
1290 GOTO 4000
2000 REM " --- Description"
2010 LET V0$="S",V1$="C",V2$=A0$(9,20),V3$="",V0=20,V1=18,V2=L0-2
2020 LET V4$="Enter the description for this report",I0=0
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,2000,2000,3900
2060 LET A0$(9,20)=V$
2090 GOTO 3900
2100 REM " --- Heading No"
2110 LET V0$="Z",V1$="C",V2$=A0$(29,2),V3$="",V0=2,V1=15,V2=6
2120 LET V4$="Enter a valid heading number"
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2160 LET A0$(29,2)=V$
2190 GOTO 3900
2200 REM " --- Dollar Mask"
2210 LET V4$="Enter the mask to be used for amounts"
2220 LET V0$="S",V1$="C",V2$=A1$,V3$="",V0=15,V1=35,V2=6
2230 GOSUB 7000
2235 ON V3 GOTO 2240,2200,3900,2200,3900
2250 IF V$="" THEN GOTO 2200
2260 LET A1$=V$
2290 GOTO 3900
2300 REM " --- Unit Mask"
2310 LET V4$="Enter the mask to be used for units"
2320 LET V0$="S",V1$="C",V2$=A2$,V3$="",V0=12,V1=35,V2=7
2330 GOSUB 7000
2335 ON V3 GOTO 2340,2300,3900,2300,3900
2350 IF V$="" THEN GOTO 2300
2360 LET A2$=V$
2390 GOTO 3900
2400 REM " --- Ratio Mask"
2410 LET V4$="Enter the mask to be used for ratios"
2420 LET V0$="S",V1$="C",V2$=A3$,V3$="",V0=10,V1=65,V2=6
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 IF V$="" THEN GOTO 2400
2460 LET A3$=V$
2490 GOTO 3900
2500 REM " --- % Mask"
2510 LET V4$="Enter the mask to be used for percentages"
2520 LET V0$="S",V1$="C",V2$=A4$,V3$="",V0=10,V1=65,V2=7
2530 GOSUB 7000
2540 ON V3 GOTO 2550,2500,3900,2500,3900
2550 IF V$="" THEN GOTO 2500
2560 LET A4$=V$
2690 GOTO 3900
3000 REM " --- Columns"
3100 FOR X=0 TO 7
3200 REM " --- Period"
3210 PRINT @(0,21),'CL','LD','LD',@(3,21),"P=Current Period      Q=Current Quarter-To-Date      Y=Current Year-To-Date",@(3,22),"A=Year Ago Period     B=Year Ago Quarter-To-Date     C=Year Ago Year-To-Date",
3220 LET V0$="S",V1$="",V2$=PERIOD$[X],V3$=" PQYABC",V0=1,V1=15,V2=11+X
3230 GOSUB 7000
3235 ON V3 GOTO 3240,3200,3240,3200,3240
3240 LET PERIOD$[X](1)=V$
3245 PRINT @(0,21),'LD','LD',
3250 IF V3=4 THEN GOTO 4000
3260 IF V3<>2 THEN GOTO 3300
3270 IF X=0 THEN EXITTO 3900
3280 LET X=X-1
3290 GOTO 3200
3300 REM " --- Actual/Budget"
3310 LET V1$="C",V4$="A=Actual  B=Budget  V=Variance"
3320 LET V0$="S",V2$=ACT_BUDG$[X],V3$=" ABV",V0=1,V1=30,V2=11+X
3330 GOSUB 7000
3335 IF V3=1 OR V3=3 THEN GOTO 3300
3340 LET ACT_BUDG$[X](1)=V$
3350 IF V$=" " THEN IF PERIOD$[X]<>" " THEN GOTO 3300
3360 PRINT @(0,21),'CL',@(0,22),'CL',
3370 IF V3=2 THEN GOTO 3200
3380 IF V3=4 THEN GOTO 4000
3400 REM " --- Unit"
3402 PRINT @(0,21),'CL',@(0,22),'CL',
3410 LET V4$="A=Amount  U=Units"
3420 LET V0$="S",V1$="C",V2$=AMT_UNIT$[X],V3$=" AU",V0=1,V1=45,V2=11+X
3430 GOSUB 7000
3435 IF V3=1 OR V3=3 THEN GOTO 3400
3440 LET AMT_UNIT$[X](1)=V$
3450 PRINT @(0,22),'CL',
3460 IF V3=2 THEN GOTO 3300
3465 IF V3=4 THEN GOTO 4000
3470 IF V$=" " THEN IF PERIOD$[X]<>" " THEN GOTO 3300
3500 REM " --- Ratio/Percent"
3510 LET V4$="R=Ratio   P=Percent   [ ]=Dollars   $=$ Var Prev 2 Cols   %=% Var Prev 2 Cols"
3520 LET V0$="S",V1$="C",V2$=RATIO_PER$[X],V3$=" RP$%",V0=1,V1=60,V2=11+X
3530 GOSUB 7000
3535 IF V3=1 OR V3=3 THEN GOTO 3500
3540 LET RATIO_PER$[X](1)=V$
3560 PRINT @(0,22),'CL',@(0,21),'CL',
3570 IF V3=2 THEN GOTO 3400
3580 IF V3=4 THEN GOTO 4000
3600 REM " --- Default Pos"
3610 PRINT @(0,21),'CL',
3620 LET V0$="Z",V1$="C",V2$=DEFAULT_POS$[X],V3$="",V4$="Print Position 0 To 131 (Note: The number 31 may not be used)",V0=3,V1=75,V2=11+X
3640 GOSUB 7000
3645 ON V3 GOTO 3650,3600,3900,3600,3900
3650 IF V<0 OR V>131 THEN GOTO 3600
3652 IF V<>31 THEN GOTO 3660
3653 PRINT 'RB',
3654 LET V0$="S",V1$="C",V2$="",V3$="",V4$="The Number 31 May Not Be Used (<Enter>=Continue): ",V0=1,V1=FNV(V4$),V2=22
3655 GOSUB 7000
3656 GOTO 3600
3660 LET DEFAULT_POS$[X](1)=STR(V:"000")
3670 IF V3=2 THEN GOTO 3500
3675 IF V3=4 THEN GOTO 4000
3680 IF V$=" " THEN IF PERIOD$[X]<>" " THEN GOTO 3300
3690 NEXT X
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3950 LET V3=0
3960 ON I0 GOTO 2000,2100,2200,2300,2400,2500,3000,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CR",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 WRITE (GLM12_DEV,KEY=A0$(1,K9))IOL=GLM12A
4130 GOSUB WRITE_GLM32
4180 LET O0=1
4190 RUN "GLM.DA"
4200 REM " --- Delete"
4202 PRINT 'RB',
4204 LET V0$="S",V1$="KC",V2$="NO",V3$="",V4$="Are You Sure You Want To Delete This Report (Yes/No)? ",V0=3,V1=FNV(V4$),V2=22
4206 GOSUB 7000
4207 ON V3 GOTO 4210,4200,4200,4200,4000
4210 IF V$="NO" THEN GOTO 4000
4215 IF V$<>"YES" THEN GOTO 4200
4217 REM " --- GLM-12
4220 READ (GLM12_DEV,KEY=A0$(1,4),DOM=4230)
4230 LET K$=KEY(GLM12_DEV,END=4300)
4240 IF K$(1,4)<>A0$(1,4) THEN GOTO 4300
4250 REMOVE (GLM12_DEV,KEY=K$)
4260 GOTO 4230
4300 REM " --- GLM-22
4320 READ (GLM22_DEV,KEY=A0$(1,4),DOM=4330)
4330 LET K$=KEY(GLM22_DEV,END=4400)
4340 IF K$(1,4)<>A0$(1,4) THEN GOTO 4400
4350 REMOVE (GLM22_DEV,KEY=K$)
4360 GOTO 4330
4400 REM " --- GLM-32
4420 READ (GLM32_DEV,KEY=A0$(1,4),DOM=4430)
4430 LET K$=KEY(GLM32_DEV,END=4500)
4440 IF K$(1,4)<>A0$(1,4) THEN GOTO 4500
4450 REMOVE (GLM32_DEV,KEY=K$)
4460 GOTO 4430
4500 REM " --- GLE-06
4520 READ (GLE06_DEV,KEY=A0$(1,4),DOM=4530)
4530 LET K$=KEY(GLE06_DEV,END=4600)
4540 IF K$(1,4)<>A0$(1,4) THEN GOTO 4600
4550 REMOVE (GLE06_DEV,KEY=K$)
4560 GOTO 4530
4880 LET O0=2
4900 REM " --- Return to submenu"
4990 RUN "GLM.DA"
5000 REM " --- Display"
5020 PRINT @(15,6),A0$(29,2),@(35,6),A1$,@(65,6),A3$,@(35,7),A2$,@(65,7),A4$,
5040 FOR X=0 TO 7
5050 PRINT @(15,X+11),PERIOD$[X],@(30),ACT_BUDG$[X],@(45),AMT_UNIT$[X],@(60),RATIO_PER$[X],
5060 IF PERIOD$[X]<>" " THEN PRINT @(75),DEFAULT_POS$[X],
5070 NEXT X
5090 RETURN
6100 REM " --- Assign temps to GLM-32 detail vars and write
6110 WRITE_GLM32: 
6130 FOR CNT=0 TO 7
6140 LET C0$(1)=A0$(1,4)+STR(CNT:"000")
6150 LET C1$(1)=PERIOD$[CNT]+ACT_BUDG$[CNT]+AMT_UNIT$[CNT]+RATIO_PER$[CNT]+DEFAULT_POS$[CNT]
6160 WRITE (GLM32_DEV,KEY=C0$)IOL=GLM32A
6170 NEXT CNT
6195 RETURN
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- <F4>/END Option For Locked Record"
9210 LET O0=1
9220 IF ERR=0 THEN RUN "GLM.DA"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
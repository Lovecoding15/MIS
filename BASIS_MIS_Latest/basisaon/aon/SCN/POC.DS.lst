0010 REM "POC - Dropship Line Item Maintenance Entry"
0020 REM "Program POC.DS"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "07Jun96 JAL: Use correct window title/prompts for special order items (68,405-420,510,565,1210,1410,4930-4950)"
0040 REM 
0042 REM "TYPE$      : Record type (input)"
0044 REM "           : R=Requisition/P=Purchase Order"
0046 REM "           : Q=QA Receiver/C=PO Receiver"
0048 REM "ACTION$    : Action code (input)"
0050 REM "           : D=Display only"
0052 REM "CHANNELS[n]: Channel numbers (input)"
0054 REM "           : [1]=ARM-01   [3]=ARE-03   [5]=ARE-33"
0056 REM "           : [2]=ARM-03   [4]=ARE-13"
0058 REM "PARAMS[n]  : Numeric parameters (input)"
0060 REM "           : [0]=Customer number length"
0062 REM "PARAMS$[n] : String parameters (input)"
0064 REM "           : [0]=Firm ID                [3]=AR installed (Y/N)?"
0066 REM "           : [1]=Customer Mask          [4]=OP installed (Y/N)?"
0068 REM "           : [2]=Line item 3rd string   [5]=Line code 2nd string"
0070 REM "LINE       : Current line item screen row (input)"
0072 REM "STATUS     : 0=No error (returned)"
0075 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER TYPE$,ACTION$,CHANNELS[ALL],PARAMS[ALL],PARAMS$[ALL],LINE,STATUS
0100 REM " --- Open/Lock Files"
0110 LET ARM01_DEV=CHANNELS[1],ARM03_DEV=CHANNELS[2],ARE03_DEV=CHANNELS[3]
0120 LET ARE13_DEV=CHANNELS[4],ARE33_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 ARM01A: IOLIST A0$(1),A1$(1)
0220 ARM03A: IOLIST B0$(1),B1$(1)
0230 ARE03A: IOLIST C0$(1)
0240 ARE33A: IOLIST D0$(1),D1$(1)
0400 REM " --- Parameters"
0405 DIM Y1$(32)
0410 LET N0$=PARAMS$[0],M0$=PARAMS$[1],W2$=PARAMS$[2],AR$=PARAMS$[3]
0420 LET OP$=PARAMS$[4],Y1$(1)=PARAMS$[5],CUSTLEN=PARAMS[0]
0500 REM " --- Initializations"
0510 DIM A0$(8),A1$(282),B0$(14),B1$(175),C0$(118),MESSAGE$[2]
0520 DIM D0$(15),D1$(130),W0$(20)
0550 LET M0=LEN(M0$),WIDTH=80,HEIGHT=10,WIN_X=0,WIN_Y=LINE-HEIGHT,STATUS=0
0560 LET TITLE$="Drop Shipment Information",O=12+M0,S=O+20,ORDER$="Order:"
0565 IF Y1$(22,1)<>"Y" THEN LET TITLE$="Customer/Sales Order Information"
0570 IF OP$="N" THEN LET ORDER$="",S=36
0580 IF S<36 THEN LET S=36
0600 REM " --- AR and/or OP required for drop shipments"
0610 IF AR$="Y" THEN GOTO 0700
0620 LET STATUS=11
0630 LET MESSAGE$[0]="Accounts Receivable Or Order Processing Is Required In"
0640 LET MESSAGE$[1]="Order To Use Drop Shipments (<Enter>=Continue)"
0650 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
0690 GOTO 9900
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT 'SB',@(1,0),"Customer:",@(O),ORDER$,@(S),"Ship To:",@(5,1),"Name:",@(2,2),"Address:",'SF',
0800 REM " --- Options"
0810 IF POS("D"=ACTION$) THEN GOTO 0900
0820 IF FNP$(W2$(11,6))="" THEN GOTO 1000
0900 REM " --- Existing record"
0910 GOSUB 5000
0990 GOTO 4000
1000 REM " --- Customer"
1010 PRINT 'CF',
1020 LET V4$="Enter A Valid Customer Number (<F3>=Lookup)",I0=0
1030 LET V0$="M",V1$="CKE",V2$=W2$(11,CUSTLEN),V3$=M0$,V0=6,V1=11,V2=0
1040 GOSUB 7000
1050 ON V3 GOTO 1100,1000,1000,1060,3900
1060 CALL "SYC.LC",V$
1100 REM " --- Retrieve customer"
1110 GOSUB 5200
1120 IF ERROR THEN GOTO 1000
1130 LET W2$(11,6)=A0$(3)
1190 GOTO 3900
1200 REM " --- Order Number"
1210 LET V4$="Sales Order For This PO Line Item (<F3>=Lookup)"
1220 LET V0$="Z",V1$="C",V2$=W2$(17,7),V3$="",V0=7,V1=O+7,V2=0
1230 GOSUB 7000
1240 ON V3 GOTO 1260,1200,3900,1250,3900
1250 LET V$=W2$(11,6)
1255 CALL "SYC.LO",V$,"O"
1260 LET C0$(1)=N0$+"  "+W2$(11,6)+V$+"000"
1265 FIND (ARE03_DEV,KEY=C0$(1,20),DOM=1200)IOL=ARE03A
1270 LET W2$(17,7)=V$,W2$(27,6)=C0$(102,6),V$=W2$(27,6)
1275 PRINT @(V1,V2),W2$(17,7),
1280 GOSUB 5400
1290 GOTO 3900
1400 REM " --- Order Line Item"
1410 LET V4$="Sales Order Line Item For This PO Line Item"
1420 LET V0$="Z",V1$="C",V2$=W2$(24,3),V3$="",V0=3,V1=O+15,V2=0
1430 GOSUB 7000
1440 ON V3 GOTO 1450,1400,3900,1400,3900
1450 LET W0$(1)=N0$+"  "+W2$(11,13)+V$
1460 FIND (ARE13_DEV,KEY=W0$,DOM=1400)
1470 LET W2$(24,3)=V$
1490 GOTO 3900
1600 REM " --- Ship-to Number"
1605 IF OP$="Y" THEN GOTO 3900
1610 LET V4$="Ship-To Number For This Customer (<F1>=None/<F3>=Lookup)"
1620 LET V0$="Z",V1$="C",V2$=W2$(27,6),V3$="",V0=6,V1=S+9,V2=0,ERROR=0
1630 GOSUB 7000
1635 ON V3 GOTO 1650,1800,3900,1640,3900
1640 LET X$=W2$(11,6)
1645 CALL "SYC.LT","S",X$,V$
1650 IF V$="" THEN GOTO 1800
1660 GOSUB 5400
1670 IF ERROR THEN GOTO 1600
1680 LET W2$(27,6)=V$
1690 GOTO 3900
1800 REM " --- No ship-to"
1810 LET V$="",W2$(27,6)=V$
1820 GOSUB 5400
1890 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?",V0$="Y",V2$="Y"
4020 IF POS("D"=ACTION$) THEN LET V4$="<Enter>=Continue",V0$="S",V2$=""
4030 LET V1$="CE",V3$="",V0=1,V2=HEIGHT-3,V1=FNV1(V4$,WIDTH)
4040 GOSUB 7000
4050 IF POS("D"=ACTION$) THEN GOTO 4900
4060 ON V3 GOTO 4090,4000,4000,4000,4100
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Customer number is required"
4110 LET STATUS=11
4120 IF FNP$(W2$(11,6))="" THEN GOTO 4900
4130 LET A0$(1)=N0$+W2$(11,6)
4140 FIND (ARM01_DEV,KEY=A0$,DOM=4900)
4150 LET STATUS=0
4200 REM " --- Order/Line number are required if OP is installed"
4210 IF OP$="N" THEN GOTO 4900
4220 LET W0$(1)=N0$+"  "+W2$(11,16),STATUS=11
4230 FIND (ARE13_DEV,KEY=W0$,DOM=4900)
4240 LET STATUS=0
4900 REM " --- Return updated record"
4910 LET PARAMS$[2]=W2$
4920 IF STATUS=0 THEN GOTO 4990
4930 LET MESSAGE$[0]="A Valid Customer And Order/Line Number (If Order Processing"
4940 LET MESSAGE$[1]="Is Installed) Should Be Entered For Special Order Items And"
4945 LET MESSAGE$[2]="Drop Shipments (<Enter>=Continue)"
4950 CALL "SYC.XA",2,MESSAGE$[ALL],2,22,-1,V$,V3
4990 GOTO 9900
5000 REM " --- Display ribbon information"
5010 LET V$=W2$(11,6)
5020 GOSUB 5200
5030 IF OP$="Y" THEN PRINT @(O+7,0),W2$(17,7)," ",W2$(24,3),
5040 LET V$=W2$(27,6)
5050 GOSUB 5400
5090 RETURN 
5200 REM " --- Retrieve and display customer"
5210 LET A0$(1)=N0$+V$,ERROR=11
5220 FIND (ARM01_DEV,KEY=A0$,DOM=5290)IOL=ARM01A
5230 LET X$=A1$(31,72)+A1$(179,48)+A1$(103,9),ERROR=0
5240 CALL "SYC.AA",X$,24,5,9,30
5250 PRINT @(11,0),FNF$(A0$(3,CUSTLEN),M0$),@(11,1),A1$(1,30),@(11,2),X$(1,30),@(11,3),X$(31,30),@(11,4),X$(61,30),@(11,5),X$(91,30),@(11,6),X$(121,30),
5290 RETURN 
5400 REM " --- Retrieve and display ship-to"
5410 IF OP$="Y" THEN IF NUM(V$,ERR=5420)=99 THEN GOTO 5500
5420 IF FNP$(V$)="" THEN GOTO 5480
5430 LET B0$(1)=N0$+W2$(11,6)+V$,ERROR=11
5440 FIND (ARM03_DEV,KEY=B0$,DOM=5590)IOL=ARM03A
5450 LET X$=B1$(31,72)+FILL(48)+B1$(103,9)
5460 CALL "SYC.AA",X$,24,5,9,30
5470 PRINT @(S+9,0),V$,'CL',@(47,1),B1$(1,30),@(47,2),X$(1,30),@(47,3),X$(31,30),@(47,4),X$(61,30),@(47,5),X$(91,30),@(47,6),X$(121,30),
5475 GOTO 5490
5480 PRINT @(S+9,0),"SAME",'CL',@(47,1),'CL',@(47,2),'CL',@(47,3),'CL',@(47,4),'CL',@(47,5),'CL',@(47,6),'CL',
5490 GOTO 5590
5500 REM " --- Manual Ship-to"
5510 LET D0$(1)=N0$+W2$(11,13),D1$(1)=""
5520 FIND (ARE33_DEV,KEY=D0$,DOM=5530)IOL=ARE33A
5530 LET X$=D1$(31,72)+FILL(48)+D1$(103,9)
5540 CALL "SYC.AA",X$,24,5,9,30
5550 PRINT @(S+9,0),V$,'CL',@(47,1),D1$(1,30),@(47,2),X$(1,30),@(47,3),X$(31,30),@(47,4),X$(61,30),@(47,5),X$(91,30),@(47,6),X$(121,30),
5590 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV1(Q$,Q)=FNC(Q$,Q)+LEN(Q$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 LET STATUS=999
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9950 EXIT 
9999 END

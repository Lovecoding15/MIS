0010 REM "MPR - Pegging Report (Input Overlay)"
0020 REM "Program MPR.FA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "31Dec98 WGH: Use Inventory ABC Analysis Factors, and current Period and Year (310,405,422,570)
0040 REM 
0080 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=12
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="MPM-01",FILES$[2]="APM-01",FILES$[3]="BMM-01"
0120 LET FILES$[4]="IVM-01",FILES$[5]="SYS-01",FILES$[6]="IVM-02"
0125 LET FILES$[7]="IVM-05",FILES$[8]="IVM-10",FILES$[9]="IVM-03"
0130 LET FILES$[10]="IVM-12"
0150 CALL "SYC.DA",1,1,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET MPM01_DEV=CHANNELS[1],APM01_DEV=CHANNELS[2],BMM01_DEV=CHANNELS[3]
0165 LET IVM01_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5],IVM02_DEV=CHANNELS[6]
0170 LET IVM05_DEV=CHANNELS[7],IVM10_DEV=CHANNELS[8],IVM03_DEV=CHANNELS[9]
0175 LET IVM12_DEV=CHANNELS[10]
0200 REM " --- IOLIST's"
0210 GLS01A: IOLIST G0$,G1$,G2$
0250 IVM01A: IOLIST D0$(1),D1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,X$,M1$,M2$,M3$
0275 APS01A: IOLIST X$,X$,P2$,X$,X$,VM0$
0280 ARS01A: IOLIST X$,X$,P2$,X$,X$,CM0$
0290 WOS01A: IOLIST X$,X$,P2$
0300 IVM10C: IOLIST X0$(1)
0310 IVS01C: IOLIST *,ABCP[ALL],FACTORS[ALL]
0360 MPM01A: IOLIST S0$(1),S1$(1),S2$,S[ALL]
0400 REM " --- Parameters"
0405 DIM P[4],INFO$[20],ABCP[25],FACTORS[25]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N3$=F5$,TODAY$=F0$(18,6)
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0422 FIND (SYS01_DEV,KEY=N0$+N2$+"02",DOM=9800)IOL=IVS01C
0425 LET P[0]=NUM(I2$(1,2)),IVS01_PRECISION=NUM(I2$(5,1)),NUMBER=1
0430 LET P[2]=NUM(I2$(9,2)),P[3]=NUM(I2$(11,2)),P[4]=NUM(I2$(13,2))
0435 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0440 LET AP=NUM(P2$(1,2)),AR$="N",W0$="N",AR=0
0445 CALL "SYC.VA","AR",INFO$[ALL]
0450 LET AR$=INFO$[20]
0455 IF AR$<>"Y" THEN GOTO 0470
0460 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0465 LET AR=NUM(P2$(1,2))
0470 CALL "SYC.VA","WO",INFO$[ALL]
0475 LET WO$=INFO$[20]
0480 IF WO$<>"Y" THEN GOTO 0495
0485 FIND (SYS01_DEV,KEY=N0$+"WO00",DOM=9800)IOL=WOS01A
0490 LET WO$=P2$(2,1)
0495 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0500 REM " --- Initializations"
0505 PRECISION IVS01_PRECISION
0510 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
0515 DIM FORECAST$(3),WHSE$(2),BEGVEND$(6),ENDVEND$(6)
0520 DIM BEGITEM$(20),ENDITEM$(20),J0$(20,"0")
0525 LET OPTION$="",SELECTION$=""
0530 LET WHSE$(1)=I3$(6,2),BEGPER=0,ENDPER=0,MP=26,INCLUDE$="Y"
0535 LET M5$="###0-",M5=LEN(M5$),M3=LEN(M3$)
0540 LET VM0=LEN(VM0$),CM0=LEN(CM0$),H1$=N1$,H2$=N3$
0545 CALL "SYC.RM",M2$,M2,10,15
0550 LET HEADINGS=4
0555 DIM HEADINGS$[HEADINGS]
0560 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,L9=59,L=L9+1
0565 LET CLOCK$="",WHEN$=F0$(7,3),PAGE=0,WIDTH=132
0570 LET MAX_PERIOD$=G2$(1,2),CURR_PERIOD$=I4$(1,2),CURR_YEAR$=I4$(3,2)
0600 REM " --- Additional File Opens"
0605 IF AR$="Y" THEN LET FILES$[11]="ARM-01"
0610 IF WO$="Y" THEN LET FILES$[12]="WOE-01"
0615 CALL "SYC.DA",1,11,12,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS THEN GOTO 9900
0625 IF CHANNELS[11] THEN LET ARM01_DEV=CHANNELS[11]
0630 IF CHANNELS[12] THEN LET WOE01_DEV=CHANNELS[12]
0635 REM "Set message box for Tran Type
0640 DIM MESSAGE$[8](45)
0645 LET MESS$="Select Transactions to Include",MESSAGE$[0]=MESS$
0650 LET MESSAGE$[2](1)="A=On Hand Balance",MESSAGE$[2](27)="F=Committed to Sales Order"
0655 LET MESSAGE$[3](1)="B=On Purchase Order",MESSAGE$[3](27)="G=Committed to Work Order"
0660 LET MESSAGE$[4](1)="C=On Work Order",MESSAGE$[4](27)="H=Planned Work Order"
0665 LET MESSAGE$[5](1)="D=Forecasted Demand",MESSAGE$[5](27)="I=Planned Purchase Order"
0670 LET MESSAGE$[6](1)="E=Dependent Demand"
0675 LET MESS$="Enter Any Combination of Items, <F1>=All"
0680 LET MESSAGE$[8]=MESS$
0700 REM " --- Background"
0710 FOR X=4 TO 20 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(25,3),"Forecast Type:",@(24,5),"Warehouse Code:",@(28,7),"Item Types:",@(24,9),"Beginning Item:",@(27,11),"Ending Item:",@(21,13),"Transaction Types:",@(13,15),"Include Alternate Vendors:",@(27,17),"From Period:",@(24,19),"Through Period:",'SF',
0900 REM 
0910 READ (MPM01_DEV,KEY=N0$,DOM=0920)
0920 LET K$=KEY(MPM01_DEV,END=0990)
0930 IF POS(N0$=K$)<>1 THEN GOTO 0990
0940 LET FORECAST$(1)=K$(3,3)
0990 GOSUB 5000
1000 REM " --- Forecast Type"
1010 LET V0$="S",V1$="CEK",V2$=FORECAST$,V0=3,V1=40,V2=3,I0=0
1020 LET V4$="Enter A Valid Forecast Type (<F3>=Lookup)"
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1045,3900
1045 GOSUB 6000
1050 GOSUB 5200
1060 IF STATUS<>0 THEN GOTO 1000
1070 LET FORECAST$(1)=V$
1090 GOTO 3900
1200 REM " --- Warehouse"
1210 LET V0$="S",V1$="C",V2$=WHSE$,V3$="",V0=2,V1=40,V2=5
1220 LET V4$="Enter A Valid Warehouse Code (<F3>=Lookup)"
1230 GOSUB 7000
1235 ON V3 GOTO 1250,1200,3900,1240,3900
1240 GOSUB 6100
1250 GOSUB 5300
1260 IF STATUS<>0 THEN GOTO 1200
1280 LET WHSE$(1)=V$
1290 GOTO 3900
1400 REM " --- Item Type"
1410 LET V0$="S",V1$="C",V2$=OPTION$,V3$="",V0=1,V1=40,V2=7
1415 LET V4$="Select Items To Include On The Report (A=Purchased/B=Production/<F1>=Both)"
1420 GOSUB 7000
1425 ON V3 GOTO 1440,1430,3900,1400,3900
1430 LET V$=""
1435 GOTO 1480
1440 IF V$<>"" THEN IF POS(V$="AB")=0 THEN GOTO 1400
1480 GOSUB 5400
1485 LET OPTION$=V$
1490 GOTO 3900
1600 REM " --- Beginning Item"
1610 LET V0$="S",V1$="C",V2$=BEGITEM$,V3$="",V0=P[0],V1=40,V2=9
1620 LET V4$="Enter An Item Number (<F1>=First/<F3>=Lookup)"
1630 GOSUB 7000
1640 ON V3 GOTO 1670,1645,3900,1655,3900
1645 LET V$=""
1650 GOTO 1670
1655 CALL "SYC.LI",V$
1660 IF V$="" THEN GOTO 1600
1670 GOSUB 5600
1680 LET BEGITEM$(1)=V$
1690 GOTO 3900
1800 REM " --- Ending Item"
1810 LET V0$="S",V1$="C",V2$=ENDITEM$,V3$="",V0=P[0],V1=40,V2=11
1820 LET V4$="Enter An Item Number (<F1>=Last/<F3>=Lookup)"
1830 GOSUB 7000
1840 ON V3 GOTO 1870,1845,3900,1855,3900
1845 LET V$=""
1850 GOTO 1870
1855 CALL "SYC.LI",V$
1860 IF V$="" THEN GOTO 1800
1870 GOSUB 5600
1880 LET ENDITEM$(1)=V$
1890 GOTO 3900
2000 REM " --- Transaction Types"
2050 CALL "SYC.XA",0,MESSAGE$[ALL],8,8,-1,V$,V3
2055 ON V3 GOTO 2065,2060,3900,2000,3900
2060 LET V$=""
2065 IF V$="" THEN GOTO 2085
2070 FOR X=1 TO LEN(V$)
2075 IF POS(V$(X,1)="ABCDEFGHIL")=0 THEN EXITTO 2000
2080 NEXT X
2085 LET SELECTION$=V$
2087 PRINT @(40,13),'CL',SELECTION$
2088 IF SELECTION$="" THEN PRINT @(40,13),"All"
2090 GOTO 3900
2200 REM " --- Include Alternate Vendors?"
2210 LET V0$="Y",V1$="C",V2$=INCLUDE$,V3$="",V0=1,V1=40,V2=15
2220 LET V4$="Include Alternate Vendors On The Report (Y/N)?"
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 LET INCLUDE$=V$
2260 PRINT @(V1,V2),INCLUDE$,'CL',
2290 GOTO 3900
2400 REM " --- Beginning Period"
2410 LET V0$="N",V1$="C",V2$=STR(BEGPER),V3$="##",V0=2,V1=40,V2=17
2420 LET V4$="Enter The Beginning Period To Print (1 To "+STR(S[0])+"/<F1>=First)"
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2445,3900,2400,3900
2445 LET V=1
2450 IF V<1 OR V>S[0] THEN GOTO 2400
2470 LET BEGPER=V
2480 PRINT @(V1,V2),BEGPER:"#0"
2490 GOTO 3900
2600 REM " --- Ending Period"
2610 LET V0$="N",V1$="C",V2$=STR(ENDPER),V3$="##",V0=2,V1=40,V2=19
2620 LET V4$="Enter The Ending Period To Print ("+STR(BEGPER)+" To "+STR(S[0])+"/<F1>=Last)"
2630 GOSUB 7000
2640 ON V3 GOTO 2650,2645,3900,2600,3900
2645 LET V=S[0]
2650 IF V<BEGPER OR V>S[0] THEN GOTO 2600
2660 LET ENDPER=V
2670 PRINT @(V1,V2),ENDPER:"#0",
2690 GOTO 3900
3900 REM "NEXT FIELD"
3905 IF V3=4 THEN GOSUB 5000; GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,2200,2400,2600,4000
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N)?"
4020 LET V0$="Y",V1$="C",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4060,4000,4000,4000,9900
4060 ON POS(V$="YN") GOTO 4000,4070,1000
4070 GOSUB 5000
4090 RUN "MPR.FB"
5000 REM " --- Display"
5010 LET V1=40,V2=3,V$=FORECAST$
5015 GOSUB 5200
5020 LET H5$="Forecast: "+X$,V2=5,V$=WHSE$
5030 GOSUB 5300
5040 LET H6$="Warehouse: "+X$,V2=7,V$=OPTION$
5045 GOSUB 5400
5047 LET H4$=X$,V2=9,V$=FNP$(BEGITEM$)
5050 GOSUB 5600
5060 LET H7$="Beginning Item: "+FNP$(X$),V2=11,V$=FNP$(ENDITEM$)
5070 GOSUB 5600
5080 LET H8$="   Ending Item: "+FNP$(X$),V2=13,V$=SELECTION$
5140 GOSUB 5500
5150 LET V2=17
5160 PRINT @(40,15),INCLUDE$,'CL',
5170 GOSUB 5700
5180 LET P9$=X$
5190 RETURN 
5200 REM " --- Forecast"
5210 DIM S0$(5),S1$(30),S[1]
5220 LET STATUS=1,S0$(1)=N0$+V$
5230 FIND (MPM01_DEV,KEY=S0$,DOM=5250)IOL=MPM01A
5240 LET STATUS=0
5250 LET X$=S0$(3,3)+" "+FNP$(S1$)
5255 PRINT @(V1,V2),X$,'CL',
5260 IF S0$(3,3)=FORECAST$ AND BEGPER>0 THEN GOTO 5290
5270 LET BEGPER=1,ENDPER=S[0],V2=17
5280 GOSUB 5700
5290 RETURN 
5300 REM " --- Warehouse"
5310 DIM X0$(32)
5320 LET STATUS=1,X0$(1)=N0$+"C"+V$
5330 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=5350)IOL=IVM10C
5340 LET STATUS=0
5350 LET X$=X0$(4,2)+" "+FNP$(X0$(6,24))
5360 PRINT @(V1,V2),X$,'CL',
5390 RETURN 
5400 REM " --- Item Types"
5410 LET X$="All Items"
5420 IF V$="A" THEN LET X$="Purchased Items Only"
5430 IF V$="B" THEN LET X$="Production Items Only"
5440 PRINT @(V1,V2),V$,@(V1+2),X$,'CL',
5490 RETURN 
5500 REM " --- Transaction types"
5510 IF V$="" THEN GOTO 5550
5520 PRINT @(V1,V2),V$,'CL',
5540 GOTO 5590
5550 PRINT @(V1,V2),"All",'CL',
5590 RETURN 
5600 REM " --- Item"
5610 DIM D0$(22),D1$(P[2]+P[3]+P[4]),X$(79-(61))
5615 IF FNP$(V$)="" THEN GOTO 5670
5620 LET D0$(1,22)=N0$+V$
5630 FIND (IVM01_DEV,KEY=D0$,DOM=5650)IOL=IVM01A
5635 LET X$(1)=FNZ$(D1$,P[2],P[3],P[4])
5650 PRINT @(V1,V2),D0$(3,20)," ",X$,'CL',
5655 LET X$=D0$(3,20)
5660 GOTO 5690
5670 LET X$="First"
5675 IF V2=11 THEN LET X$="Last"
5680 PRINT @(V1,V2),X$,'CL',
5690 RETURN 
5700 REM " --- Beginning/Ending Period"
5710 PRINT @(V1,V2),BEGPER:"#0",'CL',@(V1,V2+2),ENDPER:"#0",'CL',
5720 IF BEGPER<>1 THEN GOTO 5780
5730 IF ENDPER<>S[0] THEN GOTO 5780
5740 LET X$="All Periods"
5750 GOTO 5790
5780 LET X$="Periods "+STR(BEGPER)+" Through "+STR(ENDPER)
5790 RETURN 
6000 REM " --- Forecast Lookup"
6010 LET RECORD$="",TITLE$="MRP Forecast Types"
6030 LET DESCRIPTION$[0]="Type",CHANNEL=MPM01_DEV,FIELD[1]=2
6040 LET POSITION[0]=3,LENGTH[0]=3,POSITION[1]=1,LENGTH[1]=30
6090 GOTO 6400
6100 REM " --- Warehouse Lookup"
6110 LET RECORD$="C",TITLE$="Warehouse Lookup"
6130 LET DESCRIPTION$[0]="ID",CHANNEL=IVM10_DEV,FIELD[1]=1
6140 LET POSITION[0]=4,LENGTH[0]=2,POSITION[1]=6,LENGTH[1]=24
6190 GOTO 6400
6400 REM " --- Call Windowed Lookup"
6410 LET FIELD[0]=1,DESCRIPTION$[1]="Description"
6440 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,ROW=V2+2,V$="SKIP"
6450 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6490 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8085 DEF FNX(Q$)=INT((45-LEN(Q1$))/2)
8090 DEF FNZ$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

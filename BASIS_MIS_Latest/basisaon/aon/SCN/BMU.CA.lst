0010 REM "BMU - BILL OF MATERIALS RESEQUENCE DETAIL LINES
0020 REM "Program:  BMU.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "16Nov96 JWK: Can't Enter Beg/End BOM, ERR=41 if F3 selected (1275,1460)
0032 REM "06Jan97 Caj: Was displaying bill description as if bill ID len were always 20 (1285,1485)
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=13
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="BMM-01",FILES$[2]="BMM-02",FILES$[3]="BMM-03"
0130 LET FILES$[4]="BMM-04",FILES$[5]="BMM-05",FILES$[6]="BMM-06"
0135 LET FILES$[7]="BMM-10",FILES$[8]="BMM-22",FILES$[9]="BMM-23"
0140 LET FILES$[10]="BMM-24",FILES$[11]="BMM-25",FILES$[12]="IVM-01"
0145 LET FILES$[13]="SYS-01"
0160 CALL "SYC.DA",1,1,13,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET BMM01_DEV=CHANNELS[1],BMM02_DEV=CHANNELS[2],BMM03_DEV=CHANNELS[3]
0180 LET BMM04_DEV=CHANNELS[4],BMM05_DEV=CHANNELS[5],BMM06_DEV=CHANNELS[6]
0185 LET BMM10_DEV=CHANNELS[7],BMM22_DEV=CHANNELS[8],BMM23_DEV=CHANNELS[9]
0190 LET BMM24_DEV=CHANNELS[10],BMM25_DEV=CHANNELS[11],IVM01_DEV=CHANNELS[12]
0195 LET SYS01_DEV=CHANNELS[13]
0200 REM " --- IOLists
0250 BMM10A: IOLIST PHASE$,R0$,R1$,R2$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 BMS01A: IOLIST *,*,M0$,*,R0,P9
0320 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0330 IVM01A: IOLIST X0$,X$(1)
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0450 FIND (SYS01_DEV,KEY=N0$+"BM00",DOM=9800)IOL=BMS01A
0460 DIM P[2],DESC[3]
0470 FOR X=1 TO 3
0480 LET DESC[X]=NUM(P2$((X-1)*2+9,2))
0490 NEXT X
0495 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P4$(1,2)),P[2]=FNYY_YEAR(P4$(3,2))
0500 REM " --- Init Data"
0520 LET L9=59,L=L9+1
0530 DIM PHASE$(4)
0540 DIM A[11],B[10],C[11],D[9]
0700 REM " --- Background"
0710 FOR X=4 TO 12 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(17,3),"Beginning Bill Number:",@(20,5),"Ending Bill Number:",@(27,7),"Detail Type:",@(25,9),"Resequence By:",'SF'
0800 REM " --- Check for interrupted resequencing"
0810 READ (BMM10_DEV,KEY=N0$,DOM=0811)
0820 LET K$=KEY(BMM10_DEV,END=0900)
0830 IF POS(N0$=K$)<>1 THEN GOTO 0900
0840 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Interrupted Resequencing Process Detected. <Enter> To Continue Process. ",V0=1,V1=FNV(V4$),V2=22
0845 GOSUB 7000
0850 READ (BMM10_DEV,KEY=K$)IOL=BMM10A
0860 GOTO 4100
0900 REM " --- Check For Initial Phase"
0910 READ (BMM22_DEV,KEY=N0$,DOM=0911)
0915 LET K$=KEY(BMM22_DEV,END=0930)
0920 IF POS(N0$=K$)<>1 THEN GOTO 0930
0925 GOSUB 6000; GOTO 0970
0930 READ (BMM23_DEV,KEY=N0$,DOM=0931)
0935 LET K$=KEY(BMM23_DEV,END=0950)
0940 IF POS(N0$=K$)<>1 THEN GOTO 0950
0945 GOSUB 6000; GOTO 0970
0950 READ (BMM25_DEV,KEY=N0$,DOM=0951)
0955 LET K$=KEY(BMM25_DEV,END=0970)
0960 IF POS(N0$=K$)<>1 THEN GOTO 0970
0965 GOSUB 6000
0970 LET V=BMM22_DEV; CALL "SYC.FB",V,N0$
0975 LET V=BMM23_DEV; CALL "SYC.FB",V,N0$
0980 LET V=BMM24_DEV; CALL "SYC.FB",V,N0$
0990 LET V=BMM25_DEV; CALL "SYC.FB",V,N0$
1000 REM " --- Options"
1005 LET I0=0
1200 REM " --- Beginning Bill Number
1220 LET V0$="S",V1$="C",V2$=R0$,V3$="",V4$="Enter Valid Bill Number, <F1>=First, <F3>=Lookup",V0=P[0],V1=40,V2=3
1240 GOSUB 7000
1245 IF V3=4 THEN GOTO 9900
1250 IF V3=2 THEN GOTO 1290
1265 IF V3=3 THEN CALL "SYC.LB",V$
1270 IF V3=1 OR V$="" THEN PRINT @(V1,V2),'CL',"First"; LET R0$=""; GOTO 1290
1275 LET V$=V$+FILL(20-LEN(V$))
1280 FIND (BMM01_DEV,KEY=N0$+V$,DOM=1200)
1282 DIM X$(60); FIND (IVM01_DEV,KEY=N0$+V$,DOM=1283)IOL=IVM01A
1283 LET X$(1)=FNITEM$(X$,DESC[1],DESC[2],DESC[3])
1285 PRINT @(V1,V2),'CL',V$(1,P[0])," ",X$(1,38-P[0])
1287 LET R0$=V$
1290 GOTO 3900
1400 REM " --- Ending Bill Number
1420 LET V0$="S",V1$="",V2$=R1$,V3$="",V4$="Enter Valid Bill Number, <F1>=Last, <F3>=Lookup",V0=P[0],V1=40,V2=5
1430 GOSUB 7000
1435 IF V3=4 THEN GOTO 9900
1440 IF V3=2 THEN GOTO 1490
1453 IF V3=3 THEN CALL "SYC.LB",V$
1455 IF V3=1 OR V$="" THEN PRINT @(V1,V2),'CL',"Last"; LET R1$=""; GOTO 1490
1460 LET V$=V$+FILL(20-LEN(V$))
1470 IF V$<R0$ THEN GOTO 1400 ELSE LET R1$=V$
1475 FIND (BMM01_DEV,KEY=N0$+V$,DOM=1400)
1480 DIM X$(60); FIND (IVM01_DEV,KEY=N0$+V$,DOM=1481)IOL=IVM01A
1482 LET X$(1)=FNITEM$(X$,DESC[1],DESC[2],DESC[3])
1485 PRINT @(V1,V2),'CL',V$(1,P[0])," ",X$(1,38-P[0])
1487 LET R1$=V$
1490 GOTO 3900
1600 REM " --- Detail Type"
1620 LET V0$="S",V1$="C",V2$=R2$,V3$="",V4$="Any Combination: M=Materials, O=Operations, S=Subcontracts, <F1>=All",V0=3,V1=40,V2=7
1630 GOSUB 7000
1635 IF V3=4 THEN GOTO 9900
1640 IF V3=2 THEN GOTO 1690
1650 IF V3=1 THEN LET V$="MOS"
1655 IF POS(" "<>V$)=0 THEN GOTO 1600
1660 FOR X=1 TO LEN(V$)
1665 IF POS(V$(X,1)="MOS ")=0 THEN EXITTO 1600
1670 NEXT X
1675 LET R2$=V$
1680 PRINT @(V1,V2),R2$,'CL',
1690 GOTO 3900
1800 REM " --- Resequence Factor"
1810 IF R3=0 THEN LET R3=5
1820 LET V0$="N",V1$="CE",V2$=STR(R3),V3$="##0",V4$="Enter Number To Resequence By",V0=3,V1=40,V2=9
1830 GOSUB 7000
1835 IF V3=4 THEN GOTO 9900
1840 IF V3=2 THEN GOTO 1890
1850 IF V<1 THEN GOTO 1800
1875 LET R3=V
1880 PRINT @(V1,V2),R3,'CL',
1890 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1400,1600,1800,4000
4000 REM " --- Overlay
4010 LET V0$="Y",V1$="C",V2$="N",V3$="",V4$="Is The Above Information Correct (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4060 IF V$="N" THEN GOTO 1000
4080 IF V$<>"Y" THEN GOTO 4000
4100 RUN "BMU.CB"
6000 REM " --- Warn about restart"
6020 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Interrupted Resequencing Process Detected. <Enter> To Reselect Options. ",V0=1,V1=FNV(V4$),V2=22
6030 GOSUB 7000
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

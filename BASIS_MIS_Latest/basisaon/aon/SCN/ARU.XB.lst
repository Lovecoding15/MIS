0010 REM "ARR - MONTHLY CASH RECEIPTS JOURNAL PURGE"
0020 REM "Program ARU.XB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "10Dec96 JWK Using wrong start key, so nothing purges (1010)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0400 REM " --- Parameters
0710 FOR X=14 TO 16 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0900 PRINT @(28,13),'SB',"Processing: ",@(31,15),"Purging: ",'SF',
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
0990 PRINT @(28,13),'SB',"Processing: ",@(31,15),"Purging: ",'SF',
1000 REM " --- Init Read
1010 LET START$=N0$+"   "+R1$+"      ",START$=START$(1,11)+R3$
1020 READ (ART06_DEV,KEY=START$,DOM=1100)
1100 REM " --- Heading Read
1120 LET A0$=KEY(ART06_DEV,END=4000)
1130 IF A0$(1,5)<>N0$+FILL(3) THEN GOTO 4000
1140 IF R2$<>"" AND A0$(6,6)>R2$ THEN GOTO 4000
1200 READ (ART06_DEV)
1230 PRINT @(40,13),FNF$(A0$(6,P[0]),M0$)," ",FNB$(A0$(12,3))," ",A0$(15,3)
1260 IF R3$<>"" AND A0$(12,3)<R3$ THEN READ (ART06_DEV,KEY=A0$(1,11)+R3$,DOM=1100)
1280 IF R4$<>"" AND A0$(12,3)>R4$ THEN GOTO 1100
1290 IF R5$<>"" AND A0$(15,3)<>R5$ THEN GOTO 1100
1500 REM " --- Print
1510 PRINT @(40,15),FNF$(A0$(6,P[0]),M0$)," ",FNB$(A0$(12,3))," ",A0$(15,3)
1600 REM " --- Remove Header
1620 REMOVE (ART06_DEV,KEY=A0$)
1700 REM " --- Remove Invoice Detail
1710 READ (ART16_DEV,KEY=A0$(1,25),DOM=1720)
1720 LET K$=KEY(ART16_DEV,END=1800)
1730 IF K$(1,25)<>A0$(1,25) THEN GOTO 1800
1740 REMOVE (ART16_DEV,KEY=K$)
1780 GOTO 1720
1800 REM " --- Remove GL Detail
1810 READ (ART26_DEV,KEY=A0$(1,25),DOM=1820)
1820 LET K$=KEY(ART26_DEV,END=1900)
1830 IF K$(1,25)<>A0$(1,25) THEN GOTO 1900
1840 REMOVE (ART26_DEV,KEY=K$)
1880 GOTO 1820
1900 REM " --- Next Header
1990 GOTO 1100
4000 REM " --- End
4020 GOTO 9900
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

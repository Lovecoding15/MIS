0010 REM "SYM - Menu Maintenance (Header)"
0020 REM "Program SYM.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "16Apr97 JAL: Processing should default to write for new records (1320,1580,4010,4020)"
0040 REM 
0050 SETERR 9000
0060 SETESC 9000
0070 ON O0 GOTO 0080,1000
0080 BEGIN 
0085 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=5
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="SYM-01",FILES$[3]="SYM-11"
0130 LET FILES$[4]="SYM-04",FILES$[5]="SYM-01"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SYM01_DEV=CHANNELS[2],SYM11_DEV=CHANNELS[3]
0170 LET SYM04_DEV=CHANNELS[4],SYM01_DEV2=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 SYM01A: IOLIST A0$(1),A1$(1),A2$(1)
0220 SYM01B: IOLIST B0$(1),B1$(1),B2$(1)
0230 SYM04A: IOLIST C0$(1),C1$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 SYM11A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1)
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0500 REM " --- Initializations"
0510 DIM A0$(6),A1$(35),A2$(16),W0$(8),W1$(35),W2$(16),W3$(30),W4$(60)
0520 DIM B0$(6),B1$(35),B2$(16),C0$(2),C1$(13),TYPES$[4](13)
0530 DIM DESCRIPTION$[1],FIELD[1],POSITION[1],LENGTH[1],OPTLIST$[17,6]
0550 LET TYPES$[1](1)="Application",TYPES$[2](1)="Lookup"
0560 LET TYPES$[3](1)="SpeedSearch",TYPES$[4](1)="Task Submenu"
0570 LET NUMBER=1,L0=8
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,5,0
0720 CALL "SYC.WC",1,0,80,0,0,7,0
0750 PRINT 'SB',@(4,3),"Menu ID:",@(58),"Type:",@(0,4),"Return Menu:",@(56),"System:",@(67),"Security:",@(0,6),"Opt Description/Parameter",@(40),"Type Program/Menu ID",@(68),"Sec Passwd",'SF',
0900 REM " --- Position File"
0990 READ (SYM01_DEV,KEY="",DOM=1000)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(SYM01_DEV,END=1030)
1030 LET V0$="S",V1$="CEK",V3$="",V0=6,V1=13,V2=3,I0=0
1040 LET V4$="Enter A Valid Menu ID (<F3>=Lookup)"
1050 GOSUB 7000
1060 ON V3 GOTO 1080,1000,1000,1070,4500
1070 GOSUB 6200
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(1)=V$
1200 REM " --- Find key"
1210 FIND (SYM01_DEV,KEY=A0$,DOM=1400)IOL=SYM01A
1300 REM " --- Display record"
1310 GOSUB 5000
1320 LET SUFFIX$="/<Enter>=Next",DEFAULT$=""
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- New record initializations"
1510 DIM A1$(35),A2$(16)
1520 LET A2$(9,1)="A",A2$(10,1)="0",C0$(1)=A0$
1530 FIND (SYM04_DEV,KEY=C0$,DOM=1550)IOL=SYM04A
1540 LET A2$(1,2)=C0$
1550 IF A0$(3,3)="C.Q" THEN LET A2$(9,1)="S"
1560 IF A0$(3,3)="C.L" THEN LET A2$(9,1)="L"
1570 IF LEN(FNP$(A0$))>3 THEN IF POS(A2$(9,1)="SL")=0 THEN LET A2$(9,1)="T"
1580 LET SUFFIX$="",DEFAULT$="Y"
1590 GOSUB 5000
2000 REM " --- Menu Description"
2010 LET V4$="Enter The Description For This Menu"
2020 LET V0$="S",V1$="C",V2$=A1$,V3$="",V0=35,V1=20,V2=3,I0=0
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 LET A1$(1)=V$
2090 GOTO 3900
2100 REM " --- Menu Type"
2110 LET V4$="A=Application (System) Menu   L=Lookup Options   S=SpeedSearch   T=Task Submenu"
2120 LET V0$="S",V1$="C",V2$=A2$(9,1),V3$="ALST",V0=1,V1=64,V2=3
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2160 LET A2$(9,1)=V$
2190 GOTO 3900
2200 REM " --- Return menu"
2210 LET V4$="Enter The Menu To Return To From This Menu (<F3>=Lookup)"
2220 LET V$="",V0$="S",V1$="C",V2$=A2$(3,6),V3$="",V0=6,V1=13,V2=4
2225 IF POS(A2$(9,1)="LT")>0 THEN GOTO 2260
2230 GOSUB 7000
2240 ON V3 GOTO 2260,2200,3900,2250,3900
2250 GOSUB 6200
2260 LET B0$(1)=V$,B1$(1)=""
2270 IF FNP$(V$)<>"" THEN FIND (SYM01_DEV2,KEY=B0$,DOM=2200)IOL=SYM01B
2275 PRINT @(V1,V2),B0$," ",B1$,
2280 LET A2$(3,6)=V$
2290 GOTO 3900
2400 REM " --- System"
2410 LET V4$="Enter A Valid System ID (<F3>=Lookup)"
2420 LET V0$="S",V1$="C",V2$=A2$(1,2),V3$="",V0=2,V1=64,V2=4
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2445,3900
2445 CALL "SYC.LM",V$
2450 LET C0$(1)=V$,C1$(1)=""
2455 FIND (SYM04_DEV,KEY=C0$,DOM=2465)IOL=SYM04A
2460 GOTO 2470
2465 IF V$<>"" OR A2$(9,1)="A" THEN GOTO 2400
2470 PRINT @(V1,V2),C0$,
2480 LET A2$(1,2)=C0$
2490 GOTO 3900
2600 REM " --- Required Security Level"
2610 LET V4$="Enter The User Security Level Necessary To Access This Menu (0 Through 9)"
2620 LET V0$="Z",V1$="C",V2$=A2$(10,1),V3$="",V0=1,V1=77,V2=4
2630 GOSUB 7000
2640 ON V3 GOTO 2650,2600,3900,2600,3900
2650 LET A2$(10,1)=V$
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,2400,2600,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Copy/Delete"+SUFFIX$+")?"
4020 LET V0$="S",V1$="C",V2$=DEFAULT$,V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4050 IF V$="COPY" THEN GOTO 4600
4060 IF V$="DELETE" THEN GOTO 4200
4070 IF V$="" THEN GOTO 1000
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write record"
4110 WRITE (SYM01_DEV,KEY=A0$)IOL=SYM01A
4190 RUN "SYM.AB"
4200 REM " --- Remove"
4210 READ (SYM11_DEV,KEY=A0$,DOM=4220)
4220 LET K$=KEY(SYM11_DEV,END=4260)
4230 IF POS(A0$=K$)<>1 THEN GOTO 4260
4240 REMOVE (SYM11_DEV,KEY=K$,DOM=4250)
4250 GOTO 4220
4260 REMOVE (SYM01_DEV,KEY=A0$,DOM=4270)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4550 ON POS(V$="YN") GOTO 4500,4590,9900
4590 RUN "SYM.AC"
4600 REM " --- Copy menu"
4610 READ (SYM01_DEV,KEY=A0$,DOM=4000)IOL=SYM01A
4620 LET V4$="Enter The Menu ID To Copy The "+$22$+FNP$(A0$)+$22$+" Menu To:"
4630 LET V0$="S",V1$="C",V2$="",V3$="",V0=6,V1=FNV(V4$),V2=22
4640 GOSUB 7000
4650 ON V3 GOTO 4660,4600,4000,4600,4000
4660 LET MENU$=A0$,A0$(1)=V$
4670 WRITE (SYM01_DEV,KEY=A0$)IOL=SYM01A
4690 READ (SYM11_DEV,KEY=MENU$,DOM=4700)
4700 REM " --- Copy detail records"
4710 LET K$=KEY(SYM11_DEV,END=4800)
4720 IF POS(MENU$=K$)<>1 THEN GOTO 4800
4730 READ (SYM11_DEV)IOL=SYM11A
4740 LET W0$(1,6)=A0$
4750 WRITE (SYM11_DEV,KEY=W0$)IOL=SYM11A
4760 READ (SYM11_DEV,KEY=K$)
4790 GOTO 4700
4800 REM " --- Re-position and redisplay"
4810 LET A0$(1)=MENU$
4890 GOTO 1200
5000 REM " --- Display record"
5010 LET B0$(1)=A2$(3),B1$(1)="",B2$(1)="",C0$(1)=A2$(1,2)
5020 FIND (SYM01_DEV2,KEY=B0$,DOM=5030)IOL=SYM01B
5030 FIND (SYM04_DEV,KEY=C0$,DOM=5040)IOL=SYM04A
5040 PRINT @(13,3),A0$," ",A1$,@(64),A2$(9,1),@(67),TYPES$[POS(A2$(9,1)="ALST")],@(13,4),B0$," ",B1$,@(64),A2$(1,2),@(77),A2$(10,1),
5090 RETURN 
6200 REM " --- Standard windowed lookup"
6210 LET RECORD$="",CHANNEL=SYM01_DEV2,TITLE$="ADD+ON System Menus"
6230 LET DESCRIPTION$[0]="Menu",FIELD[0]=1,POSITION[0]=1,LENGTH[0]=6
6240 LET DESCRIPTION$[1]="Description",FIELD[1]=2,POSITION[1]=1,LENGTH[1]=35
6250 LET ROW=L0+1,COLUMN=79-LENGTH[0]-LENGTH[1]-NUMBER*4,V$="NO COMPANY"
6280 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

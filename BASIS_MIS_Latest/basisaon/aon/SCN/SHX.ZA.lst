0010 REM "SHX - Zone Chart Installation Program"
0020 REM "Program SHX.ZA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Feb97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Feb97 CAJ: Modified for 1997 SpeedShip Update"
0032 REM "20Oct97 KJS: Use F0$(18,6) instead of DAY function (2940,8070,8120-8140)"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SHM-09"
0140 LET FILES$[3]="SHM-10",FILES$[4]="SHM-24"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1],SHM09_DEV=CHANNELS[2]
0190 LET SHM10_DEV=CHANNELS[3],SHM24_DEV=CHANNELS[4]
0200 REM " --- IOLIST's"
0220 SHM10F: IOLIST *,C1$
0225 SHM10O: IOLIST H0$(1),H1$(1)
0230 SHM10Z: IOLIST C0$,C1$,C2$,C3$,C4$,C[ALL]
0240 SHM24A: IOLIST X0$,X[ALL]
0250 SHM24B: IOLIST Z0$,Z[ALL]
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="SH",N4$=F5$,N9$="99"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)
0500 REM " --- Initializations"
0510 DIM X[3],Z[3],C[6]
0550 LET H0=80
0700 REM " --- Background"
0710 PRINT 'SB',@(34,4),"Name:",@(28,5),"Zone Chart:",@(29,6),"Warehouse:",'SF',
1000 REM " --- Options"
1005 PRINT 'CF',
1010 LET V0$="S",V1$="CE",V2$="",V3$="",V0=6,V1=40,V2=4,I0=0
1020 LET V4$="Enter The Name Of The Zone Chart To Install (<F3>=Lookup)"
1030 GOSUB 7000
1040 ON V3 GOTO 1080,1000,1000,1050,9900
1050 CALL "SHC.LK",V1,V2,"Z",1,V$
1080 IF V$="" THEN GOTO 1000
1100 REM " --- Retrieve zone file parameters"
1105 DIM C0$(9)
1110 LET C0$(1)=N0$+"Z"+V$
1115 FIND (SHM10_DEV,KEY=C0$,DOM=1000)IOL=SHM10Z
1120 PRINT @(V1,V2),C0$(4)," ",FNP$(C1$)
1125 LET C$=C4$(1,1),F2$=FNP$(C0$(4))
1130 IF LEN(F2$)<>6 THEN GOTO 1000
1170 LET F1$="_"+F2$(1,3)+F2$(5,2)
1180 LET W=C[4]+C[6]
1190 IF W>35 THEN LET W=35
1200 REM " --- Open master zone chart"
1210 CLOSE (SOURCE_DEV,ERR=1215)
1215 LET FILES$[5]=F1$,OPTIONS$[5]="R"
1220 CALL "SYC.DA",1,5,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
1225 LET C0$(4)=F1$(2),SOURCE_DEV=CHANNELS[5]
1230 FIND (SHM10_DEV,KEY=C0$,DOM=1250)IOL=SHM10Z
1240 IF STATUS=0 THEN GOTO 1300
1250 LET V4$="Unable To Open Master Zone Chart "+$22$+F1$+$22$
1290 GOTO 1360
1300 REM " --- Open destination zone chart"
1310 CLOSE (TARGET_DEV,ERR=1320)
1320 LET FILES$[6]=F2$
1330 CALL "SYC.DA",1,6,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
1335 LET TARGET_DEV=CHANNELS[6]
1340 IF STATUS=0 THEN GOTO 1400
1350 LET V4$="Unable To Open Zone Chart "+$22$+F2$+$22$
1360 GOSUB 6900
1390 GOTO 1000
1400 REM " --- Which Zone Chart?"
1410 PRINT @(2,22),"NOTE: If the correct chart is not listed above, contact your SpeedShip Dealer",
1420 LET P=0,X$="Zone Charts"
1430 CALL "SHC.LZ",F1$,C$,P,X$,Z$,N0$
1440 IF Z$="" THEN GOTO 1000
1450 PRINT @(40,5),Z$(4),
1500 REM " --- Warehouse ID"
1505 LET V0$="S",V1$="CR",V2$=P8$,V3$="",V0=2,V1=40,V2=6
1510 LET V4$="Enter A Valid Warehouse ID (<F3>=Lookup)"
1515 GOSUB 7000
1520 ON V3 GOTO 1550,1500,1400,1525,9900
1525 CALL "SHC.LK",V1,V2,"F",0,V$
1540 IF V$="" THEN GOTO 1500
1550 REM " --- Retrieve warehouse record"
1555 IF LEN(V$)<2 THEN LET V$=V$+"  ",V$=V$(1,2)
1560 FIND (SHM10_DEV,KEY=N0$+"F"+V$,DOM=1500)IOL=SHM10F
1565 LET W$=V$
1570 PRINT @(V1,V2),W$," ",C1$(1,30),
2000 REM " --- Does a destination zone chart already exist?"
2010 READ (TARGET_DEV,KEY=N0$+W$+C$,DOM=2020)
2020 LET K$=KEY(TARGET_DEV,END=2900)
2030 READ (TARGET_DEV)
2040 IF POS(N0$+W$+C$=K$)<>1 THEN GOTO 2900
2100 REM " --- Destination zone chart exists"
2110 LET V4$="Zone Chart Exists For Warehouse "+FNP$(W$)+". Replace It"
2120 CALL "SYC.YN",0,V4$,3,V$,V3
2140 ON V3 GOTO 2150,2100,1300,2100,9900
2150 ON POS(V$="YN") GOTO 2100,2160,1300
2160 LET V4$="Clearing Warehouse "+W$+" "+FNP$(C1$(1,30))
2170 PRINT @(FNC(V4$,H0),22),V4$,
2180 CALL "SYC.NB","Clearing",W+1,COLUMN
2200 REM " --- Clear destination warehouse"
2210 REMOVE (TARGET_DEV,KEY=K$)
2220 LET K$=KEY(TARGET_DEV,END=2400)
2230 IF POS(N0$+W$+C$=K$)<>1 THEN GOTO 2400
2240 PRINT @(COLUMN,11),K$(6,W),
2290 GOTO 2200
2400 REM " --- Destination cleared"
2410 PRINT @(0,7),'CE',
2500 REM " --- Installing Early AM Zone Chart (SHM-27)?"
2510 LET EARLYAM=F2$="SHM-27"
2520 IF EARLYAM=0 THEN GOTO 2900
2540 READ (SHM24_DEV,KEY=N0$+W$+C$,DOM=2550)
2550 LET K$=KEY(SHM24_DEV,END=2600)
2560 IF POS(N0$+W$+C$=K$)=1 THEN GOTO 2900
2600 REM " --- Must install ground zones (SHM-24) before Early AM (SHM-27)"
2610 DIM MESSAGE$[1]
2620 LET MESSAGE$[0]="UPS Domestic Zone Chart (SHM-24) Must Be Installed Before"
2630 LET MESSAGE$[1]="Early AM Zone Chart (SHM-27) (<Enter>=Continue)"
2640 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
2690 GOTO 4000
2900 REM " --- Install Zone Chart Header"
2905 DIM H0$(11),H1$(32)
2910 CALL "SHC.XA",SHM09_DEV,F1$,Z$,Y$
2920 LET Y$=N9$+" "+Y$(4,16)
2940 LET H0$(1)=N0$+"O"+F2$+W$,H1$(1)=Z$(4,16)+F0$(18,6)
2950 WRITE (SHM10_DEV,KEY=H0$)IOL=SHM10O
2980 CALL "SYC.NB","Copying",W+1,COLUMN
2990 READ (SOURCE_DEV,KEY=N9$+C$+Y$(4),DOM=3000)
3000 REM " --- Copy zone chart"
3010 LET K$=KEY(SOURCE_DEV,END=4000)
3020 IF K$(4,16)<>Y$(4) THEN GOTO 4000
3030 READ (SOURCE_DEV)IOL=SHM24A
3040 PRINT @(COLUMN,11),X0$(C[3],W)
3050 IF EARLYAM=0 THEN GOTO 3100
3060 LET ZIPCODE$=K$(20,3)
3070 GOSUB 6500
3080 IF ZONE=0 THEN GOTO 3000
3090 LET X[0]=ZONE
3100 REM " --- Write new record"
3110 LET X0$=N0$+W$+C$+X0$(C[3])
3120 WRITE (TARGET_DEV,KEY=X0$)IOL=SHM24A
3190 GOTO 3000
4000 REM " --- All done"
4010 PRINT @(0,7),'CE',
4090 GOTO 0800
6500 REM " --- Position UPS ground zone chart file"
6510 LET ZONE=0
6590 READ (SHM24_DEV,KEY=N0$+W$+C$+ZIPCODE$(1,1),DOM=6600)
6600 REM " --- Lookup Early AM zip code in domestic zone chart"
6610 LET K$=KEY(SHM24_DEV,END=6690)
6615 IF POS(N0$+W$+C$=K$)<>1 THEN GOTO 6690
6620 READ (SHM24_DEV)IOL=SHM24B
6640 IF K$(6,3)>ZIPCODE$(1,3) THEN GOTO 6600
6650 IF K$(9,3)<ZIPCODE$(1,3) THEN GOTO 6600
6660 LET ZONE=Z[1]+40
6690 RETURN 
6900 REM " --- Display warning"
6910 DIM MESSAGE$[1]
6920 LET MESSAGE$[0]=V4$+" (<Enter>=Continue)"
6930 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Next Overlay"
9950 RUN "SYS.AA"
9999 END

0010 REM "PRR - Check Printing 2/Moore Form #6040A.O."
0020 REM "Program PRR.GA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Nov98 KJS: Cannot backup to date (1145)"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=14
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRE-01",FILES$[2]="PRE-01",FILES$[3]="PRE-11"
0130 LET FILES$[4]="PRE-21",FILES$[5]="PRE-31",FILES$[6]="PRM-01"
0135 LET FILES$[7]="PRM-02",FILES$[8]="PRM-10",FILES$[9]="PRT-01"
0140 LET FILES$[10]="PRT-11",FILES$[11]="PRT-21",FILES$[12]="PRT-31"
0145 LET FILES$[13]="PRW-01",FILES$[14]="SYS-01"
0160 CALL "SYC.DA",1,1,14,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET PRE01_DEV=CHANNELS[1],PRE01_DEV2=CHANNELS[2],PRE11_DEV=CHANNELS[3]
0180 LET PRE21_DEV=CHANNELS[4],PRE31_DEV=CHANNELS[5],PRM01_DEV=CHANNELS[6]
0185 LET PRM02_DEV=CHANNELS[7],PRM10_DEV=CHANNELS[8],PRT01_DEV=CHANNELS[9]
0190 LET PRT11_DEV=CHANNELS[10],PRT21_DEV=CHANNELS[11],PRT31_DEV=CHANNELS[12]
0195 LET PRW01_DEV=CHANNELS[13],SYS01_DEV=CHANNELS[14]
0200 REM " --- IOLists
0230 PRM01A: IOLIST *,X0$
0240 PRM10A: IOLIST X0$
0250 PRM10BC: IOLIST X0$
0260 PRM10H: IOLIST X0$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0320 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0330 PRS01B: IOLIST X$,P5$(1)
0400 REM " --- Parameters
0405 DIM P5$(25)
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0445 FIND (SYS01_DEV,KEY=N0$+N2$+"01",DOM=9800)IOL=PRS01B
0450 DIM P[5],G[4]
0460 FOR X=0 TO 4
0465 LET G[X]=NUM(G2$(X*2+1,2),ERR=0470)
0470 NEXT X
0480 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[3]=NUM(P3$(5,1)),P[4]=NUM(P3$(7,1)),P[5]=NUM(P3$(8,1))
0500 REM " --- Init Data
0510 LET H0=0
0520 LET L9=13,L=0
0530 DIM H9$(20,$0A$)
0540 DIM CUR_AMT[10],UNITS[10],YTD_PAY[10],CUR_DED[10],YTD_DED[10],W[2],Y[4],TOTALS[5]
0550 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$)
0555 IF M2>7 THEN LET M2$="###.00-",M2=LEN(M2$)
0560 LET P9$=F0$(7,3)
0570 LET M8$="*#######",M9$="*"+M1$,M7$="000-00-0000"
0580 LET M1$="######.00-",M9$="*"+M1$
0585 LET M1=LEN(M1$),M9=LEN(M9$)
0600 REM " --- Check Pre-Check Print Flag
0605 IF P5$(21,1)="Y" THEN GOTO 0640
0610 PRINT @(14,21),"Negative Checks Are Reported On The Pre-Check Report"
0615 LET V0$="S",V1$="CS",V2$="",V3$="",V4$="Correct And Reprint Pre-Check Report, <Enter>=Exit:",V0=1,V1=FNV(V4$),V2=22
0620 GOSUB 7000
0625 PRINT @(0,21),'CL',
0630 IF V$="OK" THEN GOTO 0640
0635 GOTO 9900
0640 LET P5$=P5$(1,3)
0645 REM " --- Additional File Opens
0650 LET GL$="N",STATUS=0
0655 CALL "GLC.BA",ERR=0660,SOURCE$,N2$,GLW11$,GL$,STATUS
0660 IF STATUS<>0 THEN GOTO 9900
0700 REM " --- Background
0720 FOR X=4 TO 12 STEP 2
0730 CALL "SYC.WC",1,0,80,0,0,X,0
0740 NEXT X
0760 PRINT 'SB',@(28,3),"Check Date:",@(26,5),"Check Number:",@(32,7),"Option:",
0770 PRINT @(23,9),"Employee Number:",
0790 PRINT 'SF',
1000 REM " --- Check Date
1010 PRINT 'CF',
1020 LET V0$="D",V1$="ER",V2$=P9$,V0=8,V1=40,V2=3,I0=0
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 9900
1053 IF GL$="Y" THEN CALL "GLC.EA",V$,"Y",PERIOD$,YEAR$,STATUS
1055 IF STATUS>99 THEN GOTO 1000
1060 LET P9$=V$
1090 GOTO 3900
1100 REM " --- Check Number
1120 LET V0$="Z",V1$="R",V2$=P8$,V0=7,V1=40,V2=5
1140 GOSUB 7000
1145 ON V3 GOTO 1146,1100,1190,1100,1190
1150 IF NUM(V$,ERR=1100)<1 THEN GOTO 1100
1160 LET P8$=V$
1190 GOTO 3900
1200 REM " --- Option
1220 LET V0$="S",V1$="CR",V2$=P7$,V3$="ARSM",V4$="A=All R=Restart S=Single M=Manual/Void Only",V0=1,V1=40,V2=7
1240 GOSUB 7000
1260 LET P7$=V$
1290 GOTO 3900
1400 REM " --- Employee
1410 IF POS(P7$="RS")=0 THEN GOTO 3900
1420 LET V0$="M",V1$="K",V2$="",V3$=P1$,V0=P[1],V1=40,V2=9
1430 GOSUB 7000
1435 IF V3=4 THEN GOTO 9900
1440 LET P6$=V$
1450 FIND (PRM01_DEV,KEY=N0$+P6$,DOM=1400)IOL=PRM01A
1460 PRINT @(41+M0,V2),X0$(1,30),
3900 REM " --- Next Field
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,1200,1400,4000
4000 REM " --- Correct?
4020 LET V0$="Y",V1$="CE",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 GOSUB 6000
4120 RUN "PRR.GB"
6000 REM " --- Create Headings
6010 LET PAYCODES$="",PAYDESCS$="",DEDCODES$="",DEDDESCS$=""
6020 PRINT @(40,11),"Formatting Heading.....",
6030 READ (PRM10_DEV,KEY=N0$+"H",DOM=6040)
6040 LET K$=KEY(PRM10_DEV,END=6150)
6050 IF K$(1,3)<>N0$+"H" THEN GOTO 6150
6060 READ (PRM10_DEV)IOL=PRM10H
6090 IF POS(X0$(4,1)="BC")<>0 THEN LET DEDCODES$=DEDCODES$+X0$(4,3),DEDDESCS$=DEDDESCS$+X0$(7,10)
6120 GOTO 6040
6150 LET DEDDESCS$=DEDDESCS$+"OTHER DED."
6180 PRINT @(40,11),'CL',
6200 READ (PRM10_DEV,KEY=N0$+"A",DOM=6220)
6220 LET K$=KEY(PRM10_DEV,END=6270)
6230 IF K$(1,3)<>N0$+"A" THEN GOTO 6270
6240 READ (PRM10_DEV)IOL=PRM10A
6250 LET PAYCODES$=PAYCODES$+X0$(4,2),PAYDESCS$=PAYDESCS$+X0$(6,12)
6260 GOTO 6220
6270 LET PAYDESCS$=PAYDESCS$+"OTHER PAY   "
6280 LET PAYCODELEN=LEN(PAYCODES$),DEDCODELEN=LEN(DEDCODES$)
6290 LET PAYDESCLEN=LEN(PAYDESCS$),DEDDESCLEN=LEN(DEDDESCS$)
6300 REM " --- X-ref
6320 READ (PRM10_DEV,KEY=N0$+"B",DOM=6340)
6340 LET K$=KEY(PRM10_DEV,END=6490)
6350 IF K$(1,3)>N0$+"C" THEN GOTO 6490
6360 READ (PRM10_DEV)IOL=PRM10BC
6370 LET U6$=U6$+X0$(3,3),U7$=U7$+X0$(22,2)
6380 GOTO 6340
6480 PRINT @(40,11),'CL',
6490 IF PAYCODELEN<>0 OR DEDCODELEN<>0 THEN GOTO 6495
6491 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Certain Payroll Codes Have Not Been Defined, <Enter>: ",V0=1,V1=FNV(V4$),V2=22
6492 PRINT @(V1,V2),'RB',
6493 GOSUB 7000
6494 EXITTO 9900
6495 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

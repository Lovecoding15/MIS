0010 REM "POU - Purge QA Receipt Rejection History
0020 REM "Program POU.HA
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "29May97 JWK: Require YES to begin purging (4050-4080)
0040 REM 
0080 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="APM-01",FILES$[2]="POT-03",FILES$[3]="SYS-01"
0155 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0165 LET APM01_DEV=CHANNELS[1],POT03_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLists
0210 APM01A: IOLIST *,X1$(1)
0300 SYS01T: IOLIST *,F0$,*,*,*,F4$,F5$
0310 APS01A: IOLIST *,VENDIMASK$,P2$,*,*,VENDOMASK$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO"
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 LET VENDLEN=NUM(P2$(1,2))
0500 REM " --- Initializations
0530 DIM VENDFNAME$(30),VENDTNAME$(30)
0580 LET TYPE$="P"; REM "Purchase Order for POC.LB
0700 REM " --- Background 
0710 FOR X=4 TO 14 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0750 PRINT 'SB',@(22,3),"Beginning Vendor: ",@(25,5),"Ending Vendor:",@(20,7),"Beginning Receiver:",@(23,9),"Ending Receiver:",@(26,11),"Beginning PO:",@(29,13),"Ending PO:",'SF',@(0,22),'CL',
0760 GOSUB REFRESH
1000 REM " --- Beginning Vendor
1010 LET V0$="M",V1$="KCE",V2$=VENDFROM$,V3$=VENDIMASK$,V4$="Enter A Valid Vendor Number (<F1>=First/<F3>=Lookup)",V0=6,V1=40,V2=3,I0=0
1020 GOSUB 7000
1030 ON V3 GOTO 1090,1040,1140,1070,4000
1040 LET VENDFROM$=""
1050 PRINT @(V1,V2),'CL',@(V1,V2),"First",
1060 GOTO 1140
1070 CALL "SYC.LV",V$
1080 IF V$="" THEN GOTO 1000
1090 IF V$="" THEN GOTO 1040
1100 DIM X1$(195)
1110 FIND (APM01_DEV,KEY=N0$+V$,DOM=1000)IOL=APM01A
1120 LET VENDFROM$=V$,VENDFNAME$(1)=X1$(1,30)
1130 PRINT @(V1,V2),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
1140 GOTO 3900
1200 REM " --- Ending Vendor
1210 LET V0$="M",V1$="KCE",V2$=VENDTHRU$,V3$=VENDIMASK$,V4$="Enter A Valid Vendor Number (<F1>=Last/<F3>=Lookup)",V0=6,V1=40,V2=5
1220 GOSUB 7000
1230 ON V3 GOTO 1290,1240,1350,1270,4000
1240 LET VENDTHRU$=""
1250 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
1260 GOTO 1350
1270 CALL "SYC.LV",V$
1280 IF V$="" THEN GOTO 1200
1290 IF V$="" THEN GOTO 1240
1300 IF VENDFROM$<>"" THEN IF V$<VENDFROM$ THEN GOTO 1200
1310 DIM X1$(195)
1320 FIND (APM01_DEV,KEY=N0$+V$,DOM=1200)IOL=APM01A
1330 LET VENDTHRU$=V$,VENDTNAME$(1)=X1$(1,30)
1340 PRINT @(V1,V2),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
1350 GOTO 3900
1400 REM " --- Beginning Receiver Number
1410 LET V4$="Enter A Valid Receiver Number (<F1>=First)"
1420 LET V0$="Z",V1$="CKE",V2$=RECFROM$,V3$="",V0=7,V1=40,V2=7
1430 GOSUB 7000
1440 ON V3 GOTO 1480,1450,1510,1480,4000
1450 LET RECFROM$=""
1460 PRINT @(V1,V2),'CL',@(V1,V2),"First",
1470 GOTO 1510
1480 IF V$="" THEN GOTO 1450
1490 LET RECFROM$=V$
1500 PRINT @(V1,V2),RECFROM$,'CL',
1510 GOTO 3900
1600 REM " --- Ending Receiver Number
1610 LET V4$="Enter A Valid Receiver Number (<F1>=Last)"
1620 LET V0$="Z",V1$="CKE",V2$=RECTHRU$,V3$="",V0=7,V1=40,V2=9
1630 GOSUB 7000
1640 ON V3 GOTO 1680,1650,1710,1680,4000
1650 LET RECTHRU$=""
1660 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
1670 GOTO 1510
1680 IF V$="" THEN GOTO 1450
1690 LET RECTHRU$=V$
1700 PRINT @(V1,V2),RECTHRU$,'CL',
1710 GOTO 3900
1800 REM " --- Beginning Purchase Order
1810 LET V0$="Z",V1$="KCE",V2$=POFROM$,V3$="",V4$="Enter A Valid Purchase Order Number (<F1>=First/<F3>=Lookup)",V0=7,V1=40,V2=11
1820 GOSUB 7000
1830 ON V3 GOTO 1890,1840,1920,1870,4000
1840 LET POFROM$=""
1850 PRINT @(V1,V2),'CL',@(V1,V2),"First",
1860 GOTO 1920
1870 CALL "POC.LB",TYPE$,VENDPO$
1880 IF LEN(VENDPO$)<15 THEN GOTO 1840 ELSE LET V$=VENDPO$(9,7)
1890 IF V$="" THEN GOTO 1840
1900 LET POFROM$=V$
1910 PRINT @(V1,V2),POFROM$,'CL',
1920 GOTO 3900
2000 REM " --- Ending Purchase Order
2010 LET V0$="Z",V1$="KCE",V2$=POTHRU$,V3$="",V4$="Enter A Valid Purchase Order Number (<F1>=Last/<F3>=Lookup)",V0=7,V1=40,V2=13
2020 GOSUB 7000
2030 ON V3 GOTO 2090,2040,2130,2070,4000
2040 LET POTHRU$=""
2050 PRINT @(V1,V2),'CL',@(V1,V2),"Last",
2060 GOTO 2130
2070 CALL "POC.LB",TYPE$,VENDPO$
2080 IF LEN(VENDPO$)<15 THEN GOTO 2040 ELSE LET V$=VENDPO$(9,7)
2090 IF V$="" THEN GOTO 2040
2100 IF POFROM$<>"" THEN IF V$<POFROM$ THEN GOTO 1400
2110 LET POTHRU$=V$
2120 PRINT @(V1,V2),POTHRU$,'CL',
2130 GOTO 3900
3900 REM " --- Next Field
3910 IF V3=4 THEN GOTO 7000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1600,1800,2000,4000
4000 REM " --- Correct
4010 GOSUB REFRESH
4020 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4051,1000
4070 CALL "SYC.YN",0,"Are You Ready To Begin Purging",0,V$,V3
4080 IF V$<>"YES" THEN GOTO 9900
4100 REM " --- Purge QA Receipt Rejection Hist files
4120 LET EXIT_CTRL=1
4130 READ (POT03_DEV,KEY=N0$+VENDFROM$+RECFROM$+POFROM$,DOM=READHIST)
4200 REM " --- Loop through POT-03 
4205 READHIST: 
4220 LET K$=KEY(POT03_DEV,END=DONE)
4230 IF K$(1,2)<>N0$ THEN GOTO DONE
4250 LET VENDOR$=K$(3,6),RECEIVER$=K$(9,7),PONUM$=K$(16,7)
4300 REM " --- Valid Vendor/Receiver/PO?
4310 IF VENDTHRU$<>"" THEN IF VENDOR$>VENDTHRU$ THEN GOTO DONE
4320 IF RECFROM$<>"" THEN IF RECEIVER$<RECFROM$ THEN GOTO 4400
4330 IF RECTHRU$<>"" THEN IF RECEIVER$>RECTHRU$ THEN GOTO 4420
4340 IF POFROM$<>"" THEN IF PONUM$<POFROM$ THEN GOTO 4440
4350 IF POTHRU$<>"" THEN IF PONUM$>POTHRU$ THEN GOTO 4460
4390 GOTO VALID
4400 REM " --- Skip to beginning receiver for this vendor
4405 READ (POT03_DEV,KEY=K$(1,8)+RECFROM$,DOM=NEXTHIST)
4410 GOTO NEXTHIST
4420 REM " --- Skip to next vendor
4425 READ (POT03_DEV,KEY=K$(1,8)+$FF$,DOM=NEXTHIST)
4430 GOTO NEXTHIST
4440 REM " --- Skip to beginning po number
4445 READ (POT03_DEV,KEY=K$(1,15)+POFROM$,DOM=NEXTHIST)
4450 GOTO NEXTHIST
4460 REM " --- Skip to next receiver
4465 READ (POT03_DEV,KEY=K$(1,15)+$FF$,DOM=NEXTHIST)
4470 GOTO NEXTHIST
4500 REM " --- Valid, so purge
4501 VALID: 
4530 REMOVE (POT03_DEV,KEY=K$,DOM=NEXTHIST)
4600 REM " --- Loop up for next receiver
4605 NEXTHIST: 
4620 GOTO READHIST
4650 REM " --- All done
4655 DONE: 
4670 GOTO 9900
5000 REM " --- Refresh screen data
5005 REFRESH: 
5010 PRINT 'CF',
5030 IF VENDFROM$="" THEN PRINT @(40,3),"First",'CL', ELSE PRINT @(40,3),FNF$(VENDFROM$,VENDOMASK$)," ",VENDFNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5040 IF VENDTHRU$="" THEN PRINT @(40,5),"Last",'CL', ELSE PRINT @(40,5),FNF$(VENDTHRU$,VENDOMASK$)," ",VENDTNAME$(1,MIN(79-V1-VENDLEN-1,30)),'CL',
5050 IF RECFROM$="" THEN PRINT @(40,7),"First" ELSE PRINT @(40,7),RECFROM$,'CL',
5060 IF RECTHRU$="" THEN PRINT @(40,9),"Last" ELSE PRINT @(40,9),RECTHRU$,'CL',
5070 IF POFROM$="" THEN PRINT @(40,11),"First",'CL', ELSE PRINT @(40,11),POFROM$,'CL',
5080 IF POTHRU$="" THEN PRINT @(40,13),"Last",'CL', ELSE PRINT @(40,13),POTHRU$,'CL',
5190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

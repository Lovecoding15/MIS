0010 REM "BMM - Bill Hard Copy
0020 REM "Program BMC.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "12Jul96 JJD: Proper yield & phantom information must print in header section of listing (6015,6020)"
0032 REM "08Oct96 JAL: Must close IVM-02 to prevent Error 16 (9950,9955)"
0033 REM "20Jan99 Caj: Displayed prev item's Cost when Item wasn't setup in the Default Warehouse. (1215,1217)
0040 REM 
0051 REM "O9       : 1 = Batch, 2 = On Demand
0052 REM "PRTR_DEV : Channel Printer Is Open On
0053 REM "BILL_NO$ : Main Bill Number
0054 REM "PAGE     : Page Number
0055 REM "COMMENT$ : Y=Print Comments, N=Not
0056 REM "SUB$     : Sub-Assembly Part Number + Non-Phantom Parent Number
0059 REM "SELECT$  : A=All Detail Lines, C=Select based on PDATE$
0060 REM "PDATE$   : Effective Date (if SELECT$ = C)
0061 REM "STATUS   : Exit Status from Heading Routine"
0075 SETESC 9000
0080 SETERR 9000
0090 ENTER O9,PRTR_DEV,BILL_NO$,PAGE,COMMENT$,SUB$,SELECT$,PDATE$,STATUS
0100 REM " --- Open/Lock Files
0110 LET FILES=10
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="BMM-01",FILES$[2]="BMM-02",FILES$[3]="BMM-03"
0130 LET FILES$[4]="BMM-05",FILES$[5]="BMM-08",FILES$[6]="BMM-09"
0140 LET FILES$[7]="IVM-01",FILES$[8]="IVM-02",FILES$[9]="SYS-01"
0150 LET FILES$[10]="APM-01"
0160 CALL "SYC.DA",1,1,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS<>0 THEN GOTO 9900
0175 LET BMM01_DEV=CHANNELS[1],BMM02_DEV=CHANNELS[2],BMM03_DEV=CHANNELS[3]
0180 LET BMM05_DEV=CHANNELS[4],BMM08_DEV=CHANNELS[5],BMM09_DEV=CHANNELS[6]
0190 LET IVM01_DEV=CHANNELS[7],IVM02_DEV=CHANNELS[8],SYS01_DEV=CHANNELS[9]
0200 REM " --- IOLists
0210 APM01A: IOLIST *,X0$
0220 BMM01A: IOLIST A0$,A1$,A2$,A[ALL]
0225 BMM01A2: IOLIST AA0$,AA1$,AA2$,AA[ALL]
0230 BMM02A: IOLIST B0$,B1$,B2$,B[ALL]
0240 BMM03A: IOLIST R0$,R1$,R[ALL]
0250 BMM05A: IOLIST S0$,S1$,S2$,S3$,S4$,S[ALL]
0260 BMM08A: IOLIST D0$,D[ALL]
0270 BMM09A: IOLIST X0$,X1$
0280 IVM01A: IOLIST C0$,C1$(1)
0290 IVM02A: IOLIST *,*,*,JUNK[ALL],UNITCOST
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0320 APS01A: IOLIST X$,X$,X0$,X$,X$,VMASK$,X$,X$,X$
0330 BMS01A: IOLIST X$,P1$,P2$,P3$,P4,X5,P6
0340 IVS01A: IOLIST X$,P5$,P6$,WHS$,X$,X$,P7$,P8$,P9$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="BM",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=BMS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV"+"00",DOM=9800)IOL=IVS01A
0450 FIND (SYS01_DEV,KEY=N0$+"AP"+"00",DOM=0480)IOL=APS01A
0460 LET VLEN=NUM(X0$(1,2))
0480 DIM P[2],DESC[3]
0490 LET P[0]=NUM(P6$(1,2))
0492 IF NUM(P6$(5,1))>P6 THEN LET P6=NUM(P6$(5,1))
0500 REM " --- Initialize Data
0510 LET H2$=N4$
0520 LET L9=59,L=L9+1,T0=0,HEADINGS=9
0530 DIM A[11],AA[11],B[10],D[13],R[11],S[9],JUNK[10]
0535 DIM J$(80,"-"),HEADINGS$[HEADINGS]
0540 PRECISION P6
0550 IF POS("Listing"=H2$)=0 THEN LET H2$=H2$+" Listing"
0555 CALL "SYC.RM",P1$,P1,9,9
0557 CALL "SYC.RM",P2$,P2,7,7
0560 LET P7=LEN(P7$),P9=LEN(P9$)
0565 LET M6$="###.00",P4$="###0"
0570 FOR X=1 TO 3
0580 LET DESC[X]=NUM(P6$((X-1)*2+9,2))
0590 NEXT X
0595 DIM C1$(DESC[1]+DESC[2]+DESC[3])
0600 REM " --- Additional File Opens
0610 DIM INFO$[20]
0620 CALL "SYC.VA","WO",INFO$[ALL]
0630 LET WO$=INFO$[20]
0640 CALL "SYC.VA","AP",INFO$[ALL]
0650 LET AP$=INFO$[20]
0655 IF AP$="N" THEN GOTO 0700
0660 CALL "SYC.DA",1,10,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0670 IF STATUS<>0 THEN GOTO 9900
0680 LET APM01_DEV=CHANNELS[10]
0700 REM " ---
0890 PRINT @(20,20),"Now Printing Hard-Copy......",'CL',
1000 REM " --- Print Material Information
1010 READ (BMM01_DEV,KEY=N0$+BILL_NO$,DOM=9900)IOL=BMM01A
1020 GOSUB 6000
1030 PRINT @(49,20),BILL_NO$,
1040 LET MATTOT=0
1050 IF SUB$<>"" THEN LET PART$=SUB$(1,20) ELSE LET PART$=BILL_NO$
1060 GOSUB 5200
1070 READ (BMM02_DEV,KEY=N0$+PART$,DOM=1080)
1080 LET K1$=KEY(BMM02_DEV,END=1400)
1090 IF POS(N0$+PART$=K1$)<>1 THEN GOTO 1400
1100 READ (BMM02_DEV,KEY=K1$)IOL=BMM02A
1110 IF SELECT$="A" THEN GOTO 1140
1120 IF B2$(5,3)<>"   " THEN IF B2$(5,3)>PDATE$ THEN GOTO 1280
1130 IF B2$(8,3)<>"   " THEN IF B2$(8,3)<=PDATE$ THEN GOTO 1280
1140 IF B2$(4,1)="M" AND L+2>L9 THEN GOSUB 5200
1150 IF B2$(4,1)="M" THEN PRINT (PRTR_DEV)@(0),B0$(23,3),@(4),B2$(35,60); LET L=L+1; GOTO 1280
1160 DIM X0$(30)
1170 FIND (IVM01_DEV,KEY=N0$+B1$,DOM=1190)IOL=IVM01A
1180 LET X0$(1)=FNITEM$(C1$,DESC[1],DESC[2],DESC[3])
1190 LET SUBBILL$=" "
1200 FIND (BMM01_DEV,KEY=N0$+B1$,DOM=1215)
1210 LET SUBBILL$="*"
1215 LET UNITCOST=0
1217 FIND (IVM02_DEV,KEY=N0$+WHS$(6,2)+B1$,DOM=1220)IOL=IVM02A
1220 LET MATCOST=UNITCOST*FNQTY(B[0],B[1],B[3],YIELD,B[4])
1230 IF L+2>L9 THEN GOSUB 5200
1240 PRINT (PRTR_DEV)@(0),B0$(23,3),SUBBILL$,@(4),B1$(1,P[0]),@(25),B[3]:P1$,@(34),B[0]:P1$,@(43),B[1]:P2$,@(52),B[4]:P2$,@(61),UNITCOST:P9$,@(70),MATCOST:P9$
1250 PRINT (PRTR_DEV)@(4),X0$(1,30),@(37),"Op Seq: ",B2$(1,3),@(50),"U/M: ",B2$(33,2),@(58),"Net Qty: ",FNQTY(B[0],B[1],B[3],YIELD,B[4]):P1$
1260 LET MATTOT=MATTOT+MATCOST
1270 LET L=L+2
1280 IF SELECT$="C" THEN GOTO 1310
1290 IF L+1>L9 THEN GOSUB 5200
1300 PRINT (PRTR_DEV)@(7),"Effective: ",FNB$(B2$(5,3)),@(35),"Obsolete: ",FNB$(B2$(8,3)); LET L=L+1
1310 GOTO 1080
1400 REM " --- Print Operation Information
1410 LET DIR_LABTOT=0,OH_LABTOT=0,SUB_COST=0
1420 GOSUB 5400
1430 READ (BMM03_DEV,KEY=N0$+PART$,DOM=1440)
1440 READ (BMM03_DEV,END=1700)IOL=BMM03A
1450 IF N0$+PART$<>R0$(1,22) THEN GOTO 1700
1460 IF SELECT$="A" THEN GOTO 1490
1470 IF R1$(65,3)<>"   " THEN IF R1$(65,3)>PDATE$ THEN GOTO 1630
1480 IF R1$(68,3)<>"   " THEN IF R1$(68,3)<=PDATE$ THEN GOTO 1630
1490 IF R1$(4,1)="M" THEN IF L+1>L9 THEN GOSUB 5400
1500 IF R1$(4,1)="M" THEN PRINT (PRTR_DEV)@(0),R0$(23,3),@(4),R1$(5,60); LET L=L+1; GOTO 1630
1510 DIM D0$(30); LET D0$(6,20)="Not On File",D[0]=0
1520 READ (BMM08_DEV,KEY=R0$(1,2)+R1$(1,3),DOM=1530)IOL=BMM08A
1530 IF L+2>L9 THEN GOSUB 5400
1540 IF R[1]=0 THEN LET R[1]=1
1550 IF LOT=0 THEN LET LOT=1
1560 LET DIR_LABCOST=FNOP1(R[0],D[0],R[1],YIELD,R[4],LOT)
1570 LET OH_LABCOST=DIR_LABCOST*D[2]
1580 PRINT (PRTR_DEV)@(0),R0$(23,3),@(4),R1$(1,3),@(29),R[4]:M6$,@(37),R[0]:P1$,@(47),R[1]:P2$,@(67),100*(R[0]/R[1])/YIELD+R[4]/LOT:P1$,
1590 IF WO$="Y" THEN PRINT (PRTR_DEV)@(19),D[3]:M6$,@(59),R[5]:M6$ ELSE PRINT (PRTR_DEV)""
1600 PRINT (PRTR_DEV)@(4),D0$(6,20),@(41),D[0]:P1$,@(51),DIR_LABCOST:P9$,@(61),OH_LABCOST:P9$,@(71),DIR_LABCOST+OH_LABCOST:P9$
1610 LET DIR_LABTOT=DIR_LABTOT+DIR_LABCOST,OH_LABTOT=OH_LABTOT+OH_LABCOST
1620 LET L=L+2
1630 IF SELECT$="C" THEN GOTO 1660
1640 IF L+1>L9 THEN GOSUB 5400
1650 PRINT (PRTR_DEV)@(7),"Effective: ",FNB$(R1$(65,3)),@(35),"Obsolete: ",FNB$(R1$(68,3)); LET L=L+1
1660 GOTO 1440
1700 REM " --- Print Subcontract Information
1710 IF AP$="N" THEN GOTO 2500
1720 GOSUB 5500
1730 READ (BMM05_DEV,KEY=N0$+PART$,DOM=1740)
1740 READ (BMM05_DEV,END=2500)IOL=BMM05A
1750 IF N0$+PART$<>S0$(1,22) THEN GOTO 2500
1760 IF SELECT$="A" THEN GOTO 1790
1770 IF S2$(1,3)<>"   " THEN IF S2$(1,3)>PDATE$ THEN GOTO 1940
1780 IF S2$(4,3)<>"   " THEN IF S2$(4,3)<=PDATE$ THEN GOTO 1940
1790 IF S1$(1,1)="S" THEN GOTO 1840
1800 IF L+1>L9 THEN GOSUB 5500
1810 PRINT (PRTR_DEV)@(0),S0$(23,3)," ",S1$(1,1)," ",S1$(10,60)
1820 LET L=L+1
1830 GOTO 1940
1840 IF L+2>L9 THEN GOSUB 5500
1850 DIM X0$(30)
1860 FIND (APM01_DEV,KEY=N0$+S1$(4,6),DOM=1870)IOL=APM01A
1870 PRINT (PRTR_DEV)@(0),S0$(23,3)," ",S1$(1,1)," ",FNF$(S1$(4,VLEN),VMASK$)," ",X0$(1,25-LEN(VMASK$)),@(33),S[3]:P1$,@(42),S[0]:P1$,@(51),S[1]:P2$,@(60),S[2]:P9$,@(69),S[2]*FNSUB(S[0],S[1],S[3]):P9$
1880 PRINT (PRTR_DEV)@(6),S1$(10,30),@(38),"Ld: ",S[4]:P4$,@(47),"U/M: ",S1$(2,2),@(55),"Op: ",S3$(1,3),@(63),"Net Qty:",FNSUB(S[0],S[1],S[3]):P1$
1890 LET SUB_COST=SUB_COST+S[2]*FNSUB(S[0],S[1],S[3])
1900 LET L=L+2
1910 IF SELECT$="C" THEN GOTO 1940
1920 IF L+1>L9 THEN GOSUB 5500
1930 PRINT (PRTR_DEV)@(7),"Effective: ",FNB$(S2$(1,3)),@(35),"Obsolete: ",FNB$(S2$(4,3)); LET L=L+1
1940 GOTO 1740
2500 REM " --- Print Total Information
2510 IF L+8>L9 THEN GOSUB 5000
2520 GOSUB 5600
2600 REM " --- Comments
2605 IF COMMENT$<>"Y" THEN GOTO 4000
2610 READ (BMM09_DEV,KEY=N0$+PART$,DOM=2620)
2620 GOSUB 5900
2640 LET K$=KEY(BMM09_DEV,END=4000)
2650 IF POS(N0$+PART$=K$)<>1 THEN GOTO 4000
2660 READ (BMM09_DEV,KEY=K$)IOL=BMM09A
2670 IF L+1>L9 THEN GOSUB 5000; GOSUB 5900
2680 PRINT (PRTR_DEV)@(0),X0$(23,2),@(7),X1$
2685 LET L=L+1
2690 GOTO 2640
4000 REM " --- End
4100 GOTO 9900
5000 REM " --- Heading
5010 FOR X=3 TO 9; LET HEADINGS$[X]=FILL(80); NEXT X
5015 LET L=HEADINGS+1
5020 LET HEADINGS$[0]=N1$,HEADINGS$[1]=H2$
5030 FIND (IVM01_DEV,KEY=N0$+BILL_NO$,DOM=5040)IOL=IVM01A
5040 LET HEADINGS$[3](6)="Bill Number: "+BILL_NO$,HEADINGS$[3](40)="Last Used: "+FNB$(A2$(41,3))
5045 IF A2$(44,1)="B" THEN LET HEADINGS$[3](60)="In Production Entry" ELSE IF A2$(44,1)="W" THEN LET HEADINGS$[3](60)="In Work Orders"
5050 LET HEADINGS$[4](6)="Description: "+C1$(1,MIN(DESC[1],36))
5055 LET HEADINGS$[4](55)="Phantom Bill?: "+A2$(40,1)
5060 LET HEADINGS$[5](19)=C1$(DESC[1]+1,MIN(DESC[2],34))
5070 LET HEADINGS$[5](53)="Unit Of Measure: "+A2$(48,2)
5080 LET HEADINGS$[6](19)=C1$(DESC[1]+DESC[2]+1,MIN(DESC[3],32))
5085 LET HEADINGS$[6](51)="Estimated Yield %: "+STR(YIELD:"##0.0")
5090 LET HEADINGS$[7](3)="Drawing Number: "+A0$(23,25)
5100 LET HEADINGS$[7](51)="Standard Lot Size: "+STR(LOT)
5110 LET HEADINGS$[8](1)="Drawing Revision: "+A0$(48,5)+" Revision Code: "+A2$(38,2)+" Created: "+FNB$(A2$(45,3))+" Revised: "+FNB$(A2$(35,3))
5120 IF SUB$<>"" THEN LET HEADINGS$[9](1)="Sub-Assembly : "+SUB$(1,20)
5130 IF SELECT$="C" THEN LET HEADINGS$[9](54)="As Of Date: "+FNB$(PDATE$)
5140 CALL "SYC.HA",PRTR_DEV,HEADINGS$[ALL],HEADINGS,PAGE,80,F0$(7,3),CLOCK$,STATUS
5150 IF STATUS>0 THEN EXITTO 9900
5190 RETURN 
5200 REM " --- Material Heading
5205 IF L+4>L9 THEN GOSUB 5000
5210 PRINT (PRTR_DEV)@(0),J$,@(8),"Material Information",'LF',@(38),"Qty",@(46),"Alt",@(55),"Scrap",@(65),"Unit",@(73),"Total"
5220 PRINT (PRTR_DEV)@(0),"Seq",@(4),"Item",@(26),"Divisor",@(38),"Reqd",@(45),"Factor",@(54),"Factor",@(65),"Cost",@(74),"Cost",'LF'
5240 LET L=L+4
5290 RETURN 
5400 REM " --- Operation Heading
5405 IF L+4>L9 THEN GOSUB 5000
5410 PRINT (PRTR_DEV)@(0),J$,@(8),"Operation Information",'LF',@(4),"Op Code",@(30),"Setup",@(36),"Hrs/Piece",@(47),"Piece/Hr",@(68),"Net Hrs",
5415 IF WO$="Y" THEN PRINT (PRTR_DEV)@(20),"Queue",@(61),"Move" ELSE PRINT (PRTR_DEV)""
5420 PRINT (PRTR_DEV)@(0),"Seq",@(4),"Description",@(41),"Dir Rate",@(51),"Dir Cost",@(61),"O/H Cost",@(71),"Tot Cost",'LF'
5440 LET L=L+4
5490 RETURN 
5500 REM " --- Subcontract Heading
5505 IF L+4>L9 THEN GOSUB 5000
5510 PRINT (PRTR_DEV)@(0),J$,@(8),"Subcontract Information",'LF',@(45),"Qty",@(54),"Alt",@(64),"Unit",@(72),"Total"
5520 PRINT (PRTR_DEV)@(0),"Seq C Vendor/Service",@(33),"Divisor",@(42),"Required",@(53),"Factor",@(64),"Cost",@(73),"Cost",'LF'
5540 LET L=L+4
5590 RETURN 
5600 REM " --- Total Information
5640 PRINT (PRTR_DEV)@(0),J$,@(8),"Total Information",'LF'
5650 PRINT (PRTR_DEV)@(18),"Material :  ",MATTOT:P9$
5660 PRINT (PRTR_DEV)@(14),"Direct Labor :  ",DIR_LABTOT:P9$
5670 PRINT (PRTR_DEV)@(18),"Overhead :  ",OH_LABTOT:P9$
5675 PRINT (PRTR_DEV)@(15),"Subcontract :  ",SUB_COST:P9$
5680 PRINT (PRTR_DEV)@(30),J$(1,P9)
5690 PRINT (PRTR_DEV)@(21),"Total :  ",MATTOT+DIR_LABTOT+OH_LABTOT+SUB_COST:P9$
5695 LET L=L+8
5699 RETURN 
5900 REM " --- Comments
5910 IF L+2>L9 THEN GOSUB 5000
5920 PRINT (PRTR_DEV)@(0),J$,@(8),"Comments"
5930 PRINT (PRTR_DEV)@(0),"Seq"
5940 LET L=L+2
5990 RETURN 
6000 REM " --- Get Lot Size and Yield for Non-Phantoms
6010 IF SUB$="" THEN LET LOT=A[5],YIELD=A[4]; GOTO 6090
6020 IF LEN(SUB$)=20 THEN LET LOT=A[5],YIELD=A[4]; GOTO 6090
6030 FIND (BMM01_DEV,KEY=N0$+SUB$(21,20),DOM=6040)IOL=BMM01A; GOTO 6050
6040 LET LOT=A[5],YIELD=A[4]; GOTO 6090
6050 LET LOT=A[5],YIELD=A[4]
6060 READ (BMM01_DEV,KEY=N0$+BILL_NO$)IOL=BMM01A
6090 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNQTY(Q1,Q2,Q3,Q4,Q5)=10000*Q1*Q2/(Q3*Q4*(100-Q5))
8085 DEF FNSUB(Q1,Q2,Q3)=Q1*Q2/Q3
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8095 DEF FNOP1(Q1,Q2,Q3,Q4,Q5,Q6)=100*(Q1*Q2/Q3)/Q4+Q5*Q2/Q6
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Exit Program
9910 CLOSE (BMM01_DEV,ERR=9920)
9920 CLOSE (BMM02_DEV,ERR=9930)
9930 CLOSE (BMM03_DEV,ERR=9940)
9940 CLOSE (BMM08_DEV,ERR=9950)
9950 CLOSE (IVM01_DEV,ERR=9955)
9955 CLOSE (IVM02_DEV,ERR=9960)
9960 CLOSE (BMM09_DEV,ERR=9970)
9970 CLOSE (SYS01_DEV,ERR=9975)
9975 CLOSE (BMM05_DEV,ERR=9980)
9980 CLOSE (APM01_DEV,ERR=9990)
9990 EXIT 
9999 END

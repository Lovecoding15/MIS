0010 REM "IVR - Inventory Synonyms Listing (Report Overlay)"
0020 REM "Program IVR.SB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "30Sep96 WGH: When Sequence is by Item, Beginning and Ending Synonyms and Items don't work (1060-1070,1230,1250-1260)"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 CALL "SYC.GA",7,1,"","",STATUS
0120 IF STATUS THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$,A5$,A6$,A[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A[12],HEADINGS$[7],O[3]
0550 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$,WHEN$=F0$(7,3),CLOCK$=""
0560 LET WIDTH=80,PAGE=0,L9=59,L=L9+1,HEADINGS=7,NUMBER=0,LAST$=""
0600 REM " --- Print positions"
0610 LET O[0]=1
0620 IF SEQUENCE$="S" THEN LET O[1]=22,O[2]=O[1]+P[0]+1,O[3]=WIDTH-3
0630 IF SEQUENCE$="I" THEN LET O[1]=P[0]+2,O[3]=WIDTH-20,O[2]=O[3]-4
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing",20,COLUMN
0800 REM " --- Initialize HEADINGS$"
0810 DIM H1$(WIDTH),H2$(WIDTH),H6$(WIDTH)
0815 IF SEQUENCE$="S" THEN LET HEADINGS$[1]=HEADINGS$[1]+" By Synonym"
0820 IF SEQUENCE$="I" THEN LET HEADINGS$[1]=HEADINGS$[1]+" By Item"
0825 LET X1$="First",X2$="First",X2=5
0830 IF BEGITEM$<>"" THEN LET X1$=BEGITEM$
0835 IF BEGPROD$<>"" THEN LET X2$=BEGPROD$,X2=3
0840 IF ENDPROD$="" THEN IF X2=3 THEN LET X2=4
0845 LET H1$(1)="From Item: "+X1$,H1$(WIDTH-X2-13)="From Product: "+X2$
0850 LET X1$="Last",X2$="Last",X3$="All Item Types"
0855 IF ENDITEM$<>"" THEN LET X1$=ENDITEM$
0860 IF ENDPROD$<>"" THEN LET X2$=ENDPROD$
0865 IF TYPE$<>"" THEN LET X3$="Item Type "+TYPE$
0870 LET HEADINGS$[1]=HEADINGS$[1]+" For "+X3$
0875 LET H2$(1)="  To Item: "+X1$,H2$(WIDTH-X2-13)="  To Product: "+X2$
0880 IF SEQUENCE$="S" THEN GOTO 0900
0885 LET H6$(O[0])="Item",H6$(O[1])="Description"
0890 LET H6$(O[2])="Prd",H6$(O[3])="Synonym"
0895 GOTO 0910
0900 LET H6$(O[0])="Synonym",H6$(O[1])="Item"
0905 LET H6$(O[2])="Description",H6$(O[3])="Prd"
0910 LET HEADINGS$[3]=H1$,HEADINGS$[4]=H2$,HEADINGS$[6]=H6$
0950 REM " --- Position files"
0980 READ (IVM14_DEV,KEY=N0$+BEGSYN$,DOM=0990)
0990 READ (IVM24_DEV,KEY=N0$+BEGITEM$,DOM=1000)
1000 REM " --- Next Synonym"
1010 IF SEQUENCE$="I" THEN GOTO 1200
1020 LET K$=KEY(IVM14_DEV,END=4000)
1030 IF POS(N0$=K$)<>1 THEN GOTO 4000
1040 IF ENDSYN$<>"" THEN IF K$(3,LEN(ENDSYN$))>ENDSYN$ THEN GOTO 4000
1050 READ (IVM14_DEV)
1060 IF BEGITEM$<>"" THEN IF K$(23,LEN(BEGITEM$))<BEGITEM$ THEN GOTO 1100
1070 IF ENDITEM$<>"" THEN IF K$(23,LEN(ENDITEM$))>ENDITEM$ THEN GOTO 1150
1080 LET SYNONYM$=K$(3,20),ITEM$=K$(23),X$=SYNONYM$
1090 GOTO 1500
1100 REM " --- Skip to beginning item"
1110 READ (IVM14_DEV,KEY=K$(1,22)+BEGITEM$(1,LEN(BEGITEM$)-1)+$FF$,DOM=3900)
1140 GOTO 3900
1150 REM " --- Skip to next synonym"
1160 READ (IVM14_DEV,KEY=K$(1,22)+$FF$,DOM=3900)
1190 GOTO 3900
1200 REM " --- Next Item"
1210 LET K$=KEY(IVM24_DEV,END=4000)
1220 IF POS(N0$=K$)<>1 THEN GOTO 4000
1230 IF ENDITEM$<>"" THEN IF K$(3,LEN(ENDITEM$))>ENDITEM$ THEN GOTO 4000
1240 READ (IVM24_DEV)
1250 IF BEGSYN$<>"" THEN IF K$(23,LEN(BEGSYN$))<BEGSYN$ THEN GOTO 1300
1260 IF ENDSYN$<>"" THEN IF K$(23,LEN(ENDSYN$))>ENDSYN$ THEN GOTO 1350
1270 LET SYNONYM$=K$(23,20),ITEM$=K$(3,20),X$=ITEM$
1290 GOTO 1500
1300 REM " --- Skip to beginning synonym"
1310 READ (IVM24_DEV,KEY=K$(1,22)+BEGSYN$(1,LEN(BEGSYN$)-1)+$FF$,DOM=3900)
1340 GOTO 3900
1350 REM " --- Skip to next item"
1360 READ (IVM24_DEV,KEY=K$(1,22)+$FF$,DOM=3900)
1390 GOTO 3900
1500 REM " --- Retrieve item"
1510 PRINT @(COLUMN,11),X$,
1520 LET A0$(1)=N0$+ITEM$
1530 FIND (IVM01_DEV,KEY=A0$,DOM=3900)IOL=IVM01A
1540 IF BEGPROD$<>"" THEN IF A2$(1,3)<BEGPROD$ THEN GOTO 3900
1550 IF ENDPROD$<>"" THEN IF A2$(1,3)>ENDPROD$ THEN GOTO 3900
1560 IF TYPE$<>"" THEN IF A2$(53,3)<>TYPE$ THEN GOTO 3900
2000 REM " --- Format detail line"
2010 DIM BUFFER$(WIDTH)
2020 LET DESCRIPTION$=FNITEM$(A1$,P[3],P[4],P[5])
2030 IF SEQUENCE$="S" THEN GOTO 2070
2040 LET BUFFER$(O[0])=ITEM$,BUFFER$(O[1])=DESCRIPTION$,CHAR$=ITEM$(1,1)
2050 LET BUFFER$(O[2])=A2$(1,3),BUFFER$(O[3])=SYNONYM$
2060 GOTO 2100
2070 LET BUFFER$(O[0])=SYNONYM$,BUFFER$(O[1])=ITEM$,CHAR$=SYNONYM$(1,1)
2080 LET BUFFER$(O[2])=DESCRIPTION$,BUFFER$(O[3])=A2$(1,3)
2100 REM " --- Skip a line?"
2110 IF CHAR$=LAST$ THEN GOTO 2200
2120 IF L+1>L9 THEN GOTO 2200
2130 PRINT (7)""
2140 LET L=L+1,LAST$=CHAR$
2200 REM " --- Print detail line"
2210 IF L+1>L9 THEN GOSUB 5000
2220 PRINT (7)BUFFER$
2230 LET L=L+1,NUMBER=NUMBER+1
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF L+2>L9 THEN GOSUB 5000
4020 PRINT (7)""
4030 PRINT (7)NUMBER," Synonyms Listed"
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 IF CHAR$<>"" THEN LET LAST$=CHAR$
5090 RETURN 
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

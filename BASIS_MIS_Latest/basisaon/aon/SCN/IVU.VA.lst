0010 REM "IVU - Physical Inventory Update
0020 REM "Program IVU.VA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 15Dec97 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "12Jun96 WGH: Print register before asking update question (110)"
0032 REM "12Jun96 WGH: Fix Physical Inventory for lotted/serialized items (250,270,1910,2010,2040-2670,3910,6000-6890,8000-8080)"
0033 REM "17Jun96 WGH: IVM-02 can be written with short key (2670)"
0034 REM "18Jun96 WGH: For LIFO/FIFO use tier cost when available, except for lotted/serialized items (150-170,600-645)"
0035 REM "02Jul96 WGH: Remove IVM-10 'P' Physical Inventory Status records when done (3920)"
0036 REM "05Aug96 WGH: Subtract 'Missing' lot/serial freeze quantity from warehouse on-hand quantity (2420,6050,6180)"
0037 REM "22Aug96 Caj: ERROR 41 @ 510 when L/S # Size param>14 cuz PRECISION statement was using the wrong IV param (510)
0038 REM "16Apr97 WGH: De-select items that Count was NOT entered (2050-2060,2350-2390,6030)"
0039 REM "27May97 WGH: For lotted/serialized items, update item's last physical date when no lot/serial number and freeze quantity is zero (2415)"
0040 REM "13Oct97 WGH: Distinguish between 'Blank' and 'Missing' lot/serial numbers (2250-2390,2415-2420,2440-2460,2605,6030,6050-6055,6180)"
0041 REM "17Feb00 WGH: Maintain IVM-08 Lot/Serial Number Xref file when lot/serial numbers are added (240,2550-2560)
0050 REM 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 CLOSE (7,ERR=120); REM "Print register
0200 REM " --- IOLIST's"
0230 IVE03A: IOLIST W0$(1),W1$(1),W[ALL]
0240 IVM08A: IOLIST IVM08A$
0250 IVM01A: IOLIST *,*,B2$(1)
0260 IVM02A: IOLIST A0$(1),A1$(1),A2$,A[ALL]
0270 IVM07A: IOLIST D0$(1),D1$(1),D[ALL]
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0290 IVM10P: IOLIST Y0$(1)
0400 REM " --- Parameters"
0410 DIM G[1]
0420 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 LET G[0]=NUM(G2$(1,2))
0500 REM " --- Initializations"
0510 PRECISION P[1]
0515 DIM IV_CHANS[44],PARAMS[10],PARAMS$[10],ITEM$[3]
0575 LET PARAMS[0]=G[0],PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3)
0580 LET PARAMS$[2]=I2$,PARAMS$[3]=I3$,PARAMS$[4]=I4$
0590 LET ACTION$="PH"
0650 REM " --- Update channels"
0660 LET IV_CHANS[0]=SYS01_DEV,IV_CHANS[1]=IVM01_DEV,IV_CHANS[2]=IVM02_DEV
0670 LET IV_CHANS[4]=IVM04_DEV,IV_CHANS[7]=IVM07_DEV,IV_CHANS[8]=IVM08_DEV
0680 LET IV_CHANS[12]=IVM12_DEV,IV_CHANS[17]=IVM17_DEV,IV_CHANS[41]=IVT01_DEV
0690 LET IV_CHANS[42]=IVT02_DEV,IV_CHANS[43]=IVT03_DEV,IV_CHANS[44]=IVT04_DEV
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0800 REM " --- Run Update?"
0810 LET V4$="Are You Ready To Update The "+F5$
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0910 CALL "SYC.NB","Updating",17+P[0],COLUMN
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
0970 REM " --- Position file"
0990 READ (IVW10_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next entry record"
1010 LET K$=KEY(IVW10_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 READ (IVW10_DEV)
1200 REM " --- Update IVM-10 cycle code record"
1210 LET Y0$(1)=N0$+"P"+WHSE$+K$(3,2)
1220 FIND (IVM10_DEV,KEY=Y0$(1,7),DOM=1900)IOL=IVM10P
1230 LET Y0$(8,1)="4"
1240 WRITE (IVM10_DEV,KEY=Y0$(1,7))IOL=IVM10P
1900 REM " --- Position physical inventory record"
1910 LET LASTITEM$="",LSITEM$="N"
1980 LET FIRST$=K$(1,2)+WHSE$+K$(3)
1990 READ (IVE03_DEV,KEY=FIRST$,DOM=2000)
2000 REM " --- Read next physical inventory record"
2010 LET K$="",K$=KEY(IVE03_DEV,END=3900)
2020 IF POS(FIRST$=K$)<>1 THEN GOTO 3900
2030 READ (IVE03_DEV)IOL=IVE03A
2100 REM " --- Lotted/Serialized Item Break?"
2110 IF LS$<>"Y" THEN GOTO 2200
2120 IF POS(LASTITEM$=K$)=1 THEN GOTO 2300
2130 GOSUB 6000
2200 REM " --- Selected for Phys Inventory?"
2210 DIM A0$(24),A1$(64),A[20]
2220 LET A0$(1)=K$(1,2)+K$(3,2)+K$(17,20)
2230 FIND (IVM02_DEV,KEY=A0$,DOM=NEXTIVE03)IOL=IVM02A
2240 IF A1$(15,1)<>"Y" THEN GOTO NEXTIVE03
2250 LET ITEMPHYSDATE$=A1$(16,3)
2300 REM " --- Got One To Update"
2310 PRINT @(COLUMN,11),W0$(3,2)," ",W0$(5,2)," ",K$(7,10)," ",K$(17,P[0]),
2320 LET COST=A[11],FREEZEQTY=W[0],COUNTQTY=W[1],LASTPHYSDATE$=W1$(1,3)
2400 REM " --- Lotted/Serialized Item"
2410 IF LSITEM$<>"Y" THEN GOTO 2600
2420 IF FNP$(W0$(37))="" AND W[0]<>0 THEN LET W1$(4,1)="Y"; REM "Count always entered for Missing and Blank
2430 DIM D0$(44),D1$(95),D[11]; LET D0$(1)=K$(1,4)+K$(17,40)
2440 FIND (IVM07_DEV,KEY=D0$,DOM=2450)IOL=IVM07A
2445 LET COST=D[3]
2450 IF W1$(4,1)<>"Y" THEN LET LASTPHYSDATE$=D1$(61,3),COUNTQTY=D[2],TOT_COUNTQTY=TOT_COUNTQTY+FREEZEQTY; GOTO 2500
2460 LET LOTSER$=W0$(37,20),TOT_COUNTQTY=TOT_COUNTQTY+COUNTQTY,ITEMPHYSDATE$=LASTPHYSDATE$,LSCNTNTRD=1
2470 IF FREEZEQTY=COUNTQTY THEN GOTO 2500
2480 GOSUB ATAMO
2490 GOTO NEXTIVE03
2500 REM " --- No Change In Lotted/Serialized On-Hand Quantity"
2520 LET D1$(61,3)=LASTPHYSDATE$,D[2]=COUNTQTY
2540 WRITE (IVM07_DEV,KEY=D0$)IOL=IVM07A
2550 LET IVM08A$=D0$(1,2)+D0$(25,20)+D0$(3,2)+D0$(5,20)
2560 WRITE (IVM08_DEV,KEY=IVM08A$)IOL=IVM08A
2590 GOTO NEXTIVE03
2600 REM " --- Update IVM-02 Item Warehouse Detail"
2605 IF W1$(4,1)<>"Y" THEN LET LASTPHYSDATE$=A1$(16,3),COUNTQTY=A[8]; GOTO 2650
2610 LET LOTSER$=""
2620 IF FREEZEQTY=COUNTQTY THEN GOTO 2650
2630 GOSUB ATAMO
2640 GOTO NEXTIVE03
2650 REM " --- No Change In On-Hand Quantity"
2660 LET A1$(15,1)="N",A1$(16,3)=LASTPHYSDATE$,A[8]=COUNTQTY
2670 WRITE (IVM02_DEV,KEY=A0$)IOL=IVM02A
2900 REM " --- Loop back for next physical inventory record"
2905 NEXTIVE03: 
2950 REMOVE (IVE03_DEV,KEY=K$)
2990 GOTO 2000
3900 REM " --- Loop back for next entry record"
3910 IF LSITEM$="Y" THEN GOSUB 6000
3920 REMOVE (IVM10_DEV,KEY=Y0$(1,7),DOM=3930); REM "Remove IVM-10 cycle code cecord"
3990 GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
6000 REM " --- Lotted/Serialized Item Break"
6010 IF LASTITEM$="" THEN GOTO 6100
6020 IF LSITEM$<>"Y" THEN GOTO 6100
6040 FIND (IVM02_DEV,KEY=A0$,DOM=6100)IOL=IVM02A
6050 LET A1$(15,1)="N"
6055 IF LSCNTNTRD THEN LET A1$(16,3)=ITEMPHYSDATE$,A[8]=TOT_COUNTQTY
6060 WRITE (IVM02_DEV,KEY=A0$)IOL=IVM02A
6100 REM " --- Is Next Item Lotted/Serialized?"
6110 LET LSITEM$="N"
6120 IF LEN(K$)<36 THEN GOTO 6190
6130 DIM B2$(62)
6140 FIND (IVM01_DEV,KEY=K$(1,2)+K$(17,20),DOM=6150)IOL=IVM01A
6150 IF B2$(19,2)<>"YY" THEN GOTO 6180
6160 LET LSITEM$="Y"
6180 LET LASTITEM$=K$(1,36),TOT_COUNTQTY=0,LSCNTNTRD=0
6190 RETURN
6800 REM " --- Initialize Varaibles For ATAMO Inventory Item Update Routine"
6805 ATAMO: 
6810 DIM REFS$[11],REFS[5]
6820 LET ITEM$[0]=W0$(1,2),ITEM$[1]=W0$(3,2),ITEM$[2]=W0$(17,20),ITEM$[3]=LOTSER$
6840 LET REFS$[0]=LASTPHYSDATE$,REFS$[4]=W0$(5,2)
6860 LET REFS[0]=COUNTQTY-FREEZEQTY,REFS[1]=COST,REFS[5]=COUNTQTY
6880 CALL "IVC.UA",ACTION$,IV_CHANS[ALL],PARAMS[ALL],PARAMS$[ALL],ITEM$[ALL],REFS$[ALL],REFS[ALL],STATUS
6890 RETURN
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END
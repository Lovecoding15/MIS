0010 REM "ARU - Simple Invoice Update"
0020 REM "Program ARU.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "29Jan97 JAL: DIM correct IOLIST array (6290)"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=14
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="ARE-05",FILES$[2]="ARE-15",FILES$[3]="ARM-01"
0135 LET FILES$[4]="ARM-02",FILES$[5]="ARM-06",FILES$[6]="ARM-10"
0140 LET FILES$[7]="ART-01",FILES$[8]="ART-11",FILES$[9]="ART-43"
0145 LET FILES$[10]="ART-53",FILES$[11]="SYS-01",FILES$[12]="GLM-01"
0150 LET FILES$[13]="GLT-04",FILES$[14]="GLT-05"
0155 LET OPTIONS$[1]="L",OPTIONS$[2]="L"
0160 CALL "SYC.DA",1,1,11,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0175 LET ARE05_DEV=CHANNELS[1],ARE15_DEV=CHANNELS[2],ARM01_DEV=CHANNELS[3]
0180 LET ARM02_DEV=CHANNELS[4],ARM06_DEV=CHANNELS[5],ARM10_DEV=CHANNELS[6]
0185 LET ART01_DEV=CHANNELS[7],ART11_DEV=CHANNELS[8],ART43_DEV=CHANNELS[9]
0190 LET ART53_DEV=CHANNELS[10],SYS01_DEV=CHANNELS[11]
0200 REM " --- IOLists
0210 ARE05A: IOLIST A0$,A1$
0240 ARM06A: IOLIST D0$,D1$,D[ALL]
0250 ARM02A: IOLIST X0$,X1$,X[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0285 ARM01A: IOLIST *,CUSTNAME$(1)
0310 ARE15A: IOLIST W0$,W1$,W[ALL]
0315 ARM10A: IOLIST T1$,T[ALL]
0320 ARM10D: IOLIST *,S1$
0370 ART01A: IOLIST X0$(1),X[ALL]
0380 ART11A: IOLIST U0$,U1$,U[ALL]
0385 ART43A: IOLIST X4$
0390 ART53A: IOLIST X5$
0400 REM " --- Parameters
0405 DIM P[6],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 FOR X=0 TO 4
0435 LET G[X]=NUM(G2$(X*2+1,2),ERR=0440)
0440 NEXT X
0445 LET G[2]=FNYY_YEAR(G2$(5,2))
0450 LET P[0]=NUM(P2$(1,2)),P[2]=NUM(P4$(1,2)),P[3]=FNYY_YEAR(P4$(3,2))
0470 CALL "SYC.VA",N2$,INFO$[ALL]
0475 LET GL$=INFO$[9]
0500 REM " --- Initializations"
0510 DIM W[2],D[21]
0550 CALL "SYC.PA",SYS01_DEV,P[2],P[3],X$,G9$,STATUS
0600 REM " --- Additional File Opens"
0610 IF GL$<>"Y" THEN GOTO 0700
0630 CALL "SYC.DA",1,12,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0640 IF STATUS THEN GOTO 9900
0650 LET GLM01_DEV=CHANNELS[12],GLT04_DEV=CHANNELS[13],GLT05_DEV=CHANNELS[14]
0700 REM " --- Background
0710 PRINT @(0,3),'CE',
0800 REM " --- Options
0810 LET V4$="Are You Ready to Update the Invoice Register"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0900 REM 
0910 CALL "SYC.NB","Updating",7,COLUMN
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
1000 REM " --- Init Read
1020 READ (ARE05_DEV,KEY=N0$,DOM=1100)
1100 REM " --- Main Read
1120 READ (ARE05_DEV,END=4000)IOL=ARE05A
1140 IF A0$(1,2)<>N0$ THEN GOTO 4000
1160 PRINT @(COLUMN,11),A0$(3,7),
1170 LET T0=0,T1=0
1190 IF A1$(17,1)="V" THEN GOTO 3900
1200 REM " --- Get Dist
1210 IF A1$(13,2)=S0$ THEN GOTO 1300
1220 DIM S1$(G[4],"0")
1230 LET S0$=A1$(13,2)
1240 FIND (ARM10_DEV,KEY=N0$+"D"+S0$,DOM=1300)IOL=ARM10D
1250 LET S1$=S1$(1,G[4])
1300 REM " --- Get Terms
1310 IF A1$(15,2)=T0$ THEN GOTO 1400
1320 DIM T1$(30),T[2]
1330 LET T0$=A1$(15,2)
1340 FIND (ARM10_DEV,KEY=N0$+"A"+T0$,DOM=1400)IOL=ARM10A
1400 REM " --- Get customer"
1410 DIM CUSTNAME$(30)
1420 LET CUSTNAME$(1)="(Not On File)"
1430 FIND (ARM01_DEV,KEY=N0$+A1$(7,6),DOM=2000)IOL=ARM01A
2000 REM " --- Get Detail
2010 READ (ARE15_DEV,KEY=A0$(1,9),DOM=2020)
2020 LET K$=KEY(ARE15_DEV,END=3000)
2040 IF K$(1,9)<>A0$(1,9) THEN GOTO 3000
2060 READ (ARE15_DEV,END=3000)IOL=ARE15A
2070 LET T0=T0+W[0],T1=T1+W[2]
2300 REM " --- GL Dist
2310 LET ACCOUNT$=W1$(1,G[4]),WHEN$=A1$(1,6),MEMO$=W1$(7+G[4],30)
2320 LET REF1$=FNF$(A1$(7,P[0]),M0$),REF2$=A0$(3,7),REF3$=W0$(10,3)
2330 LET AMOUNT=-W[2],UNITS=-W[0]
2340 GOSUB GLPOST
2900 REM " --- Remove
2920 REMOVE (ARE15_DEV,KEY=K$,DOM=2990)
2990 GOTO 2020
3000 REM " --- Update Customer
3010 LET ACCOUNT$=S1$,AMOUNT=T1,UNITS=T0,MEMO$=CUSTNAME$,REF3$=""
3015 GOSUB GLPOST
3020 LET X0$=N0$+A1$(7,6)+"  "
3040 DIM X1$(40),X[10]
3060 EXTRACT (ARM02_DEV,KEY=X0$,DOM=3080)IOL=ARM02A
3080 LET X[1]=X[1]+T1
3085 LET Q9=X[0]+X[1]+X[2]+X[3]+X[4]+X[5]; REM "Current Credit
3090 WRITE (ARM02_DEV,KEY=X0$)IOL=ARM02A
3100 REM " --- Create Dates
3110 IF POS(" "<>A1$(1,6)) THEN LET TMP$=FND$(FNN$(A1$(1,6))) ELSE LET TMP$=FND$(FNN$(F0$(18,6)))
3120 LET V=T[1]
3140 CALL "SYC.CD",T1$(26,1),TMP$,V,V$,STATUS
3150 LET Y0$=V$,V=T[2]
3160 CALL "SYC.CD",T1$(26,1),TMP$,V,V$,STATUS
3170 LET Y1$=V$
3200 REM " --- Create Invoice
3210 DIM X0$(50),X1$(11)
3220 LET X0$(1)=N0$+"  "+A1$(7,6)+A0$(3,7)+"00"+A1$(13,4)+FND$(FNN$(A1$(1,6)))+Y0$+Y1$+"S"+"     "
3230 DIM X[3],U[5]
3240 EXTRACT (ART01_DEV,KEY=X0$(1,19),DOM=3250)IOL=ART01A
3243 GOSUB 6200
3245 GOTO 3270
3250 LET X[0]=X[0]+T1,X[1]=X[1]+X[0]*T[0]/100
3260 WRITE (ART01_DEV,KEY=X0$(1,19))IOL=ART01A
3270 REM " --- Write Sales History Record
3275 DIM D0$(8),D1$(16),D[21]
3277 LET D0$(1)=X0$(1,2)+X0$(5,6)
3280 EXTRACT (ARM06_DEV,KEY=D0$(1,8),DOM=3290)IOL=ARM06A
3290 IF X0$(24,3)<=G9$ THEN LET D1$(1,3)=X0$(24,3),D[0]=D[0]+T1,D[1]=D[1]+T1 ELSE LET D1$(1,3)=X0$(24,3),D[8]=D[8]+T1
3300 IF X0$(24,3)<=G9$ THEN IF D[18]<Q9 THEN LET D[18]=Q9; REM "MTD High Credit
3310 IF X0$(24,3)<=G9$ THEN IF D[19]<Q9 THEN LET D[19]=Q9; REM "YTD High Credit
3320 IF X0$(24,3)>G9$ THEN IF D[21]<Q9 THEN LET D[21]=Q9; REM "NEXT MO High Credit
3330 WRITE (ARM06_DEV,KEY=X0$(1,2)+X0$(5,6))IOL=ARM06A
3500 REM " --- Write invoice xref records"
3510 LET X4$=X0$(1,4)+X0$(11,7)+X0$(5,6)
3520 WRITE (ART43_DEV,KEY=X4$)IOL=ART43A
3530 LET X5$=X0$(1,4)+X0$(24,3)+X0$(11,7)+X0$(5,6)
3540 WRITE (ART53_DEV,KEY=X5$)IOL=ART53A
3900 REM " --- Delete
3920 REMOVE (ARE05_DEV,KEY=A0$,DOM=3990)
3990 GOTO 1100
4000 REM " --- End
4010 IF GL$="Y" THEN CALL "GLC.CA",STATUS
4020 CALL "SYC.BB",STATUS
4100 GOTO 9900
6200 REM " --- Create Adjustment in ART-01
6210 IF X[0]<>0 THEN READ (ART11_DEV,KEY=X0$(1,17),DOM=6260)
6220 LET X[0]=T1,X[1]=X[0]*T[0]/100,X0$(33,1)="I"
6240 WRITE (ART01_DEV,KEY=X0$(1,19))IOL=ART01A
6250 RETURN 
6260 REM " --- Make Adjustment Record in ART-11
6280 LET SEQ=0
6290 DIM U[1]
6300 LET K7$=KEY(ART11_DEV,END=6370)
6310 IF K7$(1,17)<>X0$(1,17) THEN GOTO 6370
6320 READ (ART11_DEV,KEY=K7$)IOL=ART11A
6330 LET SEQ=NUM(K7$(18,2))
6340 IF U1$(1,1)<>" " THEN GOTO 6300
6350 LET U[0]=U[0]+T1,U[1]=0,U1$(1,11)=A1$(17,1)+FND$(FNN$(A1$(1,6)))
6360 GOTO 6430
6370 REM " --- End of Detail 
6380 LET SEQ=SEQ+1
6390 IF SEQ>99 THEN READ (ART11_DEV,KEY=X0$(1,17)+"99")IOL=ART11A; LET U1$(1,1)=" "; GOTO 6350
6400 DIM U1$(11),U[1]
6410 LET K7$=X0$(1,17)+STR(SEQ:"00"),U0$=K7$,U[0]=T1
6420 LET U1$(1,11)=A1$(17,1)+FND$(FNN$(A1$(1,6)))
6430 WRITE (ART11_DEV,KEY=K7$)IOL=ART11A
6440 RETURN 
6900 REM " --- Standard G/L Posting Routine"
6910 GLPOST: 
6920 IF GL$<>"Y" THEN GOTO 6990
6950 CALL "GLC.AA",GLM01_DEV,GLT04_DEV,GLT05_DEV,ACCOUNT$,WHEN$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6990 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8080 DEF FNN$(Q$)=Q$(3,4)+Q$(1,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "WOE - Closed Work Order Entry"
0020 REM "Program WOE.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "17Jan97 JJD: Recalculate standards correctly (4322,4325,4330,4332,4335,4350,4352,4355,8300-8340)"
0032 REM "16Apr97 JWK: Wrong key on partial close causes file expand (6480,6500)
0033 REM "30Oct98 WGH: Prevent Error 13 on Line 6480 when inventory is not lotted/serialized (630)
0034 REM "31Dec98 WGH: Calculate Total Units with greater precision (4332)
0040 REM 
0050 SETERR 9000
0060 ON O9 GOTO 0070,0840
0070 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=18
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-01",FILES$[2]="IVM-01",FILES$[3]="IVM-03"
0120 LET FILES$[4]="WOE-04",FILES$[5]="SYS-01",FILES$[6]="IVM-02"
0125 LET FILES$[7]="WOT-01",FILES$[8]="WOT-21",FILES$[9]="WOT-31"
0130 LET FILES$[10]="IVM-10",FILES$[11]="WOE-02",FILES$[12]="WOE-22"
0135 LET FILES$[13]="WOE-32",FILES$[14]="WOM-10"
0150 CALL "SYC.DA",1,1,14,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM03_DEV=CHANNELS[3]
0165 LET WOE04_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5],IVM02_DEV=CHANNELS[6]
0170 LET WOT01_DEV=CHANNELS[7],WOT21_DEV=CHANNELS[8],WOT31_DEV=CHANNELS[9]
0175 LET IVM10_DEV=CHANNELS[10],WOE02_DEV=CHANNELS[11],WOE22_DEV=CHANNELS[12]
0180 LET WOE32_DEV=CHANNELS[13],WOM10_DEV=CHANNELS[14]
0200 REM " --- IOLISTs"
0210 WOE01A: IOLIST A0$(1),A1$(1),A[ALL]
0220 IVM01A: IOLIST D0$,D1$(1),D2$
0230 WOS01A: IOLIST X$,P1$,P2$,P9$,P4$,M0$,M1$,M2$,M3$
0240 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0250 IVS01A: IOLIST X$,X$,P2$,P3$
0260 GLS01A: IOLIST X$,X$,G2$,G3$
0270 WOREQ: IOLIST W0$,W1$,W[ALL]
0280 WOE04B: IOLIST B0$
0290 WOE06A: IOLIST W0$,W1$,W[ALL]
0300 WOTRAN: IOLIST W0$,W1$,W[ALL]
0310 POE12A: IOLIST L0$,L1$,L2$,L3$,L4$,L5$,L6$,L[ALL]
0320 POE11A: IOLIST L0$,L1$,L2$,L3$,L4$,L5$,L6$,L[ALL]
0330 BMS01A: IOLIST *,*,M4$,M5$,M4,M5
0340 ARS01A: IOLIST *,*,X$,*,*,P8$
0350 APS01A: IOLIST *,*,X$,*,*,P7$
0360 PRS01A: IOLIST *,*,X$,*,*,P6$
0370 WOM10A: IOLIST Y0$
0375 WOM10A2: IOLIST X1$
0385 IVM02A: IOLIST *,*,*,*,*,*,*,*,*,*,*,*,*,*,A2
0400 REM " --- Parameters"
0405 DIM P[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N4$=F5$
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0422 LET BM$=P9$(1,1),AR$=P9$(2,1),PO$=P9$(3,1),PR$=P9$(4,1)
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 IF BM$<>"Y" THEN GOTO 0440
0435 FIND (SYS01_DEV,KEY=N0$+"BM00",DOM=0440)IOL=BMS01A
0440 IF AR$<>"Y" THEN GOTO 0450
0445 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=0450)IOL=ARS01A
0447 LET P8=NUM(X$(1,2))
0450 IF PO$<>"Y" THEN GOTO 0460
0455 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=0460)IOL=APS01A
0457 LET P7=NUM(X$(1,2))
0460 IF PR$<>"Y" THEN GOTO 0470
0465 FIND (SYS01_DEV,KEY=N0$+"PR00",DOM=0470)IOL=PRS01A
0467 LET P6=NUM(X$(1,2))
0470 IF PR$<>"Y" AND P9$(12,1)="P" THEN LET M5=NUM(P9$(19,6))
0475 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0480 LET P[0]=NUM(P2$(1,2)),IVS01_PRECISION=NUM(P2$(5,1)),P0=P[0],P1=IVS01_PRECISOIN
0482 LET P[3]=NUM(P4$(1,2)),P[4]=FNYY_YEAR(P4$(3,2)),P[3]=P[3]-1
0484 IF P[3]=0 THEN LET P[3]=NUM(G2$(1,2)),P[4]=P[4]-1
0486 CALL "SYC.PA",SYS01_DEV,P[3],P[4],X$,G9$,STATUS
0488 IF STATUS THEN GOTO 9900
0490 LET GL$="N",STATUS=0,SOURCE$=PGM(-2)
0492 CALL "GLC.BA",ERR=0494,SOURCE$,N2$,GLW11$,GL$,STATUS
0494 IF STATUS<>0 THEN GOTO 9900
0500 REM " --- Init Data"
0505 PRECISION IVS01_PRECISION
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," ")
0520 DIM A0$(75),A1$(138),A[11],W[13],DESC[3],L[12]
0525 DIM PARAMS[0],PARAMS$[4],ITEMS$[3],REFS$[11],REFS[5],IVFILES[44]
0526 LET PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3),PARAMS$[2]=P2$,PARAMS$[3]=P3$
0527 LET PARAMS$[4]=P4$,IVFILES[0]=SYS01_DEV,IVFILES[1]=IVM01_DEV
0528 LET IVFILES[2]=IVM02_DEV
0530 LET K0=1,K9=11,A0$(1)=N0$,M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$)
0550 FOR X=1 TO 3
0555 LET DESC[X]=NUM(P2$((X-1)*2+9,2))
0560 NEXT X
0600 REM " --- Open Additional Files"
0610 IF PO$<>"Y" THEN GOTO 0630
0620 LET FILES$[15]="POE-12",FILES$[16]="POE-11"
0630 IF POS(P3$(17,1)="LS")=0 THEN GOTO 0650
0640 LET FILES$[17]="IVM-07",FILES$[18]="WOE-06"
0650 CALL "SYC.DA",1,15,18,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0655 IF STATUS THEN GOTO 9900
0660 LET POE12_DEV=CHANNELS[15],POE11_DEV=CHANNELS[16]
0665 LET IVM07_DEV=CHANNELS[17],WOE06_DEV=CHANNELS[18]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,10,0
0720 PRINT 'SB',@(0,3),"WO Number:",@(21),"Type:",@(54),"Status:",@(0,5),"Bill Number:",@(0,6),"Description:",@(52,4),"Category:",@(51,5),"Open Date:",@(57,6),"U/M:",@(52,7),"Revision:",@(49,8),"Closed Date:",@(0,9),"Sched Production:",@(30),"Closed Qty:",@(52),"Closed Value:",
0740 GOSUB 5300
0790 PRINT 'SF',
0800 REM " --- Options"
0820 GOTO 0900
0830 PRINT 'SB',
0840 GOSUB 5300
0850 PRINT 'SF',
0860 GOTO 1000
0900 REM " --- Init File"
0920 READ (WOE01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(WOE01_DEV,END=1050)
1030 IF V2$(1,2)<>A0$(1,2) THEN LET V2$=""
1040 IF LEN(V2$)>5 THEN LET V2$=V2$(5) ELSE LET V2$=""
1050 LET V0$="Z",V1$="CER",V3$="",V0=7,V1=11,V2=3,V4$="Enter A Work Order Number (<Enter>=Next/<F3>=Lookup)"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 9900
1075 IF V3=3 THEN GOSUB 6600
1080 IF V$="" OR POS("0"<>V$)=0 THEN GOTO 0900
1085 PRINT @(V1,V2),V$,
1090 LET A0$(5)=V$
1200 REM " --- Find Key"
1210 LET CLOSED_NO_REOPEN=0
1220 FIND (WOE01_DEV,KEY=A0$(1,K9),DOM=1010)IOL=WOE01A
1230 DIM Y0$(30)
1240 FIND (WOM10_DEV,KEY=N0$+"A"+A0$(12,2),DOM=1290)IOL=WOM10A
1290 GOSUB 5000
1300 REM " --- Verify Flags...
1310 FIND (WOE04_DEV,KEY=N0$+"  "+"B"+A0$(5,7),DOM=1400)
1320 LET V4$="This Work Order Has Been Scheduled To Be Closed. Reopen It"
1325 CALL "SYC.YN",0,V4$,0,V$,V3
1340 IF V$<>"YES" THEN LET CLOSED_NO_REOPEN=1; GOTO 1400
1350 REMOVE (WOE04_DEV,KEY=N0$+"  "+"B"+A0$(5,7),DOM=1360)
1360 LET A1$(91,4)="",A[4]=0,A[5]=0
1370 WRITE (WOE01_DEV,KEY=A0$(1,11))IOL=WOE01A
1380 GOSUB 6100
1390 GOTO 1000
1400 REM " --- Check Closed & Updated
1420 IF A0$(15,1)="O" THEN GOTO 1500
1425 DIM MESSAGE$[1]
1430 IF A0$(15,1)="P" THEN LET MESSAGE$[0]="Cannot Close a Planned Work Order (<Enter>=Continue)"
1435 IF A0$(15,1)="Q" THEN LET MESSAGE$[0]="Cannot Close a Work Order Quote (<Enter>=Continue)"
1440 IF A0$(15,1)="C" THEN LET MESSAGE$[0]="This Work Order Has Been Closed and Updated (<Enter>=Continue)"
1450 CALL "SYC.XA",3,MESSAGE$[ALL],0,22,-1,V$,V3
1460 GOTO 1000
1500 REM " --- Calc Std=A0, Act=A1, Last Op Code Seq#=LOP$, Complete Qty=A9
1505 LET A0=0,A1=0,LOP$="",A9=A[4]
1510 IF A0$(14,1)<>"I" THEN GOTO 1525
1515 FIND (IVM02_DEV,KEY=N0$+A0$(54),DOM=1525)IOL=IVM02A
1520 LET A0=A[0]*A2
1525 IF P3$(8,1)<>"S" AND X1$(27,1)="S" THEN LET A0=0
1530 LET WOREQ_DEV=WOE02_DEV
1535 READ (WOREQ_DEV,KEY=A0$(1,11),DOM=1540)
1540 LET K6$=KEY(WOREQ_DEV,END=1570)
1545 IF K6$(1,11)<>A0$(1,11) THEN GOTO 1570
1550 READ (WOREQ_DEV,KEY=K6$)IOL=WOREQ
1555 IF A0$(14,1)<>"I" OR (P3$(8,1)<>"S" AND X1$(27,1)="S") THEN LET A0=A0+W[3]
1560 IF W0$(12,1)="A" THEN LET LOP$=W0$(13,3)
1565 GOTO 1540
1570 IF WOREQ_DEV=WOE02_DEV THEN LET WOREQ_DEV=WOE22_DEV; GOTO 1535
1575 IF WOREQ_DEV=WOE22_DEV THEN LET WOREQ_DEV=WOE32_DEV; GOTO 1535
1580 IF P3$(8,1)<>"S" AND X1$(27,1)="S" THEN LET A2=0
1585 LET WOTRAN_DEV=WOT01_DEV
1590 READ (WOTRAN_DEV,KEY=A0$(1,11),DOM=1595)
1595 LET K9$=KEY(WOTRAN_DEV,END=1625)
1600 IF K9$(1,11)<>A0$(1,11) THEN GOTO 1625
1605 READ (WOTRAN_DEV,KEY=K9$)IOL=WOTRAN
1610 LET A1=A1+W[2]
1615 IF W0$(15,1)="O" AND W1$(1,3)=LOP$ THEN IF CLOSED_NO_REOPEN=0 THEN LET A9=A9+W[5]
1620 GOTO 1595
1625 IF WOTRAN_DEV=WOT01_DEV THEN LET WOTRAN_DEV=WOT21_DEV; GOTO 1590
1630 IF WOTRAN_DEV=WOT21_DEV THEN LET WOTRAN_DEV=WOT31_DEV; GOTO 1590
1700 REM " --- Calc New Amounts
1710 IF A0$(14,1)="I" AND (P3$(8,1)="S" OR X1$(27,1)<>"S") THEN GOTO 1730
1720 IF A[0]<>0 THEN LET A2=A0/A[0] ELSE LET A2=0
1730 IF A[0]<>0 THEN LET A3=A1/A[0] ELSE LET A3=0
1740 IF A[5]=0 THEN IF Y0$(27,1)<>"A" THEN LET A[5]=A2
1750 LET A[4]=A9-A[2]
1760 IF A[4]<0 THEN LET A[4]=0
1770 GOSUB 5200
1800 REM " --- Now Close It
1810 IF A1$(94,1)="Y" THEN GOTO 4000
1820 FIND (WOE04_DEV,KEY=N0$+"  "+"B"+A0$(5,7),DOM=1830); GOTO 1900
1830 LET V4$="Do You Wish To Close This Work Order"
1840 CALL "SYC.YN",1,V4$,1,V$,V3
1850 IF V$="N" THEN GOTO 1000
1860 IF V3=4 THEN GOTO 1000
1900 REM " --- Date
1910 LET V0$="D",V1$="",V2$=A1$(91,3),V3$="",V4$="",V0=3,V1=19,V2=12,I0=0
1920 IF FNP$(A1$(91,3))="" THEN LET V2$=F0$(7,3); PRINT @(V1,V2),FNB$(V2$)
1930 GOSUB 7000
1940 IF V3=4 THEN GOTO 4000
1950 IF FNP$(V$)="" THEN GOTO 1900
1960 IF GL$="Y" THEN CALL "GLC.EA",V$,"Y",PERIOD$,YEAR$,STATUS
1970 IF STATUS>99 THEN GOTO 1000
1980 IF V$>G9$ THEN GOTO 2020
1990 PRINT @(0,0),'RB',
2000 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Close Date May Not Be In A Prior Period (<Enter>=Correct)",V0=1,V1=FNV(V4$),V2=22
2010 GOSUB 7000; GOTO 1900
2020 LET A1$(91,3)=V$
2030 GOTO 3200
2100 REM " --- Close Quantity
2110 LET V0$="N",V1$="C",V2$=STR(A[4]),V3$=M2$,V4$="Enter Quantity To Close This Time",V0=5,V1=19,V2=13
2120 GOSUB 7000
2130 LET A[4]=V
2140 IF A[4]+A[2]=A[0] AND A1$(94,1)=" " THEN LET A1$(94,1)="Y"
2150 IF A[4]=0 THEN GOTO 2170
2160 IF Y0$(27,1)="A" AND A1$(94,1)="Y" THEN LET A[5]=(A1-A[3])/A[4]
2170 IF A[4]+A[2]=A[0] THEN LET I0=I0+1; PRINT @(19,15),A1$(94,1),
2180 PRINT @(19,16),A[5]:M3$,@(19,17),A[4]*A[5]:M1$
2190 GOTO 3200
2300 REM " --- Complete?
2310 LET V0$="Y",V1$="CR",V2$=A1$(94,1),V3$="YN",V4$="Does This Complete The Work Order?",V0=1,V1=19,V2=15
2320 GOSUB 7000
2330 IF V$<>"Y" THEN GOTO 2410
2340 GOSUB 6400
2350 IF OPEN$="N" THEN GOTO 2410
2360 DIM MESSAGE$[1]
2370 LET MESSAGE$[0]="There Are Purchase "+OPEN$+"s Still Open For The Subcontracts On This Work"
2380 LET MESSAGE$[1]="Order. You Cannot Complete The Work Order (<Enter> To Continue)"
2390 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
2400 LET V$="N"; PRINT @(V1,V2),V$,
2410 LET A1$(94,1)=V$
2420 GOTO 3200
2500 REM " --- Estimated Cost (If Actual Only!!)
2510 IF Y0$(27,1)<>"A" THEN GOTO 3200
2520 IF A1$(94,1)="Y" THEN GOTO 2700
2530 IF A[5]=0 AND A[4]<>0 THEN LET A[5]=(A1-A[3])/A[4]
2540 LET V0$="N",V1$="C",V2$=STR(A[5]),V3$=M3$,V4$="Enter Estimated Unit Cost Per Each",V0=5,V1=19,V2=16
2550 PRINT @(V1,V2),A[5]:V3$
2560 GOSUB 7000
2570 LET A[5]=V
2580 PRINT @(19,17),A[4]*A[5]:M1$
2590 GOTO 3200
2700 REM " --- Re-Calc Avg Cost For Actual Close Qty
2710 IF A[0]=A[4] THEN GOTO 2760
2720 IF A[4]=0 THEN GOTO 2760
2730 LET A[5]=A1/A[4]
2740 IF A1$(94,1)="Y" AND Y0$(27,1)="A" THEN LET A[5]=(A1-A[3])/A[4]
2750 PRINT @(19,16),A[5]:M3$,@(19,17),A[4]*A[5]:M1$,@(65,18),A3:M3$
2760 GOTO 3200
2800 REM " --- Recalculate?
2810 IF A[0]=A[2]+A[4] OR A1$(94,1)<>"Y" THEN GOTO 3200
2820 LET V0$="Y",V1$="CR",V2$=A1$(95,1),V3$="YN",V4$="Recalculate Standards For Completed Amount Only (Y/N)?",V0=5,V1=19,V2=18
2830 GOSUB 7000
2840 LET A1$(95,1)=V$
2850 GOTO 3200
3200 REM " --- Next Field"
3210 IF V3=4 THEN GOTO 4000
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3270
3250 LET I0=I0+1
3270 ON I0 GOTO 1900,2100,2300,2500,2800,4000
4000 REM " --- Options"
4010 IF A1$(94,1)=" " THEN LET I0=2; GOTO 2300
4020 IF A1$(94,1)="N" AND A[4]=0 AND A[5]=0 THEN GOTO 1300
4030 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/#)?",V0=1,V1=FNV(V4$),V2=22
4040 GOSUB 7000
4050 LET I=NUM(V$,ERR=4070)
4060 IF I>0 THEN LET I0=I-2; GOTO 3200
4070 IF V$="OPEN" THEN GOTO 1350
4080 IF V$="" THEN LET V$="Y"
4090 ON POS(V$="YN") GOTO 4000,4100,1900
4100 REM " --- Write"
4110 WRITE (WOE01_DEV,KEY=A0$(1,K9))IOL=WOE01A
4120 LET B0$=N0$+"  "+"B"+A0$(5,7)
4125 WRITE (WOE04_DEV,KEY=B0$)IOL=WOE04B
4130 REM " --- Recalc Standards
4140 IF A1$(95,1)<>"Y" THEN GOTO 4430
4150 LET V4$="Will Recalculate Standards For "+STR(A[2]+A[4])+" Items, Continue"
4160 CALL "SYC.YN",0,V4$,0,V$,V3
4170 IF V$<>"YES" THEN GOTO 4000
4180 IF A0$(14,1)<>"I" THEN GOTO 4270
4190 REM " --- Adjust On Order Quantity If Necessary "
4200 IF A[0]-(A[2]+A[4])=0 THEN GOTO 4270
4210 LET ACTION$="OO",ITEMS$[0]=N0$,ITEMS$[1]=A0$(54,2),ITEMS$[2]=A0$(56,20)
4240 LET ON_ORDER_ADJ=A[0]-(A[2]+A[4])
4250 LET REFS[0]=-(ON_ORDER_ADJ)
4260 CALL "IVC.UA",ACTION$,IVFILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEMS$[ALL],REFS$[ALL],REFS[ALL],STATUS
4265 REM " --- Adjust Standards For Closed Incomplete WOs "
4270 LET A[0]=A[2]+A[4]
4280 LET WOREQ_DEV=WOE02_DEV
4290 READ (WOREQ_DEV,KEY=A0$(1,11),DOM=4300)
4300 LET K$=KEY(WOREQ_DEV,END=4390)
4310 IF K$(1,11)<>A0$(1,11) THEN GOTO 4390
4320 READ (WOREQ_DEV,KEY=K$)IOL=WOREQ
4322 IF W0$(12,1)="B" AND W[4]=0 THEN LET W[4]=1
4325 IF W0$(12,1)="A" AND W[5]=0 THEN LET W[5]=1
4330 IF W0$(12,1)="A" THEN LET W[2]=FNOP(A[0],W[4],W[5],W[8])
4332 IF W0$(12,1)="B" THEN LET W[0]=FNQTYNEW(W[5],W[6],W[4],W[8],A[6]),W[1]=FNQTYNEW(W[5],W[6],W[4],W[8],A[6])*W[7],W[2]=A[0]*FNQTYNEW(W[5],W[6],W[4],W[8],A[6])
4335 IF W0$(12,1)="C" THEN LET W[1]=W[0]*W[4],W[2]=W[0]*A[0]
4340 PRECISION 2
4350 IF W0$(12,1)="A" THEN LET W[3]=FNOPDOL(A[0],W[4],W[6],W[7],W[5],A[6],W[8])
4352 IF W0$(12,1)="B" THEN LET W[3]=W[2]*W[7]
4355 IF W0$(12,1)="C" THEN LET W[3]=W[1]*A[0]
4360 PRECISION IVS01_PRECISION
4370 WRITE (WOREQ_DEV,KEY=K$)IOL=WOREQ
4380 GOTO 4300
4390 IF WOREQ_DEV=WOE02_DEV THEN LET WOREQ_DEV=WOE22_DEV; GOTO 4290
4400 IF WOREQ_DEV=WOE22_DEV THEN LET WOREQ_DEV=WOE32_DEV; GOTO 4290
4410 LET A1$(95,1)="N"
4420 WRITE (WOE01_DEV,KEY=A0$(1,K9))IOL=WOE01A
4430 IF A1$(96,1)="Y" THEN RUN "WOE.EB"
4440 GOTO 1000
5000 REM " --- Display"
5005 DIM D1$(60)
5010 FIND (IVM01_DEV,KEY=N0$+A0$(56),DOM=5020)IOL=IVM01A
5020 DIM X1$(30); LET X1$(6,20)="** Not On File **",I9$="N"
5040 FIND (WOM10_DEV,KEY=N0$+"A"+A0$(12,2),DOM=5060)IOL=WOM10A2
5050 LET I9$=A0$(14,1)
5060 PRINT @(27,3),A0$(12,2),"  ",X1$(6,20),@(63),A0$(15,1),@(63,4),A0$(14,1),@(63,5),FNB$(A0$(16,3)),@(63,6),A0$(50,2),@(63,7),A0$(52,2)
5065 IF A0$(15,1)="O" THEN PRINT @(65,3),"**Open**" ELSE PRINT @(65,3),"**Closed**"
5070 IF I9$="I" THEN PRINT @(65,4),"Inventory",@(14,5),A0$(56) ELSE IF I9$="N" THEN PRINT @(65,4),"Non-Stocking", ELSE IF I9$="R" THEN PRINT @(65,4),"Recurring"
5075 IF A0$(15,1)="C" OR POS(" "<>A0$(31,3))>0 THEN PRINT @(65,8),FNB$(A0$(31,3))
5080 IF I9$<>"I" THEN PRINT @(14,6),A1$(1,30),@(14,7),A1$(31,30),
5085 IF I9$="I" THEN PRINT @(14,6),D1$(1,MIN(DESC[1],30)),@(14,7),D1$(DESC[1]+1,MIN(DESC[2],30)),
5090 PRINT @(14,8),A1$(61,30),@(18,9),A[0]:M2$,@(42,9),A[2]:M2$,@(66,9),A[3]:M1$
5100 RETURN 
5200 REM " --- Display Closed Data
5220 PRINT @(19,12),FNB$(A1$(91,3)),@(19,13),A[4]:M2$,@(19,16),A[5]:M3$,@(19,17),A[4]*A[5]:M1$,@(19,15),A1$(94,1),@(19,18),A1$(95,1),@(65,12),A[0]:M2$,@(65,13),A[2]:M2$,@(65,15),A2:M3$,@(65,16),A0:M1$,@(65,18),A3:M3$,@(65,19),A1:M1$
5290 RETURN 
5300 REM " --- Display Bottom Half of Screen
5310 PRINT 'SB',@(7,12),"Close Date:",@(8,13),"Close Qty:",@(1,16),"Closed Unit Cost:",@(5,17),"Closed Value:",@(9,15),"Complete:",@(1,18),"Recalc Standards:",
5320 PRINT @(42,12),"Sched Production Qty:",@(48,13),"Closed to Date:",@(44,15),"Standard Unit Cost:",@(45,16),"Value at Standard:",@(46,18),"Actual Unit Cost:",@(39,19),"Current Value at Actual:",
5390 RETURN 
6100 REM " --- Delete All Serial/Lot #'s
6110 IF POS(P3$(17,1)="LS")=0 THEN GOTO 6300
6190 READ (WOE06_DEV,KEY=A0$(1,11),DOM=6200)
6200 LET K8$=KEY(WOE06_DEV,END=6300)
6220 IF POS(A0$(1,11)=K8$)<>1 THEN GOTO 6300
6240 READ (WOE06_DEV,KEY=K8$)IOL=WOE06A
6260 LET W[4]=0,W[5]=0
6280 WRITE (WOE06_DEV,KEY=K8$)IOL=WOE06A
6290 GOTO 6200
6300 RETURN 
6400 REM " --- Check for closed P.O.'s for subcontracts"
6410 LET OPEN$="N"
6420 IF P9$(3,1)<>"Y" THEN GOTO 6590
6430 READ (WOE32_DEV,KEY=N0$+"  "+A0$(5,7)+"C",DOM=6440)
6440 LET K13$=KEY(WOE32_DEV,END=6590)
6450 IF POS(N0$+"  "+A0$(5,7)+"C"=K13$)<>1 THEN GOTO 6590
6460 READ (WOE32_DEV,KEY=K13$)IOL=WOREQ
6470 IF NUM(W1$(43,7))=0 THEN GOTO 6440
6480 READ (POE12_DEV,KEY=N0$+W1$(7,6)+W1$(43,10),DOM=6500)IOL=POE12A
6490 IF L2$(16,10)=W0$(5,7)+W0$(13,3) THEN LET OPEN$="Order"; GOTO 6590
6500 READ (POE11_DEV,KEY=N0$+W1$(7,6)+W1$(43,10),DOM=6520)IOL=POE11A
6510 IF L2$(16,10)=W0$(5,7)+W0$(13,3) THEN LET OPEN$="Requisition"; GOTO 6590
6520 GOTO 6440
6590 RETURN 
6600 REM " --- W/O Lookup
6610 PRINT @(0,12),'CE'
6630 LET V3$=P3$(6,2),V$=P9$(2,1),V7$=P7$,V8=P[0]
6635 LET V4=1,V5=2,V6=12,V7=21,V6$="O"
6640 CALL "SYC.LW",V$,V6$
6660 PRINT 'SB',
6670 GOSUB 5300
6680 PRINT 'SF',
6690 RETURN 
6700 REM " --- Enter Warehouse Here
6720 PRINT @(0,15),'SB',"Whse: ",'SF'
6730 LET V0$="S",V1$="KCE",V2$=P3$(6,2),V3$="",V4$="Enter Selected Warehouse (<F3>=Lookup)",V0=2,V1=8,V2=15
6740 GOSUB 7000
6750 IF V3<>3 THEN GOTO 6780
6760 LET V4=1,V5=8,V6=4,V7=2,V8=6,V9=20,V5$="C"
6770 CALL "SYC.LZ",V1,V2,V4,IVM10_DEV,V6,V7,V8,V9,V$,V5$,N0$
6780 IF V3=4 THEN LET V$=""; GOTO 6790
6785 LET V$=V$+"  ",V$=V$(1,2)
6786 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=6700)
6787 LET V1=11,V2=3
6790 PRINT @(0,15),'CL'
6795 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
8300 DEF FNQTYNEW(Q1,Q2,Q3,Q4,Q5)=100*Q1*Q2/(Q3*(100-Q4))/(Q5*.01)
8310 DEF FNOPNOSET(Q1,Q2)=100*(Q1/Q2)
8320 DEF FNOPNOSETDOL(Q1,Q2,Q3,Q4)=100*(Q1*(Q2+Q3)/Q4)
8330 DEF FNOP(Q1,Q2,Q3,Q4)=(Q1*Q2/Q3)+Q4
8340 DEF FNOPDOL(Q1,Q2,Q3,Q4,Q5,Q6,Q7)=100*(Q1*Q2*(Q3+Q4)/Q5)/Q6+Q7*(Q3+Q4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

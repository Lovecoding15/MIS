0010 REM "OPR - Invoice Printing (Plain Paper)"
0020 REM "Program OPR.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "04Dec96 JJD: Error 47 line 1520 when print test pattern (850)"
0032 REM "20Feb97 WGH: Use IV Price Mask for price fields (550)"
0033 REM "29Apr97 WGH: Prevent printing heading to wrong channel (5015)"
0034 REM "18May99 KJS: Prevent error one when getting job information (290,1840)
0040 REM 
0050 REM "O9$=Return program
0051 REM "O9=1 - Batch print from OPR.GA
0052 REM "O9=2 - On demand print from OPE.CE
0053 REM "O9=3 - Historical invoice print from ARM.ML
0054 REM "       ARE03_DEV=ART03_DEV
0055 REM "       ARE13_DEV=ART13_DEV
0056 REM "       ARE23_DEV=ART23_DEV
0057 REM "       ARE33_DEV=ART33_DEV
0058 REM "PRTR_DEV=Open printer channel
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists" 
0210 ARE03A: IOLIST A0$(1),A[ALL]
0220 ARE13A: IOLIST W0$,W1$(1),W[ALL]
0230 ARE20A: IOLIST Z0$,Z1$
0240 ARE23A: IOLIST T0$,T1$(1),T2$,T[ALL]
0250 ARE33A: IOLIST D0$,D1$
0260 ARM01A1: IOLIST B0$,B1$
0265 ARM01A2: IOLIST A0$,A1$
0270 ARM02A: IOLIST B0$,B1$,B[ALL]
0280 ARM03A: IOLIST C0$,C1$
0290 ARM09A: IOLIST X$,X8$(1)
0300 ARM10A: IOLIST X1$
0310 ARM10C: IOLIST Y7$,Y9$
0320 ARM10E: IOLIST Y0$(1)
0330 ARM10F: IOLIST X3$
0340 ARM10G: IOLIST *,X4$
0350 IVM01A: IOLIST *,X2$,X9$(1)
0500 REM " --- Initialize Data"
0510 PRECISION I[2]
0520 LET H0=0,L9=25,HEADINGS=0,WHEN$=F0$(7,3)
0530 DIM H9$(50,$0A$),J$(M2,"-"),J1$(40)
0540 DIM W1$(64),W[14],HEADINGS$[HEADINGS]
0550 LET PRICE_MASK$=M4$,EXT_MASK$=M1$
0560 CALL "SYC.RM",PRICE_MASK$,PRICE_MASK,11,11
0570 CALL "SYC.RM",EXT_MASK$,EXT_MASK,12,12
0700 REM " --- Background"
0710 IF O9>1 THEN GOTO 0990
0720 DIM A0$(117),A[10]
0750 GOSUB 6100
0800 REM " --- Test Pattern"
0810 CALL "SYC.YN",0,"Do You Wish To Print A Test Pattern",4,V$,V3
0820 IF V3=4 THEN GOTO 9900
0840 ON POS(V$="YN") GOTO 0800,0850,0990
0850 DIM B$(227,"X"),A0$(117,"X"),W0$(33+P[0],"X"),W1$(64),C$(227,"X"),Y0$(30),X2$(64,"X"),X1$(20,"X"),X9$(62,"X")
0855 LET C$(112,10)="9999999999",A0$(5,6)="999999",D2$=X2$
0860 LET A[0]=999.99,A[1]=A[0],T0=999.99,A0$(24,6)=$646464646464$,W[6]=9999.99,W[1]=999.99,W[2]=-99999,W[3]=W[2],W[4]=W[2],P=0
0900 GOSUB 5000
0910 FOR X=1 TO 3
0920 GOSUB 5200
0930 NEXT X
0950 GOSUB 5600
0960 GOTO 0800
0990 PRINT @(0,21),'CL',@(30,21),"Printing:",
0995 IF O9>1 THEN READ (ARE03_DEV,KEY=A0$(1,20),DOM=4100)IOL=ARE03A; GOTO 1145
1000 REM " --- Initial Read"
1005 REM "P5$=Starting customer number on restart"
1020 READ (ARE04_DEV,KEY=N0$+"I  "+P5$,DOM=1030)
1100 REM " --- Main Read"
1110 IF O9>1 THEN GOTO 4100
1120 LET K1$=KEY(ARE04_DEV,END=4000)
1125 READ (ARE04_DEV)
1130 IF K1$(1,3)<>N0$+"I" THEN GOTO 4000
1140 EXTRACT (ARE03_DEV,KEY=K1$(1,2)+K1$(4)+"000",DOM=1200)IOL=ARE03A
1145 IF A0$(99,1)="N" THEN GOTO 1170
1150 DIM Y7$(30),Y9$(21),Z0$(56),Z1$(30); LET Y7$(7)="Invalid Receipt Code",Y9$(21,1)="C"
1155 IF ARE20_DEV=0 THEN GOTO 1170
1160 FIND (ARE20_DEV,KEY=A0$(1,17),DOM=1170,ERR=1170)IOL=ARE20A
1165 FIND (ARM10_DEV,KEY=N0$+"C"+Z0$(21,3),DOM=1170)IOL=ARM10C
1170 IF A0$(21,1)="V" THEN GOTO 1100
1172 IF O9>1 THEN GOTO 1400
1175 IF A0$(22,1)<>"I" THEN GOTO 1200
1180 IF A0$(68,1)="Y" THEN GOTO 1100
1185 IF A0$(68,1)<>"B" AND A0$(69,1)="Y" THEN GOTO 1100
1190 LET A0$(69,1)="Y"; WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
1195 GOTO 1400
1200 REMOVE (ARE04_DEV,KEY=K1$)
1210 GOTO 1100
1400 REM " --- Heading"
1410 PRINT @(40,21),FNF$(A0$(5,P[0]),M0$),"  ",A0$(42,7),
1420 DIM B$(150),B1$(155)
1440 LET B1$(1)="Not On File"
1460 READ (ARM01_DEV,KEY=N0$+A0$(5,6),DOM=1490)IOL=ARM01A1
1470 LET B$=B1$(31,72)+B1$(179,48)+B1$(103,9)
1480 CALL "SYC.AA",B$,24,5,P[2],30
1490 LET B$=B1$(1,30)+B$
1500 REM " --- Ship-To"
1510 LET C$=B$
1520 IF FNP$(A0$(102,6))="" THEN GOTO 1600
1530 DIM C1$(155)
1540 IF A0$(102,6)<>"000099" THEN GOTO 1600
1545 LET SHIPTO$=""
1550 FIND (ARE33_DEV,KEY=N0$+A0$(5,13),DOM=1560)IOL=ARE33A
1555 LET C$=D1$(31,81),C1$(1)=D1$(1,30)
1560 CALL "SYC.AA",C$,24,3,P[2],30
1570 LET C$=C1$(1,30)+C$
1580 READ (ARE03_DEV,KEY=A0$(1,20),DOM=1690)
1590 GOTO 1690
1600 LET SHIPTO$=""
1610 FIND (ARM03_DEV,KEY=N0$+A0$(5,6)+A0$(102,6),DOM=1690)IOL=ARM03A
1620 LET SHIPTO$=A0$(102,6)
1630 LET C$=C1$(31,81)
1640 CALL "SYC.AA",C$,24,3,P[2],30
1650 LET C$=C1$(1,30)+C$
1690 IF LEN(C$)<LEN(B$) THEN LET C$=C$+J1$; GOTO 1690
1700 REM " --- Terms"
1710 DIM X1$(15)
1720 FIND (ARM10_DEV,KEY=N0$+"A"+A0$(62,2),DOM=1740)IOL=ARM10A
1730 LET X1$=X1$(6,20)
1740 REM " --- Salesperson"
1745 LET X3$=""
1750 FIND (ARM10_DEV,KEY=N0$+"F"+A0$(59,3),DOM=1800)IOL=ARM10F
1760 LET X3$=X3$(7,20)
1800 REM " --- Job Name"
1810 DIM X8$(30)
1820 IF ARM09_DEV=0 THEN GOTO 1880
1830 FIND (ARM09_DEV,KEY=N0$+A0$(5,6)+A0$(86,10),DOM=1880)IOL=ARM09A
1850 GOTO 1900
1880 LET X8$(1)=A0$(86,10)
1900 REM " --- Print Heading"
1910 LET P=0
1920 GOSUB 5000
1940 LET T0=0,X6$=""
1990 READ (ARE13_DEV,KEY=A0$(1,17),DOM=2000)
2000 REM " --- Detail"
2020 LET K$=KEY(ARE13_DEV,END=3000)
2040 IF K$(1,17)<>A0$(1,17) THEN GOTO 3000
2060 READ (ARE13_DEV)IOL=ARE13A
2100 REM " --- Type"
2120 DIM Y0$(30)
2140 FIND (ARM10_DEV,KEY=N0$+"E"+W0$(21,1),DOM=2160)IOL=ARM10E
2150 LET X2$=W0$(23)
2160 IF POS(Y0$(25,1)=" SP")=0 THEN GOTO 2200
2170 DIM X9$(62)
2180 FIND (IVM01_DEV,KEY=N0$+W0$(33),DOM=2190)IOL=IVM01A
2185 GOSUB 5900
2190 LET X2$=X2$+J1$,X2$=X2$(1,42)
2200 IF Y0$(25,1)="M" AND POS(Y0$(28,1)="BI ")=0 THEN GOTO 2300
2220 GOSUB 5200
2300 REM " --- Total"
2320 LET T0=T0+W[6]
2400 REM " --- Any Serial/Lot #'s?"
2410 IF POS(I3$(17,1)="LS")=0 THEN GOTO 2900
2420 IF POS(Y0$(25,1)="PS")=0 THEN GOTO 2900
2430 IF X9$(19,1)<>"Y" THEN GOTO 2900
2500 REM " --- OK, There Are Lots"
2505 DIM T1$(20),T[2]; LET T$="",T9=0
2510 READ (ARE23_DEV,KEY=W0$(1,20),DOM=2520)
2520 LET K9$=KEY(ARE23_DEV,END=2600)
2540 IF K9$(1,20)<>W0$(1,20) THEN GOTO 2600
2560 READ (ARE23_DEV)IOL=ARE23A
2570 IF T[0]=0 THEN GOTO 2520
2580 GOSUB 5400
2585 LET T9=T9+T[1]
2590 GOTO 2520
2610 IF T9>=W[4] THEN GOTO 2660
2620 DIM T1$(20,"_"),T[2]
2630 FOR Y=1 TO W[4]-T9
2635 IF I3$(17,1)="L" THEN LET Y=W[4]-T9
2640 GOSUB 5400
2650 NEXT Y
2660 IF L+1>L9 THEN GOTO 2700
2670 PRINT (PRTR_DEV)""
2680 LET L=L+1
2900 GOTO 2000
3000 REM " --- Total"
3020 GOSUB 5600
3080 IF O9=1 THEN LET A0$(68,1)="B"; WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A; GOTO 1100
4000 REM " --- Totals"
4005 CLOSE (PRTR_DEV,ERR=4010)
4010 IF O9=2 THEN GOSUB 4500; GOTO 4085
4015 IF O9=3 THEN GOTO 4100
4020 LET V0$="Y",V1$="C",V2$="N",V3$="",V4$="Are You Ready To Update The Invoice File (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF V$="N" THEN GOTO 4099
4035 IF V$<>"Y" THEN GOTO 4020
4040 PRINT @(20,16),"Now Updating Invoice Print File...........",
4050 READ (ARE04_DEV,KEY=N0$+"I",DOM=4060)
4060 LET K1$=KEY(ARE04_DEV,END=4100); READ (ARE04_DEV)
4070 IF K1$(1,3)<>N0$+"I" THEN GOTO 4100
4075 EXTRACT (ARE03_DEV,KEY=K1$(1,2)+K1$(4)+"000",DOM=4060)IOL=ARE03A
4080 IF A0$(68,1)<>"B" THEN GOTO 4060
4085 LET A0$(68,2)="YN"
4090 WRITE (ARE03_DEV,KEY=A0$(1,20))IOL=ARE03A
4095 IF O9=1 THEN GOTO 4060
4100 REM " --- Close Files"
4105 CLOSE (PRTR_DEV,ERR=4110)
4110 IF O9<>3 THEN GOTO 4180
4120 READ (ARE03_DEV,KEY=A0$(1,10),DOM=4130)
4130 READ (ARM01_DEV,KEY=A0$(1,2)+A0$(5,6))IOL=ARM01A2
4140 READ (ARM02_DEV,KEY=A0$+"  ")IOL=ARM02A
4190 RUN O9$
4500 REM " --- Cash Box Open"
4502 IF POS(" "<>R1$(17,2))=0 THEN GOTO 4590
4505 LET CASHBOX_DEV=UNT; OPEN (CASHBOX_DEV,ERR=4590)R1$(17,2)
4510 FOR X=1 TO POS(" "<>R1$(1,8),-1) STEP 2
4511 IF POS(" "<>R1$(X,2))=0 THEN GOTO 4515
4512 IF R1$(X,2)="1B" THEN PRINT (CASHBOX_DEV)'ES', ELSE PRINT (CASHBOX_DEV)ATH(R1$(X,2)),
4515 NEXT X; PRINT (CASHBOX_DEV)
4520 FOR I=1 TO NUM(R1$(29,4))
4530 PRINT (CASHBOX_DEV)ATH(R1$(21,POS(" "<>R1$(21,8),-1)))
4540 NEXT I
4550 FOR X=1 TO POS(" "<>R1$(9,8),-1) STEP 2
4551 IF POS(" "<>R1$(8+X,2))=0 THEN GOTO 4555
4552 IF R1$(8+X,2)="1B" THEN PRINT (CASHBOX_DEV)'ES', ELSE PRINT (CASHBOX_DEV)ATH(R1$(8+X,2)),
4555 NEXT X; PRINT (CASHBOX_DEV)
4560 CLOSE (CASHBOX_DEV,ERR=4590)
4590 RETURN 
5000 REM " --- Heading"
5005 LET L=0,P=P+1
5010 IF O9=3 THEN LET ORDER$=A0$(42,7),INVOICE$=A0$(11,7) ELSE LET ORDER$=A0$(11,7),INVOICE$=A0$(42,7)
5015 CALL "SYC.HA",PRTR_DEV,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5020 PRINT (PRTR_DEV)'LF',@(H0+INT((80-LEN(N1$))/2)),N1$
5025 IF O9=3 THEN PRINT (PRTR_DEV)@(H0+31),"Historical Invoice" ELSE PRINT (PRTR_DEV)""
5030 PRINT (PRTR_DEV)'LF',@(H0+54),"Invoice Number:",@(H0+70),INVOICE$,@(H0+78),P:"##",'LF',@(H0+64),"Date:",@(H0+70),FNB$(A0$(24,3))
5035 PRINT (PRTR_DEV)'LF','LF',@(H0+0),"Customer: ",FNF$(A0$(5,P[0]),M0$),@(H0+45),"Ship To: ",SHIPTO$
5040 FOR I=1 TO 6
5045 PRINT (PRTR_DEV)@(H0+7),B$(I*30-29,30),@(H0+50),C$(I*30-29,30)
5050 NEXT I
5055 PRINT (PRTR_DEV)@(H0+0),FILL(80,"-"),'LF',@(H0+0),"Order",@(H0+9),"Ordered",@(H0+18),"Custmr",@(H0+25),"Sls",@(H0+29),"Purch Ord",@(H0+42),"Ship Via",@(H0+56),"Shipped",@(H0+65),"Terms",@(H0+0),'LF',FILL(80,"-")
5060 PRINT (PRTR_DEV)@(H0+0),ORDER$,@(H0+9),FNB$(A0$(77,3)),@(H0+18),A0$(5,P[0]),@(H0+25),A0$(59,3),@(H0+29),A0$(49,10),@(H0+42),A0$(32,10),@(H0+56),FNB$(A0$(27,3)),@(H0+65),A0$(62,2)
5070 PRINT (PRTR_DEV)@(H0+0),X8$,@(H0+25),X3$,@(H0+65),X1$(1,15)
5080 PRINT (PRTR_DEV)@(H0+0),FILL(80,"-"),'LF',@(H0+0),"Order",@(H0+7),"Ship",@(H0+12),"B/O",@(H0+21),"Item Number",@(H0+62),"Unit",@(H0+71),"Extended",'LF',@(H0+0),"Qty",@(H0+7),"Qty",@(H0+12),"Qty",@(H0+19),"Description",@(H0+52),"U/M",@(H0+61),"Price",@(H0+74),"Price",'LF',@(H0+0),FILL(80,"-")
5190 RETURN 
5200 REM " --- Item Detail"
5210 IF L>L9-1 THEN GOSUB 5800
5250 IF POS(Y0$(25,1)="MO")<>0 THEN GOTO 5280
5260 PRINT (PRTR_DEV)@(H0+0),W[2],@(H0+7),W[4],@(H0+12),W[3],
5280 IF POS(Y0$(25,1)="MNO")<>0 THEN PRINT (PRTR_DEV)@(H0+19),W1$(1,40),
5300 IF POS(Y0$(25,1)=" SRDP")<>0 THEN PRINT (PRTR_DEV)@(H0+21),W0$(33),
5340 IF POS(Y0$(25,1)=" SRDNP")<>0 THEN PRINT (PRTR_DEV)@(H0+56),W[1]:PRICE_MASK$,
5360 IF POS(Y0$(25,1)="M")=0 THEN PRINT (PRTR_DEV)@(H0+68),W[6]:EXT_MASK$,
5370 IF POS(Y0$(25,1)="S")<>0 THEN PRINT (PRTR_DEV)@(H0+52),X9$(4,2),
5380 PRINT (PRTR_DEV)'LF',
5382 LET L=L+1
5385 IF LEN(X2$)>40 THEN LET X2$=X2$(1,40)
5387 IF POS(Y0$(25,1)="SP")<>0 AND L>L9-1 THEN GOSUB 5800
5390 IF POS(Y0$(25,1)="SP")<>0 THEN PRINT (PRTR_DEV)@(H0+19),X2$,'LF',
5394 IF POS(Y0$(25,1)="SP")<>0 THEN LET L=L+1
5396 RETURN 
5400 REM " --- Serial #'s Here"
5410 IF L<=L9-1 THEN GOTO 5440
5415 GOSUB 5800
5420 IF POS(Y0$(25,1)=" SP")<>0 THEN PRINT (PRTR_DEV)@(H0+24),W0$(33); LET L=L+1
5440 IF I3$(17,1)="L" THEN GOTO 5470
5450 PRINT (PRTR_DEV)@(H0+24),"S/N: ",T1$(1,I[3])
5460 GOTO 5480
5470 PRINT (PRTR_DEV)@(H0+24),"Lot: ",T1$(1,I[3]),"   Shipped: ",
5475 IF T[1]<>0 THEN PRINT (PRTR_DEV)T[1]:M2$ ELSE PRINT (PRTR_DEV)J$
5480 LET L=L+1
5490 RETURN 
5600 REM " --- Total"
5605 GOSUB 6200
5610 PRINT (PRTR_DEV)H9$(1,L9-L+2)
5620 IF A0$(99,1)="Y" THEN GOSUB 6900
5625 PRINT (PRTR_DEV)@(H0+53),"Invoice Total",
5630 PRINT (PRTR_DEV)@(H0+0),MESSAGE$,@(H0+68),T0:EXT_MASK$,'LF'
5635 IF A[2]>0 THEN PRINT (PRTR_DEV)@(H0+58),"Discount",
5640 PRINT (PRTR_DEV)@(H0+68),-A[2]:EXT_MASK$
5645 PRINT (PRTR_DEV)@(H0+56),"Tax Amount",
5650 PRINT (PRTR_DEV)@(H0+0),X6$,@(H0+68),A[0]:EXT_MASK$
5655 PRINT (PRTR_DEV)@(H0+52),"Freight Amount",
5660 PRINT (PRTR_DEV)@(H0+68),A[1]:EXT_MASK$,'LF'
5665 PRINT (PRTR_DEV)@(H0+48),"Net Invoice Amount",
5670 PRINT (PRTR_DEV)@(H0+68),T0+A[0]-A[2]+A[1]:EXT_MASK$
5680 PRINT (PRTR_DEV)'FF',
5690 RETURN 
5800 REM " --- Continued"
5820 PRINT (PRTR_DEV)H9$(1,L9-L+1)
5840 PRINT (PRTR_DEV)@(H0+70),"Continued",'FF',
5870 GOSUB 5000
5890 RETURN 
5900 REM " --- Compress Description"
5905 LET Z=POS(" "<>X2$); IF Z>1 THEN LET X2$=X2$(Z)
5910 LET X=POS("  "=X2$); IF X<>0 THEN LET Y=POS(" "<>X2$(X+2)); LET X2$=X2$(1,X)+X2$(X+Y+1); IF Y<>0 THEN GOTO 5910
5920 RETURN 
6100 REM " --- New Forms"
6120 PRINT @(5,20),"Please Make Sure That The Correct Invoice Forms Are Mounted On",@(6,21),"Printer ",F0$(13,2)," For ",N1$," Then Press <Enter>: ",'RB',
6140 LET V0$="S",V1$="",V2$="",V3$="",V4$="",V0=1,V1=42+LEN(N1$),V2=21
6145 GOSUB 7000
6147 PRINT @(0,20),'CL',@(0,21),'CL',
6190 RETURN 
6200 REM " --- Messages"
6220 FOR X=1 TO 2
6240 FIND (ARM10_DEV,KEY=N0$+"G"+A0$(70,2)+STR(X),DOM=6280)IOL=ARM10G
6250 FOR Y=1 TO 5
6255 IF POS(" "<>X4$(Y*40-39,40))=0 THEN GOTO 6270
6256 IF L>L9-2 THEN GOSUB 5800
6257 IF X=1 AND Y=1 THEN PRINT (PRTR_DEV)'LF',; LET L=L+1
6260 PRINT (PRTR_DEV)@(H0+27),X4$(Y*40-39,40)
6265 LET L=L+1
6270 NEXT Y
6280 NEXT X
6290 RETURN 
6900 REM " --- Print Paid Info"
6920 LET X6$="PAID: "+FNP$(Y7$(7,20))+" "
6930 IF Y9$(21,1)="P" THEN LET X6$=X6$+"# "+Z0$(31,16)
6940 IF Y9$(21,1)="C" THEN LET X6$=X6$+"# "+Z0$(24,7)
6950 LET X6$=X6$+" NAME: "+Z1$
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "SHX - Sample Manifest Data Generator"
0020 REM "Program SHX.MB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "21Oct98 WGH: Prevent Error 12 when opening PF printer (6305-6325)
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 SHE01A: IOLIST A0$,A1$(1),A2$(1),A[ALL]
0220 SHE02A: IOLIST W0$,W1$(1),W[ALL]
0240 SHE04A: IOLIST XA$
0250 SHM10A: IOLIST S0$(1),S1$(1)
0260 SHM10T: IOLIST H0$(1),H[0],H[1]
0500 REM " --- Initializations"
0510 DIM H0$(7),H[1],S0$(10),S1$(128)
0580 LET LABELPGM$=FNN$(I8$(71,6)),CODPGM$=FNN$(I8$(77,6))
0590 LET MANUAL=(ASC(P9$)-64)*1000,LOW=MANUAL,HIGH=LOW+999
0700 REM " --- Background"
0710 PRINT @(12,12),"Generation Pass",
0900 REM " --- Position files"
0990 READ (SHE01_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next header record"
1010 LET K$=KEY(SHE01_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1040 READ (SHE01_DEV)IOL=SHE01A
1080 LET A2$(1,6)=TODAY$
1090 READ (SHE02_DEV,KEY=A0$,DOM=1100)
1100 REM " --- Read next detail record"
1110 LET K$=KEY(SHE02_DEV,END=3900)
1120 IF POS(K$(1,17)=A0$)<>1 THEN GOTO 3900
1130 READ (SHE02_DEV)IOL=SHE02A
1140 IF W1$(11,1)<>P9$ THEN GOTO 3800
1150 PRINT @(COLUMN,11),W0$(3,7),"-",W0$(18,3),
1500 REM " --- Load service parameters"
1510 IF W1$(4,7)=S0$(4,7) THEN GOTO 1800
1520 FIND (SHM10_DEV,KEY=N0$+"A"+W1$(4,7),DOM=3800)IOL=SHM10A
1530 LET W1$(59,2)=P8$,I1$(70,2)=W1$(59,2)
1550 LET FRTPGM$=FNN$(S1$(30,6)),CHGPGM$=FNN$(S1$(55,6))
1560 LET RATEFILE$=FNN$(S1$(36,6)),ZONEFILE$=FNN$(S1$(42,6))
1800 REM " --- Default remaining SHE-02 fields"
1810 IF FNP$(W1$(29,24))="" THEN LET W1$(29,24)="Sample "+W0$(3,7)+"-"+W0$(18,3)
2000 REM " --- Freight calculation"
2010 LET Z2$=FNP$(W1$(1,3)),F0=0
2020 CALL FRTPGM$,ERR=2200,A0$,A1$,W0$,W1$,W[ALL],F0,P[2],Z1$,Z2$,S1$,W3$,RATEFILE$,ZONEFILE$,I1$
2030 IF F0>0 THEN LET W[4]=F0
2200 REM " --- Optional charges"
2210 CALL CHGPGM$,ERR=2220,SHM10_DEV,W[ALL],W1$,N0$,I1$
2400 REM " --- Transaction and SHE-04 update"
2410 GOSUB 6800
2600 REM " --- Print shipping label"
2610 IF LABELS$="Y" THEN GOSUB 6100
2800 REM " --- Print COD label"
2810 IF LABELS$="Y" THEN GOSUB 6200
3000 REM " --- Calculate package total"
3010 LET W[10]=0
3020 FOR X=3 TO 16
3030 IF X<>10 AND X<>14 THEN LET W[10]=W[10]+W[X]
3040 NEXT X
3050 IF W[2]>0 THEN LET W[14]=W[10]+W[2]
3500 REM " --- Update SHE-02"
3510 WRITE (SHE02_DEV,KEY=W0$)IOL=SHE02A
3600 REM " --- Update header totals"
3610 CALL "SHC.HA",SHE01_DEV,SHE02_DEV,A0$,A[ALL]
3800 REM " --- Next detail"
3890 GOTO 1100
3900 REM " --- Next header"
3910 WRITE (SHE01_DEV,KEY=A0$)IOL=SHE01A
3990 GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
6100 REM " --- Print shipping label"
6110 IF I9$(2,1)<>"Y" THEN GOTO 6190
6120 LET LPRINTER$=I9$(36,2),LPROGRAM$=LABELPGM$
6130 IF A1$(127,5)="00000" THEN LET A1$(127,5)=FILL(5)
6140 GOSUB 6300
6190 RETURN 
6200 REM " --- Print COD Label"
6210 IF I9$(3,1)<>"Y" THEN GOTO 6290
6220 LET LPRINTER$=I9$(38,2),LPROGRAM$=CODPGM$,W[14]=W[10]+W[2]
6230 GOSUB 6300
6290 RETURN 
6300 REM " --- Open label printer and print label"
6305 LET LABEL_DEV=UNT
6310 OPEN (LABEL_DEV,ERR=6320)LPRINTER$
6315 GOTO 6330
6320 CALL "SYC.GA",LABEL_DEV,0,"",LPRINTER$,PSTATUS
6325 IF PSTATUS THEN GOTO 6390
6330 CALL LPROGRAM$,ERR=6340,A1$,A2$,W0$,W1$,W[ALL],LABEL_DEV,WAREHOUSE$,CARRIER$,P[0]
6340 CLOSE (LABEL_DEV,ERR=6390)
6390 RETURN 
6800 REM " --- Update SHE-01 Package Totals"
6810 DIM H0$(6),H[1]
6815 LET H0$(1)=N0$+"T"+W1$(11,1)+W1$(59,2),H[1]=1
6850 FIND (SHM10_DEV,KEY=H0$,DOM=6860)IOL=SHM10T
6860 LET H[0]=H[0]+1,W1$(12,4)=FILL(4)
6870 IF S1$(26,1)="Y" THEN LET H[1]=H[1]+1,W1$(12,4)=STR(H[1]:"0000")
6880 WRITE (SHM10_DEV,KEY=H0$)IOL=SHM10T
6900 REM " --- Update SHE-04 Package Cross-Reference"
6950 LET XA$=N0$+"A"+W1$(11,1)+W1$(59,2)+A2$(1,6)+W1$(12,4)+W0$(3,15)+W0$(18,3)
6960 WRITE (SHE04_DEV,KEY=XA$)IOL=SHE04A
6990 RETURN 
8000 REM " --- Function Definitions"
8065 DEF FNN$(Q$)=FNP$(Q$(POS("NONE"=Q$)*4+1))
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

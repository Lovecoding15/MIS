0010 REM "WOR - Work Order Adjustment Register"
0020 REM "Program WOR.LA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "13Nov96 JJD: Don't print packed date in sort notification. (960)"
0032 REM "19Feb97 WGH: Correct rounding errors (3230,3260,3520-3525)"
0033 REM "15Oct97 JWK: Remember F0$ for date in G/L Recap headings (540,4910)
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=15
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-12",FILES$[2]="WOE-42",FILES$[3]="WOE-01"
0120 LET FILES$[4]="SYS-01",FILES$[5]="WOT-01",FILES$[6]="WOT-31"
0125 LET FILES$[7]="WOM-10",FILES$[8]="IVM-01",FILES$[9]="WOW-08"
0130 LET FILES$[10]="WOW-06"
0135 LET OPTIONS$[1]="L",OPTIONS$[2]="L",OPTIONS$[9]="C",OPTIONS$[10]="C"
0150 CALL "SYC.DA",1,1,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE12_DEV=CHANNELS[1],WOE42_DEV=CHANNELS[2],WOE01_DEV=CHANNELS[3]
0165 LET SYS01_DEV=CHANNELS[4],WOT01_DEV=CHANNELS[5],WOT31_DEV=CHANNELS[6]
0170 LET WOM10_DEV=CHANNELS[7],IVM01_DEV=CHANNELS[8],WOW08_DEV=CHANNELS[9]
0175 LET WOW06_DEV=CHANNELS[10]
0200 REM " --- IOLISTs"
0210 WOE12A: IOLIST A0$,A1$,A[ALL]
0220 WOE42A: IOLIST A0$,A1$,A[ALL]
0230 WOT01A: IOLIST B0$,B1$,B[ALL]
0240 WOT31A: IOLIST B0$,B1$,B[ALL]
0250 WOE01A: IOLIST D0$,D1$(1)
0260 WOE01B: IOLIST C0$,C1$(1)
0270 IVM01A: IOLIST X$,DESC$(1)
0280 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0290 WOS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0300 GLS01A: IOLIST X$,X$,G2$,G3$
0310 IVS01A: IOLIST I0$,I1$,I2$,I3$
0320 ARS01A: IOLIST X$,X$,C0$,X$,X$,C1$
0330 APS01A: IOLIST X$,X$,C2$,X$,X$,C3$
0340 PRS01A: IOLIST X$,X$,C4$,X$,X$,C5$
0350 WOW06A: IOLIST W0$,W[ALL]
0360 WOW08A: IOLIST SORTSEQ$
0365 EMPCODE: IOLIST *,F0$(1)
0370 OPCODE: IOLIST F1$
0375 APM01A: IOLIST *,F0$(1)
0380 WOM10A: IOLIST *,Y0$(1)
0385 WOM10A2: IOLIST *,Z0$(1)
0390 WOM10A3: IOLIST X0$(1),X1$(1)
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="WO",N3$=F5$
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=WOS01A
0422 LET BM$=P3$(1,1),AR$=P3$(2,1),AP$=P3$(3,1),PR$=P3$(4,1)
0425 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0430 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0435 LET I9=NUM(I2$(1,2)),I1=NUM(I2$(5,1)),I2=NUM(I2$(7,2))
0440 IF AR$="N" THEN GOTO 0455
0445 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0450 LET C0=NUM(C0$(1,2)),C1=LEN(C1$)
0455 IF AP$="N" THEN GOTO 0470
0460 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0465 LET C2=NUM(C2$(1,2)),C3=LEN(C3$)
0470 IF PR$="N" THEN LET C4=NUM(P2$(1,2)),C5$=M0$; GOTO 0485
0475 FIND (SYS01_DEV,KEY=N0$+"PR00",DOM=9800)IOL=PRS01A
0480 LET C4=NUM(C4$(1,2)),C5=LEN(C5$)
0485 LET GL$="N",STATUS=0,SOURCE$=PGM(-2)
0490 CALL "GLC.BA",ERR=0495,SOURCE$,N2$,GLW11$,GL$,STATUS
0495 IF STATUS THEN GOTO 9900
0500 REM " --- Init Data"
0510 DIM J$(80,"-"),A0$(18),A1$(30),A[11],B[11],W[3],T[3],HEADINGS$[1],DESC[3]
0530 LET H0=132,H=0,HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,L9=59,L=L9+1
0540 LET CLOCK$="",P8$="",WHEN$=F0$(7,3),HEADINGS=1,ERRORS=0,TERMF0$=F0$
0545 IF P9$="L" THEN LET P8$="Lot Number: "
0550 IF P9$="S" THEN LET P8$="Serial Number: "
0555 LET M0=LEN(M0$),M1=LEN(M1$),M2=LEN(M2$),M3=LEN(M3$),O9=0
0560 LET LM$="Labor Cost Adjustment",SM$="Subcontract Cost Adjustment"
0565 LET CM$="Closed Cost Adjustment"
0570 FOR X=1 TO 3; LET DESC[X]=NUM(I2$((X-1)*2+9,2)); NEXT X
0580 PRECISION 4
0590 DIM J2$(M1,"-")
0600 REM " --- Additional File Opens"
0630 IF BM$="Y" THEN LET FILES$[11]="BMM-08"
0635 IF BM$<>"Y" THEN LET FILES$[11]="WOM-02"
0640 IF PR$="Y" THEN LET FILES$[12]="PRM-01"
0645 IF PR$<>"Y" THEN LET FILES$[12]="WOM-01"
0650 IF AP$="Y" THEN LET FILES$[13]="APM-01"
0660 IF GL$="Y" THEN LET FILES$[14]="GLM-01",FILES$[15]=GLW11$,OPTIONS$[15]="CL"
0665 CALL "SYC.DA",1,11,15,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0666 IF STATUS THEN GOTO 9900
0670 LET OPCODE_DEV=CHANNELS[11],EMPCODE_DEV=CHANNELS[12],APM01_DEV=CHANNELS[13]
0675 IF STATUS THEN GOTO 9900
0680 IF CHANNELS[14] THEN LET GLM01_DEV=CHANNELS[14],GLW11_DEV=CHANNELS[15]
0800 REM " --- Options"
0810 LET TITLE=0,PROMPT$="Do You Want To Print The "+N3$,PROMPT=2
0820 CALL "SYC.YN",TITLE,PROMPT$,PROMPT,V$,V3
0830 IF V$<>"Y" THEN GOTO 9900
0900 REM " --- Clear Work File/Open Printer/Create Report Sort File "
0905 CALL "SYC.GA",7,1,"","",STATUS
0910 IF STATUS THEN GOTO 9900
0915 CALL "SYC.NB","Sorting",18,COLUMN
0920 DIM SORTSEQ$(18)
0925 LET WOADJ_DEV=WOE12_DEV
0930 READ (WOADJ_DEV,KEY=N0$,DOM=0935)
0935 LET K$=KEY(WOADJ_DEV,END=0970)
0940 IF POS(K$(1,2)=N0$)=0 THEN GOTO 0970
0945 READ (WOADJ_DEV,KEY=K$)
0950 LET SORTSEQ$=K$
0955 WRITE (WOW08_DEV,KEY=SORTSEQ$)IOL=WOW08A
0960 PRINT @(COLUMN,11),SORTSEQ$(1,11)+SORTSEQ$(15)
0965 GOTO 0935
0970 IF WOADJ_DEV=WOE42_DEV THEN GOTO 0990
0975 LET WOADJ_DEV=WOE42_DEV
0980 GOTO 0930
0990 CALL "SYC.NB","Printing",18,COLUMN
1000 REM " --- Init Read"
1020 READ (WOW08_DEV,KEY=N0$,DOM=1100)
1100 REM " --- Main Read"
1120 LET K$=KEY(WOW08_DEV,END=4000)
1130 IF POS(N0$=K$)<>1 THEN GOTO 4000
1135 READ (WOW08_DEV,KEY=K$)
1140 IF K$(15,1)="O" THEN READ (WOE12_DEV,KEY=K$)IOL=WOE12A
1150 IF K$(15,1)="S" THEN READ (WOE42_DEV,KEY=K$)IOL=WOE42A
1180 PRINT @(COLUMN,11),A0$(5,7)," ",FNB$(A0$(12)),
1200 REM " --- W/O Header
1210 DIM D1$(138),C1$(138)
1220 FIND (WOE01_DEV,KEY=K$(1,11),DOM=1240)IOL=WOE01A
1230 IF D0$(14,1)<>"I" THEN GOTO 1260
1240 DIM DESC$(60)
1245 FIND (IVM01_DEV,KEY=N0$+D0$(56),DOM=1260)IOL=IVM01A
1250 LET D1$(1,30)=DESC$(1,MIN(DESC[1],30))
1260 LET C0$=D0$,C1$=D1$
1300 REM " --- Get Additional Data
1320 IF A0$(15,1)="O" THEN READ (WOT01_DEV,KEY=A0$,DOM=1100)IOL=WOT01A
1330 IF A0$(15,1)="S" THEN READ (WOT31_DEV,KEY=A0$,DOM=1100)IOL=WOT31A
1360 FIND (WOE01_DEV,KEY=N0$+"  "+A1$(1,7),DOM=1380)IOL=WOE01B
1370 IF POS(" "<>A1$(8,3))=0 THEN LET A1$(8,3)=A0$(12,3)
1380 IF NUM(A1$(1,7))=0 THEN LET A1$(1,7)=A0$(5,7)
1390 ON POS(A0$(15,1)="OS")-1 GOTO 1400,1500
1400 REM " --- Get Employee/Op Code
1410 DIM F0$(30),F1$(20)
1415 LET F0$(1)="*** Missing Employee ***",F1$(1)="Missing Op Code"
1420 FIND (EMPCODE_DEV,KEY=N0$+B1$(7,9),DOM=1430)IOL=EMPCODE
1430 LET D9$=F0$(1,16)+", "+F0$(17,14); GOSUB 5700
1440 LET F0$=D9$
1450 FIND (OPCODE_DEV,KEY=N0$+B1$(4,3),DOM=1470)IOL=OPCODE
1460 LET F1$=F1$(6,20)
1470 LET F1$=F1$(1,POS("   "=F1$+"   ")-1)
1480 LET F0$=F0$(1,POS("   "=F0$+"   ")-1)
1485 IF LEN(F0$)+37>124-M1-M2-M3*3-6 THEN LET F0$=F0$(1,124-M1-M2-M3*3-6-37)
1487 IF LEN(F1$)+37>124-M1-M2-M3*3-6 THEN LET F1$=F1$(1,124-M1-M2-M3*3-6-37)
1490 GOTO 2000
1500 REM " --- Get Vendor Name
1510 DIM F0$(30)
1515 LET F0$(1)="*** Missing Vendor ***"
1520 FIND (APM01_DEV,KEY=N0$+B1$(4,6),DOM=2000)IOL=APM01A
2000 REM " --- Print It Here
2020 IF L>L9 THEN GOSUB 5000
2040 PRINT (7)A0$(5,7),"  ",D0$(12,2),"  ",D0$(14,1),"  ",FNB$(A0$(12)),"  ",A0$(15,1),"  ",A0$(16,3),
2050 IF A0$(15,1)="O" THEN PRINT (7)@(124-M1-M2*3-M3*3),B[5]:M2$,B[3]:M3$,B[1]-B[3]:M3$,@(124-M1-M2*2),B[6]:M2$,
2060 IF A0$(15,1)="S" THEN PRINT (7)@(124-M1-M2*2-M3),B[1]:M3$,
2100 PRINT (7)@(124-M1-M2*3-M3*3-7),"Orig: ",@(124-M1-M2),B[0]:M2$,B[2]:M1$,
2120 IF A1$(1,7)<>A0$(5,7) THEN PRINT (7)@(124),A1$(1,7) ELSE PRINT (7)""
2200 REM " --- Adj Here
2220 PRINT (7)@(3),D1$(1,30),
2240 IF A0$(15,1)="O" THEN PRINT (7)@(124-M1-M2*3-M3*3),A[4]:M2$,A[1]:M3$,A[2]:M3$,@(124-M1-M2*2),A[3]:M2$,
2250 IF A0$(15,1)="S" THEN PRINT (7)@(124-M1-M2*2-M3),A[1]:M3$,
2260 PRINT (7)@(124-M1-M2*3-M3*3-6),"Adj:",@(124-M1-M2),A[0]:M2$,(A[0]+A[3])*(A[1]+A[2]):M1$,
2270 IF A1$(8,3)<>A0$(12,3) THEN PRINT (7)@(124),FNB$(A1$(8,3)) ELSE PRINT (7)""
2300 REM " --- Net
2310 IF A0$(15,1)="O" THEN PRINT (7)@(3),FNF$(B1$(7,C4),C5$),"  ",B1$(4,3)," ",F1$,@(124-M1-M2*3-M3*3),A[4]-B[5]:M2$,A[1]-B[3]:M3$,A[2]-(B[1]-B[3]):M3$,@(124-M1-M2*2),A[3]-B[6]:M2$,
2320 IF A0$(15,1)="S" THEN PRINT (7)@(3),FNF$(B1$(4,C2),C3$),"   ",B1$(10,10),@(124-M1-M2*2-M3),A[1]-B[1]:M3$,
2330 PRINT (7)@(124-M1-M2*3-M3*3-6),"Net:",@(124-M1-M2),A[0]-B[0]:M2$,(A[0]+A[3])*(A[1]+A[2])-B[2]:M1$,
2340 IF B[2]<>(A[0]+A[3])*(A[1]+A[2]) THEN PRINT (7)@(124),"***" ELSE PRINT (7)""
2350 PRINT (7)@(3),F0$
2380 PRINT (7)""
2390 LET L=L+5,T0=T0+(A[0]+A[3])*(A[1]+A[2])-B[2]
2400 REM " --- Check G/L Posting Dates"
2410 IF GL$<>"" THEN GOTO 2500
2420 LET GLSTATUS=0
2430 CALL "GLC.EA",A0$(12,3),"N",PERIOD$,YEAR$,GLSTATUS
2440 IF GLSTATUS>0 THEN CALL "GLC.GA",7,A0$(12,3),GLSTATUS,L,ERRORS,STATUS
2450 IF FNP$(A1$(8,3))="" THEN GOTO 2500
2460 CALL "GLC.EA",A1$(8,3),"N",PERIOD$,YEAR$,GLSTATUS
2470 IF GLSTATUS>0 THEN CALL "GLC.GA",7,A1$(8,3),GLSTATUS,L,ERRORS,STATUS
2500 REM " --- Post Original Dates
2510 LET W0$=N0$+A0$(12,3)+D0$(12,2)
2515 DIM W[3]
2520 FIND (WOW06_DEV,KEY=W0$,DOM=2540)IOL=WOW06A
2540 IF A0$(15,1)="O" THEN LET W[0]=W[0]-B[3]*(B[0]+B[6]),W[1]=W[1]-(B[2]-B[3]*(B[0]+B[6]))
2550 IF A0$(15,1)="S" THEN LET W[2]=W[2]-B[2]
2560 LET W[3]=W[3]-B[2]
2580 IF D0$(12,2)=C0$(12,2) AND A0$(12,3)=A1$(8,3) THEN GOTO 2650
2590 WRITE (WOW06_DEV,KEY=W0$)IOL=WOW06A
2600 REM " --- Adjusted Dates
2620 LET W0$=N0$+A1$(8,3)+C0$(12,2)
2630 DIM W[3]
2640 FIND (WOW06_DEV,KEY=W0$,DOM=2650)IOL=WOW06A
2650 IF A0$(15,1)="O" THEN LET W[0]=W[0]+A[1]*(A[0]+A[3]),W[1]=W[1]+A[2]*(A[0]+A[3])
2660 IF A0$(15,1)="S" THEN LET W[2]=W[2]+A[1]*A[0]
2670 LET W[3]=W[3]+(A[1]+A[2])*(A[0]+A[3])
2680 WRITE (WOW06_DEV,KEY=W0$)IOL=WOW06A
3000 REM " --- Generate G/L Summary"
3010 IF GL$<>"Y" THEN GOTO 3900
3015 DIM Y0$(70,"0"),Z0$(70,"0")
3020 FIND (WOM10_DEV,KEY=N0$+"A"+D0$(12,2))IOL=WOM10A
3030 LET Z0$=Y0$
3040 IF C0$(12,2)=D0$(12,2) THEN GOTO 3100
3050 FIND (WOM10_DEV,KEY=N0$+"A"+C0$(12,2),DOM=1460)IOL=WOM10A2
3100 REM " --- Back Out The Distribution
3130 LET ACCOUNT$=Y0$(1,10),GLDATE$=A0$(12,3),AMOUNT=-B[2],UNITS=0
3140 LET REF1$="WO "+A0$(5,7),REF2$="Seq "+A0$(16,3),REF3$=FNB$(A1$(8,3))
3150 LET MEMO$=CM$
3160 GOSUB GLRECAP
3190 ON POS(A0$(15,1)="OS")-1 GOTO 3200,3300,3900
3200 REM " --- Labor "    
3220 LET ACCOUNT$=Y0$(21,10),AMOUNT=(B[0]+B[6])*B[3],MEMO$=LM$
3240 GOSUB GLRECAP
3260 LET ACCOUNT$=Y0$(31,10),AMOUNT=(B[0]+B[6])*B[4]
3280 GOSUB GLRECAP
3290 GOTO 3500
3300 REM " --- Subcontracts "     
3320 LET ACCOUNT$=Y0$(41,10),AMOUNT=B[2],MEMO$=SM$
3340 GOSUB GLRECAP
3500 REM " --- Add It Back In With New Information "
3520 LET ACCOUNT$=Z0$(1,10),GLDATE$=A1$(8,3)
3525 LET AMOUNT=ROUND((A[0]+A[3])*A[1],2)+ROUND((A[0]+A[3])*A[2],2)
3530 LET REF1$="WO "+C0$(5,7),REF2$="Seq "+A0$(16,3),REF3$=FNB$(A0$(12,3))
3540 LET MEMO$=CM$
3550 GOSUB GLRECAP
3590 ON POS(A0$(15,1)="OS")-1 GOTO 3600,3700
3600 REM " --- Labor "    
3610 LET ACCOUNT$=Z0$(21,10),AMOUNT=((-A[0])-A[3])*A[1],MEMO$=LM$
3620 GOSUB GLRECAP
3650 LET ACCOUNT$=Z0$(31,10),AMOUNT=((-A[0])-A[3])*A[2]
3660 GOSUB GLRECAP
3690 GOTO 3900
3700 REM " --- Subcontracts "    
3710 LET ACCOUNT$=Z0$(41,10),AMOUNT=((-A[0])-A[3])*(A[1]+A[2]),MEMO$=SM$
3720 GOSUB GLRECAP
3900 REM " --- Loop back for next record"
3990 GOTO 1100
4000 REM " --- End"
4020 IF L+2>L9 THEN GOSUB 5000
4040 PRINT (7)@(124-M1),J$(1,M1-1),'LF',@(37),"Report Total: ",@(124-M1),T0:M1$
4100 REM " --- Totals By Type
4120 LET L=L9+5
4170 DIM T[3],U[3]
4180 READ (WOW06_DEV,KEY=N0$,DOM=4200)
4200 READ (WOW06_DEV,END=4400)IOL=WOW06A
4210 IF POS(N0$=W0$)<>1 THEN GOTO 4400
4220 IF W0$(3,3)<>T0$ THEN GOSUB 6000
4230 DIM X0$(39),X1$(110)
4240 FIND (WOM10_DEV,KEY=N0$+"A"+W0$(6),DOM=4260)IOL=WOM10A3
4260 IF L>L9 THEN GOSUB 5200
4280 PRINT (7)W0$(6),"  ",X0$(6,20),@(30),W[0]:M1$,W[1]:M1$,W[2]:M1$,W[3]:M1$
4300 FOR X=0 TO 3; LET T[X]=T[X]+W[X],U[X]=U[X]+W[X]; NEXT X
4310 LET L0=L0+1
4320 GOTO 4200
4400 IF L+2>L9 THEN GOSUB 5000
4410 LET T9$="E"
4415 GOSUB 6000
4420 IF L>L9 THEN GOSUB 5200
4440 PRINT (7)@(30),J$(1,M1*4)
4460 PRINT (7)@(3),"Report Totals: ",@(30),U[0]:M1$,U[1]:M1$,U[2]:M1$,U[3]:M1$
4900 REM 
4910 LET UPDATE$="WOU.GA",PAGE=H,WIDTH=H0,F0$=TERMF0$
4980 IF GL$="Y" THEN RUN "GLR.XA"
4990 RUN UPDATE$
5000 REM " --- Heading"
5010 LET L=0
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (7)""
5045 IF O9=1 THEN GOTO 5190
5060 PRINT (7)@(18),"Transaction",@(124-M1-M2*2-M3*3-5),"Qty",@(124-M1-M2*2-M3*2-7),"Direct",@(124-M1-M2*2-M3-9),"Overhead",@(124-M1-M2*2-5),"Unit",@(124-M1-M2-5),"Setup",@(124),"New WO #"
5070 PRINT (7)"WO #",@(7),"Type Cat",@(18),"Date",@(25),"Src Seq",@(124-M1-M2*2-M3*3-6),"Cmplt",@(124-M1-M2*2-M3*2-6),"Labor",@(124-M1-M2*2-M3-6),"Labor",@(124-M1-M2*2-5),"Cost",@(124-M1-M2-5),"Units",@(124-M1-6),"Units",@(124-10),"Extension",@(124),"Adj Date"
5080 PRINT (7)@(3),
5090 IF P3$(3,1)="Y" THEN PRINT (7)@(3),"Vendor/PO #/",
5100 PRINT (7)"Employee/Op Code",'LF'
5190 RETURN 
5200 REM " --- Dates Here
5210 LET O9=1
5220 IF L>L9 THEN GOSUB 5000
5230 PRINT (7)"*** Work Order Adjustment Summary By Type ***",'LF'
5240 PRINT (7)@(30+M1-7),"Direct",@(30+M1*2-9),"Overhead",@(30+M1*3-5),"Subs"
5250 PRINT (7)@(30+M1-6),"Labor",@(30+M1*2-6),"Labor",@(30+M1*3-7),"Amount",@(30+M1*4-6),"Total",'LF'
5290 RETURN 
5300 REM " --- Date Subheading"
5320 IF L>L9 THEN GOSUB 5200
5340 PRINT (7)"Transaction Date: ",FNB$(T0$),":",'LF'
5360 LET L=L+2
5390 RETURN 
5700 REM " --- Compress Desc
5730 LET Z=POS(" "<>D9$); IF Z>1 THEN LET D9$=D9$(Z)
5740 LET X8=POS("  "=D9$); IF X8<>0 THEN LET Y8=POS(" "<>D9$(X8+2)); LET D9$=D9$(1,X8)+D9$(X8+Y8+1); IF Y8<>0 THEN GOTO 5740
5790 RETURN 
6000 REM " --- First Sub Total
6010 IF T0$="" THEN GOTO 6100
6020 IF L0=0 THEN GOTO 6100
6080 PRINT (7)@(30),J$(1,M1*4)
6090 PRINT (7)FNB$(T0$)," Total: ",@(30),T[0]:M1$,T[1]:M1$,T[2]:M1$,T[3]:M1$,'LF'
6095 LET L=L+3
6100 IF T9$<>"" THEN RETURN 
6120 DIM T[3]
6140 LET T0$=W0$(3,3),L0=0
6160 GOSUB 5300
6190 RETURN 
6900 REM " --- Special G/L Recap Routine"
6910 GLRECAP: 
6920 IF GL$<>"Y" THEN GOTO 6990
6930 PRECISION 2
6940 LET AMOUNT=AMOUNT*1
6950 CALL "GLC.AA",GLM01_DEV,GLW11_DEV,GLT05_DEV,ACCOUNT$,GLDATE$,REF1$,REF2$,REF3$,MEMO$,AMOUNT,UNITS,STATUS
6960 PRECISION 4
6990 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8070 DEF FNG$(Q$)=STR((ASC(Q$)-32)*POS(" "<>Q$):"000")
8080 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "POC - Purge Purchase Calendar"
0020 REM "Program POC.CD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 15Dec97 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Jun97 WGH: Display 21st century dates correctly (4225,8145-8165)"
0032 REM "02Aug99 WGH: Remove 21st century dates correctly (4120,8120-8140)"
0040 REM 
0050 REM "FILENAME$: Calendar file name POM-01/MPM-02 (input)"
0060 REM "STATUS   : 0=No error (returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER FILENAME$,STATUS
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]=FILENAME$
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],POM01_DEV=CHANNELS[2]
0200 REM " --- IOLISTs"
0210 POM01A: IOLIST A0$(1),A1$(1),A0
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2)
0500 REM " --- Init Data"
0510 DIM A0$(6),A1$(31)
0550 LET PURGE$=F0$(7,3)
0700 REM " --- Background"
0710 FOR X=4 TO 8 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(9,3),"First Day Currently Scheduled:",@(10,5),"Last Day Currently Scheduled:",@(17,7),"Date To Purge Through:",'SF',
0800 REM " --- Get First Day Scheduled"
0810 CALL "POC.CE",POM01_DEV,N0$,BEGDATE$,ENDDATE$,STATUS
0820 LET V1$="None",V2$="None"
0830 IF BEGDATE$<>"" THEN LET V1$=FNB$(BEGDATE$),V2$=FNB$(ENDDATE$)
0840 PRINT @(40,3),V1$,@(40,5),V2$,
1000 REM " --- Date To Purge Through
1010 LET V4$="Enter Date To Purge Through (<F1>=Last)"
1020 LET V0$="D",V1$="KEC",V2$=PURGE$,V3$="",V0=3,V1=40,V2=7
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1100,1000,1000,9900
1050 LET PURGE$=V$
1090 GOTO 4000
1100 REM " --- Last date"
1110 PRINT @(V1,V2),"Last",'CL',
1120 LET PURGE$=ENDDATE$
4000 REM " --- Options"
4010 LET V4$="Is The Above Information Correct"
4020 CALL "SYC.YN",1,V4$,1,V$,V3
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Position file"
4110 IF PURGE$="" THEN GOTO 9900
4120 LET PURGE$=FNB$(PURGE$),PURGE$=FNYY_YY21$(PURGE$(7,2))+PURGE$(1,2)+PURGE$(4,2)
4180 CALL "SYC.NB","Purging",5,COLUMN
4190 READ (POM01_DEV,KEY=N0$,DOM=4200)
4200 REM " --- Purge Records
4205 LET K$=KEY(POM01_DEV,END=9900)
4210 IF POS(N0$=K$)<>1 THEN GOTO 9900
4220 IF K$(3,4)>PURGE$(1,4) THEN GOTO 9900
4225 PRINT @(COLUMN,11),K$(5,2),"/",FNYY21_YY$(K$(3,2)),
4230 IF K$(3,4)<PURGE$(1,4) THEN REMOVE (POM01_DEV,KEY=K$); GOTO 4200
4240 READ (POM01_DEV)IOL=POM01A
4250 LET A1$(1,NUM(PURGE$(5,2)))=""
4280 WRITE (POM01_DEV,KEY=K$)IOL=POM01A
4285 IF FNP$(A1$)="" THEN REMOVE (POM01_DEV,KEY=K$)
4290 GOTO 4200
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to caller"
9910 CLOSE (SYS01_DEV,ERR=9920)
9920 CLOSE (POM01_DEV,ERR=9930)
9950 EXIT 
9999 END

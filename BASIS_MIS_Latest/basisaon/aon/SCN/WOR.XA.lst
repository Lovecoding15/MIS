0010 REM "WOS - Work Order Detail Printing"
0020 REM "Program WOR.XA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "06Nov96 JJD: Use consistant "Variance %" column heading.(5730)"
0032 REM "06Nov96 JJD: Include Message lines. (1551)"
0033 REM "13Jan97 JJD: Separate PO# and PO Line#, don't print Subcont. record sequence# (3030)"
0034 REM "13Mar97 JJD: Check correct flag for subcontract printing (1805)"
0035 REM "19May98 JWK: Reads and Prints too much Vendor info (3010,5315)
0036 REM "12Mar99 WGH: Check correct flag for subcontract's PO status (5325)
0040 REM 
0050 REM "Z0$="Y" Print Comments
0051 REM "Z1$="Y" Print Standards,  WOE02_DEV=WOE-02,IVM01_DEV=IVM-01
0052 REM "Z2$="Y" Print Transactions,Z3$=Start,Z4$=End,Z5$=Selection (OMS)
0053 REM "Z6$="Y" Print Cost Summary - Needs WOE02_DEV
0054 REM "O9$ - Return Program (WOE.AH, WOR.AB, WOR.EB or WOR.IB)
0056 REM "T_COST$="N" Don't Print Cost Info On Traveler
0080 SETESC 9000
0085 SETERR 9000
0090 PRECISION IVS01_PRECISION
0100 REM " --- Open/Lock Files"
0110 LET FILES=18
0115 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 ON FILES_OPENED GOTO 0125,0200
0125 LET FILES$[1]="WOE-07",FILES$[2]="WOT-03",FILES$[3]="WOT-01"
0130 LET FILES$[4]="WOT-11",FILES$[5]="WOT-12",FILES$[6]="WOT-33"
0135 LET FILES$[7]="WOT-23",FILES$[8]="WOM-01",FILES$[9]="WOM-02"
0137 LET FILES$[10]="WOE-06",FILES$[11]="WOE-22",FILES$[12]="WOE-32"
0138 LET FILES$[13]="WOT-31",FILES$[14]="WOM-07",FILES$[15]="WOT-21"
0140 CALL "SYC.DA",1,1,15,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9000
0160 LET WOE07_DEV=CHANNELS[1],WOT03_DEV=CHANNELS[2],WOT01_DEV=CHANNELS[3]
0165 LET WOT11_DEV=CHANNELS[4],WOT12_DEV=CHANNELS[5],WOT33_DEV=CHANNELS[6]
0170 LET WOT23_DEV=CHANNELS[7],WOM01_DEV=CHANNELS[8],WOM02_DEV=CHANNELS[9]
0175 LET WOE06_DEV=CHANNELS[10],WOE22_DEV=CHANNELS[11],WOE32_DEV=CHANNELS[12]
0180 LET WOT31_DEV=CHANNELS[13],WOM07_DEV=CHANNELS[14],WOT21_DEV=CHANNELS[15]
0200 REM " --- IOLISTs"
0210 IVS01A: IOLIST X$,X$,X$,P$
0220 IVM01A: IOLIST B0$,B1$(1)
0230 WOM10A: IOLIST WO0$,W01$
0240 APM01A: IOLIST *,X0$(1)
0250 CODFILE: IOLIST X0$
0260 EMPFILE: IOLIST *,X1$(1)
0270 IVM01X: IOLIST *,X0$(1),X2$
0280 WOE07A: IOLIST *,X1$(5)
0290 WOE07X: IOLIST *,X0$(5)
0310 WOE02A: IOLIST W0$,W1$,W[ALL]
0315 WOE02X: IOLIST *,X0$
0320 WOT01A: IOLIST W0$,W1$,W[ALL]
0325 WOT03A: IOLIST W0$,W1$,W[ALL]
0330 WOT11A: IOLIST X$,X$,SERL$,H[0],H[1],H[2]
0335 WOT12A: IOLIST X$,X$,SERL$,H[0],H[1],H[2]
0340 WOE06A: IOLIST W0$,W1$,W[0],W[1],W[2],W[3],W[4],W[5]
0345 WOE22A: IOLIST W0$,W1$,W[ALL]
0350 WOE32A: IOLIST W0$,W1$,W[ALL]
0351 WOREQA: IOLIST W0$,W1$,W[ALL]
0355 WOT0O: IOLIST W0$,W1$,W[ALL]
0360 WOT0M: IOLIST W0$,W1$,W[ALL]
0365 WOT0S: IOLIST W0$,W1$,W[ALL]
0400 REM " --- Parameters "
0410 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9000)IOL=IVS01A
0500 REM " --- Initialize "
0505 LET H0=132,H=0,HEADINGS=3,WHEN$=F0$(7,3),CLOCK$="",L9=59
0510 DIM HEADINGS$[HEADINGS],MESSAGE$[1]
0515 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$,HEADINGS$[2]=H3$
0520 DIM J2$(132,"-")
0525 DIM W[13]
0530 LET T0$="",T1$="",T2$="",T9$="",T0=0,T1=0,T2=0,T3=0,S0=0,S1=0,U0=0,U1=0,U2=0,U3=0,WARN$=""
0535 CALL "SYC.RM",M1$,M1,9,10
0540 CALL "SYC.RM",M2$,M2,9,10
0545 CALL "SYC.RM",M3$,M3,9,11
0550 DIM M9$(M1+M2,"-"),M8$(M3+M2,"-")
0555 LET M9$(1,1)="+",M9$(LEN(M9$))="+",M8$(1,1)="+",M8$(LEN(M8$))="+"
0560 LET M9$(INT(LEN(M9$)/2)-2,5)="Total",M8$(INT(LEN(M8$)/2)-2,4)="Each"
0565 LET I9=NUM(P3$(26,2))
0570 DIM U9[12],Y0[I9],Y1[I9],Y2[I9],Y3[I9]
0575 LET Y4=0,Y5=0,Y6=0,Y7=0,Y0$="",Y1$=""
0580 LET FMASK$="###.000-",FMASK=LEN(FMASK$)
0585 IF Z2$<>"Y" THEN LET Z3$="",Z4$="",Z5$=""
0600 REM " --- Additional File Opens "
0615 LET FILES_OPENED=1
0620 IF APM01_DEV THEN LET FILES$[16]="" ELSE IF PO$="Y" THEN LET FILES$[16]="APM-01"
0630 IF PRM01_DEV THEN LET FILES$[17]="" ELSE IF PR$="Y" THEN LET FILES$[17]="PRM-01"
0640 IF BMM08_DEV THEN LET FILES$[18]="" ELSE IF BM$="Y" THEN LET FILES$[18]="BMM-08"
0650 CALL "SYC.DA",1,16,18,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0660 IF STATUS THEN GOTO 9000
0670 IF CHANNELS[16] THEN LET APM01_DEV=CHANNELS[16]
0672 IF CHANNELS[17] THEN LET PRM01_DEV=CHANNELS[17]
0674 IF CHANNELS[18] THEN LET BMM08_DEV=CHANNELS[18]
0680 IF PR$="Y" THEN LET EMPFILE_DEV=PRM01_DEV ELSE LET EMPFILE_DEV=WOM01_DEV
0690 IF BM$="Y" THEN LET CODFILE_DEV=BMM08_DEV ELSE LET CODFILE_DEV=WOM02_DEV
1000 REM " --- Process Header"
1020 GOSUB 5000
1100 REM " --- Comments?"
1110 IF Z0$<>"Y" THEN GOTO 1400
1120 LET T0$=""
1130 READ (WOE07_DEV,KEY=A0$(1,11),DOM=1140)
1140 LET K1$=KEY(WOE07_DEV,END=1300)
1150 IF K1$(1,11)<>A0$(1,11) THEN GOTO 1300
1160 LET T0$=T0$+K1$(12,3)
1170 READ (WOE07_DEV)
1180 GOTO 1140
1300 REM " --- Print Comments"
1305 IF T0$="" THEN GOTO 1400
1310 LET X=LEN(T0$)/3+1,X=INT(X/2),X=X*3
1315 IF L+2>L9 THEN GOSUB 5000
1320 PRINT (7)"*** Comments ***",'LF',J2$
1325 LET L=L+2
1330 FOR X0=1 TO X STEP 3
1335 DIM X0$(65),X1$(65)
1340 LET X0$(1)=T0$(X0,3)
1345 FIND (WOE07_DEV,KEY=A0$(1,11)+X0$(1,3),DOM=1350)IOL=WOE07X
1350 IF X0+X<LEN(T0$) THEN LET X1$(1,3)=T0$(X0+X,3)
1355 FIND (WOE07_DEV,KEY=A0$(1,11)+X1$(1,3),DOM=1360)IOL=WOE07A
1360 GOSUB 5400
1365 NEXT X0
1370 IF L+1>L9 THEN GOSUB 5000
1375 PRINT (7)J2$
1380 LET L=L+1
1400 REM " --- Standards"
1410 IF Z1$<>"Y" AND Z6$<>"Y" THEN GOTO 7200
1420 IF Z1$<>"Y" THEN GOTO 1500
1430 IF L+1>L9 THEN GOSUB 5000
1440 GOSUB 6600
1450 PRINT (7)"*** Standards ***"
1460 LET L=L+1
1500 REM " --- Operations/Materials/Sub-Contract Requirements "
1505 REM " --- Operations
1510 LET WOREQ_DEV=WOE02_DEV,T2$=""
1515 INIT_WOREQ: 
1520 READ (WOREQ_DEV,KEY=A0$(1,11),DOM=READ_WOREQ)
1525 READ_WOREQ: 
1530 LET K1$=KEY(WOREQ_DEV,END=DONE_WOREQ)
1535 IF K1$(1,11)<>A0$(1,11) THEN GOTO DONE_WOREQ
1540 READ (WOREQ_DEV,KEY=K1$)IOL=WOREQA
1545 IF Z6$="Y" THEN GOSUB 6200; IF Z1$<>"Y" THEN GOTO READ_WOREQ
1550 IF W0$(12,1)<>T2$ THEN GOSUB 5800
1551 IF W1$(91,1)="M" THEN GOTO PROCESS_DETAIL
1555 ON POS(T2$="ABC")-1 GOTO DO_WOE02,DO_WOE22,DO_WOE32
1560 DO_WOE02: 
1600 REM " --- Operations "
1610 PROCESS_DETAIL: 
1620 GOSUB 5200
1630 GOSUB ACCUM_TOTALS
1640 GOTO READ_WOREQ
1650 DO_WOE22: 
1700 REM " --- Materials "
1710 DIM X0$(60)
1715 LET X0$(1)="*** Not On File ***"
1720 FIND (IVM01_DEV,KEY=N0$+W1$(27,20),DOM=1640)IOL=IVM01X
1730 LET X0$(1)=FNITEM$(X0$,DESC[1],DESC[2],DESC[3]),X0$=X0$(1,30)
1740 GOTO PROCESS_DETAIL
1750 DO_WOE32: 
1800 REM " --- Subcontracts"
1805 IF PO$<>"Y" THEN GOTO DONE_WOREQ
1810 DIM X0$(30)
1812 LET X0$(1)="*** Not On File ***"
1815 IF PO$="Y" THEN FIND (APM01_DEV,KEY=N0$+W1$(7,6),DOM=1820)IOL=APM01A
1820 GOTO PROCESS_DETAIL
1825 DONE_WOREQ: 
1830 IF WOREQ_DEV=WOE02_DEV THEN LET WOREQ_DEV=WOE22_DEV; GOTO INIT_WOREQ
1835 IF WOREQ_DEV=WOE22_DEV THEN LET WOREQ_DEV=WOE32_DEV; GOTO INIT_WOREQ
1840 GOTO 1875
1845 ACCUM_TOTALS: 
1850 FOR X=0 TO 3
1855 LET T[X]=T[X]+W[X]
1860 NEXT X
1865 LET T0=T0+W[3],T1=T1+W[1]
1870 RETURN 
1875 REM " --- All Through"
1880 IF Z1$<>"Y" THEN GOTO 7200
1885 LET T9$="END"; GOSUB 5800
1890 IF T_COST$<>"N" THEN IF L+2>L9 THEN GOSUB 5980
1895 IF T_COST$<>"N" THEN PRINT (7)@(10),"Work Order Totals: ",@(132-M1-M2-M3),T1:M3$,@(132-M1),T0:M1$,'LF',J2$; LET L=L+2
1900 GOTO 7200
2000 REM " --- Transactions Here"
2010 IF Z2$<>"Y" AND Z6$<>"Y" THEN GOTO 3200
2020 IF Z5$="" THEN LET Z5$="MOS"; REM "ASSUME ALL
2030 IF A0$(15,1)="C" AND A0$(31,3)<=G9$ THEN GOSUB CLOSED_WO_TRANS ELSE GOSUB OPEN_WO_TRANS
2040 IF POS(P$(17,1)="LS")>0 THEN IF A0$(15,1)="C" AND A0$(31,3)<=G9$ THEN LET LOTSER_DEV=WOT12_DEV
2050 IF POS(P$(17,1)="LS")>0 THEN IF A0$(15,1)<>"C" OR A0$(31,3)>G9$ THEN LET LOTSER_DEV=WOT11_DEV
2060 IF Z2$<>"Y" THEN GOTO 2140
2070 IF POS("S"=Z5$)=0 OR PO$<>"Y" THEN GOTO 2080
2080 IF POS("O"=Z5$)=0 THEN GOTO 2110
2090 IF PR$="Y" THEN LET EMPFILE_DEV=PRM01_DEV ELSE LET EMPFILE_DEV=WOM01_DEV
2100 IF BM$="Y" THEN LET CODFILE_DEV=BMM08_DEV ELSE LET CODFILE_DEV=WOM02_DEV
2110 DIM W[13],T[3],U[3]
2120 GOSUB 5600
2130 LET T0$="",T9$="",TS=0,US=0
2140 READ (WOM07_DEV,KEY=A0$(1,11)+Z3$,DOM=2150)
2150 WORK_ORDER_TRANSACTIONS: 
2160 LET K8$=KEY(WOM07_DEV,END=3100)
2170 READ (WOM07_DEV,KEY=K8$)
2180 IF K8$(1,11)<>A0$(1,11) THEN GOTO 3100
2190 IF Z4$<>"" THEN IF K8$(12,3)>Z4$ THEN GOTO 3100
2200 IF A[4]>0 AND A0$(14,1)="R" THEN IF K8$(12,3)>A1$(91,3) THEN GOTO 3100
2210 ON POS(K8$(15,1)="MOS")-1 GOTO READ_M,READ_O,READ_S
2220 READ_M: 
2230 READ (WOTRANM_DEV,KEY=K8$,DOM=2150)IOL=WOT0M
2240 GOTO 2300
2250 READ_O: 
2260 READ (WOTRANO_DEV,KEY=K8$,DOM=2150)IOL=WOT0O
2270 GOTO 2300
2280 READ_S: 
2290 READ (WOTRANS_DEV,KEY=K8$,DOM=2150)IOL=WOT0S
2300 GOSUB 6400; IF Z2$<>"Y" THEN GOTO WORK_ORDER_TRANSACTIONS
2310 IF WOTRAN_DEV=WOT01_DEV THEN READ (WOTRAN_DEV,KEY=K8$,DOM=2320)IOL=WOT01A
2320 IF W0$(12,2)=T0$ THEN GOTO 2710
2600 REM " --- Monthly Subtotals"
2610 IF T0$="" THEN GOTO 2650
2620 IF L+3>L9 THEN GOSUB 5000; GOSUB 5600
2630 PRINT (7)'LF',@(10),"Month ",FNG$(T0$)," Totals: ",@(132-M1-M3-M2*2),TS:M2$,@(132-M1-M2-M3),T[0]:M2$,@(132-M1),T[1]:M1$,'LF'
2640 LET L=L+3
2650 IF T9$<>"" THEN RETURN 
2660 LET T0$=W0$(12,2)
2670 LET T[0]=0,T[1]=0,TS=0
2700 REM " --- Now Print Them"
2710 DIM X0$(30),X2$(20); LET X0$(1)="*** Not On File ***",X1$=X0$
2720 IF L+1>L9 THEN GOSUB 5600
2730 PRINT (7)FNB$(W0$(12,3)),@(10),W0$(15,1),@(132-M1-M2-M3),W[0]:M2$,W[1]:M3$,W[2]:M1$,
2740 ON POS(W0$(15,1)="OMS") GOTO 2150,2800,2900,3000
2800 REM " --- Operations"
2810 LET T[0]=T[0]+W[0],U[0]=U[0]+W[0],U0=U0+W[0]*W[3],U1=U1+W[2]-W[0]*W[3],TS=TS+W[6],US=US+W[6]
2820 LET X0$=""
2830 FIND (WOE02_DEV,KEY=N0$+"  "+W0$(5,7)+"A"+W1$(1,3),DOM=2850)IOL=WOE02X
2840 LET X0$=X0$(7,20)
2850 FIND (EMPFILE_DEV,KEY=N0$+W1$(7,9),DOM=2860)IOL=EMPFILE
2860 PRINT (7)@(13),W1$(1,3),"  ",W1$(4,3),"  ",X0$,@(50),FNF$(W1$(7,P6),P6$)," ",X1$,@(132-M1-M3-M2*3),W[5]:M2$,W[6]:M2$
2870 GOTO 3040
2900 REM " --- Materials"
2910 DIM X0$(60)
2920 FIND (IVM01_DEV,KEY=N0$+W1$(24,20),DOM=2940)IOL=IVM01X
2930 LET X0$(1)=FNITEM$(X0$,DESC[1],DESC[2],DESC[3]),X0$=X0$(1,30)
2940 PRINT (7)@(13),W1$(1,3),"  ",W1$(24),"  ",W1$(20,2),"  ",@(50),X0$
2950 IF POS(P$(17,1)="LS")>0 AND X2$(19,2)="YY" THEN GOSUB 4100
2960 GOTO 3040
3000 REM " --- Subcontracts"
3010 DIM X0$(30)
3020 IF PO$="Y" THEN FIND (APM01_DEV,KEY=N0$+W1$(4,6),DOM=3030)IOL=APM01A
3030 PRINT (7)@(18),FNF$(W1$(4,P7),P7$)," ",X0$,@(50),W1$(10,7)+"-"+W1$(17,3)
3040 LET T[1]=T[1]+W[2],U[1]=U[1]+W[2],L=L+1
3050 GOTO WORK_ORDER_TRANSACTIONS
3100 REM " --- All Done"
3110 IF Z2$<>"Y" THEN GOTO 3200
3120 LET T9$="E"; GOSUB 2600
3130 LET T9$=""
3140 IF T_COST$<>"N" THEN IF L+2>L9 THEN GOSUB 5600
3150 IF T_COST$<>"N" THEN PRINT (7)@(10),"Work Order Totals:",@(132-M1-M3-M2*2),US:M2$,@(132-M1-M2-M3),U[0]:M2$,@(132-M1),U[1]:M1$,'LF',J2$; LET L=L+2
3200 REM " --- Print Cost Summary"
3210 IF Z6$<>"Y" THEN GOTO 4000
3220 LET T0=0,T1=0,T2=0,T3=0
3230 DIM M9$(3*M2+9,"-"),M8$(3*M1+9,"-"),U[9]
3240 LET M9$(1,1)="+",M9$(LEN(M9$))="+",M8$(1,1)="+",M8$(LEN(M8$))="+"
3250 LET M9$(INT(LEN(M9$)/2)-3,7)=" Hours ",M8$(INT(LEN(M8$)/2)-4,8)=" Amount "
3260 GOSUB 5700
3270 IF L+1>L9 THEN GOSUB 5000
3280 PRINT (7)"Operations"; LET L=L+1
3290 IF LEN(Y0$)=0 THEN GOTO 3400
3300 FOR X0=1 TO LEN(Y0$) STEP 3
3310 DIM X0$(20); LET X0$(1)="*** Not On File ***"
3320 FIND (CODFILE_DEV,KEY=N0$+Y1$(X0,3),DOM=3340)IOL=CODFILE
3330 LET X0$=X0$(6,20),X=INT(X0/3)
3340 IF L+1>L9 THEN GOSUB 5700
3350 IF Y0[X]<>0 THEN LET Y0=(Y0[X]-Y2[X])*100/Y0[X] ELSE LET Y0=0
3360 IF Y1[X]<>0 THEN LET Y1=(Y1[X]-Y3[X])*100/Y1[X] ELSE LET Y1=0
3370 PRINT (7)@(3),Y1$(X0,3),"  ",X0$,@(30),Y1[X]:M2$,Y3[X]:M2$,Y1[X]-Y3[X]:M2$,Y1:"#####.00-",@(44+M2*3),Y0[X]:M1$,Y2[X]:M1$,Y0[X]-Y2[X]:M1$,Y0:"#####.00-"
3380 LET T0=T0+Y0[X],T1=T1+Y1[X],T2=T2+Y2[X],T3=T3+Y3[X],L=L+1
3390 NEXT X0
3400 LET U[2]=U0,U[3]=S0,U[8]=U1,U[9]=S1
3410 IF L+3>L9 THEN GOSUB 5000; GOSUB 5700
3420 IF S0<>0 THEN LET U2=(S0-U0)*100/S0 ELSE LET U2=0
3430 IF S1<>0 THEN LET U3=(S1-U1)*100/S1 ELSE LET U3=0
3440 PRINT (7)@(44+M2*3),FILL(M1-1,"-")," ",FILL(M1-1,"-")," ",FILL(M1-1,"-")," ",FILL(8,"-")
3450 PRINT (7)@(8),"Direct Total: ",@(44+M2*3),S0:M1$,U0:M1$,S0-U0:M1$,U2:"#####.00-"
3460 PRINT (7)@(8),"Overhead Total: ",@(44+M2*3),S1:M1$,U1:M1$,S1-U1:M1$,U3:"#####.00-"
3470 LET L=L+3
3480 IF L+2>L9 THEN GOSUB 5700
3490 IF T0<>0 THEN LET Y0=(T0-T2)*100/T0 ELSE LET Y0=0
3500 PRINT (7)@(44+M2*3),FILL(M1-1,"-")," ",FILL(M1-1,"-")," ",FILL(M1-1,"-")," ",FILL(8,"-")
3510 PRINT (7)@(3),"Labor Total:",@(44+M2*3),T0:M1$,T2:M1$,T0-T2:M1$,Y0:"#####.00-"
3520 LET L=L+2
3530 IF L+2>L9 THEN GOSUB 5700
3540 IF Y4<>0 THEN LET Y0=(Y4-Y5)*100/Y4 ELSE LET Y0=0
3550 PRINT (7)'LF',@(3),"Materials: ",@(44+M2*3),Y4:M1$,Y5:M1$,Y4-Y5:M1$,Y0:"#####.00-"
3560 LET T0=T0+Y4,T2=T2+Y5,L=L+2,U[4]=Y4,U[5]=Y5
3570 IF L+2>L9 THEN GOSUB 5700
3580 IF Y6<>0 THEN LET Y0=(Y6-Y7)*100/Y6 ELSE LET Y0=0
3590 PRINT (7)'LF',@(3),"Subcontracts:",@(44+M2*3),Y6:M1$,Y7:M1$,Y6-Y7:M1$,Y0:"#####.00-"
3600 LET T0=T0+Y6,T2=T2+Y7,L=L+2,U[6]=Y6,U[7]=Y7
3700 REM " --- Totals"
3710 IF L+2>L9 THEN GOSUB 5700
3720 PRINT (7)@(30),FILL(M2-1,"-")," ",FILL(M2-1,"-")," ",FILL(M2-1,"-")," ",FILL(8,"-"),@(44+M2*3),FILL(M1-1,"-")," ",FILL(M1-1,"-")," ",FILL(M1-1,"-")," ",FILL(8,"-")
3730 IF T0<>0 THEN LET Y0=(T0-T2)*100/T0 ELSE LET Y0=0
3740 IF T1<>0 THEN LET Y1=(T1-T3)*100/T1 ELSE LET Y1=0
3750 PRINT (7)@(18),"WO Totals:",@(30),T1:M2$,T3:M2$,T1-T3:M2$,Y1:"#####.00-",@(44+M2*3),T0:M1$,T2:M1$,T0-T2:M1$,Y0:"#####.00-"
3760 LET L=L+2,U[0]=T0,U[1]=T2
3800 REM " --- Per Unit"
3810 IF A[0]=0 OR A[0]=1 THEN GOTO 3890
3820 LET T0=T0/A[0],T1=T1/A[0],T2=T2/A[0],T3=T3/A[0]
3830 IF L+3>L9 THEN GOSUB 5700
3840 PRINT (7)@(30),FILL(M2-1,"-")," ",FILL(M2-1,"-")," ",FILL(M2-1,"-")," ",FILL(8,"-"),@(44+M2*3),FILL(M1-1,"-")," ",FILL(M1-1,"-")," ",FILL(M1-1,"-")," ",FILL(8,"-")
3850 IF T0<>0 THEN LET Y0=(T0-T2)*100/T0 ELSE LET Y0=0
3860 IF T1<>0 THEN LET Y1=(T1-T3)*100/T1 ELSE LET Y1=0
3870 PRINT (7)@(12),"Per Unit Totals:",@(30),T1:M2$,T3:M2$,T1-T3:M2$,Y1:"#####.00-",@(44+M2*3),T0:M1$,T2:M1$,T0-T2:M1$,Y0:"#####.00-"
3880 LET L=L+2
3890 IF L+1>L9 THEN GOSUB 5700
3895 PRINT (7)J2$
3900 LET L=L+1
4000 REM " --- End"
4010 RUN O9$
4100 REM " --- Serial Numbers Here"
4110 DIM H[2]
4120 READ (LOTSER_DEV,KEY=W0$,ERR=4130)
4130 LET K3$=KEY(LOTSER_DEV,END=4260)
4140 IF POS(W0$=K3$)<>1 THEN GOTO 4260
4150 IF POS(P$(17,1)="LS")>0 THEN IF A0$(15,1)="C" AND A0$(31,3)<=G9$ THEN READ (LOTSER_DEV)IOL=WOT12A
4160 IF POS(P$(17,1)="LS")>0 THEN IF A0$(15,1)<>"C" OR A0$(31,3)>G9$ THEN READ (LOTSER_DEV)IOL=WOT11A
4170 IF L<L9 THEN GOTO 4210
4180 GOSUB 5600
4190 PRINT (7)@(13),W1$(1,3),"  ",W1$(18),"  ",W1$(4,2),"  ",W1$(6,5),@(50),X0$
4200 LET L=L+1
4210 IF L+1>L9 THEN GOSUB 5600
4220 IF P$(17,1)="S" THEN PRINT (7)@(18),"Serial: ", ELSE PRINT (7)@(18),"Lot #: ",
4230 PRINT (7)SERL$," Issued:",H[0]:M2$," Cost:",H[1]:M3$," Total:",H[2]:M1$
4240 LET L=L+1
4250 GOTO 4130
4260 RETURN 
5000 REM " --- Heading"
5005 LET L=HEADINGS+1
5010 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5015 IF STATUS THEN EXITTO 9900
5020 IF A0$(14,1)<>"I" THEN GOTO 5035
5025 DIM B1$(60)
5030 FIND (IVM01_DEV,KEY=N0$+A0$(56),DOM=5035)IOL=IVM01A
5035 DIM WO0$(39)
5040 FIND (WOM10_DEV,KEY=N0$+"A"+A0$(12,2),DOM=5045)IOL=WOM10A
5045 IF A0$(15,1)="O" THEN LET STAT$="**Open**" ELSE IF A0$(15,1)="P" THEN LET STAT$="*Planned*" ELSE IF A0$(15,1)="C" THEN LET STAT$="*Closed*" ELSE LET STAT$=""
5050 PRINT (7)@(2),"WO Number:  ",A0$(5,7),@(30),"Type: ",A0$(12,2)," ",WO0$(6,20),@(60),"Status: ",A0$(15,1)," ",STAT$,@(86),"Date Opened: ",FNB$(A0$(16,3))," Last Close: ",FNB$(A0$(31,3)); LET L=L+1
5055 IF A0$(14,1)<>"I" THEN PRINT (7)@(58),"Category: ",A0$(14,1),'LF',@(0),"Description:",@(14),A1$(1,30),'LF',@(14),A1$(31,30),@(58),"Priority: ",A1$(97,1),; LET L=L+2
5060 IF A0$(14,1)="I" THEN PRINT (7)@(0),"Bill Number: ",@(14),A0$(56),@(58),"Category: ",A0$(14,1),'LF',@(0),"Description:",@(14),FNITEM$(B1$,DESC[1],DESC[2],DESC[3]),'LF',@(58),"Priority: ",A1$(97,1),; LET L=L+2
5065 IF AR$="Y" AND POS(" "<>A0$(34,6))>0 THEN PRINT (7)@(80),"Customer: ",FNF$(A0$(34,P8),P8$),"  ",C1$(1,30); LET L=L+1 ELSE PRINT (7)""; LET L=L+1
5070 PRINT (7)@(2),"Drawing #:  ",A1$(61,25),@(40),"Rev: ",A1$(86,5),
5075 IF AR$="Y" AND NUM(A0$(40,10))>0 THEN PRINT (7)@(77),"Sales Order: ",A0$(40,7),"  Line: ",A0$(47,3); LET L=L+1 ELSE PRINT (7)""; LET L=L+1
5080 IF POS(A0$(14,1)="IN")>0 THEN PRINT (7)@(9),"UM:  ",A0$(50,2),
5085 PRINT (7)@(18),"Warehouse: ",A0$(54,2),@(38),"Yield: ",A[6]:"##0.0%",@(58),"Prod Qty: ",A[0]:M2$,"  Completed: ",A[2]:M2$,@(105),"Last Activity: ",FNB$(A0$(28,3)); LET L=L+1
5090 IF A[4]>0 THEN PRINT (7)'LF',@(1),"****This Close Date: ",FNB$(A1$(91,3)),"     Complete? ",A1$(94,1),@(56),"This Close Amount: ",A[4]:M2$,"  Unit Cost: ",A[5]:M1$,"****"; LET L=L+2
5095 PRINT (7)J2$
5100 LET L=L+1
5105 RETURN 
5200 REM " --- Standards"
5205 IF L+1>L9 THEN GOSUB 5980
5210 ON POS(T2$="ABC")-1 GOTO 5215,5285,5305
5215 IF LEN(SUBS$)=0 THEN GOTO 5240
5220 IF SUBS$(1,3)>=W0$(13,3) THEN GOTO 5240
5225 IF L+1>L9 THEN GOSUB 5980
5230 PRINT (7)@(3),"S",SUBS$(4,3)," ",SUBS$(7,60); LET L=L+1
5235 LET SUBS$=SUBS$(67); GOTO 5215
5240 IF L+1>L9 THEN GOSUB 5980
5245 IF POS(W1$(91,1)=" S")=0 THEN PRINT (7)@(4),W1$(92,60); GOTO 5335
5250 PRINT (7)@(0),W0$(13,3),@(4),W1$(1,3)," ",W1$(7,20),@(132-M1-M2*5-M3*3-9),FNB$(W1$(4,3)),@(132-M1-M2*5-M3*3),W[4]:M2$,W[5]:M2$,@(132-M1-M2*3-M3),W[0]:M2$,@(132-M1-M2*2),W[8]:M2$,W[2]:M2$,; IF T_COST$<>"N" THEN PRINT (7)@(132-M1-M2*3-M3*3),W[6]:M3$,W[7]:M3$,@(132-M1-M2*2-M3),W[1]:M3$,@(132-M1),W[3]:M1$ ELSE PRINT (7)""
5255 IF LEN(SUBS$)=0 THEN GOTO 5280
5260 IF SUBS$(1,3)<>W0$(13,3) THEN GOTO 5280
5265 IF L+1>L9 THEN GOSUB 5980
5270 PRINT (7)@(3),"S",SUBS$(4,3)," ",SUBS$(7,60); LET L=L+1
5275 LET SUBS$=SUBS$(67); GOTO 5255
5280 GOTO 5335
5285 IF L+1>L9 THEN GOSUB 5980
5290 IF POS(W1$(91,1)=" S")=0 THEN PRINT (7)@(4),W1$(92,60); GOTO 5335
5295 PRINT (7)@(0),W0$(13,3),@(4),W1$(27,P[0])," ",X0$,@(132-M1-M2*3-M3-FMASK*3-5),W1$(47,3),"  ",W[8]:FMASK$,W[4]:FMASK$,W[6]:FMASK$,W[5]:M2$,W[0]:M2$,@(132-M1-M2),W[2]:M2$,; IF T_COST$<>"N" THEN PRINT (7)@(132-M1-M2-M3),W[1]:M3$,@(132-M1),W[3]:M1$ ELSE PRINT (7)""
5300 GOTO 5335
5305 IF L+1>L9 THEN GOSUB 5980
5310 IF POS(W1$(91,1)=" S")=0 THEN PRINT (7)@(4),W1$(92,60); GOTO 5335
5315 DIM X0$(30)
5320 IF PO$="Y" THEN FIND (APM01_DEV,KEY=N0$+W1$(7,6),DOM=5325)IOL=APM01A
5325 IF W1$(56,1)="R" THEN LET PSTAT$="Req" ELSE IF W1$(56,1)="P" THEN LET PSTAT$="PO" ELSE IF W1$(56,1)="C" THEN LET PSTAT$="Rec" ELSE LET PSTAT$="None"
5330 PRINT (7)@(0),W0$(13,3),@(4),FNF$(W1$(7,P7),P7$),"  ",X0$(1,20),@(40),W1$(13,30),@(70),W1$(53,3)," ",FNB$(W1$(4,3))," ",PSTAT$,@(132-M1-M2*2-M3),W[0]:M2$,@(132-M1-M2),W[2]:M2$,; IF T_COST$<>"N" THEN PRINT (7)@(132-M1-M2-M3),W[1]:M3$,@(132-M1),W[3]:M1$ ELSE PRINT (7)""
5335 LET L=L+1
5340 RETURN 
5400 REM " --- Comments"
5410 IF L<L9 THEN GOTO 5450
5420 GOSUB 5000
5430 PRINT (7)"*** Comments ***",'LF',J2$
5440 LET L=L+2
5450 PRINT (7)X0$,"  ",X1$
5460 LET L=L+1
5470 RETURN 
5600 REM " --- Transactions"
5610 IF L+2>L9 THEN GOSUB 5000
5620 PRINT (7)"*** Transaction Detail ***",@(132-M1-M3-M2*2-6),"Qty",'LF',@(2),"Date",@(8),"Srce Seq",@(18),"Item/Op Code/Vendor",@(50),"Desc/Emp/Svc Invoice",@(132-M1-M3-M2*2-9),"Complete",@(132-M1-M3-M2-6),"Setup",@(132-M1-M3-6),"Units",@(132-M1-5),"Rate",@(132-6),"Total"
5630 LET L=L+2
5640 RETURN 
5700 REM " --- Cost Summary Header"
5710 IF L+2>L9 THEN GOSUB 5000
5720 PRINT (7)"*** Work Order Cost Summary ***",@(30),M9$,@(44+M2*3),M8$
5730 PRINT (7)@(26+M2),"Std",@(26+M2*2),"Act",@(24+M2*3),"Vrnce",@(31+M2*3),"Vrnce %",@(35+M2*3+M1),"Standard",@(37+M2*3+M1*2),"Actual",@(35+M2*3+M1*3),"Variance",@(33+M2*3+M1*4),"Variance %"
5740 LET L=L+2
5750 RETURN 
5760 IF Z2$<>"Y" THEN LET Z3$="",Z4$="",Z5$=""
5800 REM " --- Standards Totals"
5810 IF T2$="" THEN GOTO 5940
5820 IF T_COST$="N" THEN PRINT (7)""; LET L=L+1; GOTO 5940
5830 IF L+3>L9 THEN GOSUB 5000
5840 IF T2$<>"A" THEN GOTO 5910
5850 IF LEN(SUBS$)=0 THEN GOTO 5890
5860 IF L+1>L9 THEN GOSUB 5000
5870 PRINT (7)@(3),"S",SUBS$(4,3)," ",SUBS$(7,60); LET L=L+1
5880 LET SUBS$=SUBS$(67); GOTO 5850
5890 PRINT (7)@(132-M1-M2*3-M3),J2$(1,M2-1)," ",J2$(1,M3-1),@(132-M1-M2),J2$(1,M2-1)," ",J2$(1,M1-1),'LF',@(10),"Total ",T3$,":",@(132-M1-M2*3-M3),T[0]:M2$,T[1]:M3$,@(132-M1-M2),T[2]:M2$,T[3]:M1$,'LF'
5900 GOTO 5930
5910 PRINT (7)@(132-M1-M2-M3),J2$(1,M3-1),@(132-M1),J2$(1,M1-1),'LF',@(10),"Total ",T3$,":",@(132-M1-M2-M3),T[1]:M3$,@(132-M1),T[3]:M1$,
5920 IF T2$="A" THEN PRINT (7)@(132-M1-M2),T[2]:M2$,'LF' ELSE PRINT (7)'LF'
5930 LET L=L+3
5940 IF T9$<>"" THEN RETURN 
5950 LET T2$=W0$(12,1); DIM T[3]
5960 IF T2$="A" THEN LET T3$="Operations" ELSE IF T2$="B" THEN LET T3$="Materials" ELSE IF T2$="C" THEN LET T3$="Subcontracts" ELSE LET T3$="Other"
5970 LET T2$=W0$(12,1)
5980 IF L+2>L9 THEN GOSUB 5000
5990 IF T2$<>"A" THEN PRINT (7)@(132-M1-M2*2-M3),M8$,M9$,'LF',"-- ",T3$," ",J2$(1,10),@(132-M1-M2-M3-6),"Units",@(132-M1-6),"Units",
6000 IF T2$="A" THEN PRINT (7)@(132-M1-M2*3-M3),M8$,@(132-M1-M2),M9$,'LF',"-- ",T3$," ",J2$(1,10),@(132-M1-M2*2-M3-6),"Units",@(132-M1-6),"Units",
6010 IF T_COST$<>"N" AND T2$<>"A" THEN PRINT (7)@(132-M1-M2-5),"Cost",@(132-5),"Cost",
6020 IF T_COST$<>"N" AND T2$="A" THEN PRINT (7)@(132-M1-M2*2-5),"Cost",@(132-5),"Cost",
6030 ON POS(T2$="ABC")-1 GOTO 6040,6070,6090
6040 PRINT (7)@(132-M1-M2*5-M3*3-9),"Date Req",@(132-M1-M2*4-M3*3-6),"Hours",@(132-M1-M2*3-M3*3-6),"Pc/Hr",@(132-M1-M2-6),"Setup",
6050 IF T_COST$<>"N" THEN PRINT (7)@(132-M1-M2*3-M3*2-7),"Direct",@(132-M1-M2*3-M3-6),"Ov'hd",
6060 GOTO 6110
6070 PRINT (7)@(132-M1-M2*3-M3-FMASK*3-6),"Op Seq",@(132-M1-M2*3-M3-FMASK*2-6),"Scrap",@(132-M1-M2*3-M3-FMASK-8),"Divisor",@(132-M1-M2*3-M3-7),"Factor",@(132-M1-M2*2-M3-8),"Qty Req",
6080 GOTO 6110
6090 PRINT (7)@(70),"Op"; LET L=L+1
6100 PRINT (7)@(40),"Description",@(70),"Seq Date Req Sts",
6110 PRINT (7)""
6120 LET L=L+3
6130 PRINT (7)""
6140 RETURN 
6200 REM " --- Calculate Standards Totals"
6210 IF W0$(12,1)<>"A" THEN GOTO 6300
6220 IF W1$(91,1)<>"S" THEN GOTO 6320
6230 LET Y=POS(W0$(13,3)=Y0$,3)
6240 IF Y=0 AND LEN(Y0$)>=I9*3+3 THEN GOSUB 7900
6250 IF Y=0 THEN LET Y0$=Y0$+W0$(13,3),Y1$=Y1$+W1$(1,3); GOTO 6230
6260 LET Y=INT(Y/3)
6270 LET Y0[Y]=Y0[Y]+W[3],Y1[Y]=Y1[Y]+W[2]
6280 LET S0=S0+W[2]*W[6],S1=S1+W[3]-W[2]*W[6]
6290 GOTO 6320
6300 IF W0$(12,1)="B" THEN LET Y4=Y4+W[3]
6310 IF W0$(12,1)="C" THEN LET Y6=Y6+W[3]
6320 RETURN 
6400 REM " --- Actuals"
6410 IF W0$(15,1)<>"O" THEN GOTO 6480
6420 LET Y=POS(W1$(1,3)=Y0$,3)
6430 IF Y=0 AND LEN(Y0$)>=I9*3+3 THEN GOSUB 7900
6440 IF Y=0 THEN LET Y0$=Y0$+W1$(1,3),Y1$=Y1$+W1$(4,3); GOTO 6420
6450 LET Y=INT(Y/3)
6460 LET Y2[Y]=Y2[Y]+W[2],Y3[Y]=Y3[Y]+W[0]+W[6]
6470 GOTO 6500
6480 IF W0$(15,1)="M" THEN LET Y5=Y5+W[2]
6490 IF W0$(15,1)="S" THEN LET Y7=Y7+W[2]
6500 RETURN 
6600 REM " --- Get Subcontract Info for Operations"
6610 LET SUBS$=""
6620 DIM BLANKS$(30)
6630 READ (WOE32_DEV,KEY=A0$(1,11)+"C",DOM=6640)
6640 LET K0$=KEY(WOE32_DEV,END=6800)
6650 IF POS(A0$(1,11)=K0$)<>1 THEN GOTO 6800
6660 READ (WOE32_DEV)IOL=WOE32A
6670 IF W1$(53,3)="   " THEN GOTO 6640
6680 LET SUBS$=SUBS$+W1$(53,3)+W0$(13,3)
6690 IF W1$(91,1)="S" THEN LET SUBS$=SUBS$+W1$(13,30)+BLANKS$ ELSE LET SUBS$=SUBS$+W1$(92,60)
6700 GOTO 6640
6800 REM " --- Sort SUBS$
6810 IF LEN(SUBS$)<=66 THEN GOTO 6880
6820 FOR L0=1 TO LEN(SUBS$)-66 STEP 66
6830 FOR L1=L0+66 TO LEN(SUBS$) STEP 66
6840 IF SUBS$(L0,6)<=SUBS$(L1,6) THEN GOTO 6860
6850 LET L0$=SUBS$(L0,66),SUBS$(L0,66)=SUBS$(L1,66),SUBS$(L1,66)=L0$
6860 NEXT L1
6870 NEXT L0
6880 RETURN 
7200 REM " --- Serial Numbers Here"
7210 IF POS(P$(17,1)="LS")=0 THEN GOTO 7460
7220 LET X=0,X0=0,X1=0,X2=0
7230 LET X3=M2,X3$=M2$; IF M2<12 THEN LET M2=12
7240 IF M2$(1,1)<>"(" AND LEN(M2$)<M2 THEN LET M2$="#"+M2$; GOTO 7240
7250 IF M2$(1,1)="(" AND LEN(M2$)<M2 THEN LET M2$=M2$(1,1)+"#"+M2$(2); GOTO 7250
7260 READ (WOE06_DEV,KEY=A0$(1,11),DOM=7270)
7270 LET K8$=KEY(WOE06_DEV,END=7390)
7280 IF POS(A0$(1,11)=K8$)<>1 THEN GOTO 7390
7290 READ (WOE06_DEV,KEY=K8$)IOL=WOE06A
7300 LET S9=LEN(W1$(37))
7310 IF X=0 OR L+1>L9 THEN GOSUB 7800
7320 PRINT (7)W0$(12,3),"  ",W1$(37),@(S9+8),W1$(6,20),@(S9+39),W[0]:M2$,
7330 IF W1$(1,1)<>"Y" THEN GOTO 7350
7340 PRINT (7)@(S9+29),FNB$(W1$(2)),@(S9+39+M2),W[2]:M2$,@(S9+39+M2*3),W[3]:M3$,
7350 IF A[4]>0 THEN PRINT (7)@(S9+39+M2*2),W[4]:M2$,
7360 PRINT (7)""
7370 LET L=L+1,X=X+1,X0=X0+W[0],X1=X1+W[2],X2=X2+W[4]
7380 GOTO 7270
7390 IF X=0 THEN LET M2=X3,M2$=X3$; GOTO 7460
7400 IF L+2>L9 THEN GOSUB 7800
7410 PRINT (7)@(S9+39),J2$(1,M2-1)," ",J2$(1,M2-1)," ",J2$(1,M2-1)
7420 PRINT (7)@(30),"Total: ",@(S9+39),X0:M2$,X1:M2$,X2:M2$
7430 LET L=L+2
7440 LET M2=X3,M2$=X3$
7450 PRINT (7)J2$; LET L=L+1
7460 GOTO 2000
7600 REM " --- Channel Assignments "
7610 CLOSED_WO_TRANS: 
7620 LET WOTRANO_DEV=WOT03_DEV,WOTRANM_DEV=WOT23_DEV,WOTRANS_DEV=WOT33_DEV
7630 RETURN 
7640 OPEN_WO_TRANS: 
7650 LET WOTRANO_DEV=WOT01_DEV,WOTRANM_DEV=WOT21_DEV,WOTRANS_DEV=WOT31_DEV
7660 RETURN 
7800 REM " --- Serial Number Heading"
7810 IF L+4>L9 THEN GOSUB 5000
7820 PRINT (7)"*** Serial Or Lot # Detail ***",'LF',
7830 PRINT (7)@(S9+30),"Closed",@(S9+39+M2-9),"Schedule",@(S9+39+M2*2-7),"Closed",@(S9+39+M2*3-8),"Current",@(S9+39+M2*3+M3-5),"Unit"
7840 PRINT (7)@(0),"Seq  Serial Or Lot #",@(S9+9),"Comment",@(S9+31),"Date",@(S9+39+M2-9),"Prod Qty",@(S9+39+M2*2-4),"Qty",@(S9+39+M2*3-10),"Close Qty",@(S9+39+M2*3+M3-5),"Cost",'LF'
7850 LET L=L+4
7860 RETURN 
7900 REM " --- Number of operations exceeds max per work order"
7910 IF WARN$<>"" THEN GOTO 7950
7920 LET MESSAGE$[0]="The Number Of Operations Parameter Needs To Be Increased"
7930 LET MESSAGE$[1]="          Press <Enter> To Continue"
7940 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
7950 LET Y=LEN(Y0$)-2,WARN$="Y"
7960 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNG$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8040 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

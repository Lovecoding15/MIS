0010 REM "PRU - Payroll Calculation"
0020 REM "Program PRU.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Jul96 WGH: Modify calculations for new Connecticut state tax withholdings (6327,8415,8695-8695)
0032 REM "15Jul96 WGH: Taxable Amount not reduced by Deduction Amount (230-240,2140,3010,7280,7350)"
0033 REM "13Nov96 Caj: Tax and Contrib limits weren't working correctly for checks with multiple seq #s (215,255,280,6900-6999)
0034 REM "13Nov96 Caj: Correct infinite loop. Add Parameter error call section. (450,9800)
0035 REM "02Jan97 CAJ: Correct branch to 'Missing Ded Code' section (7060,9400-9445,9590)
0036 REM "29May97 WGH: Supplemental earnings are doubled in tax basis when not using tax table (6480)"
0037 REM "15Jan98 WGH: Earnings taxed on a Supplemental basis are not being taxed at the supplemental rate (3250,5930-5935,6360,7770,7780,7855)"
0038 REM "09Dec98 WGH: Do Maryland standard deduction for both Single, and Married Filing Jointly (8420)
0039 REM "10Dec98 WGH: Update constants used in Oklahoma tax calculations (8330,8810-8840)
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=15
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRE-01",FILES$[2]="PRE-01",FILES$[3]="PRE-11"
0130 LET FILES$[4]="PRE-21",FILES$[5]="PRE-31",FILES$[6]="PRE-41"
0135 LET FILES$[7]="PRE-51",FILES$[8]="PRM-01",FILES$[9]="PRM-10"
0140 LET FILES$[10]="PRT-01",FILES$[11]="PRT-11",FILES$[12]="PRT-21"
0155 LET FILES$[13]="PRT-31",FILES$[14]="PRW-01",FILES$[15]="SYS-01"
0160 CALL "SYC.DA",1,1,15,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0175 LET PRE01_DEV1=CHANNELS[1],PRE01_DEV2=CHANNELS[2],PRE11_DEV=CHANNELS[3]
0180 LET PRE21_DEV=CHANNELS[4],PRE31_DEV=CHANNELS[5],PRE41_DEV=CHANNELS[6]
0185 LET PRE51_DEV=CHANNELS[7],PRM01_DEV=CHANNELS[8],PRM10_DEV=CHANNELS[9]
0190 LET PRT01_DEV=CHANNELS[10],PRT11_DEV=CHANNELS[11],PRT21_DEV=CHANNELS[12]
0195 LET PRT31_DEV=CHANNELS[13],PRW01_DEV=CHANNELS[14],SYS01_DEV=CHANNELS[15]
0200 REM " --- IOLists
0210 PRE01A: IOLIST A0$
0220 PRE11A: IOLIST W0$(1),W[0],W[1],W[2],W1$,W[3]
0230 PRE21A: IOLIST W0$(1),W[0],W[1],W[2]
0240 PRE21A1: IOLIST W4$
0250 PRE31A: IOLIST W0$(1),W[0],W[1],W[2]
0255 PRE31A1: IOLIST V$,EMPLEETAXABLE,*,EMPLEETAX
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0265 PRS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 PRS01B: IOLIST U0$,U1$
0275 PRS01C: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0280 PRE41A1: IOLIST V$,EMPLEEBASIS,*,EMPLEECONTRIB
0285 PRE41A: IOLIST W0$(1),W[0],W[1],W[2]
0290 PRE51A: IOLIST W0$(1),W[0],W[1],W[2]
0295 PRM01A: IOLIST B0$,B1$
0300 PRM10A: IOLIST X0$(1),X[0],X[1],X[2],X[3],X1$,X2$,X3$
0310 PRM10B: IOLIST X0$(1),X1$,X[0],X[1],X1$,X2$
0320 PRM10C: IOLIST X0$(1),X[0],X[1],X[2],X1$,X2$
0330 PRM10C1: IOLIST Z0$(1)
0340 PRM10D: IOLIST X0$(1),X[0],X[1],X[2],X1$,X2$
0345 PRM10I: IOLIST *,LOWL
0350 PRM10I1: IOLIST D0$,D[ALL],I[ALL],J[ALL],K[ALL]
0360 PRT01A: IOLIST Y0$,Y[0],Y[1],Y[2],Y[3],Y[4],Y[5],Y[6],Y[7],Y[8],Y[9],Y[10],Y[11],Z[ALL]
0370 PRT11A: IOLIST Y0$,Y[0],Y[1],Y[2],Y[3],Y[4],Y[5],Z[0],Z[1]
0380 PRT21A: IOLIST Y0$(1),Y[ALL],Z[0],Z[1]
0385 PRT21A1: IOLIST Z1$(1)
0390 PRT31A: IOLIST Y0$(1),Y[ALL],Z[0],Z[1]
0400 REM " --- Parameters
0405 DIM P[3]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0440 FIND (SYS01_DEV,KEY=N0$+N2$+"01",DOM=9800)IOL=PRS01B
0450 FIND (SYS01_DEV,KEY=N0$+N2$+"02",DOM=0460)IOL=PRS01C
0460 IF LEN(G7$)<2 THEN DIM G7$(18)
0470 LET P9$=U1$(6,10),P[0]=NUM(P2$(1,2))
0480 LET P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(5,1)),P[3]=NUM(P3$(7,1))
0500 REM " --- Init Data
0510 DIM U[3],MESSAGE$[5],X0$(85),Y0$(48)
0520 DIM W0$(32),W[3],Y[17],X[3],Z[3],I[10],J[10],K[10],D[2],T[99,3]
0560 LET G5=LEN(G5$),FI_MC=.0765; REM "FICA + Medicare rates"
0570 LET T2$="DWBSMQA",T1$="365052026024012004001",Z1=0,Z2=0,W0=0,W1=0
0600 REM " --- Check printing started?"
0610 READ (PRW01_DEV,KEY=N0$,DOM=0620)
0620 LET K$=KEY(PRW01_DEV,END=0700)
0630 IF POS(N0$=K$)<>1 THEN GOTO 0700
0640 DIM MESSAGE$[1]
0650 LET MESSAGE$[0]="Check Printing Has Already Been Started"
0660 LET MESSAGE$[1]="      Press <Enter> To Continue"
0670 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
0680 IF V$="SPECIAL" THEN GOTO 0700
0690 GOTO 9900
0700 REM " --- Background
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,6,0
0730 PRINT 'SB',@(27,5),"Employee No:",'SF',
0800 REM " --- Options
0810 LET V4$="Do You Wish To Update The Payroll Earnings"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V3=4 OR V$="NO" THEN GOTO 9900
0840 IF V$<>"YES" THEN GOTO 0800
0900 REM " --- Disallow 'M'enu option in Error Routine
0910 PRECISION P[3]
0950 LET EXIT_CTRL=1
1000 REM " --- Init Read
1090 READ (PRE01_DEV1,KEY=N0$,DOM=1100)
1100 REM " --- Main Read
1120 LET K$=KEY(PRE01_DEV1,END=4000)
1140 IF POS(N0$=K$)<>1 THEN GOTO 4000
1150 IF K$(12,1)<>"C" THEN READ (PRE01_DEV1,KEY=K$(1,21)+"Z",DOM=1100)
1160 READ (PRE01_DEV1)IOL=PRE01A
1200 PRINT @(40,5),FNF$(A0$(3,P[0]),M0$),
1220 IF K$(1,11)<>T0$ THEN GOSUB 5000
1300 REM " --- Read Detail
1310 REM " --- Earnings
1320 READ (PRE11_DEV,KEY=A0$(1,21),DOM=1400)
1400 REM " --- Read Earnings Records
1420 LET K$=KEY(PRE11_DEV,END=2000)
1430 IF K$(1,21)<>A0$(1,21) THEN GOTO 2000
1450 READ (PRE11_DEV)IOL=PRE11A
1800 REM " --- Earnings
1820 DIM X0$(100)
1840 READ (PRM10_DEV,KEY=N0$+"A"+W0$(25,2),DOM=1880)IOL=PRM10A
1850 REM "Z2 will total gross earnings for those states that reduce the tacable amount by a percentage before it is reduced by federal taxes
1855 LET Z2=Z2+W[3],Z1=0
1860 GOSUB 5800
1880 GOTO 1400
2000 REM " --- Deductions
2020 READ (PRE21_DEV,KEY=A0$(1,21),DOM=2100)
2100 REM " --- Read Deductions Records
2120 LET K$=KEY(PRE21_DEV,END=3000)
2130 IF K$(1,21)<>A0$(1,21) THEN GOTO 3000
2140 IF T9$="" THEN GOSUB 7000; READ (PRE21_DEV,KEY=A0$(1,21)+"B",DOM=2100); GOTO 2100
2150 READ (PRE21_DEV)IOL=PRE21A
2200 REM " --- Deductions
2220 DIM X0$(100)
2240 READ (PRM10_DEV,KEY=N0$+"B"+W0$(25,2),DOM=2290)IOL=PRM10B
2260 GOSUB 5800
2290 GOTO 2100
3000 REM " --- Calculate Taxes
3005 LET W0=1
3010 IF T9$="" THEN GOSUB 7000; READ (PRE21_DEV,KEY=A0$(1,21)+"B",DOM=2100); GOTO 2100
3015 DIM Y0$(48)
3020 EXTRACT (PRT21_DEV,KEY=T0$+"C"+G7$(1,2),DOM=3080)IOL=PRT21A; REM "Start w/FED taxes
3040 GOTO 3130
3080 READ (PRT21_DEV,KEY=T0$+"C",DOM=3090)
3100 LET K$=KEY(PRT21_DEV,END=3300)
3110 IF K$(1,12)<>T0$+"C" THEN GOTO 3300
3120 READ (PRT21_DEV)IOL=PRT21A
3127 IF Y0$(13,2)=G7$(1,2) THEN GOTO 3100
3130 READ (PRM10_DEV,KEY=N0$+"C"+Y0$(13,2),DOM=3287)IOL=PRM10C
3140 IF POS(U1$(4,1)=X0$(25,5))=0 THEN GOTO 3287
3145 IF Y0$(24,1)<>"N" THEN IF POS(" "<>Y0$(24,5))>0 AND POS(U1$(4,1)=Y0$(24,5))=0 THEN GOTO 3287
3150 IF X0$(24,1)="T" THEN READ (PRM10_DEV,KEY=N0$+"I"+Y0$(15,3),DOM=3287)IOL=PRM10I1
3170 IF X0$(24,1)="T" THEN GOSUB 6000
3180 IF X0$(24,1)<>"T" THEN GOSUB 6500
3220 LET W0$(1)=A0$(1,21)+"C"+STR(W0:"00")+Y0$(13,2)+Y0$(15,3)
3240 PRECISION 2
3250 LET W[2]=W[2]*1,SUPTAX=SUPTAX*1,REGTAX=W[2]-SUPTAX; REM " This sets the tax $ amt to 2 decimal places
3252 PRECISION P[3]
3260 WRITE (PRE31_DEV,KEY=W0$(1,24))IOL=PRE31A
3280 LET W0=W0+1
3285 GOSUB 5800
3287 IF Y0$(13,2)=G7$(1,2) THEN GOTO 3080
3290 GOTO 3100
3300 REM " --- Calculate Contributions
3320 READ (PRT31_DEV,KEY=T0$+"D",DOM=3330)
3330 LET W0=1; DIM Z[3]
3400 LET K$=KEY(PRT31_DEV,END=3600)
3410 IF K$(1,12)<>T0$+"D" THEN GOTO 3600
3420 READ (PRT31_DEV)IOL=PRT31A
3430 READ (PRM10_DEV,KEY=N0$+"D"+Y0$(13,2),DOM=3400)IOL=PRM10D
3440 IF POS(" "<>X0$(25,5))=0 THEN GOTO 3460
3450 IF POS(U1$(4,1)=X0$(25,5))=0 THEN GOTO 3400
3455 IF POS(U1$(4,1)=Y0$(15,5))=0 AND Y0$(15,5)<>FILL(5) THEN GOTO 3400
3460 GOSUB 6500
3480 LET W0$(1)=A0$(1,21)+"D"+STR(W0:"00")+Y0$(13,2)
3500 PRECISION 2
3510 LET W[2]=W[2]*1; REM "Sets contrub $ amt to 2 decimal places
3520 PRECISION P[3]
3560 WRITE (PRE41_DEV,KEY=W0$(1,24))IOL=PRE41A
3580 LET W0=W0+1
3585 GOSUB 5800
3590 GOTO 3400
3600 REM " --- Calculate Accruals
3610 IF A0$(12,3)<>"C01" THEN GOTO 3900
3620 READ (PRT01_DEV,KEY=T0$+"A",DOM=3630)
3630 LET W0=1
3700 LET K$=KEY(PRT01_DEV,END=3900)
3710 IF K$(1,12)<>T0$+"A" THEN GOTO 3900
3720 READ (PRT01_DEV)IOL=PRT01A
3740 READ (PRM10_DEV,KEY=N0$+"A"+Y0$(13,2),DOM=3700)IOL=PRM10A
3750 IF X0$(26,1)="N" THEN GOTO 3700
3760 GOSUB 7400
3770 PRECISION 2
3772 LET W[2]=W[2]*1
3774 PRECISION P[3]
3780 LET W0$(1)=A0$(1,21)+"E"+STR(W0:"00")+Y0$(13,2)
3800 WRITE (PRE51_DEV,KEY=W0$(1,24))IOL=PRE51A
3820 LET W0=W0+1
3890 GOTO 3700
3900 REM " --- Clear
3920 DIM T[99,3]; LET STCHG$=""
3940 REM LET T9$=""; REM -- Implement this line if you want calculations on checks > "01"
3990 GOTO 1100
4000 REM " --- End
4100 GOTO 9900
5000 REM " --- New Employee
5020 LET T0$=K$(1,11),T9$=""
5030 DIM B1$(300)
5040 FIND (PRM01_DEV,KEY=T0$,DOM=5050)IOL=PRM01A
5090 RETURN 
5800 REM " --- Update Totals
5820 FOR X=0 TO LEN(X1$)/2-1
5830 IF X1$(X*2+1,2)="  " THEN GOTO 5950
5840 LET X0=1,Z1=0
5850 IF X2$(X+1,1)="-" THEN LET X0=-1
5855 REM "T(<T>,0)=Total Code; T(T,<0>)=REGPAY for TOTCODE T,T(T,<1>)=Units for TOTCODE T, T(T,<2>) = Supplemental pay for TOTCODE T, T(T,<3>)=Amt for Workers Comt
5860 LET T=NUM(X1$(X*2+1,2))
5862 IF W0$(22,1)="C" AND W0$(25,2)=G7$(1,2) THEN GOSUB 7700
5865 IF W0$(22,1)<>"A" THEN GOTO 5910
5870 IF X3$(X+1,1)=" " THEN LET T[T,0]=T[T,0]+X0*(W[2]+U[2])
5880 IF X[1]=0 THEN LET X[1]=1
5889 REM "          P=Calc on REG portion of premium pay
5890 IF X3$(X+1,1)="P" THEN LET T[T,0]=T[T,0]+X0*(W[2]/X[1]+U[2]/X[1])
5899 REM "          S=Earnings to be taxed on a supplemental basis
5900 IF X3$(X+1,1)="S" THEN LET T[T,2]=T[T,2]+X0*(W[2]+U[2])
5910 LET T[T,1]=T[T,1]+X0*(W[1]+U[1])
5915 IF W0$(22,1)<>"A" THEN GOTO 5930
5920 IF W[1]<>0 AND B1$(225,1)<>"S" THEN LET T[T,3]=T[T,3]+X0*(W[1]*W[3]-U[1]*U[3])
5922 IF W[1]=0 OR B1$(225,1)="S" THEN LET T[T,3]=T[T,3]+W[2]
5930 IF POS(W0$(22,1)="AC")=0 THEN LET T[T,0]=T[T,0]+X0*(W[2]+U[2]),T[T,3]=T[T,3]+X0*(W[2]+U[2])
5935 IF W0$(22,1)="C" THEN LET T[T,0]=T[T,0]+X0*(REGTAX+U[2]),T[T,2]=T[T,2]+X0*SUPTAX,T[T,3]=T[T,3]+X0*(W[2]+U[2])
5940 IF STCHG$="Y" THEN LET W[2]=Z1
5950 NEXT X
5990 RETURN 
6000 REM " --- Tax Calc
6010 LET W[2]=0,W[1]=0,T=NUM(X0$(31,2)),W[0]=T[T,0]
6020 IF W[0]=0 THEN IF T[T,2]=0 THEN RETURN 
6030 LET X0=ABS(NUM(T1$(POS(A0$(25,1)=T2$)*3-2,3))*W[0]),X2=0
6040 IF POS(" "<>X0$(60,5))=0 THEN LET X0$(60,5)="LIST"
6045 GOSUB STATES
6050 FOR Q0=0 TO 4
6060 IF X0$(60+Q0,1)="T" THEN GOSUB 6200
6070 IF X0$(60+Q0,1)="S" THEN LET X0=X0-D[1]
6080 IF X0$(60+Q0,1)="I" THEN LET X0=X0-NUM(Y0$(20,2))*D[2]
6090 IF X0$(60+Q0,1)="D" THEN LET X0=X0-NUM(Y0$(18,2))*D[1]
6100 IF X0$(60+Q0,1)="L" THEN IF D[0]<>0 THEN IF X0<D[0] THEN LET X0=0
6170 NEXT Q0
6171 IF X0$(3,3)="COK" THEN LET X0=X0+OKSUP
6172 LET X0=X0*SGN(W[0])
6175 IF X0$(3,3)<>"CEI" THEN GOTO 6185
6180 IF W[0]>0 AND X0>0 THEN LET X0=0 ELSE IF W[0]<0 AND X0<0 THEN LET X0=0
6182 GOTO 6190
6185 IF W[0]>0 AND X0<0 THEN LET X0=0 ELSE IF W[0]<0 AND X0>0 THEN LET X0=0
6190 LET W[2]=X0
6195 GOTO 6350
6200 REM " --- Table
6210 LET W[2]=0
6220 IF X0$(3,3)="COK" THEN GOSUB OKLAHOMA2
6270 FOR X=0 TO 10
6280 IF X0<=I[X] THEN EXITTO 6320
6290 IF I[X]=0 THEN EXITTO 6320
6300 NEXT X
6320 LET X=X-1
6325 IF X<0 THEN LET X=0
6327 IF Y0$(13,2)="CT" THEN GOSUB CONN; GOTO 6349
6330 LET W[2]=J[X]+K[X]*(X0-I[X])/100,X2=0
6332 IF Y0$(13,2)="PR" THEN LET W[2]=X0*K[X]/100-J[X]
6340 IF Y0$(13,2)="CA" THEN FIND (PRM10_DEV,KEY=N0$+"I"+Y0$(17,1)+Y0$(15,2),DOM=6342); GOSUB CALTAXCR
6342 IF Y0$(13,2)="IA" THEN GOSUB IOWA
6348 LET X0=W[2]
6349 RETURN 
6350 IF ABS(X2)>ABS(W[2]) THEN LET X2=W[2]
6360 LET REGTAX=(W[2]-X2)/NUM(T1$(POS(A0$(25,1)=T2$)*3-2,3)),SUPTAX=T[T,2]*X[0]/100,W[2]=REGTAX+SUPTAX
6370 IF Y0$(22,1)="N" THEN GOTO 6450
6380 IF Y0$(22,1)="R" AND Y0$(23,1)="A" THEN LET W[2]=Z[0]*SGN(W[0])
6390 IF Y0$(22,1)="R" AND Y0$(23,1)="P" THEN LET W[2]=Z[0]*W[0]/100
6410 IF Y0$(22,1)="A" AND Y0$(23,1)="A" THEN LET W[2]=W[2]+Z[0]*SGN(W[0])
6420 IF Y0$(22,1)="A" AND Y0$(23,1)="P" THEN LET W[2]=W[2]+Z[0]*W[0]/100
6480 IF X0$(24,1)="T" THEN LET W[0]=W[0]+T[T,2]
6490 RETURN 
6500 REM " --- Rate
6510 LET W[0]=0,W[1]=0,W[2]=0,T=NUM(X0$(31,2))
6520 IF X0$(24,1)="N" THEN RETURN 
6530 IF X0$(24,1)<>"F" THEN GOTO 6600
6535 REM " --- Fixed Amt Calculation
6540 IF X0$(3,1)<>"C" THEN LET W[2]=Z[0]
6550 IF W[2]=0 THEN LET W[2]=X[0]
6555 LET X1=0,X3=0; IF A0$(13,2)>"01" THEN GOSUB 6900
6560 IF X0$(3,3)="CPO" THEN GOSUB PAOPT
6565 IF SGN(T[T,0])<0 THEN LET W[2]=MIN(W[2],Y[13]+Y[14]+Y[15]+Y[16]+X3),X[2]=0
6570 IF X[2]<>0 THEN IF X[2]<W[2]+Y[13]+Y[14]+Y[15]+Y[16]+X3 THEN LET W[2]=X[2]-Y[13]-Y[14]-Y[15]-Y[16]-X3
6575 LET W[2]=W[2]*SGN(T[T,0])
6595 GOTO 6690
6600 IF X0$(24,1)="D" THEN LET W[0]=T[T,0]+T[T,2]
6610 IF X0$(24,1)="U" THEN LET W[0]=T[T,1]
6612 IF X0$(24,1)="D" THEN IF X0$(30,1)="R" THEN LET W[0]=T[T,3]
6614 IF X0$(24,1)="D" THEN IF X0$(30,1)="P" THEN LET W[0]=T[T,0]-T[T,3]
6615 IF W[0]=0 THEN RETURN 
6620 IF X0$(3,1)<>"C" THEN LET W[1]=Z[0]
6630 IF W[1]=0 THEN LET W[1]=X[0]
6640 GOSUB 6700
6660 LET W[2]=W[0]*W[1]
6669 REM "Removed 'U' from next stmt so that there is no % of units- If needed, the 'D' must be 'DU'
6670 IF POS(X0$(24,1)="D")>0 THEN LET W[2]=W[2]/100
6671 REM " --- Changes percentage rate into decimal
6680 IF W[1]<>0 AND X[2]<>0 THEN IF X[0]*X[2]/100<W[2]+Y[13]+Y[14]+Y[15]+Y[16]+X3 THEN LET W[2]=W[1]*X[2]/100-Y[13]-Y[14]-Y[15]-Y[16]-X3
6690 IF X0$(3,3)="Cny" THEN GOSUB NYSDI
6692 IF Y0$(12,1)="C" THEN GOTO 6370
6695 RETURN 
6700 REM " --- Earnings Limit
6710 LET X1=0,X3=0
6720 IF A0$(13,2)>"01" THEN GOSUB 6900
6723 REM "'D'=Cont,Z(1)=Emplee's Max Limit,X(2)=Max Limit against which the Wages-to-Date are compared
6725 IF X0$(3,1)="D" THEN IF Z[1]<>0 THEN LET X[2]=Z[1]
6730 IF X[2]<>0 THEN IF Y[7]+Y[8]+Y[9]+Y[10]+W[0]+X1>X[2] THEN LET W[0]=X[2]-Y[7]-Y[8]-Y[9]-Y[10]-X1
6790 RETURN 
6800 CALTAXCR: REM "CA Tax Credit
6810 LET X2=0
6820 READ (PRM10_DEV,KEY=N0$+"I"+Y0$(17,1)+Y0$(15,2),DOM=6890)IOL=PRM10I1
6830 LET X2=NUM(Y0$(18,2))
6840 IF X2>10 THEN LET X2=10
6850 LET X2=J[X2]
6880 LET X2=X2*SGN(W[0])
6890 RETURN 
6900 REM " --- Other Totals
6905 IF Y0$(12,1)<>"C" THEN GOTO CONTRIB_TOTS
6910 READ (PRE31_DEV,KEY=A0$(1,12),DOM=6915)
6915 LET K$=KEY(PRE31_DEV,END=DONE_TOTS)
6920 IF K$(1,12)<>A0$(1,12) THEN GOTO DONE_TOTS
6925 IF K$(1,14)>=A0$(1,14) THEN GOTO DONE_TOTS
6930 READ (PRE31_DEV)IOL=PRE31A1
6935 IF V$(22,1)+V$(25,2)<>Y0$(12,3) THEN GOTO 6915
6940 LET X1=X1+EMPLEETAXABLE,X3=X3+EMPLEETAX
6945 GOTO 6915
6950 CONTRIB_TOTS: 
6955 IF Y0$(12,1)<>"D" THEN GOTO DONE_TOTS
6960 READ (PRE41_DEV,KEY=A0$(1,12),DOM=6965)
6965 LET K$=KEY(PRE41_DEV,END=DONE_TOTS)
6970 IF K$(1,12)<>A0$(1,12) THEN GOTO DONE_TOTS
6975 IF K$(1,14)>=A0$(1,14) THEN GOTO DONE_TOTS
6980 READ (PRE41_DEV)IOL=PRE41A1
6985 IF V$(22,1)+V$(25,2)<>Y0$(12,3) THEN GOTO 6965
6990 LET X1=X1+EMPLEEBASIS,X3=X3+EMPLEECONTRIB
6993 GOTO 6965
6995 DONE_TOTS: RETURN 
7000 REM " --- Voluntary Deductions
7005 READ (PRT11_DEV,KEY=T0$+"B",DOM=7010)
7010 LET W0=0,T9$="A",W1=0
7015 GOSUB 7300; REM "Get last Seq Number of existing DED entries
7020 LET K$=KEY(PRT11_DEV,END=7290)
7025 IF K$(1,12)<>T0$+"B" THEN GOTO 7290
7030 READ (PRT11_DEV)IOL=PRT11A
7055 IF Y0$(20,1)<>"Y" THEN GOTO 7020; REM "Auto generation or not
7060 FIND (PRM10_DEV,KEY=N0$+"B"+Y0$(13,2),DOM=9400)IOL=PRM10B
7065 IF POS(U1$(4,1)=X0$(25,5))=0 THEN GOTO 7020
7070 IF POS(U1$(4,1)=Y0$(15,5))=0 AND Y0$(15,5)<>FILL(5) THEN GOTO 7020; REM "Pay Periods to be used compared to Current Pay Period
7080 IF LEN(Y0$)<21 THEN LET Y0$=Y0$+FILL(21-LEN(Y0$))
7085 IF LEN(X0$)>32 THEN LET Y0$(21,1)=X0$(33,1)
7100 LET W[0]=0,W[1]=0,W[2]=0
7110 LET T=NUM(X0$(30,2))
7120 IF Z[0]=0 THEN LET Z[0]=X[0]
7125 IF Z[1]=0 THEN LET Z[1]=X[1]
7130 IF X0$(24,1)="F" THEN GOTO 7180
7140 IF X0$(24,1)="U" THEN LET W[0]=T[T,1],W[1]=Z[0]
7150 IF X0$(24,1)="D" THEN LET W[0]=T[T,0],W[1]=Z[0]
7155 LET W[2]=W[1]*W[0]/100
7160 PRECISION 2
7165 LET W[2]=W[2]*1
7170 PRECISION P[3]
7175 GOTO 7200
7180 LET W[2]=Z[0]
7200 IF POS("B"=Y0$(21,1))<>0 THEN IF Z[1]-W[2]<0 THEN LET W[2]=Z[1]
7210 IF POS("B"=Y0$(21,1))=0 THEN IF Z[1]<>0 THEN IF (W[2]+Y[1]+Y[2]+Y[3]+Y[4])*SGN(Z[1])>Z[1]*SGN(Z[1]) THEN LET W[2]=Z[1]-Y[1]-Y[2]-Y[3]-Y[4]
7215 IF W[2]=0 THEN GOTO 7020
7225 LET W0=W0+1,W1=W0
7235 IF POS(Y0$(13,2)=W3$,2)=0 THEN GOTO 7250
7240 LET W1=NUM(W2$(POS(Y0$(13,2)=W3$,2),2))
7250 LET W0$(1)=A0$(1,21)+"B"+STR(W1:"00")+Y0$(13,2)
7255 READ RECORD(PRE21_DEV,KEY=W0$(1,24),DOM=7280)W5$
7260 IF W5$(25,2)=W0$(25,2) THEN GOTO 7280
7265 LET W0=W0+1,W1=W0
7270 GOTO 7250
7280 WRITE (PRE21_DEV,KEY=W0$(1,24))IOL=PRE21A
7285 GOTO 7020
7290 RETURN 
7300 REM " --- Get Seq Numbers & existing DED Entries
7310 LET K2$="",W2$="",W3$="",W4$=""
7320 READ (PRE21_DEV,KEY=A0$(1,21)+"B",DOM=7330,END=7390)
7330 LET K2$=KEY(PRE21_DEV,END=7390)
7340 IF K2$(1,22)<>A0$(1,21)+"B" THEN GOTO 7390
7350 READ (PRE21_DEV)IOL=PRE21A1
7360 LET W2$=W2$+W4$(23,2),W3$=W3$+W4$(25,2)
7365 REM "W2$=All the Seq Numbers, W3$=All the DED Codes
7370 GOTO 7330
7390 RETURN 
7400 REM " --- Accrual Calc
7410 LET W[0]=0,W[1]=0,W[2]=0
7420 IF X0$(26,1)="F" THEN GOTO 7500
7430 LET T=NUM(X0$(28,2))
7440 LET W[1]=Z[3]
7450 IF W[1]=0 THEN LET W[1]=X[3]
7460 IF X0$(26,1)="D" THEN LET W[0]=T[T,0]+T[T,2]
7470 IF X0$(26,1)="U" THEN LET W[0]=T[T,1]
7480 LET W[2]=W[0]*W[1]/100
7490 GOTO 7520
7500 LET W[2]=Z[3]
7510 IF W[2]=0 THEN LET W[2]=X[3]
7520 RETURN 
7700 REM " -- Deduct Fed taxes from state taxable Earnings"
7705 LET X8=0,Z1=W[2],LOWL=0,PER=NUM(T1$(POS(A0$(25,1)=T2$)*3-2,3)),STCHG$="N"
7710 DIM Z0$(33),Z1$(48)
7715 FOR X9=0 TO LEN(FNP$(X0$(70,14)))/2-1
7720 LET X9$=X0$(70+X9*2,2)
7725 FIND (PRM10_DEV,KEY=N0$+"C"+X9$,DOM=7765)IOL=PRM10C1
7730 IF Z0$(31,2)<>X1$(X*2+1,2) THEN GOTO 7765
7735 FIND (PRT21_DEV,KEY=T0$+"C"+X9$,DOM=7765)IOL=PRT21A1
7740 IF POS(" "<>Z1$(24,5))>0 AND POS(U1$(4,1)=Z1$(24,5))=0 THEN GOTO 7765
7745 FIND (PRM10_DEV,KEY=N0$+"I"+Z1$(15,3),DOM=7765)IOL=PRM10I
7750 ON POS(X9$="ORALMA",2) GOSUB 7790,OREGON,7790,ALABAMA,7790,MASS
7760 LET STCHG$="Y"
7765 NEXT X9
7770 IF REGTAX+SUPTAX<>0 THEN LET TAXRATIO=SUPTAX/(REGTAX+SUPTAX) ELSE LET TAXRATIO=0
7775 PRECISION 2
7780 LET W[2]=W[2]*1,SUPTAX=W[2]*TAXRATIO,REGTAX=W[2]-SUPTAX
7785 PRECISION P[3]
7790 RETURN 
7800 MASS: REM "Massachusetts State Tax"
7805 LET X8=ABS(T[T,0]*PER)
7810 IF NUM(Z1$(20,2))>0 AND X8<LOWL THEN GOTO 7845
7815 LET MASDED=X8*FI_MC
7820 IF MASDED>2000 THEN LET MASDED=2000
7830 LET MASDED=MASDED/PER
7840 LET W[2]=MASDED*SGN(W[0])
7845 RETURN 
7850 OREGON: REM "Oregon State Tax"
7855 LET W[2]=W[2]-SUPTAX
7860 LET X8=ABS(W[2]*PER)
7865 IF X8<3000 THEN GOTO 7890
7870 LET X8=3000
7875 LET X8=X8/PER
7885 LET W[2]=X8*SGN(W[0])
7890 RETURN 
7950 ALABAMA: REM "Alabama State Tax"
7960 LET X8=ABS(T[T,0])*PER
7965 LET ALDED=X8*.2
7970 IF Z1$(15,3)="AL2" AND ALDED>4000 THEN LET ALDED=4000
7975 IF POS(Z1$(15,3)="AL0AL1")>0 AND ALDED>2000 THEN LET ALDED=2000
7980 LET X8=ALDED/PER
7985 LET W[2]=W[2]+X8*SGN(W[0])
7990 RETURN 
8000 REM " --- Functions
8080 DEF FNP$(Q$)=CVS(Q$,2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
8300 OKLAHOMA2: 
8330 LET X0=X0-.15*(OKTAXBL-NUM(Y0$(20,2))*2750-OKSTD)
8390 RETURN 
8400 STATES: 
8405 IF X0$(3,3)="COK" THEN GOSUB OKLAHOMA1
8410 IF Y0$(13,2)="PR" THEN LET X0=X0-D[0]
8420 IF X0$(3,3)="CMD" THEN LET D[1]=MIN(MAX(D[1]*.75,X0*.15),D[1])
8425 IF Y0$(13,2)="AR" THEN IF X0*.1<D[1] THEN LET D[1]=X0*.1
8430 IF X0$(3,3)="COK" THEN LET OKSTD=D[1],D[1]=MIN(MAX(1000,X0*.15),2000)
8435 IF X0$(3,3)="CNN" OR X0$(3,3)="CYN" THEN GOSUB NEWYORK
8440 IF X0$(3,3)="CMA" THEN IF NUM(Y0$(20,2))=0 THEN LET D[1]=0,D[0]=0 ELSE IF D[0]<X0/(1-FI_MC) THEN LET D[0]=0
8490 RETURN 
8500 NEWYORK: 
8505 LET NYCDED=0
8510 FOR I=3 TO 1 STEP -1
8520 IF X0-I*10000<0 THEN LET NYCDED=NYCDED+1000
8530 NEXT I
8540 LET D[1]=NYCDED
8545 RETURN 
8550 NYSDI: 
8555 LET X8=W[0],PER=NUM(T1$(POS(A0$(25,1)=T2$)*3-2,3))
8560 LET NYSDI=X[0]/100*X[2]
8570 IF ABS(W[2])*PER<=NYSDI THEN GOTO 8590
8575 LET W[2]=NYSDI/PER,W[0]=X[2]/PER
8580 LET W[0]=W[0]*SGN(X8),W[2]=W[2]*SGN(X8)
8590 RETURN 
8600 PAOPT: 
8610 IF X[1]<>0 AND X[1]*SGN(T[T,0])>(Y[1]+Y[2]+Y[3]+Y[4]+T[T,0]+T[T,2])*SGN(T[T,0]) THEN LET W[2]=0,X[2]=0
8645 RETURN 
8650 CONN: 
8655 LET LOWLIMIT=D[0],MAXEXMP=D[1],RATEBREAK=D[2],MINTAX=J[X],CREDIT=K[X],ANNUAL=X0,TAXABLE=ANNUAL,TAXRATE=.03,TABLESTEP=0
8660 IF ANNUAL>LOWLIMIT THEN LET TABLESTEP=1+INT((ANNUAL-LOWLIMIT)/1000)
8665 LET CTEXMP=MAXEXMP-1000*TABLESTEP
8670 IF CTEXMP>0 THEN LET TAXABLE=MAX(0,ANNUAL-CTEXMP)
8675 IF TAXABLE>RATEBREAK THEN LET TAXABLE=TAXABLE-RATEBREAK,TAXRATE=.045
8680 LET W[2]=MINTAX+TAXABLE*TAXRATE
8685 LET W[2]=W[2]*(1-CREDIT/100)
8690 LET X0=W[2],X2=0
8695 RETURN 
8700 IOWA: 
8750 IF NUM(Y0$(20,2))<1 THEN GOTO 8790
8760 FOR X=1 TO NUM(Y0$(20,2))
8770 IF X<3 THEN LET X2=X2+20
8780 IF X>=3 THEN LET X2=X2+40
8785 NEXT X
8788 LET X2=X2*SGN(W[0])
8790 RETURN 
8800 OKLAHOMA1: 
8810 LET OKSUP=0,OKTAXBL=0,OKTBLTOP=22560
8820 IF OKTBLTOP>=X0 THEN GOTO 8860
8830 LET OKSUP=(X0-OKTBLTOP)*.0675
8840 LET X0=OKTBLTOP
8860 LET OKTAXBL=X0
8890 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9400 REM " --- Missing Deduction Code"
9405 LET MESSAGE$[0]="Deduction Record For Deduction Code "+Y0$(13,2)+" Is Missing"
9410 LET MESSAGE$[1]="Press Enter To Continue Or <F4> To Exit"
9415 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
9420 IF V3=4 THEN GOTO 9500
9425 REM " --- Continue when missing deduction code"
9430 LET MESSAGE$[0]="Calculations Will Not Be Performed For Deduction Code "+Y0$(13,2)+". Upon"
9435 LET MESSAGE$[1]="Completion Of This Task, Proceed To The Payroll Maintenance"
9440 LET MESSAGE$[2]="Menu And Use The Deduction Code Maintenance To Enter This"
9445 LET MESSAGE$[3]="Deduction Code. Return To This Menu And Re-Run The Payroll"
9450 LET MESSAGE$[4]="Calculation. Press <Enter> To Continue"
9460 CALL "SYC.XA",3,MESSAGE$[ALL],4,22,-1,V$,V3
9490 GOTO 7020
9500 REM " --- Exit when missing deduction code"
9510 LET MESSAGE$[0]="Proceed To The Payroll Maintenance Menu And Use"
9520 LET MESSAGE$[1]="Deduction Code Maintenance To Enter Deduction Code "+Y0$(13,2)
9530 LET MESSAGE$[2]="Return To This Menu And Re-Run The Payroll Calculation"
9540 LET MESSAGE$[3]="           Press <Enter> To Continue"
9550 CALL "SYC.XA",3,MESSAGE$[ALL],3,22,-1,V$,V3
9590 GOTO 9900
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

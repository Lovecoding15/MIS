0010 REM "ARM - Standard Message Code Maintenance"
0020 REM "Program ARM.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "23Oct96 JWK: Redisplay Code after lookup (5020)
0040 REM 
0070 BEGIN 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0140 LET FILES$[1]="ARM-10",FILES$[2]="SYS-01"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET ARM10_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists" 
0210 ARM10G1: IOLIST A0$(1),A1$(1)
0220 ARM10G2: IOLIST B0$(1),B1$(1)
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0500 REM " --- Init Data"
0510 DIM A0$(30),A1$(200),B0$(30),B1$(200)
0550 LET K0=1,K9=5,A0$(1,6)=N0$+"G  1"
0700 REM " --- Background"
0720 PRINT 'SB',@(2,3),"Std Message:",@(2,4),"Description:",@(1,6),"Detail Lines:",'SF',
0740 FOR X=1 TO 10
0750 PRINT 'SB',@(11,X+6),X:"##:",'SF',
0760 NEXT X
0900 REM " --- Init File"
0990 READ (ARM10_DEV,KEY=A0$(1,3),DOM=1000)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(ARM10_DEV,END=1050)
1030 IF V2$(1,3)<>A0$(1,3) THEN LET V2$=""
1040 IF V2$<>"" THEN LET V2$=V2$(4,2)
1050 LET V0$="S",V1$="KEC",V3$="",V0=2,V1=15,V2=3,V4$="Enter A Valid Message Code (<F3>=Lookup)"
1060 GOSUB 7000
1070 IF V3=4 THEN GOTO 4500
1075 IF V3=3 THEN CALL "ARC.LM",V$
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(4,2)=V$
1200 REM " --- Find Key"
1210 FIND (ARM10_DEV,KEY=A0$(1,K9)+"1",DOM=1400)IOL=ARM10G1
1220 FIND (ARM10_DEV,KEY=A0$(1,K9)+"2",DOM=1400)IOL=ARM10G2
1300 REM " --- Display"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New Record"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-Up"
1510 LET A0$(7)=""
1520 LET A1$(1)="",B0$=A0$,B0$(6,1)="2",B1$(1)=""
2000 REM " --- Description"
2005 FIND (ARM10_DEV,KEY=A0$(1,K9)+"2",DOM=2010)IOL=ARM10G2
2010 GOSUB 5000
2020 LET V0$="S",V1$="",V2$=A0$(7,20),V3$="",V0=20,V1=15,V2=4,I0=0
2040 GOSUB 7000
2050 ON V3 GOTO 2060,2000,3900,2000,3900
2060 LET A0$(7,20)=V$
2090 GOTO 3900
2100 REM " --- Desc Lines (A1$)
2110 LET X=I0-1
2120 LET V0$="S",V1$="",V2$=A1$(X*40+1,40),V0=40,V1=15,V2=X+7
2140 GOSUB 7000
2150 ON V3 GOTO 2160,2100,3900,2100,3900
2160 LET A1$(X*40+1,40)=V$
2190 GOTO 3900
2200 REM " --- Descr Lines (B1$)
2210 LET X=I0-6
2220 LET V0$="S",V1$="",V2$=B1$(X*40+1,40),V0=40,V1=15,V2=X+12
2240 GOSUB 7000
2250 ON V3 GOTO 2260,2200,3900,2200,3900
2260 LET B1$(X*40+1,40)=V$
2290 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2100,2100,2100,2100,2200,2200,2200,2200,2200,3990
3990 LET I0=K0
4000 REM " --- Options"
4010 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)?",V0=6,V1=64,V2=22
4020 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4050 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4110 WRITE (ARM10_DEV,KEY=A0$(1,K9)+"1")IOL=ARM10G1
4120 WRITE (ARM10_DEV,KEY=A0$(1,K9)+"2")IOL=ARM10G2
4190 GOTO 1000
4200 REM " --- Delete"
4210 REMOVE (ARM10_DEV,KEY=A0$(1,K9)+"1",DOM=4220)
4220 REMOVE (ARM10_DEV,KEY=A0$(1,K9)+"2",DOM=4230)
4290 GOTO 1000
4500 REM " --- Print"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON POS(V$="YN") GOTO 4500,4550,9900
4580 RUN "ARM.GB"
5000 REM " --- Display"
5020 PRINT @(15,3),A0$(4,2),@(15,4),A0$(7,20),
5030 FOR X=0 TO 4
5040 PRINT @(15,X+7),A1$(X*40+1,40),
5060 PRINT @(15,X+12),B1$(X*40+1,40),
5070 NEXT X
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

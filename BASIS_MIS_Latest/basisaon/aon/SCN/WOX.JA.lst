0010 REM "WOX - Work Order/P.O. Cross Reference Rebuild"
0020 REM "Program:  WOX.JA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "19Jun96 WGH: Retain existing PRECISION of numerics (505)"
0032 REM "15Oct96 JJD: Clear WO Reference from correct detail file.(3100,3120)"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=9
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="WOE-32",FILES$[2]="WOE-01",FILES$[3]="SYS-01"
0120 LET FILES$[4]="WOE-22",FILES$[5]="POM-02"
0125 LET OPTIONS$[1]="L",OPTIONS$[4]="L"
0150 CALL "SYC.DA",1,1,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET WOE32_DEV=CHANNELS[1],WOE01_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0165 LET WOE22_DEV=CHANNELS[4],POM02_DEV=CHANNELS[5]
0200 REM " --- IOLISTs"
0210 WOE32A: IOLIST A0$,A1$,A[ALL]
0215 WOE22A: IOLIST A0$,A1$,A[ALL]
0220 WOE01A: IOLIST B0$,B1$,B[ALL]
0230 POE12A: IOLIST W0$,W1$,W2$,W3$,W4$,W5$,W6$,W[ALL]
0240 POE11A: IOLIST W0$,W1$,W2$,W3$,W4$,W5$,W6$,W[ALL]
0250 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0260 WOS01A: IOLIST X$,X$,X$,P9$,X$,M0$,M1$,M2$,M3$
0270 IVS01A: IOLIST X$,X$,P2$,X$
0280 POM02A: IOLIST P0$,P1$,P2$,P3$
0400 REM " --- Parameters "
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9000)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="PO"
0500 REM " --- Initialize Data"
0505 PRECISION 14
0510 DIM A[13],B[11],W[12],B0$(75)
0600 REM " --- Additional Opens"
0605 DIM INFO$[20]
0610 CALL "SYC.VA",N2$,INFO$[ALL]
0615 IF INFO$[20]="Y" THEN GOTO 0640
0620 DIM MESS$[1]
0625 LET MESS$[0]="Purchase Orders Is Not Installed."
0626 LET MESS$[1]="Rebuild Cancelled. <Enter> To Exit."
0630 CALL "SYC.XA",2,MESS$[ALL],0,-1,-1,V$,V3
0635 GOTO 9900
0640 LET FILES$[4]="POE-01",FILES$[5]="POE-11"
0645 LET FILES$[6]="POE-02",FILES$[7]="POE-12"
0650 CALL "SYC.DA",1,4,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0655 IF STATUS THEN GOTO 9900
0660 LET POE01_DEV=CHANNELS[4],POE11_DEV=CHANNELS[5]
0665 LET POE02_DEV=CHANNELS[6],POE12_DEV=CHANNELS[7]
0800 REM " --- Options"
0820 LET V4$="Rebuild Work Order/P.O. Cross References For Firm "+N0$
0830 CALL "SYC.YN",0,V4$,0,V$,V3
0840 IF V$="NO" THEN GOTO 9900
0860 IF V$<>"YES" THEN GOTO 0800
0870 CALL "SYC.NB","Clearing: ",7,COLUMN
0900 REM " --- Initial File Read "
0910 READ (WOE32_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Clear WOE-32 of P.O./Req. Numbers"
1010 LET K$=KEY(WOE32_DEV,END=1400)
1020 IF K$(1,2)<>N0$ THEN GOTO 1400
1030 PRINT @(COLUMN,11),K$(5,7),
1100 REM " --- Make sure W.O. is Open"
1110 READ (WOE32_DEV,KEY=K$)IOL=WOE32A
1120 IF A0$(1,11)=B0$(1,11) THEN GOTO 1150
1130 READ (WOE01_DEV,KEY=K$(1,11),DOM=1300)IOL=WOE01A
1140 IF B0$(15,1)<>"O" THEN READ (WOE32_DEV,KEY=K$(1,11)+$FF$,DOM=1010)
1150 IF A1$(56,1)="C" THEN GOTO 1000
1200 REM " --- Clear record"
1210 LET A1$(43,14)=""
1220 WRITE (WOE32_DEV,KEY=K$)IOL=WOE32A
1230 GOTO 1010
1300 REM " --- Remove Unatttached WO Detail Lines "
1310 REMOVE (WOE32_DEV,KEY=K$)
1320 GOTO 1010
1400 REM " --- Read Through Purchase Order Detail "
1410 CALL "SYC.NB","Updating: ",7,COLUMN
1420 READ (POE12_DEV,KEY=N0$,DOM=1430)
1430 LET K3$=KEY(POE12_DEV,END=1700)
1440 IF K3$(1,2)<>N0$ THEN GOTO 1700
1450 PRINT @(COLUMN,11),K3$(9,7),
1500 REM " --- Get W.O. Number and Update"
1510 READ (POE12_DEV)IOL=POE12A
1520 IF FNP$(W2$(1,7))="" THEN GOTO 1430
1530 FIND (POM02_DEV,KEY=N0$+W1$(1,2),DOM=1430)IOL=POM02A
1540 IF P1$(21,1)<>"N" THEN GOTO 1430
1550 LET WOKEY$=W0$(1,2)+"  "+W2$(1,7)+"C"+W2$(8,3)
1560 READ (WOE32_DEV,KEY=WOKEY$,DOM=1600)IOL=WOE32A
1570 LET A1$(43,10)=K3$(9,10),A1$(56,1)="P"
1580 WRITE (WOE32_DEV,KEY=WOKEY$)IOL=WOE32A
1590 GOTO 1430
1600 REM " --- Clear W.O. Reference from POE-12"
1610 LET W2$(1,10)=""
1620 WRITE (POE12_DEV,KEY=K3$)IOL=POE12A
1630 GOTO 1430
1700 REM " --- Read Through Purchase Requisitions "
1710 READ (POE11_DEV,KEY=N0$,DOM=1720)
1720 LET K3$=KEY(POE11_DEV,END=2000)
1730 IF K3$(1,2)<>N0$ THEN GOTO 2000
1740 PRINT @(COLUMN,11),K3$(9,7),
1800 REM " --- Get W.O. Number and Update"
1810 READ (POE11_DEV)IOL=POE11A
1820 IF FNP$(W2$(1,7))="" THEN GOTO 1720
1830 LET WOKEY$=W0$(1,2)+"  "+W2$(1,7)+"C"+W2$(8,3)
1840 READ (WOE32_DEV,KEY=WOKEY$,DOM=1900)IOL=WOE32A
1850 LET A1$(43,10)=K3$(9,10),A1$(56,1)="R"
1860 WRITE (WOE32_DEV,KEY=WOKEY$)IOL=WOE32A
1870 GOTO 1720
1900 REM " --- Clear W.O. Reference from POE-12"
1910 LET W2$(1,10)=""
1920 WRITE (POE12_DEV,KEY=K3$)IOL=POE12A
1930 GOTO 1720
2000 REM " --- Process WOE-22 Detail Records "
2010 CALL "SYC.NB","Clearing: ",7,COLUMN
2020 READ (WOE22_DEV,KEY=N0$,DOM=2030)
2030 DIM B0$(75)
2100 REM " --- Clear WOE-22 of P.O./Req. Numbers"
2110 LET K$=KEY(WOE22_DEV,END=2600)
2120 IF K$(1,2)<>N0$ THEN GOTO 2600
2130 PRINT @(COLUMN,11),K$(5,7),
2200 REM " --- Make sure W.O. is Open"
2210 READ (WOE22_DEV,KEY=K$)IOL=WOE22A
2220 IF A0$(1,11)=B0$(1,11) THEN GOTO 2250
2230 READ (WOE01_DEV,KEY=K$(1,11),DOM=2400)IOL=WOE01A
2240 IF B0$(15,1)<>"O" THEN READ (WOE22_DEV,KEY=K$(1,11)+$FF$,DOM=2110)
2250 IF A1$(56,1)="C" THEN GOTO 2110
2300 REM " --- Clear record"
2310 LET A1$(50,11)=""
2320 WRITE (WOE22_DEV,KEY=K$)IOL=WOE22A
2330 GOTO 2110
2400 REM " --- Remove Unattached WO Detail Lines "
2410 REMOVE (WOE22_DEV,KEY=K$)
2420 GOTO 2110
2600 REM " --- Read Through Purchase Order Detail "
2610 CALL "SYC.NB","Updating: ",7,COLUMN
2620 READ (POE12_DEV,KEY=N0$,DOM=2630)
2630 LET K3$=KEY(POE12_DEV,END=2900)
2640 IF K3$(1,2)<>N0$ THEN GOTO 2900
2650 PRINT @(COLUMN,11),K3$(9,7),
2700 REM " --- Get W.O. Number and Update"
2710 READ (POE12_DEV)IOL=POE12A
2720 IF FNP$(W2$(1,7))="" THEN GOTO 2630
2730 FIND (POM02_DEV,KEY=N0$+W1$(1,2),DOM=2790)IOL=POM02A
2740 IF P1$(21,1)="N" THEN GOTO 2630
2750 LET WOKEY$=W0$(1,2)+"  "+W2$(1,7)+"B"+W2$(8,3)
2760 READ (WOE22_DEV,KEY=WOKEY$,DOM=2800)IOL=WOE22A
2770 LET A1$(50,10)=K3$(9,10),A1$(60,1)="P"
2780 WRITE (WOE22_DEV,KEY=WOKEY$)IOL=WOE22A
2790 GOTO 2630
2800 REM " --- Clear W.O. Reference from POE-12"
2810 LET W2$(1,10)=""
2820 WRITE (POE12_DEV,KEY=K3$)IOL=POE12A
2830 GOTO 2630
2900 REM " --- Read Through Purchase Requisitions "
2910 READ (POE11_DEV,KEY=N0$,DOM=2920)
2920 LET K3$=KEY(POE11_DEV,END=4000)
2930 IF K3$(1,2)<>N0$ THEN GOTO 4000
2940 PRINT @(COLUMN,11),K3$(9,7),
3000 REM " --- Get W.O. Number and Update"
3010 READ (POE11_DEV)IOL=POE11A
3020 IF FNP$(W2$(1,7))="" THEN GOTO 2920
3030 LET WOKEY$=W0$(1,2)+"  "+W2$(1,7)+"B"+W2$(8,3)
3040 READ (WOE22_DEV,KEY=WOKEY$,DOM=3100)IOL=WOE22A
3050 LET A1$(43,10)=K3$(9,10),A1$(56,1)="R"
3060 WRITE (WOE22_DEV,KEY=WOKEY$)IOL=WOE22A
3070 GOTO 2920
3100 REM " --- Clear W.O. Reference from POE-11"
3110 LET W2$(1,10)=""
3120 WRITE (POE11_DEV,KEY=K3$)IOL=POE11A
3130 GOTO 2920
4000 REM " --- End Of File"
4090 GOTO 9900
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "POR - Purchase Order Invoice Register (Invoices)"
0020 REM "Program POR.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "08Apr97 WGH: Write APT-02 records for Invoice GL Distributions not in POE-35 (105,415,483-487,677-680,697,6975-6980)"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=32
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="SYS-01",FILES$[2]="POE-02",FILES$[3]="POE-05"
0120 LET FILES$[4]="POE-12",FILES$[5]="POE-15",FILES$[6]="POE-25"
0125 LET FILES$[7]="POE-35",FILES$[8]="POM-02",FILES$[9]="POT-04"
0130 LET FILES$[10]="POT-05",FILES$[11]="POT-14",FILES$[12]="POT-15"
0135 LET FILES$[13]="POT-25",FILES$[14]="POT-35",FILES$[15]="APM-01"
0140 LET FILES$[16]="APM-02",FILES$[17]="APM-09",FILES$[18]="APM-10"
0145 LET FILES$[19]="APT-01",FILES$[20]="APT-02",FILES$[21]="APT-03"
0150 LET FILES$[22]="APT-11",FILES$[23]="IVM-01",FILES$[24]="IVM-02"
0155 CALL "SYC.DA",1,1,24,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS THEN GOTO 9900
0165 LET SYS01_DEV=CHANNELS[1],POE02_DEV=CHANNELS[2],POE05_DEV=CHANNELS[3]
0170 LET POE12_DEV=CHANNELS[4],POE15_DEV=CHANNELS[5],POE25_DEV=CHANNELS[6]
0175 LET POE35_DEV=CHANNELS[7],POM02_DEV=CHANNELS[8],POT04_DEV=CHANNELS[9]
0180 LET POT05_DEV=CHANNELS[10],POT14_DEV=CHANNELS[11],POT15_DEV=CHANNELS[12]
0185 LET POT25_DEV=CHANNELS[13],POT35_DEV=CHANNELS[14],APM01_DEV=CHANNELS[15]
0190 LET APM02_DEV=CHANNELS[16],APM09_DEV=CHANNELS[17],APM10_DEV=CHANNELS[18]
0193 LET APT01_DEV=CHANNELS[19],APT02_DEV=CHANNELS[20],APT03_DEV=CHANNELS[21]
0195 LET APT11_DEV=CHANNELS[22],IVM01_DEV=CHANNELS[23],IVM02_DEV=CHANNELS[24]
0200 REM " --- IOLIST's"
0210 POE05A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0220 APM01A: IOLIST B0$(1),B1$(1)
0230 POT04A: IOLIST C0$(1),C1$(1),C[8]
0240 POE15A: IOLIST D0$(1),D1$(1),D2$,D[ALL]
0250 GLM01A: IOLIST S0$(1),S1$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,I5$,I6$,M2$,M3$
0275 POS01A: IOLIST R0$,R1$,R2$,R3$,R4$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0285 APS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$
0290 ARS01A: IOLIST X$,U1$,U2$,U3$
0310 POT14A: IOLIST L0$(1),L1$(1),L2$(1),L3$(1),L4$(1),L5$,L6$,L[ALL]
0320 POM02A: IOLIST Y0$(1),Y1$(1),Y2$(1)
0330 ARM10D: IOLIST Z0$(1),Z1$(1)
0340 IVM01A: IOLIST S0$(1),S1$(1),S2$(1),S3$(1),S4$(1),S5$,S6$(1),S[ALL]
0360 IVM02A: IOLIST U0$(1),U1$(1),U2$,U[ALL]
0370 APM10A: IOLIST XA$(1)
0375 APM10B: IOLIST X0$(1),X1$(1)
0380 APM10C: IOLIST XC$(1),XC[ALL]
0385 POE25A: IOLIST W0$(1),W1$(1),W2$,W[ALL]
0390 POE35A: IOLIST Z0$(1),Z1$(1),Z[ALL]
0400 REM " --- Parameters"
0405 DIM I[6],P[3],G[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N2$="PO",GL$="N",AR$="N",SF$="N",STATUS=0,SOURCE$=PGM(-2)
0420 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=POS01A
0425 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=9800)IOL=APS01A
0430 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0435 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0440 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(5,1)),I[2]=NUM(I2$(7,2))
0445 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0450 LET I[6]=NUM(I2$(3,2)),P[0]=NUM(P2$(1,2)),DISTCODE$="N",CASH=P3$(4,1)="Y"
0455 LET G[0]=NUM(G2$(1,2)),G[1]=NUM(G2$(3,2)),G[2]=FNYY_YEAR(G2$(5,2))
0460 LET G[3]=NUM(G2$(7,2)),G[4]=NUM(G2$(9,2))
0465 IF CASH THEN GOTO 0500
0470 CALL "GLC.BA",ERR=0500,SOURCE$,N2$,GLW11$,GL$,STATUS
0475 IF STATUS THEN GOTO 9900
0480 IF GL$<>"Y" THEN GOTO 0500
0483 CALL "SYC.VA","WO",INFO$[ALL]
0485 LET SF$=INFO$[20]
0487 CALL "SYC.VA","AR",INFO$[ALL]
0490 LET AR$=INFO$[20]
0493 IF AR$="N" THEN GOTO 0500
0495 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0497 LET DISTCODE$=U3$(11,1)
0500 REM " --- Initializations"
0505 PRECISION I[1]
0510 DIM A0$(20),A1$(64),A[8],B0$(8),B1$(30),XA$(32),HEADINGS$[3]
0515 DIM OI[10],OG[2],VENDOR[3],TYPE[3],REPORT[3],TOTALS[3],DESC$[3]
0520 DIM OP[7],OL[8],XC$(26),XC[2],Y0$(4),Y1$(32),Y2$(2*G[4]),X0$(32)
0525 DIM D0$(24),D1$(32),D[4],W0$(24),W1$(96),W[4],ITEM$[6],X1$(4*G[4])
0530 DIM GLFILES[20],GLPARAMS$[20],GLPARAMS[20],W$[10],Y$[10],VALUES[20]
0540 LET A0$(1)=N0$,L0=11,INCREMENT=10,HL=3,APTYPE$="",RETENTION$=""
0545 LET NF$="(Not On File)",RH1$="Discount",RH2$="Amount",M0=LEN(M0$)
0550 LET APTYPE=0,RETENTION=0,HEADINGS=2,T0$="",T1$="",ERRORS=0
0555 LET WIDTH=132,PAGE=0,L9=59,L=L9+1,CLOCK$="",WHEN$=F0$(7,3)
0560 LET DESC$[1]="By Invoice",DESC$[2]="By Purchase Order",GLSTATUS=0
0565 LET DESC$[3]="By Line Item",HEADINGS$[0]=F4$,HEADINGS$[1]=F5$
0570 IF P3$(1,1)="Y" THEN LET APTYPE=1,HEADINGS=3
0575 IF P3$(3,1)="Y" THEN LET RETENTION=1,RH1$="Discount/",RH2$="Retention"
0600 REM " --- Print positions"
0605 CALL "SYC.RM",M1$,M1,10,15
0610 CALL "SYC.RM",M2$,M2,9,12
0615 CALL "SYC.RM",M3$,M3,6,12
0620 LET OI[10]=WIDTH-M1,OI[9]=OI[10]-M1,OI[8]=OI[9]-16,OI[7]=OI[8]-10
0625 LET OI[6]=OI[7]-10,OI[5]=OI[6]-5,OI[4]=OI[5]-6,OI[3]=OI[4]-4
0630 LET OI[2]=OI[3]-5,OI[1]=OI[2]-11,DW=OI[1]-1
0635 IF DW>M0+31 THEN LET DW=M0+32,OI[1]=DW
0650 REM " --- Additional file opens"
0655 IF DISTCODE$="Y" THEN LET FILES$[25]="ARM-10"
0660 IF POS(R3$(55,1)="QC") THEN LET FILES$[30]="POW-17",OPTIONS$[30]="C"
0665 IF GL$<>"Y" THEN GOTO 0680
0670 LET FILES$[26]="GLM-01",FILES$[27]=GLW11$,FILES$[28]="GLT-04"
0675 LET FILES$[29]="GLT-05",OPTIONS$[27]="C",XREF_DEV=0
0677 IF SF$="Y" THEN LET FILES$[31]="WOE-01",FILES$[32]="WOM-10"
0680 CALL "SYC.DA",1,25,32,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0685 IF STATUS THEN GOTO 9900
0690 LET ARM10_DEV=CHANNELS[25],GLM01_DEV=CHANNELS[26],GLW11_DEV=CHANNELS[27]
0695 LET GLT04_DEV=CHANNELS[28],GLT05_DEV=CHANNELS[29],POW17_DEV=CHANNELS[30]
0697 LET WOE01_DEV=CHANNELS[31],WOM10_DEV=CHANNELS[32]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,6,0
0730 PRINT 'SB',@(26,3),"Detail Level:",@(33,5),"Phase:",'SF',
0800 REM " --- Options"
0810 LET V4$="I=Summary "+DESC$[1]+"    P=Detail "+DESC$[2]+"    L=Detail "+DESC$[3]
0815 LET V0$="S",V1$="C",V2$="I",V3$="IPL",V0=1,V1=40,V2=3
0820 GOSUB 7000
0825 ON V3 GOTO 0830,0800,0800,0800,0850
0830 IF POS(V$="IPL")=0 THEN GOTO 0800
0835 LET LEVEL$=V$,V=POS(V$="IPL")
0840 PRINT @(V1,V2),LEVEL$," ",DESC$[V],'CL',
0850 REM " --- Correct"
0855 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
0860 ON V3 GOTO 0865,0850,0850,0850,9900
0865 IF V$<>"Y" THEN GOTO 0800
0870 LET X$="Detail"
0875 IF LEVEL$="I" THEN LET X$="Summary"
0880 LET HEADINGS$[1]=X$+" "+HEADINGS$[1]+" "+DESC$[V]
0900 REM " --- Remaining print positions"
0910 LET OP[0]=OI[1],OP[1]=OP[0]+4,OP[2]=OP[1]+8,OP[3]=OP[2]+8,OP[4]=OP[3]+9
0915 LET OP[5]=OP[4]+9,OP[7]=OP[5]+23,OL[0]=OI[1],OL[1]=OL[0]+4,OL[2]=OL[1]+3
0920 LET OL[3]=OL[2]+3,OL[4]=OL[3]+MAX(10,I[0]),OL[5]=OL[4]+9,OL[6]=OL[5]+M2
0925 LET OL[7]=OL[6]+2,OL[8]=OL[7]+M3,OG[0]=OI[1],OG[1]=OG[0]+4,OG[2]=OI[9]-M1
0930 IF LEVEL$="P" THEN LET OG[2]=OP[7]
0935 IF LEVEL$="L" THEN LET OG[2]=OL[8]
0940 LET TW=MIN(OP[7]-OP[5],23),GW=OG[2]-OG[1]-1
0950 REM " --- Position file"
0960 LET X=M0+11
0965 IF APTYPE THEN LET X=X+3
0970 CALL "SYC.GA",7,1,"","",STATUS
0975 IF STATUS THEN GOTO 9900
0980 CALL "SYC.NB","Printing",X,COLUMN
0985 PRINT @(40,5),"Purchase Order Invoice Register",
0990 READ (POE05_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next header"
1010 LET K$=KEY(POE05_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 READ (POE05_DEV)IOL=POE05A
1040 LET X$=FNF$(A0$(5,P[0]),M0$)+" "+A0$(11,10)
1050 IF APTYPE THEN LET X$=A0$(3,2)+" "+X$
1060 PRINT @(COLUMN,11),X$,
1200 REM " --- Level breaks?"
1210 IF A0$(3,2)<>T0$ THEN GOSUB 6000
1220 IF A0$(5,6)<>T1$ THEN GOSUB 6200
1300 REM " --- Additional reads"
1310 DIM X0$(32),X1$(4*G[4])
1320 LET X0$(1)=N0$+"B"+A1$(28,2),APCODE$=A1$(28,2)
1330 FIND (APM10_DEV,KEY=X0$(1,5),DOM=1340)IOL=APM10B
1340 LET APACCT$=X1$(1,G[4]),RETACCT$=X1$(3*G[4]+1,G[4])
1400 REM " --- Format invoice fields"
1410 LET RETENTION$="",HOLD$="No",TOTAL=0
1420 IF RETENTION THEN LET RETENTION$=STR(A[2]:M1$)
1430 IF A1$(25,1)="Y" THEN LET HOLD$="Yes"
1500 REM " --- Print invoice header"
1510 IF L+3>L9 THEN GOSUB 5000
1520 PRINT (7)""
1530 PRINT (7)@(OI[0]),VENDOR$,@(OI[1]),A0$(11,10),@(OI[2]),A1$(28,2),@(OI[3]),A1$(30,2),@(OI[4]),A1$(26,2),@(OI[5]),HOLD$,@(OI[6]),FNB6$(A1$(1,6)),@(OI[7]),FNB6$(A1$(7,6)),@(OI[8]),A1$(32,10),@(OI[9]),A[0]:M1$,@(OI[10]),A[1]:M1$
1540 PRINT (7)@(OI[6]),FNB6$(A1$(13,6)),@(OI[7]),FNB6$(A1$(19,6)),@(OI[8]),A1$(42,16),@(OI[10]),RETENTION$
1550 LET L=L+3,GLSKIP=0
1560 IF FNP$(A1$(42,16))<>"" THEN LET GLSKIP=1
1600 REM " --- Check GL posting date"
1610 LET GLDATE$=A1$(7,6)
1620 IF GL$="Y" THEN CALL "GLC.EA",GLDATE$,"N",PERIOD$,YEAR$,GLSTATUS
1630 IF GLSTATUS>0 THEN CALL "GLC.GA",7,GLDATE$,GLSTATUS,L,ERRORS,STATUS
1900 REM " --- Position POE-15 PO Detail"
1910 LET FIRST=1,GLSKIP=1
1990 READ (POE15_DEV,KEY=A0$,DOM=2000)
2000 REM " --- Read next PO record"
2010 LET K$=KEY(POE15_DEV,END=3200)
2020 IF POS(A0$=K$)<>1 THEN GOTO 3200
2030 READ (POE15_DEV)IOL=POE15A
2040 DIM RECEIVERS[99]
2050 LET NUMBER=1,RECEIVERS$=D1$(1,14),RECEIVERS[1]=D[0],SEQ$=D0$(21,3)
2060 IF FNP$(D1$(1,7))="" THEN GOTO 2100
2070 IF FNP$(D1$(8,7))="" THEN GOSUB 6500
2100 REM " --- Read history header and original terms"
2110 FOR RECEIVER=1 TO NUMBER
2120 DIM C0$(22),C1$(160),C[8],XC$(26),TERMS$(TW)
2130 LET RECEIPT$=RECEIVERS$(((RECEIVER-1)*14)+1,14),C0$(1)=B0$+RECEIPT$
2140 FIND (POT04_DEV,KEY=C0$,DOM=2150)IOL=POT04A
2150 LET XC$(1)=N0$+"C"+C1$(38,2)+NF$,ORDERED$="",RECEIVED$=""
2160 FIND (APM10_DEV,KEY=XC$(1,5),DOM=2170)IOL=APM10C
2170 LET TERMS$(1)=XC$(4,2)+" "+XC$(6,20)
2180 IF FNP$(C1$(5,6))<>"" THEN LET ORDERED$=FNB6$(C1$(5,6))
2190 IF FNP$(C1$(29,6))<>"" THEN LET RECEIVED$=FNB6$(C1$(29,6))
2200 REM " --- Print PO detail record"
2210 IF LEVEL$<>"P" THEN GOTO 2300
2220 IF FIRST THEN GOSUB 5100
2230 IF L+1>L9 THEN GOSUB 5000; GOSUB 5100
2240 LET PO$=RECEIPT$(1,7),REC$=RECEIPT$(8,7)
2250 IF FNP$(PO$)="" THEN LET PO$="Other Chgs",REC$="",TERMS$=""
2260 PRINT (7)@(OP[0]),SEQ$,@(OP[1]),PO$,@(OP[2]),REC$,@(OP[3]),ORDERED$,@(OP[4]),RECEIVED$,@(OP[5]),TERMS$,@(OP[7]),RECEIVERS[RECEIVER]:M1$
2270 LET L=L+1,FIRST=0,GLSKIP=1,SEQ$=""
2300 REM " --- Position line item file"
2390 READ (POE25_DEV,KEY=A0$,DOM=2400)
2400 REM " --- Read next line item"
2410 LET K$=KEY(POE25_DEV,END=3000)
2420 IF POS(A0$=K$)<>1 THEN GOTO 3000
2430 READ (POE25_DEV)IOL=POE25A
2440 IF W1$(1,7)<>C0$(9,7) THEN GOTO 2900
2450 IF FNP$(C0$(16,7))<>"" THEN IF W1$(11,7)<>C0$(16,7) THEN GOTO 2900
2500 REM " --- Read history detail record and line code"
2510 DIM L0$(25),L1$(64),L2$(32),L3$(22),L4$(40),L[12],DESCRIPTION$(DW)
2520 LET L0$(1)=B0$+W1$(1,7)+W1$(11,7)+W1$(8,3),QTY=W[1]
2525 LET LC$=W1$(18,2),DESCRIPTION$(1)=W1$(20,40)
2530 FIND (POT14_DEV,KEY=L0$,DOM=2540)IOL=POT14A
2535 LET LC$=L1$(1,2)
2540 IF FNP$(W1$(1,17))="" THEN LET QTY=1
2545 PRECISION 2
2550 LET EXTENSION=W[0]*QTY,POEXTENSION=W[2]*QTY
2560 PRECISION I[1]
2570 IF Y0$(1,4)=N0$+LC$ THEN GOTO 2600
2580 LET Y0$(1)=N0$+LC$,Y1$(1)="",Y2$(1)=""
2585 IF FNP$(W1$(1,7))="" THEN LET Y0$(1)=N0$+W1$(18,2)
2590 FIND (POM02_DEV,KEY=Y0$,DOM=2600)IOL=POM02A
2600 REM " --- Read inventory item and warehouse"
2610 DIM S0$(22),S1$(60),S2$(64),S3$(40),S4$(21),S6$(G[4]*9),S[12]
2620 DIM U0$(24),U1$(64),U[20]
2630 IF Y1$(21,1)<>"S" THEN GOTO 2700
2640 LET S0$(1)=N0$+L3$(3),U0$(1)=N0$+L3$
2650 FIND (IVM01_DEV,KEY=S0$,DOM=2660)IOL=IVM01A
2660 LET DESCRIPTION$(1)=FNITEM$(S1$,I[3],I[4],I[5])
2670 FIND (IVM02_DEV,KEY=U0$,DOM=2700)IOL=IVM02A
2700 REM " --- Print line item"
2710 IF LEVEL$<>"L" THEN GOTO 2800
2720 GOSUB 5500
2800 REM " --- Generate GL Recap"
2810 IF GL$="N" THEN GOTO 2900
2820 LET ACTION$="",ACCOUNT$="",ITEM$[0]=S0$(3),ITEM$[1]=S1$,ITEM$[6]=S6$
2830 LET VALUES[0]=EXTENSION,VALUES[1]=POEXTENSION,VALUES[2]=W[1]
2840 LET VALUES[3]=U[0],ARCODE$=U1$(13,2),W$[0]=W0$,W$[1]=W1$
2850 LET Y$[0]=Y0$,Y$[1]=Y1$,Y$[2]=Y2$,TOTAL=TOTAL+EXTENSION
2860 GOSUB 6900
2900 REM " --- Loop back for next line item"
2990 GOTO 2400
3000 REM " --- Loop back for next PO record"
3010 NEXT RECEIVER
3090 GOTO 2000
3200 REM " --- Position GL distribution file"
3210 DIM Z0$(23),Z1$(32),Z[4],S0$(12),S1$(35),G$(GW)
3220 DIM ITEM$[6],VALUES[4],W$[10],Y$[10]
3230 IF GL$<>"Y" THEN GOTO 3800
3240 LET FIRST=1,DISTRIBUTION=0,ARCODE$="",APCODE$=""
3290 READ (POE35_DEV,KEY=A0$,DOM=3300)
3300 REM " --- Read next GL distribution record"
3310 LET K$=KEY(POE35_DEV,END=3600)
3320 IF POS(A0$=K$)<>1 THEN GOTO 3600
3330 READ (POE35_DEV)IOL=POE35A
3340 LET S0$(1)=N0$+Z1$(1,G[4]),S1$(1)=NF$
3350 FIND (GLM01_DEV,KEY=S0$,DOM=3360)IOL=GLM01A
3360 LET G$(1)=FNF$(S0$(3,G[3]),G5$)+" "+S1$(1,35)
3400 REM " --- Print it"
3410 IF FIRST THEN GOSUB 5300
3420 IF L+1>L9 THEN GOSUB 5000; GOSUB 5300
3430 PRINT (7)@(OG[0]),Z0$(21,3),@(OG[1]),G$,@(OG[2]),Z[0]:M1$
3440 LET L=L+1,FIRST=0,GLSKIP=1
3500 REM " --- Generate GL recap postings and loop back for next record"
3510 LET ACCOUNT$=S0$(3),AMOUNT=-Z[0],VALUES[0]=AMOUNT,W$[0]=Z0$,W$[1]=Z1$
3520 LET ACTION$="D",DISTRIBUTION=DISTRIBUTION+AMOUNT,TOTAL=TOTAL+Z[0]
3530 GOSUB 6900
3590 GOTO 3300
3600 REM " --- Generate any additional GL recap postings for the invoice"
3610 IF DISTRIBUTION=0 THEN GOTO 3700
3620 LET ACTION$="D",ACCOUNT$=APACCT$,VALUES[0]=-DISTRIBUTION,W$[0]=A0$
3630 GOSUB 6900
3700 REM " --- Does GL total match invoice header?
3710 IF TOTAL=A[0] THEN GOTO 3900
3720 IF L+1>L9 THEN GOSUB 5000
3730 PRINT (7)"---> ERROR: Invoice Total Does Not Match Sum Of Line Items And/Or Distribution"
3740 LET L=L+1,ERRORS=ERRORS+1
3900 REM " --- Loop back for next header"
3910 LET TOTALS[0]=1,TOTALS[1]=A[0],TOTALS[2]=A[1],TOTALS[3]=A[2]
3920 LET TYPE[ALL]=TYPE[ALL]+TOTALS[ALL],VENDOR[ALL]=VENDOR[ALL]+TOTALS[ALL]
3930 LET REPORT[ALL]=REPORT[ALL]+TOTALS[ALL]
3990 GOTO 1000
4000 REM " --- All done"
4010 LET DONE=1
4020 GOSUB 6000
4030 IF L+3>L9 THEN GOSUB 5000
4040 PRINT (7)""
4050 LET TOTAL$="Register",TOTALS[ALL]=REPORT[ALL]
4060 GOSUB 6800
4090 RUN "POR.EB"
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+3
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS THEN EXITTO 9900
5040 PRINT (7)@(OI[2]),"Dist",@(OI[3]),"Pay",@(OI[6]),"Inv Date",@(OI[7]),"Acct Date",@(OI[8]),"Ref/",@(OI[9]+M1-8),"Invoice",@(OI[10]+M1-(LEN(RH1$)+1)),RH1$
5050 PRINT (7)@(OI[0]),"Vendor",@(OI[1]),"Invoice",@(OI[2]),"Code",@(OI[3]),"Grp",@(OI[4]),"Terms",@(OI[5]),"Hold",@(OI[6]),"Due Date",@(OI[7]),"Disc Date",@(OI[8]),"Memo",@(OI[9]+M1-8),"Amount",@(OI[10]+M1-(LEN(RH1$)+1)),RH2$
5090 RETURN 
5100 REM " --- Purchase Order Subheading"
5110 IF L+3>L9 THEN GOSUB 5000
5120 PRINT (7)""
5130 PRINT (7)@(OP[0]),"Seq",@(OP[1]),"PO #",@(OP[2]),"Rec #",@(OP[3]),"Ordered",@(OP[4]),"Received",@(OP[5]),"Original Terms",@(OP[7]+M1-6),"Total"
5140 LET L=L+2
5190 RETURN 
5200 REM " --- Line Item Subheading"
5210 IF L+5>L9 THEN GOSUB 5000
5220 PRINT (7)""
5230 PRINT (7)@(OL[0]),"Seq",@(OL[1]),"Cd",@(OL[2]),"Wh",@(OL[3]),"Item",@(OL[4]),"Received",@(OL[5]+M2-9),"Quantity",@(OL[6]),"UM",@(OL[7]+M3-5),"Cost",@(OL[8]+M1-10),"Extension"
5240 LET L=L+2
5290 RETURN 
5300 REM " --- GL Distribution Subheading"
5310 LET NEEDED=2+GLSKIP
5320 IF L+NEEDED>L9 THEN GOSUB 5000
5330 IF GLSKIP THEN PRINT (7)""
5340 PRINT (7)@(OG[0]),"Seq",@(OG[1]),"GL Account",@(OG[2]+M1-7),"Amount"
5350 LET L=L+1+GLSKIP
5390 RETURN 
5500 REM " --- Print detail line based on line type"
5510 IF FIRST THEN GOSUB 5200
5520 IF L+3>L9 THEN GOSUB 5000; GOSUB 5200
5530 PRINT (7)@(OL[0]),W0$(21),@(OL[1]),Y0$(3,2),@(OL[2]),L3$(1,2),
5590 ON POS(Y1$(21,1)="SNVMO") GOTO 5990,5600,5700,5990,5990,5800
5600 REM " --- Display Standard Line"
5610 PRINT (7)@(OL[3]),L3$(3,I[0]),@(OL[4]),RECEIVED$,@(OL[5]),W[1]:M2$,@(OL[6]),L1$(22,2),@(OL[7]),W[0]:M3$,@(OL[8]),EXTENSION:M1$
5630 GOTO 5900
5700 REM " --- Display Non-Stock Line"
5710 PRINT (7)@(OL[3]),L3$(3,I[0]),@(OL[4]),RECEIVED$,@(OL[5]),W[1]:M2$,@(OL[6]),L1$(22,2),@(OL[7]),W[0]:M3$,@(OL[8]),EXTENSION:M1$
5790 GOTO 5900
5800 REM " --- Display Other Line"
5810 PRINT (7)@(OL[4]),RECEIVED$,@(OL[7]),W[0]:M3$,@(OL[8]),EXTENSION:M1$
5900 REM " --- Display PO and Receiver"
5910 PRINT (7)@(OL[3]),DESCRIPTION$
5920 LET L=L+2,FIRST=0
5930 IF FNP$(W1$(1,7))="" THEN GOTO 5990
5940 PRINT (7)@(OL[3]),"PO # ",W1$(1,7),"-",W1$(8,3)," Rec # ",W1$(11,7)
5950 LET L=L+1
5990 RETURN 
6000 REM " --- AP Type Break"
6010 IF T0$="" THEN GOTO 6100
6020 GOSUB 6200
6030 LET TOTAL$="AP Type "+T0$+" "+FNP$(XA$(6,20)),TOTALS[ALL]=TYPE[ALL]
6040 IF TYPE[0]<>0 THEN GOSUB 6800
6100 IF DONE THEN GOTO 6190
6110 DIM XA$(32),TYPE[3]
6120 IF APTYPE=0 THEN GOTO 6190
6130 LET T0$=A0$(3,2),XA$(6,20)=NF$,L=L9+1,WAREHOUSE=0
6140 FIND (APM10_DEV,KEY=N0$+"A"+T0$,DOM=6150)IOL=APM10A
6150 LET HEADINGS$[2]="For AP Type "+T0$+" "+FNP$(XA$(6,20))
6190 RETURN 
6200 REM " --- Vendor Break"
6210 IF T1$="" THEN GOTO 6300
6220 LET TOTAL$="Vendor "+VENDOR$,TOTALS[ALL]=VENDOR[ALL]
6230 IF VENDOR[0]<>0 THEN GOSUB 6800
6300 IF DONE THEN GOTO 6390
6310 DIM B0$(8),B1$(30),VENDOR$(DW),VENDOR[3]
6320 LET T1$=A0$(5,6),B0$(1)=N0$+T1$,B1$(1)=NF$
6330 FIND (APM01_DEV,KEY=B0$,DOM=6340)IOL=APM01A
6340 LET VENDOR$(1)=FNF$(B0$(3,P[0]),M0$)+" "+B1$
6390 RETURN 
6500 REM " --- Find all receivers for a PO"
6510 DIM RECEIVERS[99]
6520 LET NUMBER=0,RECEIVERS$=""
6590 READ (POE25_DEV,KEY=A0$,DOM=6600)
6600 REM " --- Read next detail line"
6610 LET K$=KEY(POE25_DEV,END=6700)
6620 IF POS(A0$=K$)<>1 THEN GOTO 6700
6630 READ (POE25_DEV)IOL=POE25A
6640 IF W1$(1,7)<>D1$(1,7) THEN GOTO 6600
6650 LET X=POS(W1$(1,7)+W1$(11,7)=RECEIVERS$,14),X=X/14
6660 IF X THEN GOTO 6670
6665 LET NUMBER=NUMBER+1,X=NUMBER,RECEIVERS$=RECEIVERS$+W1$(1,7)+W1$(11,7)
6670 PRECISION 2
6680 LET RECEIVERS[NUMBER]=RECEIVERS[NUMBER]+(W[0]*W[1])
6685 PRECISION I[1]
6690 GOTO 6600
6700 REM " --- Done finding all receivers"
6790 RETURN 
6800 REM " --- Print total line"
6810 LET NEEDED=1
6820 IF RETENTION THEN LET NEEDED=2
6830 IF L+NEEDED>L9 THEN GOSUB 5000
6840 LET L=L+NEEDED,TOTAL$="Total For "+FNP$(TOTAL$)
6850 PRINT (7)@(OI[9]-LEN(TOTAL$)),TOTAL$,@(OI[9]),TOTALS[1]:M1$,@(OI[10]),TOTALS[2]:M1$
6860 IF RETENTION THEN PRINT (7)@(OI[10]),TOTALS[3]:M1$
6890 RETURN 
6900 REM " --- Call GL Posting Routine"
6910 LET GLFILES[1]=GLM01_DEV,GLFILES[2]=GLW11_DEV,GLFILES[3]=XREF_DEV
6920 LET GLFILES[4]=APM10_DEV,GLFILES[5]=ARM10_DEV,GLPARAMS$[0]=N0$
6930 LET GLPARAMS$[1]=APCODE$,GLPARAMS$[2]=ARCODE$,GLPARAMS$[3]=DISTCODE$
6940 LET GLPARAMS$[4]=GLDATE$,GLPARAMS$[5]=B0$(3,P[0]),GLPARAMS$[6]=B1$
6950 LET GLPARAMS$[7]=M0$,GLPARAMS$[8]=G4$,GLPARAMS$[9]=I3$(8,1)
6960 LET GLPARAMS$[10]=ACCOUNT$,GLPARAMS[0]=I[0],GLPARAMS[1]=I[1]
6970 LET GLPARAMS[2]=G[4],GLPARAMS[3]=I[3],GLPARAMS[4]=I[4],GLPARAMS[5]=I[5]
6975 DIM GLACCOUNTS$[5],GLAMOUNTS[5],GLQTYS[5]
6980 CALL "POC.GB",ACTION$,GLFILES[ALL],GLPARAMS$[ALL],GLPARAMS[ALL],ITEM$[ALL],W$[ALL],Y$[ALL],VALUES[ALL],GLACCOUNTS$[ALL],GLAMOUNTS[ALL],GLQTYS[ALL],STATUS
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

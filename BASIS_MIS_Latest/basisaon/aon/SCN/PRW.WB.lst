0010 REM "PRW - Electronic W-2 Calculation - II"
0020 REM "Program PRW.WB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 15Dec97 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Dec96: Modified for 1996 Magnetic Media Reporting (1290,1560,2330,4274-4278,4290,5474-5476,5490)"
0032 REM "08Dec97 WGH: Combine Box 13 'A' and 'B' amounts in '2W' record (1290,1520-1530)"
0033 REM "05Mar98 WGH: Prevent possible Error 47 on Line 3150 (1210-1215,1225-1230,1235-1240,3100-3240,3320-3350)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0200 REM " --- IOLISTs"
0210 PRM01A: IOLIST A0$,A1$
0220 PRM10C: IOLIST D0$(1)
0230 PRW06A: IOLIST B0$,B0
0500 REM " --- Init Data"
0510 LET L9=40,U=0
0520 DIM S[20],T[20],U[20],X[20]
0530 REM "S(X)=Individual Employee Totals"
0540 REM "T(X)=Intermediate Totals"
0550 REM "U(X)=Company Totals"
0580 REM " --- Following Used To Resolve State ID'S
0590 LET X9$="ALAKASAZARCACCCOCTDEDCFLGAGUHIIDILINIAKSKYLAMEMDMAMIMNMSMOMTNENVNHNJNMNYNCNDOHOKORPAPRRISCSDTNTXUTVTVAVIWAWVWIWY"
0990 CALL "SYC.NB","Processing:",LEN(M0$)+31,COLUMN
1000 REM " --- Init Read"
1020 READ (PRW06_DEV,KEY=N0$,DOM=1030)
1100 REM " --- Set Up Employee's Records 1W-2W"
1120 LET K$=KEY(PRW06_DEV,END=4000)
1140 IF K$(1,2)<>N0$ THEN GOTO 4000
1150 FIND (PRM01_DEV,KEY=K$(1,11),DOM=1100)IOL=PRM01A
1170 DIM S[20]
1180 PRINT @(COLUMN,11),FNF$(A0$(3,P[0]),M0$)," ",A1$(1,30),
1190 IF L>L9 THEN GOSUB 5400
1200 REM " --- Employee Information"
1204 REM " --- SSN"
1205 DIM W0$(9); LET W0$(1)="I"; REM "Default if not in file
1207 IF NUM(A1$(136,9))>0 THEN LET W0$(1)=A1$(136,9)
1209 REM " --- Employee Name And Deceased Code If Applicable"
1215 LET W1$=CVS(A1$(1,30),1+2+32),W1$=W1$+J1$(1,27-LEN(W1$)),W1$=W1$(1,27)
1220 IF A1$(173,3)="DDD" THEN LET W1$(23,5)=" DECD"
1224 REM " --- Street Address "
1230 LET W2$=CVS(A1$(31,48),1+2+32)+J1$,W2$=W2$(1,40)
1234 REM " --- City"
1240 LET W3$=CVS(A1$(79,22),1+2+32)+J1$,W3$=W3$(1,25)
1244 REM " --- State"
1245 LET W4$=A1$(101,2)+J1$(1,10-LEN(A1$(101,2)))
1249 REM " --- Foreign Postal Code"
1250 LET W5$=J1$(1,5)
1254 REM " --- Zip Code"
1255 LET X$=A1$(103,9); DIM W6$(5)
1260 GOSUB 3300
1265 LET W6$(1,LEN(X$))=X$
1269 REM " --- Statutory Employee"
1270 LET W7$=" "
1290 LET Y7=0,Z3=0; DIM Y0$(7,"0"),Y1$(7,"0"),Y2$(9,"0"),Y3$(6,"0"),Y4$(9,"0"),Y5$(9,"0"),Y6$(7,"0"),Y7$(7,"0"),Y8$(7,"0"),Y9$(7,"0"),Z0$(9,"0"),Z3$(9,"0"),Z4$(7,"0"),W8$(9,"0"),W9$(7,"0"),MILITARY$(7,"0")
1300 REM " --- Get The Numbers..."
1310 LET K$=KEY(PRW06_DEV,END=1900)
1320 IF POS(A0$=K$)<>1 THEN GOTO 1900
1350 IF K$(12,2)>"15" AND K$(12,2)<"a " THEN GOTO 1900
1360 READ (PRW06_DEV)IOL=PRW06A
1400 IF K$(12,2)="08" THEN LET Y9$=FNJ$(B0,7),S[9]=B0 ELSE IF K$(12,2)="09" THEN LET Y8$=FNJ$(B0,7),S[8]=B0 ELSE IF K$(12,2)="02" THEN LET Y4$=FNJ$(B0,9),S[4]=B0
1420 IF K$(12,2)="01" THEN LET Y2$=FNJ$(B0,9),S[2]=B0 ELSE IF K$(12,2)="04" THEN LET Y3$=FNJ$(B0,6),S[3]=B0 ELSE IF K$(12,2)="03" THEN LET Y0$=FNJ$(B0,7),S[0]=B0
1430 IF K$(12,2)="07" THEN LET Y1$=FNJ$(B0,7),S[1]=B0 ELSE IF K$(12,2)="05" THEN LET W8$=FNJ$(B0,9),S[15]=B0 ELSE IF K$(12,2)="06" THEN LET W9$=FNJ$(B0,7),S[16]=B0 ELSE IF K$(12,2)="11" THEN LET Y5$=FNJ$(B0,9),S[5]=B0
1440 IF K$(12,2)="10" THEN LET Z4$=FNJ$(B0,7),S[14]=B0 ELSE IF K$(12,2)="12" THEN LET Z0$=FNJ$(B0,9),S[10]=B0
1480 IF K$(12,2)="13" THEN GOTO 1500
1490 GOTO 1300
1500 REM " --- Box 13 Here"
1520 IF POS(B0$(14,1)="AB") THEN LET Y7=Y7+B0,Y7$=FNJ$(Y7,7),S[6]=S[6]+B0
1530 IF B0$(14,1)="C" THEN LET Y6$=FNJ$(B0,7),S[7]=B0
1540 IF B0$(14,1)>="D" AND B0$(14,1)<="H" THEN LET Z3=Z3+B0,Z3$=FNJ$(Z3,9),S[13]=S[13]+B0
1560 IF B0$(14,1)="Q" THEN LET MILITARY$=FNJ$(B0,7),S[17]=B0
1590 GOTO 1300
1900 REM " --- Pension Plan/Deferred Comp Flags"
1910 IF A1$(226,1)="Y" THEN LET Z1$="P" ELSE LET Z1$=" "
1920 IF NUM(Z3$)>0 THEN LET Z2$="D" ELSE LET Z2$=" "
2000 REM " --- Intermediate Total Record"
2020 IF L>L9 THEN GOSUB 5400
2300 REM " --- Setup Employee Record 1W-2W Write"
2305 DIM W$(128),Y$(128),S$(128)
2310 LET W$(1)="1W"+W0$+W1$+W2$+W3$+W4$+W5$+W6$+W7$
2320 LET Y$(1)="2W"+Y0$+" "+Y1$+" "+Y2$+" "+Y3$+Y4$+J0$(1,9)+" "+Y5$+J1$(1,7)+Y6$+Y7$+Y8$+Y9$+Z0$+" "+Z1$+" "+Z2$+" "+Z3$+" "+Z4$
2330 LET S$(1)="3W",S$(3,9)=W8$,S$(12,7)=W9$,S$(34,7)=MILITARY$
2400 REM " --- Write"
2420 GOSUB 5000
2430 LET T9=T9+1,U=U+1
2440 LET L=L+1
2460 FOR X=0 TO 20
2470 LET T[X]=T[X]+S[X],U[X]=U[X]+S[X],X[X]=X[X]+S[X]
2480 NEXT X
2500 REM " --- State Records '1S' And '2S'"
2510 DIM J1$(128),C0$(0),C1$(0),C$(0)
2530 LET K$=KEY(PRW06_DEV,END=2800)
2540 IF POS(A0$=K$)<>1 THEN GOTO 2800
2550 READ (PRW06_DEV)IOL=PRW06A
2560 IF B0=0 THEN GOTO 2530
2580 IF B0$(12,2)>"15" AND B0$(12,2)<"a " AND B0$(15,2)="  " THEN IF C$="" THEN GOTO 2530 ELSE LET C=0; GOTO 2720
2600 REM " --- Check And See If Already Processing"
2610 LET C=INT(POS(B0$(15,2)=C$,2)/2)
2620 IF POS(B0$(15,2)=C$,2)=0 THEN LET C$=C$+B0$(15,2),C=INT(POS(B0$(15,2)=C$,2)/2),C0$=C0$+J1$,C1$=C1$+J1$; GOSUB 3400
2700 IF B0$(12,2)="18" THEN LET C1$(C*128+63,8)=FNJ$(B0,8) ELSE IF B0$(12,2)="17" THEN LET C1$(C*128+54,9)=FNJ$(B0,9)
2720 IF B0$(12,2)="21" THEN LET C1$(C*128+96,7)=FNJ$(B0,7) ELSE IF B0$(12,2)="20" THEN LET C1$(C*128+87,9)=FNJ$(B0,9)
2790 GOTO 2530
2800 REM " --- Now Post Them"
2810 IF C$="" THEN GOTO 2900
2820 FOR X=1 TO LEN(C$) STEP 2
2830 LET C=INT(X/2)
2835 IF C1$(C*128+52,2)="06" THEN LET C1$(C*128+76,5)=C1$(C*128+98,5),C1$(C*128+96,7)=J0$(1,7),C1$(C*128+87,9)=J0$(1,9); REM "In CA, local is really CASDI
2840 LET W$=C0$(C*128+1,128),Y$=C1$(C*128+1,128),S$(1)=""
2850 GOSUB 5000
2860 NEXT X
2900 GOTO 1100
3300 REM " --- Fix Up Zip Code"
3340 LET X$=CVS(X$,1+32)+FILL(5)
3360 LET X$=X$(1,5)
3380 RETURN 
3400 REM " --- State Record Setup"
3410 DIM D0$(90)
3420 FIND (PRM10_DEV,KEY=N0$+"C"+B0$(15,2),DOM=3490)IOL=PRM10C
3440 LET C0$(C*128+1,128)=W$,C0$(C*128+1,2)="1S",C1$(C*128+1,2)="2S"
3460 LET C0$(C*128+125,2)=STR(INT(POS(B0$(15,2)=X9$,2)/2)+1:"00"),C1$(C*128+52,2)=C0$(C*128+125,2)
3470 LET C1$(C*128+3,12)=D0$(45,12),C1$(C*128+15,4)=STR(I3*3:"00")+I0$(3,2)
3480 LET C1$(C*128+37,2)="00",C1$(C*128+39,4)=FNC2$(A1$(161)),C1$(C*128+43,4)=FNC2$(A1$(170))
3490 RETURN 
4000 REM " --- Totals"
4020 IF L=0 THEN GOTO 4200
4100 IF MOD(U,41)>0 AND U>L9 THEN GOSUB 5400
4200 REM " --- Grand Total"
4205 LET U$=STR(U:J0$(1,7))
4210 LET U0$=FNJ$(U[0],13)
4215 LET U1$=FNJ$(U[1],12)
4220 LET U2$=FNJ$(U[2],13)
4225 LET U3$=FNJ$(U[3],12)
4230 LET U4$=FNJ$(U[4],12)
4235 LET U5$=FNJ$(U[5],13)
4240 LET U6$=FNJ$(U[6],12)
4245 LET U7$=FNJ$(U[7],12)
4250 LET U8$=FNJ$(U[8],12)
4255 LET U9$=FNJ$(U[9],12)
4260 LET Z0$=FNJ$(U[10],12)
4265 LET Z3$=FNJ$(U[13],13)
4270 LET Z4$=FNJ$(U[14],12)
4272 LET Z5$=FNJ$(U[15],13)
4274 LET Z6$=FNJ$(U[16],12)
4276 LET MILITARY$=FNJ$(U[17],12)
4278 DIM THIRDPARTY$(12,"0")
4280 DIM W$(128),Y$(128)
4285 LET W$(1)="1T"+U$+U0$+" "+U1$+U2$+" "+U3$+" "+U4$+U7$+U6$+U8$+U9$
4290 LET Y$(1)="2T"+Z0$+" "+Z3$+" "+Z4$+" "+J0$(1,13)+" "+U5$,Y$(71,13)=Z5$,Y$(85,12)=Z6$,Y$(98,12)=THIRDPARTY$,Y$(111,12)=MILITARY$
4292 GOSUB 5100
4295 LET U=0
4300 REM " --- Final Record Of The Whole File And All Companies 1F"
4305 LET X$=STR(T9:J0$(1,7)),X0$=FNJ$(X[0],16),X1$=FNJ$(X[1],16),X2$=FNJ$(X[2],16),X3$=FNJ$(X[3],16),X4$=FNJ$(X[4],16),X8$=FNJ$(X[8],16)
4310 DIM W$(128)
4320 LET W$(1)="1F"+X$+X0$+" "+X1$+" "+X2$+" "+X3$+" "+X4$+" "+X8$
4325 LET W$=FNU$(W$)
4340 PRINT (W2_DEV)W$,
4390 GOTO 9900
5000 REM " --- Write To W2SETTUP"
5050 LET W$=FNU$(W$)
5100 PRINT (W2_DEV)W$,
5120 LET Y$=FNU$(Y$)
5130 PRINT (W2_DEV)Y$,
5140 IF POS(" "<>S$)>0 THEN LET S$=FNU$(S$); PRINT (W2_DEV)S$,
5150 LET S$(1)=""
5160 RETURN 
5400 REM " --- Subtotal"
5405 LET L=0
5410 LET T0$=FNJ$(T[0],10)
5415 LET T1$=FNJ$(T[1],10)
5420 LET T2$=FNJ$(T[2],10)
5425 LET T3$=FNJ$(T[3],10)
5430 LET T4$=FNJ$(T[4],10)
5435 LET T5$=FNJ$(T[5],10)
5440 LET T6$=FNJ$(T[6],10)
5445 LET T7$=FNJ$(T[7],10)
5450 LET T8$=FNJ$(T[8],11)
5455 LET T9$=FNJ$(T[9],10)
5460 LET Z0$=FNJ$(T[10],10)
5465 LET Z3$=FNJ$(T[13],10)
5470 LET Z4$=FNJ$(T[14],10)
5472 LET Z5$=FNJ$(T[15],11)
5474 LET Z6$=FNJ$(T[16],10)
5476 LET MILITARY$=FNJ$(T[17],10)
5480 DIM W$(128),Y$(128)
5485 LET W$(1)="1I"+T0$+" "+T1$+" "+T2$+" "+T3$+" "+T4$+J1$(1,7)+T7$+T6$+T8$+T9$+Z0$+" "+Z3$
5490 LET Y$(1)="2I"+Z4$+" "+J0$(1,10)+" "+T5$,Y$(36,11)=Z5$,Y$(48,10)=Z6$,Y$(59,12)=MILITARY$
5500 GOSUB 5100
5520 DIM T[20]
5530 DIM Z0$(9,"0"),Z3$(9,"0"),Z4$(7,"0")
5590 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8035 DEF FNC2$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(1),Q1$)
8080 DEF FNJ$(Q,Q1)=STR(Q*100:J0$(1,Q1))
8095 DEF FNU$(Q$)=CVS(Q$,4)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

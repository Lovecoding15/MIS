0010 REM "IVX - Rebuild Lot/Serial Xrefs"
0020 REM "Program IVX.UA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "14Oct97 WGH: Remove IVM-07 and IVM-17 records with blank lot/serial number (105,120-150,170-180,860,880,1105,1500-1590,8000-8080)"
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=3
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="IVM-07",FILES$[2]="IVM-17",FILES$[3]="IVM-08"
0140 LET OPTIONS$[1]="F",OPTIONS$[2]="F",OPTIONS$[3]="CL"
0150 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET IVM07_DEV=CHANNELS[1],IVM17_DEV=CHANNELS[2]
0180 IF IVM07_DEV=0 OR IVM17_DEV=0 THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 IVM08A: IOLIST X0$(1)
0500 REM " --- Initializations"
0505 PRECISION 4
0510 DIM X0$(44)
0550 LET LAST$=""
0800 REM " --- Options
0810 LET V4$="Do You Wish To Rebuild Lot/Serial Number Cross-References"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0850 REM " --- Additional File Opens"
0860 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0870 IF STATUS>0 THEN GOTO 9900
0880 LET IVM08_DEV=CHANNELS[3]
0900 REM " --- Position files"
0910 CALL "SYC.NB","Updating",26,COLUMN
0990 READ (IVM07_DEV,KEY="",DOM=1000)
1000 REM " --- Next Lot/Serial Number"
1010 LET K$=KEY(IVM07_DEV,END=4000)
1020 READ (IVM07_DEV)
1030 IF K$(1,24)=LAST$ THEN GOTO 1100
1040 LET LAST$=K$
1050 PRINT @(COLUMN,11),K$(1,2)," ",K$(3,2)," ",K$(5,20),
1100 REM " --- Write IVM-08 xref"
1105 IF FNP$(K$(25))="" THEN GOTO 1500
1110 LET X0$(1)=K$(1,2)+K$(25)+K$(3)
1120 WRITE (IVM08_DEV,KEY=X0$)IOL=IVM08A
1500 REM " --- Remove Blank Lot/Serial Number Records"
1505 IF FNP$(K$(25))<>"" THEN GOTO 3900
1510 READ (IVM17_DEV,KEY=K$,DOM=1520)
1520 LET IVM17K$=KEY(IVM17_DEV,END=1590)
1530 IF POS(K$=IVM17K$)<>1 THEN GOTO 1590
1540 REMOVE (IVM17_DEV,KEY=IVM17K$,DOM=1550)
1550 GOTO 1510
1590 REMOVE (IVM07_DEV,KEY=K$,DOM=1600)
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

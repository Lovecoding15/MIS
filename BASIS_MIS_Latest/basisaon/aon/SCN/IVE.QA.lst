0010 REM "IVE - Cancel Physical Inventory"
0020 REM "Program IVE.QA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "21May96 Caj: Wasn't changing IVM-02's 'Selected' flag (2220)
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=5
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-10",FILES$[3]="IVW-10"
0130 LET FILES$[4]="IVM-02",FILES$[5]="IVE-03",OPTIONS$[3]="C"
0145 LET OPTIONS$[5]="L"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM10_DEV=CHANNELS[2],IVW10_DEV=CHANNELS[3]
0180 LET IVM02_DEV=CHANNELS[4],IVE03_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0220 IVM02A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0240 IVM10P: IOLIST Y0$(1)
0250 IVE03A: IOLIST W0$(1),W1$(1),W[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0400 REM " --- Parameters"
0405 DIM P[1]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="IV"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1))
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM B0$(24),B1$(64),B[20],W0$(56),W1$(60),W[2],Y0$(11)
0600 REM " --- Additional file opens"
0800 REM " --- Process input"
0810 LET ACTION$="C",WHSE$=I3$(6,2),CUTOFF$="",QUANTITY$=""
0820 CALL "IVC.IC",ACTION$,CHANNELS[ALL],WHSE$,CUTOFF$,QUANTITY$,FKEY,STATUS
0830 IF FKEY<>0 THEN GOTO 9900
0840 IF STATUS<>0 THEN GOTO 9900
0900 REM " --- Position file"
0910 CALL "SYC.NB","Clearing",17+P[0],COLUMN
0990 READ (IVW10_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next entry record"
1010 LET K$=KEY(IVW10_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 READ (IVW10_DEV)
1200 REM " --- Update IVM-10 cycle code record"
1210 LET Y0$(1)=N0$+"P"+WHSE$+K$(3,2)
1220 FIND (IVM10_DEV,KEY=Y0$(1,7),DOM=1900)IOL=IVM10P
1230 LET Y0$(8,1)="5"
1240 WRITE (IVM10_DEV,KEY=Y0$(1,7))IOL=IVM10P
1900 REM " --- Position physical inventory record"
1980 LET FIRST$=K$(1,2)+WHSE$+K$(3)
1990 READ (IVE03_DEV,KEY=FIRST$,DOM=2000)
2000 REM " --- Read next physical inventory record"
2010 LET K$=KEY(IVE03_DEV,END=3900)
2020 IF POS(FIRST$=K$)<>1 THEN GOTO 3900
2030 READ (IVE03_DEV)IOL=IVE03A
2040 PRINT @(COLUMN,11),W0$(3,2)," ",W0$(5,2)," ",K$(7,10)," ",K$(17,P[0]),
2200 REM " --- Retrieve warehouse record"
2210 DIM B0$(24),B1$(64),B[20]
2220 LET B0$(1)=K$(1,4)+K$(17)
2230 EXTRACT (IVM02_DEV,KEY=B0$,DOM=2900)IOL=IVM02A
2240 LET B1$(15,1)="N"
2250 WRITE (IVM02_DEV,KEY=B0$)IOL=IVM02A
2900 REM " --- Loop back for next physical inventory record"
2910 REMOVE (IVE03_DEV,KEY=K$,DOM=2920)
2990 GOTO 2000
3900 REM " --- Loop back for next entry record"
3910 REMOVE (IVM10_DEV,KEY=Y0$(1,7),DOM=3920)
3990 GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
4900 REM " --- File in use"
4910 CALL "IVC.SB","P",STATUS
4990 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "IVX - Rebuild Lot/Serial Transaction Xrefs"
0020 REM "Program IVX.HA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "07Oct96 WGH: Rebuild IVT-03 records for Sales Orders and skip non-inventoried items (130,230,510,860,880,1150-1190,1210-1280)"
0032 REM "14Oct97 WGH: Remove IVT-01 records with blank lot/serial number (1060,1105,1500-1510,8000-8080)"
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=5
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="IVT-01",FILES$[2]="IVM-01"
0130 LET FILES$[3]="IVT-02",FILES$[4]="IVT-03",FILES$[5]="IVM-07"
0140 LET OPTIONS$[1]="F",OPTIONS$[3]="CL",OPTIONS$[4]="CL"
0150 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET IVT01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM07_DEV=CHANNELS[3]
0180 IF IVT01_DEV=0 THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 IVT01A: IOLIST A0$(1),A1$(1)
0220 IVM01A: IOLIST B0$(1),B1$(1),B2$(1)
0230 IVM07A: IOLIST C0$,C1$(1)
0240 IVT02A: IOLIST X0$(1)
0250 IVT03A: IOLIST X0$(1)
0500 REM " --- Initializations"
0510 DIM X0$(61),A0$(52),A1$(96),B0$(22),B1$(60),B2$(64),C1$(96)
0550 LET LAST$=""
0800 REM " --- Options
0810 LET V4$="Do You Wish To Rebuild Lot/Serial Transaction Xrefs"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0850 REM " --- Additional File Opens"
0860 CALL "SYC.DA",1,3,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0870 IF STATUS>0 THEN GOTO 9900
0880 LET IVT02_DEV=CHANNELS[3],IVT03_DEV=CHANNELS[4],IVM07_DEV=CHANNELS[5]
0900 REM " --- Clear and position files"
0930 CALL "SYC.NB","Updating",26,COLUMN
0990 READ (IVT01_DEV,KEY="",DOM=1000)
1000 REM " --- Next Transaction"
1010 LET K$=KEY(IVT01_DEV,END=4000)
1020 READ (IVT01_DEV)IOL=IVT01A
1030 IF K$(1,24)=LAST$ THEN GOTO 1100
1040 LET LAST$=K$,B0$(1)=A0$(1,2)+A0$(5),B2$(1)=""
1050 PRINT @(COLUMN,11),K$(1,2)," ",K$(3,2)," ",K$(5,20),
1070 FIND (IVM01_DEV,KEY=A0$(1,2)+A0$(5,20),DOM=1100)IOL=IVM01A
1100 REM " --- IVT-02 Lot/Serial By Customer Xref"
1105 IF FNP$(K$(25,20))="" THEN GOTO 1500
1110 IF POS(A1$(1,2)="POOP",2)=0 THEN GOTO 3900
1130 GOSUB 6200
1140 IF A1$(1,2)<>"OP" THEN GOTO 1200
1150 WRITE (IVT02_DEV,KEY=X0$)IOL=IVT02A
1160 FIND (IVM07_DEV,KEY=A0$(1,44),DOM=3900)IOL=IVM07A
1170 LET X0$(3,6)=C1$(55,6)
1200 REM " --- IVT-03 Lot/Serial By Vendor Xref"
1210 IF B2$(20,1)<>"Y" THEN GOTO 3900
1280 IF POS(" "<>X0$(3,6))=0 THEN GOTO 3900
1290 WRITE (IVT03_DEV,KEY=X0$)IOL=IVT03A
1500 REM " --- Remove Blank Lot/Serial Number Records"
1505 IF FNP$(K$(25,20))<>"" THEN GOTO 3900
1510 REMOVE (IVT01_DEV,KEY=K$,DOM=1520)
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Initialize IVT-02/IVT-03 Transaction Xref Record"
6210 DIM X0$(61)
6220 LET X0$(1)=A0$(1,2)+A1$(3,6),X0$(9)=B2$(1,3),X0$(12)=A0$(25,20)
6230 LET X0$(32)=A0$(45,3),X0$(35)=A0$(3,22),X0$(57)=A0$(48)
6290 RETURN 
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

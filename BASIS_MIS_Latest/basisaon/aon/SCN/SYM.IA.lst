0010 REM "SYM - Process Maintenance (Description)"
0020 REM "Program SYM.IA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "05Jun96 WGH: Prevent batching process not designed for it (2225,2280,4020,4060,4600-4690)"
0040 REM 
0050 SETESC 9000
0055 SETERR 9000
0060 ON O0 GOTO 0070,4900
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=15
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="SYM-01",FILES$[2]="SYM-02",FILES$[3]="SYM-03"
0130 LET FILES$[4]="SYM-04",FILES$[5]="SYM-06",FILES$[6]="SYM-09"
0135 LET FILES$[7]="SYM-11",FILES$[8]="SYM-19",FILES$[9]="SYM-29"
0140 LET FILES$[10]="SYM-39",FILES$[11]="SYM-49",FILES$[12]="SYM-59"
0145 LET FILES$[13]="SYS-01"
0150 CALL "SYC.DA",1,1,13,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS>0 THEN GOTO 9900
0160 LET SYM01_DEV=CHANNELS[1],SYM02_DEV=CHANNELS[2],SYM03_DEV=CHANNELS[3]
0165 LET SYM04_DEV=CHANNELS[4],SYM06_DEV=CHANNELS[5],SYM09_DEV=CHANNELS[6]
0170 LET SYM11_DEV=CHANNELS[7],SYM19_DEV=CHANNELS[8],SYM29_DEV=CHANNELS[9]
0175 LET SYM39_DEV=CHANNELS[10],SYM49_DEV=CHANNELS[11],SYM59_DEV=CHANNELS[12]
0180 LET SYS01_DEV=CHANNELS[13]
0200 REM " --- IOLists
0210 SYM04A: IOLIST *,P1$
0220 SYM09A: IOLIST A0$(1),A1$(1),A2$(1),A3$,A[ALL]
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters
0405 DIM P[2],I[3],G[4],C9$(36),I3$(20)
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0415 LET N0$=F0$(16,2),N1$=F4$,N2$="SY",N4$=F5$
0460 DIM INFO$[20]
0470 CALL "SYC.VA","GL",INFO$[ALL]
0480 LET GL$=INFO$[3]
0500 REM " --- Initializations"
0510 LET L0=6
0520 DIM A0$(10),A1$(33),A2$(23),A[3]
0600 REM " --- Additional File Opens"
0610 IF GL$<>"Y" THEN GOTO 0640
0620 LET FILES$[14]="GLM-03",FILES$[15]="GLM-06"
0630 CALL "SYC.DA",1,14,15,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0635 IF STATUS>0 THEN GOTO 9900
0640 LET GLM03_DEV=CHANNELS[14],GLM06_DEV=CHANNELS[15]
0700 REM " --- Background
0710 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0720 PRINT 'SB',@(3,3),"Process ID:",@(53),"Required System:",@(2,4),"Description:",@(60),"Batching:",@(0,L0),'CE','SF',
0900 REM " --- Init File"
0920 READ (SYM09_DEV,KEY="",DOM=0930)
1000 REM " --- Process"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(SYM09_DEV,END=1030)
1030 LET V0$="S",V1$="KCE",V3$="",V4$="<F3>=Lookup",V0=10,V1=15,V2=3
1040 GOSUB 7000
1050 ON V3 GOTO 1070,1000,1000,1060,9900
1060 GOSUB 6000
1070 IF V$="" THEN GOTO 0900
1080 LET A0$(1)=V$
1090 PRINT @(V1,V2),A0$,
1200 REM " --- Find Key"
1220 FIND (SYM09_DEV,KEY=A0$,DOM=1400)IOL=SYM09A
1290 LET O0=1; REM "Existing record
1300 REM " --- Display"
1320 GOSUB 5000
1340 GOTO 4900
1400 REM " --- New Record"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1430 ON V3 GOTO 1440,1400,1400,1400,1000
1440 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Set-Up"
1510 LET O0=0; REM "New record
1520 LET A1$(1)="",A2$(1)=""
2000 REM " --- Description"
2020 LET V0$="S",V1$="",V2$=A1$,V3$="",V4$="",V0=30,V1=15,V2=4,I0=0
2030 GOSUB 7000
2040 ON V3 GOTO 2060,2000,2000,2000,2090
2060 LET A1$(1)=V$
2090 GOTO 2900
2100 REM " --- Required System"
2120 LET V0$="S",V1$="C",V2$=A2$(1,2),V3$="",V4$="Enter ID of required System (<F3>=Lookup)",V0=2,V1=70,V2=3
2130 GOSUB 7000
2140 ON V3 GOTO 2160,2100,2190,2150,2190
2150 CALL "SYC.LM",V$
2160 FIND (SYM04_DEV,KEY=V$,DOM=2100)IOL=SYM04A
2170 LET A2$(1,2)=V$
2180 PRINT @(V1,V2),A2$(1,2),
2190 GOTO 2900
2200 REM " --- Batching?"
2210 IF POS(A2$(3,1)="YN")=0 THEN LET A2$(3,1)=P1$(47,1)
2220 LET V0$="Y",V1$="C",V2$=A2$(3,1),V3$="",V4$="Is this a batched process?",V0=1,V1=70,V2=4
2225 IF A2$(4,1)<>"Y" THEN LET A2$(3,1)="N"; GOTO 2280
2230 GOSUB 7000
2240 ON V3 GOTO 2260,2200,2290,2200,2290
2260 LET A2$(3,1)=V$
2280 PRINT @(V1,V2),A2$(3,1),
2290 GOTO 2900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,4000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="KCS",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4900
4050 IF V$="DELETE" THEN GOTO 4400
4060 IF V$="SPECIAL" THEN GOTO 4600
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4120 WRITE (SYM09_DEV,KEY=A0$)IOL=SYM09A
4200 REM " --- Overlay For New Record"
4210 IF O0<>0 THEN GOTO 4900; REM "Not a new record
4240 RUN "SYM.IB"
4400 REM " --- Delete"
4410 READ (SYM59_DEV,KEY=A0$,DOM=4420)
4420 LET K$=KEY(SYM59_DEV,END=4440)
4430 IF POS(A0$=K$)=1 THEN GOTO 4000; REM "Can't delete when batch exists
4440 READ (SYM19_DEV,KEY=A0$,DOM=4450)
4450 LET K$=KEY(SYM19_DEV,END=4500)
4460 IF POS(A0$=K$)<>1 THEN GOTO 4500
4470 REMOVE (SYM29_DEV,KEY=K$(11)+K$(1,10),DOM=4480)
4480 REMOVE (SYM19_DEV,KEY=K$,DOM=4490)
4490 GOTO 4450
4500 READ (SYM39_DEV,KEY=A0$,DOM=4510)
4510 LET K$=KEY(SYM39_DEV,END=4550)
4520 IF POS(A0$=K$)<>1 THEN GOTO 4550
4530 REMOVE (SYM39_DEV,KEY=K$,DOM=4540)
4540 GOTO 4510
4550 REMOVE (SYM09_DEV,KEY=A0$,DOM=4560)
4580 PRINT @(0,L),'CL',
4590 GOTO 1000
4600 REM " --- Set SPECIAL Allow Batch Flag"
4610 IF POS(A2$(4,1)="YN")=0 THEN LET A2$(4,1)="N"
4620 LET V0$="Y",V1$="C",V2$=A2$(4,1),V3$="",V4$="Can batching be allowed for this process?",V0=1,V1=FNV(V4$),V2=22
4630 GOSUB 7000
4640 ON V3 GOTO 4660,4600,4600,4600,4690
4660 LET A2$(4,1)=V$
4680 IF A2$(4,1)<>"Y" THEN LET A2$(3,1)="N"; PRINT @(70,4),A2$(3,1),
4690 GOTO 4000
4900 REM " --- Process submenu"
4905 DIM MENUS$[3]
4910 LET WIN_X=22,WIN_Y=L0+1
4915 LET MENUS=1,MENUS$[MENUS]="SYM.IA"
4920 IF GL$="Y" THEN LET MENUS=MENUS+1,MENUS$[MENUS]="SYM.IE"
4925 LET MENUS=MENUS+1,MENUS$[MENUS]="SYM.IF"
4930 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4940 CALL "SYC.WC",1,0,80,0,0,L0-1,0
4950 PRINT @(0,L0),'CE',
4960 IF SELECTION$="" THEN GOTO 1000
4970 IF SELECTION$=MENUS$[1] THEN GOTO 2000
4980 PRINT @(FNC(DESCRIPTION$,80),L0-1),'SB',DESCRIPTION$,'SF',
4990 RUN SELECTION$,ERR=4900
5000 REM " --- Display"
5020 PRINT @(70,3),A2$(1,2),@(15,4),A1$,@(70),A2$(3,1),
5090 RETURN 
6000 REM " --- Process ID Lookup"
6010 LET TITLE$="Process ID Lookup",NUMBER=1,V$="NO COMPANY"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="Process ID",FIELD[0]=1,POSITION[0]=1,LENGTH[0]=10
6040 LET DESCRIPTION$[1]="Description",FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6050 CALL "SYC.LK",SYM09_DEV,"",NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,V2+1,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

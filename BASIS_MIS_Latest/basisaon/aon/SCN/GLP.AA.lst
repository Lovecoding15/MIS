0010 REM "GLP - G/L Parameter Maintenance"
0020 REM "Program GLP.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "12Jul96 JAL: Store account length when using defaults (1535)"
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0060 ON O0 GOTO 0070,4500
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GLM-08",FILES$[2]="GLM-10",FILES$[3]="SYS-01"
0160 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET GLM08_DEV=CHANNELS[1],GLM10_DEV=CHANNELS[2],SYS01_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 GLS01A: IOLIST A0$,A1$,A2$(1),A3$(1),A4$(1),A5$,A6$,A7$,A8$
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="GL",N3$=F5$
0430 LET P7=32,P8=10,P9=20,MP=13,O0=1,A3=MP*2+MP*16+MP*3+MP*1+MP*3
0500 REM " --- Initializations"
0510 DIM J0$(20,"0"),J$(80,"-"),J1$(40," "),A2$(10),A4$(32)
0520 DIM B1$(P7*P8),B2$(P7*P9),B3$(P8),A3$(A3)
0550 LET P$="NOTE: Make sure mask allows for negative amounts"
0590 LET A0$=N0$+N2$+"00",L0=3,O0=1
0600 REM " --- Proceed to submenu processing if record exists"
0610 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=0650)IOL=GLS01A
0620 GOTO 4500
0650 LET O0=2
0700 REM " --- Background"
0750 PRINT 'SB',@(26,4),"Account Mask:",@(19,5),"Detail Transactions?",@(12,6),"Retain Transaction History?",@(27,7),"Use Budgets?",@(29,8),"Use Units?",@(20,9),@(20,9),"G/L Posting Method?",@(27,10),"Amount Mask:",@(28,11),"Units Mask:",@(26,12),"Percent Mask:",'SF',
1200 REM " --- Find Record"
1210 FIND (SYS01_DEV,KEY=A0$,DOM=1400)IOL=GLS01A
1220 LET P0=NUM(A2$(7,2))
1300 REM " --- Display"
1310 GOSUB 5000
1390 GOTO 4000
1500 REM " --- New Record"
1510 DIM A2$(10),A3$(A3),A4$(32)
1520 LET O0=2,V$="##########",A1$=V$
1530 GOSUB 6000
1535 GOSUB 6200
1540 LET A2$(7,2)=STR(P0:"00"),A2$(9,2)=STR(P8:"00"),A4$(1,6)="YYYYYY"
1550 LET A6$="#,###,##0.00-",A7$="####0-",A8$="###0.00-"
1590 GOSUB 5000
2000 REM " --- Account Number Mask"
2010 LET V4$="Enter Up To "+STR(P8)+" "+$22$+"#"+$22$+" Characters Separated By Another Character"
2020 LET V0$="S",V1$="CER",V2$=A1$,V3$="",V0=15,V1=40,V2=4,I0=0
2030 GOSUB 7000
2035 ON V3 GOTO 2040,2000,2000,2000,3900
2040 GOSUB 6000
2050 IF V$="" THEN GOTO 2000
2060 LET A1$=V$
2070 GOSUB 6200
2075 IF P0>10 THEN GOTO 2000
2080 LET A2$(7,2)=STR(P0:"00")
2090 GOTO 3900
2100 REM " --- Summary/Detail?"
2110 LET V4$="Y=Detailed Postings From Subsystems     N=Summarized Postings"
2120 LET V0$="Y",V1$="CR",V2$=A4$(1,1),V3$="",V0=1,V1=40,V2=5
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2150 LET A4$(1,1)=V$
2160 IF A4$(1,1)<>"Y" THEN LET A4$(1,1)="N",A4$(4,1)="N"
2170 PRINT @(V1,V2),A4$(1,1),@(V1,V2+1),A4$(4,1)
2190 GOTO 3900
2200 REM " --- Budgets?"
2210 LET V4$="Y=Enter And Maintain Budget Information     N=Budgets Not Used"
2220 LET V0$="Y",V1$="CR",V2$=A4$(2,1),V3$="",V0=1,V1=40,V2=7
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 LET A4$(2,1)=V$
2290 GOTO 3900
2300 REM " --- Units?"
2310 LET V4$="Y=Enter And Maintain Units     N=Units Not Used"
2320 LET V0$="Y",V1$="CR",V2$=A4$(3,1),V3$="",V0=1,V1=40,V2=8
2330 GOSUB 7000
2340 ON V3 GOTO 2350,2300,3900,2300,3900
2350 LET A4$(3,1)=V$
2390 GOTO 3900
2400 REM " --- Amount Mask"
2410 LET V0$="S",V1$="C",V2$=A6$,V3$="",V4$=P$,V0=15,V1=40,V2=10
2420 GOSUB 7000
2430 ON V3 GOTO 2440,2400,3900,2400,3900
2440 GOSUB 6100
2445 IF V3=4 THEN GOTO 4000
2450 IF V$="" THEN GOTO 2420
2460 LET A6$=V$
2470 PRINT @(V1,V2),'CL',A6$,
2490 GOTO 3900
2500 REM " --- Units Mask"
2510 LET V0$="S",V1$="C",V2$=A7$,V3$="",V4$=P$,V0=12,V1=40,V2=11
2520 GOSUB 7000
2530 ON V3 GOTO 2540,2500,3900,2500,3900
2540 GOSUB 6100
2550 IF V$="" THEN GOTO 2500
2560 LET A7$=V$
2570 PRINT @(V1,V2),'CL',A7$,
2590 GOTO 3900
2600 REM " --- Percent Mask"
2610 LET V0$="S",V1$="C",V2$=A8$,V3$="",V4$=P$,V0=10,V1=40,V2=12
2620 GOSUB 7000
2630 ON V3 GOTO 2640,2600,3900,2600,3900
2640 GOSUB 6100
2650 IF V$="" THEN GOTO 2600
2660 LET A8$=V$
2670 PRINT @(V1,V2),'CL',A8$,
2690 GOTO 3900
2800 REM " --- G/L Posting Method"
2810 LET V4$="P=Post Detail By Fiscal Period/D=Post Detail By Transaction Date/N=No Posting"
2820 LET V0$="S",V1$="CR",V2$=A4$(7,1),V3$="DNP",V0=1,V1=40,V2=9
2830 GOSUB 7000
2840 IF V3=4 THEN GOTO 4000
2850 LET A4$(7,1)=V$
2890 GOTO 3900
2900 REM " --- Retain Transaction History?"
2910 LET V4$="Y=Retain Transaction History  N=Discard Transactions After Daily Detail Update"
2920 LET V0$="Y",V1$="CR",V2$=A4$(4,1),V3$="",V0=1,V1=40,V2=6
2925 IF A4$(1,1)<>"Y" THEN LET A4$(4,1)="N"; GOTO 2970
2930 GOSUB 7000
2940 ON V3 GOTO 2950,2900,3900,2900,3900
2950 LET A4$(4,1)=V$
2970 PRINT @(V1,V2),A4$(4,1),
2990 GOTO 3900
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2900,2200,2300,2800,2400,2500,2600,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V0$="S",V4$="Is The Above Information Correct (Y/N)?"
4020 LET V1$="C",V2$="Y",V3$="",V0=6,V1=42+INT(LEN(V4$)/2),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4150
4050 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4110 WRITE (SYS01_DEV,KEY=A0$)IOL=GLS01A
4180 IF O0=2 THEN GOTO 4900
4190 GOTO 4500
4200 REM " --- Delete all records"
4210 REMOVE (SYS01_DEV,KEY=N0$+N2$+"00",DOM=4220)
4220 REMOVE (SYS01_DEV,KEY=N0$+N2$+"01",DOM=4230)
4230 REMOVE (SYS01_DEV,KEY=N0$+N2$+"02",DOM=4240)
4290 GOTO 9900
4500 REM " --- Process submenu"
4510 DIM MENUS$[1]
4520 LET MENUS=1,MENUS$[MENUS]="GLP.AA",WIN_X=0,WIN_Y=4,O0=1
4530 CALL "SYC.OA",MENUS,MENUS$[ALL],WIN_X,WIN_Y,DESCRIPTION$,SELECTION$
4540 IF SELECTION$="" THEN GOTO 9900
4550 PRINT @(0,3),'CE',
4560 IF SELECTION$="GLP.AA" THEN GOTO 0700
4590 RUN SELECTION$,ERR=5000
4900 REM " --- Run next overlay"
4990 RUN "GLP.AB"
5000 REM " --- Display"
5010 IF POS(A4$(7,1)="PDN")=0 THEN LET A4$(7,1)="P"
5020 PRINT @(40,4),A1$,@(40,5),A4$(1,1),@(40,6),A4$(4,1),@(40,7),A4$(2,1),@(40,8),A4$(3,1),@(40,9),A4$(7,1),@(40,10),A6$,@(40,11),A7$,@(40,12),A8$,
5090 RETURN 
6000 REM " --- Verify Mask"
6020 IF V$="" THEN GOTO 6090
6040 FOR X=1 TO LEN(V$)
6050 IF POS(V$(X,1)="0#-")=0 THEN EXITTO 6080
6060 NEXT X
6070 GOTO 6090
6080 LET V$=""
6090 RETURN 
6100 REM " --- Test Mask"
6120 SETERR 6170
6130 PRINT @(0,21),0:V$,@(0,21),'CL',
6140 GOTO 6180
6170 LET V$=""
6180 SETERR 9000
6190 RETURN 
6200 REM " --- Calculate Mask Length"
6210 LET P0=0,A5$=""
6220 FOR X=1 TO LEN(A1$)
6230 IF POS(A1$(X,1)="#0")<>0 THEN LET P0=P0+1,A5$=A5$+A1$(X,1)
6240 IF POS("#"=A1$(X,1))=0 THEN LET A5$=A5$+A1$(X,1)
6280 NEXT X
6285 LET A5$=A1$
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

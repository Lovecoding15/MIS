0010 REM "ARR - Customer Statements (Moore Form #2090A.O.)"
0020 REM "Program ARR.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "29Jan97 JAL: Error 26/1550 when negative value is entered (520,1520,1550,1560,5820)"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ARM-02",FILES$[3]="ARM-04"
0130 LET FILES$[4]="ARM-10",FILES$[5]="ART-01",FILES$[6]="ART-11"
0135 LET FILES$[7]="SYS-01"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET ARM01_DEV=CHANNELS[1],ARM02_DEV=CHANNELS[2],ARM04_DEV=CHANNELS[3]
0185 LET ARM10_DEV=CHANNELS[4],ART01_DEV=CHANNELS[5],ART11_DEV=CHANNELS[6]
0190 LET SYS01_DEV=CHANNELS[7]
0200 REM " --- IOLists
0210 ARM01A: IOLIST B0$,B1$
0220 ARM10H: IOLIST X$(1)
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 ARS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 GLS01A: IOLIST X$,G1$,G2$,G3$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N3$=F5$,WHEN$=F0$(7,3)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[1],G[4]
0460 FOR X=0 TO 4
0470 LET G[X]=NUM(G2$(X*2+1,2),ERR=0480)
0480 NEXT X
0485 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2))
0500 REM " --- Initializations"
0510 DIM BLANKS$(80)
0520 LET M0=LEN(M0$),M1$="######.00-",M1=LEN(M1$),M9$="-######.00",M9=LEN(M9$)
0540 LET NUMBER=1; DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
0560 DIM PRD$[4]; LET PRD$[0]="All",PRD$[1]="30-Days",PRD$[2]="60-Days",PRD$[3]="90-Days",PRD$[4]="120-Days"
0570 LET STMT$=F0$(7,3),MSG$="Thank You For Your Business",AGE$="A",DATE$="I",M_BAL=.01,TERR$="",OPT$="",SEQ$="N",CUST$=""
0700 REM " --- Background"
0710 FOR X=4 TO 20 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(24,3),"Statement Date:",@(6,5),"Message:",@(26,7),"Aging Period:",@(7,9),"Age On Due Date Or Invoice Date:",@(17,11),"Aging Minimum Balance:",@(29,13),"Territory:",@(32,15),"Option:",@(30,17),"Sequence:",@(30,19),"Customer:",'SF',
0790 GOSUB 5000
1000 REM " --- Statement Date"
1020 LET V0$="D",V1$="C",V2$=STMT$,V3$="",V4$="",V0=8,V1=40,V2=3,I0=0
1030 GOSUB 7000
1040 ON V3 GOTO 1050,1000,1000,1000,3900
1060 LET STMT$=V$
1090 GOTO 3900
1100 REM " --- Message"
1120 LET V0$="S",V1$="C",V2$=MSG$,V3$="",V4$="",V0=40,V1=15,V2=5
1130 GOSUB 7000
1140 ON V3 GOTO 1150,1100,3900,1100,3900
1160 LET MSG$=V$
1190 GOTO 3900
1200 REM " --- Aging Period"
1220 LET V0$="S",V1$="C",V2$=AGE$,V3$="A1234",V4$="A=All  1=30-Days  2=60-Days  3=90-Days  4=120-Days",V0=1,V1=40,V2=7
1230 GOSUB 7000
1240 ON V3 GOTO 1250,1200,3900,1200,3900
1260 GOSUB 5100
1280 LET AGE$=V$
1290 GOTO 3900
1300 REM " --- Age On Due Or Invoice Date?"
1320 LET V0$="S",V1$="C",V2$=DATE$,V3$="DI",V4$="D=Due Date  I=Invoice Date",V0=1,V1=40,V2=9
1330 GOSUB 7000
1340 ON V3 GOTO 1350,1300,3900,1300,3900
1360 GOSUB 5200
1380 LET DATE$=V$
1390 GOTO 3900
1500 REM " --- Aging Minimum Balance"
1520 LET V0$="N",V1$="C",V2$=STR(M_BAL:M9$),V3$=M9$,V4$="Statements Will Not Be Printed For Aging Balances Less Than This Amount.",V0=M9,V1=40,V2=11
1530 GOSUB 7000
1540 ON V3 GOTO 1550,1500,3900,1500,3900
1550 LET V=NUM(STR(V:M9$))
1570 GOSUB 5800
1580 LET M_BAL=V
1590 GOTO 3900
1600 REM " --- Territory"
1620 LET V0$="S",V1$="C",V2$=TERR$,V3$="",V4$="Enter A Valid Territory Code (<F1>=All, <F3>=Lookup)",V0=3,V1=40,V2=13
1630 GOSUB 7000
1640 ON V3 GOTO 1670,1650,3900,1660,3900
1650 LET V$=""; GOTO 1670
1660 GOSUB 6100
1670 GOSUB 5300
1680 IF STATUS=0 THEN LET TERR$=V$ ELSE GOTO 1600
1690 GOTO 3900
1700 REM " --- Option"
1720 LET V0$="S",V1$="C",V2$=OPT$,V3$="",V4$="R=Restart  S=Single  (<F1>=All)",V0=1,V1=40,V2=15
1730 GOSUB 7000
1740 ON V3 GOTO 1760,1750,3900,1700,3900
1750 LET V$=""
1760 IF V$<>"" AND POS(V$="RS")=0 THEN GOTO 1700
1770 GOSUB 5400
1780 LET OPT$=V$
1790 GOTO 3900
1800 REM " --- Sequence"
1820 LET V0$="S",V1$="C",V2$=SEQ$,V3$="AN",V4$="A=Alphabetic  N=Numeric",V0=1,V1=40,V2=17
1825 IF OPT$="S" THEN LET V$="N"; GOTO 1860
1830 GOSUB 7000
1840 ON V3 GOTO 1860,1800,3900,1800,3900
1860 GOSUB 5500
1880 IF V$<>SEQ$ THEN LET SEQ$=V$,CUST$=""; GOSUB 5600
1890 GOTO 3900
1900 REM " --- Numeric Customer"
1905 IF OPT$="" THEN LET CUST$=""; GOSUB 5600; GOTO 3900
1910 IF SEQ$<>"N" THEN GOTO 2000
1915 IF LEN(CUST$)<>6 THEN DIM CUST$(6)
1920 LET V0$="M",V1$="KC",V2$=CUST$(1,P[0]),V3$=P1$,V4$="Enter Starting Customer Number (<F3>=Lookup)",V0=6,V1=40,V2=19
1930 GOSUB 7000
1940 ON V3 GOTO 1960,1900,3900,1950,3900
1950 CALL "SYC.LC",V$
1960 GOSUB 5600
1980 IF STATUS=0 THEN LET CUST$=V$ ELSE GOTO 1900
1990 GOTO 3900
2000 REM " --- Alphabetic Customer"
2005 IF OPT$="" THEN LET CUST$=""; GOSUB 5600; GOTO 3900
2010 IF SEQ$<>"A" THEN GOTO 1900
2015 IF LEN(CUST$)<>10 THEN DIM CUST$(10)
2020 LET V0$="S",V1$="C",V2$=CUST$,V3$="",V4$="Enter Starting Customer Alphabetic Sequence",V0=10,V1=40,V2=19
2030 GOSUB 7000
2040 ON V3 GOTO 2060,2000,3900,2000,3900
2080 LET CUST$(1)=V$
2090 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1100,1200,1300,1500,1600,1700,1800,1900,4000
4000 REM " --- Correct?"
4005 GOSUB 5000
4010 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)?",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run Overlay"
4110 RUN "ARR.CB"
5000 REM " --- Display"
5010 PRINT @(40,3),FNB$(STMT$),@(15,5),'CL',MSG$,
5020 LET V0=1,V1=40,V2=7,V$=AGE$; GOSUB 5100
5030 LET V0=1,V1=40,V2=9,V$=DATE$; GOSUB 5200
5040 LET V0=M1,V1=40,V2=11,V=M_BAL; GOSUB 5800
5050 LET V0=3,V1=40,V2=13,V$=TERR$; GOSUB 5300
5060 LET V0=1,V1=40,V2=15,V$=OPT$; GOSUB 5400
5070 LET V0=1,V1=40,V2=17,V$=SEQ$; GOSUB 5500
5080 LET V0=6,V1=40,V2=19,V$=CUST$; GOSUB 5600
5090 RETURN 
5100 REM " --- Aging Period Display"
5120 LET X=0,X=NUM(V$,ERR=5180)
5180 PRINT @(V1,V2),'CL',V$,@(V1+V0+2,V2),PRD$[X],
5190 RETURN 
5200 REM " --- Age On Due Or Invoice Date Display"
5220 IF V$="D" THEN LET X$="Due Date" ELSE LET V$="I",X$="Invoice Date"
5280 PRINT @(V1,V2),'CL',V$,@(V1+V0+2,V2),X$,
5290 RETURN 
5300 REM " --- Territory Display"
5310 LET STATUS=1
5320 DIM X$(26)
5330 IF V$="" THEN LET X$(7)="All Territories"; GOTO 5370
5340 LET V$=V$+BLANKS$,V$=V$(1,V0)
5350 FIND (ARM10_DEV,KEY=N0$+"H"+V$,DOM=5390)IOL=ARM10H
5370 LET STATUS=0
5380 PRINT @(V1,V2),'CL',V$,@(V1+V0+2,V2),X$(7,20),
5390 RETURN 
5400 REM " --- Option Display
5420 IF V$="R" THEN LET X$="Restart" ELSE IF V$="S" THEN LET X$="Single" ELSE LET V$="",X$="All"
5480 PRINT @(V1,V2),'CL',V$,@(V1+V0+2,V2),X$,
5490 RETURN 
5500 REM " --- Sequence Display"
5520 IF V$="A" THEN LET X$="Alphabetic" ELSE LET V$="N",X$="Numeric"
5580 PRINT @(V1,V2),'CL',V$,@(V1+V0+2,V2),X$,
5590 RETURN 
5600 REM " --- Numeric Customer Display"
5610 IF SEQ$<>"N" THEN GOTO 5700
5620 LET STATUS=1
5630 DIM X$(M0),X1$(80-V1-M0-2)
5640 IF V$="" THEN GOTO 5680
5650 FIND (ARM01_DEV,KEY=N0$+V$,DOM=5690)IOL=ARM01A
5670 LET STATUS=0,X$=FNF$(B0$(3,P[0]),M0$),X1$(1)=B1$(1,30)
5680 PRINT @(V1,V2),'CL',X$,@(V1+M0+2),X1$,
5690 RETURN 
5700 REM " --- Alphabetic Customer Display"
5720 PRINT @(40,19),'CL',CUST$,
5790 RETURN 
5800 REM " --- Aging Minimum Balance Display
5820 PRINT @(40,11),'CL',STR(V:M9$),
5890 RETURN 
6000 REM " --- Call Windowed Lookup"
6020 LET FIELD[0]=1,DESCRIPTION$[1]="Description"
6040 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,ROW=V2+2,V$="SKIP"
6060 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
6100 REM " --- Territory Lookup"
6120 LET RECORD$="H",TITLE$="Territory Lookup"
6140 LET DESCRIPTION$[0]="ID",CHANNEL=ARM10_DEV,FIELD[1]=1
6160 LET POSITION[0]=4,LENGTH[0]=3,POSITION[1]=7,LENGTH[1]=20
6180 GOSUB 6000
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

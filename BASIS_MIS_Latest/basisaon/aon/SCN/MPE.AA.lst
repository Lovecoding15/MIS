0010 REM "MPE - Production Schedule Entry"
0020 REM "Program MPE.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "07Oct98 KJS: Avoid error 42 when deleting a quantity from a schedule with 26 periods (2620)"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=7
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="MPE-01",FILES$[2]="MPM-01",FILES$[3]="IVM-01"
0120 LET FILES$[4]="IVM-02",FILES$[5]="BMM-01",FILES$[6]="SYS-01"
0125 LET FILES$[7]="IVM-10"
0150 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0155 IF STATUS THEN GOTO 9900
0160 LET MPE01_DEV=CHANNELS[1],MPM01_DEV=CHANNELS[2],IVM01_DEV=CHANNELS[3]
0165 LET IVM02_DEV=CHANNELS[4],BMM01_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0170 LET IVM10_DEV=CHANNELS[7]
0200 REM " --- IOLIST's"
0210 MPE01A: IOLIST A0$(1),A1$(1),A[ALL]
0220 MPM01A: IOLIST B0$(1),B1$(1),B2$(1),B[ALL]
0230 IVM01A: IOLIST C0$(1),C1$(1),C2$(1),C3$,C4$,C5$,C6$,C[ALL]
0240 IVM02A: IOLIST D0$(1),D1$(1),D2$,D[ALL]
0250 BMM01A: IOLIST U0$(1),U1$,U2$(1),U[ALL]
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 IVM10C: IOLIST X0$(1)
0290 IVS01A: IOLIST X$,X$,P2$,P3$,X$,X$,M1$,M2$,M3$
0400 REM " --- Parameters"
0405 DIM P[4]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 LET P[0]=NUM(P2$(1,2)),IVS01_PRECISION=NUM(P2$(5,1)),P[2]=NUM(P2$(9,2))
0450 LET P[3]=NUM(P2$(11,2)),P[4]=NUM(P2$(13,2)),NUMBER=1
0500 REM " --- Initializations"
0505 PRECISION IVS01_PRECISION
0510 DIM A0$(27),A1$(100),A[26],B0$(5),B1$(60),B2$(156),B[2]
0520 DIM C0$(22),C1$(P[2]+P[3]+P[4]),C2$(62),C[12],D0$(24),D1$(64),D2$(1)
0530 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
0540 DIM D[16],X0$(32),U0$(22),U2$(57),U[11]
0580 LET A0$(1,2)=N0$,M2=LEN(M2$),M5$="-##0.00%",L0=13
0590 CALL "SYC.RM",M2$,M2,10,10
0600 REM " --- Print positions"
0610 LET X0=INT((80-(39+3*M2))/4),X1=13+M2,P0=X0,P1=P0+X1+2,P2=P1+X1+2
0620 LET P3=P0+4,P4=P1+4,P5=P2+4,P6=P3+9,P7=P4+9,P8=P5+9,P9=80-M2
0630 LET DW=80-(17+P[0]+2)
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,L0-4,0
0720 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0730 PRINT 'SB',@(2,3),"Forecast Type:",@(1,4),"Warehouse Code:",@(11,5),"Item:",@(0,6),"Unit Of Measure:",@(23),"Bill Created:",@(P9-15),"Revision Code:",@(3,7),"Product Type:",@(22),"Last Revision:",@(P9-17),"Estimated Yield:",@(3,8),"Phantom Bill:",@(26),"Last Used:",@(P9-19),"Standard Lot Size:",@(P3+1,L0-3),"Period",@(P4+1),"Period",@(P5+1),"Period",@(P3,L0-2),"End Date",@(P6+M2-8),"Quantity",@(P4),"End Date",@(P7+M2-8),"Quantity",@(P5),"End Date",@(P8+M2-8),"Quantity",
0740 FOR X=1 TO 9
0750 PRINT @(P0,X+(L0-1)),X:"##:",@(P1),X+9:"##:",
0760 IF X+18<=26 THEN PRINT @(P2),X+18:"##:",
0770 NEXT X
0790 PRINT 'SF',
0900 REM " --- Position File"
0990 READ (MPE01_DEV,KEY=A0$(1,2),DOM=1000)
1000 REM " --- Forecast Type"
1005 PRINT 'CF',
1010 LET V2$="",FORECAST$="",ITEM$="",WHSE$=""
1013 LET V2$=KEY(MPE01_DEV,END=1030)
1015 IF POS(A0$(1,2)=V2$)<>1 THEN LET V2$=""
1020 IF V2$="" THEN GOTO 1030
1025 LET ITEM$=V2$(8,20),WHSE$=V2$(6,2),V2$=V2$(3,3),FORECAST$=V2$
1030 LET V0$="S",V1$="KC",V3$="",V0=3,V1=17,V2=3,I0=0,O8=0,O9=0
1035 LET V4$="Enter A Valid Forecast Type (<F3>=Lookup)"
1040 IF FNP$(V2$)<>"" THEN LET V4$=V4$(1,LEN(V4$)-1)+"/<Enter>=Next Record)"
1045 GOSUB 7000
1050 ON V3 GOTO 1065,1000,1000,1055,9900
1055 GOSUB 6000
1060 IF V$="" THEN GOTO 1000
1065 IF V$="" THEN GOTO 0900
1070 IF V$<>FORECAST$ THEN LET WHSE$="",ITEM$=""
1073 LET B0$(1)=N0$+V$
1075 FIND (MPM01_DEV,KEY=B0$,DOM=1000)IOL=MPM01A
1080 PRINT @(V1,V2),B0$(3,3)," ",B1$(1,30)," ",B1$(31,28),
1085 LET A0$(3,3)=B0$(3,3),FORECAST$=A0$(3,3)
1090 GOSUB 5200
1100 REM " --- Warehouse"
1105 PRINT @(17,4),'CL',@(17,5),'CL',
1115 LET V0$="S",V1$="C",V2$=WHSE$,V3$="",V0=2,V1=17,V2=4
1120 IF V2$="" THEN LET V2$=P3$(6,2)
1125 LET V4$="Enter A Valid Warehouse (<F3>=Lookup)"
1130 IF FNP$(V2$)<>"" THEN LET V4$=V4$(1,LEN(V4$)-1)+"/<Enter>=Next Record)"
1135 GOSUB 7000
1140 ON V3 GOTO 1155,1100,1000,1145,1000
1145 GOSUB 6100
1150 IF V$="" THEN GOTO 1100
1155 IF V$<>WHSE$ THEN LET ITEM$=""
1160 LET X0$(1)=N0$+"C"+V$
1165 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=1100)IOL=IVM10C
1170 PRINT @(V1,V2),X0$(4,2),"  ",X0$(6,24),
1175 LET A0$(6,2)=X0$(4,2),WHSE$=X0$(4,2)
1180 GOSUB 6500
1200 REM " --- Item"
1205 GOSUB 5500
1210 LET V0$="S",V1$="KC",V3$="",V0=P[0],V1=17,V2=5,O7=0,O8=0,O9=0
1215 LET V4$="Enter A Valid Item (<F3>=Lookup)"
1220 IF FNP$(V2$)<>"" THEN LET V4$=V4$(1,LEN(V4$)-1)+"/<Enter>=Next Record)"
1225 GOSUB 7000
1230 ON V3 GOTO 1245,1200,1100,1235,1100
1235 CALL "SYC.LI",V$
1240 IF V$="" THEN GOTO 1200
1245 LET C0$(1)=N0$+V$,D0$(1)=N0$+A0$(6,2)+V$,U0$(1)=N0$+V$,BM$="N"
1250 FIND (IVM01_DEV,KEY=C0$,DOM=1200)IOL=IVM01A
1255 FIND (IVM02_DEV,KEY=D0$,DOM=1200)IOL=IVM02A
1260 FIND (BMM01_DEV,KEY=U0$(1,22),DOM=1270)IOL=BMM01A
1265 LET BM$="Y"
1270 LET A0$(8)=C0$(3),ITEM$=C0$(3)
1290 GOSUB 5000
1300 REM " --- Retrieve MPE-01 record"
1310 READ (MPE01_DEV,KEY=A0$,DOM=1400)IOL=MPE01A
1320 GOSUB 5100
1390 GOTO 4000
1400 REM " --- New record?"
1410 LET V4$="Is This A New Record"
1430 CALL "SYC.YN",1,V4$,2,V$,V3
1440 ON V3 GOTO 1450,1400,1200,1400,1200
1490 ON POS(V$="YN") GOTO 1400,1500,1200
1500 REM " --- Initialize new record"
1510 DIM A1$(100),A[26]
1520 LET I0=0
1590 GOSUB 5100
2000 REM " --- Quantities"
2010 IF I0+1>B[0] THEN GOTO 3900
2015 LET V0$="N",V1$="C",V2$=STR(A[I0]),V3$=M2$,V1=P6,V2=L0+I0
2020 IF I0>8 THEN LET V1=P7,V2=L0+(I0-9)
2025 IF I0>17 THEN LET V1=P8,V2=L0+(I0-18)
2030 LET V4$="Enter Quantity For Period "+STR(I0+1)+" (<F1>=Insert Or Delete/<F3>=Duplicate)"
2035 GOSUB 7000
2040 ON V3 GOTO 2045,2200,3900,2800,3900
2045 LET A[I0]=V
2050 PRINT @(V1,V2),A[I0]:M2$,
2090 GOTO 3900
2200 REM " --- Insert Or Delete?"
2210 LET Y1=V1+M2,Y2=V2
2215 PRINT @(Y1,Y2),'SB',"<",'SF',
2220 LET V4$="Insert At This Position Or Delete This Quantity (<F1>=Insert/<F3>=Delete)?"
2230 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
2240 GOSUB 7000
2250 ON V3 GOSUB 2260,2400,2260,2600,2260
2260 PRINT @(Y1,Y2)," ",
2290 GOTO 2000
2400 REM " --- <F1>=Insert"
2410 FOR X=B[0] TO I0+1 STEP -1
2420 IF X>1 THEN LET A[X-1]=A[X-2]
2430 NEXT X
2440 LET A[I0]=0
2490 GOTO 2850
2600 REM " --- <F3>=Delete"
2610 FOR X=I0-1 TO B[0]
2620 IF X>-1 AND X<25 THEN LET A[X]=A[X+1]
2630 NEXT X
2640 LET A[25]=0
2690 GOTO 2850
2800 REM " --- Duplicate current amount"
2810 LET O8=1,O9=1
2820 FOR X=I0+1 TO B[0]-1
2830 LET A[X]=A[I0]
2840 NEXT X
2850 GOSUB 5100
2860 PRINT @(Y1,Y2)," ",
2890 GOTO 2000
3900 REM " --- Next field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 IF I0<26 THEN GOTO 2000
3990 LET I0=0
4000 REM " --- Correct?"
4005 LET V4$="Is The Above Information Correct (Y/N/#/"
4010 IF O8=0 THEN LET V4$=V4$+"Copy/"
4015 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$=V4$+"Delete)"
4020 LET V0=6,V1=FNV(V4$),V2=22,I0=0
4030 GOSUB 7000
4035 ON V3 GOTO 4040,4000,4000,4000,1200
4040 IF V$="DELETE" THEN GOTO 4200
4045 IF (V$="C" OR V$="COPY") AND O8=0 THEN GOTO 4300
4070 LET V=NUM(V$,ERR=4090)
4080 IF V>0 THEN GOTO 4800
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write"
4110 WRITE (MPE01_DEV,KEY=A0$)IOL=MPE01A
4120 GOSUB 6600
4190 GOTO 1200
4200 REM " --- Delete"
4210 REMOVE (MPE01_DEV,KEY=A0$,DOM=4220)
4290 GOTO 1200
4300 REM " --- Copy"
4310 LET WIDTH=14+P[0]+P[2]+P[3]+P[4],HEIGHT=5,TITLE$="Copy "+N4$
4320 IF WIDTH>78 THEN LET WIDTH=78
4330 LET ROW=L0+2,COLUMN=INT((80-WIDTH)/2)
4340 CALL "SYC.WA",0,WIDTH,HEIGHT,COLUMN,ROW,TITLE$,NAME$
4350 PRINT 'SB',@(1,0),"Warehouse Code:",@(11,1),"Item:",'SF',
4390 LET COPYWHSE$=WHSE$,COPYITEM$=ITEM$
4400 REM " --- Copy to Warehouse"
4410 LET V0$="S",V1$="C",V2$=COPYWHSE$,V3$="",V0=2,V1=17,V2=0
4420 LET V4$="Enter A Valid Warehouse (<F3>=Lookup)"
4430 GOSUB 7000
4440 ON V3 GOTO 4460,4400,4700,4445,4700
4445 GOSUB 6100
4450 IF V$="" THEN GOTO 4400
4460 LET X0$(1)=N0$+"C"+V$
4465 FIND (IVM10_DEV,KEY=X0$(1,5),DOM=4400)IOL=IVM10C
4470 PRINT @(V1,V2),X0$(4,2),"  ",X0$(6,20),
4475 LET COPYWHSE$=X0$(4,2)
4500 REM " --- Item"
4510 LET V0$="S",V1$="KC",V2$=COPYITEM$,V3$="",V0=P[0],V1=17,V2=1
4520 LET V4$="Enter A Valid Item (<F3>=Lookup)"
4525 GOSUB 7000
4530 ON V3 GOTO 4545,4500,4400,4535,4400
4535 CALL "SYC.LI",V$
4540 IF V$="" THEN GOTO 4500
4545 LET C0$(1)=N0$+V$
4550 FIND (IVM01_DEV,KEY=C0$,DOM=4500)IOL=IVM01A
4560 DIM X$(WIDTH-2-V1-P[0])
4570 LET X$(1)=FNZ$(C1$,P[2],P[3],P[4])
4580 PRINT @(V1,V2),C0$(3,P[0])," ",X$,
4590 LET COPYITEM$=C0$(3)
4600 REM " --- Copy record"
4610 LET A0$(6,2)=COPYWHSE$,A0$(8)=COPYITEM$
4620 WRITE (MPE01_DEV,KEY=A0$)IOL=MPE01A
4650 LET V0$="S",V1$="C",V2$="",V4$="Copy Completed (<Enter>=Continue)"
4660 LET V3$="",V0=1,V1=INT(WIDTH/2)+INT(LEN(V4$)/2)+2,V2=HEIGHT-2
4670 GOSUB 7000
4680 ON V3 GOTO 4690,4400,4700,4400,4700
4690 GOTO 4400
4700 REM " --- Copy Completed"
4710 CALL "SYC.WD",NAME$
4720 LET A0$(1)=N0$+FORECAST$+WHSE$+ITEM$
4790 GOTO 4000
4800 REM " --- Jump directly to quantity field"
4810 IF V<1 THEN GOTO 4000
4820 IF V>B[0] THEN GOTO 4000
4880 LET I0=V-2
4890 GOTO 3900
5000 REM " --- Display item information"
5010 DIM X$(DW)
5020 LET X$(1)=FNZ$(C1$,P[2],P[3],P[4])
5030 PRINT @(17,5),C0$(3,P[0])," ",X$,@(17,6),C2$(4,2),@(17,7),C2$(1,3),
5040 IF BM$<>"Y" THEN GOTO 5090
5050 PRINT @(17,8),U2$(40,1),@(37,6),FNB$(U2$(45,3)),@(37,7),FNB$(U2$(35,3)),@(37,8),FNB$(U2$(41,3)),@(P9,6),U2$(38,2),@(P9,7),U[4]:M5$,@(P9,8),U[5]:M2$
5090 RETURN 
5100 REM " --- Display quantities"
5110 FOR X=1 TO B[0]
5120 LET V1=P6,V2=L0+(X-1)
5130 IF X>9 THEN LET V1=P7,V2=L0+(X-10)
5140 IF X>18 THEN LET V1=P8,V2=L0+(X-19)
5150 PRINT @(V1,V2),A[X-1]:M2$,
5160 NEXT X
5190 RETURN 
5200 REM " --- Display Dates"
5210 FOR X=1 TO B[0]
5220 LET V1=P3,V2=L0+(X-1)
5230 IF X>9 THEN LET V1=P4,V2=L0+(X-10)
5250 IF X>18 THEN LET V1=P5,V2=L0+(X-19)
5260 PRINT @(V1,V2),FNB6$(B2$(6*(X-1)+1,6)),
5280 NEXT X
5290 RETURN 
5500 REM " Clear fields here
5510 FOR X=21 TO L0 STEP -1
5520 PRINT @(P6,X),FILL(M2+1," "),@(P7,X),FILL(M2+1," "),@(P8,X),FILL(M2+1," "),
5530 NEXT X
5540 FOR X=8 TO 6 STEP -1
5550 PRINT @(17,X),FILL(3," "),@(37,X),FILL(8," "),@(P9,X),'CL',
5560 NEXT X
5570 PRINT @(17,5),'CL',
5590 RETURN 
6000 REM " --- Forecast Lookup"
6010 LET RECORD$="",TITLE$="MRP Forecast Types"
6030 LET DESCRIPTION$[0]="Type",CHANNEL=MPM01_DEV,FIELD[1]=2
6040 LET POSITION[0]=3,LENGTH[0]=3,POSITION[1]=1,LENGTH[1]=30
6090 GOTO 6400
6100 REM " --- Warehouse Lookup"
6110 LET RECORD$="C",TITLE$="Warehouse Lookup"
6130 LET DESCRIPTION$[0]="ID",CHANNEL=IVM10_DEV,FIELD[1]=1
6140 LET POSITION[0]=4,LENGTH[0]=2,POSITION[1]=6,LENGTH[1]=24
6400 REM " --- Call Windowed Lookup"
6410 LET FIELD[0]=1,DESCRIPTION$[1]="Description"
6440 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,ROW=V2+2,V$="SKIP"
6450 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6490 RETURN 
6500 REM " --- Retrieve first item for forecast/whse combination"
6590 READ (MPE01_DEV,KEY=A0$(1,7),DOM=6600)
6600 REM " --- Next Item"
6610 LET V2$="",V2$=KEY(MPE01_DEV,END=6690)
6620 IF POS(A0$(1,6)=V2$)<>1 THEN LET V2$=""
6630 IF V2$<>"" THEN LET V2$=V2$(8,P[0])
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM "FUNCTIONS"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8070 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8090 DEF FNZ$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

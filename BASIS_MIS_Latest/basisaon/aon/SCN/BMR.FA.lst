0010 REM "BMR - Inventory Costing"
0020 REM "Program BMR.FA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "29May99 WGH: Can't enter single character warehouse code (1755)"
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=12
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="BMM-01",FILES$[2]="BMM-01",FILES$[3]="BMM-02"
0130 LET FILES$[4]="BMM-03",FILES$[5]="BMM-05",FILES$[6]="BMM-08"
0135 LET FILES$[7]="BMM-09",FILES$[8]="IVM-01",FILES$[9]="IVM-02"
0140 LET FILES$[10]="IVM-10",FILES$[11]="IVM-14",FILES$[12]="SYS-01"
0160 CALL "SYC.DA",1,1,12,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS<>0 THEN GOTO 9900
0175 LET BMM01_DEV=CHANNELS[1],BMM01_DEV2=CHANNELS[2],BMM02_DEV=CHANNELS[3]
0180 LET BMM03_DEV=CHANNELS[4],BMM05_DEV=CHANNELS[5],BMM08_DEV=CHANNELS[6]
0185 LET BMM09_DEV=CHANNELS[7],IVM01_DEV=CHANNELS[8],IVM02_DEV=CHANNELS[9]
0190 LET IVM10_DEV=CHANNELS[10],IVM14_DEV=CHANNELS[11],SYS01_DEV=CHANNELS[12]
0200 REM " --- IOLists
0240 IVM01A: IOLIST X$,X0$(1)
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 BMS01A: IOLIST X$,P1$,P2$,P3$,P4,X5,P6
0320 IVS01A: IOLIST X$,X$,P5$,P6$,X$,X$,P7$,P8$,P9$
0400 REM " --- Parameters
0405 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0410 LET N0$=F0$(16,2),N1$=F4$,N2$="BM",N3$=F5$
0415 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=BMS01A
0420 FIND (SYS01_DEV,KEY=N0$+"IV"+"00",DOM=9800)IOL=IVS01A
0425 DIM P[2],DESC[3],PARAMS[3]
0430 FOR X=1 TO 3
0435 LET DESC[X]=NUM(P5$((X-1)*2+9,2))
0440 LET PARAMS[X]=DESC[X]
0445 NEXT X
0450 LET P[0]=NUM(P5$(1,2))
0455 LET PARAMS[0]=P[0]
0460 IF NUM(P5$(5,1))>P6 THEN LET P6=NUM(P5$(5,1))
0465 PRECISION P6
0470 DIM INFO$[20]
0475 CALL "SYC.VA","AP",INFO$[ALL]
0480 LET AP$=INFO$[20]
0485 LET COST_PARAM$=P6$(8,1)
0500 REM " --- Init Data
0505 DIM DESCRIPTION$[1],FIELD[1],POSITION[1],LENGTH[1]
0510 LET NUMBER=1,TITLE$="Warehouse Lookup",RECORD$="C"
0515 LET DESCRIPTION$[0]="Whse",POSITION[0]=4,LENGTH[0]=2
0520 LET DESCRIPTION$[1]="Description",POSITION[1]=6,LENGTH[1]=24
0525 LET FIELD[0]=1,FIELD[1]=1,COLUMN=40,ROW=4
0530 DIM IV_CHANS[14]
0535 LET IV_CHANS[1]=IVM01_DEV,IV_CHANS[14]=IVM14_DEV
0700 REM " --- Background
0710 FOR X=4 TO 14 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(25,3),"Starting Bill:",@(27,5),"Ending Bill:",@(24,7),"Effective Date:",@(14,9),"Print/Cost/Both? <P/C/B>:",@(15,11),"Include Comments? <Y/N>:",@(28,13),"Warehouse :",'SF',
1000 REM " --- Starting Bill
1005 BILLFROM: 
1020 DIM ITEM$[6],ITEM[12],X0$(60)
1030 LET ACTION$="N",DEFAULT$=BILLFROM$,V1=40,V2=3,I0=0
1040 LET V4$="Enter A Valid Bill Number, <F1>=First, <F3>=Lookup"
1050 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1060 IF V3=2 THEN GOTO NEXT_BILLFROM
1070 IF V3=4 THEN GOTO 9900
1075 F3_BILLFROM: 
1080 IF V3<>3 THEN GOTO FIRST_BILLFROM
1090 CALL "SYC.LB",V$
1100 GOSUB GET_DESC
1110 IF V$="" THEN GOTO BILLFROM
1120 LET BILLFROM$=V$
1130 GOTO NEXT_BILLFROM
1140 FIRST_BILLFROM: 
1145 IF V3<>1 AND ITEM$[0]<>"" THEN GOTO IVCIA_BILLFROM
1150 PRINT @(V1,V2),'CL',"First"
1160 LET BILLFROM$=""
1165 GOTO NEXT_BILLFROM
1170 IVCIA_BILLFROM: 
1175 FIND (BMM01_DEV,KEY=N0$+ITEM$[0],DOM=BILLFROM)
1180 LET BILLFROM$=ITEM$[0],ITEM$[1]=ITEM$[1]+FILL(28)
1185 PRINT @(V1,V2),ITEM$[0](1,P[0])," ",ITEM$[1](1,28),
1190 NEXT_BILLFROM: 
1195 GOTO 3900
1200 REM " --- Ending Bill
1205 BILLTHRU: 
1210 DIM ITEM$[6],ITEM[12]
1220 LET ACTION$="N",DEFAULT$=BILLTHRU$,V1=40,V2=5,V$=""
1230 LET V4$="Enter A Valid Bill Number, <F1>=Last, <F3>=Lookup"
1240 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1250 IF V3=2 THEN GOTO NEXT_BILLTHRU
1260 IF V3=4 THEN GOTO 9900
1265 F3_BILLTHRU: 
1270 IF V3<>3 THEN GOTO FIRST_BILLTHRU
1280 CALL "SYC.LB",V$
1285 IF V$="" OR V$<BILLFROM$ THEN GOTO BILLTHRU
1290 GOSUB GET_DESC
1300 GOTO ASSIGN_V
1305 FIRST_BILLTHRU: 
1310 IF V3<>1 AND ITEM$[0]<>"" THEN GOTO IVCIA_BILLTHRU
1320 PRINT @(V1,V2),'CL',"Last"
1330 LET V$=""
1340 ASSIGN_V: 
1350 LET BILLTHRU$=V$
1355 GOTO NEXT_BILLTHRU
1360 IVCIA_BILLTHRU: 
1365 FIND (BMM01_DEV,KEY=N0$+ITEM$[0],DOM=BILLTHRU)
1370 IF ITEM$[0]<BILLFROM$ THEN GOTO 1200
1375 LET BILLTHRU$=ITEM$[0],ITEM$[1]=ITEM$[1]+FILL(28)
1380 PRINT @(V1,V2),ITEM$[0](1,P[0])," ",ITEM$[1](1,28),
1385 NEXT_BILLTHRU: 
1390 GOTO 3900
1400 REM " --- Effective Date
1410 IF PDATE$="" THEN LET PDATE$=F0$(7,3)
1420 LET V0$="D",V1$="CR",V2$=PDATE$,V3$="",V4$="Enter Effective Date For Rollup",V0=8,V1=40,V2=7
1430 GOSUB 7000
1440 IF V3=2 THEN GOTO 1460
1450 LET PDATE$=V$,SELECT$="C"
1460 GOTO 3900
1500 REM " --- Cost Bill
1510 LET V0$="S",V1$="CR",V2$=PRINTORCOST$,V3$="PCB",V4$="P=Print Only, C=Cost Only(No Print), B=Print And Cost Simultaneously",V0=1,V1=40,V2=9
1520 GOSUB 7000
1530 IF V3=2 THEN GOTO 1560
1540 IF V$="" THEN GOTO 1500
1550 LET PRINTORCOST$=V$
1560 GOTO 3900
1600 REM " --- Include Comments?
1610 IF COMMENTS$="" THEN LET COMMENTS$="Y"
1620 IF POS(PRINTORCOST$="PB")=0 THEN PRINT @(40,11)," "; GOTO 1670
1630 LET V0$="Y",V1$="C",V2$=COMMENTS$,V3$="",V4$="",V0=1,V1=40,V2=11
1640 GOSUB 7000
1650 IF V3=2 THEN GOTO 1670
1660 LET COMMENTS$=V$
1670 GOTO 3900
1700 REM " --- Warehouse
1710 LET V0$="S",V1$="KC",V2$=WHS$,V3$="",V4$="<F3> to Lookup",V0=2,V1=40,V2=13
1720 GOSUB 7000
1730 IF V3=2 THEN GOTO 1790
1740 IF V3=3 THEN CALL "SYC.LK",IVM10_DEV,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
1750 IF V$="" THEN GOTO 1700
1755 LET V$=V$+FILL(2),V$=V$(1,2)
1760 FIND (IVM10_DEV,KEY=N0$+"C"+V$,DOM=1700)
1770 PRINT @(V1,V2),V$,
1780 LET WHS$=V$
1790 GOTO 3900
1900 REM " --- Warning
1910 IF POS(PRINTORCOST$="CB")=0 THEN GOTO 4000
1920 DIM MESS$[2]
1940 LET MESS$[0]="This Update Will Modify Inventory Unit Costs. You should run an",MESS$[1]="Inventory Valuation Report both before and after the update and",MESS$[2]="make note of any change in valuation. <Enter> To Continue."
1950 CALL "SYC.XA",2,MESS$[ALL],2,22,-1,V$,V3
3900 REM " --- Next Field
3905 IF V3=4 THEN GOTO 4000
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1500,1600,1700,1900,4000
4000 REM " --- Correct?
4010 LET V0$="Y",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
4015 IF PRINTORCOST$<>"P" THEN LET V2$="N"
4020 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4120,1000
4120 RUN "BMR.FB"
6100 REM " --- Find Bill Description
6105 GET_DESC: 
6120 LET V$=V$+FILL(20-LEN(V$))
6130 FIND (BMM01_DEV,KEY=N0$+V$,DOM=6180)
6140 FIND (IVM01_DEV,KEY=N0$+V$,DOM=6180)IOL=IVM01A
6150 LET X0$(1)=FNITEM$(X0$,DESC[1],DESC[2],DESC[3])
6160 PRINT @(V1,V2),V$(1,P[0])," ",X0$(1,38-P[0]),
6170 GOTO 6190
6180 LET V$=""
6190 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

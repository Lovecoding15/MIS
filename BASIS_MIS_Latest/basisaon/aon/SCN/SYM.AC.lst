0010 REM "SYM - Menu Maintenance Listing"
0020 REM "Program SYM.AC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Feb99 JAL: Selection, lookup and totals not working (710,960-990,1040-1050,6020,6220,6410,6520,6910)"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 SYM01A: IOLIST A0$(1),A1$(1),A2$(1)
0220 SYM01B: IOLIST B0$(1),B1$(1)
0230 SYM04A: IOLIST C0$(1),C1$(1)
0310 SYM11A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1)
0500 REM " --- Initializations"
0510 DIM HEADINGS$[1],J$(80,"-")
0520 LET L9=50,L=L9+1,WIDTH=80,PAGE=0,HEADINGS=1,WHEN$=F0$(7,3),CLOCK$=""
0540 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$+" Listing",NUMBER=0,NAME$=""
0550 LET START$="",FINISH$="",PAGEBREAK$="N"
0700 REM " --- Background"
0710 LET WIN_WIDTH=32,WIN_HEIGHT=6,COL=16
0720 LET WIN_Y=10,WIN_X=INT((80-WIN_WIDTH)/2),TITLE$="Report Selections"
0730 CALL "SYC.WA",0,WIN_WIDTH,WIN_HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0740 PRINT 'SB',@(1,0),"Beginning Menu",@(4,1),"Ending Menu",@(5,2),"Page Break",'SF',
0800 REM " --- Get starting/ending menus"
0810 GOSUB 6000
0820 CALL "SYC.WD",NAME$
0900 REM " --- Position File"
0910 CALL "SYC.GA",7,1,"","",STATUS
0920 IF STATUS THEN GOTO 9900
0960 CALL "SYC.NB","Printing",6,COLUMN
0970 LET K$=START$,NUMBER=0
0980 READ (SYM01_DEV,KEY=K$,DOM=1000)IOL=SYM01A
0990 GOTO 1040
1000 REM " --- Main Read"
1010 LET K$=KEY(SYM01_DEV,END=4000)
1020 IF FINISH$<>"" THEN IF K$>FINISH$ THEN GOTO 4000
1030 READ (SYM01_DEV,END=4000)IOL=SYM01A
1040 PRINT @(COLUMN,11),K$,
1050 LET NUMBER=NUMBER+1
1200 REM " --- Additional reads"
1210 LET B0$(1)=A2$(3,6),B1$(1)="",C0$(1)=A2$(1,2),C1$(1)=""
1220 FIND (SYM01_DEV2,KEY=B0$,DOM=1230)IOL=SYM01B
1230 FIND (SYM04_DEV,KEY=C0$,DOM=1240)IOL=SYM04A
1240 LET TYPE$=TYPES$[POS(A2$(9,1)="ALST")]
1290 GOSUB 5200
1900 REM " --- Position detail file"
1990 READ (SYM11_DEV,KEY=A0$,DOM=2000)
2000 REM " --- Read next detail"
2010 LET K$=KEY(SYM11_DEV,END=3900)
2020 IF POS(A0$=K$)<>1 THEN GOTO 3900
2030 READ (SYM11_DEV)IOL=SYM11A
2100 REM " --- Heading?"
2110 IF L+2<L9 THEN GOTO 2200
2120 GOSUB 5000
2130 GOSUB 5200
2200 REM " --- Print detail line"
2210 PRINT (7)@(1),W0$(7,2),@(4),W1$,@(40),W2$(1,1),@(45),W3$
2220 PRINT (7)@(4),W4$,@(68),W2$(2,1),@(70),W2$(3,6)
2230 LET L=L+2
2900 REM " --- Loop back for next detail"
2990 GOTO 2000
3900 REM " --- Loop back for next menu"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF L+2>L9 THEN GOSUB 5000
4020 PRINT (7)""
4030 PRINT (7)NUMBER," Menu(s) Listed"
4090 GOTO 9900
5000 REM " --- Report heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5090 RETURN 
5200 REM " --- Menu subheading"
5210 IF L+9>L9 THEN GOSUB 5000
5220 PRINT (7)""
5230 PRINT (7)J$
5240 PRINT (7)@(4),"Menu ID: ",A0$," ",A1$,@(58),"Type: ",A2$(9,1),"  ",TYPE$
5250 PRINT (7)"Return Menu: ",B0$," ",B1$,@(56),"System: ",C0$," Security: ",A2$(10,1)
5260 PRINT (7)J$
5270 PRINT (7)"Opt Description/Parameter",@(40),"Type Program/Menu ID",@(68),"Sec Passwd"
5280 PRINT (7)J$
5285 LET L=L+7
5290 RETURN 
6000 REM " --- Beginning value"
6010 LET V0$="S",V1$="",V2$=START$,V3$=""
6020 LET V4$="<F1>=First   <F3>=Lookup",V0=6,V1=COL,V2=0
6030 GOSUB 7000
6040 ON V3 GOTO 6100,6050,6000,6090,6500
6050 LET START$=""
6060 PRINT @(V1,V2),"First",'CL',
6070 GOTO 6200
6090 GOSUB 6900
6100 REM 
6110 IF V$="" THEN GOTO 6050
6120 LET START$=V$
6130 PRINT @(V1,V2),START$,'CL',
6200 REM " --- Ending value"
6210 LET V0$="S",V1$="",V2$=FINISH$,V3$=""
6220 LET V4$="<F1>=Last   <F3>=Lookup",V0=6,V1=COL,V2=1
6230 GOSUB 7000
6240 ON V3 GOTO 6300,6250,6000,6290,6500
6250 LET FINISH$=""
6260 PRINT @(V1,V2),"Last",'CL',
6270 GOTO 6400
6290 GOSUB 6900
6300 REM 
6310 IF V$="" THEN GOTO 6250
6320 LET FINISH$=V$
6330 IF LEN(FINISH$)<8 THEN LET FINISH$=FINISH$+FILL(8-LEN(FINISH$))
6340 PRINT @(V1,V2),FINISH$,'CL',
6400 REM " --- Page break?"
6410 LET V0$="Y",V1$="",V2$=PAGEBREAK$,V3$="",V0=1,V1=COL,V2=2
6420 LET V4$="Page Break Between Menus?"
6430 GOSUB 7000
6440 ON V3 GOTO 6450,6400,6200,6400,6500
6450 LET PAGEBREAK$=V$
6500 REM " --- Correct?"
6505 LET V1$="First",V2$="Last"
6510 IF FNP$(START$)<>"" THEN LET V1$=START$
6515 IF FNP$(FINISH$)<>"" THEN LET V2$=FINISH$
6520 PRINT @(COL,0),V1$,'CL',@(COL,1),V2$,'CL',@(COL,2),PAGEBREAK$,'CL',
6525 LET V0$="Y",V1$="",V2$="Y",V3$="",V4$="Is The Above Correct (Y/N)?"
6530 LET V0=1,V1=MAX(INT((WIN_WIDTH-LEN(V4$))/2)-1,0),V2=WIN_HEIGHT-3
6535 PRINT @(V1,V2),V4$,
6540 LET V1=V1+LEN(V4$),V4$=""
6550 GOSUB 7000
6560 PRINT @(0,V2),'CL',
6570 ON V3 GOTO 6580,6000,6000,6000,9900
6580 ON POS(V$="YN") GOTO 6500,6590,6000
6590 RETURN 
6900 REM " --- Standard windowed lookup"
6910 LET RECORD$="",CHANNEL=SYM01_DEV2,TITLE$="ADD+ON System Menus",NUMBER=1
6930 LET DESCRIPTION$[0]="Menu",FIELD[0]=1,POSITION[0]=1,LENGTH[0]=6
6940 LET DESCRIPTION$[1]="Description",FIELD[1]=2,POSITION[1]=1,LENGTH[1]=35
6950 LET ROW=L0+1,COLUMN=79-LENGTH[0]-LENGTH[1]-NUMBER*4,V$="NO COMPANY"
6980 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNS$(Q$)=CVS(Q$,1)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9910 CALL "SYC.WD",NAME$
9950 RUN "SYS.AA"
9999 END

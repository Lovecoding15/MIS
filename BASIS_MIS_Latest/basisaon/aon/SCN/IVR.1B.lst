0010 REM "IVR - Inventory Item Labels"
0020 REM "Program IVR.1B"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 15Dec97 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "16Jun97 JWK: Print 3 character Product Code (5260)
0032 REM "30Mar00 KJS: Avoid error 11 with bad sort file (1120,1140)
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0210 IVM01A: IOLIST A0$,A1$(1),A2$,A3$,A4$,A5$,A6$
0220 IVM02A: IOLIST B0$,B1$,B2$,B[ALL]
0230 IVM03A: IOLIST K3$
0500 REM " --- Init Data"
0510 DIM A1$(60),B[20]
0520 LET X8$="First"
0530 IF HEIGHT<6 THEN LET X9=HEIGHT ELSE LET X9=6
0700 REM " --- Background"
0710 CALL "SYC.NB","Printing:",P[0],COL
0800 REM " --- Options"
0820 CALL "SYC.YN",0,"Do You Wish To Print A Test Pattern",3,V$,V3
0835 IF V3=4 THEN GOTO 9900
0840 ON POS(V$="YN") GOTO 800,850,900
0850 DIM W0$(ACROSS*WIDTH*6,"X")
0860 LET X0=1
0870 GOSUB 5500
0880 GOTO 0800
0900 REM " --- Determine Start"
0910 READ (IVM03_DEV,KEY=N0$+"C"+BEG_WHSE$,DOM=911)
1000 REM "RANGE"
1010 LET K3$=KEY(IVM03_DEV,END=4000)
1020 IF POS(N0$+"C"=K3$)<>1 THEN GOTO 4000
1030 READ (IVM03_DEV)
1040 IF END_WHSE$<>"" THEN IF K3$(4,2)>END_WHSE$ THEN GOTO 4000
1050 IF BEG_PROD$<>"" THEN IF K3$(6,3)<BEG_PROD$ THEN READ (IVM03_DEV,KEY=K3$(1,5)+BEG_PROD$,DOM=1000)
1060 IF END_PROD$<>"" THEN IF K3$(6,3)>END_PROD$ THEN READ (IVM03_DEV,KEY=K3$(1,5)+$FF$,DOM=1000)
1070 IF BEG_ITEM$<>"" THEN IF K3$(9,20)<BEG_ITEM$ THEN READ (IVM03_DEV,KEY=K3$(1,8)+BEG_ITEM$,DOM=1000)IOL=IVM03A
1080 IF END_ITEM$<>"" THEN IF K3$(9,20)>END_ITEM$ THEN GOTO 1000
1100 REM " --- Main Read"
1120 READ (IVM01_DEV,KEY=N0$+K3$(9,20),DOM=1000)IOL=IVM01A
1130 IF ITEM_TYPE$<>"" THEN IF A2$(53,3)<>ITEM_TYPE$ THEN GOTO 1000
1140 READ (IVM02_DEV,KEY=N0$+K3$(4,2)+K3$(9,20),DOM=1000)IOL=IVM02A
1200 REM " --- Setup"
1220 LET A1$(1)=FNITEM$(A1$,P[3],P[4],P[5])
1280 GOSUB 5100
1290 GOTO 1000
4000 REM " --- Totals"
4020 GOSUB 5500
4100 GOTO 9900
5100 REM " --- Assemble"
5120 LET X3=0
5130 LET X3=X3+1
5140 IF X3>W3 THEN RETURN
5150 IF X0>=ACROSS THEN GOSUB 5500
5160 LET X0=X0+1
5180 PRINT @(COL,11),A0$(3,P[0]),
5200 LET W0$(WIDTH*(X0-1)+1,WIDTH)=N1$
5210 LET W0$(WIDTH*(ACROSS+X0-1)+1,WIDTH)=A0$(3,P[0])+" Whse "+K3$(4,2)
5220 LET W0$(WIDTH*(2*ACROSS+X0-1)+1,WIDTH)=A1$(1,WIDTH)
5230 LET W0$(WIDTH*(3*ACROSS+X0-1)+1,WIDTH)=A1$(WIDTH+1)
5240 LET X=POS(" "<>W0$(WIDTH*(3*ACROSS+X0-1)+1,WIDTH),-1)
5250 IF X+8>WIDTH THEN GOTO 5270
5255 IF X=0 THEN LET OFFSET=-1 ELSE LET OFFSET=0
5260 LET W0$(WIDTH*(3*ACROSS+X0-1)+X+2+OFFSET,8)="Prod "+A2$(1,3)
5270 LET W0$(WIDTH*(4*ACROSS+X0-1)+1,WIDTH)="Cycle Code "+B1$(11,2)+" Location "+B1$(1,10)
5290 GOTO 5130
5500 REM " --- Print"
5520 IF X0=0 THEN RETURN
5530 IF X8$="First" THEN FOR X=0 TO HEIGHT-1; PRINT (7)""; NEXT X; LET X8$=""
5540 FOR X=0 TO X9-1
5560 PRINT (7)@(0+W9),W0$(X*ACROSS*WIDTH+1,ACROSS*WIDTH)
5580 NEXT X
5600 IF HEIGHT>6 THEN PRINT (7)W9$(1,HEIGHT-7)
5620 LET X0=0,W0$(1)=""
5690 RETURN
8000 REM " --- Functions"
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9900 RUN "SYS.AA"
9999 END
0010 REM "POR - Purchase Order Invoice Register (Purchase Price Variance)"
0020 REM "Program POR.EB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "08Jul96 WGH: Landed cost not calculated correctly (980,1480,1805-1820,1840,1890,1905,2540,2910,2990,3020,3040-3080,3220,3240,3130,3315,3370)"
0032 REM "21Oct98 WGH: Complete Item Number may not be printed (640,2210)
0033 REM "27Apr00 KJS: Use correct print masks (2210,2310)
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 POE05A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0220 APM01A: IOLIST B0$(1),B1$(1)
0230 POT04A: IOLIST C0$(1),C1$(1),C[8]
0240 POE15A: IOLIST D0$(1),D1$(1),D2$,D[ALL]
0310 POT14A: IOLIST L0$(1),L1$(1),L2$(1),L3$(1),L4$(1),L5$,L6$,L[ALL]
0320 POM02A: IOLIST Y0$(1),Y1$(1),Y2$(1)
0340 IVM01A: IOLIST S0$(1),S1$(1),S2$(1),S3$(1),S4$(1),S5$,S6$(1),S[ALL]
0360 POW17A: IOLIST U0$(1),U[ALL]
0385 POE25A: IOLIST W0$(1),W1$(1),W2$,W[ALL]
0500 REM " --- Initializations"
0505 PRECISION I[1]
0510 DIM O[11]
0550 LET HEADINGS$[2]="Purchase Price Variance",HEADINGS$[3]="",HEADINGS=3
0560 LET DONE=0,VENDOR=0,REPORT=0,L=L9+1,T0$="",T1$="",XM1=15,XM2=12,XM3=12
0570 LET DW=MAX(I[3]+I[4]+I[5],40)
0600 REM " --- Resize masks if needed"
0610 LET O[0]=0,O[1]=M0+1,O[2]=O[1]+12,O[3]=O[2]+8,O[4]=MAX(O[0]+M0+31,O[3]+9)
0620 LET O[11]=WIDTH-M1,O[10]=O[11]-M1,O[9]=O[10]-M3,O[8]=O[9]-M1-1
0630 LET O[7]=O[8]-M3,O[6]=O[7]-M2-3,O[5]=O[6]-9,PO=O[9]-O[7],IV=O[11]-O[9]
0640 IF O[5]>O[4]+12 THEN IF O[5]>O[3]+I[0] THEN GOTO 0700
0650 LET XM1=XM1-1,XM2=XM2-1,XM3=XM3-1
0660 CALL "SYC.RM",M1$,M1,10,XM1
0670 CALL "SYC.RM",M2$,M2,9,XM2
0680 CALL "SYC.RM",M3$,M3,6,XM3
0690 GOTO 0620
0800 REM " --- Subheadings"
0810 DIM PO$(PO-1,"-"),IV$(IV-1,"-")
0820 LET X$=" PO ",PO$(FNC(X$,PO),4)=X$,DW=MIN(DW,O[6]-O[3])
0830 LET X$=" Invoice ",IV$(FNC(X$,IV),9)=X$
0900 REM " --- Position file"
0990 READ (POE05_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read next header"
1010 LET K$=KEY(POE05_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 READ (POE05_DEV)IOL=POE05A
1040 LET X$=FNF$(A0$(5,P[0]),M0$)+" "+A0$(11,10)
1050 IF APTYPE THEN LET X$=A0$(3,2)+" "+X$
1060 PRINT @(COLUMN,11),X$,
1400 REM " --- Position POE-25 PO Detail"
1410 LET ADDITIONAL=0,INVAMT=0,INVQTY=0
1480 PRINT @(40,5),"Purchase Price Variance",'CL',
1490 READ (POE25_DEV,KEY=A0$,DOM=1500)
1500 REM " --- Read next PO Detail record"
1510 LET K$=KEY(POE25_DEV,END=2500)
1520 IF POS(A0$=K$)<>1 THEN GOTO 2500
1530 READ (POE25_DEV)IOL=POE25A
1600 REM " --- Retrieve line code"
1610 GOSUB 6400
1620 IF POS(Y1$(21,1)="MV") THEN GOTO 2400
1700 REM " --- Calculate variance"
1710 LET QTY=W[1]
1720 IF Y1$(21,1)="O" THEN LET QTY=1
1730 PRECISION 2
1740 LET PO_EXTENSION=W[2]*QTY,IV_EXTENSION=W[0]*QTY
1750 PRECISION I[1]
1760 LET VARIANCE=IV_EXTENSION-PO_EXTENSION
1800 REM " --- Accumulate totals needed for landed cost"
1805 IF POS(R3$(55,1)="QC")=0 THEN GOTO 1900
1810 IF Y1$(24,1)<>"Y" THEN GOTO 1900
1820 IF FNP$(W1$(1,7))="" THEN GOTO 1850
1830 LET INVAMT=INVAMT+IV_EXTENSION,INVQTY=INVQTY+QTY
1845 GOTO 1900
1850 LET ADDITIONAL=ADDITIONAL+IV_EXTENSION
1900 REM " --- Retrieve receipt detail record"
1905 IF W[0]=W[2] THEN GOTO 2400
1910 DIM L0$(25),L1$(64),L2$(32),L3$(22),L4$(40),L[12],DESCRIPTION$(DW)
1920 LET L0$(1)=N0$+W0$(5,6)+W1$(1,7)+W1$(11,7)+W1$(8,3),RECEIVED$=""
1930 FIND (POT14_DEV,KEY=L0$,DOM=2000)IOL=POT14A
1940 IF L0$(1,22)=C0$(1,22) THEN GOTO 2000
1950 LET C0$(1)=L0$
1960 FIND (POT04_DEV,KEY=C0$,DOM=2000)IOL=POT04A
1970 IF FNP$(C1$(29,6))<>"" THEN LET RECEIVED$=FNB6$(C1$(29,6))
2000 REM " --- Retrieve inventory item"
2010 DIM S0$(22),S1$(60),S2$(64),S3$(40),S4$(21),S6$(G[4]*6),S[12]
2020 LET DESCRIPTION$(1)=W1$(20,40)
2030 IF Y1$(21,1)<>"S" THEN GOTO 2100
2040 LET S0$(1)=N0$+L3$(3),S1$(1)=NF$,S2$(1)=""
2050 FIND (IVM01_DEV,KEY=S0$,DOM=2060)IOL=IVM01A
2060 LET DESCRIPTION$(1)=FNITEM$(S1$,I[3],I[4],I[5])
2100 REM " --- Level breaks?"
2110 IF T0$<>W0$(5,6) THEN GOSUB 6000
2120 IF T1$<>W0$(11,10) THEN GOSUB 6200
2170 IF L+2>L9 THEN GOSUB 5000
2180 LET L=L+2,VENDOR=VENDOR+VARIANCE,REPORT=REPORT+VARIANCE
2190 ON POS(Y1$(21,1)="SNO") GOTO 2400,2200,2200,2300
2200 REM " --- Standard or Non-stock line"
2210 PRINT (7)@(O[1]),W1$(1,7),"-",W1$(8,3),@(O[2]),W1$(11,7),@(O[3]),S0$(3,I[0]),@(O[5]),RECEIVED$,@(O[6]),W[1]:M2$," ",L1$(22,2),@(O[7]),W[2]:M3$,@(O[8]),PO_EXTENSION:M1$,@(O[9]),W[0]:M3$,@(O[10]),IV_EXTENSION:M1$,@(O[11]),VARIANCE:M1$
2220 PRINT (7)@(O[3]),DESCRIPTION$
2290 GOTO 2400
2300 REM " --- Other line"
2310 PRINT (7)@(O[1]),W1$(1,7),"-",W1$(8,3),@(O[2]),W1$(11,7),@(O[5]),RECEIVED$,@(O[7]),W[2]:M3$,@(O[8]),PO_EXTENSION:M1$,@(O[9]),W[0]:M3$,@(O[10]),IV_EXTENSION:M1$,@(O[11]),VARIANCE:M1$
2320 PRINT (7)@(O[3]),DESCRIPTION$
2400 REM " --- Loop back for next line item"
2490 GOTO 1500
2500 REM " --- Done with price variances. Update landed cost?"
2510 IF POS(R3$(55,1)="QC")=0 THEN GOTO 3900
2520 IF R3$(55,1)="Q" THEN IF INVQTY=0 THEN GOTO 3900
2530 IF R3$(55,1)="C" THEN IF INVAMT=0 THEN GOTO 3900
2900 REM " --- Postion detail file for landed cost pass"
2980 PRINT @(40,5),"Landed Cost Calculation",'CL',
2990 READ (POE25_DEV,KEY=A0$,DOM=3000)
3000 REM " --- Read next detail"
3010 LET K$=KEY(POE25_DEV,END=3900)
3020 IF POS(A0$=K$)<>1 THEN GOTO 3900
3030 READ (POE25_DEV)IOL=POE25A
3100 REM " --- Retrieve line code"
3110 IF FNP$(W1$(1,7))="" THEN GOTO 3800
3120 GOSUB 6400
3130 IF POS(Y1$(21,1)="MV") THEN GOTO 3800
3140 IF Y1$(24,1)<>"Y" THEN GOTO 3800
3200 REM " --- Retrieve detail record"
3210 DIM L0$(25),L1$(64),L2$(32),L3$(22),L4$(40),L[12]
3220 LET L0$(1)=N0$+W0$(5,6)+W1$(1,7)+W1$(11,7)+W1$(8,3)
3230 FIND (POT14_DEV,KEY=L0$,DOM=3800)IOL=POT14A
3240 IF L[0]=0 THEN IF POS(Y1$(21,1)="ON") THEN LET L[0]=1 ELSE GOTO 3800
3300 REM " --- Calculate allocation"
3310 LET COST=W[0],QTY=W[1]
3315 IF QTY=0 THEN IF Y1$(21,1)="O" THEN LET QTY=1 ELSE GOTO 3800
3320 PRECISION 2
3330 LET IV_EXTENSION=QTY*COST
3340 PRECISION I[1]
3350 IF R3$(55,1)="C" THEN LET PERCENT=(IV_EXTENSION*100)/INVAMT
3360 IF R3$(55,1)="Q" THEN LET PERCENT=(QTY*100)/INVQTY
3370 LET AMOUNT=ADDITIONAL*PERCENT/100,LANDED=(IV_EXTENSION+AMOUNT)/QTY
3500 REM " --- Write work file record"
3510 DIM U0$(24),U[1]
3520 LET U0$(1)=N0$+L3$,U[0]=LANDED/L[0]
3530 WRITE (POW17_DEV,KEY=U0$)IOL=POW17A
3800 REM " --- Loop back for next detail"
3890 GOTO 3000
3900 REM " --- Loop back for next header"
3990 GOTO 1000
4000 REM " --- All done"
4010 LET DONE=1
4020 GOSUB 6000
4030 IF L+2>L9 THEN GOSUB 5000
4040 PRINT (7)""
4050 LET TOTAL$="Register",TOTAL=REPORT
4060 GOSUB 6800
4070 LET UPDATE$="POU.EA"
4080 IF GL$<>"Y" THEN RUN UPDATE$,ERR=9900
4090 RUN "GLR.XA"
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+3
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS THEN EXITTO 9900
5040 PRINT (7)@(O[7]),PO$,@(O[9]),IV$
5050 PRINT (7)@(O[0]),"Vendor",@(O[1]),"PO #",@(O[2]),"Rec #",@(O[3]),"Item",@(O[4]),"Invoice",@(O[5]),"Received",@(O[6]+M2-9),"Quantity UM",@(O[7]+M3-5),"Cost",@(O[8]+M1-10),"Extension",@(O[9]+M3-5),"Cost",@(O[10]+M1-10),"Extension",@(O[11]+M1-9),"Variance"
5090 RETURN
6000 REM " --- Vendor Break"
6010 IF T0$="" THEN GOTO 6100
6020 LET TOTAL$="Vendor "+VENDOR$,TOTAL=VENDOR
6030 IF VENDOR<>0 THEN GOSUB 6800
6100 IF DONE THEN GOTO 6190
6110 DIM B0$(8),B1$(30),VENDOR$(31+M0)
6120 LET T0$=W0$(5,6),B0$(1)=N0$+T0$,B1$(1)=NF$,VENDOR=0
6130 FIND (APM01_DEV,KEY=B0$,DOM=6140)IOL=APM01A
6140 LET VENDOR$(1)=FNF$(B0$(3,P[0]),M0$)+" "+B1$
6190 RETURN
6200 REM " --- Invoice Break"
6210 IF L+5>L9 THEN GOSUB 5000
6220 PRINT (7)""
6230 PRINT (7)@(O[0]),VENDOR$,@(O[4]),W0$(11,10)
6240 PRINT (7)""
6250 LET T1$=W0$(11,10),L=L+3
6290 RETURN
6400 REM " --- Retrieve line code"
6410 IF N0$+W1$(18,2)=Y0$ THEN GOTO 6490
6420 LET Y0$(1)=N0$+W1$(18,2),Y1$(1)=""
6430 FIND (POM02_DEV,KEY=Y0$,DOM=6490)IOL=POM02A
6490 RETURN
6800 REM " --- Print total line"
6810 IF L+1>L9 THEN GOSUB 5000
6820 LET L=L+1,TOTAL$="Purchase Price Variance Total For "+FNP$(TOTAL$)
6850 PRINT (7)@(O[11]-LEN(TOTAL$)),TOTAL$,@(O[11]),TOTAL:M1$
6890 RETURN
8000 REM " --- Functions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END
0010 REM "WOC - Work Order SpeedSearch (Material Usage By Work Order)"
0020 REM "Program WOC.QE"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.4 - 15Dec97 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "08Oct97 WGH: Quantity Issued not using Item entered in previous SpeedSearch (6640)"
0032 REM "21Oct97 KJS: Returns wrong item to other SpeedSearch programs (211,511,1720-1740)
0033 REM "19May98 JWK: Last Activity Date and Start Date switched (1615,1670)
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open/Lock Files"
0105 LET FILES=10
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 LET FILES$[1]="IVM-01",FILES$[2]="IVM-03",FILES$[3]="IVM-10"
0120 LET FILES$[4]="IVM-14",FILES$[5]="WOE-01",FILES$[6]="WOE-03"
0125 LET FILES$[7]="WOE-22",FILES$[8]="WOT-21",FILES$[9]="IVM-01"
0130 LET FILES$[10]="ARM-01"
0140 CALL "SYC.DA",1,1,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET IVM01_DEV=CHANNELS[1],IVM03_DEV=CHANNELS[2],IVM10_DEV=CHANNELS[3]
0170 LET IVM14_DEV=CHANNELS[4],WOE01_DEV=CHANNELS[5],WOE03_DEV=CHANNELS[6]
0180 LET WOE22_DEV=CHANNELS[7],WOT21_DEV=CHANNELS[8],IVM01_DEV2=CHANNELS[9]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1)
0211 IVM01A2: IOLIST AA0$(1),AA1$(1)
0220 WOE01A: IOLIST B0$,B1$,B[ALL]
0230 ARM01A: IOLIST C0$(1),C1$(1)
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,X$,X$,X$
0270 IVS01A: IOLIST X$,I1$,I2$,I3$,I4$,X$,M1$,M2$,M3$
0280 WOS01A: IOLIST X$,I1$,I2$,I3$,I4$,X$,M1$,M2$,M3$
0300 IVM10C: IOLIST X0$(1)
0310 WOE22A: IOLIST W0$(1),W1$(1),W[ALL]
0320 WOT21A: IOLIST T0$(1),T1$(1),T[ALL]
0400 REM " --- Parameters"
0405 DIM P[2],I[5],X$(128),INFO$[20]
0410 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2),MAXPAGE=NUM(X$(66,2))
0415 LET TNAME$=FNP$(X$(3,8)),ONAME$=FNP$(X$(27,8)),SNAME$=FNP$(X$(19,8))
0420 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0425 LET SHEIGHT=NUM(X$(45,2)),MAXOPT=NUM(X$(57,2))
0430 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0435 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0440 LET PAGES=INT((DSZ-5000)/4000),SYS01_DEV=NUM(STBL("!SYS01"))
0445 IF PAGES<3 THEN GOTO 9900
0450 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0455 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9900)IOL=IVS01A
0460 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(3,2))
0465 LET I[3]=NUM(I2$(9,2)),I[4]=NUM(I2$(11,2)),I[5]=NUM(I2$(13,2))
0470 FIND (SYS01_DEV,KEY=N0$+"WO00",DOM=9900)IOL=WOS01A
0473 LET OP$=I3$(2,1)
0475 IF OP$="N" THEN GOTO 0500
0477 CALL "SYC.VA","OP",INFO$[ALL]
0480 LET OP$=INFO$[20]
0485 IF OP$="N" THEN GOTO 0500
0490 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9900)IOL=ARS01A
0495 LET P[0]=NUM(P2$(1,2))
0500 REM " --- Initializations"
0510 DIM OPTLIST$[2,MAXOPT,6],A0$(22),A1$(60),B0$(75),B1$(141),B[11]
0511 LET AA0$=A0$,AA1$=A1$
0515 DIM C0$(8),C1$(320),X0$(32),HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0520 DIM PARAMS[10],FILES[14],ITEM$[6],ITEM[12]
0525 DIM W0$(15),W1$(151),W[13],T0$(18),T1$(43),T[11]
0540 LET D_STATUS=0,MAXROW=HEIGHT-5,PAGE=1,M2=LEN(M2$),DONE=0,OPTION=0
0550 LET FILES[1]=IVM01_DEV,FILES[14]=IVM14_DEV,NF$="(Not On File)"
0560 LET PARAMS[0]=I[0],PARAMS[1]=I[3],PARAMS[2]=I[4],PARAMS[3]=I[5]
0570 LET A0$(1)=N0$+OPTION$[3],P=WIDTH-13-M2,DW=P-1,PGM$=PGM(-2)
0600 REM " --- Additional file opens"
0610 IF OP$="N" THEN GOTO 0650
0620 CALL "SYC.DA",1,10,10,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0630 IF STATUS THEN GOTO 9900
0640 LET ARM01_DEV=CHANNELS[10]
0650 REM " --- Option Window"
0660 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,""
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0720 LET HEADING$(2)="Wh WO #    Type Status Started      Last Act Est Comp"
0730 LET FOOTING$(2)=TITLE$,FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0740 PRINT @(0,0),'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",SNAME$,0
0920 PRINT 'CF',
0930 CALL "SYC.WB",TNAME$,0
0940 PRINT 'CS',
0990 GOTO 1200
1000 REM " --- Process key field"
1010 GOSUB 6000
1015 CALL "SYC.WB",TNAME$,0
1020 PRINT @(0,0),'CS',@(0,2),'SB'," Item:",'SF',
1025 LET V2$="",V2$=KEY(IVM01_DEV,END=1040)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1035 IF V2$<>"" THEN LET V2$=V2$(3,I[0])
1040 LET ACTION$="V",V1=7,V2=2,V4$=""
1045 CALL "IVC.IA",ACTION$,V1,V2,FILES[ALL],N0$,V2$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1050 ON V3 GOTO 1080,1000,1000,1000,1060,1070,1060
1060 LET OPTION$[1]="EXIT"
1065 GOTO 4000
1070 LET OPTION$[1]="STARTOVER"
1075 GOTO 4000
1080 LET V$=FNP$(ITEM$[0])
1090 IF V$="" THEN GOTO 0900
1095 LET A0$(3)=V$
1200 REM " --- Process top window display"
1210 DIM LLIST$[PAGES,MAXROW,5],X0$(32),DESCRIPTION$(WIDTH-10-I[0])
1220 DIM MORE$[PAGES,MAXROW,SHEIGHT-2],UNITS[12],SALES[12]
1230 LET LINE=1,L=1,X0=0,X1=1,PAGE=1
1240 READ (IVM01_DEV,KEY=A0$,DOM=1000)IOL=IVM01A
1250 LET DESCRIPTION$(1)=FNITEM$(A1$,I[3],I[4],I[5])
1260 LET X$='SB'+" Item: "+'SF'+A0$(3,I[0])+" "+DESCRIPTION$
1265 LET X1$=A0$(3,I[0]),X2$=PARAM$
1270 CALL "SYC.QB",X$,X1$,X2$
1280 PRINT @(0,2),X$,
1290 READ (IVM03_DEV,KEY=N0$+"G"+A0$(3),DOM=1300)
1300 REM " --- Read next warehouse record"
1310 LET K$=KEY(IVM03_DEV,END=2000)
1320 IF POS(N0$+"G"+A0$(3)=K$)<>1 THEN GOTO 2000
1330 READ (IVM03_DEV)
1350 REM " --- Position WOE-03"
1360 LET X$=N0$(1,2)+"E"+"  "+K$(24,2)+A0$(3)
1390 READ (WOE03_DEV,KEY=X$,DOM=1400)
1400 REM " --- Read next WOE-03 record"
1410 LET K$=KEY(WOE03_DEV,END=1900)
1420 IF POS(X$=K$)<>1 THEN GOTO 1900
1430 READ (WOE03_DEV)
1440 LET B0$(1)=K$(1,2)+K$(4,2)+K$(28,7),W0$(1)=B0$(1,11)+"B"+K$(35,3)
1450 FIND (WOE01_DEV,KEY=B0$(1,11),DOM=1400)IOL=WOE01A
1460 IF B0$(15,1)="C" THEN GOTO 1400
1470 FIND (WOE22_DEV,KEY=W0$,DOM=1400)IOL=WOE22A
1480 IF W1$(91,1)<>"S" THEN GOTO 1400
1490 GOSUB 6500
1500 REM " --- Display page?"
1510 IF L<MAXROW THEN GOTO 1600
1520 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
1530 GOSUB 5000
1540 IF DONE THEN GOTO 4000
1570 IF ANSWER$<>"" THEN GOTO 3000
1590 LET L=1,X1=1
1600 REM " --- Middle window record"
1610 DIM G$(WIDTH-2)
1615 LET STATUS$="",EST$="",STARTED$=B0$(25,3)
1620 IF B0$(15,1)="O" THEN LET STATUS$="Open"
1625 IF B0$(15,1)="P" THEN LET STATUS$="Plan"
1630 IF B0$(15,1)="Q" THEN LET STATUS$="Quote"
1640 IF FNP$(STARTED$)="" THEN IF FNP$(B0$(19,3))<>"" THEN LET STARTED$=B0$(19,3),EST$="est"
1650 LET G$(2)=B0$(54,2)+" "+B0$(5,7)+" "+B0$(12,2)+"   "+STATUS$
1660 IF FNP$(STARTED$)<>"" THEN LET G$(25)=FNB$(STARTED$)
1665 LET G$(34)=EST$
1670 IF FNP$(B0$(28,3))<>"" THEN LET G$(38)=FNB$(B0$(28,3))
1680 IF FNP$(B0$(22,3))<>"" THEN LET G$(47)=FNB$(B0$(22,3))
1690 LET LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=A0$(3)+B0$(5,7)
1700 REM " --- Retrieve finished good and customer"
1710 DIM CUST$(DW),ITEM$(DW),DESC$(DW)
1715 IF B0$(14,1)<>"I" THEN GOTO 1750
1720 LET AA0$(1)=N0$+B0$(56),AA1$(1)=NF$
1730 FIND (IVM01_DEV,KEY=AA0$,DOM=1735)IOL=IVM01A2
1735 LET ITEM$(1)="{Item}"+AA0$(3)
1740 LET DESC$(1)="{Desc}"+FNITEM$(AA1$,I[3],I[4],I[5])
1745 GOTO 1760
1750 LET ITEM$(1)="{Desc}"+B1$(1,30),DESC$(1)="      "+B1$(31,30)
1760 IF OP$="N" THEN GOTO 1800
1770 LET C0$(1)=N0$+B0$(34,6),C1$(1)=NF$
1780 FIND (ARM01_DEV,KEY=C0$,DOM=1800)IOL=ARM01A
1790 LET CUST$(1)="{Cust}"+FNF$(C0$(3,P[0]),M0$)+" "+C1$(1,30)
1800 REM " --- Bottom window record"
1810 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),G$(1)=""
1820 LET G$(1)=ITEM$,G$(P+1)="{Qty Req'd}"+STR(W[2]:M2$)
1830 LET MORE$[PAGE,X1,1]=G$,G$(1)=DESC$,G$(P)="{Qty Issued}"+STR(ISSUES:M2$)
1840 LET MORE$[PAGE,X1,2]=G$,G$(1)=CUST$
1850 IF FNP$(B0$(40,7))<>"" THEN LET G$(P-11)="{Order}"+B0$(40,7)
1870 LET G$(P+3)="{Balance}"+STR(W[2]-ISSUES:M2$),MORE$[PAGE,X1,3]=G$
1880 LET LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
1890 GOTO 1400
1900 REM " --- Loop back for next warehouse"
1990 GOTO 1300
2000 REM " --- No more work orders"
2010 IF X0>0 THEN GOTO 2500
2020 DIM MESSAGE$[1]
2030 LET MESSAGE$[0]="No Work Orders Found (<Enter>=Continue)"
2040 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
2060 IF V3=5 THEN GOTO 4000
2070 IF V3=6 THEN GOTO 4000
2080 LET SELECTION$="PRIOR"
2090 GOTO 3000
2500 REM " --- Display last page"
2510 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2520 GOSUB 5000
2530 IF DONE THEN GOTO 4000
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$=A0$(3),CHOICE2$=""
3030 LET A0$(1)=N0$+ANSWER$
3040 IF LEN(ANSWER$)>20 THEN LET B0$(1)=N0$+"  "+ANSWER$(21)
3050 FIND (IVM01_DEV,KEY=A0$,DOM=3060)IOL=IVM01A
3060 FIND (WOE01_DEV,KEY=B0$(1,11),DOM=3070)IOL=WOE01A
3070 GOSUB 5000
3080 IF CONTROL=1 THEN GOTO 1000
3090 IF OPTION$[2]="ITEM/ORDER" THEN LET OPTION$[3]=A0$(3)+B0$(5,7)
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CF',
6090 RETURN 
6500 REM " --- Accumulate total issues"
6510 DIM T0$(18),T1$(43),T[11]
6520 LET T0$(1)=B0$(1,11),ISSUES=0
6590 READ (WOT21_DEV,KEY=T0$,DOM=6600)
6600 REM " --- Next transaction"
6610 LET K$=KEY(WOT21_DEV,END=6700)
6620 IF POS(B0$(1,11)=K$)<>1 THEN GOTO 6700
6630 READ (WOT21_DEV)IOL=WOT21A
6640 IF W1$(27,20)=T1$(24) THEN LET ISSUES=ISSUES+T[0]
6690 GOTO 6600
6700 REM " --- Done with issues"
6790 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9930)
9930 NEXT X
9950 EXIT 
9999 END

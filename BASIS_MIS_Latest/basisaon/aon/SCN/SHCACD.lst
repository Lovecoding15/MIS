0010 REM "SHC - UPS Freight Calculation (International Air)"
0020 REM "Program SHCACD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Feb97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Feb97 CAJ: Modified for 1997 SpeedShip Update"
0040 REM 
0080 SETESC 9000
0085 SETERR 9000
0090 ENTER A0$,A1$,W0$,W1$,W[ALL],CHARGE,ZIPLENGTH,ZIPCODE$,ZONE$,SERVICE$,RESERVED2$,RATEFILE$,ZONEFILE$,SPEEDSHIP$
0100 REM " --- Open/Lock Files"
0110 LET FILES=1
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]=RATEFILE$
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET SHM22_DEV=CHANNELS[1]
0200 REM " --- IOLIST's"
0210 SHM22A: IOLIST X$,F[ALL]
0400 REM " --- Parameters"
0410 LET FIRM$=A0$(1,2),CARRIER$=W1$(11,1),LETTER$=W1$(127,1),M=36
0500 REM " --- Initializations"
0510 DIM F[M],Z[M]
0550 LET WEIGHT=W[0],L=19,CHARGE=-1
0600 REM " --- Define valid zones"
0610 LET Z[0]=80,Z[1]=84,Z[2]=900,Z[11]=74
0620 FOR X=1 TO 8
0630 LET Z[X+2]=900+X,Z[X+11]=600+X
0640 NEXT X
1000 REM " --- Valid zone?"
1010 LET ZONE=NUM(ZONE$,ERR=4000)
1020 FOR X=0 TO L
1030 IF ZONE=Z[X] THEN EXITTO 2000
1040 NEXT X
1090 GOTO 9900
2000 REM " --- Retrieve shipping rate for weight/zone"
2010 LET ZONE=X,K$=FIRM$+CARRIER$+STR(WEIGHT:"00000")
2020 IF LETTER$="L" THEN LET K$=FIRM$+CARRIER$+"LETTR"
2030 READ (SHM22_DEV,KEY=K$,DOM=2100)IOL=SHM22A
2090 GOTO 2200
2100 REM " --- Round up for weights over 50 lbs"
2110 IF LETTER$="L" THEN GOTO 4000
2120 IF WEIGHT<51 THEN GOTO 4000
2130 LET K$=KEY(SHM22_DEV,END=4000)
2140 IF POS(FIRM$+CARRIER$=K$)=0 THEN GOTO 4000
2150 FIND (SHM22_DEV,KEY=K$,DOM=4000)IOL=SHM22A
2200 REM " --- Assign charge"
2210 LET CHARGE=F[ZONE]
4000 REM " --- All done"
4090 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9910 CLOSE (SHM22_DEV,ERR=9950)
9950 EXIT 
9999 END

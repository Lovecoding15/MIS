0010 REM "ARR - CUSTOMER LEDGER CARDS"
0020 REM "Program ARR.9A"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "19Jun96 WGH: Can't enter alpha Customer numbers (2020,2050-2055,2120,2140-2150)"
0032 REM "15Aug96 WGH: Ending Customer field always returns LAST (2140)"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARM-01",FILES$[2]="ART-01",FILES$[3]="ART-11"
0130 LET FILES$[4]="SYS-01"
0160 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET ARM01_DEV=CHANNELS[1],ART01_DEV=CHANNELS[2],ART11_DEV=CHANNELS[3]
0185 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLists
0230 ARM01A: IOLIST *,X0$
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 GLS01A: IOLIST X$,G1$,G2$,X$,G4$,G5$,G6$,G7$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$,WHEN$=F0$(7,3)
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0440 FIND (SYS01_DEV,KEY=N0$+"GL00",DOM=9800)IOL=GLS01A
0450 DIM P[1],G[4]
0460 FOR X=0 TO 4
0470 LET G[X]=NUM(G2$(X*2+1,2),ERR=0480)
0480 NEXT X
0485 LET G[2]=FNYY_YEAR(G2$(5,2))
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2))
0500 REM " --- Init Data"
0510 DIM J$(80,"-")
0530 LET K0=1,K9=6
0540 LET P7$=F0$(7,3)
0550 LET M0=LEN(M0$)
0700 REM " --- Background"
0710 FOR X=4 TO 10 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(20,3),"Beginning Customer:",@(23,5),"Ending Customer:",@(26,7),"Through Date:",'SF',
2000 REM " --- Beginning Customer"
2010 LET I0=0
2020 LET V0$="M",V1$="CE",V2$=P5$,V3$=M0$,V4$="<F1>=First   <F3>=Lookup",V0=6,V1=40,V2=3
2040 GOSUB 7000
2045 IF V3=4 THEN GOTO 9900
2047 IF V3=1 THEN LET V$=""
2048 IF V3=3 THEN CALL "SYC.LC",V$
2050 IF NUM(V$,ERR=2060)=0 THEN PRINT @(V1,V2),"First",'CL',; LET P5$=""; GOTO 2090
2060 FIND (ARM01_DEV,KEY=N0$+V$,DOM=2000)IOL=ARM01A
2070 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$),@(V1+M0+1,V2),X0$(1,30),
2080 LET P5$=V$(1,P[0])
2090 GOTO 3900
2100 REM " --- Ending Customer"
2120 LET V0$="M",V1$="CE",V2$=P6$,V3$=M0$,V4$="<F1>=Last   <F3>=Lookup",V0=6,V1=40,V2=5
2130 GOSUB 7000
2135 IF V3=3 THEN CALL "SYC.LC",V$
2138 IF V3=1 THEN LET V$=""
2140 IF NUM(V$,ERR=2155)=0 THEN PRINT @(V1,V2),"Last",'CL',; LET P6$=""; GOTO 2190
2155 IF V$<P5$ THEN GOTO 2100
2160 FIND (ARM01_DEV,KEY=N0$+V$,DOM=2100)IOL=ARM01A
2170 PRINT @(V1,V2),FNF$(V$(1,P[0]),M0$),@(V1+M0+1,V2),X0$(1,30),
2180 LET P6$=V$(1,P[0])
2190 GOTO 3900
2200 REM " --- Through Date
2210 LET V0$="D",V1$="RC",V2$=P7$,V3$="",V4$="Print Ledger Cards Through This Date",V0=3,V1=40,V2=7
2240 GOSUB 7000
2280 LET P7$=V$
2290 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,3990
3990 LET I0=K0
4000 REM " --- Options"
4020 LET V0$="Y",V1$="CE",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N)? ",V0=1,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF V3=4 THEN GOTO 9900
4050 ON POS(V$="YN") GOTO 4000,4060,2000
4090 RUN "ARR.9B"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "SHC - Package Detail Lookup And Inquiry"
0020 REM "Program SHC.LD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "05Nov97 JWK: On History display, only show detail for same date as header (1425)
0032 REM "06Feb98 JWK: Error 47 because of misdirected DOM (670)
0033 REM "13Jan99 JAL: Skipping display of last package on multi-page displays (560)"
0040 REM 
0041 REM "TYPE$      : L=Lookup/I=Inquiry (input)"
0042 REM "KEYVAL$    : Shipment ID (input)"
0054 REM "CHANNELS[N]: Channel Numbers (input)"
0055 REM "             [0] = ARM-01/SHM-02   [3] = SHE-02/SHT-02"
0056 REM "             [1] = SHM-10          [4] = SHE-04/SHT-04"
0057 REM "             [2] = SHE-01/SHT-01   [5] = SYS-01"
0060 REM "PARAMS$[n] : SpeedShip Parameters (input)"
0061 REM "             [0] = Firm ID            [2] = Customer Input Mask"
0062 REM "             [1] = Parameter string   [3] = Customer Output Mask"
0064 REM "CUSTLEN    : Customer number length (input)"
0065 REM "OPTIONS$[n]: Inventory Item Information (input/returned)"
0066 REM "             [0] = Sequence              [7] = Ending Order"
0067 REM "             [1] = Detail Level          [8] = Beginning PO"
0068 REM "             [2] = Beginning date        [9] = Ending PO"
0069 REM "             [3] = Ending date          [10] = Carrier"
0070 REM "             [4] = Beginning customer   [11] = Beginning Service"
0071 REM "             [5] = Ending customer      [12] = Ending Service"
0072 REM "             [6] = Beginning order"
0075 REM "SELECTION$: User selection (returned)"
0077 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER TYPE$,KEYVAL$,CHANNELS[ALL],PARAMS$[ALL],CUSTLEN,OPTIONS$[ALL],SELECTION$
0100 REM " --- Open/Lock Files"
0110 LET ARM01_DEV=CHANNELS[0],SHM10_DEV=CHANNELS[1],SHE01_DEV=CHANNELS[2]
0120 LET SHE02_DEV=CHANNELS[3],SHT04_DEV=CHANNELS[4],SYS01_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 SHE01A: IOLIST A0$,A1$,A2$,A[ALL]
0220 ARM01A: IOLIST B0$(1),B1$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 SHE02A: IOLIST W0$,W1$(1),W[ALL]
0400 REM " --- Parameters"
0405 DIM A[8],P[2],SPEED$(128)
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0420 IF PAGES<3 THEN GOTO 9900
0430 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0440 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0445 LET I1$=PARAMS$[1],M0$=PARAMS$[3],A0$=KEYVAL$
0450 LET N0$=F0$(16,2),SPEEDSEARCH$="N",P[0]=CUSTLEN,M0=LEN(M0$)
0460 FIND (SHE01_DEV,KEY=A0$,DOM=9900)IOL=SHE01A
0480 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=0500)
0490 LET N0$=SPEED$(1,2),SPEEDSEARCH$="Y"
0500 REM " --- Initializations"
0510 DIM B0$(8),B1$(30),W1$(128),W[20]
0540 LET WIDTH=80,HEIGHT=12,WIN_X=0,WIN_Y=4,ORDERED$=""
0550 IF TYPE$="I" THEN LET WIN_Y=3,HEIGHT=20
0560 LET MAX_ROW=HEIGHT-9
0570 IF FNP$(A2$(7,6))<>"" THEN LET ORDERED$=FNB6$(A2$(7,6))
0580 LET M1$="###0.00",M2$="#####0"
0600 REM " --- Window arrays"
0610 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0615 LET HEADING$(2)="Pkg Service Zone Weight Decl Val COD Amt O/S Haz Ack"
0620 LET HEADING$(55)="Freight   Add'l   Total"
0625 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0630 LET TITLE$="Package Lookup"
0635 IF TYPE$="I" THEN LET TITLE$="Package Detail"
0650 REM " --- Read customer"
0660 LET B0$(1)=N0$+A1$(1,6),B1$(1)="",CUSTOMER$=B0$(3,P[0])
0670 FIND (ARM01_DEV,KEY=B0$,DOM=0671)IOL=ARM01A
0680 IF I1$(1,1)="Y" THEN LET CUSTOMER$=FNF$(CUSTOMER$,M0$)
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 CALL "SYC.WC",1,0,WIDTH-2,1,0,3,0
0730 PRINT 'SB',@(1,0),"Customer",@(20),"Shipment Ordered  Shipped",@(47),"Customer PO         Total Pkgs",@(1,1),CUSTOMER$," ",B1$(1,30),@(20,2),A0$(3,7),"  ",ORDERED$," ",FNB6$(A2$(1,6)),@(47),A2$(13,10),@(65),A[0]+A[1]:M1$,@(73),A[2]:"###0",'SF',
0790 PRINT @(0,4),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Range parameters"
0810 LET SEQUENCE$=OPTIONS$[0],LEVEL$=OPTIONS$[1],BEGDATE$=OPTIONS$[2]
0820 LET ENDDATE$=OPTIONS$[3],BEGCUST$=OPTIONS$[4],ENDCUST$=OPTIONS$[5]
0830 LET BEGSHIP$=OPTIONS$[6],ENDSHIP$=OPTIONS$[7],BEGPO$=OPTIONS$[8]
0840 LET ENDPO$=OPTIONS$[9],CARRIER$=OPTIONS$[10],BEGSERV$=OPTIONS$[11]
0850 LET ENDSERV$=OPTIONS$[12],START$=BEGSHIP$
0860 IF SEQUENCE$="C" THEN LET START$=BEGCUST$
0875 IF SEQUENCE$="D" THEN LET START$=BEGDATE$
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,2]
1020 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$=""
1100 REM " --- Shipment Number"
1110 READ (SHE02_DEV,KEY=A0$,DOM=1400)
1400 REM " --- Read next record"
1410 LET K$=KEY(SHE02_DEV,END=3000)
1420 IF POS(A0$(1,17)=K$)<>1 THEN GOTO 3000
1425 IF LEN(K$)>20 THEN IF K$(21,6)<>A0$(21,6) THEN READ (SHE02_DEV); GOTO 1400
1430 LET X$=K$(3)
1900 REM " --- Read record"
1910 LET LOCKED=0
1920 READ (SHE02_DEV,KEY=K$,ERR=1950)IOL=SHE02A
1940 GOTO 2000
1950 REM " --- Error reading record"
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1980 LET LOCKED=1
2000 REM " --- Display page?"
2010 IF L+1<MAX_ROW THEN GOTO 2100
2020 LET XMODE=34
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 1000
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2100 REM " --- Range tests"
2110 IF CARRIER$<>"" THEN IF W1$(11,1)<>CARRIER$ THEN GOTO 2900
2120 IF BEGSERV$<>"" THEN IF W1$(4,7)<BEGSERV$ THEN GOTO 2900
2130 IF ENDSERV$<>"" THEN IF W1$(4,7)>ENDSERV$ THEN GOTO 2900
2200 REM " --- Format fields"
2210 LET ZONE$=FNP$(W1$(1,3)),WEIGHT$="",OS$="No ",HAZ$="No ",ACK$="No "
2220 IF W[0]<>0 THEN LET WEIGHT$=STR(W[0]:M2$)
2230 LET ZONE$="   "+ZONE$,ZONE$=ZONE$(LEN(ZONE$)-2)
2240 IF W1$(16,1)="Y" THEN LET OS$="Yes"
2250 IF W1$(17,1)="Y" THEN LET ACK$="Yes"
2260 IF W1$(19,1)="Y" THEN LET HAZ$="Yes"
2270 LET OTHER=W[3]+W[5]+W[6]+W[7]+W[8]+W[9]+W[11]+W[12]+W[13]+W[15]+W[16]
2280 LET TOTAL=W[4]+OTHER
2700 REM " --- Lookup window record"
2710 DIM G$(WIDTH-2)
2720 IF LOCKED THEN GOTO 2770
2730 LET G$(2)=W0$(18,3)+" "+W1$(4,7)+"  "+ZONE$+" "+WEIGHT$
2740 LET G$(27)=STR(W[1]:M1$)+" "+STR(W[2]:M1$)+" "+OS$+" "+HAZ$+" "+ACK$
2750 LET G$(55)=STR(W[4]:M1$)+" "+STR(OTHER:M1$)+" "+STR(TOTAL:M1$)
2760 GOTO 2780
2770 LET G$(2)=W0$(18,3)+" Record In Use"
2780 LET LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=K$
2790 LET X0=X0+1,X1=X1+1,L=L+1
2800 REM " --- Second line of window"
2810 LET G$(1)="",G$(6)="Memo: "+W1$(29,24)
2820 LET G$(40)="Tracking #: "+W1$(108,18)
2880 LET LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=K$
2890 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2910 IF LOCKED=0 THEN GOTO 1400
2920 LET LOCKED=0
2930 IF PREFIX$<>"" AND PREFIX$<>"*" THEN GOTO 1400
2940 LET K$=K$(1,LEN(K$)-1)+CHR(ASC(K$(LEN(K$),1))+1)
2950 READ (SHE02_DEV,KEY=K$,DOM=1400,END=3000)IOL=SHE02A
2990 GOTO 1400
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3035 LET MESSAGE$[0]="No Packages Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page"
3510 LET XMODE=34,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
8000 REM " --- Functions"
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8070 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to calling program"
9910 IF FKEY=4 THEN LET SELECTION$="END"
9920 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9930 IF SELECTION$="END" THEN LET SELECTION$=""
9940 IF SELECTION$="PRIOR" AND SPEEDSEARCH$="N" THEN LET SELECTION$=""
9950 EXIT 
9999 END

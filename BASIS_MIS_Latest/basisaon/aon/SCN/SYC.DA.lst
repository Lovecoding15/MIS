0010 REM "SYC - Open Database Files"
0020 REM "Program SYC.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "23Sep96 WGH: Warn when Fiscal Calendar is not setup (2140-2150,2240-2280)"
0040 REM 
0042 REM "ACTION     : 0=Open files using relative channel number"
0044 REM "             1=Open files using UNT"
0046 REM "BEGFILE    : First file to open (normally 1)"
0048 REM "ENDFILE    : Last file to open"
0050 REM "FILES$[n]  : List of files to be opened (1 per element)"
0052 REM "OPTIONS$[n]: Open options by file"
0054 REM "             C=Clear after open"
0055 REM "             F=Unnecessary file (do not create)"
0056 REM "             L=Lock after open"
0057 REM "             M=Make if not found regardless of system installation"
0058 REM "             R=Required file (do not create)"
0060 REM "CHANNELS[n]: Returned. Channel that FILES$[x] was opened on"
0065 REM "BATCH      : Returned. Batch number (if applicable)"
0070 REM "STATUS     : Returned. (0=No error)"
0075 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER ACTION,BEGFILE,ENDFILE,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0200 REM " --- IOLIST's"
0210 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0500 REM " --- Initializations"
0550 LET OPENED7=0,STATUS=0,BATCH=0,PATH$=""
0600 REM " --- Batched Process?"
0610 LET PROCESS$=STBL("!PROCESS",ERR=0700),BATCH=NUM(PROCESS$(13,3))
0630 OPEN (7,ERR=0640)FID(0)
0635 LET OPENED7=1
0640 LET SYM02_DEV=UNT
0645 OPEN (SYM02_DEV)"SYM-02"
0650 LET SYM09_DEV=UNT
0655 OPEN (SYM09_DEV)"SYM-09"
0660 LET SYM39_DEV=UNT
0665 OPEN (SYM39_DEV)"SYM-39"
0670 LET SYM49_DEV=UNT
0675 OPEN (SYM49_DEV)"SYM-49"
0680 LET SYM59_DEV=UNT
0685 OPEN (SYM59_DEV)"SYM-59"
0690 IF OPENED7 THEN CLOSE (7); LET OPENED7=0
0800 REM " --- Edit arguments"
0810 IF BEGFILE>ENDFILE THEN GOTO 9800
1000 REM " --- Open requested files"
1010 FOR X=BEGFILE TO ENDFILE
1020 LET CHANNELS[X]=0,BASENAME$=FNP$(FILES$[X])
1030 LET PATHNAME$=PATH$+BASENAME$,BASEPATH$=PATHNAME$
1040 IF BASENAME$="" THEN GOTO 2900
1050 IF ACTION>0 THEN GOTO 1100
1060 LET CHANNEL=X
1090 GOTO 1200
1100 REM " --- Open using UNT"
1110 LET CHANNEL=UNT
1120 IF CHANNEL<>7 THEN GOTO 1200
1130 OPEN (CHANNEL)FID(0)
1140 LET OPENED7=1
1190 GOTO 1100
1200 REM " --- Batched File?"
1210 LET BATCH$=""
1220 IF PROCESS$="" THEN GOTO 1500
1230 DIM SYM39_K$(16)
1240 LET SYM39_K$(1)=PROCESS$(1,10)+FILES$[X]
1250 FIND (SYM39_DEV,KEY=SYM39_K$(1),DOM=1500)
1260 IF BATCH>0 THEN GOTO 1400
1300 REM " --- Generate new batch number"
1310 IF OPENED7=0 THEN GOTO 1350
1320 CLOSE (7)
1330 LET OPENED7=0
1350 CALL "SYC.BA",SYM02_DEV,SYM09_DEV,SYM49_DEV,SYM59_DEV,STATUS
1360 IF STATUS THEN EXITTO 9800
1370 LET PROCESS$=STBL("!PROCESS",ERR=1380),BATCH=NUM(PROCESS$(13,3))
1380 IF BATCH=0 THEN EXITTO 9800
1400 REM " --- Add batch number to file name"
1420 LET BATCH$="."+STR(BATCH:"000")
1440 IF POS(BATCH$=BASENAME$)=0 THEN LET BASENAME$=BASENAME$+BATCH$
1460 IF POS(BATCH$=PATHNAME$)=0 THEN LET PATHNAME$=PATHNAME$+BATCH$
1500 REM " --- Attempt to open file"
1510 OPEN (CHANNEL,ERR=1600)PATHNAME$
1520 LET CHANNELS[X]=CHANNEL,FILES$[X]=BASENAME$
1590 GOTO 2900
1600 REM " --- Error opening file"
1610 IF ERR=12 THEN GOTO 2000
1620 IF ERR=0 THEN GOTO 2800
1630 LET ERROR$=STR(ERR)
1700 REM " --- For Error 16 (Too Many Files) Close 'Em"
1710 IF ERR<>16 THEN GOTO 1800
1720 FOR CHANNEL=CHANNELS[BEGFILE] TO CHANNELS[ENDFILE]
1730 CLOSE (CHANNEL,ERR=1740)
1740 NEXT CHANNEL
1800 REM " --- Report open error"
1810 LET TYPE=2,X$="Error "+ERROR$+" attempting to open "+BASENAME$
1890 EXITTO 4900
2000 REM " --- Error 12 (File Not Found)"
2010 IF POS("R"=OPTIONS$[X])>0 THEN EXITTO 9800
2020 IF POS("F"=OPTIONS$[X])>0 THEN GOTO 2900
2030 IF POS("M"=OPTIONS$[X])>0 THEN GOTO 2300
2100 REM " --- System installed?"
2110 DIM INFO$[20]
2120 LET MODULE$=FILES$[X](1,2),X$=MODULE$
2130 CALL "SYC.VA",MODULE$,INFO$[ALL]
2140 IF MODULE$="GL" AND INFO$[19]="N" THEN GOTO 2250
2150 IF INFO$[20]="Y" THEN GOTO 2250
2200 REM " --- System not installed. Notify user"
2210 IF FNP$(INFO$[0])<>"" THEN LET X$=FNP$(INFO$[0])
2220 IF X$="" THEN LET X$=MODULE$
2230 LET TYPE=2,X$=X$+" must be installed to perform this function."
2240 EXITTO 4900
2250 REM " --- Fiscal Calendar Setup?"
2260 IF INFO$[9]="N" AND MODULE$<>"GL" THEN GOTO 2300; REM "Fiscal Calendar not needed
2270 IF INFO$[19]="Y" THEN GOTO 2300
2280 LET TYPE=2,X$="The Fiscal Calendar must be setup."
2290 EXITTO 4900
2300 REM " --- Create file"
2310 LET FILETYPE$="",MODULE$="",KEYLEN$="",RECLEN=0,LENGTH=0
2320 IF BATCH$<>"" THEN GOTO 2500
2400 REM " --- Create non-batched file using SYM-03 definition"
2410 CALL "SYC.MA",FILETYPE$,BASEPATH$,MODULE$,KEYLEN$,RECLEN,LENGTH,STATUS
2420 IF STATUS THEN GOTO 2600
2430 IF BATCH$="" THEN GOTO 1500
2500 REM " --- Create new batch file using non-batched template"
2510 REM " --- STATUS=912 indicates missing template"
2520 CALL "SYC.ML",PATHNAME$,BASEPATH$,STATUS
2530 IF STATUS=912 THEN GOTO 2400
2550 IF STATUS=0 THEN GOTO 1500
2600 REM " --- Couldn't create file"
2610 GOSUB 6000
2620 LET TYPE=1,X$="Unable to define the "+DESCRIPTION$,STATUS=12
2690 EXITTO 4900
2800 REM " --- Error 0 (File Locked)"
2810 GOSUB 6000
2820 LET TYPE=2,X$="The "+DESCRIPTION$+" is locked by another user"
2890 EXITTO 4900
2900 REM " --- End of open loop"
2990 NEXT X
3000 REM " --- Process options by file"
3010 LET BATCH$=""
3020 FOR X=BEGFILE TO ENDFILE
3030 LET BASENAME$=FNP$(FILES$[X])
3040 IF FNP$(FILES$[X])="" THEN GOTO 3900
3050 IF OPTIONS$[X]="" THEN GOTO 3900
3100 REM " --- Clear file (after we make sure nobody else is using it)"
3110 IF POS("C"=OPTIONS$[X])=0 THEN GOTO 3200
3120 LET CHANNEL=CHANNELS[X]
3130 LOCK (CHANNEL,ERR=3800)
3140 CALL "SYC.FA",CHANNEL
3200 REM " --- Lock file"
3210 IF POS("L"=OPTIONS$[X])=0 THEN GOTO 3900
3220 LOCK (CHANNELS[X],ERR=3800)
3290 GOTO 3900
3800 REM " --- Error 0 - Can't lock or clear file"
3810 GOSUB 6000
3820 LET TYPE=2,X$="Can't lock the "+DESCRIPTION$+" (Currently in use)"
3890 EXITTO 4900
3900 REM " --- End of options loop"
3990 NEXT X
4000 REM " --- All done"
4090 GOTO 9900
4900 REM " --- Display error message and exit"
4910 DIM MESSAGE$[1]
4920 LET MESSAGE$[0]=X$,LENGTH=LEN(MESSAGE$[0]),X=MAX(INT((LENGTH-35)/2),0)
4930 LET MESSAGE$[1]=FILL(X)+"Press <Enter> To Continue"
4940 CALL "SYC.XA",TYPE,MESSAGE$[ALL],1,22,-1,V$,V3
4990 GOTO 9800
6000 REM " --- Return file description"
6010 DIM A0$(6),A1$(64),A2$(30),A[2]
6020 LET SYM03_DEV=UNT,A0$(1)=FILES$[X],A1$(1)="(Unknown)"
6030 OPEN (SYM03_DEV)"SYM-03"
6040 FIND (SYM03_DEV,KEY=A0$,DOM=6050)IOL=SYM03A
6050 LET DESCRIPTION$=BASENAME$+" "+FNP$(A1$(1,30))+" file"
6060 CLOSE (SYM03_DEV,ERR=6070)
6090 RETURN 
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9800
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Return STATUS = ERR"
9810 LET STATUS=ERR
9820 IF STATUS=0 THEN LET STATUS=999
9900 REM " --- Return to Calling Program"
9910 IF OPENED7 THEN CLOSE (7,ERR=9920)
9920 IF PROCESS$="" THEN GOTO 9950
9925 CLOSE (SYM02_DEV,ERR=9930)
9930 CLOSE (SYM09_DEV,ERR=9935)
9935 CLOSE (SYM39_DEV,ERR=9940)
9940 CLOSE (SYM49_DEV,ERR=9945)
9945 CLOSE (SYM59_DEV,ERR=9950)
9950 EXIT 
9999 END

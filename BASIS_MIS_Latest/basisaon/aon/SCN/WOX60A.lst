0010 REM "WOX Convert WO to 6.0"
0020 REM "Program WOX60A"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "21May96 Caj: Diplay problems: 1)File names overwrote 'convert' label 2)Data too long for notification box (940,950,1020,1090,1210,1290,1610,1790,1905,1950,2105,2160,2305,2355,2505,2540,2605,2655,2805,2870,3005,3055,3205,3245,3280,3320,3610,3695,3805,3905,6810)
0032 REM "25Jun96 WGH: WOM-06 is not required, so may be missing (3657)"
0033 REM "17Jul96 JJD: Infinite loop reading empty WOS-10 (1140)"
0034 REM "21May99 WGH: Remove obsolete file WOT-02 (143,304-306,3800-3975)"
0040 REM 
0070 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files "   
0110 LET FILES=33
0115 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="WOE-01",FILES$[3]="WOS-10"
0122 LET FILES$[4]="WOE-02",FILES$[5]="WOE-22",FILES$[6]="WOE-32"
0124 LET FILES$[7]="WOE-06",FILES$[8]="WOE-12",FILES$[9]="WOE-42"
0126 LET FILES$[10]="WOE-13",FILES$[11]="WOE-23",FILES$[12]="WOE-14"
0128 LET FILES$[13]="WOE-15",FILES$[14]="WOE-25",FILES$[15]="WOT-01"
0130 LET FILES$[16]="WOT-21",FILES$[17]="WOT-31",FILES$[18]="WOT-03"
0132 LET FILES$[19]="WOT-23",FILES$[20]="WOT-33",FILES$[21]="WOT-11"
0134 LET FILES$[22]="WOT-12",FILES$[23]="      ",FILES$[24]="         "
0135 LET FILES$[25]="WOM-06",FILES$[26]="WOM06.TMP",OPTIONS$[24]="F"
0136 LET FILES$[27]="WOE-04",FILES$[28]="WOE04.TMP",OPTIONS$[28]="F"
0138 LET FILES$[29]="      ",FILES$[30]="         "
0143 LET FILES$[31]="",FILES$[32]=""
0144 LET FILES$[33]="WOM-07"
0145 CALL "SYC.DA",1,1,22,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],WOE01_DEV=CHANNELS[2],WOS10_DEV=CHANNELS[3]
0162 LET WOE02_DEV=CHANNELS[4],WOE22_DEV=CHANNELS[5],WOE32_DEV=CHANNELS[6]
0164 LET WOE06_DEV=CHANNELS[7],WOE12_DEV=CHANNELS[8],WOE42_DEV=CHANNELS[9]
0166 LET WOE13_DEV=CHANNELS[10],WOE23_DEV=CHANNELS[11],WOE14_DEV=CHANNELS[12]
0168 LET WOE15_DEV=CHANNELS[13],WOE25_DEV=CHANNELS[14],WOT01_DEV=CHANNELS[15]
0170 LET WOT21_DEV=CHANNELS[16],WOT31_DEV=CHANNELS[17],WOT03_DEV=CHANNELS[18]
0172 LET WOT23_DEV=CHANNELS[19],WOT33_DEV=CHANNELS[20],WOT11_DEV=CHANNELS[21]
0174 LET WOT12_DEV=CHANNELS[22]
0200 REM " --- IOLISTS      
0210 SYS01T: IOLIST X$,F0$,X1$,X2$,F4$,F5$,F6$
0220 WOE01A: IOLIST A0$(1),A1$(1),A[ALL]
0230 WOS10N: IOLIST C0$,C[ALL]
0240 IVS01A: IOLIST *,*,I2$(1)
0256 WOE02A: IOLIST A0$(1),A1$(1),A[ALL]
0257 WOE22A: IOLIST B0$(1),B1$(1),B[ALL]
0258 WOE32A: IOLIST C0$(1),C1$(1),C[ALL]
0260 WOE04A: IOLIST A0$(1)
0262 WOE06A: IOLIST A0$(1),A1$(1),A[ALL]
0264 WOE12B: IOLIST A0$,A1$,A[ALL]
0266 WOE42A: IOLIST A0$,A1$,A[ALL]
0267 WOE13A: IOLIST C0$,C1$(1)
0268 WOE13B: IOLIST A0$,A1$(1),A[ALL]
0270 WOE23A: IOLIST B0$,B1$(1),B[ALL]
0272 WOE14A: IOLIST A0$,A1$(1),A2$,A[ALL]
0274 WOE15A: IOLIST A0$,A1$(1)
0276 WOE15B: IOLIST B0$,B1$(1),B[ALL]
0278 WOE25A: IOLIST C0$,C1$(1),C[ALL]
0280 WOT01B: IOLIST A0$,A1$(1),A[ALL]
0282 WOT21A: IOLIST B0$,B1$(1),B[ALL]
0284 WOT01C: IOLIST C0$,C1$,C[ALL]
0286 WOT31A: IOLIST C0$,C1$,C[ALL]
0288 WOT03B: IOLIST A0$,A1$(1),A[ALL]
0290 WOT23A: IOLIST B0$,B1$(1),B[ALL]
0292 WOT03C: IOLIST C0$,C1$,C[ALL]
0294 WOT33A: IOLIST C0$,C1$,C[ALL]
0296 WOT11A: IOLIST A0$,A1$,A2$(1),A[ALL]
0298 WOT12A: IOLIST A0$,A1$,A2$(1),A[ALL]
0302 WOM06A: IOLIST A0$
0310 WOM07A: IOLIST K$
0390 WOE01B: IOLIST A[ALL]
0400 REM " --- Parameters      
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=1200)IOL=SYS01T
0420 LET N1$=F4$,N2$="WO",N3$=F4$
0500 REM " --- Initialize Data 
0510 PRECISION 14
0800 REM " --- Display Warning"
0810 CALL "SYC60A",N2$,SYS01_DEV,STATUS
0820 IF STATUS THEN GOTO 9900
0900 REM " --- Notification Box     
0910 CALL "SYC.NB","Removing",7,COLUMN
0920 GOSUB ERASE_CROSS_REFERENCE
0940 LET DISPLEN=37
0950 CALL "SYC.NB","Processing",DISPLEN,COLUMN
1000 REM " --- Initialize For WOE-01 Conversion 
1020 PRINT @(40,3),"WOE-01 Work Order Master Conversion "
1030 REM " --- Create WOS-10 "N" Record, Retain all WOE-01 Records "
1040 READ_WOE01: 
1045 DIM A0$(75),A1$(141),A[11],C[0]
1050 LET K$=KEY(WOE01_DEV,END=DONE_WOE01)
1055 IF POS(N0$=K$)<>1 THEN GOSUB NEW_COMPANY
1056 IF IV_PARAM$="NO" THEN REMOVE (WOE01_DEV,KEY=K$); GOTO 1200
1060 IF LEN(K$)=2 THEN GOTO READ_WOS10
1070 READ (WOE01_DEV)IOL=WOE01A
1080 LET A1$(56,20)=A1$(56,ITEM_LEN)
1090 PRINT @(COLUMN,11),K$+FILL(DISPLEN-LEN(K$)),
1100 WRITE (WOE01_DEV,KEY=K$)IOL=WOE01A
1110 GOTO READ_WOE01
1120 READ_WOS10: 
1125 DIM A[0],C[0]
1130 READ (WOE01_DEV)IOL=WOE01B
1150 LET C0$=N0$+"N",C[0]=A[0]
1160 IF C[0]=0 THEN LET C[0]=1
1170 WRITE_WOS10: 
1180 WRITE (WOS10_DEV,KEY=C0$)IOL=WOS10N
1185 REMOVE (WOE01_DEV,KEY=K$)
1190 GOTO READ_WOE01
1195 DONE_WOE01: 
1200 REM " --- Convert WOE-02 "B" & "C" Records to "A" Records In Files
1205 REM " --- WOE-22 & WOE-32 "
1210 PRINT @(40,3),"WOE-02 Work Order Detail",'CL'
1215 REM " --- Initialize "
1220 DIM A0$(15),A1$(151),A[13],B0$(15),B1$(151),B[13],C0$(15),C1$(152),C[13]
1230 REM " --- Process "   
1240 READ_WOE02: 
1250 LET K$=KEY(WOE02_DEV,END=DONE_WOE02)
1260 IF POS(N0$=K$)<>1 THEN GOSUB NEW_COMPANY
1270 IF IV_PARAM$="NO" THEN REMOVE (WOE02_DEV,KEY=K$); GOTO READ_WOE02
1280 READ (WOE02_DEV)IOL=WOE02A
1290 PRINT @(COLUMN,11),K$+FILL(DISPLEN-LEN(K$)),
1300 ON POS(A0$(12,1)="ABC") GOTO READ_WOE02,READ_WOE02,E02_B,E02_C
1310 E02_B: 
1320 LET B0$(1)=A0$(1)
1330 LET B0$(12,1)="B"
1340 LET B1$(1)=A1$(1)
1350 LET B[ALL]=A[ALL]
1360 WRITE (WOE22_DEV,KEY=B0$(1,15))IOL=WOE22A
1370 REMOVE (WOE02_DEV,KEY=K$)
1380 GOTO READ_WOE02
1390 E02_C: 
1400 LET C0$(1)=A0$(1)
1410 LET C0$(12,1)="C"
1420 LET C1$(1,49)=A1$(1,49)
1430 IF A1$(50,2)<>"  " THEN LET C1$(50,3)="0"+A1$(50,2)
1440 LET C1$(53,3)=A1$(53,3)
1450 LET C1$(56,1)=A1$(52,1)
1460 LET C1$(57,34)=A1$(56,34)
1470 LET C1$(91,1)=A1$(91,1)
1480 LET C1$(92,60)=A1$(92,60)
1490 LET C[ALL]=A[ALL]
1500 WRITE (WOE32_DEV,KEY=C0$(1,15))IOL=WOE32A
1510 REMOVE (WOE02_DEV,KEY=K$)
1520 GOTO READ_WOE02
1530 DONE_WOE02: 
1600 REM " --- Convert WOE-04 "
1610 PRINT @(40,3),"WOE-04 Work Order Transactions",'CL'
1620 LET NUMBER=1
1630 DIM FROM$[NUMBER],TMPFILE$[NUMBER],FLAG$[NUMBER]
1640 LET FROM$[1]="WOE-04",TMPFILE$[1]="WOE04.TMP"
1650 CALL "SYC.RN",FROM$[ALL],TMPFILE$[ALL],FLAG$[ALL],NUMBER,STATUS
1660 IF STATUS THEN GOTO 9900
1670 CALL "SYC.DA",1,27,28,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
1680 IF STATUS THEN GOTO 9900
1690 LET WOE04_DEV=CHANNELS[27],WOE04TMP_DEV=CHANNELS[28]
1700 IF WOE04TMP_DEV=0 THEN GOTO DONE_WOE04
1710 REM " --- Convert WOE-04 Transaction File "
1715 REM " --- Initialize "
1720 DIM A0$(12)
1730 REM " --- Process Records "
1740 READ_WOE04: 
1750 LET K$=KEY(WOE04TMP_DEV,END=DONE_WOE04)
1760 IF POS(N0$=K$)<>1 THEN GOSUB NEW_COMPANY
1770 IF IV_PARAM$="NO" THEN READ (WOE04TMP_DEV); GOTO READ_WOE04
1780 READ (WOE04TMP_DEV)
1790 PRINT @(COLUMN,11),K$+FILL(DISPLEN-LEN(K$)),
1800 LET A0$(1)=K$
1810 WRITE (WOE04_DEV,KEY=A0$(1))IOL=WOE04A
1820 GOTO READ_WOE04
1830 DONE_WOE04: 
1840 CLOSE (WOE04TMP_DEV,ERR=1850)
1850 ERASE "WOE04.TMP",ERR=1900
1900 REM " --- Convert WOE-06 Lot/Serial Numbers "
1905 PRINT @(40,3),"WOE-06 Work Order Lot/Serial Numbers "
1910 REM " --- Initialize "
1915 DIM A0$(14),A1$(56),A[5]
1920 REM " --- Process "
1930 READ_WOE06: 
1940 LET K$=KEY(WOE06_DEV,END=DONE_WOE06)
1950 PRINT @(COLUMN,11),K$+FILL(DISPLEN-LEN(K$)),
1960 IF POS(N0$=K$)<>1 THEN GOSUB NEW_COMPANY
1970 IF IV_PARAM$="NO" THEN REMOVE (WOE06_DEV,KEY=K$); GOTO READ_WOE06
1980 READ (WOE06_DEV)IOL=WOE06A
1990 LET A1$(37,20)=A1$(37,LOTSER_LEN)
2000 WRITE (WOE06_DEV,KEY=K$)IOL=WOE06A
2010 GOTO READ_WOE06
2020 DONE_WOE06: 
2100 REM " --- Convert WOE-12 "
2105 PRINT @(40,3),"WOE-12 Subcontract Adjustments ",'CL'
2110 REM " --- Initlalize "
2115 DIM A0$(18),A1$(30),A[11]
2120 REM " --- Process "
2125 READ_WOE12: 
2130 LET K$=KEY(WOE12_DEV,END=DONE_WOE12)
2140 IF POS(N0$=K$)<>1 THEN GOSUB NEW_COMPANY
2150 IF IV_PARAM$="NO" OR LEN(K$)<15 THEN REMOVE (WOE12_DEV,KEY=K$); GOTO READ_WOE12
2160 PRINT @(COLUMN,11),K$+FILL(DISPLEN-LEN(K$)),
2170 ON POS(K$(15,1)="S") GOTO NEXT_E12_RECORD,E12_S
2180 NEXT_E12_RECORD: 
2190 READ (WOE12_DEV)
2200 GOTO READ_WOE12
2210 E12_S: 
2220 READ (WOE12_DEV)IOL=WOE12B
2230 WRITE (WOE42_DEV,KEY=K$)IOL=WOE42A
2240 REMOVE (WOE12_DEV,KEY=K$)
2250 GOTO READ_WOE12
2260 DONE_WOE12: 
2300 REM " --- Convert WOE-13 "B" Records To WOE-23 "
2305 PRINT @(40,3)," WOE-13 Material Commitments ",'CL'
2310 REM " --- Initialize "
2315 DIM A0$(14),A1$(40),A[4]
2320 DIM B0$(14),B1$(40),B[4]
2325 DIM C0$(14),C1$(70)
2330 REM " --- Process "
2335 READ_WOE13: 
2340 LET K$=KEY(WOE13_DEV,END=DONE_WOE13)
2345 IF POS(N0$=K$)<>1 THEN GOSUB NEW_COMPANY
2350 IF IV_PARAM$="NO" OR LEN(K$)<14 THEN REMOVE (WOE13_DEV,KEY=K$); GOTO READ_WOE13
2355 PRINT @(COLUMN,11),K$+FILL(DISPLEN-LEN(K$)),
2360 IF K$(12,3)<>"000" THEN GOTO E13_B
2365 READ (WOE13_DEV)IOL=WOE13A
2370 LET C1$(51,20)=C1$(51,ITEM_LEN)
2375 WRITE (WOE13_DEV,KEY=K$)IOL=WOE13A
2380 GOTO READ_WOE13
2385 E13_B: 
2390 READ (WOE13_DEV)IOL=WOE13B
2395 LET B0$=A0$
2400 LET B1$(1,20)=A1$(1,20)
2405 LET B1$(21,20)=A1$(21,ITEM_LEN)
2410 LET B[ALL]=A[ALL]
2415 WRITE (WOE23_DEV,KEY=B0$)IOL=WOE23A
2420 REMOVE (WOE13_DEV,KEY=K$)
2425 GOTO READ_WOE13
2430 DONE_WOE13: 
2500 REM " --- WOE-14 Conversion "
2505 PRINT @(40,3),"WOE-14 Lotted/Serialized Issues ",'CL'
2510 REM " --- Initialization "
2515 DIM A0$(17),A1$(20),A2$(20),A[1]
2520 REM " --- Process "
2525 READ_WOE14: 
2530 LET K$=KEY(WOE14_DEV,END=DONE_WOE14)
2535 IF POS(N0$=K$)<>1 THEN GOSUB NEW_COMPANY
2540 IF IV_PARAM$="NO" THEN REMOVE (WOE14_DEV,KEY=K$); GOTO READ_WOE14
2545 PRINT @(COLUMN,11),K$+FILL(DISPLEN-LEN(K$)),
2550 READ (WOE14_DEV)IOL=WOE14A
2555 LET A1$(1,20)=A1$(1,LOTSER_LEN)
2560 WRITE (WOE14_DEV,KEY=K$)IOL=WOE14A
2565 GOTO READ_WOE14
2570 DONE_WOE14: 
2600 REM " --- WOE-15 Conversion "
2605 PRINT @(40,3),"WOE-15 Material Issues ",'CL'
2610 REM " --- Initialization "
2615 DIM A0$(14),A1$(70)
2620 DIM B0$(14),B1$(40),B[4]
2625 DIM C0$(14),C1$(40),C[4]
2630 REM " --- Process "
2635 READ_WOE15: 
2640 LET K$=KEY(WOE15_DEV,END=DONE_WOE15)
2645 IF POS(N0$=K$)<>1 THEN GOSUB NEW_COMPANY
2650 IF IV_PARAM$="NO" OR LEN(K$)<14 THEN REMOVE (WOE15_DEV,KEY=K$); GOTO READ_WOE15
2655 PRINT @(COLUMN,11),K$+FILL(DISPLEN-LEN(K$)),
2660 IF K$(12,3)<>"000" THEN GOTO E15_B
2665 READ (WOE15_DEV)IOL=WOE15A
2670 LET A1$(51,20)=A1$(51,ITEM_LEN)
2675 WRITE (WOE15_DEV,KEY=K$)IOL=WOE15A
2680 GOTO READ_WOE15
2685 E15_B: 
2690 READ (WOE15_DEV)IOL=WOE15B
2695 LET C0$=B0$
2700 LET C1$(1,20)=B1$(1,20)
2705 LET C1$(21,20)=B1$(21,ITEM_LEN)
2710 LET C[ALL]=B[ALL]
2715 WRITE (WOE25_DEV,KEY=C0$)IOL=WOE25A
2720 REMOVE (WOE15_DEV,KEY=K$)
2725 GOTO READ_WOE15
2730 DONE_WOE15: 
2800 REM " --- WOT-01 Conversion "
2805 PRINT @(40,3),"WOT-01 Open Work Order Transactions",'CL'
2810 CALL "SYC.DA",1,33,33,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
2815 IF STATUS THEN GOTO 9900
2820 LET WOM07_DEV=CHANNELS[33]
2825 REM " --- Initialize "
2830 DIM A0$(18),A1$(37),A[11]
2835 DIM B0$(18),B1$(43),B[11]
2840 DIM C0$(18),C1$(39),C[11]
2845 REM " --- Process "
2850 READ_WOT01: 
2855 LET K$=KEY(WOT01_DEV,END=DONE_WOT01)
2860 IF POS(N0$=K$)<>1 THEN GOSUB NEW_COMPANY
2865 IF IV_PARAM$="NO" OR LEN(K$)<18 THEN REMOVE (WOT01_DEV,KEY=K$); GOTO READ_WOT01
2870 PRINT @(COLUMN,11),K$+FILL(DISPLEN-LEN(K$)),
2875 WRITE (WOM07_DEV,KEY=K$)IOL=WOM07A
2880 ON POS(K$(15,1)="OMS") GOTO NEXT_T01_RECORD,NEXT_T01_RECORD,T01_M,T01_S
2885 NEXT_T01_RECORD: 
2890 READ (WOT01_DEV)
2895 GOTO READ_WOT01
2900 T01_M: 
2905 READ (WOT01_DEV)IOL=WOT01B
2910 LET B0$=A0$
2915 LET B1$(1,3)=A1$(1,3)
2920 LET B1$(20,2)=A1$(4,2)
2925 LET B1$(22,2)=A1$(16,2)
2930 LET B1$(24,20)=A1$(18,ITEM_LEN)
2935 LET B[ALL]=A[ALL]
2940 WRITE (WOT21_DEV,KEY=B0$)IOL=WOT21A
2945 REMOVE (WOT01_DEV,KEY=K$)
2950 GOTO READ_WOT01
2955 T01_S: 
2960 READ (WOT01_DEV)IOL=WOT01C
2965 WRITE (WOT31_DEV,KEY=K$)IOL=WOT31A
2970 REMOVE (WOT01_DEV,KEY=K$)
2975 GOTO READ_WOT01
2980 DONE_WOT01: 
3000 REM " --- WOT-03 Conversion "
3005 PRINT @(40,3),"WOT-03 Closed Work Order Transactions",'CL'
3010 REM " --- Initialize "
3015 DIM A0$(18),A1$(37),A[11]
3020 DIM B0$(18),B1$(43),B[11]
3025 DIM C0$(18),C1$(31),C[11]
3030 REM " --- Process "
3035 READ_WOT03: 
3040 LET K$=KEY(WOT03_DEV,END=DONE_WOT03)
3045 IF POS(N0$=K$)<>1 THEN GOSUB NEW_COMPANY
3050 IF IV_PARAM$="NO" OR LEN(K$)<18 THEN REMOVE (WOT03_DEV,KEY=K$); GOTO READ_WOT03
3055 PRINT @(COLUMN,11),K$+FILL(DISPLEN-LEN(K$)),
3060 WRITE (WOM07_DEV,KEY=K$)IOL=WOM07A
3065 ON POS(K$(15,1)="OMS") GOTO NEXT_T03_RECORD,NEXT_T03_RECORD,T03_M,T03_S
3070 NEXT_T03_RECORD: 
3075 READ (WOT03_DEV)
3080 GOTO READ_WOT03
3085 T03_M: 
3090 READ (WOT03_DEV)IOL=WOT03B
3095 LET B0$=A0$
3100 LET B1$(1,3)=A1$(1,3)
3105 LET B1$(20,2)=A1$(4,2)
3110 LET B1$(22,2)=A1$(16,2)
3115 LET B1$(24,20)=A1$(18,ITEM_LEN)
3120 LET B[ALL]=A[ALL]
3125 WRITE (WOT23_DEV,KEY=B0$)IOL=WOT23A
3130 REMOVE (WOT03_DEV,KEY=K$)
3135 GOTO READ_WOT03
3140 T03_S: 
3145 READ (WOT03_DEV)IOL=WOT03C
3150 WRITE (WOT33_DEV,KEY=K$)IOL=WOT33A
3155 REMOVE (WOT03_DEV,KEY=K$)
3160 GOTO READ_WOT03
3165 DONE_WOT03: 
3200 REM " --- WOT-11 Conversion "
3205 PRINT @(40,3),"WOT-11 Open Work Order Lot/Serial Transactions",'CL'
3210 REM " ___ Initialize "
3215 DIM A0$(21),A1$(10),A2$(20),A[2]
3220 REM " --- Process "
3225 READ_WOT11: 
3230 LET K$=KEY(WOT11_DEV,END=DONE_WOT11)
3235 IF POS(N0$=K$)<>1 THEN GOSUB NEW_COMPANY
3240 IF IV_PARAM$="NO" THEN REMOVE (WOT11_DEV,KEY=K$); GOTO READ_WOT11
3245 PRINT @(COLUMN,11),K$+FILL(DISPLEN-LEN(K$)),
3250 READ (WOT11_DEV)IOL=WOT11A
3255 LET A2$(1,20)=A2$(1,ITEM_LEN)
3260 WRITE (WOT11_DEV,KEY=K$)IOL=WOT11A
3265 GOTO READ_WOT11
3270 DONE_WOT11: 
3275 REM " --- WOT-12 Conversion "
3280 PRINT @(40,3),"WOT-12 Closed Work Order Lot/Serial Transactions",'CL'
3285 REM " ___ Initialize "
3290 DIM A0$(21),A1$(10),A2$(20),A[2]
3295 REM " --- Process "
3300 READ_WOT12: 
3305 LET K$=KEY(WOT12_DEV,END=DONE_WOT12)
3310 IF POS(N0$=K$)<>1 THEN GOSUB NEW_COMPANY
3315 IF IV_PARAM$="NO" THEN REMOVE (WOT12_DEV,KEY=K$); GOTO READ_WOT12
3320 PRINT @(COLUMN,11),K$+FILL(DISPLEN-LEN(K$)),
3325 READ (WOT12_DEV)IOL=WOT12A
3330 LET A2$(1,20)=A2$(1,ITEM_LEN)
3335 WRITE (WOT12_DEV,KEY=K$)IOL=WOT12A
3340 GOTO READ_WOT12
3345 DONE_WOT12: 
3600 REM " --- Convert WOM-06 "
3605 REM " --- Create Temporary File "
3610 PRINT @(40,3),"WOM-06 Work Order Schedule Sort",'CL'
3615 LET NUMBER=1
3620 DIM FROM$[NUMBER],TMPFILE$[NUMBER],FLAG$[NUMBER]
3625 LET FROM$[1]="WOM-06",TMPFILE$[1]="WOM06.TMP"
3630 CALL "SYC.RN",FROM$[ALL],TMPFILE$[ALL],FLAG$[ALL],NUMBER,STATUS
3635 IF STATUS THEN GOTO 9900
3640 CALL "SYC.DA",1,25,26,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
3645 IF STATUS THEN GOTO 9900
3650 LET WOM06_DEV=CHANNELS[25],WOM06TMP_DEV=CHANNELS[26]
3655 REM " --- Initialize "
3657 IF WOM06TMP_DEV=0 THEN GOTO DONE_WOM06
3660 DIM A0$(18)
3665 REM " --- Process "        
3670 READ_WOM06: 
3675 LET K$=KEY(WOM06TMP_DEV,END=DONE_WOM06)
3680 IF POS(N0$=K$)<>1 THEN GOSUB NEW_COMPANY
3685 IF IV_PRAM$="NO" THEN READ (WOM06TMP_DEV); GOTO READ_WOM06
3690 READ (WOM06TMP_DEV)
3695 PRINT @(COLUMN,11),K$+FILL(DISPLEN-LEN(K$)),
3700 LET A0$(1)=K$
3705 WRITE (WOM06_DEV,KEY=A0$(1))IOL=WOM06A
3710 GOTO READ_WOM06
3715 DONE_WOM06: 
3720 CLOSE (WOM06TMP_DEV,ERR=3725)
3725 ERASE "WOM06.TMP",ERR=3800
3800 REM " --- Erase Obsolete WOT-02 "
3810 ERASE "WOT-02",ERR=3830
3820 GOTO 3810
6500 REM " --- Get Inventory Parameters For Current Firm"
6501 NEW_COMPANY: 
6505 DIM I2$(53)
6510 LET N0$=K$(1,2)
6525 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=NO_PARAMETERS)IOL=IVS01A
6530 LET ITEM_LEN=NUM(I2$(1,2)),VENDITEM_LEN=NUM(I2$(3,2)),LOTSER_LEN=NUM(I2$(7,2)),IV_PARAM$="YES"
6535 GOTO 6690
6540 NO_PARAMETERS: 
6545 LET IV_PARAM$="NO"
6690 RETURN 
6800 REM " --- Erase Cross Reference Files "
6805 ERASE_CROSS_REFERENCE: 
6810 PRINT @(COLUMN,11),"WOM-07",
6820 ERASE "WOM-07",ERR=6830
6825 GOTO 6890
6830 IF ERR<>12 THEN GOTO 9000
6890 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

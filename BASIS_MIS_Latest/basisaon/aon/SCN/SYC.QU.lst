0010 REM "SYC - PF Reports Display (Print to File)"
0020 REM "Program SYC.QU"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "24Mar98 KJS: Use correct text separator in DOS/Windows (105,130,140,160,260,400,420,530,5055,5060,9920)
0040 REM 
0045 REM "D_STATUS   : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n] : Standard SpeedSearch Options Array"
0055 REM "INFO$[n]   : String parameters (see SYC.QR)"
0060 REM "INFO[n]    : Numeric parameters (see SYC.QR)"
0065 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL],INFO$[ALL],INFO[ALL]
0100 REM " --- Open files"
0105 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]=INFO$[7],FILES$[2]="SYS-01"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET REPORT_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IO Lists
0260 SYS01S: IOLIST *,*,*,OS$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="S",ERR=0411)IOL=SYS01S
0500 REM " --- Initializations"
0510 LET L0=1,L9=20,P=1,L1=0,W=132,L=L0,N=1,S9=0,H0=132+LEN('LF')
0520 LET HEADER$="",PRINTER=INFO[13]
0530 IF OS$<>"U" THEN LET SEP$=$0D0A$ ELSE LET SEP$=$0A$
0550 DIM S$(H0*L9),Z$(H0),S0$(H0*2)
0700 REM " --- Background"
0710 CALL "SYC.WB",INFO$[5],0
0720 PRINT @(1,0),'CL',"Printing ",$22$,INFO$[8],$22$,
1000 REM " --- Read next record"
1010 READ RECORD(REPORT_DEV,END=4000)X$
1100 REM " --- Check for LF"
1110 IF LEN(X$)=0 THEN GOTO 1150
1120 IF X$(LEN(X$),1)<>$0A$ THEN GOTO 1150
1130 LET X$=X$(1,LEN(X$)-1)+'LF'
1140 GOTO 2000
1150 LET X=POS('LF'=X$)
1160 IF X<>0 THEN GOTO 2000
1200 REM " --- Append to current buffer"
1210 READ RECORD(REPORT_DEV,END=4000)Y$
1220 LET X$=X$+Y$
1290 GOTO 1100
2000 REM " --- Parse and store lines"
2010 IF X$="" THEN GOTO 2900
2015 LET X=POS('FF'=X$),Y=POS('LF'=X$)
2020 IF X<>1 AND Y<>1 THEN GOTO 2050
2030 LET X$=X$(4)
2040 GOTO 2400
2050 LET Z0=POS($1B$=X$)
2060 IF Z0=0 THEN GOTO 1200
2070 IF Z0<>1 THEN GOTO 2200
2100 REM " --- Print positioning"
2110 IF POS($1B4031$=X$)=1 THEN LET P=ASC(X$(4,1))+1,X$=X$(5) ELSE LET P=ASC(X$(3,1))+1,X$=X$(4)
2190 GOTO 2000
2200 REM " --- Remove mnemonics"
2210 IF P>H0 THEN LET P=H0-1
2220 IF P+Z0-1>H0 AND W1<>1 THEN LET W1=1
2230 LET Z2=POS($1B$=X$)
2240 IF W1=1 AND Z2>0 THEN LET X$=X$(1,Z0-1)+X$(Z2)
2250 LET Z1=Z0
2260 IF P+Z0-1>H0 THEN LET Z1=Z0+(H0-(P+Z0))
2270 LET Z$(P,Z1-1)=X$(1,Z1-1),X$=X$(Z0),P=P+Z1-1
2290 GOTO 2000
2400 REM " --- Parsing completed"
2410 IF L>2 AND Z$=S0$(1,H0) THEN IF S$(H0*(L-2)+1,H0*2)=S0$ THEN GOTO 2450
2420 LET S$(H0*(L-1)+1,H0)=Z$,L1=0,L=L+1
2450 DIM Z$(H0)
2460 LET P=1
2480 IF L>L9 THEN GOTO 2500
2490 GOTO 2000
2500 REM " --- Buffer full"
2510 GOSUB 5000
2590 GOTO 2000
2900 REM " --- Loop back for more of the report"
2990 GOTO 1000
4000 REM " --- All done"
4010 GOSUB 5000
4090 GOTO 9900
5000 REM " --- Buffer full or EOF"
5010 LET T$=S$
5020 FOR LINE=1 TO L9
5030 LET X=H0*(LINE-1)+1,LINE$=T$(X,W)
5040 WHILE POS($1F$>LINE$)>0
5045 LET LINE$(POS($1F$>LINE$),1)=" "
5050 WEND
5055 LET LINE$=CVS(LINE$,2)+SEP$
5060 WRITE RECORD(PRINTER)LINE$
5070 PRINT @(1,2),T$(X,INFO[7]-2),
5080 NEXT LINE
5085 LET S$(1)="",L=1
5090 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9910 CLOSE (REPORT_DEV,ERR=9911)
9920 CLOSE (SYS01_DEV,ERR=9921)
9950 EXIT 
9999 END

0010 REM "PRR - Accrual Status Report"
0020 REM "Program PRR.NA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "18Nov98 KJS: IOList wrong to find rate (220)
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="PRM-01",FILES$[2]="PRM-02",FILES$[3]="PRM-10"
0130 LET FILES$[4]="PRM-10",FILES$[5]="PRT-01",FILES$[6]="SYS-01"
0160 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET PRM01_DEV=CHANNELS[1],PRM02_DEV=CHANNELS[2],PRM10_DEV=CHANNELS[3]
0185 LET PRM10_DEV2=CHANNELS[4],PRT01_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0200 REM " --- IOLists"
0210 PRM01A: IOLIST B0$,B1$
0220 PRM02A: IOLIST X$,Y9
0230 PRM10A: IOLIST A0$,A[0],A[1],A[2],A[3]
0240 PRM10F: IOLIST X$,Y9
0250 PRT01A: IOLIST Y0$,Y[ALL],Z[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 PRS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="PR",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=PRS01A
0480 DIM P[3]
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P4$(1,2)),P[2]=FNYY_YEAR(P4$(3,2)),P[3]=NUM(P3$(7,1))
0500 REM " --- Init Data"
0505 PRECISION P[3]
0510 LET L9=59,L=L9+1,HEADINGS=2,WIDTH=132,PAGE=0,WHEN$=F0$(7,3),CLOCK$=""
0520 DIM HEADINGS$[HEADINGS]
0530 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N4$
0540 DIM T[5],Y[11],Z[4],A[3]
0550 LET M0=MAX(11,LEN(M0$)),M1=LEN(M1$),M3=LEN(M3$)
0560 LET R0$="DWBSMQA",R1$="365052026024012004001"
0950 REM " --- Open Printer"
0960 CALL "SYC.GA",7,1,"","",STATUS
0965 IF STATUS>0 THEN GOTO 9900
0970 CALL "SYC.NB","Printing",16,COLUMN
1000 REM " --- Init read"
1020 READ (PRM10_DEV,KEY=N0$+"A",DOM=1030)
1100 REM " --- Main Read"
1120 LET K0$=KEY(PRM10_DEV,END=4000)
1130 IF K0$(1,3)<>N0$+"A" THEN GOTO 4000
1140 READ (PRM10_DEV)IOL=PRM10A
1150 IF A0$(26,1)="N" THEN GOTO 1100
1160 PRINT @(COLUMN,11),A0$(6,16),
1180 LET L=L9+1,L1=0
1190 READ (PRM01_DEV,KEY=N0$,DOM=1200)
1400 REM " --- Employee"
1410 READ (PRM01_DEV,END=3000)IOL=PRM01A
1420 IF B0$(1,2)<>N0$ THEN GOTO 3000
1430 IF B1$(170,3)<>"   " THEN GOTO 1400
1440 LET X0$=B1$(1,16),X=POS("  "=X0$)
1450 IF X>1 THEN LET X0$=X0$(1,X-1)
1460 LET X0$=X0$+", "+B1$(17,1)+"."
1500 REM " --- Rate"
1510 LET Y9=0
1520 FIND (PRM02_DEV,KEY=B0$+B1$(223,2),DOM=1530)IOL=PRM02A
1540 IF Y9=0 THEN FIND (PRM10_DEV2,KEY=N0$(1,2)+"F"+B1$(223,2),DOM=1600)IOL=PRM10F
1560 IF B1$(225,1)<>"S" THEN GOTO 1600
1570 LET X=POS(B1$(220,1)=R0$)
1580 IF X>1 THEN LET Y9=Y9/(2080/NUM(R1$(3*(X-1)+1,3)))
1600 REM " --- Get Detail"
1620 READ (PRT01_DEV,KEY=B0$+A0$(3,3),DOM=1400)IOL=PRT01A
1630 LET Y0=0
1640 FOR X=1 TO 4
1660 IF POS(A0$(26,1)="FU")<>0 THEN LET Y0=Y0+Y[6+X]
1670 IF POS(A0$(26,1)="FU")=0 THEN LET Y0=Y0+Y[X]
1680 NEXT X
1690 IF A0$(27,1)="A" THEN LET Y0=Z[4]
1700 IF Z[3]=0 THEN LET Z[3]=A[3]
1800 REM " --- Special Modifications"
1810 LET Z8=0
1860 LET Z0=Z[0]+Z[1]-Y0; REM "Due=(Prior Yr Acc)+(Curr Yr Acc)-(Curr Yr Used) 
1900 PRECISION 2
1920 IF POS(A0$(26,1)="FU")>0 THEN LET X9=Y9*Z0 ELSE LET X9=Y9*1
1940 PRECISION P[3]
2000 REM " --- Print"
2010 IF Z[0]+Z[1]+Y0+Z0+X9+Z8=0 THEN GOTO 1400
2015 REM "IF Z(0)=0 THEN IF Z(1)=0 THEN IF Y0=0 THEN IF Z0=0 THEN IF X9=0 THEN IF Z8=0 THEN GOTO 01400
2020 IF L+2>L9 THEN GOSUB 5000
2040 PRINT (7)@(0),FNF$(B0$(3,P[0]),M0$),@(M0+1),FNB$(B1$(164,3)),@(M0+10),FNB$(B1$(161,3)),@(M0+19),Z[3]:M3$,Z[0]:M1$,Z[1]:M1$,Y0:M1$,Z0:M1$,X9:M1$
2050 PRINT (7)@(2),X0$
2060 LET L=L+2,L1=L1+1
2100 LET T[0]=T[0]+Z[0],T[1]=T[1]+Z[1],T[2]=T[2]+Y0,T[3]=T[3]+Z0,T[4]=T[4]+X9,T[5]=T[5]+Y8
2190 GOTO 1400
3000 REM " --- Subtotal"
3020 GOSUB 6000
3090 GOTO 1100
4000 REM " --- Totals"
4100 REM " --- Done"
4120 GOTO 9900
5000 REM " --- Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5070 PRINT (7)@(0),"Accrual: ",A0$(6,16),'LF'
5100 PRINT (7)@(0),"Employee",@(M0+2),"Anniv",@(M0+12),"Hire",@(M0+11+M3),"Accrual",@(M0+12+M3+M1),"Last-Yr",@(M0+12+M3+2*M1),"This-Yr"
5110 PRINT (7)@(0),"No. & Name",@(M0+3),"Date",@(M0+12),"Date",@(M0+13+M3),"Rate",@(M0+13+M3+M1),"Earned",@(M0+12+M3+2*M1),"Accrued",@(M0+15+M3+3*M1),"Used",@(M0+16+M3+4*M1),"Due",@(M0+10+M3+5*M1),"Liability",@(M0+15+M3+6*M1),'LF'
5180 LET L=L+5
5190 RETURN 
6000 REM " --- Subtotal"
6020 IF L1=0 THEN RETURN 
6030 IF L+1>L9 THEN GOSUB 5000
6040 PRINT (7)'LF',@(5),"Totals:",@(M0+19+M3),T[0]:M1$,T[1]:M1$,T[2]:M1$,T[3]:M1$,T[4]:M1$
6050 LET L=L+1
6060 DIM T[5]
6090 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

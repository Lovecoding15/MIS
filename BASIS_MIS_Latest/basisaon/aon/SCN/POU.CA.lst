0010 REM "POU - Quality Assurance Receipt Register Update"
0020 REM "Program POU.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "12Jul96 JAL: Remove POE-63 entries after update to POT-03 (1725)"
0032 REM "15Jul96 Caj: Remove POE-23 entries after update to POE-24 (1585)
0033 REM "14Aug96 WGH: POE-24 not updated correctly for multiple QA receipts of lotted/serialized items (1425-1580)"
0034 REM "11Oct96 WGH: Write POE-44 x-ref only for 'S' Line Types (320,520,2150,2180)"
0040 REM 
0080 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0120 CLOSE (7,ERR=0130)
0200 REM " --- IOLIST's"
0210 POE03A: IOLIST A0$,A1$,A2$,A3$,A[ALL]
0220 POE04A: IOLIST A0$,A1$,A2$,A3$,A[ALL]
0230 POE13A: IOLIST B0$,B1$,B2$,B3$(1),B4$,B5$,B6$,B[ALL]
0240 POE14A: IOLIST B0$,B1$,B2$,B3$(1),B4$,B5$,B6$,B[ALL]
0250 POE23A: IOLIST C0$,C1$,C2$,C[ALL]
0260 POE24A: IOLIST C0$,C1$,C2$,C[ALL]
0270 POE34A: IOLIST POE34_K$
0280 POE44A: IOLIST POE44_K$
0290 POE54A: IOLIST POE54_K$
0300 POE63A: IOLIST D0$,D1$,D2$,D[ALL]
0310 POT03A: IOLIST D0$,D1$,D2$,D[ALL]
0320 POM02A: IOLIST Y0$,Y1$(1),Y2$
0400 REM " --- Parameters"
0500 REM " --- Initializations"
0510 PRECISION I[1]
0520 DIM B3$(22),B[12],C[1],D[6],Y1$(32)
0700 REM " --- Background"
0720 PRINT @(0,22),'CE',
0800 REM " --- Run Update?"
0810 LET V4$="Are You Ready To Update The "+F5$
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0840 PRINT @(COLUMN-9,11),'SB',"Updating",'SF',
0900 REM " --- Disallow 'M'enu Option In Error Routine
0910 LET EXIT_CTRL=1
0950 REM " --- Position Sort File"
0990 READ (POW13_DEV,KEY=N0$,DOM=1000)
1000 REM " --- Read Sort Record"
1010 LET POW13_K$=KEY(POW13_DEV,END=4000)
1020 IF POS(N0$=POW13_K$)<>1 THEN GOTO 4000
1030 READ (POW13_DEV)
1200 REM " --- Get POE-13 QA Receipt Detail"
1220 READ (POE13_DEV,KEY=POW13_K$,DOM=3900)IOL=POE13A
1400 REM " --- Move POE-23 QA Receipt Lot/Serial Detail To POE-24"
1405 IF LS$<>"Y" THEN GOTO 1600
1410 READ (POE23_DEV,KEY=POW13_K$,DOM=1420)
1420 LET POE23_K$=KEY(POE23_DEV,END=1600)
1425 IF POS(POW13_K$=POE23_K$)<>1 THEN GOTO 1600
1430 READ (POE23_DEV)IOL=POE23A
1440 LET QNTY=C[0],UCOST=C[1],LSNUM$=C1$
1450 REM " --- Update Existing POE-24 Record For This Lot/Serial Number"
1455 READ (POE24_DEV,KEY=POE23_K$(1,25),DOM=1460)
1460 LET POE24_K$=KEY(POE24_DEV,END=1500)
1465 IF POS(POE23_K$(1,25)=POE24_K$)<>1 THEN GOTO 1500
1470 READ (POE24_DEV)IOL=POE24A
1475 IF C1$<>LSNUM$ THEN GOTO 1460
1480 LET TOT_QNTY=C[0]+QNTY,TOT_COST=C[1]*C[0]+UCOST*QNTY,C[0]=TOT_QNTY,C[1]=TOT_COST/TOT_QNTY
1490 GOTO 1580
1500 REM " --- Write New POE-24 Record For This Lot/Serial Number"
1505 LET SEQ=0
1510 READ (POE23_DEV,KEY=POE23_K$)IOL=POE23A
1520 LET SEQ=SEQ+1,C0$(26,3)=STR(SEQ:"000")
1525 IF SEQ>999 THEN GOTO 1550
1530 WRITE (POE24_DEV,KEY=C0$,DOM=1520)IOL=POE24A
1540 GOTO 1585
1550 REM " --- Write Consolidated POE-24 Record For Multiple Lot/Serial Numbers"
1555 LET QNTY=C[0],UCOST=C[1]
1560 READ (POE24_DEV,KEY=C0$(1,25)+"999")IOL=POE24A
1570 LET TOT_QNTY=C[0]+QNTY,TOT_COST=C[1]*C[0]+UCOST*QNTY,C[0]=TOT_QNTY,C[1]=TOT_COST/TOT_QNTY,C1$(1)="Consolidated Lot/Serial"
1580 WRITE (POE24_DEV,KEY=C0$)IOL=POE24A
1585 REMOVE (POE23_DEV,KEY=POE23_K$)
1590 GOTO 1420
1600 REM " --- Move POE-63 QA Receipt Rejection Detail To POT-03"
1610 LET SEQ=999,POT03_K$=""
1620 READ (POT03_DEV,KEY=POW13_K$+STR(SEQ:"000"),DOM=1640)
1630 GOTO 1660
1640 LET SEQ=0,POT03_K$=KEYP(POT03_DEV,END=1650)
1650 IF POS(POW13_K$=POT03_K$)=1 THEN LET SEQ=NUM(POT03_K$(26,3))
1660 READ (POE63_DEV,KEY=POW13_K$,DOM=1670)
1670 LET POE63_K$=KEY(POE63_DEV,END=1800)
1680 IF POS(POW13_K$=POE63_K$)<>1 THEN GOTO 1800
1690 READ (POE63_DEV)IOL=POE63A
1700 REM " --- Write Next POT-03 Record"
1705 IF SEQ=999 THEN GOTO 1750
1710 LET SEQ=SEQ+1,D0$=POW13_K$+STR(SEQ:"000")
1720 WRITE (POT03_DEV,KEY=D0$,DOM=1740)IOL=POT03A
1725 REMOVE (POE63_DEV,KEY=POE63_K$)
1730 GOTO 1670
1740 GOTO 1700
1750 REM " --- Write Consolidated POT-03 Record"
1760 LET REJECTED=D[0],D0$=POW13_K$+"999"
1770 READ (POT03_DEV,KEY=D0$,DOM=1780)IOL=POT03A
1775 LET D1$(1)=":::",D1$(7,15)="Consolidated",D[0]=D[0]+REJECTED
1780 WRITE (POT03_DEV,KEY=D0$)IOL=POT03A
1790 GOTO 1670
2000 REM " --- Move POE-13 QA Receipt Detail To POE-14"
2010 LET UCOST=B[1],QA_RECEIVED=B[5],PREV_RECEIVED=B[7]
2020 READ (POE14_DEV,KEY=POW13_K$,DOM=2100)IOL=POE14A
2040 LET TOT_RECEIVED=B[7]+QA_RECEIVED,TOT_COST=B[1]*B[7]+UCOST*QA_RECEIVED
2050 IF TOT_RECEIVED=0 THEN LET B[7]=0,B[1]=0; GOTO 2080
2060 LET B[7]=TOT_RECEIVED,B[1]=TOT_COST/TOT_RECEIVED
2080 WRITE (POE14_DEV,KEY=POW13_K$)IOL=POE14A
2090 GOTO 2500
2100 REM " --- Write New POE-14 Record"
2120 LET B[4]=RECEIVED,B[5]=0,B[6]=0,B[7]=QA_RECEIVED
2140 WRITE (POE14_DEV,KEY=POW13_K$)IOL=POE14A
2150 READ (POM02_DEV,KEY=N0$+B1$(1,2),DOM=2200)IOL=POM02A
2160 LET POE44_K$=POW13_K$(1,2)+B3$(3,20)+POW13_K$(16,10)+POW13_K$(3,13)
2180 IF Y1$(21,1)="S" THEN WRITE (POE44_DEV,KEY=POE44_K$)IOL=POE44A
2200 REM " --- Write New POE-04 Record"
2210 READ (POE04_DEV,KEY=POW13_K$(1,22),DOM=2230)
2220 GOTO 2500
2230 DIM A0$(22),A1$(160),A[8]
2240 LET A0$(1)=POW13_K$
2250 READ (POE03_DEV,KEY=A0$,DOM=2260)IOL=POE03A
2260 WRITE (POE04_DEV,KEY=A0$)IOL=POE04A
2270 LET POE34_K$=A0$(1,2)+A0$(16,7)+A0$(3,13),POE54_K$=A0$(1,2)+A0$(9,7)+A0$(3,6)+A0$(16,7)
2280 WRITE (POE34_DEV,KEY=POE34_K$)IOL=POE34A
2290 WRITE (POE54_DEV,KEY=POE54_K$)IOL=POE54A
2500 REM " --- Update POE-13 QA Receipt Detail Record"
2520 READ (POE13_DEV,KEY=POW13_K$,DOM=3900)IOL=POE13A
2540 LET B[4]=B[4]-B[5]-B[6],B[5]=0,B[6]=0
2560 IF B[4]=0 THEN GOTO 2600
2580 WRITE (POE13_DEV,KEY=POW13_K$)IOL=POE13A
2590 GOTO 3900
2600 REM " --- Remove Empty POE-13 Record"
2620 REMOVE (POE13_DEV,KEY=POW13_K$,DOM=2630)
2640 REMOVE (POE43_DEV,KEY=POW13_K$(1,2)+B3$(3,20)+POW13_K$(16,10)+POW13_K$(3,13),DOM=2650)
2700 REM " --- If Last POE-13 Detail Record, Remove POE-03 Header Record"
2710 READ (POE13_DEV,KEY=POW13_K$(1,22),DOM=2720)
2720 LET POE13_K$=KEY(POE13_DEV,END=2750)
2730 IF POS(POW13_K$(1,22)=POE13_K$)<>1 THEN GOTO 2750
2740 GOTO 3900
2750 REMOVE (POE03_DEV,KEY=POW13_K$(1,22),DOM=2760)
2760 REMOVE (POE33_DEV,KEY=POW13_K$(1,2)+POW13_K$(16,7)+POW13_K$(3,13),DOM=2770)
2770 REMOVE (POE53_DEV,KEY=POW13_K$(1,2)+POW13_K$(9,7)+POW13_K$(3,6)+POW13_K$(16,7),DOM=2780)
3900 REM " --- Get Next Sort Record"
3990 GOTO 1000
4000 REM " --- All Done"
4090 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "ARE - Cash Receipts Line Item Entry"
0020 REM "Program ARE.AC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.3 - 15May97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "22Apr97 WGH: Can't select specific invoices after AUTO apply cash (2015)"
0032 REM "13Apr98 JWK: Wrong variable used in Next Input Control routine (3030)
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLists
0220 ARE01A: IOLIST W0$,W[ALL]
0230 ARE11A1: IOLIST W0$,W[ALL]
0240 ARE11A2: IOLIST X0$,X0,X1
0250 ARE21A1: IOLIST X0$,X0,X1
0260 ARE21A2: IOLIST W0$,W[ALL]
0270 ARE31A: IOLIST X0$,X[ALL]
0300 ART01A: IOLIST Z0$,Z[ALL]
0350 ART11A: IOLIST Z$,Z1$,Z[ALL]
0500 REM " --- Init Data
0520 LET W0$(1)=C0$(1,24),W0$(25,1)=FILL(1)
0530 GOSUB 6000
0700 REM " --- Background
0710 CALL "SYC.WC",1,0,80,0,0,9,0
0720 PRINT 'SB',@(9,9)," AR Distribution ",@(0,L0),'CE','SB',@(O[0]),"Invoice",@(O[1]+1),"Date",@(O[2]+1),"Due",@(O[3]+M1-8),"Inv Amt",@(O[4]+M1-11),"Avail Disc",@(O[5]+M1-8),"Applied",@(O[6]+M1-9),"Discount",@(O[7]+M1-8),"Balance",'SF',
0800 REM " --- Get Balance
0820 GOSUB 6000
0840 GOSUB 5800
0860 LET L=L1
0900 REM " --- Init File
0910 LET SAVEV$=""
0920 READ (ART01_DEV,KEY=C0$(1,4)+C0$(9,6),DOM=0930)
1000 REM " --- Check Line
1020 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L1),'LD',
1040 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key
1110 LET W0$(26,7)=""
1120 LET K$=KEY(ART01_DEV,END=2000)
1140 IF K$(1,10)<>C0$(1,4)+C0$(9,6) THEN GOTO 2000
1180 LET W0$(26,7)=K$(11,7)
2000 REM " --- Invoice Number
2010 IF SAVEV$="AUTO" THEN IF T0<>0 THEN LET V$=W0$(26,7); GOTO 2045
2015 LET SAVEV$=""
2020 LET V0$="S",V1$="KCE^",V2$=W0$(26,7),V0=7,V1=O[0],V2=L,V4$="<Enter>=Next Inv  <F3>=On-Acct  List=List Inv/On-Acct  Auto=Auto Application"
2022 IF V2$(1,2)="OA" THEN LET V2$=""
2024 IF V2$="ON-ACCT" THEN LET V2$=""
2030 GOSUB 7000
2032 IF V$="L" OR V$="LIST" THEN LET SAVEV$="LIST"; GOSUB 5200; GOSUB 6600; DIM W9$(32,"Z"); GOTO 0900
2034 IF V$="AUTO" THEN LET SAVEV$="AUTO"; PRINT @(0,L1),'CE'; LET L=L1+1; LET V$=W0$(26,7); READ (ART01_DEV,KEY=C0$(1,4)+C0$(9,6),DOM=1000)
2035 IF V3=4 THEN GOTO 4000
2037 IF LEN(V$)>1 THEN IF V$(1,2)="OA" THEN GOTO 2050
2038 IF V$="ON-ACCT" THEN GOTO 2050
2040 IF V3=3 THEN LET X$=FNM$(FNC$(C0$(6))),V$="OA"+X$(2)
2042 IF POS("OA"=V$)<>1 THEN LET V$=STR(NUM(V$,ERR=2000):"0000000")
2045 IF POS(" "<>V$)=0 THEN GOTO 0900
2050 LET W0$(26,7)=V$
2060 GOSUB 6200
2070 IF V3<>3 THEN IF V$="" THEN GOTO 2020
2080 IF V$="" THEN LET Z0$=C0$(1,4)+C0$(9,6)+W0$(26,7)+"00"+"    "+C0$(6,3)+C0$(6,3)+C0$(6,3)+" "
2100 GOSUB 6400
2140 FIND (ARE11_DEV,KEY=W0$(1,32),DOM=2200)IOL=ARE11A1
2160 LET W9=-W[0],W8=-W[1],Z0=Z0+W[0]+W[1],Z1=Z1+W[1]
2170 IF W0$(1,32)<=W9$ THEN GOSUB 5400
2180 GOSUB 5000
2190 GOTO 3500
2200 LET W9=0,W8=0,W[1]=Z1
2210 IF Z0$(30,3)<C0$(6,3) THEN LET W[1]=0
2215 LET W[0]=Z0-W[1]
2220 IF W[0]>T0 THEN LET W[0]=T0
2230 IF Y0$(28,1)<>"Y" THEN LET W[1]=0
2240 IF Y0$(27,1)<>"Y" THEN LET W[0]=0
2250 IF W0$(1,32)<=W9$ THEN GOSUB 5400
2260 GOSUB 5000
2500 REM " --- Amount
2510 LET V4$="Enter The Amount For This Transaction"
2520 LET V0$="N",V1$="C",V2$=STR(W[0]),V3$=M1$,V0=M1,V1=O[5],V2=L,I0=0
2530 IF SAVEV$="AUTO" THEN GOTO 2580
2540 GOSUB 7000
2560 LET W[0]=V
2570 IF W[0]=0 THEN LET W[1]=0
2580 PRINT @(O[7],L),Z0-W[0]-W[1]:M1$,
2590 GOTO 3000
2600 REM " --- Discount
2610 IF Y0$(28,1)<>"Y" THEN GOTO 3000
2615 LET V4$="Enter Any Discount Amount To Be Applied"
2620 LET V0$="N",V1$="C",V2$=STR(W[1]),V3$=M1$,V0=M1,V1=O[6],V2=L
2630 IF SAVEV$="AUTO" THEN GOTO 2680
2640 GOSUB 7000
2650 IF V3<>3 THEN GOTO 2660
2652 LET W[0]=Z0-V
2654 PRINT @(O[5],L),W[0]:M1$,
2660 LET W[1]=V
2680 PRINT @(O[7],L),Z0-W[0]-W[1]:M1$,
2690 GOTO 3000
3000 REM " --- Next
3010 IF V3=4 THEN GOTO 3500
3020 IF V3<>2 THEN GOTO 3050
3030 IF I0>0 THEN LET I0=I0-1
3040 GOTO 3060
3050 LET I0=I0+1
3060 ON I0 GOTO 2500,2600,3500
3500 REM " --- Correct?
3520 LET V0$="S",V1$="C",V2$="",V0=1,V1=78,V2=L,V4$="<Enter>=OK   <F2>=Modify   <D>=Delete"
3530 IF SAVEV$="AUTO" THEN GOTO 3600
3540 GOSUB 7000
3550 IF V3=2 THEN GOTO 3000
3560 IF V$="D" THEN GOTO 3700
3570 IF V$<>"" THEN GOTO 3500
3600 IF W[0]<>0 OR W[1]<>0 OR W9<>0 OR W8<>0 THEN WRITE (ARE11_DEV,KEY=W0$(1,32))IOL=ARE11A1
3620 LET X[0]=X[0]+W9+W[0],X[1]=X[1]+W8+W[1],T0=T0-W9-W[0]
3640 WRITE (ARE31_DEV,KEY=X0$)IOL=ARE31A
3650 GOSUB 5800
3660 LET L=L+1,W9$=W0$(1,32)
3690 GOTO 1000
3700 REM " --- Delete Line
3710 REMOVE (ARE11_DEV,KEY=W0$(1,32),DOM=3720)
3720 LET X[0]=X[0]+W9,X[1]=X[1]+W8,T0=T0-W9
3740 IF X[0]=0 THEN IF X[1]=0 THEN REMOVE (ARE31_DEV,KEY=X0$,DOM=3760)
3750 IF X[0]<>0 OR X[1]<>0 THEN WRITE (ARE31_DEV,KEY=X0$)IOL=ARE31A
3760 GOSUB 5800
3780 PRINT @(0,L),'CL',@(0,22),'CL',
3790 GOTO 1000
4000 REM " --- Options
4010 LET V0$="S",V1$="KC",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/",V0=6,V2=22,X$=""
4015 IF Y0$(29,1)<>"A" THEN IF GL$="Y" THEN LET X$="GL/"
4017 LET V4$=V4$+X$+"Delete)",V1=FNV(V4$)
4020 GOSUB 7000
4030 IF V$="DELETE" THEN GOTO 4200
4040 IF V$="GL" THEN IF Y0$(29,1)<>"A" THEN IF GL$="Y" THEN RUN "ARE.AD"
4060 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Check Balance
4110 IF GL$<>"Y" THEN LET T0=0
4120 IF T0=0 THEN RUN "ARE.AB"
4125 DIM MESSAGE$[1]
4130 LET MESSAGE$[0]="This Transaction Is Out Of Balance (<Enter>=Continue)"
4140 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
4160 GOTO 1000
4200 REM " --- Delete
4220 READ (ARE21_DEV,KEY=C0$(1,25),DOM=4230)
4230 LET K$=KEY(ARE21_DEV,END=4390)
4250 IF K$(1,24)<>C0$(1,24) THEN GOTO 4390
4260 READ (ARE21_DEV)IOL=ARE21A2
4270 LET X0$=C0$(1,4)+" "+C0$(9,6)+W0$(26,7)
4280 EXTRACT (ARE31_DEV,KEY=X0$,DOM=4320)IOL=ARE31A
4290 LET X[0]=X[0]-W[0],X[1]=X[1]-W[1]
4300 IF X[0]=0 THEN IF X[1]=0 THEN REMOVE (ARE31_DEV,KEY=X0$)
4310 IF X[0]<>0 OR X[1]<>0 THEN WRITE (ARE31_DEV,KEY=X0$)IOL=ARE31A
4320 REMOVE (ARE21_DEV,KEY=K$)
4330 GOTO 4230
4400 REM 
4420 READ (ARE11_DEV,KEY=C0$(1,25),DOM=4430)
4430 LET K$=KEY(ARE11_DEV,END=4590)
4450 IF K$(1,24)<>C0$(1,24) THEN GOTO 4590
4460 READ (ARE11_DEV)IOL=ARE11A1
4470 LET X0$=C0$(1,4)+" "+C0$(9,6)+W0$(26,7)
4480 EXTRACT (ARE31_DEV,KEY=X0$,DOM=4520)IOL=ARE31A
4490 LET X[0]=X[0]-W[0],X[1]=X[1]-W[1]
4500 IF X[0]=0 THEN IF X[1]=0 THEN REMOVE (ARE31_DEV,KEY=X0$)
4510 IF X[0]<>0 OR X[1]<>0 THEN WRITE (ARE31_DEV,KEY=X0$)IOL=ARE31A
4520 REMOVE (ARE11_DEV,KEY=K$)
4530 GOTO 4430
4600 REM 
4620 REMOVE (ARE01_DEV,KEY=C0$(1,25),DOM=4630)
4630 LET K$=KEY(ARE01_DEV,END=4790)
4650 IF K$(1,24)<>C0$(1,24) THEN GOTO 4790
4660 READ (ARE01_DEV)IOL=ARE01A
4670 LET X0$=C0$(1,4)+" "+C0$(9,6)+W0$(26,7)
4680 EXTRACT (ARE31_DEV,KEY=X0$,DOM=4720)IOL=ARE31A
4690 LET X[0]=X[0]-W[0],X[1]=X[1]-W[1]
4700 IF X[0]=0 THEN IF X[1]=0 THEN REMOVE (ARE31_DEV,KEY=X0$)
4710 IF X[0]<>0 OR X[1]<>0 THEN WRITE (ARE31_DEV,KEY=X0$)IOL=ARE31A
4720 REMOVE (ARE01_DEV,KEY=K$)
4730 GOTO 4630
4890 RUN "ARE.AB"
5000 REM " --- Display
5010 PRINT @(O[0],L),W0$(26,7),@(O[1]),FNC$(Z0$(24,3)),@(O[2]),FNC$(Z0$(27,3)),@(O[3]),Z0:M1$,@(O[4]),Z1:M1$,@(O[5]),W[0]:M1$,@(O[6]),W[1]:M1$,@(O[7]),Z0-W[0]-W[1]:M1$,
5020 IF SAVEV$="LIST" THEN GOTO 5090
5030 PRINT @(0,22),'CL',@(28),'SB',"Discount Date: ",'SF',FNB$(Z0$(30,3)),
5090 RETURN 
5200 REM " --- Select List Invoices or List On-Account Records
5210 LET V0$="S",V1$="CE",V2$="",V4$="Press <Enter> To List Invoices Or "+$22$+"O"+$22$+" To List On-Account Records",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5230 IF V$="" THEN LET S0=1; REM "List regular invoices"
5240 IF V$="O" THEN LET S0=2; REM "List on-account records"
5290 RETURN 
5400 REM " --- Clear Screen
5420 PRINT @(0,L1),'CE',
5440 LET L=L1
5490 RETURN 
5800 REM " --- Balance
5810 PRINT @(O[7],8),T0:M1$,
5890 RETURN 
6000 REM " --- Accumulate
6010 LET T0=C[0]
6020 READ (ARE11_DEV,KEY=C0$(1,24),DOM=6030)
6030 LET K11$=KEY(ARE11_DEV,END=6090)
6050 IF K11$(1,24)<>C0$(1,24) THEN GOTO 6090
6060 READ (ARE11_DEV)IOL=ARE11A2
6070 LET T0=T0-X0
6080 GOTO 6030
6090 REM 
6120 READ (ARE21_DEV,KEY=C0$(1,24),DOM=6130)
6130 LET K21$=KEY(ARE21_DEV,END=6190)
6150 IF K21$(1,24)<>C0$(1,24) THEN GOTO 6190
6160 READ (ARE21_DEV)IOL=ARE21A1
6170 LET T0=T0+X0
6180 GOTO 6130
6190 RETURN 
6200 REM " --- Get Open Invoice
6210 LET V$=""
6215 DIM Z0$(50)
6220 LET Z0$(1)=C0$(1,4)+C0$(9,6)+W0$(26,7)+"00",Z0=0,Z1=0
6240 READ (ART01_DEV,KEY=Z0$(1,19),DOM=6380)IOL=ART01A
6260 LET Z0=Z[0],Z1=Z[1],V$=Z0$
6270 READ (ART11_DEV,KEY=Z0$(1,17),DOM=6280)
6300 LET K$=KEY(ART11_DEV,END=6390)
6310 IF K$(1,17)<>Z0$(1,17) THEN GOTO 6380
6320 READ (ART11_DEV)IOL=ART11A
6340 IF Z1$(1,1)=" " THEN GOTO 6360
6350 LET Z0=Z0+Z[0]+Z[1],Z1=Z1+Z[1]
6370 GOTO 6300
6380 IF Z0=0 THEN LET Z1=0
6390 RETURN 
6400 REM " --- App Today
6410 DIM X[1]
6420 LET X0$=C0$(1,4)+" "+C0$(9,6)+W0$(26,7)
6440 EXTRACT (ARE31_DEV,KEY=X0$,DOM=6490)IOL=ARE31A
6460 LET Z0=Z0-X[0]-X[1],Z1=Z1-X[1]
6490 RETURN 
6600 REM " --- List
6605 IF S0=2 THEN READ (ART01_DEV,KEY=C0$(1,4)+C0$(9,6)+"O",DOM=6620)
6610 READ (ART01_DEV,KEY=C0$(1,4)+C0$(9,6),DOM=6620)
6620 PRINT @(0,L1),'CE'
6630 LET L=L1+1
6640 LET K$=KEY(ART01_DEV,END=6800)
6650 IF LEN(K$)<19 THEN GOTO 6800
6655 IF S0=1 THEN IF K$(11,2)="OA" OR K$(11,7)="ON-ACCT" THEN GOTO 6800
6660 IF K$(1,10)<>C0$(1,4)+C0$(9,6) THEN GOTO 6800
6700 LET W0$(26,7)=K$(11,7),W[0]=0,W[1]=0
6710 GOSUB 6200
6720 GOSUB 6400
6730 FIND (ARE11_DEV,KEY=W0$(1,32),DOM=6740)IOL=ARE11A1
6735 LET W9=-W[0],W8=-W[1],Z0=Z0+W[0]+W[1],Z1=Z1+W[1]
6740 IF L>L9 THEN GOSUB 6900; PRINT @(0,L1),'CE',
6750 IF V3=4 THEN GOTO 6820
6760 GOSUB 5000
6770 LET L=L+1
6790 GOTO 6640
6800 GOSUB 6900
6820 LET L=L1
6890 RETURN 
6900 REM 
6910 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Press <Enter> To Continue",V0=1,V1=FNV(V4$),V2=22
6920 GOSUB 7000
6930 LET L=L1+1
6940 IF V3<>4 THEN PRINT @(0,L1),'CE',
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8080 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

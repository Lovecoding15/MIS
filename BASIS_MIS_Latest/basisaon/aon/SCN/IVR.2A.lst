0010 REM "IVR - Inventory Alternate/Superceded Items Listing"
0020 REM "Program IVR.2A"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Jan96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "27Nov96 WGH: <F3>=Lookups don't work, and may cause Error 13 on Line 5630 of IVC.IB (180)"
0032 REM "18Feb98 WGH: Possible Error 47 on line 1420 when Beginning Item matches exactly (970-980)"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=6
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-01"
0130 LET FILES$[4]="IVM-10",FILES$[5]="IVM-14",FILES$[6]="IVM-15"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM01_DEV2=CHANNELS[3]
0180 LET IVM10_DEV=CHANNELS[4],IVM14_DEV=CHANNELS[5],IVM15_DEV=CHANNELS[6]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A4$(1),A5$,A6$,A[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0270 IVS01A: IOLIST I0$,I1$,I2$,I3$,I4$,M0$,M1$,M2$,M3$,M4$,M5$
0400 REM " --- Parameters"
0405 DIM P[5]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="IV"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0450 LET P[0]=NUM(I2$(1,2)),P[1]=NUM(I2$(5,1))
0460 LET P[3]=NUM(I2$(9,2)),P[4]=NUM(I2$(11,2)),P[5]=NUM(I2$(13,2))
0500 REM " --- Initializations"
0505 PRECISION P[1]
0510 DIM FILES[15],PARAMS$[3],OPTIONS$[7],HEADINGS$[7],O[5]
0520 DIM A0$(22),A1$(60),A2$(64),A3$(40),A4$(21),A[12]
0550 LET FILES[1]=IVM01_DEV,FILES[10]=IVM10_DEV,FILES[14]=IVM14_DEV
0555 LET PARAMS$[0]=N0$,PARAMS$[1]=I1$,PARAMS$[2]=I2$,PARAMS$[3]=I3$
0560 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$,WHEN$=F0$(7,3),CLOCK$=""
0565 LET WIDTH=132,PAGE=0,L9=59,L=L9+1,HEADINGS=7,NUMBER=0,SELECTION$="S"
0570 LET P[0]=MAX(14,P[0]),DW=P[3]+P[4]+P[5],X=(P[0]*2)+(DW*2)+20
0575 LET FILES[15]=IVM15_DEV
0600 REM " --- Print positions"
0610 WHILE X>WIDTH
0620 LET DW=DW-1,X=(P[0]*2)+(DW*2)+20
0630 WEND
0640 LET O[0]=1,O[1]=O[0]+P[0]+1,O[2]=O[1]+DW+1
0650 LET O[3]=O[2]+8,O[4]=O[3]+P[0]+1,O[5]=O[4]+DW+1
0700 REM " --- Background"
0710 PRINT 'SB',@(15,3),"Alternate Or Superceded:",'SF',
0720 CALL "SYC.WC",1,0,80,0,0,4,0
0770 CALL "IVC.IB","BL","DW",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0780 PRINT @(40,3),SELECTION$,
0790 CALL "IVC.IB","DL","DW",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0800 REM " --- Sequence"
0805 LET V4$="List Alternate Or Superceded Items (A=Alternate/S=Superceded)"
0810 LET V0$="S",V1$="CE",V2$=SELECTION$,V3$="AS",V0=1,V1=40,V2=3
0815 GOSUB 7000
0820 ON V3 GOTO 0825,0800,0800,0800,0865
0825 LET SELECTION$=V$
0850 REM " --- Process input"
0855 CALL "IVC.IB","IL","DW",5,FILES[ALL],PARAMS$[ALL],OPTIONS$[ALL],V3
0860 ON V3 GOTO 0865,0850,0800,0850,0865
0865 PRINT @(40,3),SELECTION$,
0870 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
0875 ON V3 GOTO 0880,0870,0870,0870,9900
0880 ON POS(V$="YN") GOTO 0870,0885,0800
0885 LET BEGPROD$=OPTIONS$[3],ENDPROD$=OPTIONS$[4],BEGITEM$=OPTIONS$[5]
0890 LET ENDITEM$=OPTIONS$[6],TYPE$=OPTIONS$[7]
0900 REM " --- Build headings"
0910 GOSUB 5800
0950 REM " --- Position files"
0955 CALL "SYC.GA",7,1,"","",STATUS
0960 IF STATUS THEN GOTO 9900
0965 CALL "SYC.NB","Printing",P[0],COLUMN
0970 LET K$=N0$+BEGITEM$
0980 READ (IVM01_DEV,KEY=K$,DOM=1000)IOL=IVM01A
0990 GOTO 1050
1000 REM " --- Next Item"
1010 LET K$=KEY(IVM01_DEV,END=4000)
1020 IF POS(N0$=K$)<>1 THEN GOTO 4000
1030 IF ENDITEM$<>"" THEN IF K$(3)>ENDITEM$ THEN GOTO 4000
1040 READ (IVM01_DEV)IOL=IVM01A
1050 PRINT @(COLUMN,11),A0$(3,P[0]),
1055 IF A4$(1,1)<>SELECTION$ THEN GOTO 3900
1060 IF FNP$(A4$(2))="" THEN GOTO 3900
1070 IF BEGPROD$<>"" THEN IF A2$(1,3)<BEGPROD$ THEN GOTO 3900
1080 IF ENDPROD$<>"" THEN IF A2$(1,3)>ENDPROD$ THEN GOTO 3900
1090 IF TYPE$<>"" THEN IF A2$(53,3)<>TYPE$ THEN GOTO 3900
1100 REM " --- Store source item information"
1110 LET DESCRIPTION$=FNITEM$(A1$,P[3],P[4],P[5]),PRODUCT$=A2$(1,3)
1200 REM " --- Retrieve alternate/substitute item"
1210 LET A0$(3)=A4$(2)
1220 FIND (IVM01_DEV2,KEY=A0$,DOM=3900)IOL=IVM01A
1400 REM " --- Format detail line"
1410 DIM BUFFER$(WIDTH)
1420 LET BUFFER$(O[0])=K$(3),BUFFER$(O[1],DW)=DESCRIPTION$
1430 LET BUFFER$(O[2])=PRODUCT$,BUFFER$(O[3])=A0$(3)
1440 LET BUFFER$(O[4],DW)=FNITEM$(A1$,P[3],P[4],P[5]),BUFFER$(O[5])=A2$(1,3)
1500 REM " --- Print detail line"
1510 IF L+1>L9 THEN GOSUB 5000
1520 PRINT (7)BUFFER$
1530 LET L=L+1,NUMBER=NUMBER+1
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF L+2>L9 THEN GOSUB 5000
4020 PRINT (7)""
4030 PRINT (7)NUMBER," Item(s) Listed"
4090 GOTO 9900
5000 REM " --- Report Heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 IF CHAR$<>"" THEN LET LAST$=CHAR$
5090 RETURN 
5800 REM " --- Initialize HEADINGS$"
5810 DIM H1$(WIDTH),H2$(WIDTH),H6$(WIDTH)
5815 IF SELECTION$="A" THEN LET HEADINGS$[1]="Alternate Item Listing"
5820 IF SELECTION$="S" THEN LET HEADINGS$[1]="Superceded Item Listing"
5825 LET X1$="First",X2$="First",X2=5
5830 IF BEGITEM$<>"" THEN LET X1$=BEGITEM$
5835 IF BEGPROD$<>"" THEN LET X2$=BEGPROD$,X2=3
5840 IF ENDPROD$="" THEN IF X2=3 THEN LET X2=4
5845 LET H1$(1)="From Item: "+X1$,H1$(WIDTH-X2-13)="From Product: "+X2$
5850 LET X1$="Last",X2$="Last",X3$="All Item Types"
5855 IF ENDITEM$<>"" THEN LET X1$=ENDITEM$
5860 IF ENDPROD$<>"" THEN LET X2$=ENDPROD$
5865 IF TYPE$<>"" THEN LET X3$="Item Type "+TYPE$
5870 LET HEADINGS$[1]=HEADINGS$[1]+" For "+X3$
5875 LET H2$(1)="  To Item: "+X1$,H2$(WIDTH-X2-13)="  To Product: "+X2$
5885 LET H6$(O[0])="Item",H6$(O[1])="Description",H6$(O[2])="Prd"
5890 IF SELECTION$="S" THEN LET H6$(O[3])="Superceded By"
5900 IF SELECTION$="A" THEN LET H6$(O[3])="Alternate Item"
5905 LET H6$(O[4])="Description",H6$(O[5])="Prd"
5980 LET HEADINGS$[3]=H1$,HEADINGS$[4]=H2$,HEADINGS$[6]=H6$
5990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

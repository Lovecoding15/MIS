0010 REM "POR - PO Billed/Unbilled Receipts Report (Print Overlay)
0020 REM "Program POR.TB
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "17Jun96 Caj: 'O' type lines' values were omitted (1985,1990,1995,2005,2810,2825,2845)
0032 REM "09Apr98 JWK: Detail not printed if first receipt was billed (2285)
0033 REM "07Mar00 WGH: Don't report POs received complete with zero receipt amount (2055)
0040 REM 
0080 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 APM01A: IOLIST R0$(1),R1$(1)
0240 POM02A: IOLIST S0$(1),S1$(1)
0250 POT04A: IOLIST A0$(1),A1$(1),A2$,A3$,A[ALL]
0260 POT14A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1),B4$(1),B5$,B6$,B[ALL]
0270 POT05A: IOLIST C0$(1),C1$(1),C2$,C3$,C[ALL]
0280 POT25A: IOLIST D0$(1),D1$(1),D2$(1),D[ALL]
0290 POT35A: IOLIST XREF$(1)
0300 POW18A: IOLIST WFKEY$(1),WFPOAMT
0500 REM " --- Init Data
0510 PRECISION IVPRECISION
0520 DIM A0$(22),A1$(160),A[8]
0530 LET PRINTVTOT$="N"
0600 REM " --- Headings and print positions
0610 LET HEADINGS=4
0620 DIM HEADINGS$[HEADINGS],O[14]
0630 LET WIDTH=132,PAGE=0,L9=59,L=L9+1
0650 GOSUB ASSIGNHEADS
0760 POSITIONS: 
0770 LET O[1]=O[0]+8,O[2]=O[1]+9,O[3]=O[2]+M1+1,O[4]=O[3]+9
0780 LET O[5]=O[4]+M1+1,O[6]=O[5]+5,O[7]=O[6]+11,O[8]=O[7]+9
0790 LET O[9]=O[8]+M1+1,O[10]=O[9]+M1+1
0810 IF O[10]+15<WIDTH THEN GOTO 0950
0820 LET O[10]=WIDTH-M1,O[9]=O[10]-M1,O[8]=O[9]-M1,O[7]=O[8]-9
0830 LET O[6]=O[7]-9,O[5]=O[6]-3,O[4]=O[5]-M1,O[3]=O[4]-9,O[2]=O[3]-M1
0950 REM " --- Position files
0980 CALL "SYC.NB","Printing",22,COLUMN
1000 REM " --- Sort by Alt Seq or Vendor Num?
1005 SORTORDER: 
1020 IF PRINTSEQ$<>"A" THEN GOTO SORTBYVEND
1100 REM " --- Sort by alternate sequence
1105 SORTBYALT: 
1120 IF INITIALIZING$="N" THEN GOTO KEYBYALT
1130 LET INITIALIZING$="N"
1160 READ (APM04_DEV,KEY=N0$+"A"+VENDFROM$,DOM=KEYBYALT)
1170 REM " --- Find a POT04 record using alt seq
1180 KEYBYALT: 
1190 LET KM04A$=KEY(APM04_DEV,END=4000)
1200 IF KM04A$(1,3)<>N0$+"A" THEN GOTO 4000
1210 LET KM04B$=KM04A$(1,2)+KM04A$(14,6)
1220 READ (APM04_DEV); REM "Move ptr
1230 READ (POT04_DEV,KEY=KM04B$,DOM=1240)
1240 LET KT04$=KEY(POT04_DEV,END=NEXTALT)
1250 IF KT04$(1,8)<>KM04B$ THEN GOTO NEXTALT
1260 LET K$=KT04$
1270 GOTO TESTKEY
1280 NEXTALT: 
1290 GOTO KEYBYALT
1300 REM " --- Sort by Vendor number
1305 SORTBYVEND: 
1320 IF INITIALIZING$="N" THEN GOTO KEYBYVEND
1330 LET INITIALIZING$="N"
1340 READ (POT04_DEV,KEY=N0$+VENDFROM$,DOM=KEYBYVEND)
1400 REM " --- Read through Receipt Hist Header file
1405 KEYBYVEND: 
1420 LET K$=KEY(POT04_DEV,END=4000)
1430 TESTKEY: 
1440 IF K$(1,2)<>N0$ THEN GOTO 4000
1450 REM " --- Valid Vendor?
1460 IF VENDTHRU$<>"" AND K$(3,6)>VENDTHRU$ THEN GOTO 4000
1470 LET VENDOR$=K$(3,6)
1490 READ (POT04_DEV)IOL=POT04A
1500 REM " --- Valid Receipt Date?
1510 IF DATEFROM$<>"" AND A1$(29,6)<DATEFROM$ THEN GOTO NEXTPOT04
1520 IF DATETHRU$<>"" AND A1$(29,6)>DATETHRU$ THEN GOTO NEXTPOT04
1600 REM " --- Init/Assign PO variables
1620 IF PONUM$<>A0$(9,7) THEN LET POAMT_TOTALED=0
1630 LET PONUM$=A0$(9,7),POAMT=0,POAMT$=STR(POAMT:M1$)
1640 DIM PLINE$(132),ORDDATE$(8),RCPTDATE$(8)
1650 IF A1$(5,6)<>FILL(6) THEN LET ORDDATE$=FNB6$(A1$(5,6))
1660 IF A1$(29,6)<>FILL(6) THEN LET RCPTDATE$=FNB6$(A1$(29,6))
1680 PRINT @(COLUMN,11),FNF$(A0$(3,VENDLEN),VENDOMASK$)," ",A0$(9,7)," ",A0$(16,7)
1700 REM " --- Accumulate PO Amt (POW-18)
1705 ACCUMPOAMT: 
1710 DIM WFKEY$(18)
1720 READ (POW18_DEV,KEY=A0$(1,15),DOM=READACCUMPO)
1725 READACCUMPO: 
1730 LET K18$=KEY(POW18_DEV,END=DONEACCUMPO)
1740 IF K18$(1,15)<>A0$(1,15) THEN GOTO DONEACCUMPO
1780 READ (POW18_DEV)IOL=POW18A
1790 PRECISION 2
1810 LET POAMT=POAMT+WFPOAMT
1820 PRECISION IVPRECISION
1830 GOTO READACCUMPO
1835 DONEACCUMPO: 
1840 LET POAMT$=STR(POAMT:M1$),POAMT_TOTALED=POAMT_TOTALED+1
1850 REM " --- Assign PLINE$ for vendor and PO
1870 LET PLINE$(1)=PONUM$,PLINE$(O[1]+1)=ORDDATE$,PLINE$(O[2]+1)=POAMT$
1880 IF POAMT_TOTALED>1 THEN DIM PLINE$(132); LET POAMT=0
1900 REM " --- Process PO/Receiver 
1910 DIM B0$(25),B1$(48),B2$(32),B3$(22),B4$(40),B[12]
1915 DIM APTYPE$(2),INVNUM$(10),INVDATE$(8)
1920 LET RCPTAMT=0,RCPTAMT$=STR(RCPTAMT:M1$)
1930 LET INVAMT=0,RCPTAMT_IV=0,IVNAMT$=STR(0:M1$),RCPTAMT_IV$=STR(0:M1$)
1935 REM " --- Accumulate receipt total
1940 READ (POT14_DEV,KEY=A0$,DOM=READPOT14)
1945 READPOT14: 
1950 LET K14$=KEY(POT14_DEV,END=DONEACCUM)
1960 IF K14$(1,22)=A0$ THEN GOTO 1980
1970 IF K14$(9,7)<>PONUM$ OR K14$(16,7)<>A0$(16,7) THEN GOTO DONEACCUM
1980 READ (POT14_DEV)IOL=POT14A
1985 DIM S0$(4),S1$(32)
1990 FIND (POM02_DEV,KEY=B0$(1,2)+B1$(1,2),DOM=1995)IOL=POM02A
1995 PRECISION 2
2000 LET UNITCOST=B[1],QTYREC=B[7]
2005 IF S1$(21,1)="O" THEN LET QTYREC=1
2010 LET RCPTAMT=RCPTAMT+(UNITCOST*QTYREC)
2020 PRECISION IVPRECISION
2030 LET HAVEDATA=1
2040 GOTO READPOT14
2045 DONEACCUM: 
2050 LET RCPTAMT$=STR(RCPTAMT:M1$)
2055 IF RCPTAMT=0 AND A1$(134,1)="Y" THEN LET HAVEDATA=0
2060 LET PLINE$(O[3]+1)=RCPTDATE$,PLINE$(O[4]+1)=RCPTAMT$
2070 IF HAVEDATA THEN GOSUB FILLRESTPLINE ELSE GOTO NEXTPOT04
2200 REM " --- Get Invoice Data (via POT-35, Xref)
2210 DIM C0$(20),C1$(54),C[8],INVNUM$(10),PREVINV$(10),APTYPE$(2),INVDATE$(8)
2220 LET INVAMT=0,RCPTAMT_IV=0,FIRSTINV4RCPT=1,BILLED=0
2230 LET INVAMT$=STR(INVAMT:M1$),RCPTAMT_IV$=STR(RCPTAMT_IV:M1$)
2240 READ (POT35_DEV,KEY=A0$(1,22),DOM=2250)
2250 LET K35$=KEY(POT35_DEV,END=NEXTRCPT)
2260 IF K35$(1,22)<>A0$ THEN GOTO NEXTRCPT
2270 IF LEVEL$<>"U" THEN GOTO INVDATA
2280 DIM PLINE$(132); REM "Unbilled: only print if no POT-04
2285 LET POAMT_TOTALED=POAMT_TOTALED-1
2290 GOTO NEXTRCPT
2600 REM " ---
2605 INVDATA: 
2640 LET INVNUM$=K35$(25,10)
2650 IF INVNUM$<>PREVINV$ AND FIRSTINV4RCPT<>1 THEN GOTO DONETHISINV
2660 LET D0$=K35$(1,2)+K35$(23,2)+K35$(3,6)+K35$(25,16)
2670 LET BILLED=1
2700 REM " --- Get POT-05 data
2710 FIND (POT05_DEV,KEY=D0$(1,23),DOM=NEXTXREF)IOL=POT05A
2720 LET INVAMT=C[0],INVAMT$=STR(INVAMT:M1$)
2730 LET APTYPE$=C0$(3,2)
2740 IF C1$(1,6)<>FILL(6) THEN LET INVDATE$=FNB6$(C1$(1,6))
2800 REM " --- Get POT-25 Data
2810 DIM D1$(31),D2$(1),D[4],S0$(4),S1$(32)
2820 FIND (POT25_DEV,KEY=D0$,DOM=NEXTXREF)IOL=POT25A
2825 FIND (POM02_DEV,KEY=D0$(1,2)+D1$(18,2),DOM=2830)IOL=POM02A
2830 PRECISION 2
2840 LET UNITCOST=D[0],QTYREC=D[1]
2845 IF S1$(21,1)="O" THEN LET QTYREC=1
2850 LET RCPTAMT_IV=RCPTAMT_IV+(UNITCOST*QTYREC)
2860 PRECISION IVPRECISION
2870 LET RCPTAMT_IV$=STR(RCPTAMT_IV:M1$)
2880 GOTO NEXTXREF
2900 REM " --- Have gotten all data for this invoice
2905 DONETHISINV: 
2910 GOSUB FILLRESTPLINE
2920 GOSUB PRINTLINE
2930 LET PREVINV$=INVNUM$
3100 REM " --- Loop up for next POT-35, Xreference record
3105 NEXTXREF: 
3120 READ (POT35_DEV)
3130 GOTO 2250
3200 REM " --- Loop up for next PO/Receiver
3205 NEXTRCPT: 
3210 IF PLINE$=FILL(132) THEN GOTO 3240
3220 IF LEVEL$<>"B" OR (LEVEL$="B" AND BILLED=1) THEN GOSUB PRINTLINE
3240 LET HAVEDATA=0
3270 READ (POT14_DEV,KEY=K14$(1,22)+$FF$,DOM=3290)
3290 GOTO READPOT14
3500 REM " --- Loop up for next header record via sort files
3505 NEXTPOT04: 
3520 IF PRINTSEQ$<>"A" THEN GOTO 3560
3530 REM " --- See if there's another PO for vend before getting next alt seq
3540 LET K$=KEY(POT04_DEV,END=3570)
3550 IF K$(3,6)=VENDOR$ THEN GOTO TESTKEY
3560 READ (POT04_DEV,KEY=K$); REM "Restore ptr
3570 GOTO SORTORDER
4000 REM " --- All Done
4010 IF L+1>L9 THEN GOSUB 5000
4030 GOSUB VENDTOTALS
4040 LET GRANDVARI=GRANDRCPTAMT-GRANDRCPTAMT_IV
4060 PRINT (7)@(O[2]-13),"Report Total",@(O[2]),GRANDPOAMT:M1$,@(O[4]),GRANDRCPTAMT:M1$,@(O[8]),GRANDINVAMT:M1$,@(O[9]),GRANDRCPTAMT_IV:M1$,@(O[10]),GRANDVARI:M1$
4090 GOTO 9900
5000 REM " --- Heading
5010 LET L=HEADINGS+1
5030 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5040 IF STATUS THEN EXITTO 9900
5100 PRINT (7)@(O[0]),"PO",@(O[1]),"PO",@(O[2]+M1-7),"PO",@(O[3]),"PO Recpt",@(O[4]+M1-9),"PO Recpt",@(O[5]),"AP",@(O[6]),"Invoice",@(O[7]),"Invoice",@(O[8]+M1-8),"Invoice",@(O[9]+M1-12),"PO Recpt Amt",@(O[10]+M1-9),"Receipt"
5110 PRINT (7)@(O[0]),"Number",@(O[1]),"Date",@(O[2]+M1-7),"Amount",@(O[3]),"Date",@(O[4]+M1-9),"Amount",@(O[5]),"Type",@(O[6]),"Number",@(O[7]),"Date",@(O[8]+M1-8),"Total",@(O[9]+M1-12),"On Invoice",@(O[10]+M1-9),"Variance"
5160 PRINT (7)""
5180 LET L=L+3
5190 RETURN
5300 REM " --- Assign headings variables
5305 ASSIGNHEADS: 
5310 DIM TMPH3$(WIDTH),TMPH4$(WIDTH)
5320 IF VENDFROM$="" THEN LET VENDF$="First" ELSE LET VENDF$=FNF$(VENDFROM$(1,VENDLEN),VENDOMASK$)
5330 IF VENDTHRU$="" THEN LET VENDT$="Last" ELSE LET VENDT$=FNF$(VENDTHRU$(1,VENDLEN),VENDOMASK$)
5340 IF DATEFROM$="" THEN LET DATEF$="First" ELSE LET DATEF$=FNB6$(DATEFROM$)
5350 IF DATETHRU$="" THEN LET DATET$="Last" ELSE LET DATET$=FNB6$(DATETHRU$)
5360 LET MIDH3$=""; REM "All (billed & unbilled)
5370 IF LEVEL$="U" THEN LET MIDH3$="*** Unbilled ***"
5380 IF LEVEL$="B" THEN LET MIDH3$="*** Billed ***"
5390 LET TMPH3$(1)="From Vendor: "+VENDF$,TMPH3$(FNC(MIDH3$,WIDTH))=MIDH3$
5400 LET TMPH3$(WIDTH-LEN(DATEF$)-18)="From Receipt Date: "+DATEF$
5420 LET TMPH4$(1)="  To Vendor: "+VENDT$
5430 LET TMPH4$(WIDTH-LEN(DATEF$)-18)="  To Receipt Date: "+DATET$
5440 LET HEADINGS$[0]=F4$,HEADINGS$[1]=F5$
5450 LET HEADINGS$[2]=TMPH3$,HEADINGS$[3]=TMPH4$
5490 RETURN
6000 REM " --- New Vendor
6005 NEWVENDOR: 
6010 DIM R0$(8),R1$(195)
6020 LET VENDNAME$="Not On File",PREVVEND$=VENDOR$,PRINTVTOT$="Y"
6030 LET VENDPOAMT=0,VENDRCPTAMT=0,VENDINVAMT=0,VENDRCPTAMT_IV=0
6040 FIND (APM01_DEV,KEY=N0$+VENDOR$,DOM=6060)IOL=APM01A
6050 LET VENDNAME$=R1$(1,30)
6060 IF L+4>L9 THEN GOSUB 5000
6070 PRINT (7)@(0),"Vendor: ",FNF$(VENDOR$(1,VENDLEN),VENDOMASK$)," ",VENDNAME$
6080 LET L=L+1
6090 RETURN
6500 REM " --- Print data line
6505 PRINTLINE: 
6510 REM " --- Test breaks
6520 IF VENDOR$=PREVVEND$ THEN GOTO 6550
6530 IF PRINTVTOT$="Y" THEN GOSUB VENDTOTALS
6540 GOSUB NEWVENDOR
6550 GOSUB FILLRESTPLINE
6560 IF L+2>L9 THEN GOSUB 5000
6570 PRINT (7)PLINE$
6580 LET L=L+1
6590 DIM PLINE$(132)
6600 REM " --- Accumulate totals
6610 LET VENDPOAMT=VENDPOAMT+POAMT,VENDRCPTAMT=VENDRCPTAMT+RCPTAMT
6620 LET VENDINVAMT=VENDINVAMT+INVAMT,VENDRCPTAMT_IV=VENDRCPTAMT_IV+RCPTAMT_IV
6630 LET GRANDPOAMT=GRANDPOAMT+POAMT,GRANDRCPTAMT=GRANDRCPTAMT+RCPTAMT
6640 LET GRANDINVAMT=GRANDINVAMT+INVAMT,GRANDRCPTAMT_IV=GRANDRCPTAMT_IV+RCPTAMT_IV
6690 RETURN
6800 REM " --- Complete filling print line
6805 FILLRESTPLINE: 
6810 LET PLINE$(O[5]+1)=APTYPE$,PLINE$(O[6]+1)=INVNUM$,PLINE$(O[7]+1)=INVDATE$
6820 LET PLINE$(O[8]+1)=INVAMT$,PLINE$(O[9]+1)=RCPTAMT_IV$
6830 LET VARIANCE=RCPTAMT-RCPTAMT_IV,VARIANCE$=STR(VARIANCE:M1$)
6840 LET PLINE$(O[10]+1)=VARIANCE$
6890 RETURN
6900 REM " --- Print Vendor total
6910 VENDTOTALS: 
6920 LET VENDVARI=VENDRCPTAMT-VENDRCPTAMT_IV
6930 IF L+1>L9 THEN GOSUB 5000
6940 PRINT (7)@(O[2]-13),"Vendor Total",@(O[2]),VENDPOAMT:M1$,@(O[4]),VENDRCPTAMT:M1$,@(O[8]),VENDINVAMT:M1$,@(O[9]),VENDRCPTAMT_IV:M1$,@(O[10]),VENDVARI:M1$
6950 PRINT (7)""
6960 LET L=L+2
6970 RETURN
8000 REM " --- Functions
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8035 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

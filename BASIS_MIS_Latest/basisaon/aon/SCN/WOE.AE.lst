0010 REM "WOS - WO Material Requirements Processing, BM Explosion "
0020 REM "Program WOE.AE"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.5 - 01Dec98 |"
0026 REM "|         Copyright (c) 1997 ADD+ON Software Inc.           |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "12Jul96 JJD: Allow item synonym entry (400-430,1010,1620,1625,1670,1675).  See related SCN in WOE.AA dated 12Jul96"
0032 REM "02Jan97 JJD: BM explosion default should be "N"o (3525)"
0033 REM "26Jan97 JJD: Set Special Order flag if appropriate (3425,3426,6465)"
0034 REM "09Jun97 WGH: Prevent possible Error 47 on Line 6465 (6005,6053"
0035 REM "26Aug98 WGH: Calculate Total Cost correctly when detail line modified (2700-2860,3120,3260,3320)
0036 REM "22OCt98 KJS: Set unit cost correctly (1780,3110)"
0037 REM "31Dec98 WGH: Calculate Total Units with greater precision (3110-3120)
0038 REM "22Jan99 WGH: New detail lines aren't being cleared when deleted (3720,3730)
0039 REM "05Mar99 WGH: Don't clear Unit Of Measure for Standard subcontract lines (5600)
0040 REM "
0050 REM "L9=MAX LINES, L1=FIRST LINES, L0=HEADER LINE, L=CURRENT LINE
0060 REM "# LEVELS OF BILLS - LINE 6010 T(X,1)
0070 SETESC 9000
0080 SETERR 9000
0090 PRECISION IVS01_PRECISION
0095 IF I9$="R" THEN GOTO 4200
0100 REM " --- Open/Lock Files
0200 REM " --- IOLISTs
0210 WOE02A: IOLIST Z0$(1),Z1$(1),Z[ALL]
0215 WOE03D: IOLIST C0$
0220 WOE22A: IOLIST W0$(1),W1$(1),W[ALL]
0230 WOE32A: IOLIST Z0$(1),Z1$(1),Z[ALL]
0240 BMM02A: IOLIST R0$,R1$,R2$,R[ALL]
0250 WOE22A1: IOLIST Z0$(1),Z1$(1),Z[ALL]
0260 WOE13A: IOLIST U0$,U1$,U[ALL]
0270 IVM02A: IOLIST B0$,B1$,B2$,B[ALL]
0280 WOE03E: IOLIST MATRSORT$
0290 IVM01A: IOLIST D0$,D1$(1),D2$,X$,X$,X$,X$,X$,X$,X$,D0
0300 IVM01X: IOLIST *,D1$(1),UM$
0310 BMM03A: IOLIST S0$,S1$,S[ALL]
0320 BMM05A: IOLIST S0$,S1$,S2$,S3$,S4$,S[ALL]
0330 WOE22A2: IOLIST X0$,X1$,X[ALL]
0340 BMM01A: IOLIST BM0$,BM1$,BM2$,BM[ALL]
0360 OPCODEA: IOLIST X0$,X0,X1,X2
0400 REM " --- Parameters 
0410 DIM SY_PARAMS[3],SY_CHANS[14],SY_ITEM$[6],SY_ITEM[12]
0420 LET SY_PARAMS[0]=P[0]
0430 LET SY_CHANS[1]=IVM01_DEV,SY_CHANS[14]=IVM14_DEV
0500 REM " --- Init Data
0510 LET MESS$=""
0520 LET L0=11,L1=L0+2,L9=19,K9$=""
0530 DIM W0$(15),W1$(151),W[13],V[3],BM[11],D1$(60),X[13],O[7]
0540 DIM MATRSORT$(37)
0550 LET W0$(1)=A0$(1,11)+"B",W1$(91,1)="S",L=L1
0560 LET M9$="###.00-",M9=LEN(M9$); REM "ALT FACTOR/DIVISOR MASK
0570 LET O[0]=75-M3-M2*2-M9,O[1]=76-M3-M2-M9,O[2]=77-M3-M2,O[3]=78-M3
0580 LET O[4]=75-M3-M2-M9*2,O[5]=76-M3-M2-M9,O[6]=77-M3-M2,O[7]=78-M3
0590 DIM T0$(0),T[10,1]
0600 LET X=0,T[X,0]=1,T[X,1]=1,T=1
0700 REM " --- Background
0710 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0720 PRINT @(0,L0),'CE'
0730 PRINT 'SB',@(2,L0-1),"Material Requirements",@(0,L0),"Seq Item Code",@(25),"UM Op Seq",@(O[1]-10),"Required",@(O[2]-7),"Scrap",@(66),"Each/Total"
0740 PRINT @(5),"Description",@(26),"Date Req",@(O[5]-9),"Divisor",@(O[6]-9),"Alt Fct",@(O[7]-7),"Units",@(73),"Cost"
0750 PRINT 'SF'
0800 IF O0=1 THEN GOTO 0810
0805 IF I9$="I" AND BM$="Y" THEN GOTO 0830 ELSE GOTO 1100
0810 GOSUB 5100
0815 IF K9$<>"" THEN GOTO 4000
0820 IF I9$<>"I" THEN GOTO 1100
0825 IF BM$<>"Y" THEN GOTO 1100
0830 READ (BMM02_DEV,KEY=A0$(1,2)+A0$(56,20),DOM=0835)
0835 LET K7$=KEY(BMM02_DEV,END=4000)
0840 IF K7$(3,20)=A0$(56,20) THEN GOTO 0940
0845 READ (WOE02_DEV,KEY=A0$(1,11)+"A",DOM=0850)
0850 LET X9$=KEY(WOE02_DEV,END=0900)
0855 IF X9$(1,11)=A0$(1,11) THEN GOTO 4000
0900 REM " --- No Bill Of Materials
0910 LET V0$="S",V1$="C",V2$="",V3$="",V4$="No Bill Of Materials Exists For This Item, <Enter> To Delete, <F1>=Accept: ",V0=1,V1=76,V2=22
0920 GOSUB 7000
0930 IF V3<>1 THEN LET O0=2; GOTO 4200
0940 LET O$=A0$(56,20)
0950 GOSUB 6000
0960 EXTRACT (BMM01_DEV,KEY=N0$+A0$(56,20),DOM=1000)IOL=BMM01A
0970 IF A0$(16,3)<BM2$(41,3) THEN GOTO 1000
0980 LET BM2$(41,4)=A0$(16,3)+"W"
0990 WRITE (BMM01_DEV,KEY=N0$+A0$(56,20))IOL=BMM01A
1000 GOTO 4000
1100 REM " --- Init Read
1110 IF A0$(15,1)="C" THEN GOTO 4000
1120 IF L>L9 THEN PRINT @(0,21),'CL',@(0,22),'CL',@(0,L1),'LD','LD'; LET L=L-2; GOTO 1120
1200 REM " --- Get Next Key"
1210 LET K$=KEY(WOE22_DEV,END=1300)
1220 IF POS(K$="")=1 THEN GOTO 1300
1230 IF POS(K$(1,2)=N0$)<>1 THEN GOTO 1300
1240 IF POS(K$(1,12)=A0$(1,11)+"B")=0 THEN GOTO 1300
1250 FIND (WOE22_DEV,KEY=K$)IOL=WOE22A
1260 DIM D1$(60); LET D1$(1)="*** Not On File ***"
1270 FIND (IVM01_DEV,KEY=N0$+W1$(27,20),DOM=1280)IOL=IVM01A
1280 LET D1$(1)=FNITEM$(D1$,DESC[1],DESC[2],DESC[3])
1290 GOTO 1440
1300 REM " --- Seq"
1310 IF A0$(14,1)="I" AND BM$="Y" THEN GOTO 4000
1320 PRINT @(0,21),'CL',@(0,22),'CL',
1330 IF L>L9 THEN PRINT @(0,L1),'LD','LD'; LET L=L-2; GOTO 1330
1340 IF V3<>2 THEN LET W0=NUM(W0$(13,3))+5
1350 LET V0$="Z",V1$="ER",V2$=STR(W0),V3$="",V0=3,V1=0,V2=L,I0=0,O9=1
1360 GOSUB 7000
1370 IF V3=4 THEN GOTO 4000
1380 IF V$="000" OR V$="" THEN GOTO 1350
1390 LET W0$(13,3)=V$
1400 FIND (WOE22_DEV,KEY=W0$(1,15),DOM=1500)IOL=WOE22A
1410 DIM D1$(60),D2$(10); LET D1$(1)="*** Not On File ***"
1420 FIND (IVM01_DEV,KEY=N0$+W1$(27,20),DOM=1430)IOL=IVM01A
1430 LET D1$(1)=FNITEM$(D1$,DESC[1],DESC[2],DESC[3])
1440 IF W0$(1,15)<=K9$ THEN GOSUB 5400
1450 GOSUB 5000
1460 GOTO 3300
1500 REM " --- Setup"
1510 DIM W[13]
1520 REM " --- Set Up Defaults
1530 LET W1$(1,90)="",W1$(4,3)=A0$(19,3),W1$(92)=""
1540 LET W[4]=1,W[5]=1,W[6]=1
1550 GOTO 3200
1600 REM " --- Item Code
1610 IF W1$(91,1)="M" THEN LET MESS$="Y"; GOTO 2900
1620 LET DEFAULT$=W1$(27,20),V4$="Enter A Valid Item Number",COLUMN=4,ROW=L
1625 LET ACTION$="N"
1630 FIND (WOE22_DEV,KEY=W0$(1,15),DOM=1660)IOL=WOE22A
1640 LET EXIST$="Y",V4$=V4$+", <F3>=Lookup"
1650 GOTO 1670
1660 LET V4$=V4$+", <F1>=Message Line, <F3>=Lookup",EXIST$="N"
1670 CALL "IVC.IA",ACTION$,COLUMN,ROW,SY_CHANS[ALL],N0$,DEFAULT$,V4$,SY_PARAMS[ALL],SY_ITEM$[ALL],SY_ITEM[ALL],V3
1675 LET V$=SY_ITEM$[0]
1680 IF EXIST$="N" THEN IF V3=1 THEN LET MESS$="Y",W1$(91,1)="M"; PRINT @(4,L),'CL',@(4,L+1),'CL',; GOTO 2900
1690 IF V3=2 OR V3=4 THEN GOTO 3200
1700 IF V3=3 THEN CALL "SYC.LI",V$
1710 IF V$="" THEN GOTO 1600
1720 LET W1$(27,20)=V$
1730 DIM D1$(60)
1740 FIND (IVM01_DEV,KEY=N0$+W1$(27,20),DOM=1600)IOL=IVM01A
1750 LET D1$(1)=FNITEM$(D1$,DESC[1],DESC[2],DESC[3])
1760 LET W1$(25,2)=A0$(54,2),W1$(27,20)=V$,W1$(1,2)=D2$(4,2)
1770 FIND (IVM02_DEV,KEY=N0$+W1$(25,2)+W1$(27,20),DOM=1600)IOL=IVM02A
1780 LET W[7]=B[11]
1790 GOSUB 3100
1800 GOSUB 5000
1810 GOTO 3200
1900 REM " --- Unit Of Measure
1910 IF W1$(91,1)="M" THEN GOTO 1950
1920 LET V0$="S",V1$="C",V2$=W1$(1,2),V3$="",V4$="Enter Unit Of Measure",V0=2,V1=25,V2=L
1930 GOSUB 7000
1940 LET W1$(1,2)=V$
1950 GOTO 3200
2000 REM " --- Date Required
2010 GOTO 2060
2020 IF POS(" "<>W1$(4,3))=0 THEN LET W1$(4,3)=F0$(7,3)
2030 LET V0$="D",V1$="C",V2$=W1$(4,3),V3$="",V4$="Enter The Date That This Item Will Be Required",V0=3,V1=H1-9,V2=L+1
2040 GOSUB 7000
2050 LET W1$(4,3)=V$
2060 GOTO 3200
2100 REM " --- Divisor
2110 IF W1$(91,1)="M" THEN GOTO 2170
2120 LET V0$="N",V1$="C",V2$=STR(W[4]),V3$=M9$,V0=0,V1=O[4],V2=L+1,V4$="Enter An Amount To Adjust Qty/Cost By (Must Be Greater Than 0)"
2130 GOSUB 7000
2140 IF V<=0 THEN GOTO 2100
2150 LET W[4]=V
2160 GOSUB 3100
2170 GOTO 3200
2200 REM " --- Quantity Required
2210 IF W1$(91,1)="M" THEN GOTO 2260
2220 LET V0$="N",V1$="C",V2$=STR(W[5]),V3$=M2$,V4$="Enter Qty Required To Make One Item",V0=0,V1=O[0],V2=L
2230 GOSUB 7000
2240 LET W[5]=V
2250 GOSUB 3100
2260 GOTO 3200
2300 REM " --- Alternate Factor
2310 IF W1$(91,1)="M" THEN GOTO 2370
2320 IF W[0]=0 AND V3<>2 THEN LET I0=I0+1; GOTO 3200
2330 LET V0$="N",V1$="C",V2$=STR(W[6]),V3$=M9$,V4$="Enter A Multiplying Factor",V0=0,V1=O[5],V2=L+1
2340 GOSUB 7000
2350 LET W[6]=V
2360 GOSUB 3100
2370 GOTO 3200
2400 REM " --- Operation Sequence Number"
2410 IF W1$(91,1)="M" THEN GOTO 2600
2420 LET V0$="Z",V1$="C",V2$=W1$(47,3),V3$="",V4$="Enter Operation Sequence Number This Material Follows, <F3>=Lookup",V0=3,V1=29,V2=L
2430 GOSUB 7000
2440 IF V3=2 THEN GOTO 2600
2450 IF V3<>3 THEN GOTO 2490
2460 LET V$=W0$(5,7)
2470 CALL "WOC.DA",V$,SYS01_DEV,OPCODE_DEV,WOE02_DEV
2480 GOTO 2510
2490 IF V3=4 THEN GOTO 2580
2500 IF V$="" THEN GOTO 2580
2510 FIND (WOE02_DEV,KEY=W0$(1,11)+"A"+V$,DOM=2530)
2520 GOTO 2580
2530 DIM MESSAGE$[2]
2540 LET MESSAGE$[0]="The Sequence Number Must Match An Operation"
2550 LET MESSAGE$[1]="Code Sequence.  <Enter> To Continue."
2560 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
2570 GOTO 2400
2580 LET W1$(47,3)=V$
2590 PRINT @(V1,V2),V$,
2600 GOTO 3200
2900 REM " --- Message
2910 LET V0$="S",V1$="C",V2$=W1$(92,60),V3$="",V4$="Enter Message",V0=60,V1=4,V2=L+1
2920 FIND (WOE22_DEV,KEY=W0$(1,15),DOM=2950)IOL=WOE22A
2930 LET EXIST$="Y"
2940 GOTO 2960
2950 LET V4$=V4$+", <F1>=Standard Line",EXIST$="N"
2960 GOSUB 7000
2970 IF EXIST$="N" THEN IF V3=1 THEN LET MESS$="N",W1$(91,1)="S"; PRINT @(4,L+1),'CL',; GOTO 1600
2980 LET W1$(92,60)=V$
2990 GOTO 3200
3100 REM " --- Calculate Totals Here
3110 LET W[0]=FNQTYNEW(X),W[1]=FNQTYNEW(X)*W[7]
3120 LET W[2]=A[0]*FNQTYNEW(X),W[3]=A[0]*W[1]
3130 PRECISION 2; LET W[3]=W[3]*1; PRECISION IVS01_PRECISION
3140 GOSUB 5000
3190 RETURN 
3200 REM " --- Seq Control"
3210 IF V3=4 THEN IF I0=1 THEN GOTO 3390 ELSE GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1300,1600,1900,2400,2200,5800,2000,2100,2300,3300
3300 REM " --- Correct"
3310 PRINT @(78,L),'CL',@(0,22),'CL'
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=W0$(1,15),V4$="<Enter>=OK, <F2>=Change, D=Delete",I0=9
3330 GOSUB 7000
3335 LET W0=NUM(W0$(13,3))
3340 IF V3=2 THEN GOTO 3200
3350 IF V$="" THEN GOTO 3390
3360 IF V$="D" THEN GOTO 3700
3370 GOTO 3300
3390 PRINT @(0,21),'LD','LD',
3400 REM " --- Write"
3410 IF W1$(91,1)="M" AND V3=4 AND I0=1 THEN GOTO 3700
3420 IF W1$(91,1)="S" THEN FIND (IVM01_DEV,KEY=N0$+W1$(27,20),DOM=3700)
3425 READ (IVM02_DEV,KEY=N0$+W1$(25,22),DOM=3430)IOL=IVM02A
3426 IF W1$(60,1)=" " THEN IF B1$(60,1)="Y" THEN LET W1$(60,1)="S"
3430 WRITE (WOE22_DEV,KEY=W0$(1,15))IOL=WOE22A
3435 LET MATRSORT$=N0$+"E  "+W1$(25,22)+W0$(5,7)+W0$(13,3)
3440 IF W1$(91,1)="S" THEN WRITE (WOE03_DEV,KEY=MATRSORT$)IOL=WOE03E
3450 LET W1$(92)=""
3500 REM " --- Now Check For Explosions
3505 IF BM$="N" OR W1$(91,1)<>"S" THEN GOTO 3800
3510 FIND (BMM01_DEV,KEY=N0$+W1$(27,20),DOM=3800)
3520 LET V4$="Explode This Item: "
3525 CALL "SYC.YN",1,V4$,1,V$,V3
3530 IF V$="N" THEN GOTO 3800
3540 IF V$<>"Y" THEN GOTO 3520
3550 LET O$=W1$(27,20),T=W[0],T[X,0]=W[0]
3560 REMOVE (WOE22_DEV,KEY=W0$,DOM=3570)
3570 REMOVE (WOE03_DEV,KEY=MATRSORT$,DOM=3580)
3580 GOSUB 6000
3590 GOTO 3880
3700 REM " --- Delete"
3720 REMOVE (WOE22_DEV,KEY=W0$(1,15),DOM=3725)
3725 LET MATRSORT$=N0$+"E  "+W1$(25,22)+W0$(5,7)+W0$(13,3)
3730 REMOVE (WOE03_DEV,KEY=MATRSORT$,DOM=3740)
3740 PRINT @(0,L),'LD','LD'
3780 GOTO 3880
3800 REM " --- All Done
3820 LET L=L+2
3880 IF V3=4 THEN GOTO 4000
3890 GOTO 1100
4000 REM " --- Options"
4005 LET V4$="Is The Detail Information Correct"
4010 IF A0$(15,1)="C" OR (A0$(14,1)="I" AND BM$="Y") THEN LET V4$=V4$+" (Y/List)?"; GOTO 4020
4015 LET V4$=V4$+" (Y/N/Review/List)?"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V0=1,V1=FNV(V4$),V2=22
4025 GOSUB 7000
4030 IF A0$(15,1)="C" OR (A0$(14,1)="I" AND BM$="Y") THEN IF V$="N" THEN LET V$=""; GOTO 4070 ELSE GOTO 4050
4040 IF V$="R" THEN GOTO 4400
4050 IF V$="L" THEN GOTO 4300
4070 ON POS(V$="YN") GOTO 4000,4100,1300
4100 REM " --- Close & Get Ready
4200 IF O0=0 THEN IF I9$="R" THEN RUN "WOE.AD" ELSE IF I9$="N" THEN RUN "WOE.AF"
4210 IF O0=0 THEN RUN "WOE.AK"
4240 LET O9=2
4250 RUN "WOE.AA"
4300 REM " --- List"
4310 GOSUB 5100
4320 GOTO 4000
4400 REM " --- Review"
4405 IF A0$(14,1)="I" AND BM$="Y" THEN GOTO 4100
4410 READ (WOE22_DEV,KEY=A0$(1,11)+"B",DOM=4420)
4420 GOSUB 5400
4430 LET V3=0
4440 GOTO 1100
5000 REM " --- Display"
5010 IF W1$(91,1)="M" THEN PRINT @(0,L),W0$(13,3),@(4,L+1),W1$(92,60),; GOTO 5090
5030 PRINT @(0,L),W0$(13,3),@(4),W1$(27,P[0]),@(25),W1$(1,2),@(29),W1$(47,3),@(O[0]),W[5]:M2$,@(O[1]),W[8]:M9$,@(O[2]),W[0]:M2$,@(O[3]),W[1]:M3$,
5050 PRINT @(5,L+1),D1$(1,20)+" "+FNB$(W1$(4,3)),@(O[4]),W[4]:M9$,@(O[5]),W[6]:M9$,@(O[6]),W[2]:M2$,@(O[7]),W[3]:M3$,
5090 RETURN 
5100 REM " --- List"
5110 GOSUB 5400
5120 READ (WOE22_DEV,KEY=A0$(1,11)+"B",DOM=5130)
5130 LET K$=KEY(WOE22_DEV,END=5300)
5135 IF K$(1,2)<>N0$ THEN GOTO 5300
5140 IF POS(K$(1,11)=A0$(1,11))=0 THEN GOTO 5300
5150 FIND (WOE22_DEV,KEY=K$)IOL=WOE22A
5155 DIM D1$(60); LET D1$(1)="*** Not On File ***"
5160 FIND (IVM01_DEV,KEY=W0$(1,2)+W1$(27,20),DOM=5180)IOL=IVM01A
5180 LET D1$(1)=FNITEM$(D1$,DESC[1],DESC[2],DESC[3])
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="KCE",V2$="",V3$="",V4$="<Enter>=Continue",V0=1,V1=FNV(V4$),V2=22
5215 GOSUB 7000
5220 IF V3=4 THEN GOTO 5300
5230 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+2,K9$=W0$(1,15)
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5420 PRINT @(0,L1),'CE',
5440 LET L=L1
5450 RETURN 
5500 REM " --- Explode Subcontracts here"
5505 IF P9$(3,1)="N" THEN GOTO 5690
5510 READ (BMM05_DEV,KEY=N0$+T9$,DOM=5520)
5520 DIM Z0$(15),Z1$(151),Z[13],S[9],X0$(31)
5530 LET Z0$(1)=A0$(1,11)+"C"
5550 REM " --- Read 4
5560 LET K8$=KEY(BMM05_DEV,END=5690)
5570 IF K8$(1,22)<>N0$+T9$ THEN GOTO 5690
5580 FIND (BMM05_DEV,KEY=K8$)IOL=BMM05A
5582 DIM Z[13]
5585 LET EDATE$=S2$(1,3),ODATE$=S2$(4,3); GOSUB 5900; IF OK$="N" THEN GOTO 5550
5595 LET Z1$(4,3)=A0$(19,3),Z1$(7,6)=S1$(4,6),Z1$(91,1)=S1$(1,1)
5600 IF S1$(1,1)="S" THEN LET Z1$(2,2)=S1$(2,2),Z1$(13,30)=S1$(10,30),Z1$(53,3)="",Z[0]=FNSUB(X),Z[1]=FNSUB(X)*S[2],Z[2]=Z[0]*A[0],Z[3]=Z[1]*A[0],Z[4]=S[2],Z[5]=S[4] ELSE LET Z1$(2,2)="",Z1$(13,30)=""
5605 LET Z0=POS(S0$(3,20)+S3$(1,3)=SUBS$,26); IF Z0>0 THEN LET Z1$(53,3)=SUBS$(Z0+23,3)
5610 IF S1$(1,1)<>"S" THEN LET Z1$(92,60)=S1$(10,60)
5620 READ (WOE32_DEV,KEY=Z0$(1,12)+$FF$,DOM=5630)
5630 LET Z0$(13,3)="000"
5640 LET Z$=KEYP(WOE32_DEV,END=5670)
5650 IF POS(Z0$(1,12)=Z$)=1 THEN LET Z0$(13,3)=Z$(13,3)
5660 IF Z0$(13,3)="999" THEN LET MODULE$="Subcontracts"; GOSUB 5700; GOTO 5690
5670 LET Z0$(13,3)=STR(NUM(Z0$(13,3))+1:"000")
5680 WRITE (WOE32_DEV,KEY=Z0$)IOL=WOE32A; GOTO 5550
5690 RETURN 
5700 REM " --- Issue warning for subs"
5710 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Not enough free sequence numbers for "+MODULE$+". <Enter> to continue ",V0=1,V1=FNV(V4$),V2=22
5720 PRINT 'RB',
5730 GOSUB 7000
5790 RETURN 
5800 REM " --- Scrap Factor
5805 IF W1$(91,1)="M" THEN GOTO 5890
5820 LET V0$="N",V1$="C",V2$=STR(W[8]),V3$=M9$,V4$="Enter A Scrap Factor If Needed (5 = 5%)",V0=0,V1=O[1],V2=L
5840 GOSUB 7000
5850 IF V>=100 THEN GOTO 5800
5860 LET W[8]=V
5870 GOSUB 3100
5890 GOTO 3200
5900 REM " --- Verify date within Effective and Obsolete dates"
5910 LET OK$="Y"
5920 IF EDATE$<>"   " THEN IF A0$(16,3)<EDATE$ THEN LET OK$="N"; GOTO 5990
5930 IF ODATE$<>"   " THEN IF A0$(16,3)>=ODATE$ THEN LET OK$="N"
5990 RETURN 
6000 REM " --- Explode Bills
6005 DIM YLD[99]
6010 LET T0$=O$,T9$=T0$,SUBS$="",MATS$=""
6015 LET YLD[0]=A[6],YLD=A[6]
6020 READ (BMM02_DEV,KEY=N0$+T9$,DOM=6025)
6025 DIM Z0$(15),Z1$(151),Z[13],R[10]
6030 LET Z0$(1)=A0$(1,11)+"B"
6035 LET K7$=KEY(BMM02_DEV,END=6900)
6040 IF POS(N0$+T9$=K7$)<>1 THEN GOTO 6900
6045 FIND (BMM02_DEV,KEY=K7$)IOL=BMM02A
6050 LET D0=0
6053 DIM B1$(64),B[16]
6055 FIND (IVM01_DEV,KEY=N0$+R1$,DOM=6057)IOL=IVM01A
6057 FIND (IVM02_DEV,KEY=N0$+A0$(54,2)+R1$,DOM=6058)IOL=IVM02A
6058 LET D0=B[11]
6060 FIND (BMM01_DEV,KEY=N0$+A0$(56,20),DOM=6065)IOL=BMM01A
6065 LET EDATE$=R2$(5,3),ODATE$=R2$(8,3); GOSUB 5900; IF OK$="N" THEN GOTO 6025
6070 LET Z0$(13,3)=STR(NUM(Z0$(13,3))+5:"000")
6075 IF R2$(4,1)="M" THEN LET Z1$(91,61)="M"+R2$(35,60); GOTO 6400
6080 DIM D1$(60); LET D1$(1)="*** Not On File ***",UM$="     "
6085 FIND (IVM01_DEV,KEY=N0$+R1$,DOM=6090)IOL=IVM01X
6090 LET Z1$(1,2)=UM$(4,2),Z1$(4,3)=A0$(19,3),Z1$(25,2)=A0$(54,2),Z1$(27,20)=R1$,T[X,1]=NUM(K7$(23)),Z1$(91)="S"
6095 IF R[0]=0 THEN LET R[0]=1
6100 IF R[1]=0 THEN LET R[1]=1
6105 IF R[3]=0 THEN LET R[3]=1
6110 LET Z[4]=R[3],Z[5]=R[0]*T,Z[6]=R[1],Z[7]=D0,Z[8]=R[4]
6115 LET YLD=YLD[X]
6120 IF YLD=0 THEN LET YLD=100
6125 LET Z[0]=FNQTY(X)*T,Z[1]=FNQTY(X)*D0*T
6130 LET Z[2]=FNQTY(X)*T*A[0],Z[3]=FNQTY(X)*D0*T*A[0]
6135 PRECISION 2; LET Z[3]=Z[3]*1; PRECISION IVS01_PRECISION
6200 REM " --- Phantom?
6210 FIND (BMM01_DEV,KEY=N0$+R1$,DOM=6400)IOL=BMM01A
6220 IF BM2$(40,1)="N" THEN GOTO 6400
6300 REM " --- Down One Level Here
6310 LET T0$=T0$+R1$,T9$=R1$,BMM$=R1$,X=LEN(T0$)/20-1,T[X,0]=Z[0],T[X,1]=NUM(K7$(23))
6320 LET T=T[X,0],YLD[X]=BM[4]
6350 EXTRACT (BMM01_DEV,KEY=N0$+BMM$,DOM=6390)IOL=BMM01A
6360 IF A0$(16,3)<BM2$(41,3) THEN GOTO 6390
6370 LET BM2$(41,4)=A0$(16,3)+"W"
6380 WRITE (BMM01_DEV,KEY=N0$+BMM$)IOL=BMM01A
6390 GOTO 6020
6400 REM " --- Write Materials Here
6410 READ (WOE22_DEV,KEY=Z0$(1,12)+$FF$,DOM=6420)
6420 LET Z0$(13,3)="000"
6430 LET Z$=KEYP(WOE22_DEV,END=6460)
6440 IF POS(Z0$(1,12)=Z$)=1 THEN LET Z0$(13,3)=Z$(13,3)
6450 IF Z0$(13,3)="999" THEN LET MODULE$="Materials"; GOSUB 5700; GOTO 6900
6460 LET Z0$(13,3)=STR(NUM(Z0$(13,3))+1:"000")
6465 IF B1$(60,1)="Y" THEN LET Z1$(60,1)="S"
6470 WRITE (WOE22_DEV,KEY=Z0$)IOL=WOE22A1
6475 LET MATRSORT$=N0$+"E  "+Z1$(25,22)+Z0$(5,7)+Z0$(13,3)
6480 IF Z1$(91,1)<>"M" THEN WRITE (WOE03_DEV,KEY=MATRSORT$)IOL=WOE03E
6485 IF R2$(1,3)<>"   " THEN LET MATS$=MATS$+R0$(3,20)+R2$(1,3)+Z0$(13,3)
6490 GOTO 6025
6500 REM " --- Do Labor Here
6510 LET YLD=A[6]
6520 READ (BMM03_DEV,KEY=N0$+T9$,DOM=6540)
6540 DIM Z0$(15),Z1$(151),Z[13],S[11],X0$(31)
6550 LET Z0$(1)=A0$(1,11)+"A"
6600 REM " --- Read 3
6610 LET K8$=KEY(BMM03_DEV,END=6895)
6613 LET D0=0
6615 FIND (IVM01_DEV,KEY=N0$+W1$(27,20),DOM=6620)IOL=IVM01A
6620 IF K8$(1,22)<>N0$+T9$ THEN GOTO 6895
6625 FIND (BMM03_DEV,KEY=K8$)IOL=BMM03A
6630 FIND (BMM01_DEV,KEY=N0$+A0$(56,20),DOM=6635)IOL=BMM01A
6635 LET EDATE$=S1$(65,3),ODATE$=S1$(68,3); GOSUB 5900; IF OK$="N" THEN GOTO 6600
6640 LET X0=0,X1=0; DIM X0$(90)
6645 IF S1$(4,1)="M" THEN GOTO 6660
6650 FIND (OPCODE_DEV,KEY=N0$+S1$(1,3),DOM=6600)IOL=OPCODEA
6660 LET Z1$(1,3)=S1$(1,3),Z1$(4,3)=A0$(19,3),Z1$(7,20)=X0$(6,20),Z1$(91)=S1$(4,1)
6670 IF S1$(4,1)="M" THEN LET Z1$(92,60)=S1$(5,60); GOTO 6800
6680 IF S[1]=0 THEN IF X1<>0 THEN LET S[1]=X1 ELSE LET S[1]=1
6690 LET Z[4]=S[0]*T/YLD*100,Z[5]=S[1],Z[6]=X0,Z[7]=Z[6]*X2,Z[8]=S[4],Z[9]=S[5]
6700 LET Z[0]=FNOPNOSET(X)*T,Z[1]=FNOPNOSETDOL(X)*T,Z[2]=FNOP(X)
6705 LET TOT_UNITS=FNOP(X)
6706 LET TOT_COST=Z[6]+Z[7]
6707 LET Z[3]=TOT_UNITS*TOT_COST
6710 PRECISION 2; LET Z[3]=Z[3]*1; PRECISION IVS01_PRECISION
6800 REM " --- Write Operation Records"
6810 READ (WOE02_DEV,KEY=Z0$(1,12)+$FF$,DOM=6820)
6820 LET Z0$(13,3)="000",OCC=1
6825 LET Z$=KEYP(WOE02_DEV,END=6840)
6830 IF POS(Z0$(1,12)=Z$)=1 THEN LET Z0$(13,3)=Z$(13,3)
6835 IF Z0$(13,3)="999" THEN LET MODULE$="Operations"; GOSUB 5700; GOTO 6900
6840 LET Z0$(13,3)=STR(NUM(Z0$(13,3))+1:"000")
6845 WRITE (WOE02_DEV,KEY=Z0$)IOL=WOE02A
6850 LET SUBS$=SUBS$+T9$+S0$(23,3)+Z0$(13,3)
6855 DIM Z[13]
6860 IF Z1$(91,1)="S" THEN LET C0$=N0$+"D  "+Z1$(1,6)+Z0$(5,7)+Z0$(13,3)
6863 IF Z1$(91,1)="S" THEN WRITE (WOE03_DEV,KEY=C0$)IOL=WOE03D
6865 LET Y0=POS(S0$(3,23)=MATS$,26,OCC)
6870 IF Y0=0 THEN GOTO 6890
6875 EXTRACT (WOE22_DEV,KEY=N0$+Z0$(3,9)+"B"+MATS$(Y0+23,3),DOM=6890)IOL=WOE22A2
6880 LET X1$(47,3)=Z0$(13,3)
6885 WRITE (WOE22_DEV,KEY=N0$+Z0$(3,9)+"B"+MATS$(Y0+23,3))IOL=WOE22A2
6887 LET OCC=OCC+1; GOTO 6865
6890 GOTO 6600
6895 RETURN 
6900 REM " --- Back Up The Levels
6910 IF T0$=O$ THEN GOTO 6955
6912 GOSUB 6500; REM "Do component labor
6913 GOSUB 5500; REM "Do component subs
6915 LET T[X,0]=0,T[X,1]=0
6920 LET T0$=T0$(1,X*20),X=X-1,T9$=T0$(X*20+1,20),T=T[X,0]
6925 IF X=0 THEN GOTO 6940
6930 LET T=T[X,0]
6940 READ (BMM02_DEV,KEY=N0$+T9$+STR(T[X,1]:"000"),DOM=6920)
6950 GOTO 6025
6955 LET T9$=T0$; GOSUB 6500; REM "Now do final assembly labor
6960 GOSUB 5500; REM "Now do final assembly subs
6970 DIM Z0$(15),Z1$(151),Z[13],S[11]
6980 READ (WOE22_DEV,KEY=A0$(1,11)+"B",DOM=6990)
6990 GOSUB 5100
6995 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8015 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8065 DEF FNSUB(Q)=S[0]*S[1]/S[3]
8070 DEF FNOPNOSET(Q)=100*(S[0]/S[1])/YLD
8075 DEF FNOPNOSETDOL(Q)=100*(S[0]*(Z[6]+Z[7])/S[1])/YLD
8080 DEF FNOP(Q)=T*A[0]*100*(S[0]/S[1])/YLD+S[4]
8090 DEF FNQTY(Q)=10000*R[0]*R[1]/(R[3]*YLD*(100-R[4]))
8092 DEF FNQTYNEW(Q)=100*W[5]*W[6]/(W[4]*(100-W[8]))/(A[6]*.01)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9215 LET O9=2
9220 RUN "WOE.AA"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

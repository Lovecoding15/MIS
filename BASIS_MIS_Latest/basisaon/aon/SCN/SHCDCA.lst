0010 REM "SHC - RPS Freight Calculation"
0020 REM "Program SHCDCA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.2 - 01Feb97 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "01Feb97 CAJ: Modified for 1997 SpeedShip Update"
0040 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER A0$,A1$,W0$,W1$,W[ALL],CHARGE,ZIPLENGTH,ZIPCODE$,ZONE$,SERVICE$,RESERVED$,RATEFILE$,ZONEFILE$,SPEEDSHIP$
0100 REM " --- Open/Lock Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]=RATEFILE$
0140 IF W1$(55,1)="Y" THEN LET FILES$[2]="SHM-10",FILES$[3]="SHM-47"
0160 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS THEN GOTO 9900
0180 LET SHM41_DEV=CHANNELS[1],SHM10_DEV=CHANNELS[2],SHM47_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 SHM41A: IOLIST X$,F[ALL]
0220 SHM10C: IOLIST X$,R[ALL]
0500 REM " --- Initializations"
0510 DIM Z[28],F[36],R[20]
0520 LET Z[0]=2,Z[1]=3,Z[2]=4,Z[3]=5,Z[4]=6,Z[5]=7,Z[6]=8
0530 LET Z[7]=9,Z[8]=10,Z[9]=17,Z[10]=18,Z[11]=21,Z[12]=27
0540 LET Z[13]=28,Z[14]=29,Z[15]=72,Z[16]=73,Z[17]=74,Z[18]=75
0550 LET Z[19]=76,Z[20]=77,Z[21]=78,Z[22]=51,Z[23]=54,Z[24]=61
0555 LET Z[25]=67,Z[26]=41,Z[27]=42,Z[28]=43
0560 LET CHARGE=-1,WEIGHT=W[0],CARRIER$=W1$(11,1),FIRM$=A0$(1,2)
0570 LET RESIDENTIAL=0,XLIMIT=150
0580 IF POS(SERVICE$(79,1)="DA")>0 THEN LET XLIMIT=100
0600 REM " --- Additional file opens"
0610 IF W1$(55,1)<>"Y" THEN GOTO 1000
0620 FIND (SHM10_DEV,KEY=FIRM$+"C"+W1$(4,7),DOM=0800)IOL=SHM10C
0670 LET RESIDENTIAL=R[0],RURAL=R[17],WAREHOUSE$=SPEEDSHIP$(70,2)
0800 REM " --- Rural residential?"
0890 READ (SHM47_DEV,KEY=FIRM$+WAREHOUSE$+CARRIER$+ZIPCODE$(1,1),DOM=0900)
0900 REM " --- Lookup zip code in zone chart"
0910 LET K$=KEY(SHM47_DEV,END=1000)
0920 IF POS(FIRM$=K$)<>1 THEN GOTO 1000
0930 READ (SHM47_DEV)
0940 IF K$(3,2)<>WAREHOUSE$ THEN GOTO 1000
0950 IF K$(6,5)>ZIPCODE$(1,5) THEN GOTO 1000
0960 IF K$(11,5)<ZIPCODE$(1,5) THEN GOTO 0900
0970 LET RESIDENTIAL=RURAL
1000 REM " --- Check for valid zone"
1010 LET Z=NUM(ZONE$,ERR=9900),ZONE=-1
1020 FOR X=0 TO 28
1030 IF Z=Z[X] THEN LET ZONE=X
1040 NEXT X
1050 IF ZONE<0 THEN GOTO 4000
1200 REM " --- Check for valid weight"
1210 IF WEIGHT<0 THEN GOTO 4000
1220 IF WEIGHT>XLIMIT THEN GOTO 4000
1230 IF W1$(16,1)="Y" THEN IF WEIGHT<25 THEN LET WEIGHT=25
2000 REM " --- Consult freight rate table"
2010 FIND (SHM41_DEV,KEY=FIRM$+CARRIER$+STR(WEIGHT:"000"),DOM=4000)IOL=SHM41A
2020 LET CHARGE=F[ZONE]+RESIDENTIAL
4000 REM " --- All done"
4090 GOTO 9900
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9900 REM " --- Return to caller"
9910 CLOSE (SHM41_DEV,ERR=9920)
9920 CLOSE (SHM10_DEV,ERR=9930)
9930 CLOSE (SHM47_DEV,ERR=9940)
9950 EXIT 
9999 END

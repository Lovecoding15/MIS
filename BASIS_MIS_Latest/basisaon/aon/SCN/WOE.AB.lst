0010 REM "WOS - Work Order Processing - Header Entry & WO Delete "
0020 REM "Program WOE.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.1 - 22Jul96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0031 REM "12Jul96 JJD: Include PRECISION and Parameter information when adding a new work order (70,90,95)"
0032 REM "12Jul96 JJD: Allow item synonym entry (400,410,420,430,1740,1750,1755).  See related SCN in WOE.AB dated 12Jul96"
0033 REM "24Apr97 JWK: Writes WOE-01 header incorrectly (210)
0040 REM 
0060 SETERR 9000
0070 SETESC 9000
0090 PRECISION IVS01_PRECISION
0095 ON O9 GOTO 0400,3700,3700,DELETE_WORKORDER
0200 REM " --- IOLISTs"
0210 WOE01A: IOLIST A0$(1),A1$(1),A[ALL]
0220 WOM10A: IOLIST X1$
0230 WOE03A: IOLIST C0$
0240 IVM01A: IOLIST D0$,D1$(1),D2$,D3$,D4$,D5$,D6$,D[ALL]
0250 BMM01A: IOLIST B0$,B1$,B2$,B[ALL]
0320 WOE13A: IOLIST W0$,W1$,W[ALL]
0325 WOE23A: IOLIST W0$,W1$,W[ALL]
0330 WOE02A: IOLIST R0$,R1$,R[ALL]
0335 WOE22A: IOLIST R0$,R1$,R[ALL]
0340 WOREQA: IOLIST R0$,R1$,R[ALL]
0400 REM " --- Parameters
0410 DIM SY_PARAMS[3],SY_CHANS[14],SY_ITEM$[6],SY_ITEM[12]
0420 LET SY_PARAMS[0]=P[0]
0430 LET SY_CHANS[1]=IVM01_DEV,SY_CHANS[14]=IVM14_DEV
0500 REM " --- Init Data"
0510 DIM B[11]
0520 LET NUMBER=1
0700 REM " --- Background"
0790 PRINT 'SF',
1000 REM " --- Work Order Type"
1010 IF O0<>0 THEN GOSUB VERIFY_WIP
1020 LET V0$="S",V1$="CR",V2$=A0$(12,2),V3$="",V4$="Enter Valid Work Order Type (<F3>=Lookup)",V0=2,V1=26,V2=3,I0=0,V3=0
1030 GOSUB 7000
1040 IF V3=3 THEN GOTO 1070
1050 IF V3=2 OR V3=4 AND O0=0 THEN GOTO 5170
1060 GOTO 1140
1070 DIM DESC$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
1080 LET DESC$[0]="Type",DESC$[1]="Description"
1090 LET FIELD[0]=1,POSITION[0]=4,LENGTH[0]=2
1100 LET FIELD[1]=1,POSITION[1]=6,LENGTH[1]=20
1110 LET TITLE$="Work Order Type Lookup",COLUMN=5,ROW=5,SEL$="",RECORD$="A"
1120 CALL "SYC.LK",WOM10_DEV,RECORD$,NUMBER,TITLE$,DESC$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,SEL$
1130 LET V$=SEL$
1140 LET V$=V$+"  ",V$=V$(1,2)
1150 FIND (WOM10_DEV,KEY=N0$+"A"+V$,DOM=1000)IOL=WOM10A
1160 IF A0$(14,1)=" " OR A0$(14,1)=X1$(26,1) THEN GOTO 1230
1170 DIM MSG$[2]
1180 LET MSG$[0]="Category For Selected Type Is Not The Same As Existing Category."
1190 LET MSG$[1]="The Work Order Category May Not Be Changed. <Enter> To Reselect."
1200 LET MSG$[2]="To Change Category, You Must Delete This Work Order."
1210 CALL "SYC.XA",2,MSG$[ALL],2,22,-1,V$,V3
1220 GOTO 1000
1230 LET A0$(12,2)=V$,A0$(14,1)=X1$(26,1),I9$=X1$(26,1)
1240 PRINT @(V1,V2),A0$(12,2),"  ",X1$(6,20),@(65,4),I9$
1250 IF I9$="I" THEN PRINT @(67,4),"Inventory", ELSE IF I9$="N" THEN PRINT @(67,4),"Non-Stocking", ELSE IF I9$="R" THEN PRINT @(67,4),"Recurring"
1260 GOTO 3600
1500 REM "Quoted?
1510 IF POS(A0$(15,1)="OC")>0 THEN GOTO 1580
1520 LET V0$="S",V1$="C",V2$=A0$(15,1),V3$="PQ",V4$="Enter Work Order Status (P=Planned/Q=Quoted)",V0=1,V1=65,V2=3
1530 GOSUB 7000
1540 ON V3 GOTO 1550,1500,1580,1500,1580
1550 IF V$="P" THEN LET STATUS$="*Planned*" ELSE IF V$="Q" THEN LET STATUS$="*Quoted*" ELSE GOTO 1500
1560 PRINT @(V1,V2),'CL',V$," ",STATUS$
1570 LET A0$(15,1)=V$
1580 GOTO 3600
1700 REM " --- Item Code
1710 IF O0<>0 THEN GOTO 3600
1720 DIM D[8]
1730 IF I9$<>"I" THEN LET A0$(56)=""; GOTO 3600
1740 LET ACTION$="N",DEFAULT$=A0$(56,20),V4$="Enter A Valid Bill Of Materials Number (<F3>=Lookup)",V0=P0,COLUMN=13,ROW=5
1745 IF BM$<>"Y" THEN LET V4$(15)="Inventory Item Number (<F3>=Lookup)"
1750 CALL "IVC.IA",ACTION$,COLUMN,ROW,SY_CHANS[ALL],N0$,DEFAULT$,V4$,SY_PARAMS[ALL],SY_ITEM$[ALL],SY_ITEM[ALL],V3
1755 LET V$=SY_ITEM$[0]
1760 IF V3=2 THEN GOTO 3600
1770 IF LEN(V$)<P0 THEN LET V$=V$+J1$,V$=V$(1,20)
1780 IF V3=3 AND BM$="Y" THEN CALL "SYC.LB",V$
1785 IF V3=3 AND BM$="N" THEN CALL "SYC.LI",V$
1790 DIM D1$(60)
1800 FIND (IVM01_DEV,KEY=N0$+V$,DOM=1700)IOL=IVM01A
1810 IF A0$(50,2)="  " THEN LET A0$(50,2)=D2$(4,2)
1820 IF P3$(17,1)<>"N" AND D2$(19,2)="YY" THEN LET A1$(96,1)="Y" ELSE LET A1$(96,1)="N"
2000 REM " --- Bill Of Materials?
2010 LET A[6]=100
2020 IF BM$<>"Y" THEN GOTO 2240
2030 FIND (BMM01_DEV,KEY=N0$+V$,DOM=2200)IOL=BMM01A
2040 LET A1$(61,30)=B0$(P0+3),A[6]=B[4],A[0]=B[5]
2050 LET A0$(50,2)=B2$(48,2),A0$(52,2)=B2$(38,2)
2060 GOTO 2240
2200 REM " --- No Bill Of Materials Here
2210 LET V4$="No Bill Of Materials Exists For This Item"
2220 GOSUB 5900
2230 GOTO 1700
2240 LET A0$(56)=V$
2250 GOSUB 5500
2260 GOTO 3600
2500 REM " --- Description
2510 IF I9$="I" THEN IF V3<>2 THEN LET I0=I0+2; GOTO 3600 ELSE GOTO 3600
2520 FOR X0=0 TO 1
2530 LET V0$="S",V1$="",V2$=A1$(X0*30+1,30),V3$="",V0=30,V1=13,V2=X0+6
2540 GOSUB 7000
2550 IF V3=4 THEN EXITTO 3700
2560 IF V3=2 THEN IF X0=0 THEN EXITTO 2600 ELSE LET X0=X0-1; GOTO 2530
2570 LET A1$(X0*30+1,30)=V$
2580 NEXT X0
2590 IF I9$="R" THEN LET I0=I0+2
2600 GOTO 3600
2700 REM " --- Drawing Number"
2710 LET V0$="S",V1$="",V2$=A1$(61,25),V3$="",V0=25,V1=11,V2=8
2720 GOSUB 7000
2730 IF V3=4 THEN GOTO 2760
2740 IF V3=2 THEN GOTO 3600
2750 LET A1$(61,25)=V$
2760 GOTO 3600
2800 REM " --- Drawing Number"
2810 LET V0$="S",V1$="",V2$=A1$(86,5),V3$="",V0=5,V1=42,V2=8
2820 GOSUB 7000
2830 IF V3=4 THEN GOTO 2860
2840 IF V3=2 THEN GOTO 3600
2850 LET A1$(86,5)=V$
2860 GOTO 3600
2900 REM " --- Estimated Yield Percent"
2910 IF O0=1 AND A0$(14,1)="N" THEN GOTO 2920 ELSE GOTO 2950
2920 LET V4$="Requirements May Need Adjustment If Yield Changes"
2940 GOSUB 5900
2950 IF A0$(14,1)="I" THEN LET FLD$="Estimated Yield"; GOSUB 6000
2960 IF A[6]=0 THEN LET A[6]=100
2970 LET V0$="N",V1$="C",V2$=STR(A[6]),V3$="##0.0",V0=5,V1=42,V2=9,V4$="Enter Estimated Yield."
2980 GOSUB 7000
2990 IF V=0 OR V>100 THEN LET V4$="Yield Must Be Between 1 And 100"; GOSUB 5900; GOTO 2970
3000 LET A[6]=V
3010 GOTO 3600
3100 REM " --- Scheduled Production Amount
3110 IF O0=1 AND A0$(14,1)="N" THEN GOTO 3120 ELSE GOTO 3150
3120 LET V4$="Requirements May Need Adjustment If Qty Changes"
3140 GOSUB 5900
3150 IF A0$(14,1)="I" THEN LET FLD$="Production Qty"; GOSUB 6000
3160 LET V0$="N",V1$="C",V2$=STR(A[0]),V3$=M2$,V0=7,V1=18,V2=9,V4$="Enter Quantity To Be Built. Enter 1 If Not Applicable."
3170 GOSUB 7000
3180 IF V<=0 THEN LET V4$="Quantity Must Be Positive"; GOSUB 5900; GOTO 3160
3190 LET A[0]=V
3200 GOTO 3600
3300 REM " --- Unit Of Measure
3310 LET V0$="S",V1$="C",V2$=A0$(50,2),V3$="",V4$="Enter Unit Of Measure",V0=2,V1=73,V2=6
3320 GOSUB 7000
3330 LET A0$(50,2)=V$
3340 GOTO 3600
3350 REM " --- Revision Number
3360 LET V0$="S",V1$="C",V2$=A0$(52,2),V3$="",V4$="Enter Revision Number",V0=2,V1=65,V2=6
3370 GOSUB 7000
3380 LET A0$(52,2)=V$
3390 GOTO 3600
3400 REM " --- Opened Date
3410 LET X0$=A0$(16,3)
3420 LET V0$="D",V1$="C",V2$=A0$(16,3),V3$="",V4$="Enter Date Work Order Was Initiated (<Enter>="+FNB$(A0$(16,3))+")",V0=3,V1=65,V2=5
3430 GOSUB 7000
3440 LET A0$(16,3)=V$
3450 IF X0$<>"   " AND A0$(16,3)<>X0$ THEN GOSUB 6400
3460 GOTO 3600
3600 REM " --- Next Field"
3610 IF V3=4 THEN GOTO 3680
3620 IF V3<>2 THEN GOTO 3650
3630 IF I0>0 THEN LET I0=I0-1
3640 GOTO 3660
3650 LET I0=I0+1
3660 IF ABS(I0)>1000 THEN GOTO 3680
3670 ON I0 GOTO 1000,1500,1700,2500,2700,2800,3100,2900,3400,3350,3300,3680
3680 LET I0=K0
3700 REM " --- Options"
3710 IF A0$(14,1)="I" AND FNP$(A0$(56))="" THEN LET I0=2; GOTO 1700
3720 IF A0$(15,1)="C" THEN GOTO 3880
3730 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/#/Delete)?",V0=7,V1=FNV(V4$),V2=22
3740 GOSUB 7000
3750 LET I=INT(NUM(V$,ERR=3770))
3760 IF I>0 THEN LET I0=I-2; GOTO 3600
3770 IF V$="DELETE" THEN GOTO DELETE_WORKORDER
3780 IF V3 THEN GOTO 3700
3790 ON POS(V$="YN") GOTO 3700,3800,1000
3800 REM " --- Write "
3810 IF A[6]=0 THEN LET A[6]=100
3820 WRITE (WOE01_DEV,KEY=A0$(1,K9))IOL=WOE01A
3830 IF S9$=A0$(12,3)+A0$(34,16)+A0$(54) AND S9=A[0] THEN GOTO 3880
3840 REM " --- W/O Type Sort
3850 REMOVE (WOE03_DEV,KEY=N0$+"A"+S9$(1,2)+A0$(3,9),DOM=3860)
3860 LET C0$=N0$+"A"+A0$(12,2)+A0$(3,9)
3865 WRITE (WOE03_DEV,KEY=C0$)IOL=WOE03A
3870 LET S9$(1,2)=A0$(12,2)
3880 RUN "WOE.AC"
3900 REM " --- Delete Work Order - Check For Open Transactions "
3910 DELETE_WORKORDER: 
3920 DIM PARAMS[10],PARAMS$[10],ITEM$[3],REF$[11],REFS[5],IVFILES[44]
3930 LET IVFILES[0]=SYS01_DEV,IVFILES[1]=IVM01_DEV,IVFILES[2]=IVM02_DEV
3935 LET IVFILES[7]=IVM07_DEV,IVFILES[17]=IVM17_DEV
3940 LET PARAMS[0]=P0,PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3)
3950 LET DELETE$="YES",WOTRAN_DEV=WOT01_DEV
3960 READ (WOTRAN_DEV,KEY=A0$(1,11),DOM=3970)
3970 LET K9$=KEY(WOTRAN_DEV,END=4000)
3980 GOSUB CHECK_FOR_OPEN_TRANS
3990 IF DELETE$="NO" THEN GOTO CANNOT_DELETE
4000 IF WOTRAN_DEV=WOT01_DEV THEN LET WOTRAN_DEV=WOT21_DEV; GOTO 3960
4010 IF WOTRAN_DEV=WOT21_DEV THEN LET WOTRAN_DEV=WOT31_DEV; GOTO 3960
4020 GOTO BEGIN_DELETE
4030 REM " --- Check For Open Transactions "
4040 CHECK_FOR_OPEN_TRANS: 
4050 IF POS(N0$=K9$)<>1 THEN GOTO 4070
4060 IF POS(K9$(1,11)=A0$(1,11))=1 THEN LET DELETE$="NO"
4070 RETURN 
4080 CANNOT_DELETE: 
4090 LET V4$="Work Orders In Process Can Not Be Deleted"
4110 GOSUB 5900
4190 GOTO 3700
4200 REM " --- Begin Delete Here "
4210 BEGIN_DELETE: 
4220 REM " --- Remove Material Commitments & Uncommit Inventory "
4230 DIM W[4],PARAMS[0],PARAMS$[4],ITEMS$[3],REFS$[11],REFS[5]
4240 LET PARAMS$[0]=F0$(7,3),PARAMS$[1]=F0$(4,3),PARAMS$[2]=P2$,PARAMS$[3]=P3$,PARAMS$[4]=P4$
4250 READ (WOE13_DEV,KEY=A0$(1,11)+"000",DOM=REMOVE_WOE23)
4260 REMOVE (WOE13_DEV,KEY=A0$(1,11)+"000")
4270 REMOVE_WOE23: 
4280 DIM W0$(14),W1$(40),W[4]
4290 READ (WOE23_DEV,KEY=A0$(1,11)+"000",DOM=4300)
4300 LET K9$=KEY(WOE23_DEV,END=4400)
4310 IF POS(K9$(1,11)=A0$(1,11))<>1 THEN GOTO 4400
4320 FIND (WOE23_DEV,KEY=K9$)IOL=WOE23A
4330 REM " --- Reduce On Order by (Qty Ordered - Issued-to-date) "
4340 LET REFS[0]=W[0]-W[1]
4345 IF REFS[0]<=0 THEN GOTO 4365
4350 LET ACTION$="UC",ITEMS$[0]=W0$(1,2),ITEMS$[1]=W1$(19,2),ITEMS$[2]=W1$(21,20)
4360 GOSUB UPDATE_INVENTORY
4365 REMOVE (WOE23_DEV,KEY=K9$,DOM=4370)
4370 GOTO 4300
4400 REM " --- Remove Work Order Comments "
4410 READ (WOE07_DEV,KEY=A0$(1,11),DOM=4420)
4420 LET K9$=KEY(WOE07_DEV,END=4460)
4430 IF POS(K9$(1,11)=A0$(1,11))<>1 THEN GOTO 4460
4440 REMOVE (WOE07_DEV,KEY=K9$,DOM=4450)
4450 GOTO 4420
4460 REM " --- Remove Material Issues "
4470 FIND (WOE15_DEV,KEY=A0$(1,11)+"000",DOM=REMOVE_WOE25)
4480 REMOVE (WOE15_DEV,KEY=A0$(1,11)+"000")
4490 REMOVE_WOE25: 
4500 READ (WOE25_DEV,KEY=A0$(1,11)+"000",DOM=4510)
4510 LET K9$=KEY(WOE25_DEV,END=4550)
4520 IF POS(K9$(1,11)=A0$(1,11))<>1 THEN GOTO 4550
4530 REMOVE (WOE25_DEV,KEY=K9$)
4540 GOTO 4510
4550 REM " --- Remove Work Order Transactions "
4560 REMOVE (WOE04_DEV,KEY=N0$+"  "+"A"+A0$(5,7),DOM=4570)
4570 REMOVE (WOE04_DEV,KEY=N0$+"  "+"B"+A0$(5,7),DOM=4580)
4580 REMOVE (WOE04_DEV,KEY=N0$+"  "+"C"+A0$(5,7),DOM=4590)
4590 REMOVE (WOE04_DEV,KEY=N0$+"  "+"D"+A0$(5,7),DOM=4600)
4600 REM " --- Remove Work Order Requirements & Cross References "
4620 DIM R0$(15),R1$(151),R[13]
4630 LET WOREQ_DEV=WOE02_DEV,RECORD$="D"
4640 READ (WOREQ_DEV,KEY=A0$(1,11),DOM=4650)
4650 LET K$=KEY(WOREQ_DEV,END=4800)
4660 IF POS(K$(1,11)=A0$(1,11))<>1 THEN GOTO 4800
4670 FIND (WOREQ_DEV,KEY=K$)IOL=WOREQA
4680 ON POS(RECORD$="DEF")-1 GOTO OPERATIONS,MATERIALS,SUBCONTRACTS
4690 OPERATIONS: 
4700 LET WOE03_KEY$=A0$(1,2)+"D"+R0$(3,2)+R1$(1,6)+R0$(5,7)+R0$(13,3)
4710 GOTO 4770
4720 MATERIALS: 
4730 LET WOE03_KEY$=A0$(1,2)+"E"+R0$(3,2)+R1$(25,22)+R0$(5,7)+R0$(13,3)
4740 GOTO 4770
4750 SUBCONTRACTS: 
4760 LET WOE03_KEY$=A0$(1,2)+"F"+R0$(3,2)+R1$(7,6)+R0$(5,7)+R0$(13,3)
4770 GOSUB REMOVE_CROSS_REFERENCE
4780 REMOVE (WOREQ_DEV,KEY=K$)
4790 GOTO 4650
4800 REM " --- File Change " 
4810 IF RECORD$="D" THEN LET WOREQ_DEV=WOE22_DEV; LET RECORD$="E"; GOTO 4640
4820 IF RECORD$="E" THEN LET WOREQ_DEV=WOE32_DEV; LET RECORD$="F"; GOTO 4640
4830 REM " --- Remove Closed Work Order Transactions & Cross References "
4840 LET WOTRAN_DEV=WOT03_DEV
4850 READ (WOTRAN_DEV,KEY=A0$(1,11),DOM=4860)
4860 LET K$=KEY(WOTRAN_DEV,END=4910)
4870 IF POS(K$(1,11)=A0$(1,11))<>1 THEN GOTO 4910
4880 REMOVE (WOTRAN_DEV,KEY=K$)
4890 REMOVE (WOM07_DEV,KEY=K$,DOM=4900)
4900 GOTO 4860
4910 IF WOTRAN_DEV=WOT03_DEV THEN LET WOTRAN_DEV=WOT23_DEV; GOTO 4850
4920 IF WOTRAN_DEV=WOT23_DEV THEN LET WOTRAN_DEV=WOT33_DEV; GOTO 4850
4930 REM " --- Remove Schedule Detail & Sort Records "
4940 READ (WOM06_DEV,KEY=A0$(1,2)+A0$(5,7),DOM=4950)
4950 LET K$=KEY(WOM06_DEV,END=5000)
4960 IF POS(A0$(1,2)+A0$(5,7)=K$)<>1 THEN GOTO 5000
4970 REMOVE (WOM06_DEV,KEY=K$)
4980 REMOVE (WOM05_DEV,KEY=K$(1,2)+K$(13,6)+K$(3,10),DOM=4990)
4990 GOTO 4950
5000 REM " --- Remove Work Order Type & Customer Number Sort Records "
5010 LET WOE03_KEY$=N0$+"A"+A0$(12,2)+A0$(3,9)
5020 GOSUB REMOVE_CROSS_REFERENCE
5030 LET WOE03_KEY$=N0$+"B"+A0$(34,16)+A0$(3,9)
5040 GOSUB REMOVE_CROSS_REFERENCE
5050 REM " --- Reduce Inventory Item "ON ORDER" Amount "
5055 IF POS(A0$(15,1)="PQ")>0 THEN GOTO 5110
5060 IF A0$(14,1)<>"I" THEN GOTO 5140
5070 IF O0=0 THEN GOTO 5140
5080 LET ACTION$="OO",ITEMS$[0]=N0$,ITEMS$[1]=A0$(54,2),ITEMS$[2]=A0$(56,20)
5090 LET REFS[0]=-(A[0]-A[2])
5100 GOSUB UPDATE_INVENTORY
5110 REM " ---  Remove Item Number Cross Reference "
5120 LET WOE03_KEY$=N0$+"C"+A0$(54,22)+A0$(3,9)
5130 GOSUB REMOVE_CROSS_REFERENCE
5140 REM " --- Remove Work Order "
5150 REMOVE (WOE01_DEV,KEY=A0$(1,11),DOM=5160)
5160 LET O9=1
5170 RUN "WOE.AA"
5500 REM " --- Display"
5510 DIM D1$(60),DESC$(60)
5520 FIND (IVM01_DEV,KEY=A0$(1,2)+A0$(56),DOM=5530)IOL=IVM01A
5530 DIM X1$(30); LET X1$(6,20)="** Not On File **",I9$="N"
5540 LET DESC$(1)=FNITEM$(D1$(1),DESC[1],DESC[2],DESC[3])
5550 FIND (WOM10_DEV,KEY=N0$+"A"+A0$(12,2),DOM=5570)IOL=WOM10A
5560 LET I9$=A0$(14,1)
5570 PRINT @(26,3),A0$(12,2),"  ",X1$(6,20),@(65),A0$(15,1),@(65,4),A0$(14,1),@(65,5),FNB$(A0$(16,3)),@(65,6),A0$(52,2),@(73,6),A0$(50,2)
5580 IF A0$(15,1)="O" THEN PRINT @(67,3),"**Open**"
5582 IF A0$(15,1)="P" THEN PRINT @(67,3),"*Planned*"
5584 IF A0$(15,1)="Q" THEN PRINT @(67,3),"*Quoted*"
5586 IF A0$(15,1)="C" THEN PRINT @(67,3),"**Closed**"
5590 IF I9$="I" THEN PRINT @(67,4),"Inventory",@(13,5),A0$(56)
5592 IF I9$="N" THEN PRINT @(67,4),"Non-Stocking"
5594 IF I9$="R" THEN PRINT @(67,4),"Recurring"
5600 IF A0$(15,1)="C" OR POS(" "<>A0$(31,3))>0 THEN PRINT @(65,7),FNB$(A0$(31,3))
5610 IF I9$<>"I" THEN PRINT @(13,6),A1$(1,30),@(13,7),A1$(31,30),
5620 IF I9$="I" THEN IF DESC[3]>0 THEN PRINT @(13,6),DESC$(1,30),@(13,7),DESC$(31,30) ELSE PRINT @(13,6),D1$(1,MIN(DESC[1],47)),@(13,7),D1$(DESC[1]+1,MIN(DESC[2],38))
5630 PRINT @(11,8),A1$(61,25),@(42),A1$(86,5),@(65),A[3]:M1$,
5640 PRINT @(18,9),A[0]:M2$,@(42),A[6]:"##0.0",@(65,9),A[2]:M2$,
5650 RETURN 
5900 REM " --- Warning"
5910 DIM MESSAGE$[1]
5920 LET MESSAGE$[0]=V4$+" (<Enter>=Continue)"
5930 CALL "SYC.XA",2,MESSAGE$[ALL],0,22,-1,V$,V3
5990 RETURN 
6000 REM " --- No Changes To Exploded Work Orders
6010 READ (WOE22_DEV,KEY=A0$(1,11),DOM=6020)
6020 LET K6$=KEY(WOE22_DEV,END=6100)
6040 IF POS(A0$(1,11)=K6$)<>1 THEN GOTO 6100
6050 DIM MSG$[2]
6060 LET MSG$[0]="Material Requirements Already Exist. This Work"
6070 LET MSG$[1]="Order Must Be Deleted And Re-Entered To Adjust"
6075 LET MSG$[2]="The "+FLD$+". <Enter> To Continue."
6080 CALL "SYC.XA",3,MSG$[ALL],2,22,-1,V$,V
6095 EXITTO 3600
6100 RETURN 
6200 REM " --- Verify WIP Before Type Code Changes
6210 VERIFY_WIP: 
6240 READ (WOE01_DEV2,KEY=A0$(1,11),DOM=6250)
6250 LET K6$=KEY(WOE01_DEV2,END=6395)
6260 IF POS(A0$(1,11)=K6$)<>1 THEN GOTO 6395
6270 PRINT 'RB',@(10,21),"Work Order Transactions Exist. Work Order Type Code May"
6280 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Not Be Modified After Transactions Have Been Created, <Enter>: ",V0=1,V1=70,V2=22
6290 GOSUB 7000
6300 PRINT @(0,21),'CL'
6320 LET V3=0
6340 EXITTO 3600
6395 RETURN 
6400 REM " --- Change Required Date?
6410 READ (WOE02_DEV,KEY=A0$(1,11)+"A",DOM=6415)
6415 LET R0$=KEY(WOE02_DEV,END=6425)
6420 IF R0$(1,11)=A0$(1,11) THEN GOTO 6450
6425 READ (WOE22_DEV,KEY=A0$(1,11)+"B",DOM=6430)
6430 LET R0$=KEY(WOE22_DEV,END=6490)
6435 IF R0$(1,11)<>A0$(1,11) THEN GOTO 6490
6450 PRINT @(14,21),'RB',"Do You Wish To Change The Required Date In Material",
6455 LET V4$="Requirements and Operation Requirements (Y/N)?"
6460 LET V0$="Y",V1$="C",V2$="",V3$="",V4$="",V0=1,V1=FNV(V4$),V2=22
6470 GOSUB 7000
6480 PRINT @(0,21),'CL'
6485 IF V$="Y" THEN GOSUB 6500
6490 RETURN 
6500 REM " --- Change Required Date For Operation Requirements To New Open Date
6510 DIM R[13]
6520 READ (WOE02_DEV,KEY=A0$(1,11)+"A",DOM=6530)
6530 LET K$=KEY(WOE02_DEV,END=6600)
6540 IF POS(K$(1,11)=A0$(1,11))<>1 THEN GOTO 6600
6550 FIND (WOE02_DEV,KEY=K$)IOL=WOE02A
6560 LET R1$(4,3)=A0$(16,3)
6570 WRITE (WOE02_DEV,KEY=K$)IOL=WOE02A
6580 GOTO 6530
6600 REM " --- Change Required Date For Material Requirements To New Open Date
6610 READ (WOE22_DEV,KEY=A0$(1,11)+"B",DOM=6620)
6620 LET K$=KEY(WOE22_DEV,END=6690)
6630 IF POS(K$(1,11)=A0$(1,11))<>1 THEN GOTO 6690
6640 FIND (WOE22_DEV,KEY=K$)IOL=WOE22A
6650 LET R1$(4,3)=A0$(16,3)
6660 WRITE (WOE22_DEV,KEY=K$)IOL=WOE22A
6670 GOTO 6620
6690 GOSUB 6800
6695 RETURN 
6700 REM " --- Remove Cross Reference Records"
6705 REMOVE_CROSS_REFERENCE: 
6710 REMOVE (WOE03_DEV,KEY=WOE03_KEY$,DOM=6715)
6715 RETURN 
6750 REM " --- Update Inventory Record "
6755 UPDATE_INVENTORY: 
6760 CALL "IVC.UA",ACTION$,IVFILES[ALL],PARAMS[ALL],PARAMS$[ALL],ITEMS$[ALL],REFS$[ALL],REFS[ALL],STATUS
6765 RETURN 
6800 REM " --- Change Material Commitment Date Required "
6810 DIM W[4]
6820 READ (WOE23_DEV,KEY=A0$(1,11),DOM=6830)
6830 LET K13$=KEY(WOE23_DEV,END=6890)
6840 IF K13$(1,11)<>A0$(1,11) THEN GOTO 6890
6860 READ (WOE23_DEV,KEY=K13$)IOL=WOE23A
6870 LET W1$(4,3)=A0$(16,3)
6880 WRITE (WOE23_DEV,KEY=K13$)IOL=WOE23A
6885 GOTO 6830
6890 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9215 LET O9=1
9220 RUN "WOE.AA"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

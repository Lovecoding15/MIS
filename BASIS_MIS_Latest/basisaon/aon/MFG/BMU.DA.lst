0010 REM "BMU - Bill of Materials Component Item Replacement
0020 REM "Program:  BMU.DA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=7
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="BMM-01",FILES$[2]="BMM-02",FILES$[3]="BMM-04"
0130 LET FILES$[4]="BMM-10",FILES$[5]="IVM-01",FILES$[6]="IVM-14"
0140 LET FILES$[7]="SYS-01"
0160 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0175 LET BMM01_DEV=CHANNELS[1],BMM02_DEV=CHANNELS[2],BMM04_DEV=CHANNELS[3]
0180 LET BMM10_DEV=CHANNELS[4],IVM01_DEV=CHANNELS[5],IVM14_DEV=CHANNELS[6]
0185 LET SYS01_DEV=CHANNELS[7]
0200 REM " --- IOLists
0280 IVM01A: IOLIST X0$,X$(1)
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 BMS01A: IOLIST *,*,M0$,*,*,*,P9
0320 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="IV",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0450 FIND (SYS01_DEV,KEY=N0$+"BM00",DOM=9800)IOL=BMS01A
0460 DIM P[5],DESC[3],PARAMS[3]
0465 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P4$(1,2)),P[2]=FNYY_YEAR(P4$(3,2))
0470 FOR X=1 TO 3
0480 LET DESC[X]=NUM(P2$((X-1)*2+9,2))
0485 LET PARAMS[X]=DESC[X]
0490 NEXT X
0495 LET PARAMS[0]=P[0]
0500 REM " --- Init Data
0510 LET WORDS$="*Tenths*Hundredths*Thousandths*Ten Thousandths*Hundred Thousandths*Millionths*Ten Millionths*Hundred Millionths*Billionths*"
0520 LET R0=1
0525 DIM IV_CHANS[14]
0545 LET IV_CHANS[1]=IVM01_DEV,IV_CHANS[14]=IVM14_DEV
0550 DIM W[10],B[8]
0560 LET M0=LEN(M0$)
0570 IF P9<=0 THEN LET R6$="X"; GOTO 0700
0580 DIM R6$(P9,"X")
0590 LET R6$="."+R6$
0700 REM " --- Background
0710 FOR X=4 TO 18 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(17,3),"Beginning Bill Number:",@(20,5),"Ending Bill Number:",@(23,7),"Old Item Number:",@(23,9),"New Item Number:",@(21,11),"Conversion Factor:",@(20,13),"Rounding Precision:",@(21,15),"Obsolete Comments:",@(24,17),"Effective Date:",'SF'
1000 REM " --- Options
1005 LET I0=0
1200 REM " --- Beginning Bill Number
1210 DIM ITEM$[6],ITEM[12],X$(60)
1220 LET ACTION$="N",V1=40,V2=3,DEFAULT$=R0$
1230 LET V4$="Enter Valid Bill Number, <F1>=First, <F3>=Lookup"
1240 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1250 IF V3=4 THEN GOTO 9900
1260 IF V3=2 THEN GOTO 1380
1270 IF V3<>3 THEN GOTO 1320
1280 CALL "SYC.LB",V$
1290 LET V$=V$+FILL(20-LEN(V$))
1300 FIND (IVM01_DEV,KEY=N0$+V$,DOM=1200)IOL=IVM01A
1310 LET ITEM$[0]=X0$(3,20),ITEM$[1]=FNITEM$(X$,DESC[1],DESC[2],DESC[3])
1320 IF V3=1 OR ITEM$[0]="" THEN PRINT @(V1,V2),'CL',"First"; LET R0$=""; GOTO 1380
1330 LET ITEM$[0]=ITEM$[0]+FILL(20-LEN(ITEM$[0]))
1340 FIND (BMM01_DEV,KEY=N0$+ITEM$[0],DOM=1200)
1350 LET X$(1)=ITEM$[1]
1360 PRINT @(V1,V2),'CL',ITEM$[0](1,P[0])," ",X$(1,38-P[0])
1370 LET R0$=ITEM$[0]
1380 GOTO 3900
1400 REM " --- Ending Bill Number
1410 DIM ITEM$[6],ITEM[12],X$(60)
1420 LET ACTION$="N",V1=40,V2=5,DEFAULT$=R1$
1430 LET V4$="Enter Valid Bill Number, <F1>=Last, <F3>=Lookup"
1440 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1450 IF V3=4 THEN GOTO 9900
1460 IF V3=2 THEN GOTO 1590
1470 IF V3<>3 THEN GOTO 1510
1475 CALL "SYC.LB",V$
1480 LET V$=V$+FILL(20-LEN(V$))
1490 FIND (IVM01_DEV,KEY=N0$+V$,DOM=1400)IOL=IVM01A
1500 LET ITEM$[0]=X0$(3,20),ITEM$[1]=FNITEM$(X$,DESC[1],DESC[2],DESC[3])
1510 IF V3=1 OR ITEM$[0]="" THEN PRINT @(V1,V2),'CL',"Last"; LET R1$=""; GOTO 1590
1520 LET ITEM$[0]=ITEM$[0]+FILL(20-LEN(ITEM$[0]))
1530 IF ITEM$[0]<R0$ THEN GOTO 1400
1540 FIND (BMM01_DEV,KEY=N0$+ITEM$[0],DOM=1400)
1550 DIM X$(60)
1560 LET X$(1)=ITEM$[1]
1570 PRINT @(V1,V2),'CL',ITEM$[0](1,P[0])," ",X$(1,38-P[0])
1580 LET R1$=ITEM$[0]
1590 GOTO 3900
1600 REM " --- Old Item Number
1605 DIM ITEM$[6],ITEM[12]
1610 LET ACTION$="",V1=40,V2=7,DEFAULT$=R2$
1620 LET V4$="Enter Item Number"
1640 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1645 IF V3=4 THEN GOTO 9900
1650 IF V3=2 THEN GOTO 1690
1665 LET ITEM$[0]=ITEM$[0]+FILL(20-LEN(ITEM$[0]))
1670 IF POS(" "<>ITEM$[0])=0 THEN GOTO 1600
1675 DIM X$(60); LET X$(1)="Invalid Item Number"
1683 LET X$(1)=ITEM$[1]
1685 PRINT @(V1,V2),'CL',ITEM$[0](1,P[0])," ",X$(1,38-P[0])
1687 LET R2$=ITEM$[0]
1690 GOTO 3900
1800 REM " --- New Item Number
1805 DIM ITEM$[6],ITEM[12]
1810 LET ACTION$="",V1=40,V2=9,DEFAULT$=R3$
1820 LET V4$="Enter Valid Item Number"
1830 CALL "IVC.IA",ACTION$,V1,V2,IV_CHANS[ALL],N0$,DEFAULT$,V4$,PARAMS[ALL],ITEM$[ALL],ITEM[ALL],V3
1835 IF V3=4 THEN GOTO 9990
1840 IF V3=2 THEN GOTO 1890
1860 LET ITEM$[0]=ITEM$[0]+FILL(20-LEN(ITEM$[0]))
1870 DIM X$(60)
1880 LET X$(1)=ITEM$[1]
1885 PRINT @(V1,V2),'CL',ITEM$[0](1,P[0])," ",X$(1,38-P[0])
1887 LET R3$=ITEM$[0]
1890 GOTO 3900
1900 REM " --- Conversion Factor
1920 LET V0$="N",V1$="C",V2$=STR(R0),V3$=M0$,V4$="Enter Number Of New Items To Make Old Item",V0=M0,V1=40,V2=11
1930 GOSUB 7000
1940 IF V3=4 THEN GOTO 9900
1950 LET R0=V
1960 PRINT @(V1,V2),V$,'CL',
1990 GOTO 3900
2000 REM " --- Obsolete Comments
2010 IF R4$="" THEN LET R4$="Y"
2020 LET V0$="Y",V1$="C",V2$=R4$,V3$="",V4$="Y=Obsolete Associated Comments, N=Ignore Comments",V0=1,V1=40,V2=15
2030 GOSUB 7000
2040 IF V3=4 THEN GOTO 9900
2050 LET R4$=V$
2090 GOTO 3900
2100 REM " --- Rounding Precision
2120 LET V0$="S",V1$="CK^",V2$=R6$,V3$="",V4$="X=Round To Whole Digits, .XX=Round To Hundredths, etc.",V0=P9+1,V1=40,V2=13
2130 GOSUB 7000
2135 IF V3=4 THEN GOTO 9900
2140 IF LEN(V$)=0 THEN GOTO 2100
2145 FOR X=1 TO LEN(V$); IF V$(X,1)<>"X" AND V$(X,1)<>"." THEN EXITTO 2100
2147 NEXT X
2150 LET R6$=V$
2160 IF R6$="X" THEN PRINT @(V1,V2),R6$," ","Whole Digits",'CL',; LET PRE=0; GOTO 2190
2165 LET X=POS("."=R6$)
2170 IF X=0 THEN GOTO 2100
2175 LET PRE=LEN(R6$(X+1)),X0=POS("*"=WORDS$,1,PRE)+1,X1=POS("*"=WORDS$(X0))-1
2180 LET X$=WORDS$(X0,X1)
2185 PRINT @(V1,V2),V$," ",X$,'CL',
2190 GOTO 3900
2200 REM " --- Effective Date
2210 IF R5$="" THEN LET R5$=F0$(7,3)
2220 LET V0$="D",V1$="CR",V2$=R5$,V3$="",V4$="Enter Date To Make The Change Effective",V0=8,V1=40,V2=17
2230 GOSUB 7000
2240 IF V3=4 THEN GOTO 9900
2250 LET R5$=V$
2290 GOTO 3900
3900 REM " --- Next Field
3910 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1400,1600,1800,1900,2100,2000,2200,4000
4000 REM " --- Overlay
4010 LET V0$="S",V1$="C^",V2$="No",V3$="",V4$="Is The Above Information Correct (Yes/No)? ",V0=3,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4040 IF V3=4 THEN GOTO 9900
4060 IF V$="NO" THEN GOTO 1000
4080 IF V$<>"YES" THEN GOTO 4000
4100 RUN "BMU.DB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

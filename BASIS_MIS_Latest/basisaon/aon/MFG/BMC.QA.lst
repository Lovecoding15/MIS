0010 REM "BMC - Bill of Materials SpeedSearch (Component Availability)"
0020 REM "Program BMC.QA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0060 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0070 REM "
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open Files"
0105 LET FILES=7
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="IVM-01",FILES$[2]="IVM-02",FILES$[3]="IVM-03"
0130 LET FILES$[4]="IVM-10",FILES$[5]="IVM-14",FILES$[6]="BMM-01"
0140 LET FILES$[7]="BMM-02"
0150 CALL "SYC.DA",1,1,7,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET IVM01_DEV=CHANNELS[1],IVM02_DEV=CHANNELS[2],IVM03_DEV=CHANNELS[3]
0180 LET IVM10_DEV=CHANNELS[4],IVM14_DEV=CHANNELS[5],BMM01_DEV=CHANNELS[6]
0190 LET BMM02_DEV=CHANNELS[7]
0200 REM " --- IOLIST's"
0210 BMM02A: IOLIST A0$,A1$,A2$,A[ALL]
0230 IVM01A: IOLIST C0$(1),C1$(1),C2$(1),C3$,C4$,C5$,C6$,C[ALL]
0235 IVM10A: IOLIST PRODUCT$(1)
0240 IVM10C: IOLIST WAREHOUSE$(1)
0250 BMM01A: IOLIST D0$(1),D1$(1),D2$(1),D[ALL]
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,F1$,F2$,F3$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM P[5],X$(128)
0410 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2),ONAME$=FNP$(X$(27,8))
0415 LET TNAME$=FNP$(X$(3,8)),TWIDTH=NUM(X$(35,2)),THEIGHT=NUM(X$(37,2))
0420 LET NAME$=FNP$(X$(11,8)),WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2))
0425 LET SNAME$=FNP$(X$(19,8)),SWIDTH=NUM(X$(43,2)),SHEIGHT=NUM(X$(45,2))
0430 LET MAXOPT=NUM(X$(57,2)),SYS01_DEV=NUM(STBL("!SYS01"))
0435 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0440 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0445 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0450 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9900)IOL=IVS01A
0455 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2))
0460 LET P[3]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2)),P[5]=NUM(P2$(13,2))
0465 LET PAGES=INT((DSZ-5000)/4000),MAXPAGE=21,M9$="###,##0-",M9=LEN(M2$)
0470 IF PAGES<3 THEN GOTO 9900
0475 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0480 CALL "SYC.RM",M2$,M2,10,11
0500 REM " --- Initializations"
0505 PRECISION NUM(P2$(5,1))
0510 DIM A0$(25),A1$(20),A2$(114),A[10],C0$(22),C1$(60),C2$(64),C[12],B[20]
0515 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2),BILLDESC$(TWIDTH-9-P[0]),O[4]
0520 DIM OPTLIST$[2,MAXOPT,6],FILES[14],PARAMS[3],ITEM$[6],PRODUCT$(32)
0525 DIM D0$(22),D1$(30),D2$(58),D[11],INFO$[2],WHSE$[3],WAREHOUSE$(32)
0540 LET D_STATUS=0,MAXROW=HEIGHT-5,PGM$=PGM(-2),OPTION=0,WHSE$=""
0545 LET C0$(1)=N0$+OPTION$[3],M2=LEN(M2$),H0=WIDTH-2
0550 LET PARAMS[0]=P[0],PARAMS[1]=P[3],PARAMS[2]=P[4],PARAMS[3]=P[5],DONE=0
0555 LET FILES[1]=IVM01_DEV,FILES[14]=IVM14_DEV
0560 LET O[4]=WIDTH-2-M2,O[3]=O[4]-M2,O[2]=O[3]-M2,O[0]=2,O[1]=O[0]+P[0]
0565 LET HEADING$(O[0])="Item",DW=O[2]-O[1],QUANTITY=1,FILES[2]=IVM02_DEV
0570 IF DW<10 THEN LET O[1]=0
0575 LET HEADING$(O[2]+M2-9)="Required",HEADING$(O[3]+M2-10)="Available"
0580 LET HEADING$(O[4]+M2-9)="On Order",FOOTING$(2)="Component Availability"
0585 LET FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End",WHEN$=F0$(7,3)
0590 LET FILES[3]=IVM03_DEV
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],TITLE$,OMAXROW,PREVMENU$,""
0700 REM " --- Background"
0710 CALL "SYC.WB",TNAME$,0
0720 PRINT 'CS','SB',@(1,0),"Item:",@(1,1),"Whse:",@(1,2),"Production Date:",@(27,2),"Quantity Required:",'SF',
0730 CALL "SYC.WB",NAME$,0
0740 PRINT 'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",SNAME$,0
0915 PRINT 'CF',
0920 CALL "SYC.WB",TNAME$,0
0990 GOTO 1100
1000 REM " --- Process key field"
1010 GOSUB 6000
1020 CALL "SYC.WB",TNAME$,0
1025 LET V2$="",V2$=KEY(BMM01_DEV,END=1040)
1030 IF POS(N0$=V2$)<>1 THEN LET V2$=""
1035 IF V2$<>"" THEN LET V2$=V2$(3,P[0])
1040 LET ACTION$="NV",V1=7,V2=0,V4$=""
1045 CALL "IVC.IA",ACTION$,V1,V2,FILES[ALL],N0$,V2$,V4$,PARAMS[ALL],ITEM$[ALL],C[ALL],V3
1050 ON V3 GOTO 1070,1000,1000,1055,3800,3900,3800
1055 CALL "SYC.LB",V$
1060 GOTO 1080
1070 LET V$=FNP$(ITEM$[0])
1080 IF V$="" THEN GOTO 1000
1090 LET C0$(3)=V$
1100 REM " --- Retrieve item"
1110 FIND (IVM01_DEV,KEY=C0$,DOM=1000)IOL=IVM01A
1120 FIND (BMM01_DEV,KEY=C0$,DOM=1000)IOL=BMM01A
1130 LET BILLDESC$(1)=FNITEM$(C1$,P[3],P[4],P[5]),SCRAP=D[4]
1140 PRINT @(7,0),C0$(3,P[0])," ",BILLDESC$,
1200 REM " --- Warehouse"
1210 LET V0$="S",V1$="",V2$=WHSE$,V3$="",V4$="",V0=2,V1=7,V2=1
1220 GOSUB 7000
1230 ON V3 GOTO 1260,1235,1000,1250,3800,3900,3800
1235 LET WHSE$=""
1240 PRINT @(V1,V2),'CL',"All",
1245 GOTO 1400
1250 CALL "IVC.LK",3,V1,V2,V$
1260 IF V$="" THEN GOTO 1235
1270 LET WAREHOUSE$(1)=N0$+"C"+V$
1280 FIND (IVM10_DEV,KEY=WAREHOUSE$(1,5),DOM=1200)IOL=IVM10C
1285 LET WHSE$=WAREHOUSE$(4,2)
1290 PRINT @(V1,V2),WAREHOUSE$(4,2)," ",WAREHOUSE$(6,24),
1400 REM " --- Production Date"
1410 LET V0$="D",V1$="R",V2$=WHEN$,V3$="",V4$="",V0=3,V1=18,V2=3
1420 GOSUB 7000
1430 ON V3 GOTO 1440,1400,1200,1400,3800,3900,3800
1440 LET WHEN$=V$
1450 PRINT @(V1,V2),FNB$(WHEN$),
1600 REM " --- Production Quantity"
1610 LET V0$="N",V1$="R",V2$=STR(QUANTITY),V3$=M9$,V4$="",V0=M9,V1=46,V2=2
1620 GOSUB 7000
1630 ON V3 GOTO 1640,1600,1400,1600,3800,3900,3800
1640 IF V<=0 THEN GOTO 1600
1650 LET QUANTITY=V
1900 REM " --- Retrieve records for display"
1910 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,SHEIGHT-2],X$(WIDTH-2)
1920 LET LINE=1,L=1,X0=0,X1=1,PAGE=1,SELECTION$="",BILLNUMBER$=C0$
1930 LET X1$=A0$(3,P[0]),X2$=PARAM$
1940 LET X$(1)='SB'+" Item: "+'SF'+A0$(3,P[0])+" "+BILLDESC$
1950 CALL "SYC.QB",X$,X1$,X2$
1960 PRINT 'SB',@(1,2),"Production Date: ",'SF',FNB$(WHEN$),'SB',@(27,2),"Quantity Required:",'SF',QUANTITY:M9$,
1990 READ (BMM02_DEV,KEY=D0$,DOM=2000)
2000 REM " --- For each component in BMM-02 record"
2010 LET K$=KEY(BMM02_DEV,END=3000)
2020 IF POS(BILLNUMBER$=K$)<>1 THEN GOTO 3000
2030 READ (BMM02_DEV)IOL=BMM02A
2040 IF A2$(4,1)<>"S" THEN GOTO 2900
2050 IF FNP$(A2$(5,3))<>"" THEN IF A2$(5,3)>WHEN$ THEN GOTO 2900
2060 IF FNP$(A2$(8,3))<>"" THEN IF A2$(8,3)<=WHEN$ THEN GOTO 2900
2200 REM " --- Additional reads"
2210 DIM DESCRIPTION$(WIDTH-15),ABBREV$(DW)
2220 LET C0$(1)=N0$+A1$,C1$(1)="",C2$(1)="",D0$(1)=A0$,D2$(1)="",PHANTOM$=""
2230 FIND (IVM01_DEV,KEY=C0$,DOM=2240)IOL=IVM01A
2240 LET DESCRIPTION$=FNITEM$(C1$,P[3],P[4],P[5]),ABBREV$(1)=DESCRIPTION$
2250 LET PRODUCT$(1)=N0$+"A"+C2$(1,3)
2260 FIND (IVM10_DEV,KEY=PRODUCT$(1,6),DOM=2270)IOL=IVM10A
2270 FIND (BMM01_DEV,KEY=D0$,DOM=2400)IOL=BMM01A
2280 LET PHANTOM$="Assembly"
2290 IF D2$(40,1)="Y" THEN LET PHANTOM$="Phantom"
2400 REM " --- Accumulate on hand, on order and committed"
2410 LET ACTION$="",INFO$[0]=N0$,INFO$[1]=WHSE$,INFO$[2]=C0$(3)
2420 IF WHSE$="" THEN LET ACTION$="A"
2430 CALL "IVC.WA",ACTION$,FILES[ALL],INFO$[ALL],WHSE$[ALL],B[ALL],VALUE,STATUS
2440 LET AVAILABLE=B[0]-B[2]
2500 REM " --- Display page?"
2510 IF L<MAXROW THEN GOTO 2600
2520 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2530 GOSUB 5000
2540 IF DONE THEN GOTO 4000
2570 IF ANSWER$<>"" THEN GOTO 3000
2590 LET L=1,X1=1
2700 REM " --- Middle window record"
2710 DIM G$(WIDTH-2)
2720 LET G$(2)=C0$(3)
2730 IF O[1]>0 THEN LET G$(O[1],DW)=FNITEM$(C1$,P[3],P[4],P[5])
2740 LET G$(O[2])=STR(FNQTY(A[0],A[1],A[3],SCRAP,A[4])*QUANTITY:M2$)
2750 LET G$(O[3])=STR(AVAILABLE:M2$),G$(O[4])=STR(B[1]:M2$)
2790 LET LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=C0$(3)
2800 REM " --- Bottom window record"
2810 LET G$(1)="",MORE$[PAGE,X1,0]=STR(SHEIGHT-2)
2820 LET G$(1)="{Description:}"+" "+DESCRIPTION$,MORE$[PAGE,X1,1]=G$,G$(1)=""
2830 LET G$(5)="{On Hand:}"+STR(B[0]:M2$),G$(26)="{Prod:}"+C2$(1,3)+" "+FNP$(PRODUCT$(7,24))
2840 IF FNP$(A2$(5,3))<>"" THEN LET G$(36)="{Effective:}"+FNB$(A2$(5,3))
2850 LET MORE$[PAGE,X1,2]=G$,G$(1)=""
2860 LET G$(3)="{Committed:}"+STR(B[2]:M2$),G$(27)=PHANTOM$
2870 IF FNP$(A2$(8,3))<>"" THEN LET G$(37)="{Obsolete:}"+FNB$(A2$(8,3))
2890 LET MORE$[PAGE,X1,3]=G$,LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2990 GOTO 2000
3000 REM " --- No more components"
3010 IF X0>0 THEN GOTO 3100
3020 DIM MESSAGE$[1]
3030 LET MESSAGE$[0]="No Components Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,-1,-1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 LET SELECTION$="PRIOR"
3090 GOTO 3500
3100 REM " --- Display last page"
3110 LET ACTION$="M",CHOICE1$=C0$(3),CHOICE2$=""
3120 GOSUB 5000
3130 IF DONE THEN GOTO 4000
3500 REM " --- Process option window"
3510 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3520 LET ACTION$="O",CHOICE1$=A0$(3),CHOICE2$=""
3530 GOSUB 5000
3540 IF CONTROL=1 THEN GOTO 1000
3590 GOTO 4000
3800 REM " --- Exit"
3810 LET OPTION$[1]="EXIT"
3890 GOTO 4000
3900 REM " --- Startover"
3910 LET OPTION$[1]="STARTOVER"
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OMAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CS',
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8070 DEF FNQTY(Q1,Q2,Q3,Q4,Q5)=10000*Q1*Q2/(Q2*Q4*(100-Q5))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9921)
9930 NEXT X
9990 EXIT 
9999 END

0010 REM "BMR - Order/Production Register"
0020 REM "Program BMR.JA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=8
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="ARE-03",FILES$[2]="ARE-13",FILES$[3]="ARM-01"
0130 LET FILES$[4]="BME-01",FILES$[5]="BME-02",FILES$[6]="BMM-01"
0135 LET FILES$[7]="IVM-02",FILES$[8]="SYS-01"
0150 LET OPTIONS$[5]="L"
0160 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS<>0 THEN GOTO 9900
0175 LET ARE03_DEV=CHANNELS[1],ARE13_DEV=CHANNELS[2],ARM01_DEV=CHANNELS[3]
0180 LET BME01_DEV=CHANNELS[4],BME02_DEV=CHANNELS[5],BMM01_DEV=CHANNELS[6]
0190 LET IVM02_DEV=CHANNELS[7],SYS01_DEV=CHANNELS[8]
0200 REM " --- IOLists
0210 ARM01A: IOLIST *,X0$
0230 BME02A: IOLIST A0$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 ARS01A: IOLIST R0$,R1$,R2$,R3$,R4$,R5$,R6$
0320 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0405 DIM P[4],R[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="IV"
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=IVS01A
0440 CALL "SYC.VA","OP",INFO$[ALL]
0445 LET OP$=INFO$[20]
0450 IF OP$<>"Y" THEN GOTO 4900
0470 FIND (SYS01_DEV,KEY=N0$+"AR"+"00",DOM=9800)IOL=ARS01A
0475 LET R[0]=NUM(R2$(1,2)),R0=LEN(R5$),HEADINGS=2
0480 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P2$(5,2)),P[3]=NUM(P2$(7,2))
0485 CALL "SYC.VA","BM",INFO$[ALL]
0490 LET GL$=INFO$[9]
0500 REM " --- Init Data
0505 PRECISION NUM(P2$(5,1))
0510 DIM HEADINGS$[HEADINGS]
0550 LET H0=80,H=0,L9=55,L=L9+1,ERRORS=0,GLSTATUS=0
0560 LET WHEN$=F0$(7,3),CLOCK$="",HEADINGS$[0]=F4$,HEADINGS$[1]=F5$
0700 REM " --- Background
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.NB","Printing:",15,COLUMN
0800 REM " --- Run Report?
0810 LET V4$="Do You Want To Print The "+F5$
0820 CALL "SYC.YN",1,V4$,2,V$,V3
0830 IF V3=4 THEN GOTO 9900
0840 IF V$<>"Y" THEN GOTO 9900
0900 REM " --- Position file
0910 CALL "SYC.GA",7,1,"","",STATUS
0920 IF STATUS THEN GOTO 9900
1000 REM " --- Initial Read
1020 READ (BME02_DEV,KEY=N0$,DOM=1030)
1030 LET K$=KEY(BME02_DEV,END=4000)
1035 IF POS(N0$=K$)<>1 THEN GOTO 4000
1100 REM " --- Heading Read
1120 READ (BME02_DEV,END=4000)IOL=BME02A
1200 REM " --- Breaks
1220 IF POS(N0$=A0$)<>1 THEN GOTO 4000
1240 IF A0$(3,3)<>T0$ THEN GOSUB 6000
1280 PRINT @(COLUMN,11),FNB$(A0$(3,3))," ",A0$(6,R[0]),
1400 REM " --- Get Customer Name
1410 LET X0$="***Not On File***"
1420 FIND (ARM01_DEV,KEY=N0$+A0$(6,6),DOM=1500)IOL=ARM01A
1440 LET X0$=X0$(1,30)
2000 REM " --- Print
2020 IF L+1>L9 THEN GOSUB 5200
2040 PRINT (7)@(10),FNF$(A0$(6,R[0]),R5$)," ",X0$,@(R0+42),A0$(12,7),@(R0+50),A0$(19,7)
2060 LET L=L+1
2080 LET T0=T0+1
2100 REM " --- Verify Date
2110 IF GL$<>"Y" THEN GOTO 2900
2120 LET WHEN$=A0$(3,3)
2130 IF GL$="Y" THEN CALL "GLC.EA",WHEN$,"N",PERIOD$,YEAR$,GLSTATUS
2140 IF GLSTATUS>0 THEN CALL "GLC.GA",7,WHEN$,GLSTATUS,L,ERRORS,STATUS
2900 GOTO 1100
4000 REM " --- End
4010 PRINT (7)'LF',@(10),"Total Orders:",T0
4020 IF ERRORS>0 THEN GOTO 9900
4090 RUN "BMU.JA"
4900 REM " --- OP Not Installed
4910 DIM MESSAGE$[1]
4920 LET MESSAGE$[0]="Order Processing Is Required In Order To Use This Task"
4930 LET MESSAGE$[1]="          Press <Enter> To Continue"
4940 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
4990 GOTO 9900
5000 REM " --- Heading
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)@(2),"Prod",@(10),"Customer",@(42+R0),"Order"
5110 PRINT (7)@(2),"Date",@(10),"No.",@(11+R0),"Name",@(42+R0),"Number",@(50+R0),"Reference"
5190 RETURN 
5200 REM " --- Date Heading
5220 IF L+2>L9 THEN GOSUB 5000
5240 PRINT (7)'LF',@(0),FNB$(A0$(3,3)),
5260 LET L=L+1
5290 RETURN 
6000 REM " --- Date Break
6020 GOSUB 5200
6040 LET T0$=A0$(3,3)
6090 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

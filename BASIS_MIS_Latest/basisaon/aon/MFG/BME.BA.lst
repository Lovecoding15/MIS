0010 REM "BME - Order/Production Entry"
0020 REM "Program BME.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files
0110 LET FILES=4
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="BME-02",FILES$[2]="SYS-01"
0130 LET FILES$[3]="ARE-03",FILES$[4]="ARM-01"
0160 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS<>0 THEN GOTO 9900
0175 LET BME02_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOLists
0210 BME02A: IOLIST A0$(1)
0220 ARE03A: IOLIST X0$
0230 ARM01A: IOLIST *,X0$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters
0405 DIM P[4],INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="AR",N4$=F5$
0430 CALL "SYC.VA","OP",INFO$[ALL]
0440 LET OP$=INFO$[20]
0445 IF OP$<>"Y" THEN GOTO 4900
0450 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=ARS01A
0460 LET P[0]=NUM(P2$(1,2))
0490 CALL "SYC.VA","BM",INFO$[ALL]
0495 LET GL$=INFO$[9]
0500 REM " --- Init Data
0520 DIM A0$(30)
0530 LET A0$(1)=N0$
0600 REM " --- Additional File Opens
0660 CALL "SYC.DA",1,3,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0670 IF STATUS<>0 THEN GOTO 9900
0675 LET ARE03_DEV=CHANNELS[3],ARM01_DEV=CHANNELS[4]
0700 REM " --- Background
0730 PRINT 'SB',@(0,3),"Production Date:",@(0,4),"Customer Number:",@(3,5),"Order Number:",@(6,6),"Reference:",@(3,7),"Update Item?:",'SF',
0900 REM " --- Init File
0920 READ (BME02_DEV,KEY=N0$,DOM=0950)
0950 LET K$=KEY(BME02_DEV,END=1000)
0960 IF K$="" THEN GOTO 1000
0970 IF POS(N0$=K$)<>1 THEN GOTO 1000
0980 LET A0$(1)=K$
1000 REM " --- Date
1010 PRINT 'CF',
1020 LET V0$="D",V1$="ERC",V2$=A0$(3,3),V4$="Enter the Production Date",V0=8,V1=17,V2=3,I0=0
1025 IF FNP$(V2$)="" THEN LET V2$=F0$(7,3)
1030 GOSUB 7000
1035 IF V3=4 THEN GOTO 9900
1040 IF GL$="Y" THEN CALL "GLC.EA",V$,"Y",PERIOD$,YEAR$,STATUS
1045 IF STATUS>99 THEN GOTO 1000
1050 LET A0$(3,3)=V$
1055 LET A0$(6)=""
1060 READ (BME02_DEV,KEY=A0$(1,5),DOM=1070)
1070 LET K$=KEY(BME02_DEV,END=1090)
1080 IF POS(K$(1,5)=A0$)=1 THEN LET A0$(1)=K$
1090 GOTO 3900
1200 REM " --- Customer
1210 PRINT @(17,4),'CL',
1220 LET V0$="M",V1$="KCE",V2$=A0$(6,P[0]),V3$=P1$,V0=6,V1=17,V2=4,V4$="Enter a Valid Customer Number (<F3>=Lookup)",I0=1
1230 GOSUB 7000
1235 IF V3=2 THEN PRINT @(V1,V2),'CL'; GOTO 1290
1240 IF V3=3 THEN CALL "SYC.LC",V$
1245 IF V3=4 THEN GOTO 9900
1250 LET A0$(6,6)=V$
1255 FIND (ARM01_DEV,KEY=N0$+A0$(6,6),DOM=1200)IOL=ARM01A
1260 PRINT @(V1,V2),FNF$(A0$(6,P[0]),M0$)," ",X0$(1,30),
1265 LET A0$(12)=""
1270 READ (BME02_DEV,KEY=A0$(1,11),DOM=1275)
1275 LET K$=KEY(BME02_DEV,END=1290)
1280 IF POS(K$(1,11)=A0$)=1 THEN LET A0$(1)=K$
1290 GOTO 3900
1400 REM " --- Order Number
1410 PRINT @(17,7),'CL',@(17,6),'CL',@(17,5),'CL',
1420 LET V0$="Z",V1$="KCE",V2$=A0$(12,7),V4$="Enter a Valid Order Number (<F3>=Lookup)",V0=7,V1=17,V2=5,I0=2
1430 GOSUB 7000
1440 IF V3=2 THEN PRINT @(V1,V2),'CL',; GOTO 3900 ELSE IF V3=3 THEN GOTO 3000 ELSE IF V3=4 THEN GOTO 9900 ELSE IF V$="" THEN GOTO 1400
1450 IF V3=4 OR NUM(V$)=0 THEN GOTO 1400
1460 LET A0$(12,7)=V$
1470 FIND (BME02_DEV,KEY=A0$(1,18),DOM=1500)IOL=BME02A
1480 GOSUB 5000
1490 GOTO 4000
1500 FIND (ARE03_DEV,KEY=N0$+"  "+A0$(6,13)+"000",DOM=1400)IOL=ARE03A
1510 PRINT @(V1+V0+2,V2),FNB$(X0$(77,3)),
1520 IF X0$(22,1)<>"O" THEN GOTO 1400
1660 LET A0$(19,7)=A0$(12,7)
1670 GOTO 3900
1700 REM " --- Ref Number
1710 LET V0$="S",V1$="C",V2$=A0$(19,7),V3$="",V4$="Enter Up To 7 Characters",V0=7,V1=17,V2=6,I0=3
1720 GOSUB 7000
1730 LET A0$(19,7)=V$
1740 GOTO 3900
1800 REM " --- Update
1810 IF A0$(26,1)=" " THEN LET A0$(26,1)="Y"
1820 LET V0$="Y",V1$="C",V2$=A0$(26,1),V4$="N=Relieve Components Only During Production Register Update",V0=1,V1=17,V2=7
1830 PRINT @(6,21),"Y=Add Finished Items To Inventory During Production Register Update",
1840 GOSUB 7000
1860 LET A0$(26,1)=V$
1870 PRINT @(0,21),'CL',
1890 GOTO 3900
3000 REM " --- Display Open Orders
3010 LET V$=A0$(6,6),SEL$="O"
3020 CALL "SYC.LO",V$,SEL$
3030 PRINT @(17,5),V$,
3040 GOTO 1460
3900 REM " --- Next Field
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3960
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,1700,1800,4000
4000 REM " --- Correct?
4015 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="Is The Above Information Correct (Y/N/Delete)? ",V0=6,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V$="Y" THEN GOTO 4100
4040 IF V$="" THEN GOTO 4100
4050 IF V$="N" THEN GOTO 1700
4060 IF V$="D" OR V$="DELETE" THEN GOTO 4200
4090 GOTO 4000
4100 REM " --- Write
4120 WRITE (BME02_DEV,KEY=A0$(1,18))IOL=BME02A
4130 LET A0$(12,7)=""
4140 LET K$=KEY(BME02_DEV,END=4190)
4150 IF POS(K$(1,11)=A0$)<>1 THEN GOTO 4190
4160 LET A0$(12,7)=K$(12,7)
4190 GOTO 1400
4200 REM " --- Delete
4220 REMOVE (BME02_DEV,KEY=A0$(1,18),DOM=4290)
4290 GOTO 1400
4900 REM " --- O/P Not Installed
4910 DIM MESSAGE$[1]
4920 LET MESSAGE$[0]="Order Processing Is Required In Order To Use This Task"
4930 LET MESSAGE$[1]="          Press <Enter> To Continue"
4940 CALL "SYC.XA",2,MESSAGE$[ALL],1,22,-1,V$,V3
4990 GOTO 9900
5000 REM " --- Display
5020 PRINT @(17,5),A0$(12,7),@(17,6),A0$(19,7),@(17,7),A0$(26,1),
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

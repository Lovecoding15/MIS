0010 REM "BMM - Packing List"
0020 REM "Program BMC.CA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "PRTR_DEV : Printer Channel Number
0051 REM "BILL_NO$ : Bill to print
0052 REM "PAGE_NO  : Page counter
0053 REM "STATUS   : Exit Status from Heading Routine
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER PRTR_DEV,BILL_NO$,PAGE_NO,PDATE$,STATUS
0100 REM " --- Open/Lock Files
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0125 LET FILES$[1]="BMM-01",FILES$[2]="BMM-01",FILES$[3]="BMM-02"
0130 LET FILES$[4]="BMW-02",FILES$[5]="IVM-01",FILES$[6]="SYS-01"
0140 LET OPTIONS$[4]="C"
0160 CALL "SYC.DA",1,1,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS<>0 THEN GOTO 9900
0175 LET BMM01_DEV=CHANNELS[1],BMM01_DEV2=CHANNELS[2],BMM02_DEV=CHANNELS[3]
0180 LET BMW02_DEV=CHANNELS[4],IVM01_DEV=CHANNELS[5],SYS01_DEV=CHANNELS[6]
0200 REM " --- IOLists
0210 BMM01A: IOLIST A0$,A1$,A2$,A[ALL]
0220 BMM02A: IOLIST B0$,B1$,B2$,B[ALL]
0230 BMW02A: IOLIST KEY$
0250 IVM01A: IOLIST C0$,C1$(1),C2$
0300 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0310 BMS01A: IOLIST X$,P1$,P2$,P3$,P4,X5
0320 IVS01A: IOLIST X$,P5$,P6$,P$,X$,X$,P7$,P8$,P9$
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="BM",N3$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=BMS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0450 DIM P[2],DESC[3],A[11],B[10]
0460 FOR X=1 TO 3
0470 LET DESC[X]=NUM(P6$((X-1)*2+9,2))
0480 NEXT X
0490 LET P[0]=NUM(P6$(1,2))
0495 PRECISION NUM(P6$(5,1))
0500 REM " --- Init Data
0510 LET N0=2,N1=M0+M2+22,HEADINGS=2
0520 DIM HEADINGS$[HEADINGS],DASHES$(80,"-"),UNDERS$(80,"_")
0530 LET L9=45,L=L9+1,H0=80,WHEN$=F0$(7,3),CLOCK$=""
0540 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0700 REM " --- Sort File
0705 LET OP_CODE$="   "
0710 READ (BMM02_DEV,KEY=N0$+BILL_NO$,DOM=0720)
0720 LET K$=KEY(BMM02_DEV,END=1000)
0730 IF POS(N0$+BILL_NO$=K$)<>1 THEN GOTO 1000
0740 READ (BMM02_DEV,KEY=K$)IOL=BMM02A
0750 IF B2$(4,1)="S" THEN LET OP_CODE$=B2$(1,3)
0760 LET KEY$=N0$+OP_CODE$+B0$(3)
0770 WRITE (BMW02_DEV,KEY=KEY$)IOL=BMW02A
0780 GOTO 0720
1000 REM " --- Read
1020 READ (BMM01_DEV,KEY=N0$+BILL_NO$,DOM=9900)IOL=BMM01A
1200 REM " --- Heading
1220 GOSUB 5000
1400 REM " --- Detail
1410 READ (BMW02_DEV,KEY=N0$,DOM=1411)
1420 LET FIRST_TIME$="Y"
1500 REM " --- Detail
1510 LET K$=KEY(BMW02_DEV,END=3000)
1520 READ (BMW02_DEV)
1525 IF FIRST_TIME$="Y" THEN LET FIRST_TIME$="N",T0$=K$(3,3)
1530 LET K$=K$(1,2)+K$(6)
1540 READ (BMM02_DEV,KEY=K$)IOL=BMM02A
1550 IF POS(A0$(1,22)=B0$)<>1 THEN GOTO 3000
1552 IF B2$(5,3)<>"   " THEN IF B2$(5,3)>PDATE$ THEN GOTO 1500
1554 IF B2$(8,3)<>"   " THEN IF B2$(8,3)<=PDATE$ THEN GOTO 1500
1560 IF B2$(4,1)="S" THEN GOTO 1600
1570 IF L+1>L9 OR T0$<>B2$(1,3) THEN LET T0$=B2$(1,3); GOSUB 5000
1580 PRINT (PRTR_DEV)@(9),B2$(35,60); LET L=L+1
1590 GOTO 1500
1600 REM " --- Standard Item
1610 IF L+2>L9 OR T0$<>B2$(1,3) THEN LET T0$=B2$(1,3); GOSUB 5000
1620 LET R0$=" "
1630 FIND (BMM01_DEV2,KEY=N0$+B1$,DOM=1650)
1640 LET R0$="*"
1650 REM " --- Additional Reads
1670 DIM C1$(60),C2$(62)
1680 FIND (IVM01_DEV,KEY=N0$+B1$(1,20),DOM=1681)IOL=IVM01A
1690 LET C1$=FNZ$(C1$,DESC[1],DESC[2],DESC[3])
1800 REM " --- Print Detail
1820 PRINT (PRTR_DEV)@(1),B2$(1,3),@(9),B1$(1,P[0]),@(41),B2$(33,2),@(56-P1),FNQTY(B[0],B[1],B[3],A[4],B[4]):P1$,@(68),UNDERS$(1,10)
1830 PRINT (PRTR_DEV)@(12),C1$
1890 LET L=L+2
1900 REM " --- Serial/Lot Numbers
1910 IF C2$(19,1)<>"Y" OR P$(17,1)="N" THEN GOTO 1990
1920 IF P$(17,1)="S" THEN LET X$="Serial Number: " ELSE LET X$="Lot Number: "
1930 IF P$(17,1)="L" AND B[0]>5 THEN LET B[0]=5
1940 FOR X=1 TO B[0]
1950 IF L+1>L9 THEN GOSUB 5000
1960 PRINT (PRTR_DEV)@(20),X$,UNDERS$(1,30)
1970 LET L=L+1
1980 NEXT X
1990 GOTO 1500
3000 REM " --- Totals
3020 GOSUB 5600
4000 REM " --- End
4100 GOTO 9900
5000 REM " --- Heading
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",PRTR_DEV,HEADINGS$[ALL],HEADINGS,PAGE_NO,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 DIM C1$(60)
5050 FIND (IVM01_DEV,KEY=N0$+BILL_NO$,DOM=5051)IOL=IVM01A
5060 PRINT (PRTR_DEV)'LF',@(5),"Bill Number: ",BILL_NO$
5070 PRINT (PRTR_DEV)@(5),"Description: ",C1$(1,MIN(DESC[1],36)),@(54),"Phantom Bill?: ",A2$(40,1)
5080 PRINT (PRTR_DEV)@(18),C1$(DESC[1]+1,MIN(DESC[2],34)),@(52),"Unit Of Measure: ",A2$(48,2)
5090 PRINT (PRTR_DEV)@(18),C1$(DESC[1]+DESC[2]+1,MIN(DESC[3],32)),@(50),"Estimated Yield %: ",A[4]:"##0.00"
5100 PRINT (PRTR_DEV)@(2),"Drawing Number: ",A0$(23,25),@(50),"Standard Lot Size: ",A[5]
5110 PRINT (PRTR_DEV)@(0),"Drawing Revision:",A0$(48,5),@(24),"Revision Code: ",A2$(38,2),@(42),"Created: ",FNB$(A2$(45,3)),@(60),"Revised: ",FNB$(A2$(35,3))
5120 PRINT (PRTR_DEV)@(62),"As Of: ",FNB$(PDATE$)
5130 PRINT (PRTR_DEV)DASHES$
5140 PRINT (PRTR_DEV)@(0),"Op Seq",@(9),"Component",@(41),"U/M",@(55-P1),"Quantity",@(70),"Picked"
5150 PRINT (PRTR_DEV)DASHES$
5190 RETURN 
5600 REM " --- Totals
5610 IF L+4>L9 THEN GOSUB 5000
5620 PRINT (PRTR_DEV)'LF',
5630 PRINT (PRTR_DEV)@(30),"Assembled __________"
5640 PRINT (PRTR_DEV)@(32),"Checked __________"
5660 PRINT (PRTR_DEV)@(33),"Packed __________"
5690 RETURN 
8000 REM " --- Functions
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8080 DEF FNQTY(Q1,Q2,Q3,Q4,Q5)=10000*Q1*Q2/(Q3*Q4*(100-Q5))
8090 DEF FNZ$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 LET STATUS=999
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Close Files and Exit
9910 CLOSE (BMM01_DEV,ERR=9911)
9920 CLOSE (BMM02_DEV,ERR=9921)
9930 CLOSE (BMM01_DEV2,ERR=9931)
9940 CLOSE (SYS01_DEV,ERR=9941)
9950 CLOSE (BMW02_DEV,ERR=9951)
9960 CLOSE (IVM01_DEV,ERR=9961)
9990 EXIT 
9999 END

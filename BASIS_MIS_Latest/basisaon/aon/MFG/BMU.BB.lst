0010 REM "BMU - BILL OF MATERIAL PURGE OBSOLETE DATA
0020 REM "Program BMU.BB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 BMM01A: IOLIST A0$,A1$,A2$,A[ALL]
0220 BMM02A: IOLIST B0$,B1$,B2$,B[ALL]
0230 BMM03A: IOLIST C0$,C1$,C[ALL]
0240 BMM05A: IOLIST W0$,W1$,W2$,W3$,W4$,W[ALL]
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
1000 REM " --- Initial Read
1020 READ (BMM01_DEV,KEY=N0$+BILLFROM$,DOM=1100)
1030 LET K0$=N0$+BILLFROM$
1040 GOTO 1140
1100 REM " --- Main Read
1120 LET K0$=KEY(BMM01_DEV,END=4000)
1140 IF POS(N0$=K0$)<>1 THEN GOTO 4000
1160 IF BILLTHRU$<>"" THEN IF K0$(3,20)>BILLTHRU$ THEN GOTO 4000
1180 READ (BMM01_DEV,KEY=K0$)IOL=BMM01A
1190 PRINT @(40,9),A0$(3,P[0]),
1200 REM " --- Material Detail Lines
1220 READ (BMM02_DEV,KEY=N0$+A0$(3,20),DOM=1221)
1300 REM " --- Material Detail Loop
1310 LET K1$=KEY(BMM02_DEV,END=1400)
1320 IF POS(A0$(1,20)=K1$)<>1 THEN GOTO 1400
1330 READ (BMM02_DEV,KEY=K1$)IOL=BMM02A
1340 IF B2$(8,3)<>"   " AND B2$(8,3)<=R2$ THEN REMOVE (BMM02_DEV,KEY=K1$)
1350 IF B2$(8,3)<>"   " AND B2$(8,3)<=R2$ THEN REMOVE (BMM04_DEV,KEY=B0$(1,2)+B1$+B0$(3),DOM=1360)
1390 GOTO 1300
1400 REM " --- Operation Detail Lines
1420 READ (BMM03_DEV,KEY=N0$+A0$(3,20),DOM=1421)
1500 REM " --- Operation Detail Loop
1510 LET K1$=KEY(BMM03_DEV,END=1600)
1520 IF POS(A0$(1,22)=K1$)<>1 THEN GOTO 1600
1530 READ (BMM03_DEV,KEY=K1$)IOL=BMM03A
1540 IF C1$(68,3)<>"   " AND C1$(68,3)<=R2$ THEN REMOVE (BMM03_DEV,KEY=K1$)
1550 IF C1$(68,3)<>"   " AND C1$(68,3)<=R2$ THEN REMOVE (BMM06_DEV,KEY=C0$(1,2)+C1$(1,3)+C0$(3),DOM=1560)
1590 GOTO 1500
1600 REM " --- Subcontract Detail Lines
1620 READ (BMM05_DEV,KEY=N0$+A0$(3,20),DOM=1621)
1700 REM " --- Subcontract Detail Loop
1710 LET K1$=KEY(BMM05_DEV,END=1800)
1720 IF POS(A0$(1,22)=K1$)<>1 THEN GOTO 1800
1730 READ (BMM05_DEV,KEY=K1$)IOL=BMM05A
1740 IF W2$(4,3)<>"   " AND W2$(4,3)<=R2$ THEN REMOVE (BMM05_DEV,KEY=K1$)
1790 GOTO 1700
3990 GOTO 1100
4000 REM " --- End
4100 GOTO 9900
8000 REM " --- Functions"
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

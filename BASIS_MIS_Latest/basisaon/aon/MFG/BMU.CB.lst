0010 REM "BMU - Bill of Material Resequence Detail Lines
0020 REM "Program BMU.CB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0095 PRECISION 9
0200 REM " --- IOLists
0210 BMM01A: IOLIST A0$,A1$,A2$,A[ALL]
0220 BMM02A: IOLIST B0$,B1$,B2$,B[ALL]
0230 BMM03A: IOLIST C0$,C1$,C[ALL]
0235 BMM04A: IOLIST U0$
0240 BMM05A: IOLIST D0$,D1$,D2$,D3$,D4$,D[ALL]
0245 BMM06A: IOLIST V0$
0250 BMM10A: IOLIST K2$,R0$,R1$,R2$
0260 BMM22A: IOLIST B0$,B1$,B2$,B[ALL]
0270 BMM23A: IOLIST C0$,C1$,C[ALL]
0280 BMM24A: IOLIST X0$,X1$
0290 BMM25A: IOLIST D0$,D1$,D2$,D3$,D4$,D[ALL]
0500 REM " --- Restart Logic
0510 IF PHASE$(4,1)="R" THEN GOTO 2800
0520 IF PHASE$(4,1)="D" THEN GOTO 3200
0710 PRINT 'SB',@(31,11),"Phase 1:",'SF',
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
1000 REM " --- Initial Read
1020 READ (BMM01_DEV,KEY=N0$+R0$,DOM=1100)
1030 LET K0$=N0$+R0$
1040 GOTO 1140
1100 REM " --- Main Read
1120 LET K0$=KEY(BMM01_DEV,END=1900)
1140 IF POS(N0$=K0$)<>1 THEN GOTO 1900
1160 IF FNP$(R1$)<>"" THEN IF K0$(3,20)>R1$ THEN GOTO 1900
1180 READ (BMM01_DEV,KEY=K0$)IOL=BMM01A
1190 PRINT @(40,11),A0$(3,P[0]),
1200 REM " --- Material Detail Lines
1210 IF POS("M"=R2$)=0 THEN GOTO 1400
1220 READ (BMM02_DEV,KEY=N0$+A0$(3,20),DOM=1221)
1230 LET X=R3
1300 REM " --- Material Detail Loop
1310 LET K1$=KEY(BMM02_DEV,END=1400)
1320 IF POS(A0$(1,22)=K1$)<>1 THEN GOTO 1400
1330 READ (BMM02_DEV,KEY=K1$)IOL=BMM02A
1340 LET X=X+R3
1350 IF X>999 THEN LET ERROR$=ERROR$+"M"+K1$(3,P[0]); GOTO 1400
1390 GOTO 1300
1400 REM " --- Operation Detail Lines
1410 IF POS("O"=R2$)=0 THEN GOTO 1600
1420 READ (BMM03_DEV,KEY=N0$+A0$(3,20),DOM=1421)
1430 LET X=R3
1500 REM " --- Operation Detail Loop"
1510 LET K1$=KEY(BMM03_DEV,END=1600)
1520 IF POS(A0$(1,22)=K1$)<>1 THEN GOTO 1600
1530 READ (BMM03_DEV,KEY=K1$)IOL=BMM03A
1540 LET X=X+R3
1550 IF X>999 THEN LET ERROR$=ERROR$+"O"+K1$(3,P[0]); GOTO 1600
1590 GOTO 1500
1600 REM " --- Subcontract Detail Lines
1610 IF POS("S"=R2$)=0 THEN GOTO 1800
1620 READ (BMM05_DEV,KEY=N0$+A0$(3,20),DOM=1621)
1630 LET X=R3
1700 REM " --- Subcontract Detail Loop"
1710 LET K1$=KEY(BMM05_DEV,END=1800)
1720 IF POS(A0$(1,22)=K1$)<>1 THEN GOTO 1800
1730 READ (BMM05_DEV,KEY=K1$)IOL=BMM05A
1740 LET X=X+R3
1750 IF X>999 THEN LET ERROR$=ERROR$+"S"+K1$(3,P[0]); GOTO 1800
1790 GOTO 1700
1890 GOTO 1100
1900 REM " --- Prepare for Phase 2"
1910 IF ERROR$="" THEN GOTO 2000
1920 LET L0=13,X=1,X$="Material   Operation  Subcontract"
1930 PRINT @(0,L0),"Bill Number "+ERROR$(X+1,P[0])+" "+FNP$(X$(POS(ERROR$(X,1)=X$),11))," lines cannot be resequenced by "+STR(R3)
1940 LET X=X+P[0]+1,L0=L0+1
1950 IF LEN(ERROR$(X))=0 THEN GOSUB 6000; GOTO 9900
1960 IF L0>20 THEN GOSUB 6000
1970 GOTO 1930
2000 REM " --- Phase 2"
2010 PRINT 'SB',@(31,11),"Phase 2:",'SF','CL',
2200 REM " --- Read Material Detail Lines
2210 IF POS("M"=R2$)=0 THEN GOTO 2400
2220 READ (BMM02_DEV,KEY=N0$+R0$,DOM=2221)
2230 LET T0$=""
2300 REM " --- Material Detail Loop"
2310 LET K1$=KEY(BMM02_DEV,END=2400)
2320 IF POS(N0$=K1$)<>1 THEN GOTO 2400
2330 IF FNP$(R1$)<>"" THEN IF K1$(3,20)>R1$ THEN GOTO 2400
2340 IF T0$<>K1$(3,20) THEN LET T0$=K1$(3,20),X=R3
2350 READ (BMM02_DEV,KEY=K1$)IOL=BMM02A
2360 LET B0$(23,3)=STR(X:"000"),K1$(23,3)=STR(X:"000")
2365 PRINT @(40,11),B0$(3,P[0]),
2370 WRITE (BMM22_DEV,KEY=K1$)IOL=BMM02A
2380 LET X=X+R3
2390 GOTO 2300
2400 REM " --- Read Operation Detail Lines
2410 IF POS("O"=R2$)=0 THEN GOTO 2600
2420 READ (BMM03_DEV,KEY=N0$+R0$,DOM=2421)
2430 LET T0$=""
2500 REM " --- Operation Detail Loop"
2510 LET K1$=KEY(BMM03_DEV,END=2600)
2520 IF POS(N0$=K1$)<>1 THEN GOTO 2600
2530 IF FNP$(R1$)<>"" THEN IF K1$(3,20)>R1$ THEN GOTO 2600
2540 IF T0$<>K1$(3,20) THEN LET T0$=K1$(3,20),X=R3
2545 READ (BMM03_DEV,KEY=K1$)IOL=BMM03A
2550 LET X0$=N0$+C0$(3,23),X1$=STR(X:"000")
2555 WRITE (BMM24_DEV,KEY=X0$)IOL=BMM24A
2560 LET C0$(23,3)=STR(X:"000"),K1$(23,3)=STR(X:"000")
2565 PRINT @(40,11),C0$(3,P[0]),
2570 WRITE (BMM23_DEV,KEY=K1$)IOL=BMM03A
2580 LET X=X+R3
2590 GOTO 2500
2600 REM " --- Read Subcontract Detail Lines
2610 IF POS("S"=R2$)=0 THEN GOTO 2800
2620 READ (BMM05_DEV,KEY=N0$+R0$,DOM=2621)
2630 LET T0$=""
2700 REM " --- Subcontract Detail Loop"
2710 LET K1$=KEY(BMM05_DEV,END=2800)
2720 IF POS(N0$=K1$)<>1 THEN GOTO 2800
2730 IF FNP$(R1$)<>"" THEN IF K1$(3,20)>R1$ THEN GOTO 2800
2740 IF T0$<>K1$(3,20) THEN LET T0$=K1$(3,20),X=R3
2750 READ (BMM05_DEV,KEY=K1$)IOL=BMM05A
2760 LET D0$(23,3)=STR(X:"000"),K1$(23,3)=STR(X:"000")
2765 PRINT @(40,11),D0$(3,P[0]),
2770 WRITE (BMM25_DEV,KEY=K1$)IOL=BMM05A
2780 LET X=X+R3
2790 GOTO 2700
2800 REM " --- Write control record"
2810 LET K2$=N0$+"AR"
2820 WRITE (BMM10_DEV,KEY=K2$(1,3))IOL=BMM10A
2830 PRINT 'SB',@(31,11),"Phase 3:",'SF','CL',
2900 REM " --- Delete Material Lines"
2910 IF POS("M"=R2$)=0 THEN GOTO 3000
2920 READ (BMM02_DEV,KEY=N0$+R0$,DOM=2921)
2930 LET K1$=KEY(BMM02_DEV,END=3000)
2935 READ (BMM02_DEV,KEY=K1$)IOL=BMM02A
2940 IF POS(N0$=K1$)<>1 THEN GOTO 3000
2950 IF FNP$(R1$)<>"" THEN IF K1$(3,20)>R1$ THEN GOTO 3000
2960 REMOVE (BMM02_DEV,KEY=K1$)
2965 REMOVE (BMM04_DEV,KEY=N0$+B1$+B0$(3,23),DOM=2966)
2970 PRINT @(40,11),K1$(3,P[0]),
2990 GOTO 2900
3000 REM " --- Delete Operation Lines"
3010 IF POS("O"=R2$)=0 THEN GOTO 3100
3020 READ (BMM03_DEV,KEY=N0$+R0$,DOM=3021)
3030 LET K1$=KEY(BMM03_DEV,END=3100)
3035 READ (BMM03_DEV,KEY=K1$)IOL=BMM03A
3040 IF POS(N0$=K1$)<>1 THEN GOTO 3100
3050 IF FNP$(R1$)<>"" THEN IF K1$(3,20)>R1$ THEN GOTO 3100
3060 REMOVE (BMM03_DEV,KEY=K1$)
3065 REMOVE (BMM06_DEV,KEY=N0$+C1$(1,3)+C0$(3,23),DOM=3066)
3070 PRINT @(40,11),K1$(3,P[0]),
3090 GOTO 3000
3100 REM " --- Delete Subcontract Lines"
3110 IF POS("S"=R2$)=0 THEN GOTO 3200
3120 READ (BMM05_DEV,KEY=N0$+R0$,DOM=3121)
3130 LET K1$=KEY(BMM05_DEV,END=3200)
3140 IF POS(N0$=K1$)<>1 THEN GOTO 3200
3150 IF FNP$(R1$)<>"" THEN IF K1$(3,20)>R1$ THEN GOTO 3200
3160 REMOVE (BMM05_DEV,KEY=K1$)
3170 PRINT @(40,11),K1$(3,P[0]),
3190 GOTO 3100
3200 REM " --- Write control record"
3210 LET K2$=N0$+"AD"
3220 WRITE (BMM10_DEV,KEY=K2$(1,3))IOL=BMM10A
3230 PRINT 'SB',@(31,11),"Phase 4:",'SF','CL',
3300 REM " --- Update Material Lines
3310 IF POS("M"=R2$)=0 THEN GOTO 3400
3320 READ (BMM22_DEV,KEY=N0$,DOM=3321)
3330 LET K1$=KEY(BMM22_DEV,END=3400)
3335 IF POS(N0$=K1$)<>1 THEN GOTO 3400
3340 READ (BMM22_DEV,KEY=K1$)IOL=BMM02A
3350 WRITE (BMM02_DEV,KEY=K1$)IOL=BMM02A
3360 LET U0$=N0$+B1$+B0$(3,23)
3365 IF B2$(4,1)="S" THEN WRITE (BMM04_DEV,KEY=U0$)IOL=BMM04A
3370 REMOVE (BMM22_DEV,KEY=K1$)
3380 PRINT @(40,11),K1$(3,P[0]),
3390 GOTO 3330
3400 REM " --- Update Operation Lines
3410 IF POS("O"=R2$)=0 THEN GOTO 3500
3420 READ (BMM23_DEV,KEY=N0$,DOM=3421)
3430 LET K1$=KEY(BMM23_DEV,END=3500)
3435 IF POS(N0$=K1$)<>1 THEN GOTO 3500
3440 READ (BMM23_DEV,KEY=K1$)IOL=BMM03A
3450 WRITE (BMM03_DEV,KEY=K1$)IOL=BMM03A
3460 LET V0$=N0$+C1$(1,3)+C0$(3,23)
3465 IF C1$(4,1)="S" THEN WRITE (BMM06_DEV,KEY=V0$)IOL=BMM06A
3470 REMOVE (BMM23_DEV,KEY=K1$)
3480 PRINT @(40,11),K1$(3,P[0]),
3490 GOTO 3430
3500 REM " --- Update Subcontract Lines
3510 IF POS("S"=R2$)=0 THEN GOTO 3600
3520 READ (BMM25_DEV,KEY=N0$,DOM=3521)
3530 LET K1$=KEY(BMM25_DEV,END=3600)
3535 IF POS(N0$=K1$)<>1 THEN GOTO 3600
3540 READ (BMM25_DEV,KEY=K1$)IOL=BMM05A
3550 WRITE (BMM05_DEV,KEY=K1$)IOL=BMM05A
3560 REMOVE (BMM25_DEV,KEY=K1$)
3570 PRINT @(40,11),K1$(3,P[0]),
3590 GOTO 3530
3600 REM " --- Rewrite Material records with new Op sequence"
3610 IF POS("O"=R2$)=0 THEN GOTO 3700
3620 READ (BMM02_DEV,KEY=N0$,DOM=3621)
3630 LET K1$=KEY(BMM02_DEV,END=3700)
3640 IF POS(N0$=K1$)<>1 THEN GOTO 3700
3645 PRINT @(40,11),B0$(3,P[0]),
3650 READ (BMM02_DEV,KEY=K1$)IOL=BMM02A
3660 READ (BMM24_DEV,KEY=B0$(1,22)+B2$(1,3),DOM=3630)IOL=BMM24A
3670 LET B2$(1,3)=X1$(1,3)
3680 WRITE (BMM02_DEV,KEY=K1$)IOL=BMM02A
3690 GOTO 3630
3700 REM " --- Rewrite Subcontract record with new Op sequence"
3710 IF POS("O"=R2$)=0 THEN GOTO 3800
3720 READ (BMM05_DEV,KEY=N0$,DOM=3721)
3730 LET K1$=KEY(BMM05_DEV,END=3800)
3740 IF POS(N0$=K1$)<>1 THEN GOTO 3800
3745 PRINT @(40,11),D0$(3,P[0]),
3750 READ (BMM05_DEV,KEY=K1$)IOL=BMM05A
3760 READ (BMM24_DEV,KEY=D0$(1,22)+D3$(1,3),DOM=3730)IOL=BMM24A
3770 LET D3$(1,3)=X1$(1,3)
3780 WRITE (BMM05_DEV,KEY=K1$)IOL=BMM05A
3790 GOTO 3730
3800 REM " --- Remove Control Record
3810 REMOVE (BMM10_DEV,KEY=N0$+"A")
4000 REM " --- Now Rebuild Cross References"
4100 GOTO 9900
6000 REM " --- Print next screen"
6010 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="<Enter> To Continue  ",V0=1,V1=FNV(V4$),V2=22
6020 GOSUB 7000
6025 IF V3=4 THEN EXITTO 9900
6030 LET L0=13
6040 PRINT @(0,L0),'CE',
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8090 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "BMR - Bill Material Availability Inquiry"
0020 REM "Program BMR.IA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 SETESC 9000
0050 SETERR 9000
0200 REM " --- IOLists
0220 BMM02A: IOLIST B0$,B1$,B2$,B[ALL]
0230 IVM02A: IOLIST C0$,C1$,C2$,C[ALL]
0240 IVM01A: IOLIST D0$,D1$(1)
0260 BMS01A: IOLIST X$,P1$,P2$,P3$,P4,P5
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0290 IVS01A: IOLIST X$,P5$,P6$,X1$,X$,X$,P7$,P8$,P9$
0395 GOTO 0440
0400 REM " --- Parameters
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="BM",N4$=F5$
0430 FIND (SYS01_DEV,KEY=N0$+N2$+"00",DOM=9800)IOL=BMS01A
0440 FIND (SYS01_DEV,KEY=N0$+"IV"+"00",DOM=9800)IOL=IVS01A
0450 DIM P[2],C[20],C0$(24),C1$(64)
0490 LET P[0]=NUM(P6$(1,2))
0495 PRECISION NUM(P6$(5,1))
0500 REM " --- Init Data
0515 DIM B[10],B0$(55),B1$(75),B2$(10)
0530 LET L9=21
0540 LET A0$(1,2)=N0$,H1$=N1$
0560 CALL "SYC.RM",P8$,P8,5,15
0575 LET M2$=P8$
0580 LET M2=LEN(M2$)
0700 REM " --- Background
0730 PRINT 'SB',@(0,10),"Quantity Required This Order/Quote:",@(50),"Production Date:",
0740 PRINT @(0,11),"Seq",@(5,11),"Wh",@(9,11),"Item",@(79-4*M2-3),"Qty",@(79-3*M2-3),"On",@(79-3),"On",
0750 PRINT @(10,12),"Description",@(79-4*M2-3),"Req",@(79-3*M2-4),"Hand",@(79-2*M2-6),"Commit",@(79-M2-5),"Avail",@(79-5),"Order",
0760 CALL "SYC.WC",1,0,80,0,0,13,0
0790 PRINT 'SF',
1000 REM " --- Key
1010 PRECISION NUM(P6$(5,1))
1020 DIM B[10]
1030 LET V0$="N",V1$="ERC",V2$="",V3$=P1$,V4$="# Of Units Required",V1=36,V2=10,I0=0
1040 GOSUB 7000
1050 IF V3=4 THEN GOTO 4200
1060 LET X0=V
1090 GOTO 3900
1100 REM " --- Warehouse ID
1110 LET V0$="S",V1$="RC",V1=5,V2=12,V4$="Enter Warehouse",V0=2,V2$=X1$(6,2)
1120 GOSUB 7000
1125 IF V3=4 THEN GOTO 4200
1130 LET V$=V$+"  ",V$=V$(1,2)
1140 LET X0$=V$
1150 FIND (IVM10_DEV,KEY=N0$+"C"+X0$,DOM=1100)
1190 GOTO 3900
1200 REM " --- Production Date
1210 IF PDATE$="" THEN LET PDATE$=F0$(7,3)
1220 LET V0$="D",V1$="ERC",V2$=PDATE$,V3$="",V4$="Enter Production Date",V0=8,V1=67,V2=10
1230 GOSUB 7000
1240 IF V3=4 THEN GOTO 4200
1250 LET PDATE$=V$
1290 GOTO 3900
2000 REM " --- Read Loop
2010 READ (BMM02_DEV,KEY=A0$(1,22),DOM=2011)
2020 LET L=14
2030 LET K$=KEY(BMM02_DEV,END=4000)
2040 IF POS(A0$(1,22)=K$)<>1 THEN GOTO 4000
2050 READ (BMM02_DEV)IOL=BMM02A
2060 IF B2$(4,1)<>"S" THEN GOTO 2030
2070 IF B2$(5,3)<>"   " AND B2$(5,3)>PDATE$ THEN GOTO 2030
2080 IF B2$(8,3)<>"   " AND B2$(8,3)<=PDATE$ THEN GOTO 2030
2100 REM " --- Find Item Record
2110 DIM D1$(60)
2120 FIND (IVM01_DEV,KEY=N0$+B1$,DOM=2121)IOL=IVM01A
2130 LET D1$(1)=FNITEM$(D1$,DESC[1],DESC[2],DESC[3])
2140 DIM C[17]
2150 FIND (IVM02_DEV,KEY=N0$+X0$+B1$,DOM=2170)IOL=IVM02A
2160 GOTO 2200
2170 LET C0$(3,2)="  "
2200 REM " --- Display
2210 GOSUB 5000
2220 GOTO 2030
3900 REM " --- Next Field
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1100,2000
4000 REM " --- Verification
4010 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Select An Option (List/Print): ",V0=1,V1=FNV(V4$),V2=22
4020 GOSUB 7000
4030 IF V3=4 THEN GOTO 4100
4040 IF V$="P" THEN RUN "BMR.IB"
4050 IF V$="L" THEN GOSUB 5200; GOTO 1100
4060 IF V$="" THEN GOSUB 5200; PRINT @(36,10),FILL(LEN(P1$)),@(5,12),"  ",; GOTO 1000
4070 GOTO 4000
4200 LET O1=3; RUN "BMM.MA"
5000 REM " --- Display
5010 GOSUB 6300
5020 IF L<=L9 THEN GOTO 5070
5030 LET V0$="S",V1$="C",V2$="",V3$="",V4$="<Enter>=Continue, <F4>=End: ",V0=1,V1=FNV(V4$),V2=22
5040 GOSUB 7000
5050 IF V3=4 THEN EXITTO 4000
5060 GOSUB 5200
5070 PRINT @(0,L),K$(23,3),@(5),C0$(3,2),@(8),R0$,@(9),B1$,@(79-4*M2-P1),X0*FNQTY(B[0],B[1],B[3],A[4],B[4]):P1$,@(79-4*M2),C[0]:M2$,@(79-3*M2),C[2]:M2$,@(79-2*M2),C[0]-C[2]:M2$,@(79-M2),C[1]:M2$,@(10,L+1),D1$(1,30),
5080 IF X0*FNQTY(B[0],B[1],B[3],A[4],B[4])>C[0]-C[2] THEN PRINT 'SB',@(3,L),"**",'SF',
5085 LET L=L+2
5090 RETURN 
5200 REM " --- Clear Screen
5240 PRINT @(0,14),'CE',
5280 LET L=14
5290 RETURN 
6300 REM " --- Verify if Item is a Bill Number
6320 LET R0$=" "
6340 FIND (BMM01_DEV,KEY=N0$+B1$,DOM=6390)
6360 LET R0$="*"
6390 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8080 DEF FNQTY(Q1,Q2,Q3,Q4,Q5)=10000*Q1*Q2/(Q3*Q4*(100-Q5))
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

0010 REM "BMU - Bill of Material Component Item Replacement
0020 REM "Program BMU.DB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0220 BMM02A: IOLIST W0$,W1$,W2$,W[ALL]
0240 BMM04A: IOLIST D0$
0500 REM " --- Initializations
0510 PRECISION PRE
0710 PRINT 'SB',@(31,19),"Phase 1:",'SF',
0940 REM " --- Disallow 'M'enu option in Error Routine
0950 LET EXIT_CTRL=1
1000 REM " --- Initial Read"
1020 READ (BMM02_DEV,KEY=N0$+R0$,DOM=1100)
1100 REM " --- Main Read"
1120 LET K1$=KEY(BMM02_DEV,END=1900)
1140 IF POS(N0$=K1$)<>1 THEN GOTO 1900
1160 IF R1$<>"" THEN IF K1$(3,20)>R1$ THEN GOTO 1900
1190 PRINT @(40,19),K1$(3,P[0]),
1210 READ (BMM02_DEV,KEY=K1$)IOL=BMM02A
1220 IF W2$(8,3)<>"   " THEN IF W2$(8,3)<=R5$ THEN GOTO 1290
1230 IF W2$(5,3)<>"   " THEN IF W2$(5,3)>R5$ THEN GOTO 1290
1240 IF W1$<>R2$ THEN GOTO 1290
1250 LET X=NUM(K1$(3+P[0],3))+1
1260 IF X>999 THEN LET ERROR$=ERROR$+"1"+K1$(3,P[0]+3); GOTO 1290
1270 FIND (BMM02_DEV,KEY=K1$(1,22)+STR(X:"000"),DOM=1285)
1280 LET ERROR$=ERROR$+"1"+K1$(3,P[0]+3); GOTO 1290
1285 IF K1$(3,20)=R3$ THEN LET ERROR$=ERROR$+"2"+K1$(3,P[0]+3)
1290 GOTO 1100
1900 REM " --- Prepare for Phase 2"
1910 IF ERROR$="" THEN GOTO 2000
1915 IF LEN(ERROR$)/(P[0]+4)*2<15 THEN LET HEIGHT=4+LEN(ERROR$)/(P[0]+4)*2 ELSE LET HEIGHT=19
1920 CALL "SYC.WA",0,78,HEIGHT,1,22-HEIGHT,"",ENAME$
1925 LET L0=0,X=1
1930 IF ERROR$(X,1)="1" THEN PRINT @(1,L0),"Sequence following component in Bill # "+ERROR$(X+1,P[0])+" already used.",@(5,L0+1),"Resequence Bill or manually delete lines."
1935 IF ERROR$(X,1)="2" THEN PRINT @(1,L0),"Sequence Number "+ERROR$(X+P[0]+1,3)+" for Bill Number "+ERROR$(X+1,P[0])+" would be same as",@(5,L0+1),"Bill Number. Cannot replace component."
1940 LET X=X+P[0]+4,L0=L0+2
1960 IF LEN(ERROR$(X))=0 THEN GOSUB 6000; GOTO 1990
1970 IF L0>HEIGHT-4 THEN GOSUB 6000
1980 GOTO 1930
1990 CALL "SYC.WD",ENAME$
1995 GOTO 4000
2000 REM " --- Phase 2"
2010 PRINT 'SB',@(31,19),"Phase 2:",'SF','CL',
2020 READ (BMM02_DEV,KEY=N0$+R0$,DOM=2100)
2100 REM " --- Main Read"
2120 LET K1$=KEY(BMM02_DEV,END=4000)
2140 IF POS(N0$=K1$)<>1 THEN GOTO 4000
2160 IF R1$<>"" THEN IF K1$(3,20)>R1$ THEN GOTO 4000
2190 PRINT @(40,19),K1$(3,P[0]),
2210 READ (BMM02_DEV,KEY=K1$)IOL=BMM02A
2220 IF W2$(8,3)<>"   " THEN IF W2$(8,3)<=R5$ THEN GOTO 2100
2230 IF W2$(5,3)<>"   " THEN IF W2$(5,3)>R5$ THEN GOTO 2100
2240 IF W1$<>R2$ THEN GOTO 2100
2250 LET W2$(8,3)=R5$
2260 WRITE (BMM02_DEV,KEY=K1$)IOL=BMM02A
2265 REM " --- Write New Record With Effective Date
2270 LET X=NUM(K1$(23,3))+1
2275 LET K1$(23,3)=STR(X:"000"),W0$(23,3)=STR(X:"000")
2280 LET W2$(5,6)=R5$,W1$=R3$,W[0]=W[0]*R0
2290 WRITE (BMM02_DEV,KEY=K1$)IOL=BMM02A
2300 LET D0$=K1$(1,2)+R3$+FILL(20-LEN(R3$))+K1$(3)
2305 WRITE (BMM04_DEV,KEY=D0$)IOL=BMM04A
2310 IF R4$="N" THEN GOTO 2100
2315 REM " --- Rewrite Comments with Obsolete Date"
2320 LET K1$=KEY(BMM02_DEV,END=4000)
2330 IF POS(N0$=K1$)<>1 THEN GOTO 4000
2340 IF R1$<>"" THEN IF K1$(3,20)>R1$ THEN GOTO 4000
2345 IF K1$(3,20)<>W0$(3,20) THEN GOTO 2100
2350 READ (BMM02_DEV,KEY=K1$)IOL=BMM02A
2360 IF W2$(4,1)<>"M" THEN GOTO 2220
2370 IF W2$(8,3)<>"   " THEN IF W2$(8,3)<=R5$ THEN GOTO 2320
2380 IF W2$(5,3)<>"   " THEN IF W2$(5,3)>R5$ THEN GOTO 2320
2390 LET W2$(8,3)=R5$
2400 WRITE (BMM02_DEV,KEY=K1$)IOL=BMM02A
2410 GOTO 2320
4000 REM " --- End"
4100 GOTO 9900
6000 REM " --- Print next screen"
6010 LET V0$="S",V1$="E",V2$="",V3$="",V4$="<Enter> To Continue  ",V0=1,V1=FNV(V4$),V2=HEIGHT-2
6020 GOSUB 7000
6025 IF V3=4 THEN EXITTO 9900
6030 LET L0=0
6040 PRINT @(0,0),'CS',
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9220 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

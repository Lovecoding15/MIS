0010 REM "BMR - Print Unlinked Operation Sequences in Mat/Subs"
0020 REM "Program BMR.LB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 BMM02A: IOLIST A0$,A1$,A2$,A[ALL]
0220 BMM05A: IOLIST B0$,B1$,B2$,B3$,B4$,B[ALL]
0230 BMM03A: IOLIST C0$,C1$,C[ALL]
0500 REM " --- Init Data
0510 LET H0=80,H=0,HEADINGS=2,WHEN$=F0$(7,3),CLOCK$=""
0520 DIM D1$(60),HEADINGS$[HEADINGS]
0540 LET HEADINGS$[0]=N1$,HEADINGS$[1]=N3$
0600 CALL "SYC.GA",7,1,"","",STATUS
0605 IF STATUS THEN GOTO 9900
0700 REM " --- Background
0750 CALL "SYC.NB","Printing:",P[0],COLUMN
1000 REM " --- Initial Read Of Bill
1030 READ (BMM01_DEV,KEY=N0$+BILLFROM$,DOM=1040)
1100 REM " --- Read Loop For Materials
1110 LET K$=KEY(BMM01_DEV,END=4000)
1120 IF POS(N0$=K$)<>1 THEN GOTO 4000
1130 IF BILLTHRU$<>"" THEN IF K$(3,LEN(BILLTHRU$))>BILLTHRU$ THEN GOTO 4000
1140 READ (BMM01_DEV)
1150 READ (BMM02_DEV,KEY=K$(1,22),DOM=1151)
1160 LET K1$=KEY(BMM02_DEV,END=2000)
1165 IF POS(K$=K1$)<>1 THEN GOTO 2000
1170 READ (BMM02_DEV)IOL=BMM02A
1180 LET COUNT=COUNT+1
1190 IF MOD(COUNT,5)=0 THEN PRINT @(COLUMN,11),K1$(3,P[0]),
1200 REM " --- Test for Print
1210 IF A2$(1,3)="   " THEN GOTO 1160
1220 FIND (BMM03_DEV,KEY=A0$(1,22)+A2$(1,3),DOM=1300)IOL=BMM03A
1230 IF C1$(4,1)="M" THEN GOTO 1300
1240 GOTO 1160
1300 REM " --- Print Material Line
1310 IF L+1>L9 THEN GOSUB 5000
1320 PRINT (7)@(0),A0$(3,P[0]),@(25),"Material",@(40),A0$(23,3),@(52),A2$(1,3),
1330 IF C1$(4,1)="M" THEN PRINT (7)@(60),"Linked to comment" ELSE PRINT (7)@(60),"Unlinked line"
1340 LET T0=T0+1,L=L+1
1380 GOTO 1160
2000 REM " --- Initial Read Of Subcontracts
2030 READ (BMM05_DEV,KEY=K$(1,22),DOM=2031)
2100 REM " --- Read Loop For Subcontracts
2110 LET K1$=KEY(BMM05_DEV,END=1100)
2120 IF POS(K$=K1$)<>1 THEN GOTO 1100
2140 READ (BMM05_DEV)IOL=BMM05A
2150 LET COUNT=COUNT+1
2160 IF MOD(COUNT,5)=0 THEN PRINT @(COLUMN,11),K1$(3,P[0]),
2200 REM " --- Test for Print
2210 IF B3$(1,3)="   " THEN GOTO 2100
2220 FIND (BMM03_DEV,KEY=B0$(1,22)+B3$(1,3),DOM=2300)IOL=BMM03A
2230 IF C1$(4,1)="M" THEN GOTO 2300
2240 GOTO 2100
2300 REM " --- Print Subcontract Line
2310 IF L+1>L9 THEN GOSUB 5000
2320 PRINT (7)@(0),B0$(3,P[0]),@(25),"Subcontract",@(40),B0$(23,3),@(52),B3$(1,3),
2330 IF C1$(4,1)="M" THEN PRINT (7)@(60),"Linked to comment" ELSE PRINT (7)@(60),"Unlinked line"
2340 LET T0=T0+1,L=L+1
2380 GOTO 2100
4000 REM " --- Totals
4040 IF H=0 THEN GOSUB 5000
4100 PRINT (7)'LF',@(10),"Total Records:",T0
4200 GOTO 9900
5000 REM " --- Heading
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,H,H0,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5100 PRINT (7)@(0),"Bill No.",@(25),"Type",@(40),"Seq",@(50),"Op Seq",@(60),"Comments",'LF'
5180 LET L=L+2
5190 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

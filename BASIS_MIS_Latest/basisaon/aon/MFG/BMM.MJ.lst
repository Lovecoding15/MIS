0010 REM "BMM - Bill Maintenance - Subcontract Info
0020 REM "Program BMM.MJ "
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists
0210 APM01A: IOLIST Y0$,Y1$
0220 BMM05A: IOLIST Z0$(1),Z1$(1),Z2$(1),Z3$(1),Z4$(1),Z[ALL]
0310 APS01A: IOLIST *,*,X$,*,*,VMMASK$
0400 REM " --- Parameters
0410 PRECISION P6
0440 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=6000)IOL=APS01A
0450 LET VMLEN=NUM(X$(1,2))
0500 REM " --- Initializations
0520 LET L0=11,L1=L0+1,L9=20,SAVEZ0$=""
0540 DIM Z0$(25),Z1$(69),Z2$(6),Z3$(20),Z4$(20),Z[9]
0560 LET Z0$(1)=A0$(1,22),L=L1,DEFAULT$="S"
0700 REM " --- Background
0710 PRINT @(0,L0-1),'CE',
0720 PRINT 'SB',@(10,9),"Subcontract Information"
0730 PRINT @(45),"Qty",@(54),"Alt",@(64),"Unit",@(72),"Total",
0740 PRINT @(0,L0),"Seq C Vendor",@(34),"Divisor",@(42),"Required",@(53),"Factor",@(64),"Cost",@(73),"Cost",'SF',
0900 REM 
0910 GOSUB 5100
0920 IF SAVEZ0$<>"" THEN GOTO 4000
1000 REM 
1020 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L1),'LD','LD'; LET L=L-2; GOTO 1020
1100 REM " --- Get Next Key
1120 LET K$=KEY(BMM05_DEV,END=1400)
1140 IF POS(A0$(1,22)=K$)<>1 THEN GOTO 1400
1150 READ (BMM05_DEV)IOL=BMM05A
1155 DIM Y1$(30); LET Y1$(1)="*** Not On File ***"
1160 FIND (APM01_DEV,KEY=N0$+Z1$(4,6),DOM=1170)IOL=APM01A
1180 GOTO 1490
1400 REM " --- Sequence
1405 IF L>L9 THEN PRINT @(0,L1),'LD','LD',; LET L=L9
1410 IF V3<>2 THEN LET Z0=NUM(Z0$(23,3))+5
1420 LET V0$="Z",V1$="ER",V2$=STR(Z0),V3$="",V0=3,V1=0,V2=L,I0=0,O9=1
1430 GOSUB 7000
1440 IF V3=4 THEN GOTO 4000
1450 IF V$="000" OR V$="" THEN GOTO 1400
1460 LET Z0$(23,3)=V$
1465 DIM Z[9],Z1$(69),Z2$(6),Z3$(20),Z4$(20)
1480 FIND (BMM05_DEV,KEY=Z0$(1,25),DOM=1600)IOL=BMM05A
1485 FIND (APM01_DEV,KEY=N0$+Z1$(4,6),DOM=1486)IOL=APM01A
1490 PRINT @(0,L),'CL',@(0,L+1),'CL',
1495 IF Z0$(1,25)<=SAVEZ0$ THEN GOSUB 5400
1500 GOSUB 5000
1520 LET I0=9
1530 GOTO 3300
1600 REM " --- Setup
1620 DIM Z[9]
1630 REM " --- Set Up Defaults
1640 LET Z1$(1)="",I0=0
1650 LET Z1$(1,1)=DEFAULT$
1660 LET Z[1]=1,Z[3]=1
1670 PRINT @(0,L),'CL',@(0,L+1),'CL',
1680 PRINT @(0,L),Z0$(23,3),
1690 GOTO 3200
2000 REM " --- Vendor Code
2010 IF Z1$(1,1)<>"S" THEN GOTO 3200
2020 LET V0$="M",V1$="CR",V2$=Z1$(4,VMLEN),V3$=VMMASK$
2030 LET V4$="Enter A Valid Vendor Number, <F3>=Lookup",V0=6,V1=6,V2=L
2040 GOSUB 7000
2045 IF V3=2 THEN GOTO 3200
2047 IF V3=4 THEN GOTO 4000
2050 IF V3<>3 THEN GOTO 2090
2060 IF L>L9 THEN PRINT @(0,L1),'LD','LD',; LET L=L-2; GOTO 2060
2070 LET V0$=VMMASK$,L0=L+1,L9=20,V5=21,V6=22,V$=""
2080 CALL "SYC.LV",V$
2090 LET Z1$(4,6)=V$
2095 FIND (APM01_DEV,KEY=N0$+Z1$(4,6),DOM=2000)IOL=APM01A
2120 PRINT @(V1,V2),FNF$(V$(1,VMLEN),VMMASK$)," ",Y1$(1,25-LEN(VMMASK$))
2140 PRINT @(1,L+1),'SB',"Srvc:",@(38),"Ld:",@(47),"U/M:",@(55),"Op:",@(63),"Net Qty:",'SF',
2190 GOTO 3200
2200 REM " --- Description
2210 LET V0$="S",V1$="C",V0=30,V3$="",V4$="Enter Service Description",V0=30,V1=7,V2=L+1
2215 IF Z1$(1,1)="S" THEN GOTO 2240
2230 LET V4$="Enter Optional Memo Information",V0=60,V2=L,V1=6
2240 LET V2$=Z1$(10,V0)
2250 GOSUB 7000
2260 LET Z1$(10,V0)=V$
2290 GOTO 3200
2300 REM " --- Divisor
2310 IF Z1$(1,1)<>"S" THEN GOTO 3200
2340 LET V0$="N",V1$="C",V2$=STR(Z[3]),V3$=P1$,V0=0,V1=33,V2=L
2350 GOSUB 7000
2355 IF V=0 THEN GOTO 2300
2360 LET Z[3]=V
2390 GOTO 3200
2400 REM " --- Quantity Required
2410 IF Z1$(1,1)<>"S" THEN GOTO 3200
2440 LET V0$="N",V1$="C",V2$=STR(Z[0]),V3$=P1$,V0=0,V1=42,V2=L
2450 GOSUB 7000
2460 LET Z[0]=V
2490 GOTO 3200
2500 REM " --- Alternate Factor
2510 IF Z1$(1,1)<>"S" THEN GOTO 3200
2540 LET V0$="N",V1$="C",V2$=STR(Z[1]),V3$=P2$,V0=0,V1=51,V2=L
2550 GOSUB 7000
2560 LET Z[1]=V
2570 GOSUB 5050
2590 GOTO 3200
2600 REM " --- Unit Cost
2610 IF Z1$(1,1)<>"S" THEN GOTO 3200
2640 LET V0$="N",V1$="C",V2$=STR(Z[2]),V3$=P1$,V0=0,V1=60,V2=L
2650 GOSUB 7000
2660 LET Z[2]=V
2670 GOSUB 5050
2690 GOTO 3200
2700 REM " --- Lead Time
2710 IF Z1$(1,1)<>"S" THEN GOTO 3200
2740 LET V0$="N",V1$="C",V2$=STR(Z[4]),V3$=P4$,V4$="Enter Lead Time In Days",V0=0,V1=42,V2=L+1
2750 GOSUB 7000
2760 LET Z[4]=V
2790 GOTO 3200
2800 REM " --- Op Code
2810 IF Z1$(1,1)<>"S" THEN GOTO 3200
2820 LET V0$="Z",V1$="C",V2$=Z3$(1,3),V3$="",V4$="Enter The Operation Seq This Subcontract Follows, <F1>=None, <F3>=Lookup",V0=3,V1=59,V2=L+1
2830 GOSUB 7000
2835 IF V3=2 THEN GOTO 2890
2840 IF V3=3 THEN LET V$=A0$(3,20); CALL "BMC.DA",V$; GOTO 2870
2850 IF V3=1 OR LEN(V$)=0 THEN LET V$="   "; GOTO 2880
2870 FIND (BMM03_DEV,KEY=N0$+A0$(3,20)+V$,DOM=2800)
2880 LET Z3$(1,3)=V$
2885 PRINT @(V1,V2),Z3$(1,3)
2890 GOTO 3200
3000 REM " --- Line Type"
3005 IF V3<>2 THEN LET Z1$(1,1)=DEFAULT$; GOTO 3070
3010 LET V0$="S",V1$="CKER",V2$=Z1$(1,1),V3$="SMI",V0=1,V1=4,V2=L
3020 LET V4$="S=Standard Subcontract Item   M=Memo Information   I=Internal Memo Information"
3030 GOSUB 7000
3040 ON V3 GOTO 3050,3000,3200,3000,3200
3050 LET Z1$(1,1)=V$,DEFAULT$=Z1$(1,1)
3070 PRINT @(4,L),Z1$(1,1),
3080 IF Z1$(1,1)<>"S" THEN PRINT @(0,L+1),'CL',
3090 GOTO 3200
3100 REM " --- Unit of Measure"
3105 IF Z1$(1,1)<>"S" THEN GOTO 3190
3110 LET V0$="S",V1$="C",V2$=Z1$(2,2),V3$="",V0=2,V1=52,V2=L+1
3120 LET V4$="Enter Purchase Order Unit Of Measure"
3130 GOSUB 7000
3135 ON V3 GOTO 3140,3100,3200,3100,3200
3140 LET Z1$(2,2)=V$
3150 PRINT @(V1,V2),Z1$(2,2),
3190 GOTO 3200
3200 REM " --- Seq Control
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,3000,2000,2300,2400,2500,2600,2200,2700,3100,2800,3300
3300 REM " --- Correct?
3303 IF Z1$(1,1)<>"S" THEN GOTO 3310
3305 PRINT @(69,L),Z[2]*FNSUB(Z[0],Z[1],Z[3]):P1$
3310 PRINT @(78,L),'CL',@(0,22),'CL'
3315 LET Z0=NUM(Z0$(23,3))
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,SAVEZ0$=Z0$(1,25),V4$="<Enter>=OK, <F1>=Additional Options, <F2>=Change, D=Delete",I0=9
3330 GOSUB 7000
3335 IF V3=1 THEN LET X0$=Z2$(1,3),X1$=Z2$(4,3); CALL "BMC.BA",X0$,X1$; LET Z2$(1,3)=X0$,Z2$(4,3)=X1$; GOTO 3300
3340 IF V3=2 THEN LET I0=11; GOTO 3200
3345 IF V3=4 THEN LET V$="END"
3350 IF V$="" THEN GOTO 3380
3360 IF V$="D" THEN GOTO 3700
3365 IF V$="END" THEN GOTO 4000
3370 GOTO 3300
3390 PRINT @(0,22),'LD','LD',
3400 REM " --- Write"
3430 WRITE (BMM05_DEV,KEY=Z0$(1,25))IOL=BMM05A
3450 IF Z1$(1,1)="S" THEN LET L=L+2 ELSE LET L=L+1
3470 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3700 REM " --- Delete
3720 REMOVE (BMM05_DEV,KEY=Z0$(1,25),DOM=3730)
3780 PRINT @(0,L),'LD','LD'
3790 GOTO 1000
4000 REM " --- Options
4010 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="Is The Detail Information Correct (Y/N/#/Review/List) ",V0=6,V1=FNV(V4$),V2=22
4012 GOSUB 7000
4014 LET I=NUM(V$,ERR=4020); LET V$=STR(I:"000")
4015 IF V$="000" THEN GOTO 4020
4016 IF L>L9 THEN PRINT @(0,L2),'LD','LD',; LET L=L9
4017 GOTO 1450
4020 IF A0$(15,1)="C" THEN IF V$="N" OR V$="R" OR V$="REVIEW" THEN LET V$="Y"
4030 IF V$="" THEN GOTO 4100
4040 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4050 IF V$="L" OR V$="LIST" THEN GOTO 4400
4070 ON POS(V$="YN") GOTO 4000,4100,1400
4200 REM " --- Done
4205 PRECISION P6
4220 LET O9=2,L0=11
4230 IF O0<>0 THEN GOTO 4290
4240 LET O0=3
4250 PRINT @(0,10),'CE',
4260 CALL "SYC.WC",1,0,80,0,0,9,0
4290 RUN "BMM.MA"
4400 REM " --- List
4420 GOSUB 5100
4490 GOTO 4000
4600 REM " --- Review
4620 READ (BMM05_DEV,KEY=A0$(1,22),DOM=4630)
4640 GOSUB 5400
4690 GOTO 1000
5000 REM " --- Display
5010 PRINT @(0,L),Z0$(23,3),@(4),Z1$(1,1),
5015 IF Z1$(1,1)<>"S" THEN GOTO 5080
5020 DIM Y1$(30); LET Y1$(1)="*** Not On File ***"
5025 FIND (APM01_DEV,KEY=N0$+Z1$(4,6),DOM=5030)IOL=APM01A
5030 PRINT @(4,L),Z1$(1,1),@(6),FNF$(Z1$(4,VMLEN),VMMASK$),@(8+VMLEN),Y1$(1,25-LEN(VMMASK$)),@(7,L+1),Z1$(10,30),
5040 IF Z[3]=0 THEN LET Z[3]=1
5050 PRINT @(33,L),Z[3]:P1$,@(42),Z[0]:P1$,@(51),Z[1]:P2$,@(60),Z[2]:P1$,@(69),Z[2]*FNSUB(Z[0],Z[1],Z[3]):P1$,
5060 PRINT @(1,L+1),'SB',"Srvc:",@(38),"Ld: ",'SF',Z[4]:P4$,@(47),'SB',"U/M: ",'SF',Z1$(2,2),@(55),'SB',"Op: ",'SF',Z3$(1,3),@(63),'SB',"Net Qty:",'SF',FNSUB(Z[0],Z[1],Z[3]):P1$
5070 GOTO 5090
5080 PRINT @(6,L),Z1$(10,60),
5090 RETURN 
5100 REM " --- List
5110 GOSUB 5400
5120 READ (BMM05_DEV,KEY=A0$(1,22),DOM=5130)
5130 LET K$=KEY(BMM05_DEV,END=5300)
5140 IF POS(A0$(1,22)=K$)<>1 THEN GOTO 5300
5150 READ (BMM05_DEV)IOL=BMM05A
5200 IF L<=L9 THEN GOTO 5250
5210 LET V0$="S",V1$="KC",V2$="",V3$="",V4$="<Enter>=Continue, <F4>=End",V0=1,V1=FNV(V4$),V2=22
5215 GOSUB 7000
5220 IF V3=4 THEN GOTO 5300
5230 GOSUB 5400
5250 GOSUB 5000
5255 IF Z1$(1,1)="S" THEN LET L=L+2 ELSE LET L=L+1
5260 LET SAVEZ0$=Z0$(1,25)
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen
5420 PRINT @(0,L1),'CE',
5440 LET L=L1
5490 RETURN 
5700 REM " --- Compress Desc
5720 LET D9$=D1$
5730 LET Z=POS(" "<>D9$); IF Z>1 THEN LET D9$=D9$(Z)
5740 LET X8=POS("  "=D9$); IF X8<>0 THEN LET Y8=POS(" "<>D9$(X8+2)); LET D9$=D9$(1,X8)+D9$(X8+Y8+1); IF Y8<>0 THEN GOTO 5740
5750 LET D1$=D9$
5790 RETURN 
6000 REM " --- A/P Not Installed
6010 LET V0$="S",V1$="C",V2$="",V3$="",V4$="Accounts Payable Must Be Installed For Subcontracts. <Enter> To Continue. ",V0=1,V1=FNV(V4$),V2=22
6020 GOSUB 7000
6090 GOTO 4200
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions
8070 DEF FNSUB(Q1,Q2,Q3)=Q1*Q2/Q3
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 4300
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

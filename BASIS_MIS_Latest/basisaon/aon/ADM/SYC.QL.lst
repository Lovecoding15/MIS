0010 REM "SYC - System SpeedSearch (Interpreter)"
0020 REM "Program SYC.QL"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open Files"
0110 LET FILES=1
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYM-02"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYM02_DEV=CHANNELS[1]
0200 REM " --- IOLIST's"
0210 SYM02A: IOLIST A0$(1),A1$(1)
0400 REM " --- Parameters"
0405 DIM X$(128)
0410 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2),USER$=X$(47,3)
0420 LET TNAME$=FNP$(X$(3,8)),NAME$=FNP$(X$(11,8)),ONAME$=FNP$(X$(27,8))
0430 LET WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2)),MAXOPT=NUM(X$(57,2))
0440 LET SNAME$=X$(19,8)
0500 REM " --- Initializations"
0510 DIM OPTLIST$[2,MAXOPT,6],HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0520 DIM A0$(3),A1$(24)
0540 LET D_STATUS=0,PAGE=1,PAGES=2,PGM$=PGM(-2),OPTION=0
0550 LET X$="Interpreter Information",HEADING$(FNC(X$,WIDTH-2))=X$
0560 LET FOOTING$(2)=X$,FOOTING$(WIDTH-10)="F4=End",M4$="###0"
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],X$,MAXROW,PREVMENU$,ONAME$
0700 REM " --- Background"
0710 CALL "SYC.WB",TNAME$,0
0720 PRINT 'CS','SB',@(10,0),"Operating System",@(31),"ADD+ON",@(1,1),"Login ID",@(5,2),"Name",'SF',
0730 CALL "SYC.WB",NAME$,0
0740 PRINT 'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER',@(6,1),"BBx Release",@(29),"2 Char ID",@(9,2),"Revision",@(29),"8 Char ID",@(4,3),"Serial Number",@(1,4),"Operating System",@(6,5),"O/S Version",@(9,6),"CPU Type",@(8,7),"Host Name",@(4,8),"GUI Interface",@(3,9),"Max Start Size",@(32),"Pipes?",'SF',
0750 CALL "SYC.WB",SNAME$,0
0760 PRINT 'SB',@(17,0),"Foreground   Background   Current",@(5,1),"Total Users",@(43),"User Slot",@(1,2),"Available Users",'SF',
1000 REM " --- Top window"
1010 CALL "SYC.WB",TNAME$,0
1020 LET A0$(1)=USER$,A1$(1)="(Not On File)"
1030 FIND (SYM02_DEV,KEY=A0$,DOM=1040)IOL=SYM02A
1040 PRINT @(10,1),INFO(3,2),@(30)," ",A0$,@(10,2),INFO(3,3),@(30)," ",A1$,
1500 REM " --- Middle window"
1510 CALL "SYC.WB",NAME$,0
1520 LET GUI$="None",PIPES$="No",MAXSTART$="",HOSTNAME$=""
1530 LET GUI$=INFO(6,0,ERR=1540)
1540 LET MAXSTART$=STR(DEC(INFO(6,1,ERR=1550)))
1550 LET PIPES$=INFO(6,2,ERR=1560)
1560 LET HOSTNAME$=INFO(3,4,ERR=1570)
1570 LET ID2$=HTA(INFO(3,0)),ID8$=HTA(INFO(3,1))
1580 PRINT @(18,1),SYS,@(39),ID2$,@(18,2),REV,@(39),ID8$,@(18,3),SSN,@(18,4),INFO(0,0),@(18,5),INFO(0,1),@(18,6),INFO(1,0),@(18,7),HOSTNAME$,@(18,8),GUI$,@(18,9),MAXSTART$,@(39),PIPES$,
2000 REM " --- Bottom window"
2010 CALL "SYC.WB",SNAME$,0
2020 PRINT @(23,1),FNN(2,0):M4$,@(36,1),FNN(2,2):M4$,@(23,2),FNN(2,1):M4$,@(36,2),FNN(2,3):M4$,@(48),FNN(2,4):M4$,
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$="",CHOICE2$=""
3030 GOSUB 5000
4000 REM " --- All done"
4010 CALL "SYC.WB",TNAME$,0
4020 PRINT 'CS',
4030 CALL "SYC.WB",NAME$,0
4040 PRINT 'CS',
4050 CALL "SYC.WB",SNAME$,0
4060 PRINT 'CS',
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=MAXROW
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8025 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8070 DEF FNN(Q1,Q2)=DEC(INFO(Q1,Q2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 CLOSE (SYM02_DEV,ERR=9911)
9950 EXIT 
9999 END

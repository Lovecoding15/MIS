0010 REM "SYX - Generate Executive Summary (Work In Process)"
0020 REM "Program SYX.EH"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=4
0120 DIM FILES$[FILES],CHANNELS[FILES],OPTIONS$[FILES]
0130 LET FILES$[1]="WOE-01",FILES$[2]="WOT-01",FILES$[3]="WOT-21"
0140 LET FILES$[4]="WOT-31"
0200 REM " --- IOLIST's"
0210 SYE10A: IOLIST A0$(1),A1$(1),WIP
0220 WOE01A: IOLIST B0$(1),B1$(1),B[ALL]
0230 WOT01A: IOLIST C0$(1),C1$(1),C[ALL]
0260 SYM06A: IOLIST N0$,N1$(1)
0270 SYM04A: IOLIST X$,S$(1)
0280 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0400 REM " --- Parameters"
0410 LET PROGRAM$="SYX.EI"
0420 FIND (SYM04_DEV,KEY="WO",DOM=4000)IOL=SYM04A
0430 IF S$(39,1)<>"Y" THEN GOTO 4000
0440 LET TITLE$=S$(1,35)
0500 REM " --- Initialize"
0510 DIM A0$(8),A1$(32)
0550 LET YMD$=F0$(7,3),Y$=YY$,M$=MM$
0600 REM " --- Additional File Opens"
0610 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS THEN GOTO 9900
0630 LET WOE01_DEV=CHANNELS[1],WOT01_DEV=CHANNELS[2]
0640 LET WOT21_DEV=CHANNELS[3],WOT31_DEV=CHANNELS[4]
0700 REM " --- Background"
0710 PRINT @(40,5),TITLE$,'CL',@(40,7),'CL',@(COLUMN,11),FILL(K9),
1000 REM " --- For each firm"
1010 FOR FIRM=1 TO FIRMS STEP 2
1020 LET N0$=FIRMS$(FIRM,2),WIP=0
1030 FIND (SYM06_DEV,KEY=N0$,DOM=3900)IOL=SYM06A
1040 PRINT @(40,7),N0$," ",N1$,
1400 REM " --- Parameters"
1410 FIND (SYS01_DEV,KEY=N0$+"WO00",DOM=3900)
1900 REM " --- Position WOE-01"
1910 DIM B0$(75),B1$(141),B[11]
1990 READ (WOE01_DEV,KEY=N0$,DOM=2000)
2000 REM " --- Read next work order"
2010 LET K$=KEY(WOE01_DEV,END=3900)
2020 IF POS(N0$=K$)<>1 THEN GOTO 3900
2050 PRINT @(COLUMN,11),K$(5,7),
2060 READ (WOE01_DEV)IOL=WOE01A
2070 IF B0$(15,1)<>"O" THEN GOTO 3000
2100 REM " --- For each type of work order transaction"
2110 FOR X=2 TO FILES
2200 REM " --- Position WOT-01/21/31"
2210 DIM C0$(18),C1$(40),C[11]
2220 LET C0$(1)=B0$(1,11),CHANNEL=CHANNELS[X]
2290 READ (CHANNEL,KEY=C0$(1,11),DOM=2500)
2500 REM " --- Read next WOT-01/21/31 record"
2510 LET K$=KEY(CHANNEL,END=2900)
2520 IF POS(B0$(1,11)=K$)<>1 THEN GOTO 2900
2530 IF K$(12,3)>YMD$ THEN GOTO 2900
2540 READ (CHANNEL)IOL=WOT01A
2550 LET WIP=WIP+C[2]
2590 GOTO 2500
2900 REM " --- Next file"
2990 NEXT X
3000 REM " --- Loop back for next record"
3090 GOTO 2000
3900 REM " --- Next firm"
3910 GOSUB 6000
3990 NEXT FIRM
4000 REM " --- Run next overlay"
4010 FOR X=1 TO FILES
4020 CLOSE (CHANNELS[X],ERR=4030)
4030 NEXT X
4090 RUN PROGRAM$,ERR=9900
6000 REM " --- Update SYE-10 Daily Totals"
6010 DIM A0$(8),A1$(32)
6020 LET A0$(1)=N0$+YEAR$+M$
6030 IF WIP<>0 THEN WRITE (SYE10_DEV,KEY=A0$)IOL=SYE10A
6090 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

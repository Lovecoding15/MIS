0010 REM "SYM - Process Maintenance (Batch Control)"
0020 REM "Program SYM.ID"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOList's"
0210 SYM02A: IOLIST *,X1$(1)
0220 SYM04A: IOLIST *,X2$
0230 SYM06A: IOLIST *,*,X3$
0240 SYM39A: IOLIST C0$
0250 SYM49A: IOLIST D0$(1),D1$(1),D2$(1),D3$,D[ALL]
0260 SYM59A: IOLIST Z0$
0500 REM " --- Initializations"
0510 LET L=L0+1,L9=19
0520 DIM D0$(3),D1$(50),D2$(30),D[3]
0530 LET K9$=""
0700 REM " --- Background"
0710 PRINT @(0,L0),'CE','SB',
0720 PRINT @(1,L0),"Batch",@(7),"Description",@(38),"Created By",@(63),"Opened",
0790 PRINT 'SF',
0800 REM " --- Verify Batching Is Turned On"
0805 LET MSG$=" for this Process."
0810 IF A2$(3,1)<>"Y" THEN GOTO 0850
0815 LET MSG$=" for the Required System."
0820 READ (SYM04_DEV,KEY=A2$(1,2),DOM=0850)IOL=SYM04A
0825 IF X2$(47,1)<>"Y" THEN GOTO 0850
0830 LET MSG$=" for this Firm."
0835 READ (SYM06_DEV,KEY=N0$,DOM=0850)IOL=SYM06A
0840 IF X3$(2,1)<>"Y" THEN GOTO 0850
0845 GOTO 0900
0850 REM " --- Warn Batching Is Turned Off"
0860 LET NUMBER=1
0865 DIM MESSAGE$[NUMBER]
0870 LET MESSAGE$[0]="Batching is NOT turned ON"+MSG$
0875 LET MESSAGE$[1]="          <Enter>=Continue "
0880 CALL "SYC.XA",3,MESSAGE$[ALL],NUMBER,-1,-1,V$,V3
0890 GOTO 4100
0900 REM " --- Display any existing lines"
0920 GOSUB 5100
0990 GOTO 4000
1000 REM " --- Overflow"
1010 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L0+1),'LD',
1020 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1110 LET D0$(1)="",K$=KEY(SYM59_DEV,END=1400)
1120 IF POS(A0$(1,10)+N0$=K$)<>1 THEN GOTO 1400
1130 READ (SYM59_DEV)
1140 READ (SYM49_DEV,KEY=K$(13,3),DOM=1100)IOL=SYM49A
1150 LET D0$(1)=K$(13,3)
1190 GOTO 1500
1400 REM " --- Batch Number"
1420 LET V0$="Z",V1$="KERC",V2$=D0$,V3$="",V4$="Enter Batch Number",V0=3,V1=2,V2=L,I0=0
1440 GOSUB 7000
1445 IF V3=4 THEN GOTO 4000
1450 IF NUM(V$,ERR=1400)=0 THEN GOTO 1400
1460 FIND (SYM49_DEV,KEY=V$,DOM=1600)IOL=SYM49A
1470 IF POS(A0$(1,10)+N0$=D1$)<>1 THEN GOTO 1400
1480 IF FNP$(D1$(26,10))="" THEN GOTO 1400; REM "Batch creation not completed
1490 LET D0$(1)=V$
1500 REM " --- Display Existing Batch"
1510 GOSUB 5000
1520 LET I0=2
1530 GOTO 3300
1600 REM " --- Initialize New Batch"
1605 DIM D[3]
1610 LET D0$(1)=V$,D1$(1)=A0$(1,10)+N0$+F0$(4,3)+F0$(18,6)+DATE(0:"%Hz%mz"),D2$(1)=A1$
1620 WRITE (SYM49_DEV,KEY=D0$,DOM=1400)IOL=SYM49A
1630 LET Z0$=D1$(1,10)+D1$(11,2)+D0$
1640 WRITE (SYM59_DEV,KEY=Z0$)IOL=SYM59A
1650 REM " --- Confirm New Batch"
1660 CALL "SYC.YN",1,"Is This A New Batch",1,V$,V3
1670 ON V3 GOTO 1680,1650,1650,1650,3900
1680 ON POS(V$="YN") GOTO 1650,1690,3900
1690 GOSUB 5000
2000 REM " --- Description"
2020 LET V0$="S",V1$="C",V2$=D2$,V3$="",V4$="Enter Batch Description",V0=30,V1=7,V2=L,I0=1
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3200,2000,3200
2050 LET D2$(1)=V$
2090 GOTO 3200
3200 REM " --- Sequence Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,2000,3300
3300 REM " --- Line Correct?"
3310 LET V4$="<Enter>=Correct     <F2>=Changes     <D>=Delete"
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=D0$
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3300,3200,3300,3350
3350 IF V$="" THEN GOTO 3400
3360 IF V$="D" THEN GOTO 3700
3390 GOTO 3300
3400 REM " --- Write"
3410 IF FNP$(D1$(26,10))="" THEN LET D1$(26,10)=F0$(18,6)+DATE(0:"%Hz%mz")
3420 WRITE (SYM49_DEV,KEY=D0$)IOL=SYM49A
3430 LET Z0$=D1$(1,10)+D1$(11,2)+D0$
3440 WRITE (SYM59_DEV,KEY=Z0$)IOL=SYM59A
3470 LET L=L+1
3480 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3700 REM " --- Remove - Check For Existing Batched Files"
3710 LET NMBR=99,COUNT=0,EXIST=0
3720 DIM FLS$[NMBR],OPTS$[NMBR],CHNLS[NMBR]
3730 READ (SYM39_DEV,KEY=A0$(1,10),DOM=3740)
3740 LET K$=KEY(SYM39_DEV,END=3800)
3750 IF POS(A0$(1,10)=K$)<>1 THEN GOTO 3800
3760 READ (SYM39_DEV)IOL=SYM39A
3770 IF COUNT>=NMBR THEN GOTO 3850
3780 LET COUNT=COUNT+1,FLS$[COUNT]=C0$(11)+"."+D0$,OPTS$[COUNT]="F"
3790 GOTO 3740
3800 IF COUNT=0 THEN GOTO 3900
3810 CALL "SYC.DA",1,1,COUNT,FLS$[ALL],OPTS$[ALL],CHNLS[ALL],BTCH,STATUS
3815 IF STATUS>0 THEN GOTO 3850
3820 FOR X=1 TO COUNT
3825 IF CHNLS[X] THEN CLOSE (CHNLS[X],ERR=3840); LET EXIST=EXIST+1
3830 NEXT X
3840 IF EXISTS=0 THEN GOTO 3900
3850 REM " --- Remove - Batched Files Exist So Can NOT Purge"
3860 LET NUMBER=1
3865 DIM MESSAGE$[NUMBER]
3870 LET MESSAGE$[0]="Files exist for this batch.  Can NOT delete it!"
3875 LET MESSAGE$[1]="          <Enter>=Continue "
3880 CALL "SYC.XA",2,MESSAGE$[ALL],NUMBER,-1,-1,V$,V3
3890 GOTO 3990
3900 REM " --- Remove - Okay To Purge Batch"
3920 REMOVE (SYM49_DEV,KEY=D0$,DOM=3930)
3940 REMOVE (SYM59_DEV,KEY=D1$(1,10)+D1$(11,2)+D0$,DOM=3950)
3960 PRINT @(0,L),'CL',
3990 GOTO 1000
4000 REM " --- Options"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/List/Review)?",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4100
4050 IF V$="L" OR V$="LIST" THEN GOTO 4200
4070 IF V$="R" OR V$="REVIEW" THEN GOTO 4400
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run Header Overlay"
4120 RUN "SYM.IA"
4200 REM " --- List"
4210 GOSUB 5100
4290 GOTO 4000
4400 REM " --- Review"
4410 READ (SYM59_DEV,KEY=A0$(1,10)+N0$,DOM=4420)
4420 GOSUB 5400
4490 GOTO 1000
5000 REM " --- Display"
5010 IF D0$<=K9$ THEN GOSUB 5400
5020 GOSUB 6000
5040 PRINT @(2,L),'CL',D0$,@(7),D2$,@(38),X1$,@(63),FNB6$(D1$(16,6))," ",D1$(22,2),":",D1$(24,2),
5090 RETURN 
5100 REM " --- List"
5110 GOSUB 5400
5120 READ (SYM59_DEV,KEY=A0$(1,10)+N0$,DOM=5130)
5130 LET K$=KEY(SYM59_DEV,END=5300)
5140 IF POS(A0$(1,10)+N0$=K$)<>1 THEN GOTO 5300
5160 READ (SYM59_DEV)
5170 READ (SYM49_DEV,KEY=K$(13,3),DOM=5290)IOL=SYM49A
5180 IF FNP$(D1$(26,10))="" THEN GOTO 5290; REM "Batch creation not completed
5190 IF L<=L9 THEN GOTO 5250
5200 REM 
5210 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Press <Enter> To Continue",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5230 IF V3=4 THEN GOTO 5300
5240 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+1,K9$=D0$
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5410 PRINT @(0,L0+1),'CE',
5440 LET L=L0+1
5490 RETURN 
6000 REM " --- Retrieve Operator Description"
6010 DIM X1$(24)
6020 LET X1$(1)="Not On File"
6030 READ (SYM02_DEV,KEY=D1$(13,3),DOM=6090)IOL=SYM02A
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

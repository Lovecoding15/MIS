0010 REM "SYM - Public Program Maintenance"
0020 REM "Program SYM.PA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=2
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="SYM-05"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],SYM05_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 SYM05A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0500 REM " --- Initializations"
0510 DIM A0$(11),A1$(8),A2$(64),A[2],X0$(8),DESCRIPTION$(50)
0560 LET L0=7,L9=21,L=L0,M2$="###,##0-"
0690 READ (SYM05_DEV,KEY="",DOM=0700)
0700 REM " --- Background"
0710 PRINT 'SB',@(0,3),"Program Name:",'SF',
0720 CALL "SYC.WC",1,0,80,0,0,L0-3,0
0730 PRINT 'SB',@(1,L0-2),"Seq",@(5),"Program",@(14),"Description",@(60),"Size",@(65),"Add?",@(70),"Priority",'SF',
0740 CALL "SYC.WC",1,0,80,0,0,L0-1,0
0800 REM " --- Source Program"
0810 PRINT 'CF',
0820 LET V2$="",V2$=KEY(SYM05_DEV,END=0830)
0830 IF V2$<>"" THEN LET V2$=V2$(1,8)
0840 LET V4$="Enter The Name Of The Program For Which Subprograms Should Be Added"
0850 LET V0$="S",V1$="CE",V3$="",V0=8,V1=14,V2=3
0860 GOSUB 7000
0870 ON V3 GOTO 0880,0800,0800,0800,4500
0880 LET A0$(1)=V$,X0$(1)=V$
0900 REM " --- Verify existance and retrieve description"
0910 LET PGM$=FNP$(A0$)
0915 GOSUB 6500
0920 IF STATUS>0 THEN GOTO 0850
0925 LET SIZE$=FNS$(STR(SIZE:M2$)+"Byte(s)")
0930 PRINT @(14,3),A0$(1,8)," ",DESCRIPTION$,@(79-LEN(SIZE$))," ",SIZE$
0950 REM " --- Display any existing lines"
0970 GOSUB 5100
0980 IF Q1=0 THEN GOTO 1400
0990 GOTO 4000
1000 REM " --- Overflow"
1010 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L0),'LD',
1020 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1120 LET K$=KEY(SYM05_DEV,END=1400)
1140 IF POS(X0$=K$)<>1 THEN GOTO 1400
1150 READ (SYM05_DEV)IOL=SYM05A
1190 GOTO 1490
1400 REM " --- Sequence Number"
1410 LET W0=NUM(A0$(9))+10
1415 LET V4$="Enter A 3 Digit Sequence Number"
1420 LET V0$="Z",V1$="KERC",V2$=STR(W0),V3$="",V0=3,V1=1,V2=L,I0=0
1430 GOSUB 7000
1440 IF V3=4 THEN PRINT @(V1,L),FILL(3); GOTO 4000
1450 IF V$="000" OR V$="" THEN GOTO 1400
1460 LET A0$(9)=V$
1480 FIND (SYM05_DEV,KEY=A0$,DOM=1600)IOL=SYM05A
1490 IF A0$<=K9$ THEN GOSUB 5400
1500 GOSUB 5000
1520 LET I0=3
1530 GOTO 3300
1600 REM " --- New Record"
1610 DIM A1$(8),A2$(64),A[2]
1620 LET A2$(51,1)="Y",A2$(52,1)="A"
1630 PRINT @(6,L),'CL',
2000 REM " --- Program Name"
2010 LET V4$="Enter The Name Of The Program To Add",I0=1
2020 LET V0$="S",V1$="C",V2$=A1$,V3$="",V0=8,V1=5,V2=L
2030 GOSUB 7000
2040 ON V3 GOTO 2100,2000,3200,2050,2100
2100 REM 
2110 LET PGM$=FNP$(V$)
2120 GOSUB 6500
2130 IF STATUS>0 THEN GOTO 2000
2140 LET A1$(1)=PGM$,A2$(1,50)=DESCRIPTION$,A[0]=SIZE
2150 GOSUB 5000
2190 GOTO 3200
2200 REM " --- Add?"
2210 LET V0$="Y",V1$="C",V2$=A2$(51,1),V3$="",V0=1,V1=66,V2=L
2220 LET V4$="Add This Program To Memory When "+FNP$(X0$)+" Is Run (Y/N)?"
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3200,2200,3200
2250 LET A2$(51,1)=V$
2290 GOTO 3200
2400 REM " --- Priority"
2410 LET V4$="Enter The Priority Code Assigned To This Program"
2420 LET V0$="S",V1$="C",V2$=A2$(52,1),V3$="",V0=1,V1=74,V2=L
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3200,2400,3200
2450 LET A2$(52,1)=V$
2490 GOTO 3200
3200 REM " --- Sequence Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,2000,2200,2400,2600,2800,3300
3300 REM " --- Line Correct?"
3310 LET V4$="<Enter>=Correct     <D>=Delete"
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=B0$(1,K9)
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3300,3200,3300,3350
3350 IF V$="" THEN GOTO 3400
3360 IF V$="D" THEN GOTO 3700
3390 GOTO 3300
3400 REM " --- Write"
3410 WRITE (SYM05_DEV,KEY=A0$)IOL=SYM05A
3450 LET L=L+1
3460 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3700 REM " --- Remove"
3710 REMOVE (SYM05_DEV,KEY=A0$,DOM=3720)
3750 PRINT @(0,L),'CL',
3760 IF V3=4 THEN GOTO 4000
3790 GOTO 1000
4000 REM " --- Options"
4010 LET V4$="Is The Above Information Correct (Y/N/List/Review/Delete)?"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4100
4050 IF V$="L" OR V$="LIST" THEN GOTO 4400
4060 IF V$="R" OR V$="REVIEW" THEN GOTO 4600
4070 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Return to header"
4110 READ (SYM05_DEV,KEY=A0$(1,8)+$FF$,DOM=4120)
4190 GOTO 0800
4200 REM " --- Delete"
4210 READ (SYM05_DEV,KEY=A0$(1,6),DOM=4220)
4220 LET K$=KEY(SYM05_DEV,END=0800)
4230 IF POS(A0$(1,6)=K$)<>1 THEN GOTO 0800
4240 REMOVE (SYM05_DEV,KEY=K$)
4290 GOTO 4220
4400 REM " --- List"
4410 GOSUB 5100
4490 GOTO 4000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4550 ON POS(V$="YN") GOTO 4500,4590,9900
4590 RUN "SYM.PB"
4600 REM " --- Review"
4610 READ (SYM05_DEV,KEY=A0$(1,8),DOM=4620)
4620 GOSUB 5400
4690 GOTO 1000
5000 REM " --- Display"
5010 PRINT @(1,L),A0$(9),@(5),A1$," ",A2$(1,50),@(57),A[0]:M2$,@(66),A2$(51,1),@(74),A2$(52,1),
5090 RETURN 
5100 REM " --- List"
5105 LET Q1=0
5110 GOSUB 5400
5120 READ (SYM05_DEV,KEY=X0$,DOM=5130)
5130 LET K$=KEY(SYM05_DEV,END=5300)
5150 IF POS(X0$=K$)<>1 THEN GOTO 5300
5160 READ (SYM05_DEV)IOL=SYM05A
5190 IF L<=L9 THEN GOTO 5250
5200 REM 
5210 LET V4$="<Enter>=Continue:"
5215 LET V0$="S",V1$="CE",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5230 IF V3=4 THEN GOTO 5300
5240 GOSUB 5400
5250 GOSUB 5000
5260 LET Q1=1,L=L+1,K9$=A0$
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5420 PRINT @(0,L0),'CE',
5440 LET L=L0
5450 READ RECORD(CHANNEL,SIZ=10,ERR=6680)X$
5490 RETURN 
6500 REM " --- Verify program existance and return size/description"
6510 LET CHANNEL=UNT,STATUS=11,DESCRIPTION$(1)="",SIZE=0
6520 OPEN (CHANNEL,ERR=6680)PGM$
6530 LET X$=FID(CHANNEL,ERR=6680)
6540 IF X$(1,1)<>$04$ THEN GOTO 6680
6550 READ RECORD(CHANNEL,SIZ=10)X$
6560 LET SIZE=DEC($00$+X$(1,2))
6570 CALL "SYC.UB",CHANNEL,"",X$,"10",STATUS
6580 LET STATUS=0,P=POS("REM"=X$)
6590 IF P=0 THEN GOTO 6680
6600 LET X$=X$(P+3),P=POS("-"=X$)
6610 IF P=0 THEN GOTO 6680
6620 LET X$=FNP$(FNS$(X$(P+1)))
6630 IF X$="" THEN GOTO 6680
6640 IF X$(LEN(X$))=$22$ THEN LET X$=X$(1,LEN(X$)-1)
6650 LET DESCRIPTION$(1)=X$
6680 CLOSE (CHANNEL,ERR=6690)
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNS$(Q$)=CVS(Q$,1)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 RUN "SYS.AA"
9999 END

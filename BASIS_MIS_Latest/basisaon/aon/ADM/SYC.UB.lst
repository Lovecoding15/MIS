0010 REM "SYC - Return Program Source Code Line"
0020 REM "Program SYC.UB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "UNIT        : Optional channel number of open program file (input)"
0050 REM "PROGRAM$    : Program name. Required if UNIT=0 (input)"
0055 REM "DESCRIPTION$: Program statement (returned)"
0060 REM "LINE_NO$    : Line of program to return (input)"
0065 REM "STATUS      : 0=No Error (returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER UNIT,PROGRAM$,DESCRIPTION$,LINE_NO$,STATUS
0100 REM " --- Open/Lock files"
0110 LET CHANNEL=UNIT
0120 IF UNIT>0 THEN GOTO 0500
0130 LET CHANNEL=UNT
0140 OPEN (CHANNEL,ERR=9200)PROGRAM$
0500 REM " --- Initializations"
0550 LET DESCRIPTION$="",STATUS=0,HEADER$=$$,D$=$$
1000 REM " --- Position file and read variable table"
1010 READ RECORD(CHANNEL,IND=0,SIZ=10)HEADER$
1020 LET PGMLEN=DEC($00$+HEADER$(1,2)),VARADD=DEC($00$+HEADER$(5,2))
1030 LET VARNUM=DEC(HEADER$(7,2)),VARLEN=PGMLEN-VARADD-2
1040 IF VARLEN>0 THEN READ RECORD(CHANNEL,IND=VARADD,SIZ=VARLEN)VARLIST$
1050 READ RECORD(CHANNEL,IND=0,SIZ=10)
2000 REM " --- Read until LINE_NO$ is encountered"
2010 READ RECORD(CHANNEL,SIZ=512,END=4000)D0$
2020 LET D$=D$+D0$,D0$=$$
2100 REM " --- Extract next line"
2110 IF LEN(D$)<4 THEN GOTO 2000
2120 LET LINLEN=DEC($00$+D$(3,2))+4,LINENUM=DEC($00$+D$(1,2))
2130 IF LEN(D$)>=LINLEN THEN GOTO 2500
2140 READ RECORD(CHANNEL,SIZ=512,END=4000)D0$
2150 LET D$=D$+D0$,D0$=$$
2190 GOTO 2100
2500 REM " --- Is this the requested line?"
2510 LET LINE$=D$(1,LINLEN),D$=D$(LINLEN+1)
2520 LET TEMP$=LST(LINE$,VARLIST$),TEMP$=TEMP$(POS(" "=TEMP$)+1)
2530 IF LINENUM<>NUM(LINE_NO$) THEN GOTO 2100
2540 LET DESCRIPTION$=STR(LINENUM:"0000")+" "+TEMP$
4000 REM " --- All done"
4010 IF FNP$(DESCRIPTION$)="" THEN LET STATUS=11
4090 GOTO 9900
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 LET STATUS=999
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 IF UNIT=0 THEN CLOSE (CHANNEL,ERR=9911)
9990 EXIT 
9999 END

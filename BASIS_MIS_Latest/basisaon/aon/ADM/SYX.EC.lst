0010 REM "SYX - Generate Executive Summary (Purchase Orders)"
0020 REM "Program SYX.EC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],CHANNELS[FILES],OPTIONS$[FILES]
0130 LET FILES$[1]="POE-12",FILES$[2]="POM-02"
0200 REM " --- IOLIST's"
0210 SYE06A: IOLIST A0$(1),A1$(1),A[ALL],U[ALL]
0220 POE12A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1),B4$(1),B5$,B6$,B[ALL]
0240 SYM04A: IOLIST X$,S$(1)
0260 SYM06A: IOLIST N0$,N1$(1)
0310 POM02A: IOLIST Y0$(1),Y1$(1)
0400 REM " --- Parameters"
0410 LET PGM$="SYX.ED"
0420 FIND (SYM04_DEV,KEY="PO",DOM=4000)IOL=SYM04A
0430 IF S$(39,1)<>"Y" THEN GOTO 4000
0440 LET TITLE$=S$(1,35)
0500 REM " --- Initialize"
0510 DIM A0$(8),A1$(32),A[31],U[31],L[31],Y0$(4),Y1$(32)
0520 DIM B0$(18),B1$(48),B2$(32),B3$(22),B4$(40),B[12]
0550 LET MILESTONE$=""
0600 REM " --- Additional File Opens"
0610 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS THEN GOTO 9900
0630 LET POE12_DEV=CHANNELS[1],POM02_DEV=CHANNELS[2]
0700 REM " --- Background"
0710 PRINT @(40,5),TITLE$,'CL',@(40,7),'CL',@(COLUMN,11),FILL(K9),
1000 REM " --- For each firm"
1010 FOR FIRM=1 TO FIRMS STEP 2
1020 LET N0$=FIRMS$(FIRM,2)
1030 FIND (SYM06_DEV,KEY=N0$,DOM=3900)IOL=SYM06A
1040 PRINT @(40,7),N0$," ",N1$
1200 REM " --- Parameters"
1210 FIND (SYS01_DEV,KEY=N0$+"PO00",DOM=3900)
1900 REM " --- Position POE-12"
1910 LET A0$(1)=N0$+WHEN$
1990 READ (POE12_DEV,KEY=N0$,DOM=2000)
2000 REM " --- Get next key"
2010 LET K$=KEY(POE12_DEV,END=3000)
2020 IF POS(N0$=K$)<>1 THEN GOTO 3900
2030 IF K$(1,15)=MILESTONE$ THEN GOTO 2100
2040 LET MILESTONE$=K$(1,15)
2050 PRINT @(COLUMN,11),K$(3,6)," ",K$(9,7),
2100 REM " --- Read line item/line code"
2110 READ (POE12_DEV)IOL=POE12A
2120 IF N0$+B1$(1,2)=Y0$ THEN GOTO 2200
2130 LET Y0$(1)=N0$+B1$(1,2),Y1$(1)=""
2140 FIND (POM02_DEV,KEY=Y0$,DOM=2200)IOL=POM02A
2200 REM " --- Message or Vendor Item Line?"
2210 IF POS(Y1$(21,1)="SNO")=0 THEN GOTO 2900
2400 REM " --- Assemble data for update"
2410 LET X$=B1$(3,6),M$=X$(3,2),D$=X$(5,2),D=NUM(D$)
2420 LET Y$=STR(1900+FNYY_YEAR(X$(1,2)):"0000"),W$=Y$+M$
2430 LET AMOUNT=(B[3]-B[7])*B[1],UNITS=1
2700 REM " --- Update SYE-06"
2710 GOSUB 6000
2900 REM " --- Loop back for next line item"
2990 GOTO 2000
3900 REM " --- Next firm"
3990 NEXT FIRM
4000 REM " --- Run next overlay"
4010 FOR X=1 TO FILES
4020 CLOSE (CHANNELS[X],ERR=4030)
4030 NEXT X
4090 RUN PGM$,ERR=9900
6000 REM " --- Update SYE-06 Daily Totals"
6010 DIM A0$(8),A1$(32),A[31],U[31],L[31]
6030 LET A0$(1)=N0$+W$
6040 FIND (SYE06_DEV,KEY=A0$,DOM=6050)IOL=SYE06A
6050 LET A[D]=A[D]+AMOUNT,U[D]=U[D]+UNITS
6060 WRITE (SYE06_DEV,KEY=A0$)IOL=SYE06A
6070 IF A0$(3)>LAST$ THEN LET LAST$=A0$(3)
6090 RETURN 
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "SYM - G/L Posting Control Maintenance"
0020 REM "Program SYM.IE"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLists"
0210 SYM19A: IOLIST B0$(1),B1$(1),B2$(1),B3$,B[ALL]
0220 GLM03A: IOLIST Y0$(1),Y1$,Y[ALL]
0230 GLM06A: IOLIST X0$(1),X1$(1),X2$(1),X3$,X[ALL]
0400 REM " --- Parameters"
0500 REM " --- Initializations"
0510 DIM B0$(18),B1$(30),B2$(11),B[3],X0$(20),X1$(64),X2$(30),X[6],Y0$(32),Y[2]
0520 LET MASK$="000000",MASK=LEN(MASK$)
0530 LET X0$(1)=N0$+A0$
0700 REM " --- Background"
0710 PRINT @(0,L0),'CE','SB',
0750 PRINT @(26,L0),"Program Name:",@(27,L0+1),"Description:",@(28,L0+2),"Journal ID:",@(21,L0+3),"Use Audit Control:",@(21,L0+4),"Last Audit Number:",@(22,L0+5),"Use Summary File:",@(21,L0+6),"Summary File Name:",@(21,L0+7),"Print G/L Summary:",@(15,L0+8),"Summarized Posting Memo:",@(19,L0+9),"Detail Transactions:",@(29,L0+10),"Last Used:",@(26,L0+11),"Last Updated:",'SF',
0900 REM " --- Position file"
0990 READ (SYM19_DEV,KEY=A0$,DOM=1000)
1000 REM " --- Program Name"
1005 GOSUB CLEAR_FIELDS
1010 LET V2$="",B1$(1)=""
1020 LET SYM19_K$=KEY(SYM19_DEV,END=0900)
1025 IF SYM19_K$(1,10)<>A0$ THEN GOTO 0990
1030 LET V2$=SYM19_K$(11,8)
1040 LET V0$="S",V1$="C",V3$="",V0=8,V1=40,V2=L0,I0=0
1045 LET V4$="Enter Program Name (<F3>=Lookup)"
1050 GOSUB 7000
1055 ON V3 GOTO 1070,1000,1000,1060,4400
1060 GOSUB 6050
1070 LET V$=V$+FILL(V0),V$=V$(1,V0)
1080 FIND (SYM19_DEV,KEY=X0$(3,10)+V$,DOM=1000)IOL=SYM19A
1090 LET X0$(13)=V$
1095 PRINT @(V1,V2),X0$(13,V0)
1200 REM " --- Retrieve record"
1220 FIND (GLM06_DEV,KEY=X0$,DOM=1400)IOL=GLM06A
1300 REM " --- Display record"
1320 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record"
1410 LET V4$="Is This A New Record (Y/N)?"
1420 LET V0$="Y",V1$="C",V2$="N",V3$="",V0=1,V1=FNV(V4$),V2=22
1430 GOSUB 7000
1440 ON V3 GOTO 1490,1400,1400,1400,4400
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record"
1510 DIM X1$(64),X2$(30),X[6]
1520 LET X1$(31,6)="GLW-11",X1$(37,3)="NNA"
1590 GOSUB 5000
1700 REM " --- Journal ID Code"
1710 LET V0=2,V0$="S",V1$="CR",V2$=X1$(1,V0),V3$="",V1=40,V2=L0+2,I0=1
1720 LET V4$="Enter The Journal ID Code To Be Used By This Program (<F3>=Lookup)"
1730 GOSUB 7000
1740 ON V3 GOTO 1765,1700,3900,1750,3900
1750 GOSUB 6000
1760 IF V$="" THEN GOTO 1700
1770 LET V$=V$+"  ",V$=V$(1,2)
1775 FIND (GLM03_DEV,KEY=N0$+V$,DOM=1700)IOL=GLM03A
1780 LET X1$(1,V0)=V$
1785 PRINT @(V1,V2),X1$(1,V0)," ",Y0$(5,20),
1790 GOTO 3900
1800 REM " --- Use Audit Control?"
1810 LET V0=1,V0$="Y",V1$="C",V2$=X1$(37,1),V3$="",V1=40,V2=L0+3
1820 LET V4$="Should This Task Use G/L Audit Control Numbers (Y/N)?"
1830 GOSUB 7000
1840 ON V3 GOTO 1850,1800,3900,1800,3900
1850 LET X1$(37,1)=V$
1860 GOSUB 5400
1890 GOTO 3900
1900 REM " --- Audit Control Number"
1905 IF X1$(37,1)<>"Y" THEN GOTO 3900
1910 LET V0$="N",V1$="CR",V2$=STR(X[0]),V3$=MASK$,V0=MASK,V1=40,V2=L0+4
1920 LET V4$="Enter The Current Audit Control Number For This Task"
1930 GOSUB 7000
1940 ON V3 GOTO 1950,1900,3900,1900,3900
1950 IF V<0 THEN GOTO 1900
1960 PRINT @(V1,V2),'CL',V:MASK$,
1970 LET X[0]=V
1990 GOTO 3900
2000 REM " --- Use Summary File?"
2010 LET V0=1,V0$="Y",V1$="C",V2$=X1$(38,1),V3$="",V1=40,V2=L0+5
2020 LET V4$="Does This Task Require A G/L Summary Work File (Y/N)?"
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 LET X1$(38,1)=V$,X$=X1$(31,6)
2060 IF X1$(38,1)="N" THEN LET X1$(31,6)="",X$="None",X1$(40,1)="N"
2070 PRINT @(V1,V2),X1$(38,1),@(V1,V2+1),X$,'CL',@(V1,V2+2),X1$(40,1),
2090 GOTO 3900
2100 REM " --- Summary File Name"
2105 IF X1$(38,1)<>"Y" THEN GOTO 3900
2110 LET V0$="S",V1$="CR^",V2$=X1$(31,6),V3$="",V0=6,V1=40,V2=L0+6
2120 LET V4$="Enter The File Name To Be Used For The General Ledger Summary"
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2150 IF V$="" THEN GOTO 2100
2155 FOR X=1 TO LEN(V$)
2160 IF POS(V$(X,1)="ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789-+.")=0 THEN EXITTO 2100
2165 NEXT X
2180 LET X1$(31,6)=V$
2185 PRINT @(V1,V2),X1$(31,6),
2190 GOTO 3900
2200 REM " --- Print G/L Summary"
2205 IF X1$(38,1)<>"Y" THEN GOTO 3900
2210 LET V4$="Y=Print G/L Summary     N=Don't Print G/L Summary"
2220 LET V0$="Y",V1$="CR",V2$=X1$(40,1),V3$="",V0=1,V1=40,V2=L0+7
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 IF POS(V$="YN")=0 THEN GOTO 2200
2260 LET X1$(40,1)=V$
2270 PRINT @(V1,V2),X1$(40,1),
2290 GOTO 3900
2300 REM " --- Summarized Posting Memo"
2310 LET V0$="S",V1$="C",V2$=X2$,V3$="",V0=30,V1=40,V2=L0+8
2315 IF FNP$(V2$)="" THEN LET V2$="Summarized "+B1$
2320 LET V4$="Enter The Default Summarized Posting Memo For This Task"
2330 GOSUB 7000
2340 ON V3 GOTO 2350,2300,3900,2300,3900
2350 LET X2$(1)=V$
2360 PRINT @(V1,V2),X2$,
2390 GOTO 3900
2400 REM " --- Summary/Detail?"
2410 LET V4$="Y=Detailed Postings From Task   N=Summarized Postings   A=Use G/L Account"
2420 LET V0$="S",V1$="CR",V2$=X1$(39,1),V3$="YNA",V0=1,V1=40,V2=L0+9
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3900,2400,3900
2450 IF POS(V$="YNA")=0 THEN GOTO 2400
2460 LET X1$(39,1)=V$
2470 PRINT @(V1,V2),X1$(39,1),
2490 GOTO 3900
3900 REM " --- Next field"
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1700,1800,1900,2000,2100,2200,2300,2400,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4400
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,1700
4100 REM " --- Write record"
4130 WRITE (GLM06_DEV,KEY=X0$)IOL=GLM06A
4190 GOTO 1000
4200 REM " --- Remove record"
4210 REMOVE (GLM06_DEV,KEY=X0$,DOM=4220)
4290 GOTO 1000
4400 REM " --- Run Overlay"
4420 RUN "SYM.IA"
5000 REM " --- Display record"
5010 DIM Y0$(32),Y[2]
5020 FIND (GLM03_DEV,KEY=N0$+X1$(1,2),DOM=5025)IOL=GLM03A
5025 PRINT @(40,L0+1),B1$,@(40,L0+2),X1$(1,2)," ",Y0$(5,20),
5030 LET V1=40,V2=L0+3,X$="None"
5035 IF X1$(38,1)="Y" THEN LET X$=X1$(31,6)
5040 GOSUB 5400
5045 PRINT @(40,L0+5),X1$(38,1),@(40,L0+6),X$,@(40,L0+7),X1$(40,1),@(40,L0+8),X2$,@(40,L0+9),X1$(39,1)
5070 IF FNP$(X1$(11,6))<>"" THEN PRINT @(40,L0+10),FNB6$(X1$(11,6))," ",FNT$(X1$(17,4)),
5080 IF FNP$(X1$(21,6))<>"" THEN PRINT @(40,L0+11),FNB6$(X1$(21,6))," ",FNT$(X1$(27,4)),
5090 RETURN 
5400 REM " --- Display G/L Audit Control"
5410 PRINT @(V1,V2),X1$(37,1),
5420 IF X1$(37,1)="Y" THEN PRINT @(V1,V2+1),X[0]:MASK$
5430 IF X1$(37,1)="N" THEN PRINT @(V1,V2+1),"None",'CL',
5490 RETURN 
5500 REM " --- Clear Field Display"
5505 CLEAR_FIELDS: 
5510 FOR X=L0 TO 22
5520 PRINT @(40,X),'CL',
5530 NEXT X
5590 RETURN 
6000 REM " --- Journal ID Lookup"
6010 LET NMBR=1
6015 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6020 LET DESCRIPT$[0]="Code",FIELD[0]=1,POSITION[0]=3,LENGTH[0]=2
6025 LET DESCRIPT$[1]="Journal",FIELD[1]=1,POSITION[1]=5,LENGTH[1]=20
6030 LET V$="SKIP"
6035 CALL "SYC.LK",GLM03_DEV,"",NMBR,"Journal ID Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6040 RETURN 
6050 REM " --- Process Program Lookup"
6060 LET NMBR=1
6065 DIM DESCRIPT$[NMBR],FIELD[NMBR],POSITION[NMBR],LENGTH[NMBR]
6070 LET DESCRIPT$[0]="Program",FIELD[0]=1,POSITION[0]=11,LENGTH[0]=8
6075 LET DESCRIPT$[1]="Description",FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6080 LET V$="NO COMPANY"
6085 CALL "SYC.LK",SYM19_DEV,X0$(3,10),NMBR,"Process Program Lookup",DESCRIPT$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],V1,V2,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8070 DEF FNT$(Q$)=Q$(1,2)+":"+Q$(3,2)
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

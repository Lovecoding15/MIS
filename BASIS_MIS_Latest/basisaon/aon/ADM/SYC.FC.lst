0010 REM "SYC - Days Forecast Ahead/Behind (Public/Called)"
0020 REM "Program SYC.FC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "FILE_DEV : Channel number MPM-02 or POM-01 is opened on (Input)
0052 REM "FIRM$    : Firm ID (Input)
0054 REM "CURRENT$ : From date 'YYMMDD' or packed 'YMD' (Input)
0056 REM "FUTURE$  : Calculated future date (Returned)
0058 REM "V0       : Number of days from CURRENT$ for future Date (Input)
0060 REM "FLAG$    : 'Y' if calendar found non-forecast days
0065 REM 
0070 SETESC 9000
0080 SETERR 9000
0090 ENTER FILE_DEV,FIRM$,CURRENT$,FUTURE$,V0,FLAG$
0200 REM " --- IOLists"
0210 FILE_IOL: IOLIST A0$,A1$,A0
0500 REM " --- Init Data"
0550 LET V0=INT(C0),FLAG$=""
0800 REM " --- Standardize Date Formate To YYMMDD"
0810 IF LEN(CURRENT$)=3 THEN LET CURRENT$=FNYY_YY21$(FNM$(FNC$(CURRENT$))),V0_LEN=3
0820 IF LEN(CURRENT$)>3 THEN LET CURRENT$=FNYY_YY21$(CURRENT$),V0_LEN=6
1000 REM " --- Check Validity of Dates"
1020 LET V$=CURRENT$,X$="",X0=0
1030 CALL "SYC.CA",V$,X$,X0
1040 IF V$="N" THEN GOTO 4100
3000 REM " --- Calculate Date
3020 LET V$=CURRENT$,V=V0
3040 IF V>0 THEN GOSUB 6000 ELSE GOSUB 5000
3060 LET FUTURE$=STR(F_MTH:"00")+STR(F_DAY:"00")+STR(F_YR:"00")
4000 REM " --- Done - Successful"
4010 IF V0_LEN=3 THEN LET FUTURE$=FND$(FUTURE$) ELSE LET FUTURE$=FNYY_YY21$(FNM$(FUTURE$))
4090 GOTO 9950
4100 REM " --- Done - Unsuccessful"
4190 GOTO 9900
5000 REM " --- Calculate Past Date"
5010 LET X$=V$
5030 LET YY$=X$(1,2),F_YR=FNYY_YEAR(X$(1,2)),F_MTH=NUM(X$(3,2)),F_DAY=NUM(X$(5,2))
5040 FIND (FILE_DEV,KEY=FIRM$+YY$+STR(F_MTH:"00"),DOM=5200)IOL=FILE_IOL
5042 IF A1$(F_DAY,1)="W" THEN GOTO 5050
5045 IF F_DAY>1 THEN LET F_DAY=F_DAY-1; GOTO 5042
5047 GOTO 5100
5050 IF V=0 THEN GOTO 5390
5055 FOR X=F_DAY TO 1 STEP -1
5060 IF A1$(X,1)="W" THEN LET V=V+1
5070 IF A1$(X,1)=" " THEN LET V=V+1,FLAG$="Y"
5080 IF V>0 THEN LET F_DAY=X; EXITTO 5390
5090 NEXT X
5100 LET X$=""
5110 CALL "SYC.CA",FNYEAR_YY21$(F_YR)+STR(F_MTH:"00")+"01",X$,-1
5120 LET YY$=X$(1,2),F_YR=FNYY_YEAR(X$(1,2)),F_MTH=NUM(X$(3,2)),F_DAY=NUM(X$(5,2))
5130 GOTO 5040
5190 RETURN 
5200 REM " --- Month Record Not Found"
5210 LET FLAG$="Y"
5220 GOSUB 6500
5240 FOR X=F_DAY TO 1 STEP -1
5250 LET V=V+1
5260 IF V>-1 THEN LET F_DAY=X; EXITTO 5390
5270 NEXT X
5280 LET X$=""
5290 CALL "SYC.CA",FNYEAR_YY21$(F_YR)+STR(F_MTH:"00")+"01",X$,-1
5300 LET YY$=X$(1,2),F_YR=FNYY_YEAR(X$(1,2)),F_MTH=NUM(X$(3,2)),F_DAY=NUM(X$(5,2))
5310 GOTO 5040
5390 RETURN 
6000 REM " --- Calculate Future Date"
6010 LET X$=""
6015 IF V=0 THEN LET X0=0 ELSE LET X0=1
6020 CALL "SYC.CA",V$,X$,X0
6030 LET YY$=X$(1,2),F_YR=FNYY_YEAR(X$(1,2)),F_MTH=NUM(X$(3,2)),F_DAY=NUM(X$(5,2))
6040 FIND (FILE_DEV,KEY=FIRM$+YY$+STR(F_MTH:"00"),DOM=6200)IOL=FILE_IOL
6050 FOR X=F_DAY TO A0
6055 IF A1$(X,1)<>"W" AND V=0 THEN LET V=1
6060 IF A1$(X,1)="W" THEN LET V=V-1
6070 IF A1$(X,1)=" " THEN LET V=V-1,FLAG$="Y"
6080 IF V<1 THEN LET F_DAY=X; EXITTO 6190
6090 NEXT X
6100 LET F_MTH=F_MTH+1,F_DAY=1
6110 IF F_MTH>12 THEN LET F_YR=F_YR+1,F_MTH=1
6120 GOTO 6040
6190 RETURN 
6200 REM " --- Month Record Not Found"
6210 LET FLAG$="Y"
6220 GOSUB 6500
6240 FOR X=F_DAY TO A0
6250 LET V=V-1
6260 IF V<1 THEN LET F_DAY=X; EXITTO 6390
6270 NEXT X
6280 LET F_MTH=F_MTH+1,F_DAY=1
6290 IF F_MTH>12 THEN LET F_YR=F_YR+1,F_MTH=1
6300 GOTO 6040
6390 RETURN 
6500 REM " --- Days In The Month (one less than first day of next month)"
6520 LET YEAR=F_YR,MONTH=F_MTH+1
6540 IF MONTH>12 THEN LET YEAR=YEAR+1,MONTH=1
6560 CALL "SYC.CA",FNYEAR_YY21$(YEAR)+STR(MONTH:"00")+"01",X$,-1
6580 LET A0=NUM(X$(5,2))
6590 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8080 DEF FNM$(Q$)=Q$(5,2)+Q$(1,4)
8110 REM " --- FNYEAR_YY21$ Convert Numeric Year to 21st Century 2-Char Year"
8115 DEF FNYEAR_YY21$(Q)=FNYY_YY21$(STR(MOD(Q,100):"00"))
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Exit - Unsuccessful"
9920 LET CURRENT$="N",FUTURE$="N",V0=0,FLAG$="Y"
9950 REM " --- Return to caller"
9980 EXIT 
9999 END

0010 REM "SYC - Delete Batched Files"
0020 REM "Program SYC.BB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "STATUS      : 0=No Error (Returned)"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER STATUS
0100 REM " --- Batched Process?"
0110 LET STATUS=0
0120 LET PROCESS$=STBL("!PROCESS",ERR=9950)
0130 LET BATCH=NUM(PROCESS$(13,3)),BATCH$="."+STR(BATCH:"000")
0140 IF BATCH=0 THEN GOTO 9950
0150 REM " --- Open Files"
0155 LET FILES=3
0160 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0170 LET FILES$[1]="SYM-39",FILES$[2]="SYM-49",FILES$[3]="SYM-59"
0180 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BTCH,STATUS
0185 IF STATUS>0 THEN GOTO 9900
0190 LET SYM39_DEV=CHANNELS[1],SYM49_DEV=CHANNELS[2],SYM59_DEV=CHANNELS[3]
0500 REM " --- Initialize Data"
0510 LET B_LIST$=""
1000 REM " --- Build List Of Batched Files"
1010 READ (SYM39_DEV,KEY=PROCESS$(1,10),DOM=1020)
1020 LET K$=KEY(SYM39_DEV,END=1090)
1030 IF POS(PROCESS$(1,10)=K$)<>1 THEN GOTO 1090
1040 READ (SYM39_DEV)
1050 LET X$=FNP$(K$(11))+BATCH$+FILL(10)
1060 LET B_LIST$=B_LIST$+X$(1,10)
1080 GOTO 1020
1090 IF LEN(B_LIST$)=0 THEN GOTO 4000
1200 REM " --- Close Batched Files"
1210 LET CHNLS$=CHN
1220 FOR X=1 TO LEN(CHNLS$) STEP 2
1230 LET CHNL=DEC(CHNLS$(X,2),ERR=1290),F$=FID(CHNL,ERR=1290)
1240 IF LEN(F$)<9 THEN GOTO 1290
1250 LET F$=FNP$(F$(9))
1260 IF POS("/"=F$)>0 THEN LET F$=F$(POS("/"=F$,-1)+1)
1280 IF POS(F$=B_LIST$,10)>0 THEN CLOSE (CHNL,ERR=1290)
1290 NEXT X
1400 REM " --- Verify Batched Files Not Being Used"
1410 LET B_FILES=LEN(B_LIST$)/10
1420 DIM B_FILES$[B_FILES],B_OPTS$[B_FILES],B_CHNLS[B_FILES]
1430 FOR X=1 TO B_FILES
1440 LET B_FILES$[X]=FNP$(B_LIST$(X*10-9,10)),B_OPTS$[X]="LF"
1450 NEXT X
1460 CALL "SYC.DA",1,1,B_FILES,B_FILES$[ALL],B_OPTS$[ALL],B_CHNLS[ALL],BTCH,STATUS
1480 IF STATUS>0 THEN GOTO 4200
2000 REM " --- Delete Batched Files"
2020 FOR X=1 TO B_FILES
2040 CLOSE (B_CHNLS[X],ERR=2050)
2060 ERASE B_FILES$[X],ERR=4400
2080 NEXT X
2200 REM " --- Remove Batch Records"
2220 REMOVE (SYM49_DEV,KEY=BATCH$(2))
2240 REMOVE (SYM59_DEV,KEY=PROCESS$(1,12)+BATCH$(2),ERR=2250)
4000 REM " --- Exit - Normal"
4090 GOTO 9900
4200 REM " --- Exit - Can't Delete Batch"
4210 FOR X=1 TO B_FILES
4220 CLOSE (B_CHNLS[X],ERR=4230)
4230 NEXT X
4240 LET NMBR=1
4250 DIM MSG$[NMBR]
4260 LET MSG$[0]="Can NOT delete batch "+BATCH$(2)+". Someone else is using it."
4270 LET MSG$[1]="            <Enter>=Continue"
4280 CALL "SYC.XA",2,MSG$[ALL],NMBR,-1,-1,V$,V3
4290 GOTO 9900
4400 REM " --- Exit - Error While Deleting Batch"
4410 FOR X=1 TO B_FILES
4420 CLOSE (B_CHNLS[X],ERR=4430)
4430 NEXT X
4440 LET NMBR=1
4450 DIM MSG$[NMBR]
4460 LET MSG$[0]="ERROR encounter while deleting batch "+BATCH$(2)+"."
4470 LET MSG$[1]="         <Enter>=Continue"
4480 CALL "SYC.XA",2,MSG$[ALL],NMBR,-1,-1,V$,V3
4490 GOTO 9800
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9800
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Return STATUS = ERR"
9810 LET STATUS=ERR
9820 IF STATUS=0 THEN LET STATUS=999
9900 REM " --- Return to calling program"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9921)
9930 NEXT X
9950 EXIT 
9999 END

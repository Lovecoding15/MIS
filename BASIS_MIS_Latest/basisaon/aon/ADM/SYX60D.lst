0010 REM "SYX - v6.0 Administrator Upgrade (_FILES Overlay)"
0020 REM "Program SYX60D"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9900
0200 REM " --- IOLIST's"
0210 SYM04A: IOLIST X0$(1),X1$(1),X2$(1),X3$(1),X[ALL]
0220 UFILES: IOLIST A0$(1),A1$(1)
0400 REM " --- Parameters"
0410 LET MODULES=1
0500 REM " --- Initialization"
0510 DIM MODULES$[2,4],A0$(6),A1$(32),X0$(6),X1$(64),X[2]
0540 LET UPGRADE$=""
0550 REM " --- Software Modules"
0560 LET MODULES$[1,1]="SY",MODULES$[1,2]="ADD+ON Administrator"
0600 REM " --- Build UPGRADE$ string"
0610 FOR X=1 TO MODULES
0620 LET UPGRADE$=UPGRADE$+MODULES$[X,1]
0630 NEXT X
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0720 CALL "SYC.WC",ERR=9800,0,0,65,4,8,4,0
0730 PRINT @(10,5),"  This phase of the upgrade will merge needed Administrator"
0740 PRINT @(10,6),"data records into the converted Version 6.0 files."
0790 GOSUB 6900
0900 REM " --- Check for merge files"
0910 LET UFILES_DEV=UNT
0920 OPEN (UFILES_DEV,ERR=9700)"_FILES"
1000 REM " --- Format screen"
1090 PRINT @(0,9),'CE',
1100 REM " --- Which modules?"
1110 FOR X=1 TO MODULES
1120 LET PROGRAM$=MODULES$[X,1]+"C.VN"
1130 CALL PROGRAM$,ERR=1170,VERSION$
1140 LET MODULES$[X,3]="Y",MODULES$[X,4]=VERSION$
1170 NEXT X
1190 GOSUB 6900
1300 REM " --- Look for files which might be overwritten"
1310 PRINT @(0,9),'CE',"Verifying Merge File",
1320 LET FOUND=0,ROW=12,COLUMN=0
1400 REM " --- Read next _FILES record"
1410 LET K$=KEY(UFILES_DEV,END=1800)
1420 READ (UFILES_DEV)IOL=UFILES
1430 IF POS(A1$(25,2)=UPGRADE$,2)=0 THEN GOTO 1400
1440 LET FILENAME$=FNP$(K$)
1450 PRINT @(22,9),FILENAME$,'CL',
1500 REM " --- Does this file exist?"
1510 LET SOURCE_DEV=UNT,TARGET_DEV=0
1520 OPEN (SOURCE_DEV,ERR=1700)"_"+FILENAME$
1530 LET TARGET_DEV=UNT
1540 OPEN (TARGET_DEV,ERR=1700)FILENAME$
1600 REM " --- It does!"
1610 IF FOUND=0 THEN PRINT @(0,11),'CE',"The following files will receive merge records if installation continues:"
1620 LET ROW=ROW+1,FOUND=FOUND+1
1630 PRINT @(COLUMN,ROW),FILENAME$,@(COLUMN+8),A1$(1,24),
1640 IF ROW<20 THEN GOTO 1690
1650 IF COLUMN<40 THEN LET ROW=12,COLUMN=40; GOTO 1690
1660 GOSUB 6900
1670 LET ROW=12,COLUMN=0
1680 PRINT @(COLUMN,ROW+1),'CE',
1700 REM " --- Loop back for next _FILES record"
1710 CLOSE (SOURCE_DEV,ERR=1720)
1720 CLOSE (TARGET_DEV,ERR=1730)
1790 GOTO 1400
1800 REM " --- Done"
1810 IF FOUND=0 THEN GOTO 2000
1820 GOSUB 6800
1830 IF V$<>"YES" THEN GOTO 9900
2000 REM " --- Install Merge Records"
2010 PRINT @(0,9),'CE',"Installing Merge Records",
2050 LET ROW=11,COLUMN=0
2090 READ (UFILES_DEV,KEY="",DOM=2100)
2400 REM " --- Read next _FILES record"
2405 LET K$=KEY(UFILES_DEV,END=3000)
2410 READ (UFILES_DEV)IOL=UFILES
2415 LET FILENAME$=FNP$(K$)
2420 FOR X=1 TO MODULES
2430 IF A1$(25,2)<>MODULES$[X,1] THEN GOTO 2440
2435 IF MODULES$[X,3]="Y" THEN EXITTO 2450
2440 NEXT X
2445 GOTO 2400
2450 PRINT @(COLUMN,ROW),FILENAME$,"  ",A1$(1,24),
2460 LET X=COLUMN+34,Y=ROW,NUMBER=0,ROW=ROW+1
2470 IF ROW<19 THEN GOTO 2500
2490 LET ROW=6,COLUMN=40
2500 REM " --- Does this file exist?"
2510 LET SOURCE_DEV=UNT,TARGET_DEV=0
2520 OPEN (SOURCE_DEV,ERR=2900)"_"+FILENAME$
2530 LET TARGET_DEV=UNT
2540 OPEN (TARGET_DEV,ERR=2700)FILENAME$
2550 GOTO 2800
2700 REM " --- Missing target file. Create it using source file parameters."
2710 CALL "SYC.JA",SOURCE_DEV,X$,PATHNAME$,FILETYPE$,KEYLEN,RECORDS,RECLEN,NUMREC,STATUS
2720 IF FILETYPE$="K" THEN LET FILETYPE$="D"
2730 IF FILETYPE$="D" THEN IF RECLEN=0 THEN LET FILETYPE$="S"
2750 CALL "SYC.MA",FILETYPE$,FILENAME$,A1$(25,2),STR(KEYLEN),RECORDS,RECLEN,STATUS
2780 CLOSE (SOURCE_DEV,ERR=2790)
2790 GOTO 2500
2800 REM " --- Merge records from _xxx-xx into xxx-xx"
2810 LET K$=KEY(SOURCE_DEV,END=2900)
2820 READ RECORD(SOURCE_DEV,KEY=K$)R$
2830 LET NUMBER=NUMBER+1
2840 IF MOD(NUMBER,10)=0 THEN PRINT @(X,Y),NUMBER:M1$
2880 WRITE RECORD(TARGET_DEV,KEY=K$)R$
2890 GOTO 2800
2900 REM " --- Loop back for next _FILES record"
2910 PRINT @(X,Y),NUMBER:M1$
2920 CLOSE (SOURCE_DEV,ERR=2930)
2930 CLOSE (TARGET_DEV,ERR=2940)
2990 GOTO 2400
3000 REM " --- Update version number for installed modules"
3010 PRINT @(0,9),'CE',"Updating System Version Number"
3020 CALL "SYC.VN",ERR=9600,VERSION$
3030 LET ROW=11,COLUMN=0
3040 DIM FILES$[1],OPTIONS$[1],CHANNELS[1]
3050 LET FILES$[1]="SYM-04",OPTIONS$[1]="F"
3060 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
3070 IF STATUS OR CHANNELS[1]=0 THEN GOTO 9750
3080 LET SYM04_DEV=CHANNELS[1]
3100 REM " --- Update each installed module"
3110 FOR X=1 TO MODULES
3120 IF MODULES$[X,3]<>"Y" THEN GOTO 3290
3130 PRINT @(COLUMN,ROW),MODULES$[X,1],"  ",MODULES$[X,2],"  ",VERSION$
3140 LET ROW=ROW+1
3150 IF ROW>20 THEN LET ROW=11,COLUMN=40
3200 REM " --- Update v6.0 SYM-04 System Description version number"
3210 DIM X0$(2),X1$(65),X2$(50),X3$(50),X[2]
3220 LET X0$(1)=MODULES$[X,1]
3230 LET X1$(1)=MODULES$[X,2],X1$(36,1)="Y",X1$(37,2)="99"
3240 FIND (SYM04_DEV,KEY=X0$,DOM=3250)IOL=SYM04A
3250 LET X1$(39,1)="Y",X1$(40,6)=VERSION$,X1$(46,1)="N"
3270 WRITE (SYM04_DEV,KEY=X0$)IOL=SYM04A
3290 NEXT X
3300 REM " --- Remove merge files"
3310 GOSUB 6900
3320 PRINT @(0,9),'CE',"Removing Merge Files",
3330 LET ROW=11,COLUMN=0
3390 READ (UFILES_DEV,KEY="",DOM=3400)
3400 REM " --- Read next _FILES record"
3410 LET K$=KEY(UFILES_DEV,END=3500)
3420 READ (UFILES_DEV)IOL=UFILES
3430 IF POS(A1$(25,2)=UPGRADE$)=0 THEN GOTO 3400
3440 LET X$="_",FILENAME$=FNP$(K$)
3450 GOSUB 6000
3490 GOTO 3400
3800 REM " --- Erase version programs"
3810 FOR X=1 TO MODULES
3820 IF MODULES$[X,3]<>"Y" THEN GOTO 3890
3830 LET PROGRAM$=MODULES$[X,1]+"C.VN"
3840 ERASE PROGRAM$,ERR=3890
3850 GOTO 3840
3890 NEXT X
3900 REM " --- Erase Administrator upgrade trigger"
3910 ERASE "SYX.VN",ERR=4000
3920 GOTO 3900
4000 REM " --- Installation completed"
4010 GOSUB 6900
4020 PRINT @(0,9),'CE',@(7),"The Version 6.0 Administrator upgrade is now complete. Follow"
4030 PRINT @(7,10),"the upgrade procedures in the 6.0 Release Notes to upgrade the"
4040 PRINT @(7,11),"remaining installed modules to Version 6.0."
4050 GOSUB 6900
4100 REM " --- Re-Start ADD+ON System"
4110 RUN "SYS.ZA"
6000 REM " --- Erase _FILE"
6010 LET SOURCE_DEV=UNT
6020 OPEN (SOURCE_DEV,ERR=6050)X$+FILENAME$
6030 CLOSE (SOURCE_DEV,ERR=6040)
6040 GOTO 6070
6050 OPEN (SOURCE_DEV,ERR=6190)FILENAME$
6060 CLOSE (SOURCE_DEV,ERR=6070)
6070 PRINT @(COLUMN,ROW),FILENAME$,@(COLUMN+8),A1$(1,24)
6080 ERASE X$+FILENAME$,ERR=6100
6090 GOTO 6080
6100 LET ROW=ROW+1
6110 IF ROW<21 THEN GOTO 6190
6120 IF COLUMN<40 THEN LET ROW=11,COLUMN=40; GOTO 6190
6130 GOSUB 6900
6140 LET ROW=11,COLUMN=0
6150 PRINT @(COLUMN,ROW),'CE',
6190 RETURN 
6800 REM " --- Verify operator's intent to overwrite existing files"
6810 PRINT @(0,22),'CL',"Are you SURE that you wish to proceed (Enter YES to continue/<F4> to exit)?",
6820 LET V0$="S",V1$="CEF",V2$="NO",V3$="YESNO ",V4$="",V0=3,V1=76,V2=22
6830 GOSUB 7000
6840 IF V3<>0 THEN LET V$="NO"
6890 RETURN 
6900 REM " --- Proceed or quit?"
6910 PRINT @(0,22),'CL',@(14,22),"Press <Enter> to continue or <F4> to exit:",'CI',
6920 LET V0$="S",V1$="CF",V2$="",V3$="",V4$="",V0=1,V1=57,V2=22
6930 GOSUB 7000
6940 ON V3 GOTO 6950,6900,6900,6900,9800
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9800
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9600 REM " --- Missing Administrator Module"
9610 PRINT @(0,3),'CE',@(1,4),"ERROR: The ADD+ON Administrator Module (Version 6.0.0 or greater) is required"
9620 PRINT @(1,5),"       to run these applications."
9690 GOTO 9800
9700 REM " --- Missing distribution file(s)"
9710 PRINT @(0,3),'CE',@(1,4),"ERROR: Unable to locate or open installation data file _FILES"
9740 GOTO 9800
9750 REM " --- Missing SYM-04"
9760 PRINT @(0,3),'CE',@(1,4),"ERROR: Unable to locate or open ADD+ON System Master File (SYM-04)"
9800 REM " --- Distribution installation and/or rename problem"
9810 PRINT @(0,10),'CE',
9820 CALL "SYC.WC",ERR=9830,0,0,80,9,0,10,0
9830 PRINT @(2,11),"If you are upgrading to ADD+ON Software Version 6.0:"
9840 PRINT @(2,13),"Restore your original version 5.2 Administrator data files, reload the"
9845 PRINT @(2,14),"version 6.0 Administrator media and try the installation procedure again."
9850 PRINT @(2,16),"If problems persist:"
9860 PRINT @(2,17),"Call ADD+ON Software Support at (800) 275-6350"
9870 LET V4$="Press <Enter> to continue:"
9880 LET V0$="S",V1$="CF",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
9890 GOSUB 7000
9900 REM " --- Abort installation"
9910 PRINT @(1,20),'CE','SF',"ADD+ON Software Installation Terminated."
9950 RELEASE 
9999 END

0010 REM "SYC - Executive Summary (Main Display)"
0020 REM "Program SYC.QE"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0060 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0070 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open Files"
0105 LET FILES=11,WINDOWS=4
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0115 DIM TITLES$[WINDOWS],NAMES$[WINDOWS],ROWS[WINDOWS],HEIGHT[WINDOWS]
0120 LET FILES$[1]="SYE-01",FILES$[2]="SYE-02",FILES$[3]="SYE-03"
0125 LET FILES$[4]="SYE-04",FILES$[5]="SYE-05",FILES$[6]="SYE-06"
0130 LET FILES$[7]="SYE-07",FILES$[8]="SYE-08",FILES$[9]="SYE-09"
0135 LET FILES$[10]="SYE-10",FILES$[11]="SYM-06"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS THEN GOTO 9900
0160 LET SYE01_DEV=CHANNELS[1],SYE02_DEV=CHANNELS[2],SYE03_DEV=CHANNELS[3]
0170 LET SYE04_DEV=CHANNELS[4],SYE05_DEV=CHANNELS[5],SYE06_DEV=CHANNELS[6]
0180 LET SYE07_DEV=CHANNELS[7],SYE08_DEV=CHANNELS[8],SYE09_DEV=CHANNELS[9]
0190 LET SYE10_DEV=CHANNELS[10],SYM06_DEV=CHANNELS[11]
0200 REM " --- IOLIST's"
0240 SYM06A: IOLIST X$,COMPANY$
0270 SYS01T: IOLIST X$,F0$
0310 SYE01A: IOLIST X$,P1$(1)
0320 SYE02A: IOLIST A0$(1),A1$(1),A[ALL],U[ALL]
0390 SYE09A: IOLIST A0$(1),A1$(1),A[ALL]
0395 SYE10A: IOLIST A0$(1),A1$(1),A[ALL]
0400 REM " --- Parameters"
0405 DIM SPEED$(128),INFO$[20],P1$(32)
0410 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=SPEED$(1,2)
0415 LET ONAME$=FNP$(SPEED$(27,8)),OWIDTH=NUM(SPEED$(83,2))
0420 LET OHEIGHT=NUM(SPEED$(85,2)),SYS01_DEV=NUM(STBL("!SYS01"))
0425 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0435 FIND (SYM06_DEV,KEY=N0$,DOM=9900)IOL=SYM06A
0440 FIND (SYE01_DEV,KEY=N0$,DOM=4900)IOL=SYE01A
0450 LET MAXOPT=NUM(SPEED$(57,2)),MONTH$=F0$(20,2)
0460 LET YEAR$=STR(1900+FNYY_YEAR(F0$(18,2)):"0000")
0500 REM " --- Initializations"
0505 DIM A0$(8),A1$(32),A[31],U[31],L[31]
0515 DIM OPTLIST$[2,MAXOPT,6],PAST$[5],FUTURE$[5],WHEN$[5]
0540 LET D_STATUS=0,O=13,WIDTH=80,COLUMN=0,ROWS[0]=0,TYPE=0,DONE=0
0545 LET HEIGHT[0]=3,HEIGHT[1]=5,HEIGHT[2]=5,HEIGHT[3]=5,HEIGHT[4]=4
0560 LET TITLES$[1]="AR & AP Analysis",TITLES$[2]="Order Analysis"
0565 LET TITLES$[3]="Sales Analysis",TITLES$[4]="Inventory Analysis"
0570 LET M1$="##,###,##0-",M2$="###,###,##0-",M4$="##,###,###,###,##0-"
0575 LET M1=LEN(M1$),M2=LEN(M2$),M4=LEN(M4$),P$="Prior ",F$="Future "
0580 LET M3$="-##,##0.00%",M3=LEN(M3$),D=NUM(P1$(7,2))
0590 LET PGM$=PGM(-2),OPTIONS=0,FIELDS=4
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTIONS,OPTLIST$[ALL],X$,OMAXROW,Q$,ONAME$
0700 REM " --- Background"
0710 DIM HEADING$(WIDTH-2)
0720 FOR X=1 TO WINDOWS
0730 IF X>0 THEN LET ROWS[X]=ROWS[X-1]+HEIGHT[X-1]
0740 CALL "SYC.WA",0,WIDTH,HEIGHT[X],COLUMN,ROWS[X],TITLES$[X],NAME$
0750 LET NAMES$[X]=NAME$
0760 NEXT X
1000 REM " --- New company"
1010 DIM HEADING$(WIDTH-2)
1020 LET X$=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2),COMPANY$=""
1030 FIND (SYM06_DEV,KEY=N0$,DOM=1020)IOL=SYM06A
1040 FIND (SYE01_DEV,KEY=N0$,DOM=9900)IOL=SYE01A
1050 LET ASOF$=P1$(1,6),X$=P1$(5,2)+"/"+P1$(7,2)+"/"+P1$(3,2)
1060 LET TITLES$[0]="Executive Summary - Last Generated "+X$
1070 IF NAMES$[0]<>"" THEN CALL "SYC.WD",NAMES$[0]
1080 CALL "SYC.WA",0,WIDTH,HEIGHT[0],COLUMN,ROWS[0],TITLES$[0],NAME$
1090 LET NAMES$[0]=NAME$,D=1,D=NUM(P1$(7,2),ERR=1200)
1200 REM " --- Display top window information"
1210 LET M=NUM(MONTH$),Y=NUM(YEAR$),MF=M,MP=M,YF=Y,YP=Y
1220 LET LABEL$=DATE(JUL(Y,M,1):"%Ms")+" "+YEAR$(3,2)
1230 LET HEADING$(1)=" Company: "+COMPANY$,HEADING$(WIDTH-9)=" "+LABEL$
1240 CALL "SYC.WB",NAMES$[0],0
1250 PRINT @(0,0),'SB','BR',HEADING$,'ER','SF',
1300 REM " --- Type of display"
1310 LET DISPLAY$="Amounts"
1320 IF TYPE=1 THEN LET DISPLAY$="Units"
1330 IF TYPE=2 THEN LET DISPLAY$="Averages"
1400 REM " --- Fill PAST/FUTURE date arrays"
1410 FOR X=1 TO FIELDS
1420 LET PAST$[X]=STR(YP:"0000")+STR(MP:"00")
1430 LET FUTURE$[X]=STR(YF:"0000")+STR(MF:"00")
1440 LET MF=MF+1,MP=MP-1
1450 IF MF>12 THEN LET YF=YF+1,MF=1
1460 IF MP<1 THEN LET YP=YP-1,MP=12
1490 NEXT X
1600 REM " --- AR/AP Analysis"
1610 DIM ARLIST$[1,2,2],LINES$[2]
1620 LET LABEL$="A/R",CHANNEL=SYE02_DEV,WINDOW=1,LINES=2
1630 GOSUB 5000
1640 GOSUB 6000
1650 LET ARLIST$[1,1,1]=G$,ARLIST$[1,1,2]="AR"+A0$,LINES$[1]=G$
1660 LET LABEL$="A/P",CHANNEL=SYE03_DEV
1670 GOSUB 6000
1680 LET ARLIST$[1,2,1]=G$,ARLIST$[1,2,2]="AP"+A0$,LINES$[2]=G$
1690 GOSUB 5200
1800 REM " --- Order Analysis"
1810 DIM OPLIST$[1,2,2]
1820 LET LABEL$="Orders",CHANNEL=SYE04_DEV,WINDOW=2
1830 GOSUB 5100
1840 GOSUB 6000
1850 LET OPLIST$[1,1,1]=G$,OPLIST$[1,1,2]="OP"+A0$,LINES$[1]=G$
1860 LET LABEL$="Purchases",CHANNEL=SYE06_DEV
1870 GOSUB 6000
1880 LET OPLIST$[1,2,1]=G$,OPLIST$[1,2,2]="PO"+A0$,LINES$[2]=G$
1890 GOSUB 5200
2000 REM " --- Sales Analysis"
2010 DIM SALIST$[1,2,2],R[FIELDS]
2020 LET LABEL$="Sales",CHANNEL=SYE07_DEV,WINDOW=3
2030 GOSUB 5000
2040 GOSUB 6000
2050 LET SALIST$[1,1,1]=G$,SALIST$[1,1,2]="SA"+A0$,LINES$[1]=G$
2060 IF TYPE<>0 THEN GOTO 2100
2070 FOR X=0 TO FIELDS
2080 LET R[X]=S[X]
2090 NEXT X
2100 REM " --- Retrieve cost record and calculate gross profit"
2110 LET LABEL$="Profit",CHANNEL=SYE08_DEV,SALES=TOTAL
2120 GOSUB 6000
2130 LET G$(O)=""
2140 FOR X=0 TO FIELDS
2150 LET PERCENT=0,PROFIT=R[X]-S[X]
2160 IF R[X]>0 THEN LET PERCENT=PROFIT*100/R[X]
2170 LET G$(O+M3*X)=STR(PERCENT:M3$)
2180 NEXT X
2190 LET PERCENT=0,PROFIT=SALES-TOTAL
2200 IF SALES>0 THEN LET PERCENT=(PROFIT*100)/SALES
2210 LET G$(O+M3*(FIELDS+1))=STR(PERCENT:M3$)
2220 LET SALIST$[1,2,1]=G$,SALIST$[1,2,2]="SP"+A0$,LINES$[2]=G$
2290 GOSUB 5200
2400 REM " --- Inventory window"
2410 DIM IVLIST$[1,2,2],A0$(8),A1$(32),A[31],G$(WIDTH-2)
2420 LET HEADING$(1)=" "+DISPLAY$,HEADING$(O+M2-8)="On Hand",LINES=1
2430 LET HEADING$(O+M2*2-10)="Committed",HEADING$(O+M2*3-10)="Available"
2440 LET HEADING$(O+M2*3+M4-16)="Work In Process"
2450 LET G$(2)="Inventory",A0$(1)=N0$+YEAR$+MONTH$
2460 LET ONHAND=0,COMMITTED=0,WIP=0
2480 IF TYPE<>0 THEN GOTO 2890
2490 IF A0$(3)<ASOF$ THEN GOTO 2890
2500 REM " --- Retrieve inventory amounts"
2510 READ (SYE09_DEV,KEY=A0$,DOM=2530)IOL=SYE09A
2520 LET COMMITTED=A[0],ONHAND=A[D]
2530 IF A0$(3)=ASOF$ THEN GOTO 2600
2540 LET K$=KEYP(SYE09_DEV,END=2600)
2550 IF POS(N0$=K$)<>1 THEN GOTO 2600
2560 READ (SYE09_DEV,KEY=K$,DOM=2600)IOL=SYE09A
2570 LET COMMITTED=A[0],ONHAND=A[31]
2600 REM " --- Retrieve work in process amount"
2610 DIM A[1]
2620 LET WIP=0,A0$(1)=N0$+YEAR$+MONTH$
2630 READ (SYE10_DEV,KEY=A0$,DOM=2650)IOL=SYE10A
2640 LET WIP=A[0]
2650 IF A0$(3)=ASOF$ THEN GOTO 2800
2700 REM " --- Retrieve last available work in process"
2710 LET K$=KEYP(SYE10_DEV,END=2800)
2720 IF POS(N0$=K$)<>1 THEN GOTO 2800
2730 FIND (SYE10_DEV,KEY=K$,DOM=2800)IOL=SYE10A
2740 LET WIP=A[0]
2800 REM " --- Store inventory information"
2810 LET G$(O)=STR(ONHAND:M2$),G$(O+M2)=STR(COMMITTED:M2$)
2820 LET G$(O+M2*2)=STR(ONHAND-COMMITTED:M2$),G$(O+M2*3)=STR(WIP:M4$)
2890 LET IVLIST$[1,1,1]=G$,IVLIST$[1,1,2]="IV"+A0$,LINES$[1]=G$
2900 REM " --- Display inventory data"
2910 LET WINDOW=4
2920 GOSUB 5200
3000 REM " --- Process windows"
3010 LET MORE=1,WINDOW=1
3020 WHILE MORE
3030 LET MAXROW=HEIGHT[WINDOW]-3,PAGE=1
3040 DIM LLIST$[2,MAXROW,6],MORE$[2,MAXROW,1]
3100 REM " --- Load LLIST$ with information for current window"
3110 FOR X=1 TO MAXROW
3120 LET LINE$="",ANSWER$=""
3130 IF WINDOW=1 THEN LET LINE$=ARLIST$[1,X,1],ANSWER$=ARLIST$[1,X,2]
3140 IF WINDOW=2 THEN LET LINE$=OPLIST$[1,X,1],ANSWER$=OPLIST$[1,X,2]
3150 IF WINDOW=3 THEN LET LINE$=SALIST$[1,X,1],ANSWER$=SALIST$[1,X,2]
3160 IF WINDOW=4 THEN LET LINE$=IVLIST$[1,X,1],ANSWER$=IVLIST$[1,X,2]
3170 LET LLIST$[1,X,1]=LINE$,LLIST$[1,X,2]=ANSWER$
3190 NEXT X
3400 REM " --- Perform lightbar selection"
3410 GOSUB 5400
3420 LET SELECTION$=ANSWER$
3430 IF DONE THEN LET MORE=0
3440 IF ANSWER$<>"" THEN LET MORE=0
3460 LET WINDOW=WINDOW+1
3470 IF WINDOW>WINDOWS THEN LET WINDOW=1
3490 WEND
3500 REM " --- Done?"
3510 IF DONE THEN GOTO 4100
4000 REM " --- Process option window"
4010 LET CONTROL=0,FIRM$=N0$,UNITS=TYPE,PERIOD$=YEAR$+MONTH$
4020 GOSUB 5500
4030 IF DONE THEN GOTO 4100
4040 IF CONTROL=3 THEN GOTO 1200
4050 IF CONTROL=2 THEN GOTO 1200
4060 IF CONTROL=1 THEN GOTO 1000
4090 GOTO 1600
4100 REM " --- All done"
4190 GOTO 9900
4900 REM " --- Never generated"
4910 DIM MESSAGE$[1]
4920 LET MESSAGE$[0]="The Generate Executive Summary Task Must Be Run In Order To Provide"
4930 LET MESSAGE$[1]="Information For This Display (<Enter>=Continue):",D_STATUS=0
4940 CALL "SYC.XA",3,MESSAGE$[ALL],1,-1,-1,V$,V3
4990 GOTO 9900
5000 REM " --- Move PAST$ into WHEN$"
5010 DIM WHEN$[FIELDS]
5020 LET N1=FIELDS-1,N2=1,DIRECTION=1,H1$=F$,H2$=P$
5030 FOR X=N2 TO N1
5040 LET WHEN$[X]=PAST$[X]
5050 NEXT X
5060 GOSUB 6800
5090 RETURN 
5100 REM " --- Move FUTURE$ into WHEN$"
5110 DIM WHEN$[FIELDS]
5120 LET N1=1,N2=FIELDS-1,DIRECTION=-1,H1$=P$,H2$=F$
5130 FOR X=N1 TO N2
5140 LET WHEN$[X]=FUTURE$[X]
5150 NEXT X
5160 GOSUB 6800
5190 RETURN 
5200 REM " --- Switch windows and display heading"
5210 CALL "SYC.WB",NAMES$[WINDOW],0
5220 PRINT 'CS','SB',HEADING$,'SF',
5230 FOR X=1 TO LINES
5240 PRINT @(0,X),LINES$[X],
5250 NEXT X
5290 RETURN 
5400 REM " --- Middle window processing"
5410 DIM MORE$[1,2,5]
5420 LET XMODE=2,ANSWER$="",FIRM$=N0$,UNITS=TYPE,PERIOD$=YEAR$+MONTH$
5440 CALL "SYC.WB",NAMES$[WINDOW],0
5450 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],ANSWER$,PAGE,MAXROW,NAME$,MAXROW+3,WIDTH,FKEY
5460 IF FKEY=-1 OR FKEY=-2 THEN LET OPTION$[1]="PRIOR"
5470 IF FKEY=4 OR ANSWER$="END" THEN LET OPTION$[1]="EXIT"
5480 IF FKEY=5 THEN LET OPTION$[1]="STARTOVER"
5490 GOTO 5900
5500 REM " --- Option window processing"
5510 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
5520 LET PAGE=1
5530 CALL "SYC.WB",ONAME$,0
5540 PRINT 'CS',
5600 REM " --- Call lightbar routine"
5610 CALL "SYC.SA",0,OPTLIST$[ALL],MORE$[ALL],ANSWER$,PAGE,OMAXROW,ONAME$,OHEIGHT,OWIDTH,FKEY
5620 IF FKEY=-16 THEN GOTO 5500
5625 IF FKEY=-1 OR FKEY=-2 OR ANSWER$="PRIOR" THEN LET OPTION$[1]="PRIOR"; GOTO 5900
5630 IF FKEY=4 OR ANSWER$="END" THEN LET OPTION$[1]="EXIT"; GOTO 5900
5640 IF FKEY=5 THEN LET OPTION$[1]="STARTOVER"; GOTO 5900
5645 FOR X=1 TO OPTIONS
5650 IF OPTLIST$[1,X,2]<>ANSWER$ THEN GOTO 5665
5655 LET OPTION$[2]=FNU$(OPTLIST$[1,X,3]),SELECTION=X,TYPE$=OPTLIST$[1,X,4]
5660 LET SECURITY$=OPTLIST$[1,X,5],PASSWORD$=OPTLIST$[1,X,6],PARAM$=OPTION$[2]
5665 NEXT X
5675 IF SECURITY$="" AND PASSWORD$="" THEN GOTO 5690
5680 CALL "SYC.QX","M",SECURITY$,SELECTION,OPTLIST$[ALL],STATUS
5685 IF STATUS>0 THEN GOTO 5600
5690 LET SUBROUTINE$=FNP$(ANSWER$),OPTION$[1]=SUBROUTINE$,OPTION$[3]=""
5700 REM " --- Search (Change display, month or firm)"
5710 IF TYPE$="D" THEN GOTO 5800
5720 IF PARAM$="MONTH" THEN LET OPTION$[3]=PERIOD$
5730 IF PARAM$="TYPE" THEN LET OPTION$[3]=STR(UNITS)
5740 CALL SUBROUTINE$,ERR=5600,D_STATUS,OPTION$[ALL]
5750 IF OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" THEN GOTO 5900
5760 IF OPTION$[1]="PRIOR" THEN LET OPTION$[1]=""; GOTO 5900
5770 IF PARAM$="MONTH" THEN LET PERIOD$=OPTION$[3]
5780 IF PARAM$="TYPE" THEN LET UNITS=NUM(OPTION$[3])
5790 GOTO 5900
5800 REM " --- Do now (calendar)"
5810 LET OPTION$[3]=SELECTION$(1,2)+PERIOD$,OPTION$[4]=NAMES$[0]
5815 LET OPTION$[5]=STR(UNITS),TNAME$=NAMES$[0]
5820 CALL SUBROUTINE$,ERR=5600,D_STATUS,OPTION$[ALL],CHANNELS[ALL],NAME$
5830 IF OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" THEN GOTO 5900
5840 IF OPTION$[1]="PRIOR" THEN LET OPTION$[1]=""; GOTO 5900
5850 LET UNITS=NUM(OPTION$[5]),PERIOD$=OPTION$[3](3)
5900 REM " --- Option processing completed"
5910 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5920 IF UNITS<>TYPE THEN LET TYPE=UNITS,CONTROL=3
5930 IF PERIOD$=YEAR$+MONTH$ THEN GOTO 5950
5940 LET YEAR$=PERIOD$(1,4),MONTH$=PERIOD$(5,2),CONTROL=2
5950 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=5990),FIRM$=SPEED$(1,2)
5960 IF N0$<>FIRM$ THEN LET N0$=FIRM$,CONTROL=1
5990 RETURN 
6000 REM " --- Load and store AR, AP, Orders and Purchases"
6010 DIM A0$(8),A1$(32),A[31],U[31],L[31],G$(WIDTH-2)
6020 DIM S[FIELDS],AT[FIELDS],UT[FIELDS]
6030 LET A0$(1)=N0$,G$(2)=LABEL$
6090 READ (CHANNEL,KEY=A0$,DOM=6100)
6100 REM " --- Loop through records for firm and system"
6110 LET K$=KEY(CHANNEL,END=6400)
6120 IF POS(N0$=K$)<>1 THEN GOTO 6400
6130 READ (CHANNEL,KEY=K$)IOL=SYE02A
6200 REM " --- Accumulate total for month"
6210 LET T=0,ATOTAL=0,UTOTAL=0
6220 FOR X=1 TO 31
6230 IF TYPE=0 THEN LET T=T+A[X]
6240 IF TYPE=1 THEN LET T=T+U[X]
6250 IF TYPE=2 THEN LET ATOTAL=ATOTAL+A[X],UTOTAL=UTOTAL+U[X]
6260 NEXT X
6300 REM " --- Which month?"
6310 LET W=0
6320 IF A0$(3)<WHEN$[N1] THEN LET W=N1+DIRECTION
6330 IF A0$(3)>WHEN$[N2] THEN LET W=N2-DIRECTION
6340 FOR X=1 TO 4
6350 IF A0$(3)=WHEN$[X] THEN LET W=X
6360 NEXT X
6370 LET S[W]=S[W]+T
6380 IF TYPE=2 THEN LET AT[W]=AT[W]+ATOTAL,UT[W]=UT[W]+UTOTAL
6390 GOTO 6100
6400 REM " --- Fill display buffer"
6410 LET TOTAL=0,ATOTAL=0,UTOTAL=0
6420 FOR X=0 TO FIELDS
6430 LET N=S[X]
6435 IF TYPE<>2 THEN GOTO 6455
6440 LET N=0,ATOTAL=ATOTAL+AT[X],UTOTAL=UTOTAL+UT[X]
6445 IF UT[X]<>0 THEN LET N=INT(AT[X]/UT[X])
6455 LET G$(O+M1*X)=STR(N:M1$)
6460 IF TYPE<>2 THEN LET TOTAL=TOTAL+N
6465 NEXT X
6470 IF TYPE=2 THEN IF UTOTAL<>0 THEN LET TOTAL=INT(ATOTAL/UTOTAL)
6480 LET G$(O+M1*(FIELDS+1))=STR(TOTAL:M1$),A0$(3)=YEAR$+MONTH$
6490 RETURN 
6800 REM " --- Build heading"
6810 LET HEADING$(1)=" "+DISPLAY$,HEADING$(O+M1-LEN(H1$))=H1$
6820 FOR X=2 TO FIELDS
6830 LET M=NUM(WHEN$[X-1](5,2)),Y=NUM(WHEN$[X-1](1,4)),W=JUL(Y,M,1)
6840 LET M$=DATE(W:"%Ml"),HEADING$(O+M1*X-(LEN(M$)+1))=M$
6850 NEXT X
6870 LET HEADING$(O+M1*(FIELDS+1)-LEN(H2$))=H2$
6880 LET HEADING$(O+M1*(FIELDS+2)-6)="Total"
6890 RETURN 
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,6)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9915 CLOSE (CHANNELS[X],ERR=9916)
9920 NEXT X
9930 FOR X=0 TO WINDOWS
9935 IF NAMES$[X]<>"" THEN CALL "SYC.WD",NAMES$[X]
9940 NEXT X
9950 EXIT 
9999 END

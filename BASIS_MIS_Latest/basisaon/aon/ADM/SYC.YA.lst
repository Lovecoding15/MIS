0010 REM "SYC - Parameter Error Handler"
0020 REM "Program SYC.YA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "PGM$ : Line of code which caused error (Input)"
0052 REM "APP$ : Application code (Input)"
0054 REM "ERROR: Error code encountered (Input)"
0060 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER PGM$,APP$,ERROR
1000 REM " --- Error 11"
1010 IF ERROR<>11 THEN GOTO 9900
1100 REM " --- Parse Out Line Of Code"
1110 LET X0=POS(" ("=PGM$)
1120 IF X0=0 THEN GOTO 9900
1130 LET LINE$=PGM$(X0+2)
1140 LET X0=POS(","=LINE$)
1150 IF X0=0 THEN GOTO 9900
1160 LET FILE$=LINE$(1,X0-1)
1170 LET X1=POS("KEY="=LINE$)
1180 IF X1=0 THEN GOTO 9900
1190 LET LINE$=LINE$(X1+4)
1200 LET X0=POS(","=LINE$)
1210 IF X0=0 THEN LET X0=POS(")"=LINE$)
1220 IF X0=0 THEN GOTO 9900
1230 LET LINE$=LINE$(1,X0-1)
1300 REM " --- Check For SYS-01"
1310 IF FILE$<>"6" AND FILE$<>"SYS01_DEV" AND FILE$<>"SYS01" THEN GOTO 9900
1320 IF FILE$="SYS01_DEV" OR FILE$="SYS01" THEN GOTO 1400
1330 CALL "SYC.JA",6,FILE$,Y0$,Y1$,Y0,Y1,Y2,Y3,STAT
1340 IF STAT<>0 THEN GOTO 9900
1350 IF FNP$(FILE$)<>"SYS-01" THEN GOTO 9900
1400 REM " --- Get Module Name"
1410 IF LINE$="N0$" THEN GOTO 2100
1420 LET X0=POS("N2$"=LINE$)
1430 IF X0>0 THEN LET MOD$=APP$
1440 LET X1=POS("+"=LINE$)
1450 IF X1=0 AND X0=0 THEN LET MOD$=LINE$(4,2); GOTO 1500
1460 IF X0=0 THEN LET MOD$=LINE$(X1+2,2)
1500 REM " --- Get Record Type"
1510 LET X2=POS("+"=LINE$,1,2)
1520 IF X0>0 AND X2=0 THEN GOTO 9900
1530 IF X0>0 THEN LET TYPE$=LINE$(X2+2,2); GOTO 1600
1540 IF X2=0 THEN LET TYPE$=LINE$(X1+4,2); GOTO 1600
1550 LET TYPE$=LINE$(X2+2,2)
1700 REM " --- Check For Terminal Control Record Missing"
1710 IF LINE$(1,3)<>$22$+"T"+$22$ THEN GOTO 1800
1720 LET MESSAGE$="Terminal Control Record Missing for "+FID(0)
1730 GOSUB 6000
1790 GOTO 9900
1800 REM " --- Check For Report Forms Parameter Missing"
1810 IF POS(MOD$="APARPR",2)=0 THEN GOTO 1900
1820 IF TYPE$<>"02" THEN GOTO 1900
1830 LET MESSAGE$="Report Form Parameter Record Missing For Module "+MOD$
1840 GOSUB 6000
1890 GOTO 9900
1900 REM " --- Check For System Parameter Missing"
1910 IF TYPE$<>"00" THEN GOTO 2000
1920 LET MESSAGE$="System Parameter Record Missing For Module "+MOD$
1930 GOSUB 6000
1990 GOTO 9900
2000 REM " --- Generic Parameter Error Message"
2010 LET MESSAGE$="Parameter Record Missing For Module "+MOD$
2020 GOSUB 6000
2090 GOTO 9900
2100 REM " --- Firm Record Missing"
2110 LET MESSAGE$="Firm Description Record Missing"
2120 GOSUB 6000
2130 GOTO 9900
6000 REM " --- Call windowed display routine"
6010 DIM M$[1]
6020 LET M$[0]=MESSAGE$+" (<Enter>=Continue)"
6030 CALL "SYC.XA",1,M$[ALL],0,22,-1,V$,V3
6090 RETURN 
8000 REM " --- Functions"
8090 DEF FNP$(Q$)=CVS(Q$,2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9950 EXIT 
9999 END

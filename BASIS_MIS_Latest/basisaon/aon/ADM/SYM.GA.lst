0010 REM "SYM - Printer Maintenance (Header)"
0020 REM "Program SYM.GA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETERR 9000
0060 SETESC 9000
0070 ON O0 GOTO 0080,1000
0080 BEGIN 
0085 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=3
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="SYM-07",FILES$[3]="SYM-17"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SYM07_DEV=CHANNELS[2],SYM17_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 SYM07A: IOLIST A0$(1),A1$(1)
0220 SYM17A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0500 REM " --- Initializations"
0510 DIM A0$(2),A1$(48),DESCRIPTION$[1],FIELD[1],POSITION[1],LENGTH[1]
0520 DIM B0$(3),B1$(30),B2$(60),B3$(16),MESSAGE$[1]
0570 LET NUMBER=1,L0=7
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,6,0
0750 PRINT 'SB',@(0,3),"Printer ID:",@(59),"Initial Form Feed?",@(0,5),"Opt Description/Font Hex Codes",@(57),"Compressed/Standard",'SF',
0900 REM " --- Position File"
0990 READ (SYM07_DEV,KEY="",DOM=1000)
1000 REM " --- Key"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(SYM07_DEV,END=1030)
1030 LET V0$="S",V1$="CEK",V3$="",V0=2,V1=12,V2=3
1040 LET V4$="Enter A Valid Printer ID (<F3>=Lookup)"
1050 GOSUB 7000
1060 ON V3 GOTO 1080,1000,1000,1070,4500
1070 GOSUB 6200
1080 IF V$="" THEN GOTO 0900
1090 LET A0$(1)=V$
1200 REM " --- Find key"
1210 FIND (SYM07_DEV,KEY=A0$,DOM=1400)IOL=SYM07A
1300 REM " --- Display record"
1310 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- New record initializations"
1510 DIM A1$(48)
1520 LET A1$(31,1)="N"
1590 GOSUB 5000
2000 REM " --- Printer Description"
2010 LET V4$="Enter The Description For This Printer"
2020 LET V0$="S",V1$="C",V2$=A1$(1,30),V3$="",V0=30,V1=15,V2=3,I0=0
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 LET A1$(1,30)=V$
2090 GOTO 3900
2100 REM " --- Initial Form Feed?"
2110 LET V4$="Issue Form Feed When Printing First Page (Y/N)?"
2120 LET V0$="Y",V1$="C",V2$=A1$(31,1),V3$="",V0=1,V1=78,V2=3
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2150 IF POS(V$="YN")=0 THEN GOTO 2100
2160 LET A1$(31,1)=V$
2190 GOTO 3900
3900 REM " --- Next Field"
3905 IF V3=4 THEN GOTO 3990
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,3990
3990 GOSUB 5000
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Copy/Delete)?"
4020 LET V0$="S",V1$="C",V2$="",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4050 IF V$="C" OR V$="COPY" THEN GOTO 4600
4060 IF V$="DELETE" THEN GOTO 4200
4070 IF V$="" THEN GOTO 1000
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write record"
4110 WRITE (SYM07_DEV,KEY=A0$)IOL=SYM07A
4190 RUN "SYM.GB"
4200 REM " --- Remove"
4210 READ (SYM17_DEV,KEY=A0$,DOM=4220)
4220 LET K$=KEY(SYM17_DEV,END=4260)
4230 IF POS(A0$=K$)<>1 THEN GOTO 4260
4240 REMOVE (SYM17_DEV,KEY=K$,DOM=4250)
4250 GOTO 4220
4260 REMOVE (SYM07_DEV,KEY=A0$,DOM=4270)
4290 GOTO 1000
4500 REM " --- Print?"
4510 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4550 ON POS(V$="YN") GOTO 4500,4590,9900
4590 RUN "SYM.GC"
4600 REM " --- Copy printer"
4610 READ (SYM07_DEV,KEY=A0$,DOM=4000)IOL=SYM07A
4620 LET V4$="Enter The Printer ID To Receive The Copied Information:"
4630 LET V0$="S",V1$="C",V2$="",V3$="",V0=2,V1=FNV(V4$),V2=22
4640 GOSUB 7000
4650 ON V3 GOTO 4660,4600,4000,4600,4000
4660 LET PRINTER$=A0$,A0$(1)=V$
4670 WRITE (SYM07_DEV,KEY=A0$)IOL=SYM07A
4690 READ (SYM17_DEV,KEY=PRINTER$,DOM=4700)
4700 REM " --- Copy detail records"
4710 LET K$=KEY(SYM17_DEV,END=4800)
4720 IF POS(PRINTER$=K$)<>1 THEN GOTO 4800
4730 READ (SYM17_DEV)IOL=SYM17A
4740 LET B0$(1,2)=A0$
4750 WRITE (SYM17_DEV,KEY=B0$)IOL=SYM17A
4760 READ (SYM17_DEV,KEY=K$)
4790 GOTO 4700
4800 REM " --- Re-position and redisplay"
4810 LET A0$(1)=PRINTER$
4890 GOTO 1200
5000 REM " --- Display record"
5010 PRINT @(12,3),A0$," ",A1$(1,30),@(78),A1$(31,1),
5090 RETURN 
6200 REM " --- Standard windowed lookup"
6210 LET RECORD$="",CHANNEL=SYM07_DEV,TITLE$="ADD+ON System Printers"
6230 LET DESCRIPTION$[0]="ID",FIELD[0]=1,POSITION[0]=1,LENGTH[0]=2
6240 LET DESCRIPTION$[1]="Description",FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6250 LET ROW=L0+1,COLUMN=79-LENGTH[0]-LENGTH[1]-NUMBER*4,V$="NO COMPANY"
6280 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

0010 REM "SYM - File Definition Listing (Report)"
0020 REM "Program SYM.FB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0170 CALL "SYC.GA",7,1,"","",STATUS
0175 IF STATUS THEN GOTO 9900
0200 REM " --- IOLIST's"
0210 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0500 REM " --- Initializations"
0510 DIM A0$(6),A1$(64),A2$(30),A[2],HEADINGS$[1],MILESTONE$(39),J$(H0,"-")
0520 DIM TYPES$[8]
0550 LET H=0,H1$=N1$,H2$=N4$,CLOCK$="",ELEMENTS=0,L9=55,L=L9+1
0560 LET TYPES$[1]="Serial",TYPES$[2]="Direct",TYPES$[3]="Global"
0570 LET TYPES$[4]="Index",TYPES$[5]="MKeyed",TYPES$[6]="Ref"
0580 LET TYPES$[7]="Sort",TYPES$[8]="Text",H0=132
0600 REM 
0660 LET HEADINGS$[0]=H1$,HEADINGS$[1]=H2$
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,10,0
0720 PRINT 'SB',@(31,9),"Printing",'SF',
0900 REM " --- Position file"
0990 READ (SYM03_DEV,KEY="",DOM=1000)
1000 REM " --- Read next sort record"
1010 LET K$=KEY(SYM03_DEV,END=4000)
1030 READ (SYM03_DEV)IOL=SYM03A
1100 REM " --- Range/Edit Tests"
1150 LET MILESTONE$(1)=A0$+" "+A1$(1,30)
1160 PRINT @(40,9),MILESTONE$
1500 REM " --- Print detail line"
1510 IF L>L9 THEN GOSUB 5000
1520 LET X$=A1$(37,1),X=POS(X$="CDGIMRST"),PROTECT$="No"
1530 IF X>0 THEN LET X$=TYPES$[X]
1540 IF A1$(38,1)="Y" THEN LET PROTECT$="Yes"
1550 PRINT (7)A0$,@(11),A1$(1,30),@(43),A2$(1,30),@(83),X$,@(93),PROTECT$,@(101),A[0]:"####",@(107),A[2]:"##,###",@(115),A[1]:"###,###",@(124),FNB6$(A1$(31,6))
1560 LET L=L+1,ELEMENTS=ELEMENTS+1
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF L>L9 THEN GOSUB 5000
4020 LET X$=STR(ELEMENTS)
4030 IF ELEMENTS=0 THEN LET X$="No"
4040 PRINT (7)""
4050 PRINT (7)X$," File Definitions Listed"
4090 GOTO 9900
5000 REM " --- Print report heading"
5010 LET L=HEADINGS+7
5020 CALL "SYC.HA",7,HEADINGS$[ALL],1,H,H0,F0$(7,3),CLOCK$,STATUS
5030 IF STATUS<>0 THEN GOTO 9900
5040 PRINT (7)""
5050 PRINT (7)J$
5055 PRINT (7)@(83),"File     Write    Key   Record  Numb of    Last"
5060 PRINT (7)"File Name",@(11),"Description",@(43),"Help",@(83),"Type    Protect?  Size  Length  Records  Revision"
5070 PRINT (7)J$
5080 PRINT (7)""
5090 RETURN 
8000 REM " --- Functions"
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8060 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

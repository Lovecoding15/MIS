0010 REM "SYR - Generic Code Maintenance Listing Program"
0020 REM "Program SYR.AA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0042 REM "CHANNEL     : Channel number of source file for report"
0044 REM "RECORD$     : Record ID"
0046 REM "NUMBER      : Number of fields to print"
0050 REM "FIELD$[*]   : Column heading for each field"
0052 REM "FIELD(*)    : String position of each field (1=1st, 2=2nd, etc)"
0054 REM "POSITION(*) : Starting position of field within FIELD(*)"
0056 REM "LENGTH(*)   : Length of field"
0058 REM "TYPE$[*]    : Field type (same as SYC.IA V0$ string)"
0060 REM "MASK$[*]    : Optional mask for numeric fields"
0062 REM "HEADINGS$[*]: Report headings"
0064 REM "HEADINGS    : Number of report heading lines"
0066 REM "WIDTH       : Width of report line (Normally 80 or 132)"
0068 REM "PROMPT$     : Skip beginning/ending prompt if N"
0075 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOLIST's"
0210 LIST02: IOLIST A$[1],A$[2]
0220 LIST04: IOLIST A$[1],A$[2],A$[3],A$[4]
0230 LIST06: IOLIST A$[1],A$[2],A$[3],A$[4],A$[5],A$[6]
0500 REM " --- Initializations"
0510 DIM A$[6],P[NUMBER+1],J$(WIDTH)
0530 LET P[0]=1,KEYPOS=POSITION[0],KEYLEN=LENGTH[0],MAX=0
0535 LET L9=59,L=L9+1,TOTAL=0,PAGE=0,CLOCK$="",WHEN$=F0$(7,3)
0540 LET START$="",FINISH$="",DESC$=FIELD$[0],NAME$="",DISPLAY=KEYLEN
0545 IF DISPLAY=0 THEN LET DISPLAY=20
0550 IF KEYLEN=0 THEN LET PROMPT$="N"
0600 REM " --- Build heading line"
0610 DIM BUFFER$(WIDTH)
0615 FOR X=0 TO NUMBER
0620 LET W=LENGTH[X],D=LEN(FIELD$[X])
0625 IF POS(TYPE$[X]="NM")>0 THEN LET W=LEN(MASK$[X])
0630 IF TYPE$[X](1,1)="D" THEN LET W=8
0635 IF TYPE$[X](1,1)="T" THEN LET W=5
0640 IF D>W THEN LET W=D
0650 LET W=P[X]+W+2,P[X+1]=W,BUFFER$(P[X])=FIELD$[X]
0660 IF FIELD[X]>MAX THEN LET MAX=FIELD[X]
0670 NEXT X
0680 LET HEADINGS$[HEADINGS]=BUFFER$
0700 REM " --- Background"
0710 IF PROMPT$="N" THEN GOTO 0950
0715 IF DESC$="" THEN LET DESC$="Value"
0720 LET WIN_WIDTH=MAX(32,16+LEN(DESC$)+KEYLEN),WIN_HEIGHT=5
0725 LET WIN_Y=10,WIN_X=INT((80-WIN_WIDTH)/2),TITLE$="Report Selections"
0750 CALL "SYC.WA",0,WIN_WIDTH,WIN_HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0760 PRINT 'SB',@(1,0),"Beginning ",DESC$,@(4,1),"Ending ",DESC$,'SF',
0770 LET COLUMN=12+LEN(DESC$),LTITLE$=DESC$+" Lookup"
0800 REM " --- Prompt for beginning/ending values"
0810 GOSUB 6000
0820 CALL "SYC.WD",NAME$
0900 REM " --- Position file"
0910 LET PRINTER=UNT
0920 CALL "SYC.GA",PRINTER,1,"","",STATUS
0930 IF STATUS THEN GOTO 9900
0940 CALL "SYC.NB","Printing",DISPLAY,COLUMN
0950 LET K$=N0$+RECORD$+START$
0980 READ (CHANNEL,KEY=K$,DOM=1000)
0990 GOTO 1030
1000 REM " --- Read next record"
1010 LET K$=KEY(CHANNEL,END=4000)
1020 IF N0$+RECORD$<>"" AND POS(N0$+RECORD$=K$)<>1 THEN GOTO 4000
1030 IF FINISH$<>"" THEN IF K$(KEYPOS,KEYLEN)>FINISH$ THEN GOTO 4000
1035 IF MAX<3 THEN READ (CHANNEL,KEY=K$)IOL=LIST02
1040 IF MAX>2 AND MAX<5 THEN READ (CHANNEL,KEY=K$)IOL=LIST04
1045 IF MAX>4 THEN READ (CHANNEL,KEY=K$)IOL=LIST06
1050 IF KEYLEN>0 THEN PRINT @(COLUMN,11),A$[1](KEYPOS,KEYLEN)
1055 IF KEYLEN=0 THEN PRINT @(COLUMN,11),A$[1](KEYPOS)
1060 LET TOTAL=TOTAL+1
1500 REM " --- Format detail line"
1510 DIM BUFFER$(WIDTH)
1520 IF KEYLEN>0 THEN LET BUFFER$(P[0])=A$[1](KEYPOS,KEYLEN)
1525 IF KEYLEN=0 THEN LET BUFFER$(P[0])=A$[1](KEYPOS)
1530 IF TYPE$[0]<>"M" THEN GOTO 1600
1540 LET VALUE=NUM(A$[1](POSITION[0],LENGTH[0]),ERR=1600)
1550 LET BUFFER$(P[0])=STR(-VALUE:MASK$[0],ERR=1600)
1600 REM " --- Field with no length specified"
1610 FOR X=1 TO NUMBER
1620 IF LENGTH[X]>0 THEN GOTO 1700
1630 IF POS(TYPE$[X]="NM")=0 THEN GOTO 1680
1640 LET VALUE=NUM(A$[FIELD[X]],ERR=1680)
1650 LET BUFFER$(P[X])=STR(VALUE:MASK$[X],ERR=1680)
1660 IF TYPE$[X]="M" THEN LET BUFFER$(P[X])=STR(-VALUE:MASK$[X],ERR=1680)
1670 GOTO 1690
1680 LET BUFFER$(P[X])=A$[FIELD[X]]
1690 GOTO 1990
1700 REM " --- Field with specified length"
1710 LET Y=LENGTH[X]
1715 IF POSITION[X]+LENGTH[X]-1>LEN(A$[FIELD[X]]) THEN LET Y=LEN(A$[FIELD[X]])-POSITION[X]+1
1720 LET X$=A$[FIELD[X]](POSITION[X],Y),BUFFER$(P[X])=X$
1725 IF TYPE$[X]="T" THEN IF FNP$(X$)<>"" THEN LET BUFFER$(P[X])=FNT$(X$) ELSE LET BUFFER$(P[X])=""
1730 IF TYPE$[X](1,1)="D" THEN IF FNP$(TYPE$[X])="D" THEN LET BUFFER$(P[X])=FNB$(X$) ELSE LET BUFFER$(P[X])=FNB6$(X$)
1740 IF TYPE$[X](1,1)="D" THEN IF BUFFER$(P[X],8)="  /  /  " THEN LET BUFFER$(P[X])=""
1745 IF POS(TYPE$[X]="NM")=0 THEN GOTO 1800
1750 IF TYPE$[X]="M" THEN IF FNP$(X$)="" THEN GOTO 1800
1755 LET VALUE=NUM(X$,ERR=1800)
1760 LET BUFFER$(P[X])=STR(VALUE:MASK$[X],ERR=1800)
1770 IF TYPE$[X]="M" THEN LET BUFFER$(P[X])=STR(-VALUE:MASK$[X],ERR=1800)
1800 REM " --- Yes/No"
1810 IF TYPE$[X]<>"Y" THEN GOTO 1900
1820 IF LEN(FIELD$[X])<3 THEN GOTO 1900
1830 IF X$="Y" THEN LET X$="Yes"
1840 IF X$="N" THEN LET X$="No"
1850 LET BUFFER$(P[X])=X$
1900 REM " --- Loop back for next field"
1990 NEXT X
2000 REM " --- Print detail line"
2010 IF L+1>L9 THEN GOSUB 5000
2020 PRINT (PRINTER)BUFFER$
2030 LET L=L+1
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- All done"
4010 IF TOTAL$="" THEN GOTO 4090
4020 IF L+2>L9 THEN GOSUB 5000
4030 LET X$="No"
4040 IF TOTAL>0 THEN LET X$=STR(TOTAL)
4050 PRINT (PRINTER)""
4060 PRINT (PRINTER)X$," ",TOTAL$
4090 GOTO 9900
5000 REM " --- Report heading"
5010 LET L=HEADINGS+2
5020 CALL "SYC.HA",PRINTER,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5040 PRINT (PRINTER)""
5090 RETURN 
6000 REM " --- Beginning value"
6010 LET V0$=TYPE$[0],V1$="",V2$=START$,V3$=MASK$[0]
6020 LET V4$="<F1>=First     <F3>=Lookup",V0=KEYLEN,V1=COLUMN,V2=0
6030 GOSUB 7000
6040 ON V3 GOTO 6150,6050,6000,6100,6900
6050 LET START$=""
6060 PRINT @(V1,V2),"First",'CL',
6090 GOTO 6200
6100 REM 
6110 LET X=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,Y=4,V$="SKIP"
6115 IF N0$="" THEN LET V$=V$+" & NO COMPANY"
6120 CALL "SYC.LK",CHANNEL,RECORD$,1,LTITLE$,FIELD$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],X,Y,V$
6130 IF V$="" THEN GOTO 6000
6150 REM 
6160 IF V$="" THEN GOTO 6050
6170 LET START$=V$
6180 PRINT @(V1,V2),START$,'CL',
6200 REM " --- Ending value"
6210 LET V0$=TYPE$[0],V1$="",V2$=FINISH$,V3$=MASK$[0]
6220 LET V4$="<F1>=Last     <F3>=Lookup",V0=KEYLEN,V1=COLUMN,V2=1
6230 GOSUB 7000
6240 ON V3 GOTO 6350,6250,6000,6300,6900
6250 LET FINISH$=""
6260 PRINT @(V1,V2),"Last",'CL',
6290 GOTO 6900
6300 REM 
6310 LET X=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4,Y=4,V$="SKIP"
6315 IF N0$="" THEN LET V$=V$+" & NO COMPANY"
6320 CALL "SYC.LK",CHANNEL,RECORD$,1,LTITLE$,FIELD$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],X,Y,V$
6330 IF V$="" THEN GOTO 6200
6350 REM 
6360 IF V$="" THEN GOTO 6250
6370 LET FINISH$=V$
6380 IF LEN(FINISH$)<KEYLEN THEN LET FINISH$=FINISH$+J$,FINISH$=FINISH$(1,KEYLEN)
6390 PRINT @(V1,V2),FINISH$,'CL',
6900 REM " --- Correct?"
6910 LET V0$="Y",V1$="",V2$="Y",V3$="",V4$="Is The Above Correct (Y/N)?"
6920 LET V0=1,V1=MAX(INT((WIN_WIDTH-LEN(V4$))/2)-1,0),V2=WIN_HEIGHT-3
6930 PRINT @(V1,V2),V4$,
6940 LET V1=V1+LEN(V4$),V4$=""
6950 GOSUB 7000
6960 PRINT @(0,V2),'CL',
6970 ON V3 GOTO 6980,6000,6000,6000,9900
6980 ON POS(V$="YN") GOTO 6900,6990,6000
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8025 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8080 DEF FNT$(Q$)=Q$(1,2)+":"+Q$(3,2)
8090 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9910 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9920 RUN PROGRAM$,ERR=9950
9950 RUN "SYS.AA"
9999 END

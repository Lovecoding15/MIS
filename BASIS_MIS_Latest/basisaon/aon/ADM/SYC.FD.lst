0010 REM "SYC - Find Strings In Programs"
0020 REM "Program SYC.FD"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0085 SETESC 9000
0500 REM " --- Initializations"
0550 LET FILENAME$=PGM(-1)
0600 REM " --- Open/lock files"
0610 LET CHANNEL=UNT,LAST$=""
0620 CLOSE (CHANNEL,ERR=0630)
0630 OPEN (CHANNEL,ERR=1200)FILENAME$
0640 LET FID$=FID(CHANNEL)
0700 REM " --- Background"
0710 PRINT 'CL',"Searching ",FID$(9)
1000 REM " --- Enter text to search for"
1010 SETESC 9900
1020 PRINT 'CL',"Search ",FILENAME$," For: ",
1030 INPUT TEXT$,
1040 ON CTL GOTO 1050,1100,1200,1000,9900
1050 IF TEXT$="" THEN IF LAST$<>"" THEN LET TEXT$=LAST$
1060 SETESC 4000
1070 LET OCCURS=0,STATEMENTS=0,LINE=1,LAST$=TEXT$,L=0
1090 GOTO 2000
1100 REM " --- <F1> Clear LAST$"
1110 LET LAST$=""
1190 GOTO 1000
1200 REM " --- <F2> Change Program"
1210 INPUT 'CL',"Enter The Name Of The Program To Search: ",BUFFER$,
1220 IF CTL<>0 THEN GOTO 1000
1230 LET BUFFER$=CVS(BUFFER$,3)
1240 IF BUFFER$="" THEN GOTO 1000
1250 LET FILENAME$=BUFFER$
1290 GOTO 0600
2000 REM " --- Position file and skip variable table"
2010 CLOSE (CHANNEL,ERR=2020)
2020 OPEN (CHANNEL,ERR=1200)FILENAME$
2030 LET HEADER$=$$,D$=$$
2040 READ RECORD(CHANNEL,IND=0,SIZ=10)HEADER$
2050 LET PGMLEN=DEC($00$+HEADER$(1,2)),VARADD=DEC($00$+HEADER$(5,2))
2060 LET VARNUM=DEC(HEADER$(7,2)),VARLEN=PGMLEN-VARADD-2
2070 IF VARLEN>0 THEN READ RECORD(CHANNEL,IND=VARADD,SIZ=VARLEN)VARLIST$
2080 READ RECORD(CHANNEL,IND=0,SIZ=10)
2900 REM 
2910 READ RECORD(CHANNEL,SIZ=512,END=4000)D0$
2920 LET D$=D$+D0$,D0$=$$
3000 REM " --- Search for requested string"
3010 IF LEN(D$)<4 THEN GOTO 2900
3040 LET LINLEN=DEC($00$+D$(3,2))+4,LINENUM=DEC($00$+D$(1,2))
3050 IF LEN(D$)>=LINLEN THEN GOTO 3090
3060 READ RECORD(CHANNEL,SIZ=512,END=4000)D0$
3070 LET D$=D$+D0$,D0$=$$
3080 GOTO 3050
3090 LET LINE$=LST(D$(1,LINLEN),VARLIST$),D$=D$(LINLEN+1)
3100 REM " --- Does statement contain string?"
3110 IF TEXT$="" THEN GOTO 3500
3120 IF POS(TEXT$=LINE$)=0 THEN GOTO 3900
3130 LET STATEMENTS=STATEMENTS+1
3500 REM " --- List entire line"
3510 LET LENGTH=LEN(LINE$)
3520 IF LENGTH<81 THEN GOTO 3600
3530 LET BUFFER$=LINE$(1,80)
3540 GOSUB 5000
3550 LET LINE$=LINE$(81)
3590 GOTO 3500
3600 REM " --- Print remainder of line"
3610 IF LINE$="" THEN GOTO 3900
3620 LET BUFFER$=LINE$
3630 GOSUB 5000
3900 REM " --- Next line"
3990 GOTO 3000
4000 REM " --- All done"
4010 IF TEXT$="" THEN GOTO 1000
4020 IF OCCURS>0 THEN PRINT STR(OCCURS)," Occurrences (",STR(STATEMENTS)," Statements) Of ",TEXT$," Found In ",FILENAME$
4030 IF OCCURS=0 THEN PRINT "No Occurrences Of ",TEXT$," Found In ",FILENAME$
4050 PRINT ""
4090 GOTO 1000
5000 REM " --- Count occurrences of TEXT$ in BUFFER$"
5010 IF TEXT$="" THEN GOTO 5100
5020 LET X$="",W$=BUFFER$
5030 WHILE POS(TEXT$=W$)
5040 LET P=POS(TEXT$=W$),X$=X$+W$(1,P-1)+'SB'+W$(P,LEN(TEXT$))+'SF'
5050 LET W$=W$(P+LEN(TEXT$)),OCCURS=OCCURS+1
5060 WEND
5080 LET BUFFER$=X$+W$
5100 REM " --- Display BUFFER$"
5110 IF L>21 THEN GOSUB 6000
5120 PRINT BUFFER$
5130 LET L=L+1
5190 RETURN 
6000 REM " --- Full screen"
6010 INPUT "Press <Enter> To Continue: ",*,
6020 LET L=0
6090 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9910 CLOSE (CHANNEL,ERR=9920)
9920 PRINT ""
9950 EXIT 
9999 END

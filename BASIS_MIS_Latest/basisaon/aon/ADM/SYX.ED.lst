0010 REM "SYX - Generate Executive Summary (Inventory)"
0020 REM "Program SYX.ED"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=6
0120 DIM FILES$[FILES],CHANNELS[FILES],OPTIONS$[FILES]
0130 LET FILES$[1]="IVM-01",FILES$[2]="IVM-02",FILES$[3]="IVM-03"
0140 LET FILES$[4]="IVT-04"
0200 REM " --- IOLIST's"
0210 SYE09A: IOLIST A0$(1),A1$(1),A[ALL]
0220 IVM01A: IOLIST B0$(1),B1$,B2$,B3$,B4$,B5$,B6$,B[ALL]
0230 IVM02A: IOLIST C0$(1),C1$(1),C2$,C[ALL]
0240 IVT04A: IOLIST D0$(1),D1$(1),D2$(1),D[ALL]
0250 IVM04A: IOLIST X0$(1),X[ALL]
0255 SYM04A: IOLIST X$,S$(1)
0260 SYM06A: IOLIST N0$,N1$(1)
0270 IVS01A: IOLIST P0$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0280 IVM07A: IOLIST W0$(1),W1$(1),W[ALL]
0400 REM " --- Parameters"
0410 LET PGM$="SYX.EE",LF$="N",LS$="N"
0420 FIND (SYM04_DEV,KEY="IV",DOM=4000)IOL=SYM04A
0430 IF S$(39,1)<>"Y" THEN GOTO 4000
0440 LET TITLE$=FNP$(S$(1,35))
0500 REM " --- Initialize"
0510 DIM A0$(8),A1$(32),A[31]
0590 LET D=NUM(DD$),M=NUM(MM$),Y=NUM(YEAR$),PASTDUE=0
0600 REM " --- Additional File Opens"
0610 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS>0 THEN GOTO 9900
0630 LET IVM01_DEV=CHANNELS[1],IVM02_DEV=CHANNELS[2]
0640 LET IVM03_DEV=CHANNELS[3],IVT04_DEV=CHANNELS[4]
0700 REM " --- Background"
0710 PRINT @(40,5),TITLE$,'CL',@(40,7),'CL',@(COLUMN,11),FILL(K9),
1000 REM " --- For each firm"
1010 FOR FIRM=1 TO FIRMS STEP 2
1020 LET N0$=FIRMS$(FIRM,2)
1030 FIND (SYM06_DEV,KEY=N0$,DOM=3900)IOL=SYM06A
1040 PRINT @(40,7),N0$," ",N1$
1200 REM " --- Parameters"
1210 DIM P[3]
1220 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=3900)IOL=IVS01A
1230 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),K=P[0]
1240 LET P[2]=NUM(P2$(5,1)),P[3]=NUM(P2$(7,2)),LF$="N",LS$="N"
1250 IF POS(P3$(3,1)="LF")>0 THEN LET LF$="Y"
1260 IF POS(P3$(17,1)="LS")>0 THEN LET LS$="Y"
1400 REM " --- Additional File Opens"
1410 IF LF$="N" AND LS$="N" THEN GOTO 1600
1420 IF IVM04_DEV AND IVM07_DEV THEN GOTO 1600
1430 LET FILES$[4]="",FILES$[5]=""
1440 IF LF$="Y" THEN IF IVM04_DEV=0 THEN LET FILES$[5]="IVM-04"
1450 IF LS$="Y" THEN IF IVM07_DEV=0 THEN LET FILES$[6]="IVM-07"
1460 CALL "SYC.DA",1,5,6,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
1470 IF STATUS THEN GOTO 3900
1480 IF IVM04_DEV=0 THEN LET IVM04_DEV=CHANNELS[5]
1490 IF IVM07_DEV=0 THEN LET IVM07_DEV=CHANNELS[6]
1700 REM " --- Company initializations"
1710 DIM A0$(8),A1$(32),A[31],CHANGE[31]
1720 LET ONHAND=0,COMMITTED=0
1900 REM " --- Position IVM-01"
1910 DIM B0$(22),B[12],C0$(24),C1$(64),C[20]
1920 DIM D0$(32),D1$(63),D2$(16),D[4]
1990 READ (IVM01_DEV,KEY=N0$,DOM=2000)
2000 REM " --- Read next record"
2010 LET K$=KEY(IVM01_DEV,END=3000)
2020 IF POS(N0$=K$)<>1 THEN GOTO 3000
2050 PRINT @(COLUMN,11),K$(3),
2060 READ (IVM01_DEV)IOL=IVM01A
2080 LET START$=B0$(1,2)+"G"+B0$(3)
2090 READ (IVM03_DEV,KEY=START$,DOM=2100)
2100 REM " --- Read warehouse records to accumulate quantities"
2110 LET K$=KEY(IVM03_DEV,END=2900)
2120 IF POS(START$=K$)<>1 THEN GOTO 2900
2125 READ (IVM03_DEV)
2130 DIM C0$(24),C1$(16),C[20]
2140 PRINT @(COLUMN+P[0]+1,11),K$(24),
2150 LET C0$(1)=B0$(1,2)+K$(24)+B0$(3)
2160 FIND (IVM02_DEV,KEY=C0$,DOM=2800)IOL=IVM02A
2170 LET COST=C[11]
2180 IF POS(P3$(3,1)="LF")>0 THEN GOSUB 6400
2185 IF POS(P3$(17,1)="SL")>0 THEN IF B2$(19,2)="YY" THEN GOSUB 6500
2190 LET COMMITTED=COMMITTED+C[2]*COST,ONHAND=ONHAND+C[0]*COST
2200 REM " --- Accumulate net amounts by day"
2210 IF D=1 THEN GOTO 2800
2230 FOR X=1 TO D
2240 LET D0$(1)=N0$+K$(24)+B0$(3)+FND$(MM$+STR(X:"00")+YY$)
2290 READ (IVT04_DEV,KEY=D0$,DOM=2300)
2300 REM " --- Cycle through IVT-04 records"
2310 LET K$=KEY(IVT04_DEV,END=2700)
2315 IF POS(N0$=K$)<>1 THEN GOTO 2700
2320 IF POS(D0$(1,7+P[0])=K$)<>1 THEN GOTO 2700
2330 READ (IVT04_DEV)IOL=IVT04A
2350 IF D1$(1,2)<>"BB" THEN LET CHANGE[X]=CHANGE[X]+(-D[0])*COST
2390 GOTO 2300
2700 REM " --- Next date"
2790 NEXT X
2800 REM " --- Next warehouse"
2890 GOTO 2100
2900 REM " --- Loop back for next IVM-01 record"
2990 GOTO 2000
3000 REM " --- Initialize current month record"
3010 LET A0$(1)=N0$+WHEN$,A[0]=COMMITTED,A[D]=ONHAND
3050 REM " --- Calculate actual value by day for current month"
3060 IF D<2 THEN GOTO 3100
3070 FOR X=D-1 TO 1 STEP -1
3080 LET A[X]=CHANGE[X+1]+A[X+1]
3090 NEXT X
3100 REM " --- Fill remainder of month"
3110 IF D>30 THEN GOTO 3150
3120 FOR X=D+1 TO 31
3130 LET A[X]=A[D]
3140 NEXT X
3150 REM " --- Write current month record"
3160 WRITE (SYE09_DEV,KEY=A0$)IOL=SYE09A
3170 LET M=NUM(MM$),Y=NUM(YEAR$)
3200 REM " --- Initialize remaining month records"
3210 LET M=M+1
3220 IF M>12 THEN LET Y=Y+1,M=1
3230 LET PERIOD$=STR(Y:"0000")+STR(M:"00")
3240 IF PERIOD$>LAST$ THEN GOTO 3300
3250 DIM A0$(8),A1$(32),A[31],U[31],L[31]
3255 LET A[0]=COMMITTED
3260 FOR X=1 TO 31
3265 LET A[X]=ONHAND
3270 NEXT X
3275 LET A0$(1)=N0$+PERIOD$
3280 WRITE (SYE09_DEV,KEY=A0$)IOL=SYE09A
3290 GOTO 3200
3300 REM " --- Prepare for order updating"
3310 PRINT @(COLUMN,11),FILL(K9),
3500 REM " --- Update inventory records with sales/purchase orders"
3510 LET CHANNEL=SYE05_DEV,M=-1
3520 GOSUB 6000
3530 LET CHANNEL=SYE06_DEV,M=1
3540 GOSUB 6000
3900 REM " --- Next firm"
3990 NEXT FIRM
4000 REM " --- Run next overlay"
4010 FOR X=1 TO FILES
4020 CLOSE (CHANNELS[X],ERR=4030)
4030 NEXT X
4090 RUN PGM$,ERR=9900
5500 REM " --- Update all future inventory records"
5510 DIM A1$(32),A[31],U[31],L[31]
5580 READ (SYE09_DEV,KEY=A0$,DOM=5600)IOL=SYE09A
5590 GOTO 5650
5600 REM " --- Read next inventory record"
5610 LET K$=KEY(SYE09_DEV,END=5900)
5620 IF POS(N0$=K$)<>1 THEN GOTO 5900
5630 READ (SYE09_DEV)IOL=SYE09A
5650 FOR Y=T TO 31
5660 LET A[Y]=A[Y]+AMOUNT
5670 NEXT Y
5675 LET T=1
5680 WRITE (SYE09_DEV,KEY=A0$)IOL=SYE09A
5690 GOTO 5600
5900 REM " --- Done updating inventory"
5990 RETURN 
6000 REM " --- Update inventory record with sales/purchase orders"
6050 LET K$=N0$+WHEN$
6080 READ (CHANNEL,KEY=K$,DOM=6100)
6090 GOTO 6120
6100 REM " --- Read next sales/purchase order record and save daily totals"
6110 LET K$=KEY(CHANNEL,END=6300)
6115 IF POS(N0$=K$)<>1 THEN GOTO 6300
6120 READ (CHANNEL,KEY=K$)IOL=SYE09A
6125 PRINT @(COLUMN,11),K$(1,2)," ",K$(3,2)," ",K$(5,2)," ",K$(7,2)
6130 DIM S[31]
6135 LET F=D+1,T=0,L$=K$
6140 IF K$(3)>WHEN$ THEN LET F=1
6145 IF F>31 THEN GOTO 6290
6170 FOR X=F TO 31
6180 LET S[X]=A[X]*M,T=T+ABS(S[X])
6190 NEXT X
6200 REM " --- Update inventory records"
6210 IF T=0 THEN GOTO 6290
6220 FOR X=F TO 31
6225 IF S[X]=0 THEN GOTO 6250
6230 LET AMOUNT=S[X],T=X,A0$(1)=L$
6240 GOSUB 5500
6250 NEXT X
6280 READ (SYE09_DEV,KEY=L$)
6290 GOTO 6100
6300 REM " --- All records updated"
6390 RETURN 
6400 REM " --- Determine LIFO/FIFO average cost"
6410 DIM X0$(4+P[0]),X[9]
6420 LET X0$(1)=C0$,T=0,Q=0
6430 FIND (IVM04_DEV,KEY=X0$,DOM=6490)IOL=IVM04A
6440 FOR X=0 TO 4
6450 LET Q=Q+X[X+5],T=T+X[X]*X[X+5]
6460 NEXT X
6470 GOSUB 6700
6490 RETURN 
6500 REM " --- Determine serial/lot average cost"
6510 DIM W0$(44),W1$(64),W[11]
6520 IF B2$(19,1)<>"Y" THEN GOTO 6790
6540 LET W0$(1)=C0$,T=0,Q=0
6590 READ (IVM07_DEV,KEY=W0$,DOM=6600)
6600 REM " --- Read all serial/lot records for current item/warehouse"
6610 LET K$=KEY(IVM07_DEV,END=6700)
6620 IF POS(K$(1,24)=C0$)<>1 THEN GOTO 6700
6630 READ (IVM07_DEV)IOL=IVM07A
6635 IF W1$(54,1)="C" THEN GOTO 6690
6640 LET Q=Q+W[0],T=T+W[0]*W[9]
6690 GOTO 6600
6700 REM " --- Now calculate average of all serial/lot records"
6710 IF Q<>C[2] THEN LET X=C[2]-Q,Q=Q+X,T=T+X*COST
6720 IF Q<>0 THEN LET COST=T/Q
6790 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8030 DEF FNC$(Q1$)=FNA$(Q1$(2),Q1$)+FNA$(Q1$(3),Q1$)+FNA$(Q1$(1),Q1$)
8040 DEF FND$(Q$)=CHR(FNYY_YEAR(Q$(5,2))+32)+CHR(NUM(Q$(1,2))+32)+CHR(NUM(Q$(3,2))+32)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

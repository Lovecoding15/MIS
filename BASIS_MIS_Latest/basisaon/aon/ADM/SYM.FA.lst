0010 REM "SYM - Record Layout Maintenance (Header)"
0020 REM "Program SYM.FA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 SETERR 9000
0060 SETESC 9000
0065 ON O0 GOTO 0070,1000
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=8
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="SYM-03"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SYM03_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 DDM01A: IOLIST X0$(1),X1$(1),X[ALL]
0220 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0230 DDM03A: IOLIST B0$(1),B1$(1),B[ALL]
0240 DDM04A: IOLIST C0$(1),C1$(1),C[ALL]
0250 DDM05A: IOLIST D0$(1),D1$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0405 DIM INFO$[20]
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="DD",N4$=F5$
0430 CALL "SYC.VA",N2$,INFO$[ALL]
0440 LET DD$=INFO$[20]
0500 REM " --- Initializations"
0510 DIM A0$(6),A1$(64),A2$(30),A[2],B0$(7),B1$(64),B[2],J1$(80),O[2]
0520 DIM C0$(10),C1$(64),C[2],D0$(10),D1$(60),X0$(12),X1$(64),X[2],XREF$(57)
0550 LET L0=12,L9=21,REVISION$=F0$(18,6)
0600 REM " --- Additional file opens"
0610 IF DD$="N" THEN GOTO 0700
0615 LET FILES$[3]="DDM-01",FILES$[4]="DDM-03",FILES$[5]="DDM-04"
0620 LET FILES$[6]="DDM-05",FILES$[7]="DDM-06",FILES$[8]="DDM-04"
0650 CALL "SYC.DA",1,3,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0660 IF STATUS>0 THEN GOTO 9900
0680 LET DDM01_DEV=CHANNELS[3],DDM03_DEV=CHANNELS[4],DDM04_DEV=CHANNELS[5]
0690 LET DDM05_DEV=CHANNELS[6],DDM06_DEV=CHANNELS[7],DDM04_DEV2=CHANNELS[8]
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,L0-4,0
0720 PRINT 'SB',@(4,3),"File Name",@(60),"File Type",@(2,4),"Description",@(61),"Key Size",@(9,5),"Help",@(56),"Record Length",@(0,6),"Write Protect",@(52),"Number of Records",@(56,7),"Last Revision",'SF',
0730 IF DD$="N" THEN GOTO 0800
0740 CALL "SYC.WC",1,0,80,0,0,L0-2,0
0750 PRINT 'SB',@(0,L0-3),"Record ID",@(56),"Last Revision",'SF',
0900 REM " --- Position file"
0990 READ (SYM03_DEV,KEY="",DOM=1000)
1000 REM " --- File Name"
1005 PRINT 'CF',
1010 LET V2$="",V2$=KEY(SYM03_DEV,END=1020)
1020 LET V0$="S",V1$="CK^",V3$="",V0=6,V1=14,V2=3,I0=0
1030 LET V4$="Enter A Valid File Name (<F3>=Lookup/<Enter>=Next Record)"
1040 GOSUB 7000
1050 ON V3 GOTO 1070,1000,1000,1060,4700
1060 GOSUB 6000
1070 IF V$="" THEN GOTO 1100
1080 LET A0$(1)=V$
1090 GOTO 1200
1100 REM " --- Get next record"
1110 LET K$=KEY(SYM03_DEV,END=0900)
1120 READ (SYM03_DEV)
1130 LET A0$=K$
1200 REM " --- Retrieve record"
1210 READ (SYM03_DEV,KEY=A0$,DOM=1400)IOL=SYM03A
1220 FIND RECORD(SYM03_DEV,KEY=A0$)OLDRECORD$
1300 REM " --- Display record"
1320 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record?"
1410 CALL "SYC.YN",1,"Is This A New Record",1,V$,V3
1440 ON V3 GOTO 1460,1400,1400,1400,4700
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record"
1510 DIM A1$(64),A2$(30),A[2]
1520 LET OLDRECORD$="",A1$(38,1)="N"
1600 REM " --- Description"
1610 LET V0$="S",V1$="C",V2$=A1$(1,30),V3$="",V0=30,V1=14,V2=4,I0=1
1620 LET V4$="Enter the description of this data file"
1630 GOSUB 7000
1640 ON V3 GOTO 1650,1600,3900,1600,3900
1650 LET A1$(1,30)=V$
1690 GOTO 3900
1700 REM " --- Help"
1710 LET V0$="S",V1$="C",V2$=A2$(1,30),V3$="",V0=30,V1=14,V2=5
1720 LET V4$="Enter The Help Text For This Data File"
1730 GOSUB 7000
1740 ON V3 GOTO 1750,1700,3900,1700,3900
1750 LET A2$(1,30)=V$
1790 GOTO 3900
1800 REM " --- Write Protect?"
1810 LET V0$="Y",V1$="C",V2$=A1$(38,1),V3$="",V0=1,V1=14,V2=6
1820 LET V4$="Should This File Be Write-Protected (Y/N)?"
1830 GOSUB 7000
1840 ON V3 GOTO 1850,1800,3900,1800,3900
1850 LET A1$(38,1)=V$
1890 GOTO 3900
1900 REM " --- File Type"
1910 LET V0$="S",V1$="CR",V2$=A1$(37,1),V3$="GCDIMSTR",V0=1,V1=70,V2=3
1920 LET V4$="C=Serial/D=Direct/M=MKeyed/I=Index/R=Reference/S=Sort/T=Text/G=Global Variable"
1930 GOSUB 7000
1940 ON V3 GOTO 1950,1900,3900,1900,3900
1950 IF POS(V$="GCDIMSTR")=0 THEN GOTO 1900
1960 LET A1$(37,1)=V$
1990 GOTO 3900
2000 REM " --- Key Length"
2010 LET V0$="N",V1$="CR",V2$=STR(A[0]),V3$="##",V0=2,V1=70,V2=4
2015 IF POS(A1$(37,1)="CITG")>0 THEN LET A[0]=0; GOTO 2080
2020 LET V4$="Enter The Length Of The Key To This File"
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3900,2000,3900
2050 IF V<1 THEN GOTO 2000
2060 LET A[0]=V
2080 PRINT @(V1,V2),'CL',STR(A[0])
2090 GOTO 3900
2100 REM " --- Record Length"
2110 LET V0$="N",V1$="CR",V2$=STR(A[2]),V3$="####",V0=4,V1=70,V2=5
2115 IF POS(A1$(37,1)="ST")>0 THEN LET A[2]=0; GOTO 2180
2120 LET V4$="Enter The Record Length For This File"
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2150 IF V<1 THEN GOTO 2100
2160 LET A[2]=V
2180 PRINT @(V1,V2),'CL',STR(A[2])
2190 GOTO 3900
2200 REM " --- Number of Records"
2210 LET V0$="N",V1$="CR",V2$=STR(A[1]),V3$="#####",V0=5,V1=70,V2=6
2215 IF POS(A1$(37,1)="CMTG")>0 THEN LET A[1]=0; GOTO 2280
2220 LET V4$="Enter The Number Of Records To Specify When Defining This File"
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3900,2200,3900
2250 IF V<0 THEN GOTO 2200
2260 LET A[1]=V
2280 PRINT @(V1,V2),'CL',STR(A[1])
2290 GOTO 3900
3900 REM " --- Next field"
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 1000,1600,1700,1800,1900,2000,2100,2200,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete"
4015 IF DD$="Y" THEN LET V4$=V4$+"/Copy"
4020 LET V0$="S",V1$="CR",V2$="Y",V3$="",V4$=V4$+")?",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4700
4050 IF V$="DELETE" THEN GOTO 4200
4060 IF DD$="Y" THEN IF V$="C" OR V$="COPY" THEN GOTO 4600
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record"
4110 WRITE (SYM03_DEV,KEY=A0$)IOL=SYM03A
4120 FIND RECORD(SYM03_DEV,KEY=A0$)NEWRECORD$
4130 IF NEWRECORD$=OLDRECORD$ THEN GOTO 4180
4140 LET A1$(31,6)=REVISION$
4150 WRITE (SYM03_DEV,KEY=A0$)IOL=SYM03A
4180 IF DD$="Y" THEN GOTO 4900
4190 GOTO 1000
4200 REM " --- Remove record"
4210 IF DD$="N" THEN GOTO 4580
4220 LET V4$="OK To Delete All Record Layouts For This File"
4230 CALL "SYC.YN",0,V4$,0,V$,V3
4240 IF V$<>"YES" THEN GOTO 4000
4290 READ (DDM05_DEV,KEY=A0$,DOM=4300)
4300 REM " --- Delete remarks"
4310 LET K$=KEY(DDM05_DEV,END=4390)
4320 IF POS(A0$=K$)<>1 THEN GOTO 4390
4330 REMOVE (DDM05_DEV,KEY=K$)
4350 GOTO 4300
4390 READ (DDM04_DEV,KEY=A0$,DOM=4400)
4400 REM " --- Delete fields"
4410 LET K$=KEY(DDM04_DEV,END=4490)
4420 IF POS(A0$=K$)<>1 THEN GOTO 4490
4430 READ (DDM04_DEV,KEY=K$)IOL=DDM04A
4440 LET X0$(1)=C1$(1),X1$(1,30)=C1$(17,30)
4445 LET X1$(37,1)=C1$(53,1),X[0]=C[0]
4450 FIND (DDM01_DEV,KEY=X0$,DOM=4455)IOL=DDM01A
4455 LET LENGTH=X[0]
4460 IF C[1]>0 THEN LET LENGTH=LENGTH*C[1]
4465 LET XREF$(1)=X0$(1,12)+X1$(1,30)+X1$(37,1)+STR(LENGTH:"0000")+C0$
4470 REMOVE (DDM06_DEV,KEY=XREF$,DOM=4475)
4475 REMOVE (DDM04_DEV,KEY=K$)
4480 GOTO 4400
4490 READ (DDM03_DEV,KEY=A0$,DOM=4500)
4500 REM " --- Remove records"
4510 LET K$=KEY(DDM03_DEV,END=4580)
4520 IF POS(A0$=K$)<>1 THEN GOTO 4580
4530 REMOVE (DDM03_DEV,KEY=K$)
4550 GOTO 4500
4580 REMOVE (SYM03_DEV,KEY=A0$,DOM=4590)
4590 GOTO 1000
4600 REM " --- Run copy overlay"
4690 RUN "DDM.BF",ERR=4000
4700 REM " --- Run report overlay"
4710 IF DD$="Y" THEN GOTO 9900
4720 CALL "SYC.YN",1,"Print A List Of All Records",1,V$,V3
4730 ON V3 GOTO 4740,4700,4700,4700,9900
4740 ON POS(V$="YN") GOTO 4700,4750,9900
4790 RUN "SYM.FB"
4900 REM " --- Run ribbon overlay"
4910 LET O0=0
4990 RUN "DDM.BB",ERR=4000
5000 REM " --- Display record"
5010 PRINT @(14,3),A0$,@(14,4),A1$(1,30),@(14,5),A2$(1,30),@(14,6),A1$(38,1),@(70,3),A1$(37,1),@(70,4),STR(A[0]),@(70,5),STR(A[2]),@(70,6),STR(A[1]),@(70,7),FNB6$(A1$(31,6))
5090 RETURN 
6000 REM " --- File Name Lookup"
6010 LET CHANNEL=SYM03_DEV,RECORD$="",NUMBER=1,TITLE$="ADD+ON System Files"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET DESCRIPTION$[0]="File",DESCRIPTION$[1]="Description"
6040 LET FIELD[0]=1,POSITION[0]=1,LENGTH[0]=6
6050 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30
6060 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6070 LET V$="NO COMPANY",ROW=V2+2
6080 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8095 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

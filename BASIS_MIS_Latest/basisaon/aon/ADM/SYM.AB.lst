0010 REM "SYM - Menu Maintenance (Detail Lines)"
0020 REM "Program SYM.AB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0085 SETESC 9000
0090 SETERR 9000
0200 REM " --- IOLIST's"
0210 SYM11A: IOLIST W0$(1),W1$(1),W2$(1),W3$(1),W4$(1)
0500 REM " --- Initializations"
0510 DIM ID$[9],OPTLIST$[17,6]
0550 LET O0=1,L=L0,L9=20,COUNT=0,K9$="",OPTION=0,MAXOPT=15
0555 LET ID$[1]="(Up To) 6 Character Menu ID",ID$[2]="Program Name(s)"
0560 LET ID$[3]="GENERAL Report Name",ID$[4]="TAOS Procedure Name"
0565 LET ID$[5]="System Command",ID$[6]="Filler Text"
0570 LET ID$[7]="Lookup Method",ID$[8]="SpeedSearch Program Name"
0575 LET ID$[9]="Program Name"
0600 REM " --- Set option prompts and types based on menu type"
0605 ON POS(A2$(9,1)="ALST") GOTO 9900,0610,0630,0650,0650
0610 LET TASKTYPE$="MPRTYF",MAXOPT=17
0620 LET TASKPROMPT$="M=Menu   P=Program   R=GENERAL Report   T=TAOS Procedure"
0625 GOTO 0700
0630 LET TASKTYPE$="LF",TASKPROMPT$="L=Lookup Method     F=Filler"
0645 GOTO 0700
0650 LET TASKTYPE$="MSDCYF"
0660 LET TASKPROMPT$="M=Menu   S=Search   D=Do Now   C=Call Now   Y=System Command   F=Filler"
0665 GOTO 0700
0670 LET TASKTYPE$="PYF"
0680 LET TASKPROMPT$="P=Program     Y=System Command     F=Filler"
0700 REM " --- Background"
0710 PRINT @(0,L0),'CE',
0800 REM " --- Load existing options"
0810 READ (SYM11_DEV,KEY=A0$,DOM=0820)
0820 LET K$=KEY(SYM11_DEV,END=0900)
0830 IF POS(A0$=K$)<>1 THEN GOTO 0900
0840 READ (SYM11_DEV)IOL=SYM11A
0850 LET COUNT=COUNT+1,OPTLIST$[COUNT,1]=W1$,OPTLIST$[COUNT,2]=W3$
0860 LET OPTLIST$[COUNT,3]=W4$,OPTLIST$[COUNT,4]=W2$(1,1)
0870 LET OPTLIST$[COUNT,5]=W2$(2,1),OPTLIST$[COUNT,6]=W2$(3,6)
0880 IF FNP$(OPTLIST$[COUNT,5])="" THEN LET OPTLIST$[COUNT,5]="0"
0890 GOTO 0820
0900 REM " --- Display any existing lines"
0910 IF COUNT=0 THEN GOTO 1100
0920 GOSUB 5100
0930 LET OPTION=COUNT
0990 GOTO 4000
1000 REM " --- Overflow"
1010 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L0),'LD','LD',
1020 IF L>L9 THEN LET L=L9
1100 REM " --- Get next option"
1110 LET OPTION=OPTION+1
1120 IF OPTION>MAXOPT THEN GOTO 4000
1180 GOSUB 6900
1190 IF THERE THEN GOTO 1500
1400 REM " --- Option Number"
1410 LET V4$="Enter A Valid Sequence Number Between 1 And "+STR(MAXOPT)
1420 LET V0$="N",V1$="C",V2$=STR(OPTION),V3$="00",V0=2,V1=1,V2=L
1430 GOSUB 7000
1440 ON V3 GOTO 1460,1400,1400,1400,1450
1450 PRINT @(0,L),'CL',@(0,L+1),'CL',
1455 GOTO 3200
1460 IF INT(V)<>V THEN GOTO 1400
1465 IF V<1 OR V>MAXOPT THEN GOTO 1400
1470 LET OPTION=V
1475 GOSUB 6900
1480 IF THERE=0 THEN GOTO 1600
1490 IF OPTION<=K9 THEN GOSUB 5400
1500 REM " --- Display current line"
1510 GOSUB 5000
1520 LET I0=5
1530 GOTO 3300
1600 REM " --- New Record"
1610 DIM W1$(35),W2$(16),W3$(30),W4$(60)
1620 LET OPTLIST$[OPTION,1]=W1$,OPTLIST$[OPTION,2]=W3$,OPTLIST$[OPTION,3]=W4$
1630 LET OPTLIST$[OPTION,4]=W2$(1,1),OPTLIST$[OPTION,5]="0"
1640 LET OPTLIST$[OPTION,6]=W2$(3,6),COUNT=MAX(OPTION,COUNT)
1690 PRINT @(4,L),'CL',@(4,L+1),'CL',
2000 REM " --- Task Description"
2010 LET V4$="Enter The Description For This Task"
2020 LET V0$="S",V1$="C",V2$=OPTLIST$[OPTION,1],V3$="",V0=35,V1=4,V2=L,I0=1
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3200,2000,3200
2050 LET OPTLIST$[OPTION,1]=FNW$(V$,V0)
2090 GOTO 3200
2100 REM " --- Task Type"
2110 LET V0$="S",V1$="C",V2$=OPTLIST$[OPTION,4],V4$=TASKPROMPT$
2120 LET V3$=TASKTYPE$,V0=1,V1=40,V2=L
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3200,2100,3200
2150 LET OPTLIST$[OPTION,4]=V$
2190 GOTO 3200
2200 REM " --- Program/Menu ID"
2210 LET V4$="Enter The "+ID$[POS(OPTLIST$[OPTION,4]="MPRTYFLSD")]+" For This Task"
2220 LET V0$="S",V1$="C",V2$=OPTLIST$[OPTION,2],V3$="",V0=30,V1=45,V2=L,V$=""
2225 IF OPTLIST$[OPTION,4]="F" THEN GOTO 2250
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3200,2200,3200
2250 LET OPTLIST$[OPTION,2]=FNW$(V$,V0)
2260 PRINT @(V1,V2),OPTLIST$[OPTION,2],
2290 GOTO 3200
2300 REM " --- Parameters"
2310 LET V4$="Enter Any Optional Parameters For This Task",V$=""
2320 LET V0$="S",V1$="C",V2$=OPTLIST$[OPTION,3],V3$="",V0=60,V1=4,V2=L+1
2325 IF OPTLIST$[OPTION,4]="F" THEN GOTO 2350
2330 GOSUB 7000
2340 ON V3 GOTO 2350,2300,3200,2300,3200
2350 LET OPTLIST$[OPTION,3]=FNW$(V$,V0)
2360 PRINT @(V1,V2),OPTLIST$[OPTION,3],
2390 GOTO 3200
2400 REM " --- Security level"
2410 LET V4$="Enter The Required Security Level For This Task (0 Through 9)"
2420 LET V0$="Z",V1$="C",V2$=OPTLIST$[OPTION,5],V3$="",V0=1,V1=68,V2=L+1,V$=""
2425 IF OPTLIST$[OPTION,4]="F" THEN GOTO 2450
2430 GOSUB 7000
2440 ON V3 GOTO 2450,2400,3200,2400,3200
2450 IF V$="" THEN LET V$="0"
2460 LET OPTLIST$[OPTION,5]=V$
2470 PRINT @(V1,V2),V$,
2490 GOTO 3200
2500 REM " --- Password"
2510 LET V4$="Enter An Optional Password For This Task",V$=""
2520 LET V0$="S",V1$="C",V2$=OPTLIST$[OPTION,6],V3$="",V0=6,V1=71,V2=L+1
2525 IF OPTLIST$[OPTION,4]="F" THEN GOTO 2550
2530 GOSUB 7000
2540 ON V3 GOTO 2550,2500,3200,2500,3200
2550 LET OPTLIST$[OPTION,6]=FNW$(V$,V0)
2560 PRINT @(V1,V2),OPTLIST$[OPTION,6],
2590 GOTO 3200
3200 REM " --- Sequence Control"
3210 IF V3=4 THEN GOTO 3270
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,2000,2100,2200,2300,2400,2500,3270
3270 GOSUB 6900
3280 IF THERE=0 THEN GOTO 3500
3300 REM " --- Line Correct?"
3310 LET V4$="<Enter>=Correct   <F1>=Insert   <F2>=Change   <D>=Delete"
3320 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9=OPTION,I0=7
3325 IF OPTLIST$[OPTION,4]="F" THEN LET I0=3
3330 GOSUB 7000
3340 ON V3 GOTO 3350,3200,3200,3300,3350
3350 GOSUB 6900
3360 IF THERE=0 THEN GOTO 3500
3370 IF V$="" THEN GOTO 3400
3380 IF V$="D" THEN GOTO 3500
3390 GOTO 3300
3400 REM " --- Write"
3450 LET L=L+2,COUNT=MAX(OPTION,COUNT)
3460 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3500 REM " --- Remove"
3510 PRINT @(0,L),'CL',@(0,L+1),'CL',
3520 IF OPTION=COUNT THEN GOTO 3560
3530 FOR X=OPTION TO COUNT-1
3535 FOR W=1 TO 6
3540 LET OPTLIST$[X,W]=OPTLIST$[X+1,W]
3545 NEXT W
3550 NEXT X
3560 FOR X=1 TO 6
3565 LET OPTLIST$[COUNT,X]=""
3570 NEXT X
3575 LET OPTION=OPTION-1,COUNT=COUNT-1
3580 IF V3=4 THEN GOTO 4000
3590 GOTO 1000
3600 REM " --- Insert"
3610 PRINT @(0,L),'LI','LI',@(0,22),'CE',
3620 FOR X=MAXOPT TO OPTION+1 STEP -1
3625 FOR W=1 TO 6
3630 LET OPTLIST$[X,W]=OPTLIST$[X-1,W]
3635 NEXT W
3640 NEXT X
3645 FOR X=1 TO 6
3650 LET OPTLIST$[OPTION,X]=""
3655 NEXT X
3660 LET COUNT=COUNT+1
3665 IF COUNT>MAXOPT THEN LET COUNT=MAXOPT
3670 IF L<L9-2 THEN PRINT @(1,L+2),OPTION+1:"00",
3680 PRINT @(1,L),OPTION:"00",
3690 GOTO 1600
4000 REM " --- Options"
4010 LET V4$="Is The Above Information Correct (Y/N/List/Review/Delete)?"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4600
4050 IF V$="L" OR V$="LIST" THEN GOTO 4800
4070 IF V$="R" OR V$="REVIEW" THEN GOTO 4900
4080 IF V$="DELETE" THEN GOTO 4700
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Update"
4110 GOSUB 6000
4115 LET COUNT=0
4120 FOR OPTION=1 TO MAXOPT
4130 GOSUB 6900
4140 IF THERE=0 THEN GOTO 4190
4150 DIM W0$(8),W1$(35),W2$(16),W3$(30),W4$(60)
4160 LET COUNT=COUNT+1,W0$(1)=A0$+STR(COUNT:"00"),W1$(1)=OPTLIST$[OPTION,1]
4165 LET W2$(1)=OPTLIST$[OPTION,4],W2$(2)=OPTLIST$[OPTION,5]
4170 LET W2$(3)=OPTLIST$[OPTION,6],W3$(1)=OPTLIST$[OPTION,2]
4175 LET W4$(1)=OPTLIST$[OPTION,3]
4180 WRITE (SYM11_DEV,KEY=W0$)IOL=SYM11A
4190 NEXT OPTION
4600 REM " --- Run Header Overlay"
4690 RUN "SYM.AA"
4700 REM " --- Remove"
4710 READ (SYM11_DEV,KEY=A0$,DOM=4720)
4720 LET K$=KEY(SYM11_DEV,END=4760)
4730 IF POS(A0$=K$)<>1 THEN GOTO 4760
4740 REMOVE (SYM11_DEV,KEY=K$,DOM=4750)
4750 GOTO 4720
4760 REMOVE (SYM01_DEV,KEY=A0$,DOM=4770)
4790 RUN "SYM.AA"
4800 REM " --- List"
4810 GOSUB 5100
4890 GOTO 4000
4900 REM " --- Review"
4910 LET OPTION=0
4920 GOSUB 5400
4990 GOTO 1000
5000 REM " --- Display"
5010 PRINT @(0,L),'CL',@(1),OPTION:"00",@(4),OPTLIST$[OPTION,1],@(40),OPTLIST$[OPTION,4],@(45),OPTLIST$[OPTION,2],
5020 PRINT @(0,L+1),'CL',@(4),OPTLIST$[OPTION,3],@(68),OPTLIST$[OPTION,5],@(71),OPTLIST$[OPTION,6],
5090 RETURN 
5100 REM " --- List"
5110 GOSUB 5400
5120 FOR OPTION=1 TO COUNT
5130 IF L<=L9 THEN GOTO 5200
5140 LET V4$="<Enter>=Continue:"
5150 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
5160 GOSUB 7000
5170 IF V3=4 THEN EXITTO 5290
5180 GOSUB 5400
5200 GOSUB 5000
5210 LET L=L+2,K9=OPTION
5220 NEXT OPTION
5290 RETURN 
5400 REM " --- Clear Screen"
5410 PRINT @(0,L0),'CE',
5420 LET L=L0
5490 RETURN 
6000 REM " --- Delete all lines"
6010 FOR X=1 TO MAXOPT
6020 LET W0$(1)=A0$+STR(X:"00")
6030 REMOVE (SYM11_DEV,KEY=W0$,DOM=6040)
6040 NEXT X
6090 RETURN 
6900 REM " --- Does option line exist?"
6910 LET THERE=0
6920 FOR W=1 TO 4
6930 IF FNP$(OPTLIST$[OPTION,W])<>"" THEN LET THERE=1
6940 NEXT W
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8090 DEF FNW$(Q$,Q)=Q$+FILL(Q-LEN(Q$))
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

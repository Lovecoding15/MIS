0010 REM "SYS - File Mapping"
0020 REM "Program SYS.BA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0105 LET FILES=3
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYM-03",FILES$[2]="SYM-04"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYM03_DEV=CHANNELS[1],SYM04_DEV=CHANNELS[2]
0200 REM " --- IOLIST's"
0210 SYM03A: IOLIST A0$(1),A1$(1),A2$(1),A[ALL]
0220 SYM04A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1),B[ALL]
0500 REM " --- Initialization"
0510 DIM A0$(6),A1$(64),A2$(30),A[2],B0$(2),B1$(64),B2$(50),B3$(50),B[2]
0520 DIM HELP$(30),MESSAGE$[1]
0550 LET L0=7,L9=22,M1$="######"
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,6,0
0730 PRINT 'SB',@(0,3),"System:",@(0,5),"File",@(7),"Description",@(38),"Size Information",@(69),"Records",'SF',
0900 REM " --- Position File"
0990 READ (SYM04_DEV,KEY="00",DOM=1000)
1000 REM " --- System"
1010 PRINT 'CF',
1020 LET V2$="",V2$=KEY(SYM04_DEV,END=1050)
1050 LET V0$="S",V1$="CK",V3$="",V0=2,V1=8,V2=3,I0=0
1060 LET V4$="Enter a Valid System ID (<F3>=Lookup)"
1070 GOSUB 7000
1080 ON V3 GOTO 1100,1000,1000,1090,9900
1090 CALL "SYC.LM",V$
1100 REM " --- Retrieve record"
1110 LET B0$(1)=V$
1120 FIND (SYM04_DEV,KEY=B0$,DOM=1000)IOL=SYM04A
1130 IF B1$(39,1)="Y" THEN GOTO 1900
1140 LET MESSAGE$[0]=FNP$(B1$(1,35))+" is not installed (<Enter>=Continue)"
1150 CALL "SYC.XA",2,MESSAGE$[ALL],0,-1,-1,V$,V3
1190 IF V$<>"SPECIAL" THEN GOTO 1000
1900 REM " --- Position SYM-03"
1910 LET L=L0-1
1920 PRINT @(V1,V2),B0$," ",B1$(1,35),'CL',
1990 READ (SYM03_DEV,KEY=B0$,DOM=2000)
2000 REM " --- Next file"
2010 LET K$=KEY(SYM03_DEV,END=1000)
2020 IF POS(B0$=K$)<>1 THEN GOTO 1000
2030 READ (SYM03_DEV)IOL=SYM03A
2040 IF POS(A1$(37,1)="DIS")>0 THEN IF A[1]=0 THEN LET A[1]=100
2050 IF A1$(37,1)="M" THEN LET A[1]=0
2060 GOSUB 5000
2070 IF A1$(37,1)="R" THEN GOTO 2000
2080 IF A1$(38,1)="Y" THEN GOTO 2000
2090 GOTO 4000
3000 REM " --- Number of records"
3005 IF POS(A1$(37,1)="DIS")=0 THEN GOTO 4000
3010 LET V0$="N",V1$="E",V2$=STR(A[1]),V3$=M1$,V1=70,V2=L
3020 LET V4$="Enter the number of records to be contained in this file"
3030 GOSUB 7000
3040 ON V3 GOTO 3050,3000,3000,3000,1000
3050 IF V<0 THEN GOTO 3000
3060 LET A[1]=V
3070 PRINT @(V1,V2),A[1]:M1$
3090 GOTO 4000
4000 REM " --- Correct?"
4010 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L
4020 LET V4$="Define this file as shown (<F2>=Change/S=Skip/<Enter>=Define File)?"
4025 IF POS(A1$(37,1)="DIS")=0 THEN LET V4$="Define this file as shown (S=Skip/<Enter>=Define File)?"
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,3000,4000,1000
4050 IF V$="S" THEN GOTO 2000
4060 IF V$<>"" THEN GOTO 4000
4100 REM " --- Does file already exist?"
4110 IF POS(A1$(37,1)="DIS")>0 THEN IF A[1]=0 THEN GOTO 2000
4120 LET FILES$[3]=A0$,OPTIONS$[3]="F"
4130 CALL "SYC.DA",1,3,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
4140 IF STATUS>0 THEN GOTO 2000
4150 LET CHANNEL=CHANNELS[3]
4155 CLOSE (CHANNELS[3],ERR=4160)
4160 IF CHANNEL=0 THEN GOTO 4310
4170 LET V4$=$22$+A0$+$22$+" already exists. Delete and redefine"
4180 CALL "SYC.YN",1,V4$,1,V$,V3
4190 IF V$<>"Y" THEN GOTO 2000
4200 REM " --- Erase existing file(s)"
4210 ERASE A0$,ERR=4300
4290 GOTO 4200
4300 REM " --- Define file"
4310 CALL "SYC.MA",A1$(37,1),A0$,A0$(1,2),STR(A[0]),A[1],A[2],X
4390 GOTO 2000
5000 REM " --- Display"
5010 LET L=L+1
5020 IF L<L9 THEN GOTO 5050
5030 PRINT @(0,L0),'LD',
5040 LET L=L9-1
5050 LET HELP$(1)=A2$(1,30),X$=STR(A[1]:M1$)
5055 IF A1$(37,1)="M" THEN LET X$="Dynamic"
5060 IF A1$(38,1)="Y" THEN LET HELP$(1)="(Write Protected)"
5070 IF A1$(37,1)="R" THEN LET HELP$(1)="(Reference Only)",X$=""
5080 PRINT @(0,L),A0$,@(7),A1$(1,30),@(38),HELP$,@(70),X$
5090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "SYS - TAOS Front End Program"
0020 REM "Program SYS.RT"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=1
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01"
0140 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1]
0200 REM " --- IOLIST's"
0260 SYS01T: IOLIST TERMINAL$,F0$,F1$,F2$,F3$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0500 REM " --- Initializations"
0550 LET STATUS=0,APPLICATION$=FNP$(F1$),CONFIG$="",MISSING$=""
0800 REM " --- Save ADD+ON function key and terminator definitions"
0810 LET X$=STBL("!AEDIT",STBL("!EDIT",ERR=0820))
0820 LET X$=STBL("!ATERMS",STBL("!TERMS",ERR=0830))
0850 REM " --- Restore original BBx function key and terminator definitions"
0860 LET X$=STBL("!EDIT",STBL("!SEDIT",ERR=0870))
0870 LET X$=STBL("!TERMS",STBL("!STERMS",ERR=0880))
1000 REM " --- Call TAOS or a specific procedure?"
1010 IF APPLICATION$="" THEN GOTO 2000
1100 REM " --- User specified CONFIG.xxx file?"
1110 IF LEN(APPLICATION$)>3 THEN GOTO 1200
1120 LET CONFIG$=APPLICATION$,APPLICATION$=""
1200 REM " --- TAOS initializations"
1210 LET MISSING$="BBx Extended Utilities"
1220 IF CONFIG$="" THEN GOTO 1300
1230 CALL "_acu.utl",ERR=3000,CONFIG$
1290 GOTO 2000
1300 REM 
1310 CALL "_acu.utl",ERR=3000
1500 REM " --- Initialize ADDON$"
1510 DIM ADDON$(60)
1530 LET ADDON$(1,3)=F0$(4,3),ADDON$(4,2)=F0$(16,2)
1540 LET ADDON$(6,6)=F0$(20,2)+F0$(22,2)+FNYY21_YY$(F0$(18,2))
1550 LET ADDON$(12,6)="6.0.0",ADDON$(12,6)=STBL("!VERSION",ERR=1560)
1560 LET ADDON$(31,30)=APPLICATION$
1600 REM " --- Call AONTAOS to invoke a specific procedure"
1610 LET MISSING$="(BBX) Global"
1620 LET PROCEDURE$=STBL("BBX",ERR=3000)+"AONTAOS.bbx"
1630 LET MISSING$="BBX Extended Utilities"
1640 CALL "_setpath.utl",ERR=3000,PROCEDURE$
1650 LET MISSING$="AONTAOS.bbx"
1660 CALL PROCEDURE$,ERR=3000,ADDON$,STATUS
1670 LET MISSING$=""
1690 GOTO 3000
2000 REM " --- Call TAOS"
2010 CALL "Taos",ERR=2100
2090 GOTO 3000
2100 REM " --- Missing TAOS"
2110 LET MISSING$="TAOS"
3000 REM " --- Restore ADD+ON function key and terminator definitions"
3010 LET X$=STBL("!EDIT",STBL("!AEDIT",ERR=3020))
3020 LET X$=STBL("!TERMS",STBL("!ATERMS",ERR=3030))
3030 LET X$=STBL("!CLEAR","!AEDIT",ERR=3040)
3040 LET X$=STBL("!CLEAR","!ATERMS",ERR=3050)
3900 REM " --- Missing TAOS"
3910 DIM MESSAGE$[1]
3920 LET MESSAGE$[0]=MISSING$+" can not be accessed (<Enter>=Continue)"
3930 IF MISSING$<>"" THEN CALL "SYC.XA",1,MESSAGE$[ALL],0,22,-1,V$,V3
4000 REM " --- All done"
4090 GOTO 9900
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

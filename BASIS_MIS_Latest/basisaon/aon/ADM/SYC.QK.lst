0010 REM "SYC - Speedsearch Calendar"
0020 REM "Program SYC.QK"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0200 REM " --- IOLIST's"
0260 SYS01T: IOLIST TERMINAL$,F0$
0400 REM " --- Parameters"
0410 DIM SPEED$(128)
0420 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=0430)
0430 LET N0$=SPEED$(1,2),SYS01_DEV=NUM(STBL("!SYS01"))
0440 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0500 REM " --- Initializations"
0550 LET YEAR=FNYY_YEAR(F0$(18,2)),YEAR=1900+YEAR
0560 LET MONTH=NUM(F0$(20,2)),TODAY=NUM(F0$(22,2)),WHEN=JUL(YEAR,MONTH,TODAY)
0570 LET TODAY=WHEN,DAYS$="SunMonTueWedThuFriSat"
0590 LET D_STATUS=0,WIDTH=78,HEIGHT=21,COLUMN=1,ROW=2,TITLE$=""
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,COLUMN,ROW,TITLE$,NAME$
0720 FOR X=10 TO 65 STEP 11
0730 CALL "SYC.WC",1,1,0,HEIGHT-2,X,0,0
0740 NEXT X
0750 CALL "SYC.WC",1,0,WIDTH-2,0,0,1,0
0760 FOR X=4 TO 16 STEP 3
0770 CALL "SYC.WC",1,0,WIDTH-2,0,0,X,0
0780 NEXT X
0790 PRINT @(22,HEIGHT-4),'CL',@(22,HEIGHT-3),'CL',@(23,HEIGHT-3),'SB',"PgUp=Prior  PgDn=Next  <Enter>=Change Month/Year",'SF',
1000 REM " --- Set window title"
1010 LET TITLE$=DATE(WHEN:"%Ml %Y")
1020 PRINT 'CF',@(9+INT((80-LEN(TITLE$))/2),17),TITLE$,
1400 REM " --- Days of week"
1410 LET COLUMN=0,ROW=0
1420 FOR X=1 TO 7
1430 PRINT @(COLUMN,ROW),DAYS$((X-1)*3+1,3),
1440 LET COLUMN=COLUMN+11
1450 NEXT X
1500 REM " --- Determine starting day of the week"
1510 DIM DAYS[42]
1520 LET FIRST=JUL(YEAR,MONTH,1),FIRST$=DATE(FIRST:"%Ds")
1530 LET FIRST=INT((POS(FIRST$=DAYS$,3)-1)/3)+1,D=0
1540 IF FIRST>7 THEN LET FIRST=1
1550 FOR X=FIRST TO 42
1560 LET D=D+1,J=JUL(YEAR,MONTH,D,ERR=1570),DAYS[X]=J
1590 NEXT X
1900 REM " --- Display dates on calendar"
1910 LET COLUMN=0,ROW=2
1920 FOR X=1 TO 42
1930 IF DAYS[X]=0 THEN GOTO 1970
1940 LET X$=DATE(DAYS[X]:"%Dz")
1950 IF DAYS[X]=TODAY THEN LET X$='BR'+X$+'ER',TCOL=COLUMN,TROW=ROW
1960 PRINT @(COLUMN,ROW),X$,
1970 LET COLUMN=COLUMN+11
1980 IF COLUMN>76 THEN LET COLUMN=0,ROW=ROW+3
1990 NEXT X
3000 REM " --- Request Input"
3010 LET V0$="S",V1$="",V2$="",V3$="",V4$="",V0=1,V1=72,V2=HEIGHT-3
3020 GOSUB 7000
3030 IF V3=-17 THEN GOTO 3200
3040 IF V3=-16 THEN GOTO 3400
3050 ON V3 GOTO 3070,3000,3000,3000,4000
3090 IF V$<>"" THEN GOTO 3000
3100 REM " --- Change month/year"
3110 LET OPTION$[1]=PGM(-2),OPTION$[3]=STR(YEAR:"0000")+STR(MONTH:"00")
3120 CALL "SYC.QG",D_STATUS,OPTION$[ALL]
3130 LET X$=OPTION$[3],YEAR=NUM(X$(1,4)),MONTH=NUM(X$(5,2))
3190 GOTO 3900
3200 REM " --- PgUp (Prior Month)
3210 LET MONTH=MONTH-1
3220 IF MONTH<1 THEN LET MONTH=12,YEAR=YEAR-1
3290 GOTO 3900
3400 REM " --- PgDn (Next Month)
3410 LET MONTH=MONTH+1
3420 IF MONTH>12 THEN LET MONTH=1,YEAR=YEAR+1
3900 REM " --- Loop back to redisplay"
3910 LET WHEN=JUL(YEAR,MONTH,1)
3990 GOTO 1000
4000 REM " --- End
4090 GOTO 9900
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8070 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 LET OPTION$[1]="PRIOR"
9920 CALL "SYC.WD",NAME$
9950 EXIT 
9999 END

0010 REM "SYC - System SpeedSearch (Environment)"
0020 REM "Program SYC.Q2"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0400 REM " --- Parameters"
0405 DIM X$(128)
0410 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2),USER$=X$(47,3)
0420 LET TNAME$=FNP$(X$(3,8)),NAME$=FNP$(X$(11,8)),ONAME$=FNP$(X$(27,8))
0430 LET WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2)),MAXOPT=NUM(X$(57,2))
0440 LET SNAME$=X$(19,8),SHEIGHT=NUM(X$(45,2))
0470 LET PAGES=INT((DSZ-5000)/4000),MAXPAGE=NUM(X$(66,2))
0480 IF PAGES<3 THEN GOTO 9900
0490 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0500 REM " --- Initializations"
0510 DIM OPTLIST$[2,MAXOPT,6],HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0540 LET D_STATUS=0,PGM$=PGM(-2),OPTION=0,MAXROW=HEIGHT-5
0570 LET FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End"
0580 LET HEADING$(2)="Environment Information"
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],X$,OPTROW,PREVMENU$,ONAME$
0700 REM " --- Background"
0710 CALL "SYC.WB",TNAME$,0
0720 PRINT 'CS',
0730 CALL "SYC.WB",NAME$,0
0740 PRINT 'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0750 CALL "SYC.WB",SNAME$,0
0760 PRINT 'CS',
1300 REM " --- Retrieve records for display"
1310 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,SHEIGHT-2]
1320 LET L=1,X0=0,X1=1,PAGE=1
1400 REM " --- Read next terminal"
1410 LET X$=INFO(4,X0,ERR=1900)
1500 REM " --- Display page?"
1510 IF L<MAXROW THEN GOTO 1600
1520 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
1530 GOSUB 5000
1540 IF DONE THEN GOTO 4000
1580 IF ANSWER$<>"" THEN GOTO 3000
1590 LET L=1,X1=1
1600 REM " --- Middle window record"
1610 DIM G$(WIDTH-2)
1680 LET G$(2)=X$,G$(WIDTH-2)="",LLIST$[PAGE,X1,1]=G$
1690 LET LLIST$[PAGE,X1,2]=STR(X0)
1700 REM " --- Bottom window record"
1710 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),G2$(1)="",X=0
1720 IF LEN(X$)>WIDTH-3 THEN LET X$=X$(WIDTH-3) ELSE LET X$=""
1730 WHILE X$<>""
1740 LET X=X+1
1750 IF X<4 THEN LET G$(2)=X$,G$(WIDTH-3)="",MORE$[PAGE,X1,X]=G$
1760 IF LEN(X$)>WIDTH-3 THEN LET X$=X$(WIDTH-3) ELSE LET X$=""
1780 WEND
1790 LET LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
1800 REM " --- Loop back for subdirectory"
1890 GOTO 1400
1900 REM " --- No more info"
1910 IF X0>0 THEN GOTO 2000
1915 CALL "SYC.WB",NAME$,0
1920 LET V0$="Sa",V1$="",V2$="",V3$="",V4$=""
1925 LET V0=1,V1=41,V2=INT(HEIGHT/2)-1
1930 PRINT @(1,V2),"No Information Found (<Enter>=Continue)"
1940 GOSUB 7000
1950 PRINT @(0,V2),'CL',
1960 IF V3=5 THEN LET OPTION$[1]="STARTOVER"; GOTO 4000
1970 IF V3=6 THEN LET OPTION$[1]="EXIT"; GOTO 4000
1990 GOTO 3000
2000 REM " --- Display last page"
2010 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2020 GOSUB 5000
2030 IF DONE THEN GOTO 4000
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$="",CHOICE2$=""
3030 GOSUB 5000
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OPTROW,WININFO[4]=SHEIGHT-2
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CF',
6090 RETURN 
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9950 EXIT 
9999 END

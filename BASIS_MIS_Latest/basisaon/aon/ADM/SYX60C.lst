0010 REM "SYX - v6.0 Administrator Upgrade (Conversion Overlay)"
0020 REM "Program SYX60C"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9900
0100 REM " --- Open/Lock Files"
0110 LET SYS01_DEV=UNT
0115 OPEN (SYS01_DEV,ERR=9750)"SYS-01"; REM "At this point SYS-01 is still v5.2
0120 LET SYM04_DEV=UNT
0125 OPEN (SYM04_DEV,ERR=9750)"SYM-04"
0200 REM " --- IOLIST's
0210 SYM02A: IOLIST A0$(1),A1$,A2$(1),A3$(1),A[ALL]
0220 SYM02B: IOLIST B0$(1),B1$(1),B[ALL]
0230 SYM04A: IOLIST B0$(1),B1$(1),B2$(1),B3$(1),B[ALL]
0240 SYM06A: IOLIST A0$(1),A1$(1)
0250 SYM07A: IOLIST B0$(1),B1$(1)
0260 SYM12A: IOLIST X0$(1)
0270 SYM17A: IOLIST C0$(1),C1$(1),C2$(1)
0280 SYM22A: IOLIST X0$(1)
0300 SYS01A: IOLIST A0$(1),A1$(1),A2$(1),A3$(1),A[ALL]
0310 SYS01C: IOLIST A0$(1),A1$(1)
0320 SYS01E: IOLIST A0$(1),A1$(1),A2$(1),A3$(1)
0330 SYS11A: IOLIST A0$(1),A1$(1),A2$[ALL],A3$(1),A4$
0340 SYS11B: IOLIST B0$(1),B1$(1),B2$(1)
0350 SYS21A: IOLIST C0$(1),C1$(1)
0500 REM " --- Initializations"
0590 LET DEVICE=0,MORE=1,PRINTERS$=""
0600 REM " --- Assemble list of valid printers"
0610 WHILE MORE
0620 LET X$=TSK(DEVICE,ERR=0680)
0630 LET DEVICE=DEVICE+1
0640 IF X$(1,1)<>$01$ THEN GOTO 0690
0660 LET PRINTERS$=PRINTERS$+X$(3,2)
0670 GOTO 0690
0680 LET MORE=0
0690 WEND
0695 LET PRINTERS$=PRINTERS$+"PF"
0700 REM " --- Background"
0710 PRINT @(0,3),'CE',
0720 CALL "SYC.WC",ERR=9800,0,0,59,4,11,4,0
0730 PRINT @(13,5)," This phase of the upgrade will convert your current"
0740 PRINT @(13,6),"Administrator data files to the new Version 6.0 format."
0750 PRINT @(9,9),"Your current user, help and system control files will be"
0760 PRINT @(9,10),"converted. Several files which are new at this version may"
0770 PRINT @(9,11),"be created by the standard error handler if they do not"
0780 PRINT @(9,12),"exist. Each of the new files will be cleared after they are"
0785 PRINT @(9,13),"created and opened."
0790 GOSUB 6900
0900 REM " --- System records"
0910 DIM A0$(6),A1$(64),A2$(40),A3$(100),A[2]
0920 DIM B0$(2),B1$(64),B2$(50),B3$(50),B[2]
0930 PRINT @(0,9),'CE',@(29,9),'SB',"Converting",'SF',
0970 LET X$="System Records",X=2
0980 GOSUB 5000
0990 READ (SYS01_DEV,KEY="00",DOM=1000)
1000 REM " --- Copy system records"
1010 LET K$=KEY(SYS01_DEV,END=1200)
1020 IF POS("00"=K$)<>1 THEN GOTO 1200
1030 READ (SYS01_DEV)IOL=SYS01A
1040 PRINT @(COLUMN,11),A0$(3,2),
1050 LET B0$(1)=A0$(3,2),B1$(1)=A1$,B2$(1)=A3$(1),B3$(1)=A3$(51)
1060 WRITE (SYM04_DEV,KEY=B0$)IOL=SYM04A
1070 REMOVE (SYS01_DEV,KEY=K$)
1090 GOTO 1000
1200 REM " --- Rename remaining v5.2 files so 6.0 versions can be created"
1210 LET NUMBER=2
1220 DIM FROM$[NUMBER],TMPFILE$[NUMBER],FLAG$[NUMBER]
1230 LET FROM$[1]="SYM-02",TMPFILE$[1]="SYM02.TMP"
1240 LET FROM$[2]="SYS-11",TMPFILE$[2]="SYS11.TMP"
1250 CALL "SYC.RN",FROM$[ALL],TMPFILE$[ALL],FLAG$[ALL],NUMBER,STATUS
1260 IF STATUS THEN GOTO 9700
1300 REM " --- Open remaining files. The error routine should be able"
1305 REM " --- to create any missing files now."
1310 LET SYM02TMP_DEV=UNT
1315 OPEN (SYM02TMP_DEV,ERR=9700)"SYM02.TMP"; REM "5.2 version of SYM-02
1320 LET SYS11TMP_DEV=UNT
1325 OPEN (SYS11TMP_DEV,ERR=9700)"SYS11.TMP"; REM "5.2 version of SYS-11
1330 LET SYM02_DEV=UNT
1335 OPEN (SYM02_DEV)"SYM-02"
1340 LET SYM06_DEV=UNT
1345 OPEN (SYM06_DEV)"SYM-06"
1350 LET SYM07_DEV=UNT
1355 OPEN (SYM07_DEV)"SYM-07"
1360 LET SYM12_DEV=UNT
1365 OPEN (SYM12_DEV)"SYM-12"
1370 LET SYM17_DEV=UNT
1375 OPEN (SYM17_DEV)"SYM-17"
1380 LET SYM22_DEV=UNT
1385 OPEN (SYM22_DEV)"SYM-22"
1390 LET SYM32_DEV=UNT
1395 OPEN (SYM32_DEV)"SYM-32"
1400 LET SYS11_DEV=UNT
1405 OPEN (SYS11_DEV)"SYS-11"
1410 LET SYS21_DEV=UNT
1415 OPEN (SYS21_DEV)"SYS-21"
1900 REM " --- Firm ID's"
1910 DIM A0$(2),A1$(40)
1970 LET X$="Firm ID's",X=2
1980 GOSUB 5000
2000 REM " --- Copy firm records"
2010 FOR X=1 TO 99
2020 LET A0$(1)=STR(X:"00")
2030 FIND (SYS01_DEV,KEY=A0$,DOM=2090)IOL=SYS01C
2040 PRINT @(COLUMN,11),A0$,
2050 WRITE (SYM06_DEV,KEY=A0$)IOL=SYM06A
2060 REMOVE (SYS01_DEV,KEY=A0$)
2090 NEXT X
2300 REM " --- Printers"
2310 DIM A0$(4),A1$(20),A2$(60),A3$(32),B0$(2),B1$(48),C0$(3),C1$(30),C2$(60)
2370 LET X$="Printers",X=2
2380 GOSUB 5000
2390 READ (SYS01_DEV,KEY="C",DOM=2400)
2400 REM " --- Copy printers"
2410 LET K$=KEY(SYS01_DEV,END=2600)
2420 IF POS("C"=K$)<>1 THEN GOTO 2600
2430 READ (SYS01_DEV)IOL=SYS01E
2440 IF A0$(4,1)<>" " THEN GOTO 2500
2450 LET B0$(1)=A0$(2,2),B1$(1,30)=A1$,B1$(31,1)=A3$(1,1)
2455 IF POS(A3$(1,1)="YN")=0 THEN LET B1$(31,1)="Y"
2460 PRINT @(COLUMN,11),B0$,
2470 WRITE (SYM07_DEV,KEY=B0$)IOL=SYM07A
2480 REMOVE (SYS01_DEV,KEY=K$)
2490 GOTO 2400
2500 REM " --- Font record"
2510 LET C0$(1)=A0$(2),C1$(1)=A1$,C2$(1)=A2$
2520 FIND (SYM07_DEV,KEY=A0$(2,2),DOM=2550)
2530 WRITE (SYM17_DEV,KEY=C0$)IOL=SYM17A
2550 REMOVE (SYS01_DEV,KEY=K$)
2590 GOTO 2400
2600 REM " --- Remove last SpeedSearch records"
2610 LET X$="Last SpeedSearch By Operator",X=3
2620 GOSUB 5000
2630 READ (SYS01_DEV,KEY="X",DOM=2700)
2700 REM " --- Remove SpeedSearch records"
2710 LET K$=KEY(SYS01_DEV,END=2800)
2720 IF POS("X"=K$)<>1 THEN GOTO 2800
2730 READ (SYS01_DEV)
2740 PRINT @(COLUMN,11),K$(2,3),
2750 REMOVE (SYS01_DEV,KEY=K$)
2790 GOTO 2700
2900 REM " --- Position v5.2 SYM-02 file"
2910 DIM A0$(30),A2$(24),A3$(64),A[4],B0$(3),B1$(128),B[4],X0$(5)
2970 LET X$="Operator ID's",X=3
2980 GOSUB 5000
2990 READ (SYM02TMP_DEV,KEY="A",DOM=3000)
2995 LET OPCNT=0
3000 REM " --- Next operator record"
3010 LET K$=KEY(SYM02TMP_DEV,END=3500)
3020 IF POS("A"=K$)<>1 THEN GOTO 3500
3030 READ (SYM02TMP_DEV)IOL=SYM02A
3040 IF LEN(K$)<>4 THEN GOTO 3400
3045 LET OPCNT=OPCNT+1
3050 PRINT @(COLUMN,11),OPCNT:"000"
3060 LET B0$(1)=A0$(2),B1$(1)=A0$(5),B1$(26)=A2$(10),B1$(41)=A3$,B[0]=A[0]
3070 LET B1$(34,3)="",B1$(104,6)=A2$(18,3),B1$(110,1)="N"
3080 IF B1$(25,1)="9" THEN LET B1$(110,1)="Y"
3090 WRITE (SYM02_DEV,KEY=B0$)IOL=SYM02B
3100 REM " --- Valid firms"
3110 IF FNP$(A1$)="" THEN GOTO 3200
3120 FOR X=1 TO LEN(A1$) STEP 2
3130 LET X0$(1)=B0$,X0$(4)=A1$(X,2)
3140 FIND (SYM06_DEV,KEY=A1$(X,2),DOM=3190)
3150 WRITE (SYM12_DEV,KEY=X0$)IOL=SYM12A
3190 NEXT X
3200 REM " --- Printers"
3210 FOR X=1 TO LEN(PRINTERS$) STEP 2
3220 LET X0$(1)=B0$,X0$(4)=PRINTERS$(X,2)
3230 IF PRINTERS$(X,2)<>"PF" THEN FIND (SYM07_DEV,KEY=PRINTERS$(X,2),DOM=3290)
3240 WRITE (SYM22_DEV,KEY=X0$)IOL=SYM22A
3290 NEXT X
3300 REM " --- Remove all PF files for operator"
3310 LET PFNAME$="PF"+FNP$(B1$(101,3))
3320 ERASE PFNAME$+"A",ERR=3340
3330 GOTO 3320
3340 FOR X=1 TO 99
3350 ERASE PFNAME$+STR(X),ERR=3370
3360 GOTO 3350
3370 NEXT X
3400 REM " --- Loop back for next record"
3490 GOTO 3000
3500 REM " --- Position v5.2 SYS-11 file"
3510 LET T0$="",LINES=9,SEQ=0
3520 DIM A0$(16),A1$(40),A2$[LINES](72),A3$(20)
3530 DIM B0$(14),B1$(40),B2$(16),C0$(17),C1$(70)
3570 LET X$="On-Line Help",X=16
3580 GOSUB 5000
3590 READ (SYS11TMP_DEV,KNUM=0,KEY="",DOM=3600)
3600 REM " --- Next v5.2 On-Line Help record"
3610 LET K$=KEY(SYS11TMP_DEV,END=3950)
3620 READ (SYS11TMP_DEV,KEY=K$)IOL=SYS11A
3640 PRINT @(COLUMN,11),A0$(1,2)," ",A0$(3,8)," ",A0$(11,4),
3660 IF B0$=A0$(1,14) THEN GOTO 3800
3680 GOSUB 6000
3700 REM " --- Write v6.0 SYS-11 On-Line Help Master"
3720 LET SEQ=0
3740 LET B0$(1)=A0$(1,14),B1$(1)=A1$,B2$(1)=FNYY_YY21$(DATE(0:"%Yz%Mz%Dz"))
3760 WRITE (SYS11_DEV,KEY=B0$)IOL=SYS11B
3800 REM " --- Write v6.0 SYS-21 On-Line Help Detail"
3810 FOR X=1 TO LINES
3820 LET SEQ=SEQ+1
3840 LET C0$(1)=B0$+STR(SEQ:"000"),C1$(1)=A2$[X]
3860 WRITE (SYS21_DEV,KEY=C0$)IOL=SYS21A
3890 NEXT X
3900 REM " --- Loop back for next record"
3910 GOTO 3600
3950 REM " --- Finish processing last v5.2 help record"
3960 GOSUB 6000
4000 REM " --- All done"
4010 CLOSE (SYM02TMP_DEV,ERR=4020); LET SYM02TMP_DEV=0
4020 ERASE "SYM02.TMP",ERR=4040
4030 GOTO 4020
4040 CLOSE (SYS11TMP_DEV,ERR=4050); LET SYS11TMP_DEV=0
4050 ERASE "SYS11.TMP",ERR=4070
4060 GOTO 4050
4090 RUN "SYX60D"
5000 REM " --- Update screen"
5010 PRINT @(40,9),X$,'CE',
5020 CALL "SYC.NB","Processing",X,COLUMN
5090 RETURN 
6000 REM " --- Remove trailing blank help records"
6010 IF FNP$(B0$)="" THEN GOTO 6090
6020 LET C0$(1)=B0$+STR(SEQ:"000")
6030 READ (SYS21_DEV,KEY=C0$,DOM=6070)IOL=SYS21A
6040 IF FNP$(C1$)<>"" THEN GOTO 6090
6050 REMOVE (SYS21_DEV,KEY=C0$)
6070 LET SEQ=SEQ-1
6080 IF SEQ>0 THEN GOTO 6020
6090 RETURN 
6900 REM " --- Proceed or quit?"
6910 PRINT @(0,22),'CL',@(14,22),"Press <Enter> to continue or <F4> to exit:",'CI',
6920 LET V0$="S",V1$="CF",V2$="",V3$="",V4$="",V0=1,V1=57,V2=22
6930 GOSUB 7000
6940 ON V3 GOTO 6950,6900,6900,6900,9800
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8050 DEF FNE$(Q$)=DATE(0:"%Hz%mZ")
8080 DEF FNP$(Q$)=CVS(Q$,2)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
8120 REM " --- FNYY_YY21$ Convert 2-Char Year to 21st Century 2-Char Year"
8125 DEF FNYY_YY21$(Q1$)
8130 LET Q3$=" ABCDE56789ABCDEFGHIJ",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8135 RETURN Q1$
8140 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9800
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9700 REM " --- Error renaming v5.2 files"
9710 PRINT @(0,3),'CE',@(1,4),"ERROR: Unable to rename 5.2 version of ADD+ON Operator Master File"
9720 PRINT @(1,5),"       (SYM-02) or the ADD+ON On-Line Help File (SYS-11)."
9740 GOTO 9800
9750 REM " --- Missing SYS-01 or SYM-04"
9760 PRINT @(0,3),'CE',@(1,4),"ERROR: Unable to locate or open the ADD+ON System Control File (SYS-01)"
9770 PRINT @(1,5),"       or the ADD+ON System Master File (SYM-04)."
9800 REM " --- Distribution installation and/or rename problem"
9810 PRINT @(0,10),'CE',
9820 CALL "SYC.WC",ERR=9830,0,0,80,9,0,10,0
9830 PRINT @(2,11),"If you are upgrading to ADD+ON Software Version 6.0:"
9840 PRINT @(2,13),"Restore your original version 5.2 Administrator data files, reload the"
9845 PRINT @(2,14),"version 6.0 Administrator media and try the installation procedure again."
9850 PRINT @(2,16),"If problems persist:"
9860 PRINT @(2,17),"Call ADD+ON Software Support at (800) 275-6350"
9870 LET V4$="Press <Enter> to continue:"
9880 LET V0$="S",V1$="CF",V2$="",V3$="",V0=1,V1=FNV(V4$),V2=22
9890 GOSUB 7000
9900 REM " --- Abort installation"
9910 PRINT @(1,20),'CE','SF',"ADD+ON Software Installation Terminated."
9950 RELEASE 
9999 END

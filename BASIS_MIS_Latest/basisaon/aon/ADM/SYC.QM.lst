0010 REM "SYC - System SpeedSearch (Users)"
0020 REM "Program SYC.QM"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS  : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n]: Standard SpeedSearch Options Array"
0055 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL]
0100 REM " --- Open Files"
0110 LET FILES=3
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYM-02",FILES$[2]="SYM-07",FILES$[3]="SYM-17"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYM02_DEV=CHANNELS[1],SYM07_DEV=CHANNELS[2],SYM17_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 SYM02A: IOLIST A0$(1),A1$(1)
0230 SYM01A: IOLIST B0$(1),B1$(1)
0240 SYM07A: IOLIST C0$(1),C1$(1)
0250 SYM17A: IOLIST D0$(1),D1$(1)
0260 SYS01T: IOLIST TERMINAL$,F0$(1),F1$,F2$,F3$,F4$,F5$
0400 REM " --- Parameters"
0405 DIM X$(128)
0410 LET X$(1)=STBL("!SPEEDSEARCH",ERR=9900),N0$=X$(1,2),USER$=X$(47,3)
0420 LET TNAME$=FNP$(X$(3,8)),NAME$=FNP$(X$(11,8)),ONAME$=FNP$(X$(27,8))
0430 LET WIDTH=NUM(X$(39,2)),HEIGHT=NUM(X$(41,2)),MAXOPT=NUM(X$(57,2))
0440 LET SNAME$=X$(19,8),SYS01_DEV=NUM(STBL("!SYS01"))
0445 LET SYM01_DEV=NUM(STBL("!SYM01")),SHEIGHT=NUM(X$(45,2))
0450 LET PARAM$=FNU$(FNP$(OPTION$[2])),SUBPARAM$="",P=POS("/"=PARAM$)
0460 IF P>0 THEN LET SUBPARAM$=PARAM$(P+1),PARAM$=PARAM$(1,P-1)
0470 LET PAGES=INT((DSZ-5000)/4000),MAXPAGE=NUM(X$(66,2))
0480 IF PAGES<3 THEN GOTO 9900
0490 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0500 REM " --- Initializations"
0510 DIM OPTLIST$[2,MAXOPT,6],HEADING$(WIDTH-2),FOOTING$(WIDTH-2),PRINTER$(6)
0520 DIM A0$(3),A1$(24),B0$(6),B1$(35),F0$(25),C0$(2),C1$(30),D0$(3),D1$(30)
0540 LET D_STATUS=0,PGM$=PGM(-2),OPTION=0,NF$="(Not On File)"
0550 LET FOOTING$(2)="User Information",TERMINAL$=OPTION$[3]
0560 IF TERMINAL$="" OR TERMINAL$(1,1)<>"T" THEN LET TERMINAL$="T"
0570 LET FOOTING$(WIDTH-26)="PgUp  PgDn  F4=End",HEADING$(2)="ID   User"
0580 LET HEADING$(33)="Firm",MAXROW=HEIGHT-5
0600 REM " --- Option Window"
0610 CALL "SYC.QC",SYM01_DEV,SYM11_DEV,PGM$,SECURITY$,OPTION,OPTLIST$[ALL],X$,OPTROW,PREVMENU$,ONAME$
0700 REM " --- Background"
0710 CALL "SYC.WB",NAME$,0
0740 PRINT 'CS','SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0900 REM " --- Position file"
0910 CALL "SYC.WB",SNAME$,0
0920 PRINT 'CF',
0930 CALL "SYC.WB",TNAME$,0
0940 PRINT 'CS',
1000 REM " --- Process key field"
1010 READ (SYS01_DEV,KEY="T",DOM=1020)
1020 LET K$=KEY(SYS01_DEV,END=1900)
1030 IF POS("T"=K$)<>1 THEN GOTO 1900
1090 LET TERMINAL$=K$
1100 REM " --- Store starting terminal ID"
1110 DIM X$(WIDTH-2)
1130 LET X1$="",X2$=PARAM$
1140 CALL "SYC.QB",X$,X1$,X2$
1300 REM " --- Retrieve records for display"
1310 DIM LLIST$[PAGES,MAXROW,5],MORE$[PAGES,MAXROW,SHEIGHT-2]
1320 LET L=1,X0=0,X1=1,PAGE=1
1400 REM " --- Read next terminal"
1410 LET K$=KEY(SYS01_DEV,END=1900)
1420 IF POS("T"=K$)<>1 THEN GOTO 1900
1430 READ (SYS01_DEV,KEY=K$)IOL=SYS01T
1440 LET A0$(1)=F0$(4,3),A1$(1)=NF$,B0$(1)=F2$,B1$(1)="",PRINTER$(1)=F0$(13,2)
1445 LET C0$(1)=F0$(13,2),C1$(1)="",D0$(1)=C0$+F0$(25),D1$(1)=""
1450 IF FNP$(F0$(25,1))<>"" THEN LET PRINTER$(3)="-"+F0$(25,1)
1455 IF FNP$(A0$)="" THEN GOTO 1800
1460 FIND (SYM02_DEV,KEY=A0$,DOM=1470)IOL=SYM02A
1470 FIND (SYM01_DEV,KEY=B0$,DOM=1480)IOL=SYM01A
1480 FIND (SYM07_DEV,KEY=C0$,DOM=1490)IOL=SYM07A
1490 FIND (SYM17_DEV,KEY=D0$,DOM=1500)IOL=SYM17A
1500 REM " --- Display page?"
1510 IF L<MAXROW THEN GOTO 1600
1520 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
1530 GOSUB 5000
1540 IF DONE THEN GOTO 4000
1580 IF ANSWER$<>"" THEN GOTO 3000
1590 LET L=1,X1=1
1600 REM " --- Middle window record"
1610 DIM G$(WIDTH-2)
1620 LET G$(2)=TERMINAL$(2),G$(7)=A1$,G$(33)=F0$(16,2),G$(36)=F4$
1690 LET LLIST$[PAGE,X1,1]=G$,LLIST$[PAGE,X1,2]=TERMINAL$
1700 REM " --- Bottom window record"
1710 DIM G$(WIDTH-2)
1720 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2)
1730 LET G$(4)="{Menu}"+B0$+" "+B1$,MORE$[PAGE,X1,1]=G$
1740 LET G$(4)="{Task}"+FILL(7)+F5$,MORE$[PAGE,X1,2]=G$
1750 IF F0$(13,2)="PF" THEN LET C1$(1)="Print To File",D1$(1)=""
1760 LET FONT$=FNP$(C1$)
1770 IF FNP$(D1$)<>"" THEN LET FONT$=FONT$+"/"+D1$
1780 LET G$(1)="{Printer}"+PRINTER$+" "+FONT$,MORE$[PAGE,X1,3]=G$
1790 LET LLIST$[PAGE,X1,5]=SNAME$,X0=X0+1,X1=X1+1,L=L+1
1800 REM " --- Loop back for next terminal"
1890 GOTO 1400
1900 REM " --- No more terminals"
1910 IF X0>0 THEN GOTO 2000
1915 CALL "SYC.WB",NAME$,0
1920 LET V0$="Sa",V1$="",V2$="",V3$="",V4$=""
1925 LET V0=1,V1=35,V2=INT(HEIGHT/2)-1
1930 PRINT @(1,V2),"No Users Found (<Enter>=Continue)"
1940 GOSUB 7000
1950 PRINT @(0,V2),'CL',
1960 IF V3=5 THEN LET OPTION$[1]="STARTOVER"; GOTO 4000
1970 IF V3=6 THEN LET OPTION$[1]="EXIT"; GOTO 4000
1990 GOTO 3000
2000 REM " --- Display last page"
2010 LET ACTION$="M",CHOICE1$="",CHOICE2$=""
2020 GOSUB 5000
2030 IF DONE THEN GOTO 4000
3000 REM " --- Process option window"
3010 DIM MORE$[1,MAXOPT,1],LLIST$[0,0,0]
3020 LET ACTION$="O",CHOICE1$="",CHOICE2$=""
3030 GOSUB 5000
4000 REM " --- All done"
4010 GOSUB 6000
4090 GOTO 9900
5000 REM " --- Standard window processing"
5010 DIM WININFO$[10],WININFO[10]
5020 LET WININFO$[1]=CHOICE1$,WININFO$[2]=CHOICE2$
5030 LET WININFO[1]=PAGE,WININFO[2]=PAGES,WININFO[3]=OPTROW,WININFO[4]=SHEIGHT-2
5050 CALL "SYC.QW",ACTION$,OPTION$[ALL],OPTLIST$[ALL],LLIST$[ALL],MORE$[ALL],WININFO$[ALL],WININFO[ALL],FKEY
5060 LET ANSWER$=WININFO$[0],CONTROL=WININFO[0],PAGE=WININFO[1]
5080 LET DONE=OPTION$[1]="EXIT" OR OPTION$[1]="STARTOVER" OR OPTION$[1]="PRIOR"
5090 RETURN 
6000 REM " --- Clear all windows"
6010 CALL "SYC.WB",TNAME$,0
6020 PRINT 'CF',
6030 CALL "SYC.WB",NAME$,0
6040 PRINT 'CF',
6050 CALL "SYC.WB",SNAME$,0
6060 PRINT 'CF',
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNU$(Q$)=CVS(Q$,4)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to caller"
9910 FOR X=1 TO FILES
9920 CLOSE (CHANNELS[X],ERR=9921)
9930 NEXT X
9950 EXIT 
9999 END

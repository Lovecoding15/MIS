0010 REM "SYM - SpeedInstall Record Maintenance"
0020 REM "Program SYM.JA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=5
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYM-03",FILES$[2]="SYM-04",FILES$[3]="SYM-99",FILES$[4]="SYS-01"
0135 LET FILES$[5]="DDM-03"
0140 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYM03_DEV=CHANNELS[1],SYM04_DEV=CHANNELS[2],SYM99_DEV=CHANNELS[3],SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLIST's"
0210 DDM03A: IOLIST C0$,C1$(1),C[0],C[1],C[2]
0220 SYM03A: IOLIST B0$,B1$(1),B2$,B[0],B[1],B[2]
0230 SYM99A: IOLIST A0$(1),A1$(1),A2$,A[0],A[1],A[2],A[3],A[4]
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="SY",N4$=F5$
0430 DIM INFO$[20]
0440 CALL "SYC.VA","DD",INFO$[ALL]
0450 IF INFO$[3]<>"Y" THEN GOTO 9900
0500 REM " --- Initializations"
0520 DIM A0$(7),A1$(64),A[4],B1$(63),B[2],C1$(63),C[2]
0600 REM " --- Additional Open/Lock Files"
0610 CALL "SYC.DA",1,5,5,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS>0 THEN GOTO 9900
0630 LET DDM03_DEV=CHANNELS[5]
0700 REM " --- Background"
0750 PRINT 'SB',@(6,4),"File Name:",@(6,5),"Record ID:",@(2,6),"Use Record ID?",@(8,7),"Install?",@(0,8),"Required System:",'SF',
0900 REM " --- Position file"
0990 READ (SYM99_DEV,KEY="",DOM=1000)
1000 REM " --- File Name"
1005 PRINT 'CF',
1010 LET V0$="S",V1$="C",V2$="",V3$="",V0=6,V1=17,V2=4
1015 LET V4$="Enter File Name (<F3>=Lookup/<Enter>=Next Record)"
1020 GOSUB 7000
1025 ON V3 GOTO 1050,1000,1000,1030,4500
1030 GOSUB 6000
1050 IF V$="" THEN GOTO 1150
1060 LET A0$(1,6)=V$,A0$(7)=""
1070 READ (SYM03_DEV,KEY=A0$(1,6),DOM=1000)IOL=SYM03A
1080 PRINT @(V1,V2),A0$(1,6),@(V1+7,V2),B1$(1,30),
1100 READ (SYM99_DEV,KEY=A0$(1,6),DOM=1110)
1110 LET K$=KEY(SYM99_DEV,END=1140)
1120 IF K$(1,6)=A0$(1,6) THEN LET A0$(7,1)=K$(7,1)
1140 GOTO 1200
1150 REM " --- Get next record"
1160 LET K$=KEY(SYM99_DEV,END=0900)
1170 READ (SYM99_DEV)
1180 LET A0$=K$
1190 GOTO 1290
1200 REM " --- Record ID"
1210 LET V0$="S",V1$="C",V2$=A0$(7,1),V3$="",V0=1,V1=17,V2=5
1220 LET V4$="Enter File Record ID (<F3>=Lookup)"
1230 GOSUB 7000
1240 ON V3 GOTO 1260,1200,1000,1250,1000
1250 GOSUB 6100
1260 READ (DDM03_DEV,KEY=A0$(1,6)+V$,DOM=1200)IOL=DDM03A
1270 LET A0$(7,1)=V$,A1$(1)=C1$(1,30)
1280 PRINT @(V1,V2),A0$(7,1),@(V1+7,V2),A1$(1,30),
1290 READ (SYM99_DEV,KEY=A0$,DOM=1400)IOL=SYM99A
1300 REM " --- Display record"
1320 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record"
1405 PRINT @(17,6),'CL',@(17,7),'CL',@(17,8),'CL',
1410 LET V4$="Is This A New Record (Y/N)?"
1420 LET V0$="Y",V1$="C",V2$="N",V3$="",V0=1,V1=FNV(V4$),V2=22
1430 GOSUB 7000
1440 ON V3 GOTO 1490,1400,1400,1400,4500
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record"
1510 DIM A[4]
1520 LET A1$(31,1)="Y",A1$(32,1)="Y",A1$(33,2)=A0$(1,2)
1550 GOSUB 5000
2000 REM " --- Use Record ID?"
2010 LET V0$="Y",V1$="C",V2$=A1$(31,1),V3$="",V0=1,V1=17,V2=6,I0=0
2020 LET V4$="Is Source Record Keyed By Firm ID Plus Record ID (Y/N)?"
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,1200,2000,3900
2050 LET A1$(31,1)=V$
2090 GOTO 3900
2100 REM " --- Install?"
2110 LET V0$="Y",V1$="C",V2$=A1$(32,1),V3$="",V0=1,V1=17,V2=7
2120 LET V4$="Install This File/Record (Y/N)?"
2130 GOSUB 7000
2140 ON V3 GOTO 2150,2100,3900,2100,3900
2150 LET A1$(32,1)=V$
2190 GOTO 3900
2200 REM " --- Required System"
2210 LET V0$="S",V1$="CR",V2$=A1$(33,2),V3$="",V0=2,V1=17,V2=8
2220 LET V4$="Enter ADD+ON System Required By This File (<F3>=Lookup)"
2230 GOSUB 7000
2240 ON V3 GOTO 2260,2200,3900,2250,3900
2250 CALL "SYC.LM",V$
2260 IF LEN(V$)<2 THEN GOTO 2200
2270 READ (SYM04_DEV,KEY=V$,DOM=2200)
2275 LET A1$(33,2)=V$
2280 PRINT @(V1,V2),A1$(33,2),
2290 GOTO 3900
3900 REM " --- Next field"
3915 IF V3=4 THEN GOTO 4000
3920 IF V3<>2 THEN GOTO 3950
3930 IF I0>0 THEN LET I0=I0-1
3940 GOTO 3960
3950 LET I0=I0+1
3960 ON I0 GOTO 2000,2100,2200,3990
3990 LET I0=0
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Information Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=22,V1=FNV(V4$)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4500
4060 IF V$="DELETE" THEN GOTO 4200
4090 ON POS(V$="YN") GOTO 4000,4100,2000
4100 REM " --- Write record"
4120 WRITE (SYM99_DEV,KEY=A0$)IOL=SYM99A
4190 GOTO 1000
4200 REM " --- Remove record"
4220 REMOVE (SYM99_DEV,KEY=A0$,DOM=4230)
4290 GOTO 1000
4500 REM " --- Print?"
4510 LET V4$="Print A List Of All Records (Y/N)?"
4520 LET V0$="Y",V1$="C",V2$="N",V3$="",V0=1,V1=FNV(V4$),V2=22
4530 GOSUB 7000
4540 ON V3 GOTO 4550,4500,4500,4500,9900
4590 ON POS(V$="YN") GOTO 4500,4900,9900
4900 REM " --- Report Initializations"
4905 LET CHANNEL=SYM99_DEV,NUMBER=5,RECORD$=""
4910 LET PROGRAM$="",TOTAL$="SpeedInstall Files/Records",HEADINGS=3,WIDTH=80
4915 DIM FIELD$[NUMBER],FIELD[NUMBER],POSITION[NUMBER]
4920 DIM LENGTH[NUMBER],TYPE$[NUMBER],MASK$[NUMBER]
4925 DIM HEADINGS$[HEADINGS]
4930 LET FIELD$[0]="File",FIELD[0]=1,POSITION[0]=1
4935 LET LENGTH[0]=6,TYPE$[0]="S",MASK$[0]=""
4940 LET FIELD$[1]="Record",FIELD[1]=1,POSITION[1]=7
4945 LET LENGTH[1]=7,TYPE$[1]="S",MASK$[1]=""
4950 LET FIELD$[2]="Description",FIELD[2]=2,POSITION[2]=1
4955 LET LENGTH[2]=30,TYPE$[2]="S",MASK$[2]=""
4960 LET FIELD$[3]="Use ID?",FIELD[3]=2,POSITION[3]=31
4965 LET LENGTH[3]=1,TYPE$[3]="S",MASK$[3]=""
4970 LET FIELD$[4]="Install?",FIELD[4]=2,POSITION[4]=32
4975 LET LENGTH[4]=1,TYPE$[4]="S",MASK$[4]=""
4980 LET FIELD$[5]="System",FIELD[5]=2,POSITION[5]=33
4985 LET LENGTH[5]=2,TYPE$[5]="S",MASK$[5]=""
4990 LET HEADINGS$[0]=N1$,HEADINGS$[1]=TOTAL$+" Listing",N0$=""
4995 RUN "SYR.AA"
5000 REM " --- Display record"
5010 DIM B1$(63)
5020 LET B1$(1)="Not On File"
5030 READ (SYM03_DEV,KEY=A0$(1,6),DOM=5040)IOL=SYM03A
5080 PRINT @(17,4),A0$(1,6),@(24,4),B1$(1,30),@(17,5),A0$(7,1),@(24,5),A1$(1,30),@(17,6),A1$(31,1),@(17,7),A1$(32,1),@(17,8),A1$(33,2),
5090 RETURN 
6000 REM " --- File Name Lookup"
6010 LET RECORD$="",NUMBER=1,TITLE$="ADD+ON System Files"
6020 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6030 LET FIELD[0]=1,POSITION[0]=1,LENGTH[0]=6,CHANNEL=SYM03_DEV
6040 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30,DESCRIPTION$[0]="File"
6090 GOTO 6200
6100 REM " --- Record ID Lookup"
6110 LET RECORD$=A0$(1,6),NUMBER=1,TITLE$=A0$(1,6)+" Record ID's"
6120 DIM DESCRIPTION$[NUMBER],FIELD[NUMBER],POSITION[NUMBER],LENGTH[NUMBER]
6130 LET FIELD[0]=1,POSITION[0]=7,LENGTH[0]=1,CHANNEL=DDM03_DEV
6140 LET FIELD[1]=2,POSITION[1]=1,LENGTH[1]=30,DESCRIPTION$[0]="ID"
6200 REM " --- Call windowed lookup"
6210 LET COLUMN=79-LENGTH[0]-LENGTH[1]-2-NUMBER*4
6220 LET V$="NO COMPANY",ROW=V2+2,DESCRIPTION$[1]="Description"
6230 CALL "SYC.LK",CHANNEL,RECORD$,NUMBER,TITLE$,DESCRIPTION$[ALL],FIELD[ALL],POSITION[ALL],LENGTH[ALL],COLUMN,ROW,V$
6290 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9210 SETERR 9000
9220 GOTO 0900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9700 REM " -- Data Dictionary Missing"
9710 LET INDEX=1
9720 DIM MESSAGE$[INDEX]
9730 LET MESSAGE$[0]="Data Dictionary must be installed to run this task."
9740 LET MESSAGE$[1]="               <Enter> to Continue."
9750 CALL "SYC.XA",2,MESSAGE$[ALL],INDEX,22,-1,V$,V3
9790 GOTO 9900
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

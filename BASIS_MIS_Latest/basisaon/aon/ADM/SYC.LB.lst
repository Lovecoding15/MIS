0010 REM "SYC - Bill of Materials Lookup"
0020 REM "Program SYC.LB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "SELECTION$: Item Number (returned)"
0050 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER SELECTION$
0100 REM " --- Open Files"
0110 LET FILES=9
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="IVM-01",FILES$[3]="IVM-02"
0140 LET FILES$[4]="IVM-03",FILES$[5]="IVM-03",FILES$[6]="IVM-14"
0150 LET FILES$[7]="SYM-06",FILES$[8]="BMM-01",FILES$[9]="APM-04"
0160 CALL "SYC.DA",1,1,8,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0170 IF STATUS>0 THEN GOTO 9900
0180 LET SYS01_DEV=CHANNELS[1],IVM01_DEV=CHANNELS[2],IVM02_DEV=CHANNELS[3]
0185 LET IVM03_DEV=CHANNELS[4],IVM03_DEV2=CHANNELS[5],IVM14_DEV=CHANNELS[6]
0190 LET SYM06_DEV=CHANNELS[7],BMM01_DEV=CHANNELS[8]
0200 REM " --- IOLIST's"
0210 IVM01A: IOLIST A0$(1),A1$(1)
0220 IVM02A: IOLIST B0$(1),B1$(1),B2$,B[ALL]
0230 BMM01A: IOLIST C0$(1),C1$(1),C2$(1),C[ALL]
0240 SYM06A: IOLIST X$,COMPANY$
0260 IVS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$,M4$,M5$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 APS01A: IOLIST X$,I1$,I2$,I3$,I4$,M0$,X$,X$,X$
0340 IVM14A: IOLIST S0$(1)
0400 REM " --- Parameters"
0405 DIM P[5],I[2],INFO$[20],SPEED$(128)
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0415 IF PAGES<3 THEN GOTO 9900
0420 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0425 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0430 LET N0$=F0$(16,2),SPEEDSEARCH$="N",AP$="N"
0435 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=0445)
0440 LET N0$=SPEED$(1,2),SPEEDSEARCH$="Y"
0445 FIND (SYM06_DEV,KEY=N0$,DOM=9800)IOL=SYM06A
0450 FIND (SYS01_DEV,KEY=N0$+"IV00",DOM=9800)IOL=IVS01A
0455 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[3]=NUM(P2$(7,2))
0460 LET P[2]=NUM(P2$(9,2)),P[4]=NUM(P2$(11,2)),P[5]=NUM(P2$(13,2))
0470 CALL "SYC.VA","AP",INFO$[ALL]
0475 LET AP$=INFO$[20]
0480 IF AP$<>"Y" THEN GOTO 0500
0485 FIND (SYS01_DEV,KEY=N0$+"AP00",DOM=0500)IOL=APS01A
0490 LET I[0]=NUM(I2$(1,2)),I[1]=NUM(I2$(3,2)),I[2]=NUM(I3$(9,1)),AP$="Y"
0500 REM " --- Initializations"
0505 PRECISION NUM(P2$(5,1))
0510 DIM A0$(22),A1$(60),B0$(24),B1$(64),B[20],C0$(52),C1$(30),C2$(57),C[11]
0540 LET WIDTH=73,HEIGHT=12,WIN_X=1,WIN_Y=3,MAX_ROW=HEIGHT-5,M9$="###0.00%"
0550 LET SWIDTH=73,SHEIGHT=6,SWIN_X=WIN_X,SWIN_Y=WIN_Y+HEIGHT,M9=LEN(M9$)
0560 LET PGM$=PGM(-2),OPTIONS=0,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3,M8$="######0"
0570 CALL "SYC.RM",M2$,M2,M9,12
0580 LET STITLE$="Bill Of Materials Detail",W2=SWIDTH-M2-15,W1=24
0600 REM " --- Window arrays"
0610 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0620 LET HEADING$(2)="Number",HEADING$(3+P[0])="Name"
0630 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0640 LET TITLE$="Bill Of Materials Lookup"
0650 REM " --- Additional file opens"
0660 IF AP$="N" THEN GOTO 0700
0670 CALL "SYC.DA",1,9,9,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0675 IF STATUS>0 THEN GOTO 9900
0680 LET APM04_DEV=CHANNELS[9]
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Which search option?"
0810 DIM STRINGS$[10],NUMBERS[10]
0815 LET STRINGS$[0]=PGM$,STRINGS$[1]="",NUMBERS[0]=MAXROW
0820 LET NUMBERS[1]=OWIN_X,NUMBERS[2]=OWIN_Y,NUMBERS[3]=P[0]
0825 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
0830 CALL "SYC.SM",STRINGS$[ALL],NUMBERS[ALL],FKEY
0835 IF FKEY THEN GOTO 9900
0840 LET PREFIX$=STRINGS$[2],START$=STRINGS$[3],METHOD$=STRINGS$[4]
0845 LET V3$=STRINGS$[5],V0=NUMBERS[4],W=WIDTH-2-(P[0]+3)
0875 IF PREFIX$<>"D" THEN GOTO 0900
0880 IF AP$="Y" THEN GOTO 0900
0885 GOSUB 6500
0890 GOTO 0800
0900 REM " --- Detail Window"
0910 CALL "SYC.WA",0,SWIDTH,SHEIGHT,SWIN_X,SWIN_Y,STITLE$,SNAME$
0915 CALL "SYC.WB",NAME$,0
0920 LET HEADING$(W)=METHOD$
0925 PRINT @(0,0),'CF','SB','BR',HEADING$,'ER','SF',
0930 IF SPEEDSEARCH$<>"Y" THEN GOTO 1000
0950 REM " --- Display SpeedSearch Company"
0955 DIM X$(WIDTH+SWIDTH)
0960 LET X$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
0965 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
0970 CALL "SYC.WA",0,SWIDTH,3,SWIN_X,SWIN_Y+SHEIGHT,"",CNAME$
0975 PRINT @(0,0),'BR',X$,'ER',
0980 CALL "SYC.WB",NAME$,0
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,SHEIGHT-2]
1020 LET PAGE=1,L=1,X1=1,X0=0,VENDOR$="",SELECTION$=""
1030 IF PREFIX$="" THEN GOTO 1200
1035 IF PREFIX$="S" THEN GOTO 1150
1040 IF PREFIX$="*" THEN GOTO 1300
1050 IF PREFIX$<>"D" THEN GOTO 1100
1060 REM " --- Vendor Name"
1065 LET V9=V0,V0=6
1070 READ (IVM03_DEV,KEY=N0$+PREFIX$,DOM=1080)
1080 READ (APM04_DEV,KEY=N0$+"A"+START$,DOM=1600)
1090 GOTO 1650
1100 REM " --- Description/Product Type/Bar Code/UPC Code"
1110 LET V0=3
1120 IF POS(PREFIX$="AJT")=0 THEN LET V0=20
1130 READ (IVM03_DEV,KEY=N0$+PREFIX$+START$,DOM=1400)
1140 GOTO 1550
1150 REM " --- Synonym"
1160 READ (IVM14_DEV,KEY=N0$+START$,DOM=1400)IOL=IVM14A
1170 LET K$=S0$(1,2)+S0$(23),X$=K$(3,20)
1190 GOTO 1550
1200 REM " --- Bill Number"
1210 READ (BMM01_DEV,KEY=N0$+START$,DOM=1400)IOL=BMM01A
1220 LET K$=C0$(1,22),X$=C0$(3,20)
1290 GOTO 1900
1300 REM " --- Keyword"
1310 LET START$=FNU$(START$)
1320 READ (BMM01_DEV,KEY=N0$,DOM=1400)
1340 GOTO 1400
1400 REM " --- Read next record"
1410 IF PREFIX$="" THEN GOTO 1800
1420 IF PREFIX$="*" THEN GOTO 1800
1430 IF PREFIX$="S" THEN GOTO 1850
1500 REM " --- Next IVM-03 sort record"
1510 LET K$=KEY(IVM03_DEV,END=3000)
1520 IF PREFIX$="D" THEN GOTO 1540
1530 IF POS(N0$+PREFIX$=K$)<>1 THEN GOTO 3000
1535 GOTO 1550
1540 IF POS(N0$+PREFIX$+VENDOR$=K$)<>1 THEN GOTO 1600
1550 READ (IVM03_DEV,KEY=K$)
1570 IF PREFIX$<>"D" THEN LET X$=K$(4,V0)
1580 LET K$=N0$+K$(4+V0)
1590 GOTO 1900
1600 REM " --- Next APM-04 vendor name"
1610 LET K$=KEY(APM04_DEV,END=3000)
1620 IF POS(N0$+"A"=K$)<>1 THEN GOTO 3000
1650 READ (APM04_DEV,KEY=K$)
1660 LET VENDOR$=K$(4+V9),X$=K$(4,V9)
1680 READ (IVM03_DEV,KEY=N0$+PREFIX$+VENDOR$,DOM=1400)
1690 GOTO 1400
1800 REM " --- Next BMM-01 key"
1810 LET K$=KEY(BMM01_DEV,END=3000)
1820 IF POS(N0$=K$)<>1 THEN GOTO 3000
1830 LET X$=K$(3,20)
1840 GOTO 1900
1850 REM " --- Next IVM-14 synonym"
1860 LET K$=KEY(IVM14_DEV,END=3000)
1870 IF POS(N0$=K$)<>1 THEN GOTO 3000
1880 READ (IVM14_DEV)
1890 LET X$=K$(3,20),K$=N0$+K$(23)
1900 REM " --- Read bill master & item master records"
1905 LET LOCKED=0
1910 READ (BMM01_DEV,KEY=K$,DOM=1400,ERR=1950)IOL=BMM01A
1915 FIND (IVM01_DEV,KEY=K$,ERR=1950)IOL=IVM01A
1920 IF PREFIX$<>"*" THEN GOTO 2000
1925 IF START$="" THEN GOTO 1940
1927 IF POS(START$=FNU$(C0$))>0 THEN GOTO 1940
1930 IF POS(START$=FNU$(C1$))>0 THEN GOTO 1940
1933 IF POS(START$=FNU$(C2$))>0 THEN GOTO 1940
1935 IF POS(START$=FNU$(A1$))>0 THEN GOTO 1940
1937 GOTO 2900
1940 LET X$=START$
1945 GOTO 2000
1950 REM " --- Error reading bill master record"
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1980 LET LOCKED=1
2000 REM " --- Display page?"
2010 IF L<MAX_ROW THEN GOTO 2100
2020 LET XMODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 0800
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2100 REM " --- Initializations for warehouse totals"
2110 IF LOCKED THEN GOTO 2400
2120 DIM B0$(24),B1$(64),B[20]
2130 LET ONHAND=0,ITEM$=A0$(1,2)+"G"+A0$(3)
2190 READ (IVM03_DEV2,KEY=ITEM$,DOM=2200)
2200 REM " --- Read warehouse records to accumulate quantities"
2210 LET K$=KEY(IVM03_DEV2,END=2400)
2220 IF POS(ITEM$=K$)<>1 THEN GOTO 2400
2230 READ (IVM03_DEV2)
2240 LET B0$(1)=A0$(1,2)+K$(24,2)+A0$(3)
2250 FIND (IVM02_DEV,KEY=B0$,DOM=2200)IOL=IVM02A
2260 LET ONHAND=ONHAND+B[0]
2290 GOTO 2200
2400 REM " --- Lookup window record"
2410 DIM G1$(WIDTH-2),DESCRIPTION$(W-(P[0]+3))
2420 IF LOCKED THEN LET G1$(2)=K$(3)+" Record In Use"; GOTO 2490
2430 LET DESCRIPTION$(1)=FNITEM$(A1$,P[2],P[4],P[5])
2480 LET G1$(2)=A0$(3),G1$(3+P[0])=DESCRIPTION$,G1$(W)=" "+X$
2490 LET G1$(WIDTH-2)="",LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$(3)
2500 REM " --- Format detail window fields"
2510 IF LOCKED THEN GOTO 2680
2520 LET PHANTOM$="No",CREATED$=" {Bill Created}"
2530 LET REVISED$=" {Last Revised}",USED$="{Last Activity}"
2540 IF C2$(40,1)="Y" THEN LET PHANTOM$="Yes"
2550 IF FNP$(C2$(45,3))<>"" THEN LET CREATED$=CREATED$+FNB$(C2$(45,3))
2560 IF FNP$(C2$(35,3))<>"" THEN LET REVISED$=REVISED$+FNB$(C2$(35,3))
2570 IF FNP$(C2$(41,3))<>"" THEN LET USED$=USED$+FNB$(C2$(41,3))
2600 REM " --- Detail window record"
2610 DIM G2$(SWIDTH-2)
2620 LET G2$(1)="  {Drawing Number}"+C0$(23,25)
2625 LET G2$(W2+4)="{Phantom?}"+PHANTOM$,MORE$[PAGE,X1,1]=G2$
2630 LET G2$(1)="{Drawing Revision}"+C0$(48,5),G2$(W1)=CREATED$
2635 LET G2$(W2+3)="{Est Yield}"+STR(C[4]:M9$),MORE$[PAGE,X1,2]=G2$
2640 LET G2$(1)="   {Revision Code}"+C2$(38,2),G2$(W1)=REVISED$
2645 LET G2$(W2)="{Std Lot Size}"+STR(C[5]:M8$),MORE$[PAGE,X1,3]=G2$
2650 LET G2$(1)=" {Unit Of Measure}"+C2$(48,2),G2$(W1)=USED$
2655 LET G2$(W2+5)="{On Hand}"+STR(ONHAND:M2$),MORE$[PAGE,X1,4]=G2$
2680 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),LLIST$[PAGE,X1,5]=SNAME$
2690 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2910 IF LOCKED=0 THEN GOTO 1400
2920 LET LOCKED=0
2930 IF PREFIX$<>"" AND PREFIX$<>"*" THEN GOTO 1400
2940 LET K$=K$(1,LEN(K$)-1)+CHR(ASC(K$(LEN(K$),1))+1)
2950 READ (BMM01_DEV,KEY=K$,DOM=1400,END=3000)IOL=BMM01A
2990 GOTO 1820
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3035 LET MESSAGE$[0]="No Bills Of Materials Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page"
3510 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
6500 REM " --- Error detected during processing of selection"
6510 DIM MESSAGE$[1]
6540 LET MESSAGE$[0]="Accounts Payable Is Required For This"
6550 LET MESSAGE$[1]="Selection (<Enter>=Continue)"
6560 CALL "SYC.XA",2,MESSAGE$[ALL],1,-1,-1,V$,V3
6590 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNS$(Q$)=CVS(Q$,1)
8085 DEF FNU$(Q$)=CVS(Q$,4)
8090 DEF FNITEM$(Q$,Q1,Q2,Q3)=CVS(Q$(1,Q1)+" "+Q$(Q1+1,Q2)+" "+Q$(Q1+Q2+1,Q3),32)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 FOR X=1 TO FILES
9915 CLOSE (CHANNELS[X],ERR=9916)
9920 NEXT X
9930 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9935 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
9940 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9945 IF SELECTION$="END" THEN LET SELECTION$=""
9950 EXIT 
9999 END

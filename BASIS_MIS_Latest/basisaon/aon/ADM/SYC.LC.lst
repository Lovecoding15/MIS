0010 REM "SYC - Customer Lookup"
0020 REM "Program SYC.LC"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 REM "SELECTION$: User selection (returned)"
0060 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER SELECTION$
0100 REM " --- Open/Lock Files"
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SYM-06",FILES$[3]="ARM-01"
0140 LET FILES$[4]="ARM-04",FILES$[5]="ARE-43",FILES$[6]="ART-43"
0150 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYS01_DEV=CHANNELS[1],SYM06_DEV=CHANNELS[2],ARM01_DEV=CHANNELS[3]
0180 LET ARM04_DEV=CHANNELS[4],ARE43_DEV=CHANNELS[5],ART43_DEV=CHANNELS[6]
0200 REM " --- IOLIST's"
0210 ARM01A: IOLIST A0$(1),A1$(1)
0260 ARS01A: IOLIST X$,P1$,P2$,P3$,P4$,M0$,M1$,M2$,M3$
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0280 SYM06A: IOLIST X$,COMPANY$
0400 REM " --- Parameters"
0405 DIM P[2],SPEED$(128)
0410 LET PAGES=INT((DSZ-5000)/4000),MAXROW=15,MAXPAGE=21
0420 IF PAGES<3 THEN GOTO 9900
0430 IF PAGES>MAXPAGE THEN LET PAGES=MAXPAGE
0440 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0450 LET N0$=F0$(16,2),SPEEDSEARCH$="N"
0460 LET SPEED$(1)=STBL("!SPEEDSEARCH",ERR=0480)
0470 LET N0$=SPEED$(1,2),SPEEDSEARCH$="Y"
0480 FIND (SYM06_DEV,KEY=N0$,DOM=9800)IOL=SYM06A
0485 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=9800)IOL=ARS01A
0490 LET P[0]=NUM(P2$(1,2)),P[1]=NUM(P2$(3,2)),P[2]=NUM(P3$(4,1))
0500 REM " --- Initializations"
0510 DIM A0$(8),A1$(256)
0540 LET WIDTH=48,HEIGHT=12,WIN_X=0,WIN_Y=3,MAX_ROW=HEIGHT-5
0550 LET SWIDTH=80-WIDTH,SHEIGHT=14,SWIN_X=80-SWIDTH,SWIN_Y=WIN_Y-1
0560 LET PGM$=PGM(-2),OPTIONS=0,OWIN_X=WIN_X+1,OWIN_Y=WIN_Y+3,M0=LEN(M0$)
0570 LET STITLE$="Customer Detail",OTITLE$="Method",M7$="(###)-###-####"
0600 REM " --- Window arrays"
0610 DIM HEADING$(WIDTH-2),FOOTING$(WIDTH-2)
0620 LET HEADING$(2)="Number",HEADING$(3+M0)="Name",HEADING$(27+M0)="Key"
0630 LET FOOTING$(2)="F1=Restart  F4=End  PgUp  PgDn"
0640 LET TITLE$="Customer Lookup"
0700 REM " --- Background"
0710 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0720 PRINT @(0,0),'SB','BR',HEADING$,@(0,HEIGHT-3),FOOTING$,'ER','SF',
0800 REM " --- Which search option?"
0810 DIM STRINGS$[10],NUMBERS[10]
0820 LET STRINGS$[0]=PGM$,STRINGS$[1]=M0$,NUMBERS[0]=MAXROW
0830 LET NUMBERS[1]=OWIN_X,NUMBERS[2]=OWIN_Y,NUMBERS[3]=P[0]
0840 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
0850 CALL "SYC.SM",STRINGS$[ALL],NUMBERS[ALL],FKEY
0860 IF FKEY THEN GOTO 9900
0870 LET PREFIX$=STRINGS$[2],START$=STRINGS$[3],METHOD$=STRINGS$[4]
0880 LET V3$=STRINGS$[5],V0=NUMBERS[4]
0890 IF PREFIX$="I" THEN LET METHOD$=" Invoice"
0900 REM " --- Detail Window"
0910 CALL "SYC.WA",0,SWIDTH,SHEIGHT,SWIN_X,SWIN_Y,STITLE$,SNAME$
0915 CALL "SYC.WB",NAME$,0
0920 LET HEADING$(27+LEN(M0$))=METHOD$
0925 PRINT @(0,0),'CF','SB','BR',HEADING$,'ER','SF',
0930 IF SPEEDSEARCH$<>"Y" THEN GOTO 1000
0950 REM " --- Display SpeedSearch Company"
0955 DIM X$(WIDTH+SWIDTH)
0960 LET X$(2)="SpeedSearch Company: "+N0$+" "+FNP$(COMPANY$)
0965 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
0970 CALL "SYC.WA",0,WIDTH+SWIDTH,3,WIN_X,SWIN_Y+SHEIGHT,"",CNAME$
0975 PRINT @(0,0),'BR',X$,'ER',
0980 CALL "SYC.WB",NAME$,0
1000 REM " --- Position file"
1010 DIM LLIST$[PAGES,MAX_ROW,5],MORE$[PAGES,MAX_ROW,SHEIGHT-2]
1020 LET PAGE=1,L=1,X1=1,X0=0,SELECTION$="",CHANNEL=ARE43_DEV
1030 IF PREFIX$="I" THEN LET CHANNEL=ART43_DEV
1040 IF PREFIX$="" THEN GOTO 1200
1050 IF PREFIX$="*" THEN GOTO 1300
1060 IF POS(PREFIX$="OI") THEN GOTO 1150
1100 REM " --- Alternate Key"
1110 READ (ARM04_DEV,KEY=N0$+PREFIX$+START$,DOM=1400)
1140 GOTO 1550
1150 REM " --- Order/Invoice Number"
1160 READ (CHANNEL,KEY=N0$+"  "+START$,DOM=1400)
1190 GOTO 1400
1200 REM " --- Customer Number"
1210 READ (ARM01_DEV,KEY=N0$+START$,DOM=1400)IOL=ARM01A
1220 LET K$=A0$,X$=A0$(3)
1290 GOTO 1900
1300 REM " --- Keyword"
1310 LET START$=FNU$(START$)
1320 READ (ARM01_DEV,KEY=N0$,DOM=1400)
1400 REM " --- Read next record"
1410 IF PREFIX$="" THEN GOTO 1800
1420 IF PREFIX$="*" THEN GOTO 1800
1430 IF POS(PREFIX$="OI") THEN GOTO 1700
1500 REM " --- Next ARM-04 sort record"
1510 LET K$=KEY(ARM04_DEV,END=3000)
1520 IF POS(N0$+PREFIX$=K$)<>1 THEN GOTO 3000
1530 READ (ARM04_DEV,KEY=K$)
1550 LET X$=K$(4,V0)
1580 LET K$=N0$+K$(4+V0)
1590 GOTO 1900
1700 REM " --- Next ARE-43/ART-43 key"
1710 LET K$=KEY(CHANNEL,END=3000)
1720 IF POS(N0$=K$)<>1 THEN GOTO 3000
1730 READ (CHANNEL)
1740 LET X$=K$(5,7),K$=N0$+K$(12,6)
1790 GOTO 1900
1800 REM " --- Next ARM-01 key"
1810 LET K$=KEY(ARM01_DEV,END=3000)
1820 IF POS(N0$=K$)<>1 THEN GOTO 3000
1830 LET X$=K$(3,P[0])
1900 REM " --- Read customer master record"
1905 LET LOCKED=0
1910 READ (ARM01_DEV,KEY=K$,ERR=1950)IOL=ARM01A
1920 IF PREFIX$<>"*" THEN GOTO 2000
1930 IF START$<>"" THEN IF POS(START$=FNU$(A1$))=0 THEN GOTO 2900
1935 LET X$=START$
1940 GOTO 2000
1950 REM " --- Error reading customer master record"
1960 IF ERR=11 THEN GOTO 2900
1970 IF ERR<>0 THEN GOTO 9000
1980 LET LOCKED=1
2000 REM " --- Display page?"
2010 IF L<MAX_ROW THEN GOTO 2400
2020 LET XMODE=5
2030 GOSUB 6200
2040 IF FKEY=1 THEN GOTO 0800
2050 IF FKEY=4 THEN GOTO 9900
2060 IF SELECTION$<>"" THEN GOTO 4000
2400 REM " --- Lookup window record"
2410 DIM G1$(WIDTH-2)
2415 IF LOCKED THEN GOTO 2450
2420 IF PREFIX$="E" THEN LET X$=FNZ$(X$)
2430 LET G1$(2)=FNF$(A0$(3,P[0]),M0$),G1$(3+LEN(M0$))=A1$(1,24)+" "+X$
2440 GOTO 2490
2450 LET G1$(2)=FNF$(K$(3,P[0]),M0$)+" Record In Use",A0$(1)=K$,A1$(1)=""
2490 LET LLIST$[PAGE,X1,1]=G1$,LLIST$[PAGE,X1,2]=A0$(3,6)
2500 REM " --- Format address block"
2510 LET X$=A1$(31,72)+A1$(179,48)+A1$(103,9),FAX$=FNP$(FNF$(A1$(169,10),M7$))
2520 CALL "SYC.AA",X$,24,5,9,30
2600 REM " --- Detail window record"
2610 DIM G2$(SWIDTH-2)
2615 IF LOCKED=1 THEN GOTO 2680
2620 LET G2$(2)=A1$(1,30),MORE$[PAGE,X1,2]=G2$,N=2
2630 FOR X=1 TO 180 STEP 30
2640 IF FNP$(X$(X,30))="" THEN GOTO 2650
2645 LET N=N+1,G2$(2)=X$(X,30),MORE$[PAGE,X1,N]=G2$
2650 NEXT X
2660 LET G2$(2)=FNF$(A1$(112,10),M7$),MORE$[PAGE,X1,N+2]=G2$
2665 IF FAX$<>"" THEN LET G2$(2)=FAX$+" fax",MORE$[PAGE,X1,N+3]=G2$
2670 LET G2$(2)=A1$(228,20),MORE$[PAGE,X1,N+4]=G2$
2680 LET MORE$[PAGE,X1,0]=STR(SHEIGHT-2),LLIST$[PAGE,X1,5]=SNAME$
2690 LET X0=X0+1,X1=X1+1,L=L+1
2900 REM " --- Loop back for next record"
2910 IF LOCKED=0 THEN GOTO 1400
2920 LET LOCKED=0
2930 IF PREFIX$<>"" AND PREFIX$<>"*" THEN GOTO 1400
2940 LET K$=K$(1,LEN(K$)-1)+CHR(ASC(K$(LEN(K$),1))+1)
2950 READ (ARM01_DEV,KEY=K$,DOM=1400,END=3000)IOL=ARM01A
2990 GOTO 1820
3000 REM " --- End of file"
3010 IF X0>0 THEN GOTO 3500
3020 DIM MESSAGE$[1]
3030 LET V2=WIN_Y+INT(HEIGHT/2)-2,V1=-1
3035 LET MESSAGE$[0]="No Customers Found (<Enter>=Continue)"
3040 CALL "SYC.XA",3,MESSAGE$[ALL],0,V2,V1,V$,V3
3060 IF V3=5 THEN GOTO 4000
3070 IF V3=6 THEN GOTO 4000
3080 IF V3=4 THEN GOTO 4000
3090 GOTO 0800
3500 REM " --- Display last page"
3510 LET XMODE=5,SELECTION$=SELECTION$+"|EOF"
3520 GOSUB 6200
3530 IF FKEY=1 THEN GOTO 0800
4000 REM " --- All done"
4090 GOTO 9900
6200 REM " --- Display page"
6210 CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,MAX_ROW,NAME$,HEIGHT,WIDTH,FKEY
6220 IF FKEY=1 THEN GOTO 6290
6230 IF FKEY=-16 THEN LET PAGE=PAGE+1
6250 IF PAGE>PAGES-1 THEN CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,MAX_ROW,5,SHEIGHT-2
6280 LET L=1,X1=1
6290 RETURN 
8000 REM " --- Functions"
8070 DEF FNP$(Q$)=CVS(Q$,2)
8080 DEF FNU$(Q$)=CVS(Q$,4)
8085 DEF FNZ$(Q$)=FILL(10,CVS(Q$(1,3)+FILL(POS("9"<Q$(1,5))>0)+Q$(4,2)+FILL(ABS(POS("A">=Q$(1,6))>0)-POS("0">Q$(6,4))>0,"-")+Q$(6,4)+FILL(10),16))
8200 REM " --- FNF$ AlphaNumeric Masking Function"
8210 DEF FNF$(Q1$,Q2$)
8220 IF Q2$="" THEN LET Q2$=FILL(LEN(Q1$),"0")
8230 RETURN STR(-NUM(Q1$,ERR=ALPHA_FNF):Q2$,ERR=ALPHA_FNF)
8240 ALPHA_FNF: 
8245 LET Q=1,Q0=0
8250 WHILE LEN(Q2$(Q))
8255 IF POS(Q2$(Q,1)="-()") THEN LET Q0=Q0+1 ELSE LET Q2$(Q,1)="X"
8260 LET Q=Q+1
8265 WEND
8270 IF LEN(Q1$)>LEN(Q2$)-Q0 THEN LET Q1$=Q1$(1,LEN(Q2$)-Q0)
8280 RETURN STR(Q1$:Q2$)
8290 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to calling program"
9905 IF FKEY=4 THEN LET SELECTION$="END"
9910 FOR X=1 TO FILES
9915 CLOSE (CHANNELS[X],ERR=9916)
9920 NEXT X
9925 IF CNAME$<>"" THEN CALL "SYC.WD",CNAME$
9930 IF SNAME$<>"" THEN CALL "SYC.WD",SNAME$
9935 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9940 IF SELECTION$="END" THEN LET SELECTION$=""
9945 IF SELECTION$="PRIOR" AND SPEEDSEARCH$="N" THEN LET SELECTION$=""
9950 EXIT 
9999 END

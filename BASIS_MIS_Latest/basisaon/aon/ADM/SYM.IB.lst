0010 REM "SYM - Process Maintenance (Definition)"
0020 REM "Program SYM.IB"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0200 REM " --- IOList's"
0210 SYM19A: IOLIST B0$(1),B1$(1),B2$(1),B3$,B[ALL]
0220 SYM29A: IOLIST Y0$
0500 REM " --- Initializations"
0510 LET L=L0+1,L9=19
0520 DIM B0$(18),B1$(30),B2$(11),B[3]
0530 LET B0$(1)=A0$,K9$=""
0700 REM " --- Background"
0710 PRINT @(0,L0),'CE','SB',
0720 PRINT @(1,L0),"Program",@(11),"Description",@(43),"Create",
0790 PRINT 'SF',
0800 REM " --- Flow Control"
0805 REM "O0 = 0 for new record
0810 REM "   = 1 for existing record
0820 ON O0 GOTO 1400,0900
0900 REM " --- Display any existing lines"
0920 GOSUB 5100
0990 GOTO 4000
1000 REM " --- Overflow"
1010 IF L>L9 THEN PRINT @(0,22),'CL',@(0,L0+1),'LD',
1020 IF L>L9 THEN LET L=L9
1100 REM " --- Get Next Key"
1110 LET B0$(11)="",K$=KEY(SYM19_DEV,END=1400)
1120 IF POS(B0$(1,10)=K$)<>1 THEN GOTO 1400
1130 READ (SYM19_DEV)IOL=SYM19A
1190 GOTO 1490
1400 REM " --- Program"
1420 LET V0$="S",V1$="KERC",V2$=B0$(11),V3$="",V4$="Enter Program Name",V0=8,V1=1,V2=L,I0=0
1440 GOSUB 7000
1445 IF V3=4 THEN GOTO 4000
1450 IF FNP$(V$)="" THEN GOTO 1400
1460 LET V$=V$+FILL(V0),V$=V$(1,V0)
1470 FIND (SYM19_DEV,KEY=B0$(1,10)+V$,DOM=1600)IOL=SYM19A
1480 LET B0$(11)=V$
1500 GOSUB 5000
1520 LET I0=3
1530 GOTO 3300
1600 REM " --- New Record"
1610 LET PGM$=V$
1620 GOSUB 6500
1630 IF STATUS>0 THEN GOTO 1400
1640 GOSUB 6000
1650 IF STATUS>0 THEN GOTO 1400
1660 DIM B[3]
1670 LET B0$(11)=PGM$,B1$(1)=DESCRIPTION$,B2$(1)=""
1680 GOSUB 5000
2000 REM " --- Description"
2020 LET V0$="S",V1$="C",V2$=B1$,V3$="",V4$="Enter Program Description",V0=30,V1=11,V2=L,I0=1
2030 GOSUB 7000
2040 ON V3 GOTO 2050,2000,3200,2000,3200
2050 LET B1$(1)=V$
2090 GOTO 3200
2200 REM " --- Create?"
2220 LET V0$="Y",V1$="C",V2$=B2$(1,1),V3$="",V4$="Will this program need to create new batches?",V0=1,V1=45,V2=L
2230 GOSUB 7000
2240 ON V3 GOTO 2250,2200,3200,2200,3200
2250 LET B2$(1,1)=V$
2290 GOTO 3200
3200 REM " --- Sequence Control"
3210 IF V3=4 THEN IF I0>1 THEN GOTO 3300
3220 IF V3<>2 THEN GOTO 3250
3230 IF I0>0 THEN LET I0=I0-1
3240 GOTO 3260
3250 LET I0=I0+1
3260 ON I0 GOTO 1400,2000,2200,3300
3300 REM " --- Line Correct?"
3305 LET V4$="<Enter>=Correct     <F2>=Changes"
3310 READ (SYM59_DEV,KEY=B0$(1,10)+N0$,DOM=3315)
3315 LET K$=KEY(SYM59_DEV,END=3320)
3320 IF POS(B0$(1,10)+N0$=K$)<>1 THEN LET V4$=V4$+"     <D>=Delete"; REM "Can delete when batch doesn't exist
3330 LET V0$="S",V1$="C",V2$="",V3$="",V0=1,V1=78,V2=L,K9$=B0$
3335 GOSUB 7000
3340 ON V3 GOTO 3350,3300,3200,3300,3350
3350 IF V$="" THEN GOTO 3400
3360 IF V$="D" THEN GOTO 3700
3390 GOTO 3300
3400 REM " --- Write"
3410 WRITE (SYM19_DEV,KEY=B0$)IOL=SYM19A
3420 LET Y0$=B0$(11)+B0$(1,10)
3430 WRITE (SYM29_DEV,KEY=Y0$)IOL=SYM29A
3450 LET L=L+1
3460 IF V3=4 THEN GOTO 4000
3490 GOTO 1000
3700 REM " --- Remove"
3710 READ (SYM59_DEV,KEY=B0$(1,10),DOM=3720)
3720 LET K$=KEY(SYM59_DEV,END=3740)
3730 IF POS(B0$(1,10)=K$)=1 THEN GOTO 3300; REM "Can't delete when batch exists
3740 REMOVE (SYM19_DEV,KEY=B0$,DOM=3750)
3750 REMOVE (SYM29_DEV,KEY=B0$(11)+B0$(1,10),DOM=3760)
3760 PRINT @(0,L),'CL',
3790 GOTO 1000
4000 REM " --- Options"
4020 LET V0$="S",V1$="C",V2$="Y",V3$="",V4$="Is The Above Information Correct (Y/N/List/Review)?",V0=6,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,4100
4050 IF V$="L" OR V$="LIST" THEN GOTO 4200
4070 IF V$="R" OR V$="REVIEW" THEN GOTO 4400
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run Header Overlay"
4120 IF O0<>0 THEN RUN "SYM.IA"; REM "Not a new record;
4150 REM " --- Overlay For New Record"
4160 RUN "SYM.IC"
4200 REM " --- List"
4210 GOSUB 5100
4290 GOTO 4000
4400 REM " --- Review"
4410 READ (SYM19_DEV,KEY=B0$(1,10),DOM=4420)
4420 GOSUB 5400
4490 GOTO 1000
5000 REM " --- Display"
5010 IF B0$<=K9$ THEN GOSUB 5400
5030 PRINT @(1,L),'CL',@(1),B0$(11),@(11),B1$,@(45),B2$(1,1),
5090 RETURN 
5100 REM " --- List"
5110 GOSUB 5400
5120 READ (SYM19_DEV,KEY=B0$(1,10),DOM=5130)
5130 LET K$=KEY(SYM19_DEV,END=5300)
5150 IF POS(B0$(1,10)=K$)<>1 THEN GOTO 5300
5160 READ (SYM19_DEV)IOL=SYM19A
5190 IF L<=L9 THEN GOTO 5250
5200 REM 
5210 LET V0$="S",V1$="CE",V2$="",V3$="",V4$="Press <Enter> To Continue",V0=1,V1=FNV(V4$),V2=22
5220 GOSUB 7000
5230 IF V3=4 THEN GOTO 5300
5240 GOSUB 5400
5250 GOSUB 5000
5260 LET L=L+1,K9$=B0$
5290 GOTO 5130
5300 RETURN 
5400 REM " --- Clear Screen"
5410 PRINT @(0,L0+1),'CE',
5440 LET L=L0+1
5490 RETURN 
6000 REM " --- Verify Program Isn't In Another Batched Process"
6010 LET STATUS=0
6020 READ (SYM29_DEV,KEY=PGM$,DOM=6030)
6030 LET K$=KEY(SYM29_DEV,END=6090)
6040 IF POS(PGM$=K$)<>1 THEN GOTO 6090
6050 LET NUMBER=1,STATUS=1
6060 DIM MESSAGE$[NUMBER]
6070 LET MESSAGE$[0]="This Program is already used in batch process "+K$(9)
6080 CALL "SYC.XA",2,MESSAGE$[ALL],NUMBER,22,-1,V$,V3
6090 RETURN 
6500 REM " --- Verify Program Existance And Return Description"
6510 LET CHANNEL=UNT,STATUS=11,DESCRIPTION$=""
6520 OPEN (CHANNEL,ERR=6680)FNP$(PGM$)
6530 LET X$=FID(CHANNEL,ERR=6680)
6540 IF X$(1,1)<>$04$ THEN GOTO 6680
6550 READ RECORD(CHANNEL,SIZ=10)X$
6570 CALL "SYC.UB",CHANNEL,"",X$,"10",STATUS
6580 LET STATUS=0,P=POS("REM"=X$)
6590 IF P=0 THEN GOTO 6680
6600 LET X$=X$(P+3),P=POS("-"=X$)
6610 IF P=0 THEN GOTO 6680
6620 LET X$=FNP$(FNS$(X$(P+1)))
6630 IF X$="" THEN GOTO 6680
6640 IF X$(LEN(X$))=$22$ THEN LET X$=X$(1,LEN(X$)-1)
6650 LET DESCRIPTION$=X$
6680 CLOSE (CHANNEL,ERR=6690)
6690 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8080 DEF FNP$(Q$)=CVS(Q$,2)
8085 DEF FNS$(Q$)=CVS(Q$,1)
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

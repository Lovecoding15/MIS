0010 REM "SYC - On-Line Help Maintenance"
0020 REM "Program SYC.HM"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "ACTION     : Action code (input)"
0050 REM "           : 0=Process within window"
0055 REM "           : 1=Process on main screen"
0060 REM "CHANNELS[n]: Channel numbers (input)"
0065 REM "           : [1]=SYS-01   [3]=SYS-21   [5]=SYM-02"
0070 REM "           : [2]=SYS-11   [4]=SYM-01"
0075 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER ACTION,CHANNELS[ALL]
0100 REM " --- Open/Lock Files"
0110 LET SYS01_DEV=CHANNELS[1],SYS11_DEV=CHANNELS[2],SYS21_DEV=CHANNELS[3]
0120 LET SYM01_DEV=CHANNELS[4],SYM02_DEV=CHANNELS[5]
0200 REM " --- IOLIST's"
0210 SYS11A: IOLIST A0$(1),A1$(1),A2$(1)
0220 SYM02A: IOLIST B0$(1),B1$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$,F6$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N2$="SY",MENU$=FNP$(F2$)
0430 LET PGM$="",PGM$=STBL("!HELP",ERR=0440)
0500 REM " --- Initializations"
0510 DIM INFO$[5],INFO[5],A0$(14),A1$(40),A2$(16),B0$(3),B1$(24)
0550 LET INFO$[0]="SYS-21",INFO[1]=7,INFO[3]=3,INFO[4]=70
0560 LET WIDTH=80,OFFSET=3,J9=22,LOOKUP=3
0700 REM " --- Background"
0705 IF ACTION>0 THEN GOTO 0750
0710 LET WIDTH=58,HEIGHT=10,TITLE$="ADD+ON On-Line Help Maintenance"
0720 LET WIN_Y=3,WIN_X=1
0725 IF WIN_Y+HEIGHT>22 THEN LET WIN_Y=22-HEIGHT
0730 IF WIN_X+WIDTH>79 THEN LET WIN_X=79-WIDTH
0740 CALL "SYC.WA",0,WIDTH,HEIGHT,WIN_X,WIN_Y,TITLE$,NAME$
0745 LET OFFSET=0,J9=HEIGHT-2,LOOKUP=WIN_Y+2
0750 PRINT 'SB',@(5,OFFSET),"Subject",@(1,OFFSET+1),"Help Number",@(7,OFFSET+2),"Topic",@(5,OFFSET+4),"Revised",@(10,OFFSET+5),"By",'SF',
0900 REM " --- Position file"
0910 LET A0$(1)=N0$+PGM$,V9=WIDTH-2
0990 READ (SYS11_DEV,KEY=A0$,DOM=1000)
1000 REM " --- Subject (Menu, Task or Program)"
1005 PRINT 'CF',
1010 LET V0$="S",V1$="CK^",V2$=A0$(3,8),V3$="",V0=8,V1=13,V2=OFFSET
1013 LET V4$="<F1>=Menu  <F2>=Program  <F3>=Lookup"
1015 GOSUB 7000
1020 ON V3 GOTO 1040,1023,1027,1035,9900
1023 LET V$=MENU$
1025 GOTO 1040
1027 LET V$=PGM$
1030 GOTO 1040
1035 GOSUB 6000
1037 IF V$<>"" THEN GOTO 1200
1040 IF FNP$(V$)="" THEN GOTO 1100
1045 LET A0$(3)=V$
1047 PRINT @(V1,V2),A0$(3,8),
1050 REM " --- Help Number"
1055 LET V0$="Z",V1$="C",V2$=A0$(11,4),V3$="",V0=4,V1=13,V2=OFFSET+1
1060 LET V4$="Enter A Unique Sequence Number For This Topic"
1065 GOSUB 7000
1070 ON V3 GOTO 1075,1050,1000,1050,9900
1075 IF V$="" OR V$="0000" THEN GOTO 1050
1080 LET A0$(11,4)=V$
1090 GOTO 1200
1100 REM " --- Get next record"
1110 LET K$=KEY(SYS11_DEV,END=0900)
1120 READ (SYS11_DEV)
1130 LET A0$=K$
1200 REM " --- Retrieve record"
1210 READ (SYS11_DEV,KEY=A0$,DOM=1400)IOL=SYS11A
1220 FIND RECORD(SYS11_DEV,KEY=A0$)OLDRECORD$
1300 REM " --- Display record"
1320 GOSUB 5000
1390 GOTO 4000
1400 REM " --- New record"
1410 LET V4$="Is This A New Record (Y/N)?"+FILL(3)
1420 LET V0$="Y",V1$="C",V2$="N",V3$="",V0=1,V1=FNV1(V4$,V9),V2=J9
1430 GOSUB 7000
1440 ON V3 GOTO 1490,1400,1400,1400,9900
1490 ON POS(V$="YN") GOTO 1400,1500,1000
1500 REM " --- Setup for new record"
1510 DIM A1$(40),A2$(16)
1520 LET OLDRECORD$="",A2$(1,6)=F0$(18,6),A2$(7,3)=F0$(4,3)
1530 GOSUB 5000
1600 REM " --- Topic"
1610 LET V0$="S",V1$="C",V2$=A1$(1,40),V3$="",V0=40,V1=13,V2=OFFSET+2
1620 LET V4$="Enter The Topic For This Help",I0=1
1630 GOSUB 7000
1640 ON V3 GOTO 1650,1600,3900,1600,3900
1650 LET A1$(1,40)=V$
1690 GOTO 3900
4000 REM " --- Correct?"
4010 LET V4$="Is The Above Correct (Y/N/Delete)?"
4020 LET V0$="S",V1$="CER",V2$="Y",V3$="",V0=6,V2=J9,V1=FNV1(V4$,V9)
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4050 IF V$="DELETE" THEN GOTO 4500
4090 ON POS(V$="YN") GOTO 4000,4100,1600
4100 REM " --- Write record"
4110 WRITE (SYS11_DEV,KEY=A0$)IOL=SYS11A
4120 FIND RECORD(SYS11_DEV,KEY=A0$)NEWRECORD$
4130 IF NEWRECORD$=OLDRECORD$ THEN GOTO 4200
4140 LET A2$(1,6)=F0$(18,6),A2$(7,3)=F0$(4,3)
4150 WRITE (SYS11_DEV,KEY=A0$)IOL=SYS11A
4200 REM " --- Detail maintenance"
4210 LET INFO$[1]=A0$
4220 CALL "SYC.KA",10,INFO$[ALL],INFO[ALL]
4290 GOTO 1000
4500 REM " --- Position detail file"
4590 READ (SYS21_DEV,KEY=A0$,DOM=4600)
4600 REM " --- Remove detail"
4610 LET K$=KEY(SYS21_DEV,END=4700)
4620 IF POS(A0$=K$)<>1 THEN GOTO 4700
4630 REMOVE (SYS21_DEV,KEY=K$,DOM=4690)
4690 GOTO 4600
4700 REM " --- Remove header"
4710 REMOVE (SYS11_DEV,KEY=A0$,DOM=4720)
4790 GOTO 1000
5000 REM " --- Display record"
5010 LET B0$(1)=A2$(7,3),B1$(1)="(Unknown)"
5020 FIND (SYM02_DEV,KEY=B0$,DOM=5030)IOL=SYM02A
5030 PRINT @(13,OFFSET),A0$(3,8),@(13,OFFSET+1),A0$(11,4),@(13,OFFSET+2),A1$(1,40),@(13,OFFSET+4),FNB6$(A2$(1,6)),@(13,OFFSET+5),B1$,
5090 RETURN 
6000 REM " --- Help Lookup"
6010 CALL "SYC.HL",0,CHANNELS[ALL],WINDOW$,"",V$,V3
6020 IF V$="" THEN GOTO 6090
6030 LET A0$(3)=V$
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Function Definitions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8060 DEF FNC(Q$,Q)=INT((Q-LEN(Q$))/2)
8070 DEF FNV1(Q$,Q)=FNC(Q$,Q)+LEN(Q$)
8090 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9940 IF NAME$<>"" THEN CALL "SYC.WD",NAME$
9950 EXIT 
9999 END

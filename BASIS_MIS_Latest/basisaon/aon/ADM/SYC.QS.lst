0010 REM "SYC - PF Reports Display (Print to Screen)"
0020 REM "Program SYC.QS"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0045 REM "D_STATUS   : SpeedSearch Status (0=No Error)"
0050 REM "OPTION$[n] : Standard SpeedSearch Options Array"
0055 REM "INFO$[n]   : String parameters (see SYC.QR)"
0060 REM "INFO[n]    : Numeric parameters (see SYC.QR)"
0065 REM 
0080 SETERR 9000
0085 SETESC 9000
0090 ENTER D_STATUS,OPTION$[ALL],INFO$[ALL],INFO[ALL]
0400 REM " --- Parameters"
0410 LET PAGES=INT((DSZ-8000)/4000),H0=132+LEN('LF')
0420 IF PAGES>15 THEN LET PAGES=15
0450 LET COL=1,ROW=21,FILES=1
0500 REM " --- Initializations"
0510 DIM LLIST$[PAGES,ROW,5],RLIST$[PAGES,ROW,5],MORE$[PAGES,ROW,1],BUF$(H0)
0520 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES],CHOICES$[8]
0540 LET F4$="(<F4>=Interrupt)"
0550 LET DWIDTH=8,DHEIGHT=19,DWIN_X=70,DWIN_Y=0,DTITLE$="Line"
0555 LET EWIDTH=30,EHEIGHT=9,EWIN_X=39,EWIN_Y=10,ETITLE$="",CHOICE=1
0560 LET KWIDTH=50,KHEIGHT=3,KWIN_X=15,KWIN_Y=12,KTITLE$="Keyword Entry"
0565 LET MWIDTH=82,MHEIGHT=26,MWIN_X=-1,MWIN_Y=-1,MTITLE$="",M4$="##0.00%"
0580 LET HEADER$="",BYTES=INFO[10],LETTERS$="MRLTBHKE",RING=MHEIGHT-3
0585 LET CHOICES$[1]=" More",CHOICES$[2]=" Right",CHOICES$[3]=" Left"
0590 LET CHOICES$[4]=" Top",CHOICES$[5]=" Bottom",CHOICES$[6]=" Header"
0595 LET CHOICES$[7]=" Keyword",CHOICES$[8]=" Exit",SIDE$="L",PREVIOUS$=""
0600 REM " --- Additional file opens"
0610 CLOSE (REPORT_DEV,ERR=0620)
0620 LET FILES$[1]=INFO$[7]
0630 CALL "SYC.DA",1,1,1,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0640 IF STATUS THEN GOTO 9900
0650 LET REPORT_DEV=CHANNELS[1],PAGE=1,EOF=0
0700 REM " --- Background"
0710 IF WINDOW THEN GOTO 0800
0720 CALL "SYC.WA",1,MWIDTH,MHEIGHT,MWIN_X,MWIN_Y,MTITLE$,SCREEN$
0730 CALL "SYC.WB",SCREEN$,0
0740 CALL "SYC.WC",1,0,MWIDTH-2,1,1,MHEIGHT-4,0
0750 FOR X=1 TO 8
0760 PRINT @(FNX(X),RING),'SB','BR',FNR$(CHOICES$[X]),'ER','SF',
0770 NEXT X
0790 LET WINDOW=1
0900 REM " --- Final initializations"
0910 LET L0=1,L1=0,L9=ROW-1,W=80,L=L0,N=1,P=1,BOTTOM=0,EOF=0,COUNT=0,KEYWORD$=""
0920 DIM S$(H0*L9),Z$(H0),S0$(H0*2),FILES$[FILES],OPTIONS$[FILES]
0930 DIM LLIST$[PAGES,ROW,5],RLIST$[PAGES,ROW,5],BUF$(H0)
0940 DIM MFOOTER$(MWIDTH-11),MORE$[PAGES,ROW,1]
1000 REM " --- Read next record"
1010 READ RECORD(REPORT_DEV,END=4000)X$
1020 LET COUNT=COUNT+LEN(X$)
1100 REM " --- Check for LF"
1110 IF LEN(X$)=0 THEN GOTO 1150
1120 IF X$(LEN(X$),1)<>$0A$ THEN GOTO 1150
1130 LET X$=X$(1,LEN(X$)-1)+'LF'
1140 GOTO 2000
1150 LET X=POS('LF'=X$)
1160 IF X<>0 THEN GOTO 2000
1200 REM " --- Append to current buffer"
1210 READ RECORD(REPORT_DEV,END=4000)Y$
1220 LET COUNT=COUNT+LEN(Y$),X$=X$+Y$
1290 GOTO 1100
2000 REM " --- Parse and store lines"
2010 IF X$="" THEN GOTO 2900
2015 LET X=POS('FF'=X$),Y=POS('LF'=X$)
2020 IF X<>1 AND Y<>1 THEN GOTO 2050
2030 LET X$=X$(4)
2040 GOTO 2400
2050 LET Z0=POS($1B$=X$)
2060 IF Z0=0 THEN GOTO 1200
2070 IF Z0<>1 THEN GOTO 2200
2100 REM " --- Print Positioning"
2110 IF POS($1B4031$=X$)=1 THEN LET P=ASC(X$(4,1))+1,X$=X$(5) ELSE LET P=ASC(X$(3,1))+1,X$=X$(4)
2190 GOTO 2000
2200 REM " --- Remove mnemonics"
2210 IF P>H0 THEN LET P=H0-1
2220 IF P+Z0-1>H0 AND W1<>1 THEN LET W1=1
2230 LET Z2=POS($1B$=X$)
2240 IF W1=1 AND Z2>0 THEN LET X$=X$(1,Z0-1)+X$(Z2)
2250 LET Z1=Z0
2260 IF P+Z0-1>H0 THEN LET Z1=Z0+(H0-(P+Z0))
2270 LET Z$(P,Z1-1)=X$(1,Z1-1),X$=X$(Z0),P=P+Z1-1
2290 GOTO 2000
2400 REM " --- Parsing completed"
2410 IF L>2 AND Z$=S0$(1,H0) THEN IF S$(H0*(L-2)+1,H0*2)=S0$ THEN GOTO 2450
2420 LET S$(H0*(L-1)+1,H0)=Z$,L1=0,L=L+1
2450 DIM Z$(H0)
2460 LET P=1
2480 IF L>L9 THEN GOTO 2500
2490 GOTO 2000
2500 REM " --- Buffer full"
2510 GOSUB 5000
2520 IF V3=5 THEN LET OPTION$[1]="STARTOVER"; GOTO 4900
2530 IF V3=6 THEN LET OPTION$[1]="EXIT"; GOTO 4900
2540 ON POS(SELECTION$="TBE") GOTO 2000,0600,1000,4900
2590 GOTO 2000
2900 REM " --- Loop back for more of the report"
2990 GOTO 1000
4000 REM " --- All done"
4010 LET KEYWORD$="",BOTTOM=0,EOF=1
4020 GOSUB 5000
4030 IF SELECTION$="T" THEN GOTO 0600
4040 IF V3=5 THEN LET OPTION$[1]="STARTOVER"
4050 IF V3=6 THEN LET OPTION$[1]="EXIT"
4900 REM " --- Run standard exit program"
4990 GOTO 9900
5000 REM " --- Buffer full or EOF"
5010 LET T$=S$,LASTFOUND=0
5100 REM " --- Print Header Section"
5110 LET L8=1
5120 IF LEN(HEADER$)>H0-1 THEN LET L9=19-LINES
5130 IF LEN(HEADER$)<H0 THEN GOTO 5200
5150 FOR X=1 TO LEN(HEADER$) STEP H0
5160 LET BUF$(1)=FNP$(HEADER$(X,H0))
5170 LET LLIST$[PAGE,L8,1]=" "+FNP$(BUF$(1,80)),RLIST$[PAGE,L8,1]=FNP$(BUF$(53)),L8=L8+1
5180 NEXT X
5200 REM " --- Print Report Section"
5205 LET FOUND=0
5210 FOR X=1 TO L9
5215 IF POS(T$(H0*(X-1)+1,W)=HEADER$)>0 THEN GOTO 5275
5220 WHILE POS($1F$>T$(H0*(X-1)+1,H0))>0
5225 LET T$(H0*(X-1)+1+POS($1F$>T$(H0*(X-1)+1,H0))-1,1)=" "
5230 WEND
5235 LET BUF$(1)=FNP$(T$(H0*(X-1)+1,H0))
5240 IF KEYWORD$="" THEN GOTO 5265
5245 IF FOUND OR L8<=LASTFOUND THEN GOTO 5265
5250 IF POS(KEYWORD$=BUF$)=0 THEN GOTO 5265
5255 LET HITPOS=POS(KEYWORD$=BUF$),FOUND=L8,LASTFOUND=FOUND
5260 IF HITPOS>81-LEN(KEYWORD$) THEN LET SIDE$="R" ELSE LET SIDE$="L"
5265 LET LLIST$[PAGE,L8,1]=" "+FNP$(BUF$(1,80)),LLIST$[PAGE,L8,2]=STR(L8:"00")
5270 LET RLIST$[PAGE,L8,1]=" "+FNP$(BUF$(53)),RLIST$[PAGE,L8,2]=STR(L8:"00")
5275 LET L8=L8+1
5280 NEXT X
5285 IF BOTTOM THEN GOTO 5500
5290 IF KEYWORD$="" THEN GOTO 5300
5295 IF FOUND=0 THEN GOTO 5500
5300 REM " --- Adjust ring menu"
5310 IF CHOICE>0 THEN PRINT @(FNX(CHOICE),RING),'SB','BR',FNR$(CHOICES$[CHOICE]),'ER','SF',
5320 IF CHOICE<>5 AND CHOICE<>7 THEN GOTO 5350
5325 PRINT @(0,RING),'CL',
5330 FOR X=1 TO 8
5335 PRINT @(FNX(X),RING),'SB','BR',FNR$(CHOICES$[X]),'ER','SF',
5340 NEXT X
5350 PRINT @(1,RING),'BR',FNR$(CHOICES$[1]),'ER',
5360 LET MORE$[PAGE,1,0]=STR(MWIDTH-9),MORE$[PAGE,2,0]=STR(RING)
5370 LET MORE$[PAGE,3,0]=FNN$(COUNT,BYTES,M4$),KEYWORD$=""
5400 REM " --- Call lightbar routine"
5410 LET XMODE=3,SELECTION$=LETTERS$+"|"+STR(FOUND:"00")
5415 IF EOF THEN LET SELECTION$=SELECTION$+"|EOF"
5420 IF SIDE$="L" THEN CALL "SYC.SA",XMODE,LLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,ROW,SCREEN$,MHEIGHT,MWIDTH,FKEY
5425 IF SIDE$="R" THEN CALL "SYC.SA",XMODE,RLIST$[ALL],MORE$[ALL],SELECTION$,PAGE,ROW,SCREEN$,MHEIGHT,MWIDTH,FKEY
5430 LET X=NUM(SELECTION$,ERR=5435),SELECTION$=""
5435 IF SELECTION$="" THEN IF FKEY=0 THEN LET SELECTION$="M"
5440 IF FKEY=-16 THEN LET SELECTION$="M"
5445 IF FKEY=-1 THEN LET SELECTION$="R"
5450 IF FKEY=-2 THEN LET SELECTION$="L"
5455 IF FKEY=1 THEN LET SELECTION$="H"
5460 IF FKEY=4 THEN LET SELECTION$="E"
5465 IF SELECTION$="END" THEN LET SELECTION$="E"
5470 LET CHOICE=POS(SELECTION$=LETTERS$)
5480 IF CHOICE>0 THEN PRINT @(1,RING),'SB','BR',FNR$(CHOICES$[1]),'SF',@(FNX(CHOICE),RING),FNR$(CHOICES$[CHOICE]),'ER',
5490 ON CHOICE GOTO 5300,5500,5850,5850,5900,5600,5650,5700,5900
5500 REM " --- More"
5510 IF BOTTOM=0 AND KEYWORD$="" THEN GOTO 5550
5515 LET V3=0
5520 INPUT (0,SIZ=0,TIM=0,ERR=5530)CR$
5525 LET V3=CTL
5530 IF V3=4 THEN LET BOTTOM=0,V3=0; GOTO 5300
5540 PRINT @(1,RING),'SB','BR',MFOOTER$,@(MWIDTH-9),FNN$(COUNT,BYTES,M4$),'ER',
5550 LET S$(1)="",W1=0,L=1,PAGE=PAGE+1
5555 IF PAGE<PAGES THEN GOTO 5900
5560 DIM XMORE$[PAGES,ROW,1]
5570 LET XMORE$[ALL]=MORE$[ALL]
5575 CALL "SYC.RB",LLIST$[ALL],MORE$[ALL],PAGES,PAGE,ROW,5,1
5580 CALL "SYC.RB",RLIST$[ALL],XMORE$[ALL],PAGES,PAGE,ROW,5,1
5585 DIM XMORE$[0,0,0]
5590 GOTO 5900
5600 REM " --- Bottom"
5610 LET MFOOTER$(2)="Moving To Bottom Of File "+F4$,BOTTOM=1,CHOICE=5
5620 PRINT @(0,RING),'CL',
5640 GOTO 5500
5650 REM " --- Header"
5660 GOSUB 6000
5670 IF V3=5 OR V3=6 THEN GOTO 5900
5690 GOTO 5500
5700 REM " --- Keyword"
5710 CALL "SYC.WA",0,KWIDTH,KHEIGHT,KWIN_X,KWIN_Y,KTITLE$,KNAME$
5720 PRINT @(1,0),'SB',"Enter keyword:",'SF',
5730 LET V0$="S",V1$="E",V2$=PREVIOUS$,V3$="",V4$="",V0=30,V1=16,V2=0
5740 GOSUB 7000
5750 IF V3=4 OR V3=5 OR V3=6 THEN GOTO 5800
5760 IF V$<>"" THEN LET KEYWORD$=V$
5770 LET PREVIOUS$=KEYWORD$
5800 REM " --- Start keyword search"
5805 CALL "SYC.WD",KNAME$
5810 IF V3=4 THEN GOTO 5300
5815 IF V3=5 OR V3=6 THEN GOTO 5900
5820 LET MFOOTER$(2)="Searching For "+$22$+KEYWORD$+$22$+" "+F4$
5825 PRINT @(0,RING),'CL',
5830 IF EOF THEN LET KEYWORD$="",BOTTOM=0,EOF=1
5840 GOTO 5100
5850 REM " --- Right/Left"
5860 LET SIDE$=SELECTION$
5890 GOTO 5300
5900 REM " --- Return for more report text"
5990 RETURN 
6000 REM " --- Define And Initialize Header"
6010 DIM GRAPH$(DHEIGHT-2,"N")
6020 LET HEADER$="",FIRST=0,LINES=0,SELECT$="OFF",CURRENT=1
6030 LET L9=ROW-1
6100 REM " --- Fill instruction window"
6110 CALL "SYC.WA",0,EWIDTH,EHEIGHT,EWIN_X,EWIN_Y,"",ENAME$
6120 DIM EHEADER$(EWIDTH-2),EFOOTER$(EWIDTH-2)
6130 LET EHEADER$(1)=" Report Header Definition"
6140 LET EFOOTER$(INT(EWIDTH/2))="F4=End"
6150 CALL "SYC.WB",ENAME$,0
6160 PRINT 'CS','SB','BR',EHEADER$,@(0,EHEIGHT-3),EFOOTER$,'ER','SF',@(1,2),"Up & Down Arrow To Move",@(1,3),"Right Arrow To Select",@(1,4),"Left Arrow To Deselect",
6200 REM " --- Fill selection window"
6210 CALL "SYC.WA",0,DWIDTH,DHEIGHT,DWIN_X,DWIN_Y,DTITLE$,DNAME$
6220 CALL "SYC.WB",DNAME$,0
6230 PRINT 'CS',
6240 FOR X=1 TO DHEIGHT-2
6250 IF GRAPH$(X,1)="N" THEN PRINT @(0,X-1),'CL',FILL(6),@(1),X,FILL(5),
6260 IF GRAPH$(X,1)="Y" THEN PRINT @(0,X-1),'BR',FILL(6),@(1),X,FILL(5),'ER',
6270 NEXT X
6400 REM " --- Indicate current status"
6410 IF SELECT$="ON" THEN GOTO 6440
6420 LET TOKEN$=">"
6430 GOTO 6450
6440 LET TOKEN$="<"
6450 PRINT @(0,CURRENT-1),TOKEN$,
6500 REM " --- User input"
6510 LET V0$="Sa",V1$="a",V2$="",V3$="",V4$="",V0=0,V1=5,V2=CURRENT-1
6520 GOSUB 7000
6525 IF V3=5 OR V3=6 THEN GOTO 6800
6530 LET FKEY=V3
6540 IF FKEY=0 THEN GOTO 6800
6545 IF FKEY=4 THEN GOTO 6800
6550 IF FKEY=-1 THEN GOTO 6600
6555 IF FKEY=-2 THEN GOTO 6650
6560 IF SELECT$="ON" THEN GOTO 6570
6563 PRINT @(0,CURRENT-1),'CL',@(1),CURRENT,FILL(5),
6565 LET GRAPH$(CURRENT,1)="N"
6567 GOTO 6580
6570 PRINT @(0,CURRENT-1),'BR',FILL(6),@(1),CURRENT,FILL(5),'ER',
6575 LET GRAPH$(CURRENT,1)="Y"
6580 IF FKEY=-3 THEN GOTO 6700
6585 IF FKEY=2 THEN GOTO 6750
6590 GOTO 6400
6600 REM " --- Right/Left arrow"
6610 LET SELECT$="ON",ON=CURRENT
6640 GOTO 6400
6650 REM " --- Left arrow"
6660 LET SELECT$="OFF",OFF=CURRENT
6690 GOTO 6400
6700 REM " --- Down arrow"
6710 LET CURRENT=CURRENT+1
6720 IF CURRENT>DHEIGHT-2 THEN LET CURRENT=1
6740 GOTO 6400
6750 REM " --- Up arrow"
6760 LET CURRENT=CURRENT-1
6770 IF CURRENT<1 THEN LET CURRENT=DHEIGHT-2
6790 GOTO 6400
6800 REM " --- Header selection completed"
6810 CALL "SYC.WD",DNAME$
6820 CALL "SYC.WD",ENAME$
6830 LET FIRST=POS("Y"=GRAPH$),LINES=1
6840 IF FIRST=0 THEN GOTO 6990
6850 FOR X=FIRST TO DHEIGHT-2
6860 IF GRAPH$(X,1)="Y" THEN LET LINES=LINES+1
6870 NEXT X
6900 REM " --- Retrieve and store header text"
6920 LET POS_1=1+(FIRST-1)*H0,LENGTH=LINES*H0
6930 LET DISPLAY$=HEADER$+T$
6940 LET HEADER$=DISPLAY$(POS_1,LENGTH)
6990 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8075 DEF FNN$(Q1,Q2,Q$)=FNR$(STR(MIN((Q1*100)/Q2,99.99):Q$))
8080 DEF FNR$(Q$)=Q$+FILL(8-LEN(Q$))
8090 DEF FNP$(Q$)=CVS(Q$,2)
8095 DEF FNX(Q)=((Q-1)*9)+1
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,E0,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to calling program"
9910 CLOSE (REPORT_DEV,ERR=9911)
9920 IF KNAME$<>"" THEN CALL "SYC.WD",KNAME$
9925 IF DNAME$<>"" THEN CALL "SYC.WD",DNAME$
9930 IF ENAME$<>"" THEN CALL "SYC.WD",ENAME$
9935 IF SCREEN$<>"" THEN CALL "SYC.WD",SCREEN$
9950 EXIT 
9999 END

0010 REM "SYS - GENERAL Report Writer Version 4 Front End Program"
0020 REM "Program SYS.RR"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="GEN4MST",OPTIONS$[1]="F",FILES$[2]="SYS-01"
0140 CALL "SYC.DA",1,1,2,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET GEN4MST_DEV=CHANNELS[1],SYS01_DEV=CHANNELS[2]
0200 REM " --- IOList's"
0220 GEN4IOL: IOLIST USER1$,USER2$
0260 SYS01T: IOLIST TERMINAL$,F0$,F1$,F2$,F3$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0500 REM " --- Initializations"
0510 LET X0$="U:C(1),ID:C(20),A00:C(3)",X1$="NAME:C(40),ACL:C(1),PASSWORD:C(4),FILL:C(56)"
0520 DIM USER1$:X0$,USER2$:X1$
0530 LET USER1.U$="U",USER1.A00$="A00"
0540 LET GENUSER$=F0$(4,3)
0550 LET COMMAND$=CVS(F1$,3)
1000 REM " --- Verify user"
1010 LET KEY1$="U"+GENUSER$+FILL(20-LEN(GENUSER$))+"A00"
1020 GOSUB READGEN4MST
1030 IF STATUS=0 THEN GOTO 1100
1040 LET USER1.ID$=GENUSER$,USER1.NAME$="Auto Generated User "+GENUSER$,USER2.ACL$=F0$(15,1)
1050 GOSUB WRITEGEN4MST
1060 IF STATUS THEN LET GENUSER$="GEN"
1100 REM " --- Verify report"
1110 LET KEY1$="P"+COMMAND$+FILL(20-LEN(COMMAND$))+"A  "
1120 GOSUB READGEN4MST
1130 IF STATUS=0 THEN GOTO 1200
1150 LET KEY1$="R"+COMMAND$+FILL(20-LEN(COMMAND$))+"A00"
1160 GOSUB READGEN4MST
1170 IF STATUS THEN GOTO 4700
1200 REM " --- Setup Common Global Variables"
1210 LET X0$=STBL("*GENEXT_USER",GENUSER$)
1220 LET X0$=STBL("*GEN_QUIET","Y")
1230 LET X0$=STBL("*GEN_GO",COMMAND$)
1240 LET X0$=STBL("*GEN_GOEXIT","GE4.AA")
4000 REM " --- Run GEN"
4090 RUN "GE4.AA",ERR=4800
4700 REM " --- Report not defined"
4710 LET V4$="Report "+COMMAND$+" has not been defined in the GENERAL."
4790 GOTO 4900
4800 REM " --- Missing GENMST"
4810 LET V4$="The GENERAL Report Writer Has Not Been Installed."
4900 REM " --- Display windowed message and prompt for input"
4910 DIM MESS$[1]
4920 LET MESS$[0]=V4$+" (<Enter>=Continue)"
4940 CALL "SYC.XA",2,MESS$[ALL],0,22,-1,V$,V3
4990 GOTO 9900
6000 READGEN4MST: REM " --- Verify keys in GEN4MST"
6010 LET STATUS=1
6020 FIND (GEN4MST_DEV,KEY=KEY1$,DOM=6090)
6030 LET STATUS=0
6090 RETURN 
6100 WRITEGEN4MST: REM " --- Add User to GEN4MST"
6110 LET STATUS=1
6120 WRITE (GEN4MST_DEV,KEY=USER1$,DOM=6190)IOL=GEN4IOL
6130 LET STATUS=0
6190 RETURN 
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 REM " --- Return to menu"
9920 RUN "SYS.AA"
9999 END

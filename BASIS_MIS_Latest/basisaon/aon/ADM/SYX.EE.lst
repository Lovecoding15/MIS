0010 REM "SYX - Generate Executive Summary (A/R)"
0020 REM "Program SYX.EE"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=2
0120 DIM FILES$[FILES],CHANNELS[FILES],OPTIONS$[FILES]
0130 LET FILES$[1]="ART-01",FILES$[2]="ART-11"
0200 REM " --- IOLIST's"
0210 SYE02A: IOLIST A0$(1),A1$(1),A[ALL],U[ALL]
0220 ART01A: IOLIST B0$(1),B[ALL]
0230 ART11A: IOLIST C0$(1),C1$(1),C[ALL]
0260 SYM06A: IOLIST N0$,N1$(1)
0270 SYM04A: IOLIST X$,S$(1)
0400 REM " --- Parameters"
0410 LET PROGRAM$="SYX.EF"
0420 FIND (SYM04_DEV,KEY="AR",DOM=4000)IOL=SYM04A
0430 IF S$(39,1)<>"Y" THEN GOTO 4000
0440 LET TITLE$=S$(1,35)
0500 REM " --- Initialize"
0510 DIM A0$(8),A1$(32),A[31],U[31],B0$(50),B[1],C0$(19),C1$(11),C[1]
0600 REM " --- Additional File Opens"
0610 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0620 IF STATUS THEN GOTO 9900
0630 LET ART01_DEV=CHANNELS[1],ART11_DEV=CHANNELS[2]
0700 REM " --- Background"
0710 PRINT @(40,5),TITLE$,'CL',@(40,7),'CL',@(COLUMN,11),FILL(K9),
1000 REM " --- For each firm"
1010 FOR FIRM=1 TO FIRMS STEP 2
1020 LET N0$=FIRMS$(FIRM,2)
1030 FIND (SYM06_DEV,KEY=N0$,DOM=3900)IOL=SYM06A
1040 PRINT @(40,7),N0$," ",N1$
1400 REM " --- Parameters"
1410 FIND (SYS01_DEV,KEY=N0$+"AR00",DOM=3900)
1900 REM " --- Position ART-01"
1910 LET UNITS=0
1990 READ (ART01_DEV,KEY=N0$,DOM=2000)
2000 REM " --- Read next order"
2010 LET K$=KEY(ART01_DEV,END=3900)
2020 IF POS(N0$=K$)<>1 THEN GOTO 3900
2030 PRINT @(COLUMN,11),K$(5,6)," ",K$(11,7),
2040 READ (ART01_DEV)IOL=ART01A
2050 LET T=B[0],UNITS=1
2090 READ (ART11_DEV,KEY=B0$(1,19),DOM=2100)
2100 REM " --- Process detail records"
2110 LET K$=KEY(ART11_DEV,END=2400)
2120 IF POS(B0$(1,17)=K$)<>1 THEN GOTO 2400
2130 READ (ART11_DEV)IOL=ART11A
2140 LET T=T+C[0]+C[1]
2190 GOTO 2100
2400 REM " --- Assemble data for update"
2410 LET X$=FNB$(B0$(27,3))
2420 IF FNP$(B0$(27,3))="" THEN LET X$=FNB$(B0$(24,3))
2430 LET M$=X$(1,2),D$=X$(4,2),Y$=STR(1900+FNYY_YEAR(X$(7,2)):"0000"),D=NUM(D$)
2700 REM " --- Update SYE-02"
2710 GOSUB 6000
2900 REM " --- Loop back for next record"
2990 GOTO 2000
3900 REM " --- Next firm"
3990 NEXT FIRM
4000 REM " --- Run next overlay"
4010 FOR X=1 TO FILES
4020 CLOSE (CHANNELS[X],ERR=4030)
4030 NEXT X
4090 RUN PROGRAM$,ERR=9900
6000 REM " --- Update SYE-02 Daily Totals"
6010 DIM A0$(8),A1$(32),A[31],U[31]
6030 LET A0$(1)=N0$+Y$+M$
6040 FIND (SYE02_DEV,KEY=A0$,DOM=6050)IOL=SYE02A
6050 LET A[D]=A[D]+T,U[D]=U[D]+UNITS,UNITS=0
6060 WRITE (SYE02_DEV,KEY=A0$)IOL=SYE02A
6090 RETURN 
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8070 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

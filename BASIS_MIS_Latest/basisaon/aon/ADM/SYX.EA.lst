0010 REM "SYX - Generate Executive Summary"
0020 REM "Program SYX.EA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0050 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open Files"
0105 LET FILES=13
0110 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0120 LET FILES$[1]="SYS-01",FILES$[2]="SYM-04",FILES$[3]="SYM-06"
0125 LET FILES$[4]="SYE-01",FILES$[5]="SYE-02",FILES$[6]="SYE-03"
0130 LET FILES$[7]="SYE-04",FILES$[8]="SYE-05",FILES$[9]="SYE-06"
0135 LET FILES$[10]="SYE-07",FILES$[11]="SYE-08",FILES$[12]="SYE-09"
0140 LET FILES$[13]="SYE-10"
0150 FOR X=4 TO FILES
0155 LET OPTIONS$[X]="CL"
0160 NEXT X
0170 CALL "SYC.DA",1,1,3,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0180 IF STATUS>0 THEN GOTO 9900
0190 LET SYS01_DEV=CHANNELS[1],SYM04_DEV=CHANNELS[2],SYM06_DEV=CHANNELS[3]
0200 REM " --- IOLIST's"
0210 SYE01A: IOLIST A0$(1),A1$(1)
0220 SYM06A: IOLIST B0$(1),B1$(1)
0270 SYS01T: IOLIST X$,F0$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0500 REM " --- Initialize"
0510 DIM A0$(2),A1$(32),B0$(2),B1$(40),N1$(35),S$(40),INFO$[20]
0550 LET YY$=F0$(18,2),MM$=F0$(20,2),DD$=F0$(22,2),K9=29,FIRMS$=""
0560 LET YEAR$=STR(1900+FNYY_YEAR(YY$):"0000"),PGM$="SYX.EB"
0570 LET A1$(1,8)=YEAR$+MM$+DD$,WHEN$=A1$(1,6),LAST$=WHEN$
0700 REM " --- Background"
0710 FOR X=4 TO 8 STEP 2
0720 CALL "SYC.WC",1,0,80,0,0,X,0
0730 NEXT X
0740 PRINT 'SB',@(34,3),"As of ",'SF',FNB$(F0$(7,3)),'SB',@(34,5),"Phase",@(35,7),"Firm",'SF',
0800 REM " --- OK to continue?"
0810 LET V4$="OK To Generate The Executive Summary"
0820 CALL "SYC.YN",0,V4$,0,V$,V3
0830 IF V$<>"YES" THEN GOTO 9900
0850 REM " --- Open/clear remaining files"
0860 CALL "SYC.DA",1,4,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0865 IF STATUS THEN GOTO 9900
0870 LET SYE01_DEV=CHANNELS[4],SYE02_DEV=CHANNELS[5],SYE03_DEV=CHANNELS[6]
0875 LET SYE04_DEV=CHANNELS[7],SYE05_DEV=CHANNELS[8],SYE06_DEV=CHANNELS[9]
0880 LET SYE07_DEV=CHANNELS[10],SYE08_DEV=CHANNELS[11],SYE09_DEV=CHANNELS[12]
0890 LET SYE10_DEV=CHANNELS[13]
0900 REM " --- Position file"
0970 CALL "SYC.NB","Processing",K9,COLUMN
0980 PRINT @(40,5),"Valid Firms"
0990 READ (SYM06_DEV,KEY="",DOM=1000)
1000 REM " --- Build list of valid firms"
1010 LET K$=KEY(SYM06_DEV,END=4000)
1020 READ (SYM06_DEV)IOL=SYM06A
1030 LET FIRMS$=FIRMS$+K$
1040 PRINT @(40,7),B0$," ",B1$(1,37),
2000 REM " --- Update SYE-01"
2010 LET A0$(1,2)=K$
2020 WRITE (SYE01_DEV,KEY=A0$)IOL=SYE01A
3900 REM " --- Loop back for next record"
3990 GOTO 1000
4000 REM " --- Run next overlay"
4010 LET FIRMS=LEN(FIRMS$)
4090 RUN PGM$,ERR=9900
8000 REM " --- Functions"
8010 DEF FNA$(Q$,Q2$)=STR(MOD((ASC(Q$)-32)*POS(" "<>Q2$(2,1)),100):"00")
8020 DEF FNB$(Q1$)=FNA$(Q1$(2),Q1$)+"/"+FNA$(Q1$(3),Q1$)+"/"+FNA$(Q1$(1),Q1$)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
8170 REM " --- FNYY_YEAR Convert 2-Char Year to 21st Century Numeric Year"
8175 DEF FNYY_YEAR(Q1$)
8180 LET Q=NUM(FNYY21_YY$(Q1$)); IF Q<50 THEN LET Q=Q+100
8185 RETURN Q
8190 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9900 RUN "SYS.AA"
9999 END

0010 REM "SYR - On-Line Help Listing"
0020 REM "Program SYR.HA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0070 BEGIN 
0080 SETERR 9000
0090 SETESC 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYS-01",FILES$[2]="SYS-11",FILES$[3]="SYS-21"
0135 LET FILES$[4]="SYM-01",FILES$[5]="SYM-02",FILES$[6]="SYM-04"
0140 CALL "SYC.DA",1,1,FILES,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0150 IF STATUS>0 THEN GOTO 9900
0160 LET SYS01_DEV=CHANNELS[1],SYS11_DEV=CHANNELS[2],SYS21_DEV=CHANNELS[3]
0170 LET SYM01_DEV=CHANNELS[4],SYM02_DEV=CHANNELS[5],SYM04_DEV=CHANNELS[6]
0200 REM " --- IOLIST's"
0210 SYS11A: IOLIST A0$(1),A1$(1),A2$(1)
0220 SYS21A: IOLIST W0$(1),W1$(1)
0230 SYM01A: IOLIST B0$(1),B1$(1),B2$(1)
0240 SYM02A: IOLIST C0$(1),C1$(1)
0250 SYM04A: IOLIST D0$(1),D1$(1)
0270 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9900)IOL=SYS01T
0420 LET N0$=F0$(16,2),LINES=10,CHARS=70
0500 REM " --- Initializations"
0510 DIM HEADINGS$[2],IMAGE$[LINES](CHARS),A0$(14),A1$(40),A2$(16),W0$(17)
0520 DIM W1$(CHARS),B0$(6),B1$(35),B2$(16),C0$(3),C1$(24),D0$(2),D1$(35)
0550 LET WIDTH=80,PAGE=0,L9=59,L=L9+1,HEADINGS$[0]=F4$,HEADINGS$[1]=F5$
0560 LET WHEN$=F0$(7,3),CLOCK$="",BEGSUBJECT$="",ENDSUBJECT$="",HEADINGS=1
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,4,0
0720 CALL "SYC.WC",1,0,80,0,0,6,0
0750 PRINT 'SB',@(21,3),"Beginning Subject:",@(24,5),"Ending Subject:",'SF',
0800 REM " --- Beginning Subject"
0803 LET V0$="S",V1$="C",V2$=BEGSUBJECT$,V3$="",V0=6,V1=40,V2=3
0805 LET V4$="Enter The Beginning Subject To Include On This Report (<F1>=First/<F3>=Lookup)"
0807 GOSUB 7000
0810 ON V3 GOTO 0825,0840,0800,0815,9900
0815 GOSUB 6000
0820 IF V$="" THEN GOTO 0800
0825 IF V$="" THEN GOTO 0840
0830 LET BEGSUBJECT$=V$
0835 GOTO 0845
0840 LET BEGSUBJECT$="",V$="First"
0845 PRINT @(V1,V2),'CL',V$,
0850 REM " --- Ending Subject"
0853 LET V0$="S",V1$="C",V2$=ENDSUBJECT$,V3$="",V0=6,V1=40,V2=5
0855 LET V4$="Enter The Ending Subject To Include On This Report (<F1>=Last/<F3>=Lookup)"
0857 GOSUB 7000
0860 ON V3 GOTO 0875,0890,0800,0865,9900
0865 GOSUB 6000
0870 IF V$="" THEN GOTO 0850
0875 IF V$="" THEN GOTO 0890
0880 LET ENDSUBJECT$=V$
0885 GOTO 0895
0890 LET ENDSUBJECT$="",V$="Last"
0895 PRINT @(V1,V2),'CL',V$,
0900 REM " --- OK to continue?"
0910 CALL "SYC.YN",1,"Is The Above Information Correct",2,V$,V3
0920 ON V3 GOTO 0930,0900,0900,0900,9900
0930 ON POS(V$="YN") GOTO 0900,0950,0800
0950 REM " --- Position File"
0960 CALL "SYC.GA",7,1,"","",STATUS
0965 IF STATUS THEN GOTO 9900
0970 CALL "SYC.NB","Printing",8,COLUMN
0980 LET A0$(1)=N0$+BEGSUBJECT$,ENDSUBJECT=LEN(ENDSUBJECT$)
0990 READ (SYS11_DEV,KEY=A0$,DOM=1000)
1000 REM " --- Read next header record"
1010 LET K$=KEY(SYS11_DEV,END=4000)
1020 IF ENDSUBJECT$<>"" THEN IF K$(3,ENDSUBJECT)>ENDSUBJECT$ THEN GOTO 4000
1030 PRINT @(COLUMN,11),K$(3,8),
1040 READ (SYS11_DEV)IOL=SYS11A
1100 REM " --- Is help for a system?"
1110 LET REMARK$="",D0$(1)=K$(3)
1120 FIND (SYM04_DEV,KEY=D0$,DOM=1200)IOL=SYM04A
1130 LET REMARK$=D1$
1190 GOTO 1800
1200 REM " --- Is help for a menu?"
1210 LET B0$(1)=K$(3),B1$(1)=""
1220 FIND (SYM01_DEV,KEY=B0$,DOM=1300)IOL=SYM01A
1230 LET REMARK$=B1$
1300 REM " --- Must be help for a program"
1310 LET LINENUM$="00010",PGM$=FNP$(K$(3,8))
1320 CALL "SYC.UB",0,PGM$,X$,LINENUM$,STATUS
1330 IF FNP$(X$)="" OR STATUS THEN GOTO 1800
1340 LET REMARK$=X$,X=POS($22$=REMARK$)
1350 IF X>0 THEN LET REMARK$=REMARK$(X+1)
1360 LET P=LEN(REMARK$)
1370 IF REMARK$(P,1)=$22$ THEN LET REMARK$=REMARK$(1,P-1)
1380 IF LEN(REMARK$)>50 THEN LET REMARK$=REMARK$(1,50)
1800 REM " --- Revision information"
1810 LET REVISION$=FNB6$(A2$(1,6)),C0$(1)=A2$(7,3),C1$(1)="(Unknown)"
1820 FIND (SYM02_DEV,KEY=C0$,DOM=1830)IOL=SYM02A
1900 REM " --- Position detail file"
1910 LET HELPPAGE=0,LINE=0,NUMBER=0
1990 READ (SYS21_DEV,KEY=A0$,DOM=2000)
2000 REM " --- Read next detail record"
2010 LET K$=KEY(SYS21_DEV,END=3000)
2020 IF POS(A0$=K$)<>1 THEN GOTO 3000
2030 READ (SYS21_DEV)IOL=SYS21A
2100 REM " --- Store line"
2110 LET LINE=LINE+1
2120 IF LINE>LINES THEN GOSUB 5200
2130 LET IMAGE$[LINE](1)=W1$
2900 REM " --- Loop back for next detail"
2990 GOTO 2000
3000 REM " --- Print last page"
3010 GOSUB 5200
3900 REM " --- Loop back for next header"
3990 GOTO 1000
4000 REM " --- All done"
4090 GOTO 9900
5000 REM " --- Report heading"
5010 LET L=HEADINGS+1
5020 CALL "SYC.HA",7,HEADINGS$[ALL],HEADINGS,PAGE,WIDTH,WHEN$,CLOCK$,STATUS
5030 IF STATUS>0 THEN EXITTO 9900
5090 RETURN 
5200 REM " --- Print header information"
5210 IF L+LINES+6>L9 THEN GOSUB 5000
5220 LET L=L+LINES+6,HELPPAGE=HELPPAGE+1
5230 PRINT (7)""
5240 PRINT (7)@(4),"Subject: ",A0$(3,8)," ",REMARK$,@(69),"Page",HELPPAGE:"##0"
5250 PRINT (7)@(4),"  Topic: ",A1$
5260 PRINT (7)@(4),"Revised: ",REVISION$," By ",C1$
5300 REM " --- Print window image"
5310 PRINT (7)@(4),FILL(CHARS+2,"-")
5320 FOR LINE=1 TO LINES
5330 PRINT (7)@(3),"| ",IMAGE$[LINE]," |"
5340 NEXT LINE
5350 PRINT (7)@(4),FILL(CHARS+2,"-")
5400 REM " --- Reinitialize window image"
5410 DIM IMAGE$[LINES](CHARS)
5420 LET LINE=1
5490 RETURN 
6000 REM " --- Help Lookup"
6010 CALL "SYC.HL",0,CHANNELS[ALL],NAME$,"",V$,V3
6020 IF V$<>"" THEN LET V$=V$(1,8)
6090 RETURN 
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8020 DEF FNB6$(Q$)=Q$(3,2)+"/"+Q$(5,2)+"/"+FNYY21_YY$(Q$(1,2))
8090 DEF FNP$(Q$)=CVS(Q$,2)
8145 REM " --- FNYY21_YY$ Un-Convert 21st Century 2-Char Year to 2-Char Year"
8150 DEF FNYY21_YY$(Q1$)
8155 LET Q3$=" 01234567890123456789",Q1$(1,1)=Q3$(POS(Q1$(1,1)=" 0123456789ABCDEFGHIJ"))
8160 RETURN Q1$
8165 FNEND
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to menu"
9950 RUN "SYS.AA"
9999 END

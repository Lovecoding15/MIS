0010 REM "SYX - SpeedInstall Utility (Header)"
0020 REM "Program SYX.JA"
0022 REM "+-----------------------------------------------------------+"
0024 REM "| ADD+ON Software, Inc. Advantage/V Version 6.0.0 - 15May96 |"
0026 REM "|      Copyright (c) 1996 Open Systems Holdings Corp.       |"
0028 REM "|                  All Rights Reserved                      |"
0030 REM "+-----------------------------------------------------------+"
0040 REM 
0060 BEGIN 
0085 SETESC 9000
0090 SETERR 9000
0100 REM " --- Open/Lock Files"
0110 LET FILES=6
0120 DIM FILES$[FILES],OPTIONS$[FILES],CHANNELS[FILES]
0130 LET FILES$[1]="SYM-03",FILES$[2]="SYM-06",FILES$[3]="SYM-99"
0135 LET FILES$[4]="SYS-01"
0150 CALL "SYC.DA",1,1,4,FILES$[ALL],OPTIONS$[ALL],CHANNELS[ALL],BATCH,STATUS
0160 IF STATUS>0 THEN GOTO 9900
0170 LET SYM03_DEV=CHANNELS[1],SYM06_DEV=CHANNELS[2],SYM99_DEV=CHANNELS[3]
0175 LET SYS01_DEV=CHANNELS[4]
0200 REM " --- IOLIST's"
0260 SYS01T: IOLIST X$,F0$,X$,X$,X$,F4$,F5$
0265 SYM06A: IOLIST C0$(1),C1$(1)
0400 REM " --- Parameters"
0410 FIND (SYS01_DEV,KEY="T"+FID(0),DOM=9800)IOL=SYS01T
0420 LET N0$=F0$(16,2),N1$=F4$,N2$="SY",N3$=F5$
0500 REM " --- Initializations"
0520 DIM C0$(2),C1$(40)
0530 LET SOURCE$="99",TARGET$=N0$,I_R$="I"
0700 REM " --- Background"
0710 CALL "SYC.WC",1,0,80,0,0,6,0
0750 PRINT 'SB',@(3,3),"Source Firm:",@(3,4),"Target Firm:",@(0,5),"Install/Remove:",@(0,7),"File",@(7),"Description",@(37),"Rec",@(41),"Description",@(71),"Mod",@(75),"OK?",
1000 REM " --- Source Firm"
1005 PRINT 'CF',
1010 LET V0$="Z",V1$="C",V2$=SOURCE$,V0=2,V1=16,V2=3,I0=0
1020 LET V4$="Enter a Valid Company Number (<F3>=Lookup)"
1030 GOSUB 7000
1040 ON V3 GOTO 1060,1000,1000,1050,9900
1050 CALL "SYC.LX",V$
1060 FIND (SYM06_DEV,KEY=V$,DOM=1000)IOL=SYM06A
1070 LET SOURCE$=V$
1080 PRINT @(V1,V2),'CL',SOURCE$,@(V1+3,V2),C1$,
1090 GOTO 3900
1200 REM " --- Target Firm"
1210 LET V0$="Z",V1$="C",V2$=TARGET$,V0=2,V1=16,V2=4
1220 LET V4$="Enter a Valid Company Number (<F3>=Lookup)"
1230 GOSUB 7000
1240 ON V3 GOTO 1260,1200,3900,1250,9900
1250 CALL "SYC.LX",V$
1260 FIND (SYM06_DEV,KEY=V$,DOM=1200)IOL=SYM06A
1270 IF V$=SOURCE$ THEN GOTO 1200 ELSE LET TARGET$=V$
1280 PRINT @(V1,V2),'CL',TARGET$,@(V1+3,V2),C1$,
1290 GOTO 3900
1400 REM " --- Install/Remove"
1410 LET V0$="S",V1$="^C",V2$=I_R$,V3$="IR",V0=1,V1=16,V2=5
1420 LET V4$="I=Install   R=Remove"
1430 GOSUB 7000
1440 ON V3 GOTO 1450,1400,3900,1400,9900
1450 IF POS(V$="IR")=0 THEN GOTO 1400
1460 LET I_R$=V$
1470 IF I_R$="I" THEN LET X$="Install" ELSE LET X$="Remove"
1480 PRINT @(V1,V2),'CL',X$,
1490 GOTO 3900
3900 REM " --- Next Field"
3910 IF V3<>2 THEN GOTO 3940
3920 IF I0>0 THEN LET I0=I0-1
3930 GOTO 3950
3940 LET I0=I0+1
3960 ON I0 GOTO 1000,1200,1400,4000
4000 REM " --- Correct?"
4010 LET V4$="Is the above information correct (Y/N)?"
4020 LET V0$="Y",V1$="C",V2$="N",V3$="",V0=1,V1=FNV(V4$),V2=22
4030 GOSUB 7000
4040 ON V3 GOTO 4050,4000,4000,4000,9900
4090 ON POS(V$="YN") GOTO 4000,4100,1000
4100 REM " --- Run Detail Overlay"
4120 RUN "SYX.JB"
7000 REM " --- Standard Input Routine (15May95)"
7010 CALL "SYC.IA",V0$,V1$,V2$,V3$,V4$,PGM(-2),V0,V1,V2,V$,V,V3,DISPLAY_LEN
7020 IF V3=999 THEN GOTO 7200
7030 IF V3=127 THEN GOTO 7100
7090 RETURN 
7100 REM " --- Escape During Input"
7110 CALL "SYC.ES",ERR=7200,PGM(-2),TCB(8),E$,E2,V3
7120 IF V3<>127 THEN GOTO 7000
7130 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
7200 REM " --- Error During Input"
7210 ESCAPE
7290 GOTO 7000
8000 REM " --- Functions"
8090 DEF FNV(Q$)=42+INT(LEN(Q$)/2)
9000 REM " --- Standard Error Routine (15May95)"
9010 IF ERR=127 THEN GOTO 9300
9020 CALL "SYC.EA",ERR=9090,EXIT_CTRL,PGM(-2),ERR,TCB(5),E$,E1,E2
9030 IF E1=1 THEN GOTO 9100
9040 IF E1=3 THEN GOTO 9200
9080 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9090 ESCAPE
9100 REM " --- Retry"
9190 RETRY
9200 REM " --- Return"
9290 GOTO 9900
9300 REM " --- Standard Escape Routine"
9310 CALL "SYC.ES",ERR=9350,PGM(-2),TCB(8),E$,E2,V3
9320 IF V3<>127 THEN GOTO 9390
9330 PRINT @(0,E2),'CL','CURSOR'("ON"),E$,'LF'
9350 ESCAPE
9390 RETURN 
9800 REM " --- Display Parameter record error"
9810 LET LINE_ERR$=LST(PGM(TCB(5)))
9820 CALL "SYC.YA",LINE_ERR$,N2$,ERR
9900 REM " --- Return to Menu"
9950 RUN "SYS.AA"
9999 END

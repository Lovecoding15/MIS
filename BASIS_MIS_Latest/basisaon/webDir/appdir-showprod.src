0010 CALL "utaddr.wbb"
0020 CALL "utcgi.wbb",ENV$,CGI$,HTML$
0030 LET A$=OPTS; LET A$(4,1)=IOR($20$,A$(4,1)); SETOPTS A$
0040 LET APP$=STBL("DATA_SERVER")+STBL("ADIR")
0050 gosub get_country
0060 LET HTML$=header$
0070 LET SELECTION$=CGI.SEARCHTEXT$
0080 while LEN(SELECTION$)<24 
0090   SELECTION$=SELECTION$+" "
0100 wend
0110 CALL "uttags.wbb","H2",txt1$,HTML$
0120 CALL "uttags.wbb","H2",txt2$,HTML$
0130 rem LET TRACELOG$="/home/basis/pro5/appdir/trace.log",DEBUG=UNT; OPEN (DE
0130:BUG)TRACELOG$; SETTRACE (DEBUG)
0140 LET DATA_RECP$="FIRM:C(2),CUSTID:C(8*=10),NAME:C(36*=10),ADDRESS1:C(32*=1
0140:0),ADDRESS2:C(32*=10),CITY:C(32*=10),STATE:C(20*=10),POSTALCODE:C(10*=10)
0140:,COUNTRY:C(16*=10),CONTACT:C(32*=10),PHONE:C(18*=10),FAX:C(18*=10),EMAIL:
0140:C(50*=10),WEBSITE:C(50*=10),CAT1:C(32),CAT2:C(32),CAT3:C(32),CAT4:C(32),C
0140:AT5:C(32),TEXT1:C(36*=10),TEXT2:C(36*=10),GRAPHIC1:C(36*=10),GRAPHIC2:C(3
0140:6*=10),HTML:C(36*=10),LANGUAGE:C(1*=10),SHOW:C(1*=10),KEYWORDS:C(36*=10)"
0150 gosub dimrs
0160 CALL "uttags.wbb","H2",txt3$,HTML$
0170 REM define the template
0180 DIM DATARECP$:DATA_RECP$
0190 DIM DATARECR$:DATA_RES$
0200 REM open the datafile
0210 LET RSL=UNT; OPEN (RSL)STBL("DATA_SERVER")+STBL("PDIR")+"reseller"
0220 LET PROD=UNT; OPEN (PROD)APP$+"PRODUCTS"
0230 REM loop the file and build the option entries for the select control
0240 EXTRACT (RSL,KEY="",KNUM=1,DOM=main_loop)
0250 MAIN_LOOP: 
0260 READ RECORD(RSL,END=END_LOOP)DATARECR$
0265 if datarecr.firm$<>cfirm$ then goto main_loop
0270 i=0,k$=datarecr.firm$+DATARECR.CUST_NUM$
0280 while i<100
0290 READ RECORD(PROD,KEY=K$+STR(I:"00"),DOM=*break)DATARECP$;i=i+1
0300 DIM CATS$(325); LET CATS$=DATARECP.CAT1$+DATARECP.CAT2$+DATARECP.CAT3$+DA
0300:TARECP.CAT4$+DATARECP.CAT5$
0310 IF POS(SELECTION$=CATS$)=0 THEN continue
0320 LET TYPE$=DATARECP.NAME$+"-"+DATARECR.COMPANY$+FILL(180," ")
0330 LET TYPE$=TYPE$(1,80)
0340 if POS(TYPE$+$0A$=LIST$) then continue
0350 LIST$=LIST$+"<option value="+datarecp.firm$+DATARECP.CUSTID$+">"+TYPE$+$0
0350:D0A$
0360 wend
0370 GOTO MAIN_LOOP
0380 END_LOOP: 
0390 endtrace
0400 REM create the hidden control
0410 CALL "uttags.wbb","hidden style","COMPANYNAME",HIDDEN$
0420 REM create the select control
0430 LET LIST$=HIDDEN$+"<SELECT NAME="+CHR(34)+"searchtext"+CHR(34)+" SIZE=8>"
0430:+LIST$+"</SELECT>"
0440 REM add the submit / search button
0450 CALL "uttags.wbb","br","",LIST$
0460 CALL "uttags.wbb","br","",LIST$
0470 CALL "uttags.wbb","submit choice",btn$,LIST$
0480 CALL "uttags.wbb","br","",LIST$; CALL "uttags.wbb","br","",LIST$
0490 REM CALL "uttags.wbb","href appdir.sh","Return to the Menu",LIST$
0500 CALL "uttags.wbb","br","",LIST$
0510 REM wrap the form tags around the form with the post url
0520 CALL "uttags.wbb","form appdir-show.sh",LIST$,HTML$
0530 LET HTML$=HTML$+footer$
0595 CALL "uthtmout.wbb",ENV$,HTML$+"",HTML$,""
0605 CALL "utsend.wbb",HTML$
0620 CALL "utexit.wbb",CGI$
0625 rem gosub save_html
0630 RELEASE 
0640 get_country:
0650 seterr country_end
0660 country$="US",country$=cvs(CGI.ctry$,7)
0670 country_end: seterr 0
0680 Header$="{include eng_header.txt}",footer$="{include eng_footer.txt}"
0690 txt1$="BASIS International Application/Service Directory."
0700 txt2$="The following are the results of your query:"
0710 txt3$="Select an entry"
0720 btn$="Retrieve",cfirm$="01"
0730 if country$="DE" then header$="{include ger_header.txt}",footer$="{includ
0730:e ger_footer.txt}",cfirm$="02"
0740 if country$="FR" then header$="{include fra_header.txt}",footer$="{includ
0740:e fra_footer.txt}",cfirm$="02"
0770 ttx=unt
0780 open(ttx,err=end_get_country)"appdir_translation.txt"
0790 fin$=fin(ttx)
0800 sz=dec(fin$(1,4))
0810 readrecord(ttx,siz=sz)trans$
0820 srch$=country$+"1";gosub get_translation;if temp$<>"" then txt1$=temp$
0830 srch$=country$+"2";gosub get_translation;if temp$<>"" then txt2$=temp$
0840 srch$=country$+"3";gosub get_translation;if temp$<>"" then txt3$=temp$
0850 srch$=country$+"4";gosub get_translation;if temp$<>"" then btn$=temp$
0890 end_get_country:
0900 return
0910 get_translation:
0920 temp$=""
0930 p=pos(srch$+":"=trans$)
0940 if p=0 then return
0950 temp$=trans$(p+len(srch$))
0960 p=pos("{"=temp$);if p=0 then temp$="";return
0970 temp$=temp$(p+1)
0980 p=pos("}"=temp$);if p<2 then temp$="";return
0990 temp$=temp$(1,p-1)
1000 return

1760 dimrs:
1770 x1$="firm:c(2),cust_num:c(6),password:c(6*),"
1780 xx$="company:c(30*),addr1:c(30),addr2:c(30),addr3:c(30),addr4:c(30),city:
1780:c(25),state:c(2),zip:c(9),country:c(15),phone:c(14),fax:c(14)"
1790 xx$=xx$+",contact1:c(30),cont1_title:c(18),cont1_same:c(1),cont1_email:c(
1790:80),cont1_addr1:c(30),cont1_addr2:c(30),cont1_city:c(25),cont1_state:c(2)
1790:,cont1_zip:c(9),cont1_country:c(24),cont1_phone:c(14),cont1_ext:c(6),cont
1790:1_ap:c(1)"
1800 xx$=xx$+",contact2:c(30),cont2_title:c(18),cont2_same:c(1),cont2_email:c(
1800:80),cont2_addr1:c(30),cont2_addr2:c(30),cont2_city:c(25),cont2_state:c(2)
1800:,cont2_zip:c(9),cont2_country:c(24),cont2_phone:c(14),cont2_ext:c(6),cont
1800:2_adv:c(1),cont2_ann:c(1),cont2_ap:c(1)"
1810 xx$=xx$+",contact3:c(30),cont3_title:c(18),cont3_same:c(1),cont3_email:c(
1810:80),cont3_addr1:c(30),cont3_addr2:c(30),cont3_city:c(25),cont3_state:c(2)
1810:,cont3_zip:c(9),cont3_country:c(24),cont3_phone:c(14),cont3_ext:c(6),cont
1810:3_adv:c(1),cont3_ann:c(1),cont3_ap:c(1)"
1820 xx$=xx$+",contact4:c(30),cont4_title:c(18),cont4_same:c(1),cont4_email:c(
1820:80),cont4_addr1:c(30),cont4_addr2:c(30),cont4_city:c(25),cont4_state:c(2)
1820:,cont4_zip:c(9),cont4_country:c(24),cont4_phone:c(14),cont4_ext:c(6),cont
1820:4_adv:c(1),cont4_ann:c(1),cont4_ap:c(1)"
1830 data_res$=x1$+xx$+",cust_type:c(3)"
1840 return

2500 save_html:
2510 sv$="/home/basis/pro5/appdir/saved_showprod.html"
2520 erase sv$,err=*next
2530 string sv$,err=*next
2540 sv=unt,ok=0
2550 open(sv,err=*next)sv$;ok=1
2560 if ok then print(sv)html$;close(sv)
2590 return

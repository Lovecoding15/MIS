[Program]
Creation Date=2003-11-26
Creation Time=10:32:33
Event Set=LEGACY
Generator Program=gb_func
Last Build Date=2024-01-23
Last Build Time=15:3:4
Precision=2
Program Name=basis_cust
Remarks=Yes
Resource File=basis_cust.arc
Show Forms=All

[EOJ]
rem ' -----------------------------------------------------------------
rem ' EOJ
rem ' -----------------------------------------------------------------
err_exit:
if info(3,2)="thines" then escape

rem ' xresp = msgbox("Error: " + str(err) + " stmt: " + str(tcb(5)) + " " + errmes(-1),0,"debug")

pgm_exit:

rem ' clean up an invoice pdfs copied to the web server
if urlList!.size()
    for wk=0 to urlList!.size()-1
        url$ = urlList!.get(wk)
        BBUtils.deleteFromWebServer(url$)
    next wk
endif

while len(chan$)>2
  close(num(chan$(1,3)),err=*next)
  chan$=chan$(4)
wend
if gb__forms and gb__sysgui then
  for gb__temp=1 to gb__forms
  print (gb__sysgui)'context'(gb__form_context[gb__temp]),'destroy'(0)
  next gb__temp
fi
print (gb__sysgui)'context'(old_context),'focus'(0),'flush'
close (gb__sysgui)
resclose(gb__handle,err=*next)
exit

[Init]
rem ' -----------------------------------------------------------------
rem ' Init
rem ' -----------------------------------------------------------------
rem ' (1) Don't show borders on button controls
rem ' (2) Force XP borders on list and edit controls

seterr err_exit
setesc err_exit

use ::BBUtils.bbj::BBUtils

declare BBjString testPassword!
declare BBjEditBox passwordEdit!
declare BBjSysGui sysgui!
declare BBjTopLevelWindow contactsWin!
declare BBjTopLevelWindow invoiceWin!
declare BBjListBox options!
declare BBjVector urlList!
declare BBjHtmlView htmlviewer!
declare BBjCEdit invoice!

urlList! = bbjapi().makeVector()

sysgui!=bbjapi().getSysGui()
contactsWin! = cast(BBjTopLevelWindow, sysgui!.getWindow("contacts"))
options! = cast(BBjListBox, contactsWin!.getControl("option_list"))

invoiceWin! = cast(BBjTopLevelWindow, sysgui!.getWindow("invoice_history"))
htmlviewer! = cast(BBjHtmlView, invoiceWin!.getControl("InvoiceBrowser"))
invoice! = cast(BBjCEdit, invoiceWin!.getControl("invoice"))

if (info(3,6)="0" and info(0,0)="Windows XP" and info(1,1)>="1.4.2")
:  or (info(3,6)<>"0" and
:  bbjapi().getThinClient(err=*next).getClientOSName(err=*next)="Windows XP" and
:  bbjapi().getThinClient(err=*next).getClientJavaVersion(err=*next)>="1.4.2")
:  then bbjapi().getSysGui().setLookAndFeel("WindowsXPLookAndFeel")
DIM DATE$:"default:c(32*=0),sm[12]:c(3*=0),m[12]:c(32*=0),sd[7]:c(3*=0),d[7]:c(32*=0)"
date$=stbl("!DATE")
date.default$="%Mz/%Dz/%Y"
dummy$=stbl("!DATE",date$)
l=0
while l<7
  p=pos($0a$=gb__arg$)
  if p=0 then break
  if l=0 then chan$=gb__arg$(1,p-1)
  if l=1 then old_context=num(gb__arg$(1,3))
  if l=2 then x=num(gb__arg$(1,4))+25,y=num(gb__arg$(5,4))+60
  if l=3 then action$=gb__arg$(1,p-1)
  if l=4 then tmm01key$=gb__arg$(1,p-1)  
  if l=5 then security_level=num(gb__arg$(1,3))
  if l=6 then uid$=gb__arg$(1,p-1)
  l=l+1,gb__arg$=gb__arg$(p+1)
wend
rem ' get the invoice number - when drilling down from the serial number form
if len(gb__arg$) = 8 then dd_invoice_number$ = gb__arg$(1,7)

while len(chan$)>2
  f=num(chan$(1,3)),f$=fid(f),f$=cvs(f$(9),4),chan$=chan$(4)
  if pos("TMM-01"=f$) then tmm01=f
  if pos("TMM-02"=f$) then tmm02=f
  if pos("TMM-03"=f$) then tmm03=f
  if pos("TMM-05"=f$) then tmm05=f
  if pos("ECM-01"=f$) then ecm01=f
  if pos("STATES"=f$) then states=f
  if pos("COUNTRY"=f$) then country=f
  if pos("ARM-01"=f$) then arm01=f
  if pos("ARM-02"=f$) then arm02=f
  if pos("ARM-06"=f$) then arm06=f
  if pos("ARM-10"=f$) then arm10=f
  if pos("SYM-02"=f$) then sym02=f
  if pos("SNM-01"=f$) then snm01=f
  if pos("SNM-02"=f$) then snm02=f
  if pos("SNT-01"=f$) then snt01=f
  if pos("SNT-03"=f$) then snt03=f
  if pos("IVM-01"=f$) then ivm01=f
  if pos("CSM-03"=f$) then csm03=f
  if pos("SMC-01"=f$) then smc01=f
  if pos("ARS-10"=f$) then ars10n=f
  if pos("CSM-01"=f$) then csm01=f
  if pos("CSM-02"=f$) then csm02=f
  if pos("SYS-01"=f$) then ivs01=f
  if pos("CSM-04"=f$) then csm01=f
wend

call "templates.pgm::TMM01"
call "templates.pgm::TMM02"
call "templates.pgm::TMM03"
call "templates.pgm::TMM05"
call "templates.pgm::COUNTRY"

gb__context = fngb__window("101"); rem ' search_result
gb__context = fngb__window("102"); rem ' bcomm
gb__context = fngb__window("103"); rem ' contacts
gb__context = fngb__window("104"); rem ' sales
gb__context = fngb__window("105"); rem ' comments
gb__context = fngb__window("106"); rem ' invoice_history
gb__context = fngb__window("108"); rem ' renew_contracts
gb__context = fngb__window("109"); rem ' partner
gb__context = fngb__window("110"); rem ' report

if pos(action$="search add") then goto search_list
if action$="contacts" then goto contacts
if action$="bcomm" then goto bcomm
if action$="sales" then goto sales
if action$="comments" then goto comments
if action$="invoice_history" then goto invoice_history
if action$="renew_contracts" then goto renew_contracts
if action$="partner" then goto partner
if action$="report" then goto report

search_list: 
win_id_search_result$=fngb__win_id$(gb__win.search_result)
dim win_search_result$:fngb__template$(win_id_search_result$)
rem ' Set Window Focus
ctl_id=num(fattr(win_search_result$,"cust_list","ID"))
print (gb__sysgui)'context'(gb__win.search_result),'move'(0,x,y),
:'enable'(0),'show'(0),'focus'(0),'raise','focus'(ctl_id)

result_list$=""
temp$=gb__arg$
temp=pos($0a$=temp$,1,0)
dim tmkey$[temp]
l=0
while 1
  p=pos($0a$=temp$)
  if p=0 then break
  tempk$=temp$(1,p-1),temp$=temp$(p+1)
  readrecord(tmm01,key=tempk$,knum=0,dom=*continue)tmm01$
  result_list$=result_list$+tmm01.customer_nbr$+"  "+tmm01.cont_firm$+$0a$
  tmkey$[l]=tempk$,l=l+1
wend
result_list$=result_list$+$ff$+"0"
win_search_result.cust_list$=result_list$
win_search_result$=fngb__put_fields$(win_id_search_result$,win_search_result$,"cust_list")
gosub cust_detail
goto event_loop

contacts:
call "templates.pgm::STATES"
call "ec_open::SNMEU";chan$=chan$+str(SNMEU:"000")
win_id_contacts$=fngb__win_id$(gb__win.contacts)
dim win_contacts$:fngb__template$(win_id_contacts$)
options_id=num(fattr(win_contacts$,"option_list","ID"))
name_id=num(fattr(win_contacts$,"name","ID"))
ok_btn_id=num(fattr(win_contacts$,"ok_btn","ID"))
ap_id=num(fattr(win_contacts$,"ap_contact","ID"))
sam_id=num(fattr(win_contacts$,"sam_contact","ID"))
main_id=num(fattr(win_contacts$,"main_contact","ID"))
gosub contact_options
print (gb__sysgui)'context'(gb__win.contacts),'move'(0,x,y),
:'enable'(0),'show'(0),'focus'(0),'raise','focus'(options_id)
if security_level<5 then print(gb__sysgui)'disable'(ok_btn_id,main_id,ap_id)
gosub show_contact
goto event_loop

bcomm:
call "templates.pgm::ECM01"
win_id_bcomm$=fngb__win_id$(gb__win.bcomm)
dim win_bcomm$:fngb__template$(win_id_bcomm$)
options_id=num(fattr(win_bcomm$,"option_list","ID"))
user_code_id=num(fattr(win_bcomm$,"user_code","ID"))
ok_btn_id=num(fattr(win_bcomm$,"ok_btn","ID"))
eckey$=tmm01key$(1,8)
gosub bcomm_options
print (gb__sysgui)'context'(gb__win.bcomm),'move'(0,x,y),
:'enable'(0),'show'(0),'focus'(0),'raise','focus'(options_id)
if security_level<5 then print(gb__sysgui)'disable'(ok_btn_id)
gosub show_bcomm

goto event_loop

sales:
call "templates.pgm::ARM02"
call "templates.pgm::ARM06"
call "ec_open::ART01";chan$=chan$+str(ART01:"000")
call "ec_open::ART11";chan$=chan$+str(ART11:"000")
win_id_sales$=fngb__win_id$(gb__win.sales)
dim win_sales$:fngb__template$(win_id_sales$)
armkey$=tmm01key$(1,8)
readrecord(arm02,key=armkey$+"  ",dom=*next)arm02$
readrecord(arm06,key=armkey$,dom=*next)arm06$


rem ' --- Create a pie chart widget showing AR aging information"
rem ' if pos("SW" = uid$) = 0 and pos("GP" = uid$) = 0 then
rem '     xfirm_id$ = armkey$(1,2)
rem '     xcust_num$ = armkey$(3,6)
rem '     call "ARM.WI.called", xfirm_id$, xcust_num$
rem ' fi

print (gb__sysgui)'context'(gb__win.sales),'move'(0,x,y),
:'enable'(0),'show'(0),'focus'(0),'raise'
mask$="-#,###,##0.00"
win_sales$=fngb__get_screen$(win_id_sales$,win_sales$)
win_sales.mtd$=str(arm06.mtd_sales:mask$)
win_sales.ytd$=str(arm06.ytd_sales:mask$)
win_sales.pyr$=str(arm06.pyr_sales:mask$)
win_sales.nmtd$=str(arm06.nmtd_sales:mask$)
win_sales.last_inv_date$=fnaondate$(arm06.last_inv_date$)
win_sales.last_pmt_date$=fnaondate$(arm06.last_pay_date$)
win_sales.future$=str(arm02.aging_future:mask$)
win_sales.current$=str(arm02.aging_cur:mask$)
win_sales.age30$=str(arm02.aging_30:mask$)
win_sales.age60$=str(arm02.aging_60:mask$)
win_sales.age90$=str(arm02.aging_90:mask$)
win_sales.age120$=str(arm02.aging_120:mask$)
bal=arm02.aging_future+arm02.aging_cur+arm02.aging_30+arm02.aging_60+arm02.aging_90+arm02.aging_120
win_sales.bal_due$=str(bal:mask$)
win_sales.type_list$="Unpaid"+$0a$+"Paid"+$0a$+"Both"+$0a$+$ff$+"0"
win_sales$=fngb__put_screen$(win_id_sales$,win_sales$)
type_id=num(fattr(win_sales$,"type_list","ID"))
grid_id=num(fattr(win_sales$,"grid","ID"))
salesWindow!=sysgui!.getWindow(gb__win.sales)
salesWindow!.getControl(grid_id).clearMainGrid()
loadNextButton!=salesWindow!.getControl("next_btn")
loadNextButton!.setText("Load Grid")

goto event_loop

comments:
win_id_comments$=fngb__win_id$(gb__win.comments)
dim win_comments$:fngb__template$(win_id_comments$)
comment_list_id=num(fattr(win_comments$,"comment_list","ID"))
comment_id=num(fattr(win_comments$,"comment","ID"))
ok_btn_id=num(fattr(win_comments$,"ok_btn","ID"))
add_btn_id=num(fattr(win_comments$,"add_btn","ID"))
edit_btn_id=num(fattr(win_comments$,"edit_btn","ID"))
delete_btn_id=num(fattr(win_comments$,"delete_btn","ID"))
today$=date(0:"%Mz/%Dz/%Yl")
todaykey$=str(99999999-num(date(0:"%Yl%Mz%Dz")):"00000000")
print (gb__sysgui)'context'(gb__win.comments),'move'(0,x,y),
:'enable'(0),'show'(0),'focus'(0),'raise'
if security_level<5 then print(gb__sysgui)'disable'(add_btn_id,edit_btn_id,delete_btn_id)
gosub comment_list

goto event_loop

invoice_history:
gosub setup_invoice_history
goto event_loop

renew_contracts:

call "ec_open::sn_select"; chan$=chan$+str(sn_select:"000")
if smc01 then
  call "templates.pgm::SMC01"
else
  call "ec_open::SMC01"
  chan$=chan$+str(SMC01:"000")
endif
if snm01 then
  call "templates.pgm::SNM01"
else 
  call "ec_open::SNM01"
  chan$=chan$+str(SNM01:"000")
endif  
today=jul(0,0,0)
yy=num(date(today:"%Yl"))
yy1=yy,yy2=yy
mm=num(date(today:"%Mz"))
dd=num(date(today:"%Dz"))

q0$="03/31",q1$="06/30",q2$="09/30"
if mm>3 then q0$=q1$,q1$=q2$,q2$="12/31"
if mm>6 then q0$=q1$,q1$=q2$,q2$="03/31",yy2=yy2+1
if mm>9 then q0$=q1$,q1$=q2$,q2$="06/30",yy1=yy1+1
q0$=q0$+"/"+str(yy:"0000")
q1$=q1$+"/"+str(yy1:"0000")
q2$=q2$+"/"+str(yy2:"0000")
cur_list$=q0$+$0a$+q1$+$0a$+$ff$+"0"
new_list$=q1$+$0a$+q2$+$0a$+$ff$+"0"

rem '**** month to month expiration ****
mm1=mm+1
yy1=yy
if mm1>12 then mm1=mm1-12,yy1=yy+1
d0$=date(jul(yy1,mm1,1)-1:"%Mz/%Dz/%Yl")
mm1=mm1+1
if mm1>12 then mm1=mm1-12,yy1=yy1+1
d1$=date(jul(yy1,mm1,1)-1:"%Mz/%Dz/%Yl")
mm1=mm1+1;if mm1>12 then mm1=mm1-12,yy1=yy1+1
d2$=date(jul(yy1,mm1,1)-1:"%Mz/%Dz/%Yl")
xd1$=d1$
xd2$=d2$
mm1=num(d2$(1,2))+2
if mm1>12 then 
    mm1=mm1-12
    if yy1 = yy then yy1=yy1+1
fi
xd3$=date(jul(yy1,mm1,1)-1:"%Mz/%Dz/%Yl")
mm1=num(xd3$(1,2))+2
if mm1>12 then mm1=mm1-12,yy1=yy1+1
xd4$=date(jul(yy1,mm1,1)-1:"%Mz/%Dz/%Yl")

cur_list$=d0$+$0a$+d1$+$0a$+d2$+$0a$+$ff$+"0"
new_list$=xd1$+$0a$+xd2$+$0a$+xd3$+$0a$+$ff$+"0"
rem '**** end of month to month ****

rem '** bi monthly expiration
d0m$=d0$(1,3)+"15"+d0$(6)
d1m$=d1$(1,3)+"15"+d1$(6)
d2m$=d2$(1,3)+"15"+d2$(6)
d3m$=xd3$(1,3)+"15"+xd3$(6)

cur_list$=d0m$+$0a$+d0$+$0a$+d1m$+$0a$+d1$+$0a$+d2m$+$0a$+d2$+$0aff$+"0"
new_list$=d0$+$0a$+d1m$+$0a$+d1$+$0a$+d2m$+$0a$+d2$+$0a$+d3m$+$0a$+xd3$+$0aff$+"0"
rem '** end of bi monthly expiration

win_id_renew_contracts$=fngb__win_id$(gb__win.renew_contracts)
dim win_renew_contracts$:fngb__template$(win_id_renew_contracts$)
win_renew_contracts$=fngb__get_screen$(win_id_renew_contracts$,win_renew_contracts$)
current_dt_id=num(fattr(win_renew_contracts$,"current_dt","ID"))
new_dt_id=num(fattr(win_renew_contracts$,"new_dt","ID"))
search_btn_id=num(fattr(win_renew_contracts$,"search_btn","ID"))
push_btn_id=num(fattr(win_renew_contracts$,"push_btn","ID"))
win_renew_contracts.sam_chk=1
win_renew_contracts.rental_chk=1
win_renew_contracts.current_dt$=cur_list$
win_renew_contracts.new_dt$=new_list$
win_renew_contracts$=fngb__put_screen$(win_id_renew_contracts$,win_renew_contracts$)
print (gb__sysgui)'context'(gb__win.renew_contracts),'move'(0,x,y),'enable'(0),'show'(0),'focus'(0),'raise'
print(gb__sysgui)'disable'(push_btn_id)
firm$=tmm01key$(1,2)
cust$=tmm01key$(3,6)
goto event_loop

partner:
call "templates.pgm::ARM02"
call "templates.pgm::ARM06"
call "templates.pgm::ARM10I"
call "ec_open::TMM07"
chan$=chan$+str(tmm07:"000")
call "ec_open::TMM11"
chan$=chan$+str(tmm11:"000")
call "ec_open::SYS01C"
read(sys01c,key="01AR00")*,*,*,*,my$
close(sys01c)
ar_mm=num(my$(1,2))
now_mm=num(date(0:"%Mz"))
win_id_partner$=fngb__win_id$(gb__win.partner)
dim win_partner$:fngb__template$(win_id_partner$)
apdir_dt_id=num(fattr(win_partner$,"apdir_dt","ID"))
plan_dt_id=num(fattr(win_partner$,"plan_dt","ID"))
qstnr_dt_id=num(fattr(win_partner$,"qstnr_dt","ID"))
goals_dt_id=num(fattr(win_partner$,"goals_dt","ID"))
grid_id=num(fattr(win_partner$,"event_grid","ID"))
partnerWindow!=sysgui!.getWindow(gb__win.partner)
apdir_dt!=partnerWindow!.getControl(apdir_dt_id)
plan_dt!=partnerWindow!.getControl(plan_dt_id)
qstnr_dt!=partnerWindow!.getControl(qstnr_dt_id)
goals_dt!=partnerWindow!.getControl(goals_dt_id)
discount!=new java.util.HashMap()
k$=tmm01key$(1,2)+"I"
read(arm10,key=k$,dom=*next)
while 1
  readrecord(arm10,end=*break)arm10i$
  if pos(k$=arm10i$)<>1 then break
  discount!.put(arm10i.disc_percent,arm10i.disc_code$)
wend
rows=0
max=100
event$=""
gridVector!=sysgui!.makeVector()
partnerWindow!.getControl(grid_id).clearMainGrid()
rowend=partnerWindow!.getControl(grid_id).getNumRows()
read (tmm07,key=tmm01key$,knum=1,dom=*next)
while 1
  read record(tmm07,end=*break)tmm07$
  if pos(tmm01key$=tmm07.firm_id$+tmm07.customer_nbr$+tmm07.tm_number$)<>1 then break
  readrecord(tmm11,key=tmm07.firm_id$+tmm07.class_id$,dom=*continue)tmm11$
  event$=event$+tmm11.event_date$+tmm11.event_desc$
wend

l0=len(event$)
if l0>37 then event$=ssort(event$,38)
last_event=0
while l0>37
  temp$=event$(l0-37,38)
  if l0-38>0 then event$=event$(1,l0-38) else event$=""
  l0=len(event$)
  gridVector!.addItem(cvs(temp$(9),3))
  if fnnum(temp$(1,8)) then
    d$=date(fnjul(temp$(1,9)):"%Mz/%Dz/%Y")
    if last_event=0 then last_event=jul(num(temp$(1,4))+1,12,31)
  else
    d$="Unknown"
  fi
  gridVector!.addItem(d$)
  rows=rows+1
wend
events=(last_event>=jul(0,0,0))

while rows<rowend-1
  gridVector!.addItem(" ")
  gridVector!.addItem(" ")
  rows=rows+1
wend

partnerWindow!.getControl(grid_id).setNumRows(rows)
partnerWindow!.getControl(grid_id).setCellText(gridVector!)

focus_id=num(fattr(win_partner$,"exit_btn","ID"))
print (gb__sysgui)'context'(gb__win.partner),'move'(0,x,y),
:'enable'(0),'show'(0),'focus'(0),'raise','focus'(focus_id)
gosub show_partner
goto event_loop

report:
win_id_report$=fngb__win_id$(gb__win.report)
dim win_report$:fngb__template$(win_id_report$)
email_id=num(fattr(win_report$,"email","ID"))
country_id=num(fattr(win_report$,"country_lbox","ID"))
selected_id=num(fattr(win_report$,"selected_lbox","ID"))
print (gb__sysgui)'context'(gb__win.report),'move'(0,x,y),
:'enable'(0),'show'(0),'focus'(0),'raise'
firm$=tmm01key$(1,2)
temp$=""
first$=""
last$=""
read(tmm01,key=firm$+"000001",dom=*next)
while 1
  readrecord(tmm01,end=*break)tmm01$
  if firm$<>tmm01.firm_id$ then break
  if tmm01.customer_nbr$="000000" then continue
  if first$="" then first$=tmm01.customer_nbr$,name1$=tmm01.cont_firm$ 
  last$=tmm01.customer_nbr$,name2$=tmm01.cont_firm$
  if pos(tmm01.country_code$=temp$,2) then continue
  temp$=temp$+tmm01.country_code$
wend
if temp$="" then goto pgm_exit
temp$=ssort(temp$,2)
email$=""
if firm$="02" then
  readrecord(tmm05,key="01007688000000",dom=*next)tmm05$
  email$=cvs(tmm05.email$,3)
fi
c_list$="",country_list$="",selected_list$=""
while len(temp$)>1
  k$=temp$(1,2),temp$=temp$(3)
  readrecord(country,knum=0,key=k$,dom=*continue)country$
  name$=cvs(country.country_name$,3)
  c_list$=c_list$+k$+name$+$0a$
  country_list$=country_list$+name$+$0a$
wend
win_report$=fngb__get_screen$(win_id_report$,win_report$)
win_report.country_lbox$=country_list$
win_report.email$=email$
win_report.start_cust$=first$
win_report.end_cust$=last$
win_report.start_name$=name1$
win_report.end_name$=name2$
win_report$=fngb__put_screen$(win_id_report$,win_report$)
goto event_loop


event_loop:


[Event Win=101 ID=0 Code=X <WIN_CLOSE> (W101_C0_WIN_CLOSE)]
goto pgm_exit

[Event Win=101 ID=1 Code=B <PUSH_BUTTON> (W101_C1_PUSH_BUTTON)]
rem ' Push button operated
gb__arg$=tmm01key$
exitto pgm_exit

[Event Win=101 ID=100 Code=l0 <LIST_CLICK> (W101_C100_LIST_CLICK)]
rem ' Click in list box
gosub cust_detail

[Event Win=101 ID=200 Code=B <PUSH_BUTTON> (W101_C200_PUSH_BUTTON)]
rem ' Push button operated
gb__arg$=""
exitto pgm_exit

[Event Win=102 ID=0 Code=X <WIN_CLOSE> (W102_C0_WIN_CLOSE)]
rem ' Close Button Operated

goto pgm_exit

[Event Win=102 ID=1 Code=B <PUSH_BUTTON> (W102_C1_PUSH_BUTTON)]
rem ' Push button operated
win_bcomm$=fngb__get_screen$(win_id_bcomm$,win_bcomm$)
del=(cvs(win_bcomm.password$,7)="DELETE")

changed=0
if del then 
  if security_level<5 then goto redisplay
  i=msgbox("Delete this account?",4+32,"Confirm")
  if i<>6 then goto redisplay
  remove(ecm01,key=ecm01$(1,10),err=*next)
  gosub bcomm_options
  goto redisplay
fi

x$=cvs(win_bcomm.password$,3)
if x$="" then i=msgbox("Password required",0,"");goto redisplay

y$ = cvs(win_bcomm.email_1$,3)
if y$="" then i=msgbox("Email 1 required",0,"");goto redisplay
if pos("@"=y$) = 0 then i=msgbox("Email 1 does not appear to be a valid email address.",0,"");goto redisplay

rem ' ok=1
rem 'if len(x$)<6 or len(x$)>8 then ok=0
rem ' i=1
rem 'repeat
rem '   alpha=pos(x$(i,1)="ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz")
rem '  i=i+1
rem ' until !(alpha) or i>len(x$)
rem ' if alpha then ok=0
rem ' if ok=0 then
rem '  i=msgbox("Must be 6-8 characters, at least 1 numeric.",0,"Invalid password")
rem '  goto redisplay
rem ' fi

rem ' chect to ensure that the password there is valid
testPassword! = x$
if !testPassword!.matches("^.*(?=.{10,})(?=.*\d)(?=.*[a-z])(?=.*[A-Z])(?=.*[~!@#$%^&*()+=]).*$") then
    i = msgbox("The password does not meet the password requirements. It must be at least 10 characters long and contain at least one each of the following: upper case letter, lower case letter, number and one of these special characters:  ~!@#$%^&*()+=", 0, "Fails Password Rules")
    goto redisplay
fi

if action$="add" then
  dim ecm01$:fattr(ecm01$)
  ecm01.firm_id$=eckey$(1,2)
  ecm01.customer_nbr$=eckey$(3,6)
  ecm01.user_code$=cvs(win_bcomm.user_code$,3)
  added=0
else
  extracted=0
  dim oldecm$:fattr(ecm01$)
  extractrecord(ecm01,key=eckey$+ecm01.user_code$,err=*next)ecm01$;extracted=1
  oldecm$=field(ecm01$)
fi
ecm01.first_name$=cvs(win_bcomm.first_name$,3)
ecm01.last_name$=cvs(win_bcomm.last_name$,3)
ecm01.e_mail$=cvs(win_bcomm.email_1$,3)
ecm01.e_mail2$=cvs(win_bcomm.email_2$,3)
ecm01.password$=cvs(win_bcomm.password$,3)

lockUserAcct = num(win_bcomm.lockUserAccount$)
if lockUserAcct = 1 then
    ecm01.available$(1,1) = "Y"
else
    ecm01.available$(1,1) = "N"
fi

ecm01$=field(ecm01$)
if action$="add" then
  writerecord(ecm01,key=eckey$+ecm01.user_code$,dom=*next)ecm01$;added=1
  if !(added) then i=msgbox("Record already exists.",0,"Can't add");return
  changed=ecm01.user_code$="  "
  gosub bcomm_options
  goto redisplay
fi
if extracted then
  writerecord(ecm01,key=eckey$+ecm01.user_code$)ecm01$
  if oldecm$<>ecm01$ then changed=ecm01.user_code$="  "
  goto redisplay
fi
temp$=errmes(-1)
i=msgbox("Can't update: "+temp$,0,"Error "+str(err))

redisplay:
gosub show_bcomm
if changed then call "cust_web_updt.pgm",err=*next,eckey$

[Event Win=102 ID=110 Code=f0 <LOST_FOCUS> (W102_C110_LOST_FOCUS)]
rem ' Control lost focus

win_bcomm$=fngb__get_fields$(win_id_bcomm$,win_bcomm$,"user_code")
x$=pad(cvs(win_bcomm.user_code$,7),2)
ok=1
if len(x$)>2 then ok=0
for x=1 to 2
  p=pos(x$(x,1)="ABCDEFGHIJKLMNOPQRSTUVWXYZ *")
  if p=0 then ok=0
next x
if !(ok) then 
  i=msgbox("Must be 1-2 characters, uppercase letters, spaces or * only"
: ,0,"Not a valid user code")
  win_bcomm.user_code$="  "
fi
win_bcomm$=fngb__put_fields$(win_id_bcomm$,win_bcomm$,"user_code")
if !(ok) then print(gb__sysgui)'focus'(user_code_id)

[Event Win=102 ID=140 Code=f0 <LOST_FOCUS> (W102_C140_LOST_FOCUS)]
rem ' Control lost focus

win_bcomm$=fngb__get_fields$(win_id_bcomm$,win_bcomm$,"password,email_1")

rem ' if cvs(win_bcomm.password$,3)<>"" or cvs(win_bcomm.email_1$,3)="" then return

testPword$ = cvs(win_bcomm.password$,3)

if testPword$ = "" then
    rem ' generate a password
    dim charlist$[3]
    charlist$[0]="0123456789"
    charlist$[1]="abcdefghijkmnopqrstuvwxyz"
    charlist$[2]="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
    charlist$[3]="~!@#$%^&*()+="
    rem ' password will be 10 characters long 
    rem ' made up of a 6 byte segment of purely random numbers
    rem ' and a 4 byte segment made up of an number, lower, upper, special
    rem ' character in that order starting with one of them at random
    p$=""
    specchar = 0
    numchar = 0
    rem ' generate the first 6
    while len(p$) < 6
        x = rnd(4)

        rem ' limit specchar to 1 in this segment
        if x = 3 then 
            specchar = specchar + 1
            if specchar > 1 then continue
        fi

        rem ' limit the digits to 1 in this segment
        if x = 0 then 
            numchar = numchar + 1
            if numchar > 1 then continue
        fi

        charset$ = charlist$[x]
        y = rnd(len(charset$))
        p$ = p$ + charset$(y+1,1)
    wend
    rem ' generate the next 4
    x = rnd(4)
    while 1
        x = x + 1
        if x > 3 then x = 0
        rem ' special characters limited to 1
        if x = 3 and specchar > 0 then continue
        charset$ = charlist$[x]
        y = rnd(len(charset$))
        p$ = p$ + charset$(y+1,1)
        if len(p$) >= 10 then break
    wend     
    win_bcomm.password$ = p$
    win_bcomm$=fngb__put_fields$(win_id_bcomm$,win_bcomm$,"password")
else
    rem ' chect to ensure that the password there is valid
    if cvs(testPword$,4) <> "DELETE" then 
        testPassword! = testPword$
        if !testPassword!.matches("^.*(?=.{10,})(?=.*\d)(?=.*[a-z])(?=.*[A-Z])(?=.*[~!@#$%^&*()+=]).*$") then
            i = msgbox("This password does not meet the password requirements. It must be at least 10 characters long and contain at least one each of the following: upper case letter, lower case letter, number and one of these special characters:  ~!@#$%^&*()+=", 0, "Fails Password Rules")
            sysgui!.flushEvents()
        fi
    fi
fi

[Event Win=102 ID=2 Code=B <PUSH_BUTTON> (W102_C2_PUSH_BUTTON)]
rem ' Push button operated

gb__arg$="" 
goto pgm_exit

[Event Win=102 ID=200 Code=l0 <LIST_CLICK> (W102_C200_LIST_CLICK)]
rem ' Click in list box

gosub show_bcomm

[Event Win=103 ID=0 Code=X <WIN_CLOSE> (W103_C0_WIN_CLOSE)]
rem ' Close Button Operated

goto pgm_exit

[Event Win=103 ID=1 Code=B <PUSH_BUTTON> (W103_C1_PUSH_BUTTON)]
rem ' Push button operated
win_contacts$=fngb__get_screen$(win_id_contacts$,win_contacts$)
if cvs(win_contacts.name$,7)="DELETE" then
  if security_level<5 then goto redisplay_contact
  i=msgbox("Delete this contact?",4+32,"Confirm")
  if i<>6 then goto redisplay_contact
  tmm03k$=tmm03$(1,17)
  remove(tmm03,key=tmm03k$,err=*next)
  if primary$=tmm03k$ then primary$=""
  if a_p$=tmm03k$ then a_p$=""
  if sam_con$=tmm03k$ then sam_con$=""
  rem 'renumber lines after removing record
  seq=fnnum(tmm03$(15,3))
  read(tmm03,key=tmm03k$,dom=*next)
  while seq<980
    readrecord(tmm03,end=*break)tmm03$
    if tmm03k$(1,14)<>tmm03$(1,14) then break
    seq$=str(seq:"000"), seq=seq+10
    if seq$=tmm03.sequence_nbr$ then continue
    tmm03k$=tmm03$(1,17),tmm03.sequence_nbr$=seq$
    writerecord(tmm03,key=tmm03$(1,17))tmm03$
    if tmm03.primary_contact$="Y" then primary$=tmm03$(1,17)
    if tmm03.ap_contact$="Y" then a_p$=tmm03$(1,17)
    remove(tmm03,key=tmm03k$)
  wend
  goto redisplay_contact
fi
if cvs(win_contacts.name$,3)="" then i=msgbox("A name is required",0,"");goto redisplay_contact
if action$="add" then
  seq$="000"
  read(tmm03,key=tmm01key$,dom=*next)
  while 1  
    readrecord(tmm03,end=*break)tmm03$
    if pos(tmm01key$=tmm03$)<>1 then break
    seq$=tmm03.sequence_nbr$
  wend
  dim tmm03$:fattr(tmm03$)
  tmm03$(1,14)=tmm01key$(1,14)
  tmm03.sequence_nbr$=str(num(seq$)+10:"000")
  added=0
else
  extracted=0
  extractrecord(tmm03,key=tmm03$(1,17),err=*next)tmm03$;extracted=1
fi
tmm03.contact_name$=cvs(win_contacts.name$,3)
tmm03.position$=cvs(win_contacts.position$,3)
rem ' prevent the END USER display notification from being written to the reocrd
if pos("END USER -" = tmm03.position$) = 1 then
    tmm03.position$ = ""
fi
tmm03.phone_number$=cvs(win_contacts.phone$,3)
tmm03.extension$=cvs(win_contacts.extension$,3)
tmm03.e_mail$=cvs(win_contacts.email$,3)
tmm03.address_1$=cvs(win_contacts.addr1$,3)
tmm03.address_2$=cvs(win_contacts.addr2$,3)
tmm03.city$=cvs(win_contacts.city$,3)
tmm03.zip_code$=cvs(win_contacts.zip$,3)
ctl_id=num(fattr(win_contacts$,"state","ID"))
tmm03.state_code$=ctrl(gb__sysgui,ctl_id,1)
ctl_id=num(fattr(win_contacts$,"country","ID"))
dim cnkey$(30),country$:fattr(country$)
cnkey$(1)=ctrl(gb__sysgui,ctl_id,1)
readrecord(country,key=cnkey$,knum=1,dom=*next)country$
tmm03.country_code$=country.country_code$
tmm03.send_lit$="N"
if win_contacts.adv_y$="1" then tmm03.send_lit$="Y"
if win_contacts.adv_e$="1" then tmm03.send_lit$="E"
if win_contacts.main_contact$="1" then tmm03.primary_contact$="Y" else tmm03.primary_contact$="N"
if win_contacts.ap_contact$="1" then tmm03.ap_contact$="Y" else tmm03.ap_contact$="N"
if win_contacts.announcements$="1" then tmm03.announcements$="Y" else tmm03.announcements$="N"
if win_contacts.sam_contact$="1" then tmm03.sam_contact$="Y" else tmm03.sam_contact$="N"
if win_contacts.perpetNotice$="1" then tmm03.perpet_notice$="Y" else tmm03.perpet_notice$="N"

tmm03$=field(tmm03$)
if action$="add" then
  writerecord(tmm03,key=tmm03$(1,17),dom=*next)tmm03$;added=1
  if !(added) then i=msgbox("Record already exists.",0,"Can't add");return
  goto redisplay_contact
fi
if extracted then
  writerecord(tmm03,key=tmm03$(1,17))tmm03$
  goto redisplay_contact
fi
temp$=errmes(-1)
i=msgbox("Can't update: "+temp$,0,"Error "+str(err))

redisplay_contact:
if win_contacts.main_contact$="1" and primary$<>tmm03$(1,17) then
  ok=0
  readrecord(tmm03,key=primary$,dom=*next)tmm03$;ok=1
  if ok then
    tmm03.primary_contact$="N"
    tmm03$=field(tmm03$)
    writerecord(tmm03,key=primary$)tmm03$
  fi
fi
if win_contacts.sam_contact$="1" and (sam_con$<>tmm03$(1,17) or sam_email$<>tmm03.e_mail$) then
  i=msgbox("Change all SAM contracts to use "+cvs(tmm03.e_mail$,3),4+32,"SAM E-Mail")
  if i=6 then gosub change_smc01_email
  if sam_con$<>tmm03$(1,17) then
    ok=0
    readrecord(tmm03,key=sam_con$,dom=*next)tmm03$;ok=1
    if ok then
      tmm03.sam_contact$="N"
      tmm03$=field(tmm03$)
      writerecord(tmm03,key=primary$)tmm03$
    fi
  fi
fi
gosub contact_options
gosub show_contact
dim tmm03$:fattr(tmm03$)
readrecord(tmm03,key=primary$,dom=*next)tmm03$
readrecord(tmm01,key=tmm01key$)tmm01$
if cvs(tmm01.cont_name$,3)<>cvs(tmm03.contact_name$,3) then
  tmm01.cont_name$=tmm03.contact_name$
  tmm01.contact_sort$=cvs(tmm03.contact_name$,7)
  tmm01$=field(tmm01$)
  writerecord(tmm01,key=tmm01key$)tmm01$
fi

[Event Win=103 ID=2 Code=B <PUSH_BUTTON> (W103_C2_PUSH_BUTTON)]
rem ' Push button operated

goto pgm_exit

[Event Win=103 ID=300 Code=l0 <LIST_CLICK> (W103_C300_LIST_CLICK)]
rem ' Click in list box

contactSelected = options!.getSelectedIndex()
gosub show_contact
options!.selectIndex(contactSelected)

[Event Win=103 ID=594 Code=c0 <CHECK_OFF> (W103_C594_CHECK_OFF)]
rem ' Control unchecked (OFF)

[Event Win=103 ID=594 Code=c1 <CHECK_ON> (W103_C594_CHECK_ON)]
rem ' Control checked (ON)

[Event Win=103 ID=601 Code=c1 <CHECK_ON> (W103_C601_CHECK_ON)]
rem ' Control checked (ON)
if cvs(tmm03.e_mail$,3)<>"" and pos("@"=tmm03.e_mail$) then return
i=msgbox("E-mail address is required",0,"SAM Contact")
win_contacts.sam_contact$="0"
win_contacts$=fngb__put_fields$(win_id_contacts$,win_contacts$,"sam_contact")

[Event Win=104 ID=0 Code=X <WIN_CLOSE> (W104_C0_WIN_CLOSE)]
rem ' Close Button Operated

goto pgm_exit

[Event Win=104 ID=1 Code=B <PUSH_BUTTON> (W104_C1_PUSH_BUTTON)]
rem ' Push button operated

win_sales$=fngb__get_fields$(win_id_sales$,win_sales$,"inv_nbr,dir")
start_inv$=win_sales.inv_nbr$
readdir=1
if win_sales.dir then readdir=-1

rem ' type$=ctrl(gb__sysgui,type_id,1)
type$=salesWindow!.getControl(type_id).getText()

gosub invoice_grid
win_sales.inv_nbr$=start_inv$
win_sales$=fngb__put_fields$(win_id_sales$,win_sales$,"inv_nbr")
loadNextButton!.setText("Next")

[Event Win=104 ID=300 Code=N3:107 <GRID_DCLICK> (W104_C300_GRID_MOUSE_DOUBLE_CLICK)]
rem ' Notify Event - Grid - Grid Double Clicked (3)
rem ' gb__event! = cast(BBjGridDoubleClickEvent,bbjapi().getSysGui().getLastEvent())
rem ' gb__control! = gb__event!.getControl()
rem ' Row is: gb__event!.getRow()
rem ' Column is: gb__event!.getColumn()
rem ' Point clicked: gb__event!.getXLocationInCell(), 
rem '     gb__event!.getYLocationInCell()
rem ' Cell boundaries: gb__event!.getCellBounds()
rem ' Meta keys: gb__event!.isShiftDown(), gb__event!.isControlDown()

gb__event! = bbjapi().getSysGui().getLastEvent()
col = gb__event!.getColumn()
row = gb__event!.getRow()
if col <> 0 then return

dd_invoice_number! = cast(BBjString, salesWindow!.getControl(grid_id).getCell(row, col).getText())
dd_invoice_number$ = dd_invoice_number!.trim()
if dd_invoice_number$ = "" then return

salesWindow!.setVisible(0)
if invoice_history_created then
    invoiceWindow!.setVisible(1)
    invoiceWindow!.getControl("invoice_nbr").focus()
    gosub show_invoice
else
    gosub setup_invoice_history
    drilldown_mode = 1
fi

[Event Win=104 ID=320 Code=N2:19 <LIST_SELECT> (W104_C320_LIST_SELECT)]
rem ' Notify Event - List Control - List Selection (2)
rem ' gb__event! = cast(BBjListSelectEvent,bbjapi().getSysGui().getLastEvent())
rem ' gb__control! = gb__event!.getControl()

rem ' clear the starting invoice number
win_sales$=fngb__get_fields$(win_id_sales$,win_sales$,"inv_nbr")
start_inv$=win_sales.inv_nbr$
start_inv$ = ""
win_sales.inv_nbr$=start_inv$
win_sales$=fngb__put_fields$(win_id_sales$,win_sales$,"inv_nbr")
loadNextButton!.setText("Load Grid")
salesWindow!.getControl(grid_id).clearMainGrid()

[Event Win=104 ID=330 Code=c0 <CHECK_OFF> (W104_C330_CHECK_OFF)]
rem ' Control unchecked (OFF)
rem ' gb__event! = cast(BBjCheckOffEvent,bbjapi().getSysGui().getLastEvent())
rem ' gb__control! = gb__event!.getControl()

rem ' clear the starting invoice number
win_sales$=fngb__get_fields$(win_id_sales$,win_sales$,"inv_nbr")
start_inv$=win_sales.inv_nbr$
start_inv$ = ""
win_sales.inv_nbr$=start_inv$
win_sales$=fngb__put_fields$(win_id_sales$,win_sales$,"inv_nbr")
loadNextButton!.setText("Load Grid")
salesWindow!.getControl(grid_id).clearMainGrid()

[Event Win=104 ID=330 Code=c1 <CHECK_ON> (W104_C330_CHECK_ON)]
rem ' Control checked (ON)
rem ' gb__event! = cast(BBjCheckOnEvent,bbjapi().getSysGui().getLastEvent())
rem ' gb__control! = gb__event!.getControl()

rem ' clear the starting invoice number
win_sales$=fngb__get_fields$(win_id_sales$,win_sales$,"inv_nbr")
start_inv$=win_sales.inv_nbr$
start_inv$ = ""
win_sales.inv_nbr$=start_inv$
win_sales$=fngb__put_fields$(win_id_sales$,win_sales$,"inv_nbr")
loadNextButton!.setText("Load Grid")
salesWindow!.getControl(grid_id).clearMainGrid()

[Event Win=104 ID=400 Code=B <PUSH_BUTTON> (W104_C400_PUSH_BUTTON)]
rem ' Push button operated

goto pgm_exit

[Event Win=105 ID=0 Code=X <WIN_CLOSE> (W105_C0_WIN_CLOSE)]
rem ' Close Button Operated
goto pgm_exit

[Event Win=105 ID=1 Code=B <PUSH_BUTTON> (W105_C1_PUSH_BUTTON)]
rem ' Push button operated
win_comments$=fngb__get_fields$(win_id_comments$,win_comments$,"comment")
dim tmm02$:fattr(tmm02$)
com$=win_comments.comment$
seq=num(tmm02key$(23,3))
while 1
  p=pos($0a$=com$)
  if p=0 then break
  com$(p,1)=" "
wend
com$=cvs(com$,35)
while com$<>""
  x1=min(65,len(com$))
  tmm02$(1,22)=tmm02key$(1,22)
  tmm02.sequence$=str(seq:"000")
  tmm02.comment$=com$(1,x1)
  LET ffl$=FATTR(tmm02$,"dealer_str"),ffl=DEC(ffl$(10,2))
  tmm02.dealer_str$=fill(ffl)
  tmm02$=field(tmm02$)
  writerecord(tmm02,key=tmm02key$)tmm02$
  seq=seq+1
  if seq>999 then break
  com$=com$(x1+1)
wend
gosub comment_list

[Event Win=105 ID=300 Code=B <PUSH_BUTTON> (W105_C300_PUSH_BUTTON)]
rem ' Push button operated
win_comments$=fngb__get_fields$(win_id_comments$,win_comments$,"date,seq,comment")
win_comments.date$=today$
win_comments.comment$=""
seq=1
temp$=comkeys$
while 1
  p=pos($0a$=temp$)
  if p<26 then break
  if pos(todaykey$=temp$(15)) then seq=num(temp$(23,3))+1
  temp$=temp$(p+1)
wend

win_comments.seq$=str(seq:"000")
win_comments$=fngb__put_fields$(win_id_comments$,win_comments$,"date,seq,comment")
print(gb__sysgui)'enable'(ok_btn_id),'txlimit'(comment_id,650),'focus'(comment_id)
tmm02key$=tmm01key$+todaykey$+str(seq:"000")

[Event Win=105 ID=310 Code=B <PUSH_BUTTON> (W105_C310_PUSH_BUTTON)]
rem ' Push button operated

temp$=ctrl(gb__sysgui,comment_list_id,1)
temp=dec(ctrl(gb__sysgui,comment_list_id,2))
p=pos($0a$=comkeys$,1,temp+1)
if p<26 then return
tmm02key$=comkeys$(p-25,25)
win_comments$=fngb__get_fields$(win_id_comments$,win_comments$,"date,seq,comment")
p=pos(" "=temp$)
if p then win_comments.date$=temp$(1,p-1)
win_comments.seq$=tmm02key$(23,3)
com$=""
readrecord(tmm02,key=tmm02key$,err=*next)tmm02$
if pos(tmm02key$=tmm02$)=1 then com$=tmm02.comment$
while 1
  p=pos($0a$=com$)
  if p=0 then break
  com$(p,1)=" "
wend
com$=cvs(com$,35)+$0a$
win_comments.comment$=com$
win_comments$=fngb__put_fields$(win_id_comments$,win_comments$,"date,seq,comment")
print(gb__sysgui)'enable'(ok_btn_id),'txlimit'(comment_id,65),'focus'(comment_id)

[Event Win=105 ID=320 Code=B <PUSH_BUTTON> (W105_C320_PUSH_BUTTON)]
rem ' Push button operated

temp$=ctrl(gb__sysgui,comment_list_id,1)
temp=dec(ctrl(gb__sysgui,comment_list_id,2))
p=pos($0a$=comkeys$,1,temp+1)
if p<26 then return
k$=comkeys$(p-25,25)
p=pos(" "=temp$)
i=msgbox(cvs(temp$(p+1),3)+$0a$+"Delete?",4+32,"Confirm")
if i=6 then remove(tmm02,key=k$,err=*next)
gosub comment_list

[Event Win=105 ID=330 Code=B <PUSH_BUTTON> (W105_C330_PUSH_BUTTON)]
rem ' Push button operated

goto pgm_exit

[Event Win=106 ID=0 Code=X <WIN_CLOSE> (W106_C0_WIN_CLOSE)]
rem ' Close Button Operated

goto pgm_exit

[Event Win=106 ID=1 Code=B <PUSH_BUTTON> (W106_C1_PUSH_BUTTON)]
rem ' Push button operated

gosub invoice_search

[Event Win=106 ID=150 Code=l0 <LIST_CLICK> (W106_C150_LIST_CLICK)]
rem ' Click in list box

gosub show_invoice

[Event Win=106 ID=160 Code=l0 <LIST_CLICK> (W106_C160_LIST_CLICK)]
rem ' Click in list box

[Event Win=106 ID=250 Code=B <PUSH_BUTTON> (W106_C250_PUSH_BUTTON)]
rem ' Push button operated

if drilldown_mode then
    invoiceWindow!.setVisible(0)
    salesWindow!.setVisible(1)
    salesWindow!.getControl("Grid").focus()
    return
else
    goto pgm_exit
fi

[Event Win=106 ID=300 Code=B <PUSH_BUTTON> (W106_C300_PUSH_BUTTON)]
rem ' Push button operated
msg$=""
art03key$=art03$(1,20)
found=0
readrecord(art03,key=art03key$,dom=*next)art03$;found=1
if !(found) then msg$="Select an invoice to print."
temp$=ctrl(gb__sysgui,printers_id,1)
p=pos(" "=temp$),printer$=""
if p>1 then printer$=cvs(temp$(1,p-1),7)
if printer$="" then msg$="Select output."

win_invoice_history$=fngb__get_fields$(win_id_invoice_history$,
:win_invoice_history$,"email_addr")
email$=cvs(win_invoice_history.email_addr$,3)
archive=(pos("SEND"=printer$) and found)
while archive
  if email$="" then msg$="Enter an email address";break
  inv$=art03.ar_inv_nbr$
  firm$=art03.firm_id$
  call "retrieve_invoice.bbj",firm$,inv$,email$,status
  if status=0 then msg$=inv$+" Sent to "+email$;break
  msg$="Retrieval Failed for " + inv$
  break 
wend
if archive then i=msgbox(msg$,0,"Archived Invoice");return
fi

rem ' email or print invoice to a printer
if pos("EMAIL"=printer$) then 
  email=1
  rem ' firm 02 is now using the jasper invoice
  rem ' if art03.firm_id$ = "02" then
  rem '   textfile$="Invoice_"+art03.ar_inv_nbr$+".txt"
  rem '   printer$="PX"
  rem '   gosub print_to_file
  rem ' fi
else
  email=0
  msg$="Can't open printer "+printer$
  prtr_dev=unt
  open(prtr_dev,err=*next)printer$;msg$=""
fi

if email then
    rem ' both firm 02 and fimr 01 are now using the jasper invoice

    if email$="" then
        msg$="Enter an email address"
        goto inv_print_end 
    fi
    
    pdf$ = ""
    if art03.firm_id$ = "01" then
        call "renderInvoice.src", art03.firm_id$, art03.customer_nbr$, art03.ar_inv_nbr$, pdf$
    else
        call "renderInvoiceBSG.src", art03.firm_id$, art03.customer_nbr$, art03.ar_inv_nbr$, pdf$
    fi
else
    if msg$<>"" then i=msgbox(msg$,0,"Output problem");return
    print (gb__sysgui)'setcursor'(3)
    misc$=textfile$
    call "invhist_print.bbj",art03key$,prtr_dev,misc$
    close(prtr_dev,err=*next)
    if email=0 then goto inv_print_end
    pdf$=textfile$(1,len(textfile$)-3)+"pdf"
    call "PDF.01",textfile$,pdf$
    tries=0,pdf=unt
    while tries<5
        open(pdf,err=*next)pdf$;break
        tries=tries+1
        wait 1
    wend
    close(pdf,err=*next)
    if tries>4 then msg$="PDF File not found.";goto inv_print_end
    msg$="Email failed."
fi

Subj$="Invoice No. "
msgtxt$ = "Attachment is in Adobe Portable Document Format (PDF)." + $0A$
msgtxt$ = msgtxt$ + "Viewing and printing PDF files requires the Adobe Acrobat" + $0A$
msgtxt$ = msgtxt$ + "Reader, available free at www.adobe.com." + $0A$ + $0A$
msgtxt$ = msgtxt$ + "Rendered from Invoice History files." 

if misc$="DE" then 
  Subj$="Rechnung Nr. "
  msgtxt$ = "In der Anlage erhalten Sie Ihre Rechnung im PDF Format. Zum" + $0A$
  msgtxt$ = msgtxt$ + "Drucken von PDF Dokumenten benötigen Sie den Adobe Acrobat Reader." + $0A$
  msgtxt$ = msgtxt$ + "Falls dieser bei Ihnen noch nicht verfügbar sein sollte, so können" + $0A$
  msgtxt$ = msgtxt$ + "Sie diesen kostenlos downloaden www.adobe.com." + $0A$ + $0A$
  msgtxt$ = msgtxt$ + "Rendered from Invoice History files."
fi

if misc$="FR" then 
  Subj$="No. de Facture "
  msgtxt$ = "Vous trouverez ci-joint votre facture en format PDF. Si vous ne" + $0A$
  msgtxt$ = msgtxt$ + "npouviez pas l'ouvrir, vous avez besoin de la visionneuse Adobe Acrobat" + $0A$
  msgtxt$ = msgtxt$ + "Reader que vous pouvez obtenir gratuitement sur le site www.adobe.com." + $0A$ + $0A$
  msgtxt$ = msgtxt$ + "Rendered from Invoice History files."
fi

from$="BASIS Customer Service <customer-service@basis.cloud>"
subject$=subj$+art03.ar_inv_nbr$ + " from invoice history."
attach$=pdf$
to$=CVS(email$,3)
msgtxt$=msgtxt$
cc$=""
bcc$=""
call "sendEmail.src", from$, to$, cc$, bcc$, subject$, msgtxt$, attach$
msg$="Email sent to "+email$
inv_print_end:
erase textfile$,err=*next
erase pdf$,err=*next
PRINT (gb__sysgui)'SETCURSOR'(0)
if email then i=msgbox(msg$,0,"Email Status")
win_invoice_history.email_addr$=""
win_invoice_history$=fngb__put_fields$(win_id_invoice_history$,win_invoice_history$,"email_addr")

[Event Win=106 ID=320 Code=B <PUSH_BUTTON> (W106_C320_PUSH_BUTTON)]
rem ' Push button operated

warnMsg$ = "Caution: You should be careful generating a credit memo for an invoice using this function. If the original invoice has added users or special line codes they may not be reversed properly. Continue?"
warn = msgbox(warnMsg$,33+256,"Warning!")
if warn <> 1 then return

msg$=""
art03key$=art03$(1,20)
found=0
readrecord(art03,key=art03key$,dom=*next)art03$;found=1
if art03.total_sales<0 then msg$="This is a credit memo."
if !(found) then msg$="Select an invoice to credit."
if msg$<>"" then i=msgbox(msg$,0,"We have a problem");return
rem 'credit memo check
k$=art03.firm_id$+"  "+art03.customer_nbr$
credit_invoice$=""
read(are03,key=k$,dom=*next)
while credit_invoice$=""
  readrecord(are03,end=*break)are03$
  if pos(k$=are03$)<>1 then break
  if are03.total_sales>=0 then continue
  read(are13,key=are03$(1,17),dom=*next)
  while 2
    readrecord(are13,end=*break)are13$
    if are13$(1,17)<>are03$(1,17) then break
    if are13.line_code$<>"M" then continue
    ctmp$=cvs(are13.order_memo$,7)
    if pos("CREDIT"=ctmp$)=0 or pos("INV"=ctmp$)=0 then continue
    credited_invoice$=ctmp$(len(ctmp$)-6,7)
    if fnnum(art03.ar_inv_nbr$)=fnnum(credited_invoice$) then
      credit_invoice$=are03.ar_inv_nbr$
      break
    fi
  wend
wend
dim art03t$:fattr(art03$)
read(art03,key=k$+art03.ar_inv_nbr$+"000",dom=*next)
while credit_invoice$=""
  readrecord(art03,end=*break)art03t$
  if pos(k$=art03t$)<>1 then break
  if art03t.total_sales>=0 then continue
  read(art13,key=art03t$(1,17),dom=*next)
  while 2
    readrecord(art13,end=*break)art13$
    if art13$(1,17)<>art03t$(1,17) then break
    if art13.line_code$<>"M" then continue
    ctmp$=cvs(art13.order_memo$,7)
    if pos("CREDIT"=ctmp$)=0 or pos("INV"=ctmp$)=0 then continue
    credited_invoice$=ctmp$(len(ctmp$)-6,7)
    if fnnum(art03.ar_inv_nbr$)=fnnum(credited_invoice$) then
      credit_invoice$=art03t.ar_inv_nbr$
      break
    fi
  wend
wend
if credit_invoice$<>"" then
  btn=256
  msg$="Credit memo exist for this invoice: "+credit_invoice$+$0a$+"Continue with this credit?"
else
  msg$="Credit Invoice "+art03.ar_inv_nbr$+"?"
  btn=0
fi
i=msgbox(msg$,4+32+btn,"Confirm")
if i<>6 then return

print (gb__sysgui)'setcursor'(3)
call "BCS.02",art03.firm_id$,art03.customer_nbr$,art03.ar_inv_nbr$,msg$
PRINT (gb__sysgui)'SETCURSOR'(0)
order$="??"
if len(msg$)>6 then order$=msg$(len(msg$)-6,7)
i=msgbox(msg$,0,"Credit Memo Status")
if pos("ERROR"=cvs(msg$,7)) then return

xcrccaddr$=""
xcrsls$ = ""
read(arm10,key=art03.firm_id$+"F"+art03.slspsn_code$,dom=*next)xcrsls$; xcrccaddr$=cvs(xcrsls$(27,80),3)
xcrto$="customer-service@basis.cloud"
xcrfrom$="customer-service@basis.cloud"
if pos("SAM RENEW"=cvs(art03.AR_PO_NUMBER$,7)) then
    subject$="SAM Renew invoice "+art03.ar_inv_nbr$+" credited"
else
    if pos("DVK RENEW"=cvs(art03.AR_PO_NUMBER$,7)) then
        subject$="DVK Renew invoice "+art03.ar_inv_nbr$+" credited"
    else
        if pos("RNT RENEW"=cvs(art03.AR_PO_NUMBER$,7))
            subject$="RNT Renew invoice "+art03.ar_inv_nbr$+" credited"
        else
            subject$="Invoice "+art03.ar_inv_nbr$+" credited"
        fi
    fi
fi

dim tmm01$:fattr(tmm01$)
readrecord(tmm01,key=tmm01key$,knum=0,dom=*next)tmm01$
custId$ = tmm01.firm_id$ + tmm01.customer_nbr$+"  "+cvs(tmm01.cont_firm$,3)+" "

msgtxt$=art03.ar_inv_nbr$+" was credited by " + uid$ + " for " + custId$ + " " + xSNlist$ + " order #" + order$ + " in BUI Addon"
xcrbcc$=""
call "sendEmail.src", xcrfrom$, xcrto$, xcrccaddr$, xcrbcc$, subject$, msgtxt$, ""
rem ' debug: call "sendEmail.src", xcrfrom$, xcrbcc$, "", "", subject$, msgtxt$, ""

[Event Win=108 ID=0 Code=X <WIN_CLOSE> (W108_C0_WIN_CLOSE)]
rem ' Close Button Operated
goto pgm_exit

[Event Win=108 ID=105 Code=B <PUSH_BUTTON> (W108_C105_PUSH_BUTTON)]
rem ' Push button operated

print (gb__sysgui)'setcursor'(3)
win_renew_contracts$=fngb__get_fields$(win_id_renew_contracts$,win_renew_contracts$,"contract_list")
select_list$=win_renew_contracts.contract_list$
temp$=CTRL(gb__sysgui,new_dt_id,1,gb__win.RENEW_CONTRACTS)
new_date$=temp$(7,4)+temp$(1,2)+temp$(4,2)
noup=0
while 1
  readrecord(sn_select,end=*break)sn_select$
  if sn_select.new_date$=new_date$ then noup=1; break
wend
if noup then
  i=msgbox("Replace with current selection?",36,"Serial#'s for "+temp$+" on web site")
  if i<>6 then goto push_btn_end
fi
read(sn_select,key="",err=*next)
while 1
  k$=key(sn_select,end=*break)
  remove(sn_select,key=k$,err=*next)
wend
dim sn_select$:fattr(sn_select$)
while 1
  p=pos($0a$=select_list$)
  if p=0 then break
  temp$=select_list$(1,p-1)
  select_list$=select_list$(P+1)
  p=pos(" "=temp$)
  if p then 
    sn_select.serial_nbr$=temp$(1,p)
    sn_select.license_type$=cvs(temp$(p),3)
    sn_select.new_date$=new_date$
    sn_select$=field(sn_select$)
    writerecord(sn_select)sn_select$
  fi
wend
win_renew_contracts.contract_list$="Done"+$0a$+ff$+"00"
push_btn_end:
print (gb__sysgui)'disable'(push_btn_id)
win_renew_contracts$=fngb__put_screen$(win_id_renew_contracts$,win_renew_contracts$)
print (gb__sysgui)'setcursor'(0)

[Event Win=108 ID=107 Code=B <PUSH_BUTTON> (W108_C107_PUSH_BUTTON)]
rem ' Push button operated

gosub contract_search

[Event Win=108 ID=108 Code=B <PUSH_BUTTON> (W108_C108_PUSH_BUTTON)]
rem ' Push button operated

goto pgm_exit

[Event Win=109 ID=0 Code=X <WIN_CLOSE> (W109_C0_WIN_CLOSE)]
rem ' Close Button Operated

goto pgm_exit

[Event Win=109 ID=1 Code=B <PUSH_BUTTON> (W109_C1_PUSH_BUTTON)]
rem ' Push button operated
win_partner$=fngb__get_screen$(win_id_partner$,win_partner$)
temp$="NY"
extracted=0
extractrecord(tmm01,key=tmm01key$,err=*next)tmm01$;extracted=1
if extracted=0 then
  i=msgbox("Customer record locked by another user.",0,"Can't update")
  return
fi
extracted=0
extractrecord(arm02,key=armkey$+"  ",dom=*next)arm02$;extracted=1
tmm01.apdir_flag$=temp$((win_partner.apdir_chk$="1")+1,1)
tmm01.link_flag$="0"
p0=pos($ff$=win_partner.link_lbtn$)
if p0 then tmm01.link_flag$=win_partner.link_lbtn$(p0+1,1)
tmm01.bblist_flag$=temp$((win_partner.bblist_chk$="1")+1,1) 
tmm01.plan_flag$=temp$((win_partner.plan_chk$="1")+1,1) 
tmm01.qstnr_flag$=temp$((win_partner.qstnr_chk$="1")+1,1) 
tmm01.goals_flag$=temp$((win_partner.goals_chk$="1")+1,1) 
temp=apdir_dt!.getValue()
tmm01.apdir_dt$=fill(8)
if temp>0 then tmm01.apdir_dt$=date(temp:"%Y%Mz%Dz")
temp=plan_dt!.getValue()
tmm01.plan_dt$=fill(8)
if temp>0 then tmm01.plan_dt$=date(temp:"%Y%Mz%Dz")
temp=qstnr_dt!.getValue()
tmm01.qstnr_dt$=fill(8)
if temp>0 then tmm01.qstnr_dt$=date(temp:"%Y%Mz%Dz")
temp=goals_dt!.getValue()
tmm01.goals_dt$=fill(8)
if temp>0 then tmm01.goals_dt$=date(temp:"%Y%Mz%Dz")
knw=(tmm01.apdir_flag$="Y")*(tmm01.link_flag$<>"2")*(tmm01.bblist_flag$="Y")
knw=knw*(win_partner.event_chk$="1")*5
mkt=(tmm01.qstnr_flag$="Y")*(tmm01.plan_flag$="Y")*(tmm01.goals_flag$="Y")*5
partner_disc=10,partner_type$="PB"
if partner_sales>=5000 then partner_type$="PS",partner_disc=20+knw+mkt
if partner_sales>=12500 and mkt then 
  partner_type$="PG"
  partner_disc=30+mkt+knw
fi
if partner_sales>=25000 and mkt*knw then partner_disc=45,partner_type$="PP"
code_ok=(discount!.get(partner_disc)<>NULL())
if code_ok then
  disc_code$=discount!.get(partner_disc)
else
  i=msgbox(str(partner_disc)+"% is not a valid discount.",0,"")
fi
if disc_code$<>tmm01.disc_code$ and code_ok then
  if extracted=0 then 
    i=msgbox("AR Record is locked.",0,"Can't update discount.")
  else  
    tmm01.disc_code$=disc_code$
    tmm01.cust_type$=partner_type$
    arm02.disc_code$=disc_code$
    arm02.cust_type$=partner_type$
    arm02$=field(arm02$)
    writerecord(arm02,key=armkey$+"  ")arm02$
  fi
fi
  
tmm01$=field(tmm01$)
writerecord(tmm01,key=tmm01key$)tmm01$
gosub show_partner

[Event Win=109 ID=200 Code=B <PUSH_BUTTON> (W109_C200_PUSH_BUTTON)]
rem ' Push button operated

goto pgm_exit

[Event Win=110 ID=0 Code=X <WIN_CLOSE> (W110_C0_WIN_CLOSE)]
rem ' Close Button Operated
goto pgm_exit

[Event Win=110 ID=1 Code=B <PUSH_BUTTON> (W110_C1_PUSH_BUTTON)]
rem ' Push button operated
win_report$=fngb__get_screen$(win_id_report$,win_report$)
if win_report.start_cust$>win_report.end_cust$ then
  first$=win_report.end_cust$
  last$=win_report.start_cust$
  name1$=win_report.end_name$
  name2$=win_report.start_name$
  win_report.end_name$=name2$
  win_report.start_name$=name1$
  win_report.start_cust$=first$
  win_report.end_cust$=last$
  win_report$=fngb__put_fields$(win_id_report$,win_report$,"start_cust,start_name,end_cust,end_name")
fi
em$=cvs(win_report.email$,3)
p=pos("@"=em$)
if p<2 or p=len(em$) then
  print(gb__sysgui)'focus'(email_id)
  return
fi
LET list$=CTRL(gb__sysgui,selected_id,7,gb__win.report)
code$=""
p=pos($0a$=list$)
while p
  temp$=list$(1,p),list$=list$(p+1)
  p=pos($0a$=list$)
  if len(temp$)<3 then continue
  p1=pos(temp$=c_list$)
  if p1>2 then code$=code$+c_list$(p1-2,2)
wend
if code$="" then
  LET list$=CTRL(gb__sysgui,country_id,7,gb__win.report)
  if pos($ff$=list$)=0 then list$=list$+$ff$+"0"
  win_report.country_lbox$=list$
  win_report$=fngb__put_fields$(win_id_report$,win_report$,"country_lbox")
  print(gb__sysgui)'focus'(country_id)
  return
fi
call "ec_open::job_queue";chan$=chan$+str(job_queue:"000")
job_queue.firm_id$=firm$
job_queue.customer_nbr$=first$
job_queue.job$="CUSTSALES"
job_queue.e_mail$=em$
job_queue.job_date$=last$
job_queue.available$=code$
job_queue$=field(job_queue$)
writerecord(job_queue)job_queue$
title$="Report Status"
msg$="Report has been queued. It will be processed and sent in a few minutes."
LET i=MSGBOX(msg$,0,title$)
goto pgm_exit

[Event Win=110 ID=116 Code=B <PUSH_BUTTON> (W110_C116_PUSH_BUTTON)]
rem ' Push button operated
ctl_id=selected_id
gosub lbox_change

[Event Win=110 ID=120 Code=f0 <LOST_FOCUS> (W110_C120_LOST_FOCUS)]
rem ' Control lost focus
win_report$=fngb__get_fields$(win_id_report$,win_report$,"start_cust")
c$=str(fnnum(win_report.start_cust$):"000000")
read(tmm01,key=firm$+c$,dom=*next)
found=0
readrecord(tmm01,err=*next)tmm01$;found=1
if firm$=tmm01.firm_id$ then name1$=tmm01.cont_firm$,first$=tmm01.customer_nbr$
win_report.start_cust$=first$
win_report.start_name$=name1$
win_report$=fngb__put_fields$(win_id_report$,win_report$,"start_cust,start_name")

[Event Win=110 ID=125 Code=f0 <LOST_FOCUS> (W110_C125_LOST_FOCUS)]
rem ' Control lost focus
win_report$=fngb__get_fields$(win_id_report$,win_report$,"end_cust")
c$=str(fnnum(win_report.end_cust$):"000000")
read(tmm01,key=firm$+c$,dom=*next)
found=0
readrecord(tmm01,err=*next)tmm01$;found=1
if firm$=tmm01.firm_id$ then name2$=tmm01.cont_firm$,last$=tmm01.customer_nbr$
win_report.end_cust$=last$
win_report.end_name$=name2$
win_report$=fngb__put_fields$(win_id_report$,win_report$,"end_cust,end_name")

[Event Win=110 ID=2 Code=B <PUSH_BUTTON> (W110_C2_PUSH_BUTTON)]
rem ' Push button operated
goto pgm_exit

[Event Win=110 ID=7 Code=B <PUSH_BUTTON> (W110_C7_PUSH_BUTTON)]
rem ' Push button operated
ctl_id=country_id
gosub lbox_change

[Event Win=110 ID=7 Code=f0 <LOST_FOCUS> (W110_C7_LOST_FOCUS)]
rem ' Control lost focus


[Function (Contract search)]
rem ' -----------------------------------------------------------------
rem ' Contract search
rem ' -----------------------------------------------------------------

Contract_search:
print (gb__sysgui)'setcursor'(3)
win_renew_contracts$=fngb__get_screen$(win_id_renew_contracts$,win_renew_contracts$)
current$=CTRL(gb__sysgui,current_dt_id,1,gb__win.RENEW_CONTRACTS)
new$=CTRL(gb__sysgui,new_dt_id,1,gb__win.RENEW_CONTRACTS)
include_sam=win_renew_contracts.sam_chk
include_rental=win_renew_contracts.rental_chk
cur_dt$=current$(7,4)+current$(1,2)+current$(4,2)
new_dt$=new$(7,4)+new$(1,2)+new$(4,2)
snlist$=""
deactivate_list$=""
if new_dt$<=cur_dt$ then goto contract_search_end
y=num(date(today:"%Yl"))
m=num(date(today:"%Mz"))
m=m-5
if m<=0 then m=m+12,y=y-1
deact_dt$=date(jul(y,m,1):"%Yl%Mz%Dz")
read (smc01,key=firm$+cust$,knum=1,dom=*next)
while 1
  readrecord(smc01,end=*break)smc01$
  if smc01.firm_id$+smc01.customer_nbr$<>firm$+cust$ then break
  if pos("DV"=smc01.contr_type$)=1 then continue
  if smc01.expire_on_dt$>cur_dt$ then continue
  if pos("SM"=smc01.contr_type$)=1 then
    if smc01.sam_active$="N" or include_sam=0 then continue
  fi
  deactivate=0
  if pos("RN"=smc01.contr_type$)then
    if include_rental=0 then continue
    if smc01.expire_on_dt$<deact_dt$ then deactivate=1
  fi
  read(snm01,key=smc01.contract$,knum=3,dom=*next)
  while 2
    readrecord(snm01,end=*break)snm01$
    if snm01.contract$<>smc01.contract$ then break
    if snm01.active_flag$<>"Y" then continue
    if snm01.firm_id$+snm01.customer_nbr$<>firm$+cust$ then break
    if deactivate then 
      deactivate_list$=deactivate_list$+snm01.serial_nbr$+$0a$
    else
      snlist$=snlist$+snm01.serial_nbr$+snm01.license_type$+$0a$
    fi
  wend
wend
while 1
  p=pos($0a$=deactivate_list$)
  if p=0 then break
  sn$=deactivate_list$(1,p-1)
  deactivate_list$=deactivate_list$(p+1)
  readrecord(snm01,knum=0,key=sn$,dom=*continue)snm01$
  snm01.active_flag$="N"
  snm01.avail_support=0
  snm01$=field(snm01$)
  writerecord(snm01)snm01$
wend

contract_search_end:
if snlist$="" then 
  print (gb__sysgui)'disable'(push_btn_id) 
else 
 print (gb__sysgui)'enable'(push_btn_id)
 snlist$=snlist$+$ff$+"0"
fi
if firm$+cust$<>"01007808" then print (gb__sysgui)'disable'(push_btn_id)
win_renew_contracts.contract_list$=snlist$
win_renew_contracts$=fngb__put_screen$(win_id_renew_contracts$,win_renew_contracts$)
print (gb__sysgui)'setcursor'(0)

return

[Function (bcomm_options)]
rem ' -----------------------------------------------------------------
rem ' bcomm_options
rem ' -----------------------------------------------------------------

bcomm_options:
option$=""
read (ecm01,key=eckey$,dom=*next)
while 1
  readrecord(ecm01,end=*break)ecm01$
  if pos(eckey$=ecm01$)<>1 then break
  temp$=ecm01.user_code$
  if temp$="  " then 
    option$=option$+"Main account  "+$0a$
  else 
    option$=option$+"User code "+temp$+$0a$
  fi
wend
del$=$0a$+"<To delete, type 'DELETE' in"+$0a$+"the password and press Ok>"+$0a$
if security_level<5 then del$=""
option$=option$+"New account"+$0a$+del$+$ff$+"0"
win_bcomm.option_list$=option$
win_bcomm$=fngb__put_fields$(win_id_bcomm$,win_bcomm$,"option_list")
return

[Function (change_smc01_email)]
rem ' -----------------------------------------------------------------
rem ' change_smc01_email
rem ' -----------------------------------------------------------------

change_smc01_email:
call "ec_open::smc01"
firm$=tmm03.firm_id$
cust$=tmm03.customer_nbr$
email$=cvs(tmm03.e_mail$,3)
read(smc01,key=firm$+cust$,knum=1,dom=*next)
samcount=0,kitcount=0,othcount=0,ms$="",mk$="",mo$=""
while 1
  readrecord(smc01)smc01$
  if smc01.customer_nbr$<>cust$ or smc01.firm_id$<>firm$ then break
  if email$=cvs(smc01.e_mail$,3) then continue
  smc01.e_mail$=email$
  smc01$=field(smc01$)
  writerecord(smc01)smc01$
  if pos("SM"=smc01.contr_type$) then samcount=samcount+1,ms$=str(samcount)+" SAM"+$0a$
  if pos("DV"=smc01.contr_type$) then kitcount=kitcount+1,mk$=str(kitcount)+" KIT"+$0a$
  if pos("SM"=smc01.contr_type$)=0 and pos("DV"=smc01.contr_type$)=0 then othcount=othcount+1,mo$=str(othcount)+" Other"
wend
close(smc01)
if samcount+kitcount+othcount then
  i=msgbox(ms$+mk$+mo$,0,"Contracts Changed")
else
  i=msgbox("No contracts found",0,"")
fi
return

[Function (comment_list)]
rem ' -----------------------------------------------------------------
rem ' comment_list
rem ' -----------------------------------------------------------------

comment_list:
comlist$=""
comkeys$=""
read(tmm02,key=tmm01key$,dom=*next)
while 1
  readrecord(tmm02,end=*break)tmm02$
  if pos(tmm01key$=tmm02$)<>1 then break
  m$="00000000"
  temp$=m$
  temp$=str(99999999-num(tmm02.comment_dt$,err=*next):m$)
  comlist$=comlist$+temp$(5,2)+"/"+temp$(7,2)+"/"+temp$(1,4)
  comlist$=comlist$+" "+tmm02.comment$+$0a$
  comkeys$=comkeys$+tmm01key$+tmm02.comment_dt$+tmm02.sequence$+$0a$
wend
win_comments$=fngb__get_screen$(win_id_comments$,win_comments$)
win_comments.comment_list$=comlist$+$ff$+"0"
win_comments.comment$=""
win_comments.date$=""
win_comments.seq$=""
win_comments$=fngb__put_screen$(win_id_comments$,win_comments$)
print(gb__sysgui)'disable'(ok_btn_id),'focus'(comment_list_id)
return

[Function (contact_options)]
rem ' -----------------------------------------------------------------
rem ' contact_options
rem ' -----------------------------------------------------------------

contact_options:
option$="",primary$="",a_p$="",sam_con$="",sam_email$=""
read (tmm03,key=tmm01key$,dom=*next)
while 1
  readrecord(tmm03,end=*break)tmm03$
  if pos(tmm01key$=tmm03$)<>1 then break
  option$=option$+tmm03.sequence_nbr$+" "+tmm03.contact_name$+$0a$
  if tmm03.primary_contact$="Y" then primary$=tmm03$(1,17)
  if tmm03.ap_contact$="Y" then a_p$=tmm03$(1,17)
  if tmm03.sam_contact$="Y" then sam_con$=tmm03$(1,17),sam_email$=tmm03.e_mail$
wend
del$=$0a$+"<To delete, type 'DELETE' in the"+$0a$+"name field and press Ok>"+$0a$
if security_level<5 then del$=""
option$=option$+"New contact"+$0a$+del$+$ff$+"0"
win_contacts.option_list$=option$
win_contacts$=fngb__put_fields$(win_id_contacts$,win_contacts$,"option_list")

return

[Function (country_list)]
rem ' -----------------------------------------------------------------
rem ' country_list
rem ' -----------------------------------------------------------------

country_list:
temp$=fill(32)
read record(country,key=tmm03.country_code$,knum=0,dom=*next)temp$
temp$=cvs(temp$(3,30),3)
index=-1
if country_list$<>"" then goto country_loop_end
read(country,key="",knum=1,dom=country_loop)
country_loop:
read record (country,end=country_loop_end)country$
country_list$=country_list$+cvs(country.country_name$,3)+$0a$
goto country_loop
country_loop_end:
temp=pos(temp$=country_list$)
if temp then index=pos($0a$=country_list$(1,temp),1,0)
temp=pos($ff$=country_list$)
if temp then country_list$=country_list$(1,temp-1)
if index>=0 then 
    country_list$=country_list$+$ff$+str(index)
else
    country_list$=country_list$+$ff$+str(-1)
fi
return

[Function (cust_detail)]
rem ' -----------------------------------------------------------------
rem ' cust_detail
rem ' -----------------------------------------------------------------

cust_detail:
ctl_id=num(fattr(win_search_result$,"cust_list","ID"))
p=dec(ctrl(gb__sysgui,ctl_id,2))
tmm01key$=tmkey$[p]
dim tmm01$:fattr(tmm01$)
readrecord(tmm01,knum=0,key=tmm01key$,dom=*next)tmm01$
detail$=""
temp$=cvs(tmm01.address_1$,3)
if temp$<>"" then detail$=temp$+$0a$
temp$=cvs(tmm01.address_2$,3)
if temp$<>"" then detail$=detail$+temp$+$0a$
temp$=cvs(tmm01.address_3$,3)
if temp$<>"" then detail$=detail$+temp$+$0a$
temp$=cvs(tmm01.address_4$,3)
if temp$<>"" then detail$=detail$+temp$+$0a$
temp$=cvs(tmm01.city$,3)+"  "+tmm01.state$
if cvs(temp$,3)<>"" then detail$=detail$+temp$+$0a$
temp$=tmm01.zip_code$
if cvs(temp$,3)<>"" then detail$=detail$+"Zip: "+temp$+$0a$
cncode$=tmm01.country_code$
if tmm01.country_code$<>"US" then
  readrecord(country,key=tmm01.country_code$,knum=0,dom=*next)country$
  detail$=detail$+country.country_name$+$0a$
fi
temp$=fnusphone$(tmm01.phone_number$)
if temp$<>"" then detail$=detail$+"Phone: "+temp$+$0a$
dim tmm05$:fattr(tmm05$)
readrecord(tmm05,key=tmm01key$,dom=*next)tmm05$
temp$=cvs(tmm05.email$,3)
if temp$<>"" then detail$=detail$+temp$+$0a$
ct$=cvs(tmm01.cont_name$,3)
if ct$<>"" then detail$=detail$+$0a$+ct$+$0a$
read(tmm03,key=tmm01key$,dom=*next)
while 1
  readrecord(tmm03,end=*break)tmm03$
  if pos(tmm01key$=tmm03$)<>1 then break
  temp$=cvs(tmm03.contact_name$,3)
  if temp$<>"" and temp$<>ct$ then detail$=detail$+temp$+$0a$
wend
win_search_result.cust_detail$=detail$
win_search_result$=fngb__put_fields$(win_id_search_result$,win_search_result$,"cust_detail")


return

[Function (def fnaondate)]
rem ' -----------------------------------------------------------------
rem ' def fnaondate
rem ' -----------------------------------------------------------------

def fnaondate$(ymd$)
y=1900+asc(ymd$(1))-32,m=asc(ymd$(2))-32,d=asc(ymd$(3))-32
if m<1 or d<1 then d$="Unknown" else d$=date(jul(y,m,d):"%Mz/%Dz/%Y")
return d$
fnend

[Function (def fnaonjul(ymd$))]
rem ' -----------------------------------------------------------------
rem ' def fnaonjul(ymd$)
rem ' -----------------------------------------------------------------

def fnaonjul(ymd$)
  y=1900+asc(ymd$(1))-32,m=asc(ymd$(2))-32,d=asc(ymd$(3))-32
  IF m<1 OR d<1 THEN LET jd=0 ELSE LET jd=JUL(y,m,d)
  return jd
fnend

[Function (def fndate)]
rem ' -----------------------------------------------------------------
rem ' def fndate
rem ' -----------------------------------------------------------------

def fndate$(julian)
  d$=chr(asc(date(julian:"%Yp"))+32)+chr(asc(date(julian:"%Mp"))+32)+chr(asc(date(julian:"%Dp"))+32)
  return d$
fnend

[Function (fnjul)]
rem ' -----------------------------------------------------------------
rem '  def fnjul
rem ' -----------------------------------------------------------------

def fnjul(yyyymmdd$)
  yyyy=num(yyyymmdd$(1,4)),mm=num(yyyymmdd$(5,2)),dd=num(yyyymmdd$(7,2))
  annual_jul=jul(yyyy,mm,dd)
  return (annual_jul)
fnend

[Function (fnnum)]
rem ' -----------------------------------------------------------------
rem ' fnnum
rem ' -----------------------------------------------------------------

def fnnum(tempnum$)
tempnum1$=""
while len(tempnum$)
if pos(tempnum$(1,1)="-.0123456789") then tempnum1$=tempnum1$+tempnum$(1,1)
tempnum$=tempnum$(2)
wend
return num(tempnum1$,err=badnum)
badnum:
return 0
fnend

[Function (fnusphone)]
rem ' -----------------------------------------------------------------
rem ' fnusphone
rem ' -----------------------------------------------------------------

def fnusphone$(xx$)
if pos(cncode$="  US",2)=0 then return xx$
xx$=fnundo$(xx$)
if len(xx$)=10 then xx$=xx$(1,3)+"."+xx$(4,3)+"."+xx$(7)
return xx$
fnend

def fnundo$(xx$)
xx$=cvs(xx$,3),xx1$=""
while len(xx$)
 if pos(xx$(1,1)="0123456789") then xx1$=xx1$+xx$(1,1)
 xx$=xx$(2)
wend
return xx1$
fnend

[Function (invoice_grid)]
rem ' -----------------------------------------------------------------
rem ' invoice_grid
rem ' -----------------------------------------------------------------

invoice_grid:
print (gb__sysgui)'setcursor'(3)
type$=cvs(type$,7)
rows=0
max=25
count=0
gridVector!=sysgui!.makeVector()
salesWindow!.getControl(grid_id).clearMainGrid()
art01k$=armkey$(1,2)+"  "+armkey$(3),dk$=""
if readdir<0 then dk$="9999999"
x=0,x=num(start_inv$,err=*next)
if x then dk$=str(x:"0000000")
read (art01,key=art01k$+dk$,dom=*next)
while 1
  read record(art01,dir=readdir,end=*break)art01$
  if pos(art01k$=art01$)<>1 then 
    if readdir<0 and count=0 then continue else break
  fi 
  detail$=""
  tot=0
  start_inv$=art01.ar_inv_nbr$
  if count>=max then break
  read(art11,key=art01$(1,17),dom=*next)
  while 2
    read record(art11,end=*break)art11$
    if pos(art01$(1,17)=art11$)<>1 then break
    if art11.AR_TRAN_CODE$="C" then tot=tot+art11.trans_amt
    if art11.AR_TRAN_CODE$="C" and art11.adj_disc_amt <> 0 then tot=tot+art11.adj_disc_amt
    detail$=detail$+art11.trans_date$+art11.REF_CHK_NBR$
    detail$=detail$+str(art11.trans_amt+art11.adj_disc_amt:mask$)
    detail$=detail$+str(art01.invoice_amt+tot:mask$)+$0a$
  wend
  balance=art01.invoice_amt+tot
  if balance and pos("P"=type$)=1 then continue
  if balance=0 and pos("U"=type$)=1 then continue
  count=count+1
  gridVector!.addItem(art01.ar_inv_nbr$)
  gridVector!.addItem(fnaondate$(art01.invoice_date$))
  gridVector!.addItem(fnaondate$(art01.due_date$))
  gridVector!.addItem(" ")
  gridVector!.addItem(str(art01.invoice_amt:mask$))
  gridVector!.addItem(" ")
  if tot then temp$=" " else temp$=str(balance:mask$)
  gridVector!.addItem(temp$)
  rows=rows+1
  p=pos($0a$=detail$)
  while p
    gridVector!.addItem(" ")
    gridVector!.addItem(" ")
    gridVector!.addItem(fnaondate$(detail$(1,3)))
    gridVector!.addItem(detail$(4,7))
    gridVector!.addItem(" ")
    gridVector!.addItem(detail$(11,len(mask$)))
    gridVector!.addItem(detail$(11+len(mask$),len(mask$)))
    detail$=detail$(p+1)
    rows=rows+1
    p=pos($0a$=detail$)
  wend
wend
if rows then
  salesWindow!.getControl(grid_id).setNumRows(rows)
  salesWindow!.getControl(grid_id).setSelectedCell(0,0)
  salesWindow!.getControl(grid_id).setCellText(gridVector!)
fi
print (gb__sysgui)'setcursor'(0)
return

[Function (invoice_search)]
rem ' -----------------------------------------------------------------
rem ' invoice_search
rem ' -----------------------------------------------------------------

invoice_search:
print (gb__sysgui)'setcursor'(3)

rem ' load all invoices from the first of last year to today
srchYear = num(date(0:"%Y")) - 4
jul_limit = jul(srchYear,1,1)

win_invoice_history$=fngb__get_fields$(win_id_invoice_history$,
:win_invoice_history$,"invoice_nbr,po")
invoice$=cvs(win_invoice_history.invoice_nbr$,3)
invnum=num(invoice$,err=*next),invoice$=str(invnum:"0000000")
po$=cvs(win_invoice_history.po$,3)
jul_dt=invoice_dt!.getValue()
inv_search=(invoice$<>art03.ar_inv_nbr$ and invnum<>0)
po_search=(pos(po$=art03.ar_po_number$)=0 and po$<>"")
date_search=(jul_dt>=0 and jul_dt<>fnaonjul(art03.invoice_date$) and inv_search+po_search=0)
recent=(po_search+inv_search+date_search=0)
readdir=1
k$=artkey$
if inv_search then k$=k$+invoice$
if recent then readdir=-1,k$=k$+"9999999"
invoice_list$=""
count=0,first=1
read(art03,key=k$,dom=*next)
while 1
  readrecord(art03,dir=readdir,end=*break)art03$
  if pos(artkey$=art03$)<>1 then if first and readdir<0 then first=0;continue else break
  if inv_search and invoice$<>art03.ar_inv_nbr$ then break  
  if po_search and pos(cvs(po$,4)=cvs(art03.ar_po_number$,4))=0 then continue
  if date_search and jul_dt<>fnaonjul(art03.invoice_date$) then continue 
  inv_date$=fnaondate$(art03.invoice_date$)

  rem ' if recent and count>19 then break
  rem ' modified this limit test 7/02/2012
  if recent and jul_limit > fnaonjul(art03.invoice_date$) then break

  invoice_list$ = invoice_list$ + art03.ar_inv_nbr$ + " " + inv_date$ + " " +art03.ar_po_number$ + $0a$
  count = count + 1
wend
if count=0 then 
  invoice_list$="Not found"+$0a$
  art03.ar_inv_nbr$=invoice$
  art03.ar_po_number$=po$
else 
  invoice_list$=invoice_list$+$ff$+"0"
fi
win_invoice_history.invoice_list$=invoice_list$
win_invoice_history$=fngb__put_fields$(win_id_invoice_history$,
:win_invoice_history$,"invoice_list")
print (gb__sysgui)'setcursor'(0)

invoice_history_created = 1

if count then gosub show_invoice

return

[Function (lbox_change)]
rem ' -----------------------------------------------------------------
rem ' lbox_change
rem ' -----------------------------------------------------------------

lbox_change:
LET changelist$=CTRL(gb__sysgui,ctl_id,1,gb__win.report)
LET temp=LEN(changelist$)
IF temp AND changelist$(temp,1)<>$0A$ THEN LET changelist$=changelist$+$0A$
if ctl_id=country_id then 
  list1$=country_list$
  list2$=selected_list$
else 
  list2$=country_list$
  list1$=selected_list$
fi
while 1
  p=pos($0a$=changelist$)
  if p=0 then break
  temp$=changelist$(1,p), changelist$=changelist$(p+1)
  p=pos(temp$=list2$)
  if p=0 then list2$=list2$+temp$
  p=pos(temp$=list1$),lt=len(temp$)
  if p then list1$(p,lt)=fill(lt)
wend
temp$=list1$,list1$=""
while 1
  p=pos($0a$=temp$)
  if p=0 then break
  list1$=list1$+cvs(temp$(1,p),3)
  temp$=temp$(p+1) 
wend 
if ctl_id=country_id then 
  country_list$=list1$
  selected_list$=list2$
else 
  country_list$=list2$
  selected_list$=list1$
fi
win_report.country_lbox$=country_list$
win_report.selected_lbox$=selected_list$
win_report$=fngb__put_fields$(win_id_report$,win_report$,"country_lbox,selected_lbox")
return

[Function (moveArchiveInvoiceToWebServer)]
rem ' -----------------------------------------------------------------
rem ' moveArchiveInvoiceToWebServer
rem ' -----------------------------------------------------------------

moveArchiveInvoiceToWebServer:

basedir$="/mnt/data/archives/"+xFirm$+"/invoices/"
archive$=basedir$+xInvoiceNbr$(1,1)+"/"+xInvoiceNbr$(2,2)+"/"+xInvoiceNbr$(4,2)
invoicefile$ = archive$ + "/" + xInvoiceNbr$ + ".pdf"

checkInv = unt
found = 0
open(checkInv,err=*next)invoicefile$; found = 1
close(checkInv)

if found
    rem ' htmlviewer!.setVisible(1)
    rem ' invoice!.setVisible(1)
    rem ' copy the file to the web server
    sslReq = BBUtils.isWebServerSSLEnabled()
    url$ = BBUtils.copyFileToWebServer(invoicefile$,"appreviewtemp", sslReq)
    urlList! .add(url$)
    htmlviewer!.setUrl(url$, 1)
    htmlviewer!.setVisible(1)
else
    rem ' htmlviewer!.setVisible(1)
    rem ' invoice!.setVisible(1)
    rem ' htmlviewer!.setUrl("", 1)
    htmlviewer!.setVisible(0)
fi

return

[Function (print_to_file)]
rem ' -----------------------------------------------------------------
rem ' print_to_file
rem ' -----------------------------------------------------------------

print_to_file:
if printer$="PX" then goto printer_px

if file_name$="" then file_name$=desc$
gosub file_name_ok
if file_name$="" then return
PFXXXA: IOLIST A0$(1),A1$(1)
SYM02A: IOLIST P0$(1),P1$(1),P[ALL]
DIM P0$(3),P1$(128),P[4],A0$(2),A1$(64)
FIND (SYM02,KEY=UID$,DOM=*next)IOL=SYM02A
IF P[0]<1 THEN LET P[0]=10
FOR X=101 TO 103
IF POS(P1$(X,1)="0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ")=0 THEN p1$(101,3)="   "
NEXT X
IF cvs(P1$(101,3),3)="" THEN LET P1$(101,3)=uid$
PFNAME$="PF"+cvs(P1$(101,3),3),PFINDEX$=PFNAME$+"A"
status=0,tries=0
PFILE_DEV=unt
while status=0
  open(pfile_dev,err=*next)PFINDEX$;break
  CALL "SYC.MA","M",PFINDEX$,"SY","2",0,96,STATUS
  if tries then status=1
  tries=tries+1
wend

if status then msg$="Unable To Define PF Index File";goto err_msg
report=1,oldest$="",old=0
while REPORT<=P[0]
  LET A0$(1)=STR(REPORT:"00")
  FIND (PFILE_DEV,KEY=A0$,DOM=*break)IOL=PFXXXA
  if OLDEST$="" OR A1$(41,6)<OLDEST$ THEN LET OLDEST$=A1$(41,6),OLD=REPORT
  report=report+1
wend
if report>p[0] then
  report=old
  A0$(1)=STR(REPORT:"00")
  REMOVE (PFILE_DEV,KEY=A0$,DOM=*next)
fi
PFNAME$=PFNAME$+STR(REPORT)
ERASE PFNAME$,ERR=*next
CALL "SYC.MA","C",PFNAME$,"SY","",0,0,STATUS
IF STATUS then msg$="Unable To Create PF Output File";goto err_msg
msg$="Unable to open "+pfname$
prtr_dev=unt
open(prtr_dev,err=err_msg)PFNAME$
A1$(1)=file_name$
a1$(41,6)=date(0:"%Yz%Mz%Dz")
if a1$(41,1)="0" then a1$(41,1)="A"
A1$(47,8)=DATE(0:"%hz:%mz %p")
WRITE (PFILE_DEV,KEY=A0$)IOL=PFXXXA
rem LOCK (PRTR_DEV)
CLOSE (pfile_dev,ERR=*next)
msg$=""
return

printer_px:
if email$="" then msg$="Enter an email address"; goto err_msg
TMP$=stbl("TEMP")
IF tmp$(LEN(tmp$),1)<>"/" THEN LET tmp$=tmp$+"/"
textfile$=tmp$+textfile$
erase textfile$,err=*next
prtr_dev=unt
OPEN (PRTR_DEV,MODE="FILE="+textfile$,ERR=err_msg)printer$
return

file_name_ok:
i=msgbox("Print to: "+file_name$,4+32,"Output to File")
if i=7 then file_name$=""
return

err_msg:
if msg$="" then msg$="Error "+str(err)+" in "+str(tcb(10))
return

[Function (setup_invoice_history)]
rem ' -----------------------------------------------------------------
rem ' setup_invoice_history
rem ' -----------------------------------------------------------------

setup_invoice_history:

win_id_invoice_history$=fngb__win_id$(gb__win.invoice_history)
dim win_invoice_history$:fngb__template$(win_id_invoice_history$)
invoice_list_id=num(fattr(win_invoice_history$,"invoice_list","ID"))
search_btn_id=num(fattr(win_invoice_history$,"search_btn","ID"))
credit_btn_id=num(fattr(win_invoice_history$,"credit_btn","ID"))
call "ec_open::ART03"; chan$=str(ART03:"000")
call "ec_open::ART13"; chan$=chan$+str(ART13:"000")
call "ec_open::ART73"; chan$=chan$+str(ART73:"000")
call "ec_open::ART83"; chan$=chan$+str(ART83:"000")
call "ec_open::ARE03"; chan$=str(ART03:"000")
call "ec_open::ARE13"; chan$=chan$+str(ART13:"000")
f$=fid(art03)
f$=f$(9)
p=pos("ART-03"=f$)
if p then f$=f$(1,p-1) else f$=""
sym07=unt
open(sym07)f$+"SYM-07";chan$=chan$+str(sym07:"000")
sym22=unt
open(sym22)f$+"SYM-22";chan$=chan$+str(sym22:"000")
read(sym22,key=uid$,dom=*next)
printer_list$="Send signed invoice from archive"+$0a$
printer_list$=printer_list$+"Email PDF Form"+$0a$
while 1
  read(sym22,end=*break)p$
  if pos(uid$=p$)<>1 then break
  read(sym07,key=p$(4),dom=*continue)x1$,x2$
  if pos(x1$="PF,PX") then continue
  printer_list$=printer_list$+x1$+" "+x2$(1,30)+$0a$
wend
if printer_list$<>"" then printer_list$=printer_list$+$ff$+"0"
print (gb__sysgui)'context'(gb__win.invoice_history),'move'(0,x,y),
:'enable'(0),'show'(0),'focus'(0),'raise'
if security_level>4 then print (gb__sysgui)'show'(credit_btn_id) else print (gb__sysgui)'hide'(credit_btn_id)
invoice_dt_id=num(fattr(win_invoice_history$,"date","ID"))
invoice_list_id=num(fattr(win_invoice_history$,"invoice_list","ID"))
printers_id=num(fattr(win_invoice_history$,"printers","ID"))
newWindow!=sysgui!.getWindow(gb__win.invoice_history)
invoice_dt!=newWindow!.getControl(invoice_dt_id)
invoiceWindow!=sysgui!.getWindow(gb__win.invoice_history)
win_invoice_history.printers$=printer_list$
win_invoice_history$=fngb__put_fields$(win_id_invoice_history$,win_invoice_history$,"printers")
artkey$=tmm01key$(1,2)+"  "+tmm01key$(3,6)
gosub invoice_search


return

[Function (show_bcomm)]
rem ' -----------------------------------------------------------------
rem ' show_bcomm
rem ' -----------------------------------------------------------------

show_bcomm:
win_bcomm$=fngb__get_screen$(win_id_bcomm$,win_bcomm$)
temp$=ctrl(gb__sysgui,options_id,1,gb__win.bcomm)
if pos("<"=temp$) or pos(">"=temp$) or temp$="" then return
dim ecm01$:fattr(ecm01$)
if pos("New"=temp$)=1 and security_level>4 then
  action$="add" 
else
  action$="edit"
  readrecord(ecm01,key=eckey$+temp$(len(temp$)-1,2),dom=*next)ecm01$
fi
win_bcomm.customer_nbr$=eckey$(1,2)+"-"+eckey$(3)
win_bcomm.user_code$=cvs(ecm01.user_code$,3)
win_bcomm.first_name$=cvs(ecm01.first_name$,3)
win_bcomm.last_name$=cvs(ecm01.last_name$,3)
win_bcomm.email_1$=cvs(ecm01.e_mail$,3)
win_bcomm.email_2$=cvs(ecm01.e_mail2$,3)
win_bcomm.password$=cvs(ecm01.password$,3)

lockUserAcct$ = ecm01.available$(1,1)
if lockUserAcct$ = "Y" then
    win_bcomm.lockUserAccount$ = "1"
else
    win_bcomm.lockUserAccount$ = "0"
fi

win_bcomm$=fngb__put_screen$(win_id_bcomm$,win_bcomm$)
if action$="add" then focus_id=user_code_id else focus_id=options_id
print (gb__sysgui)'focus'(focus_id)
return

[Function (show_contact)]
rem ' -----------------------------------------------------------------
rem ' show_contact
rem ' -----------------------------------------------------------------

show_contact:
win_contacts$=fngb__get_screen$(win_id_contacts$,win_contacts$)
temp$=ctrl(gb__sysgui,options_id,1,gb__win.contacts)
if pos("<"=temp$) or pos(">"=temp$) or temp$="" then return
if pos("New"=temp$)=1 and security_level>4 then
  action$="add" 
  dim tmm03$:fattr(tmm03$)
else
  action$="edit"
  readrecord(tmm03,key=tmm01key$+temp$(1,3),dom=*next)tmm03$
fi

rem ' is the contact an end user
if cvs(tmm03.end_user_nbr$,3) <> "" then
    found = 0
    read record(snmeu, key = tmm03.end_user_nbr$, dom=*next)snmeu$; found = 1
    if found = 1 then
        eu_position$ = "END USER - " + cvs(snmeu.company_name$,3)
        eu_flag = 1
    else
        eu_position$ = ""
        eu_flag = 0
    fi
else
    eu_position$ = ""
    eu_flag = 0
fi

win_contacts.name$=cvs(tmm03.contact_name$,3)
if eu_position$ <> "" then
    win_contacts.position$=cvs(eu_position$,3)
else
    win_contacts.position$=cvs(tmm03.position$,3)
fi
cncode$=tmm03.country_code$
win_contacts.phone$=fnusphone$(tmm03.phone_number$)
win_contacts.extension$=cvs(tmm03.extension$,3)
win_contacts.email$=cvs(tmm03.e_mail$,3)
win_contacts.addr1$=cvs(tmm03.address_1$,3)
win_contacts.addr2$=cvs(tmm03.address_2$,3)
win_contacts.city$=cvs(tmm03.city$,3)
win_contacts.zip$=cvs(tmm03.zip_code$,3)
gosub state_list
win_contacts.state$=state_list$
gosub country_list
win_contacts.country$=country_list$
win_contacts.adv_y$=str(tmm03.send_lit$="Y")
win_contacts.adv_e$=str(tmm03.send_lit$="E")
win_contacts.adv_n$=str(pos(tmm03.send_lit$="YE")=0)
win_contacts.main_contact$=str(tmm03.primary_contact$="Y")
win_contacts.ap_contact$=str(tmm03.ap_contact$="Y")
win_contacts.sam_contact$=str(tmm03.sam_contact$="Y")
win_contacts.announcements$=str(tmm03.announcements$="Y")
win_contacts.perpetNotice$=str(tmm03.perpet_notice$="Y")
win_contacts$=fngb__put_screen$(win_id_contacts$,win_contacts$)
if action$="add" then focus_id=name_id else focus_id=options_id
print (gb__sysgui)'focus'(focus_id)
if primary$<>"" and pos(primary$=tmm03$)<>1 and tmm03.primary_contact$<>"Y" then 
  print (gb__sysgui)'disable'(main_id)
else
  print (gb__sysgui)'enable'(main_id)
fi
if a_p$<>"" and pos(a_p$=tmm03$)<>1 and tmm03.ap_contact$<>"Y" then 
  print (gb__sysgui)'disable'(ap_id)
else
  print (gb__sysgui)'enable'(ap_id)
fi
if sam_con$<>"" and pos(sam_con$=tmm03$)<>1 and tmm03.sam_contact$<>"Y" then 
  print (gb__sysgui)'disable'(sam_id)
else
  print (gb__sysgui)'enable'(sam_id)
fi
rem ' an end user cannot be the main contact or the ap contact or ths sam contact
if eu_flag then
      print (gb__sysgui)'disable'(main_id)
      print (gb__sysgui)'disable'(ap_id)
      print (gb__sysgui)'disable'(sam_id)
fi

return

[Function (show_invoice)]
rem ' -----------------------------------------------------------------
rem ' show_invoice
rem ' -----------------------------------------------------------------

show_invoice:
temp$=ctrl(gb__sysgui,invoice_list_id,1)

if cvs(dd_invoice_number$,3) <> "" then
    temp$ = dd_invoice_number$
    dd_invoice_number$ = ""
    invList! = BBjAPI().getSysGui().getActiveWindow().getControl("invoice_list")
    for invListRow = 0 to count - 1
        invListRow$ = invList!.getItemAt(invListRow)
        if pos(temp$ = invListRow$) = 1 then
            invList!.selectIndex(invListRow)
            break
        fi
    next invListRow
fi

if len(temp$)<7 then return
k$=artkey$+temp$(1,7)+"000"
readrecord(art03,key=k$)art03$
rec$="Order: "+art03.order_number$+fill(17)+"Ship via: "+cvs(art03.ar_ship_via$,3)+$0a$
dim x$(26)
read(arm10,key=art03.firm_id$+"F"+art03.slspsn_code$,dom=*next)x$(1)
rec$=rec$+"Sales: "+x$(4)+" Taken by: "+art03.ord_taken_by$+$0a$
dim x$(25)
read(arm10,key=art03.firm_id$+"A"+art03.terms_code$,dom=*next)x$(1)
dim x1$(25)
read(arm10,key=art03.firm_id$+"I"+art03.disc_code$,dom=*next)x1$(1)
rec$=rec$+"Terms: "+x$(6)+fill(4)+"Discount: "+cvs(x1$(6),3)+$0a$
rec$=rec$+fill(51,"_")+$0a$+$0a$
rec$=rec$+"Line  Description         Qty Unit Price  Extension"+$0a$
rec$=rec$+fill(51,"_")+$0a$+$0a$
m$="###,###.00-"
if art03.invoice_type$="V" then rec$=rec$+fill(6)+"* * * V O I D * *"+$0a$;goto show_invoice_end 
read(art13,key=art03$(1,17),dom=*next)
while 1
  readrecord(art13,end=*break)art13$
  if pos(art03$(1,17)=art13$)=0 then break
  rec$=rec$+art13.line_number$+" "+art13.line_code$+" "+pad(art13.item_number$,20)
  rec$=rec$+str(art13.qty_ordered:"###")+str(art13.unit_price:m$)+str(art13.ext_price:m$)+$0a$
  if cvs(art13.order_memo$,3)<>"" then rec$=rec$+fill(6)+art13.order_memo$+$0a$
  readrecord(art73,key=art13$(1,20),dom=*continue)art73$
  rec$=rec$+fill(6)+"Type: "+art73.type_of_sale$+"  Users:"+str(art73.users:"#####")
  rec$=rec$+cvs(art73.lic_property1$,3)+str(art73.users2:"#####")+art73.lic_property2$+$0a$
  read(art83,key=art73$(1,20),dom=*next)
  xSNlist$ = ""
  while 2
    readrecord(art83,end=*break)art83$
    if pos(art73$(1,20)=art83$)<>1 then break
    temp$=" Activated"
    if art83.action$="D" then temp$=" Deactivated"
    if art83.action$="M" then temp$=" Modified"
    rec$=rec$+fill(6)+art83.serial_nbr$+temp$+$0a$
    xSNlist$ = xSNlist$ + cvs(art83.serial_nbr$,3) + " "
  wend
wend
rec$=rec$+$0a$+fill(31)+"Subtotal:"+str(art03.total_sales:m$)+$0a$
rec$=rec$+fill(31)+" Freight:"+str(art03.freight_amt:m$)+$0a$
rec$=rec$+fill(31)+"     Tax:"+str(art03.tax_amount:m$)+$0a$
rec$=rec$+fill(31)+"   Total:"+str(art03.total_sales+art03.freight_amt+art03.tax_amount:m$)+$0a$
show_invoice_end:
win_invoice_history.invoice_nbr$=art03.ar_inv_nbr$
win_invoice_history.po$=art03.ar_po_number$
win_invoice_history.invoice$=rec$
win_invoice_history$=fngb__put_fields$(win_id_invoice_history$,win_invoice_history$,
:"invoice,invoice_nbr,po")
invoice_dt!.setValue(fnaonjul(art03.invoice_date$))

xInvoiceNbr$ = art03.ar_inv_nbr$
xFirm$ = art03.firm_id$
gosub moveArchiveInvoiceToWebServer

print(gb__sysgui)'focus'(invoice_list_id)
return

[Function (show_partner)]
rem ' -----------------------------------------------------------------
rem ' show_partner
rem ' -----------------------------------------------------------------

show_partner:
readrecord(tmm01,key=tmm01key$)tmm01$
armkey$=tmm01key$(1,8)
readrecord(arm02,key=armkey$+"  ",dom=*next)arm02$
readrecord(arm06,key=armkey$,dom=*next)arm06$
ytd=arm06.ytd_sales+arm06.nmtd_sales
pyr=arm06.pyr_sales
if ar_mm=12 and now_mm=1 then
  ytd=arm06.nmtd_sales
  pyr=arm06.ytd_sales
fi
partner_sales=max(ytd,pyr)
win_partner$=fngb__get_screen$(win_id_partner$,win_partner$)
win_partner.event_chk$=str(events>0)
win_partner.apdir_chk$=str(tmm01.apdir_flag$="Y")
link_list$="No Web Site"+$0a$+"Web Site w/Link to BASIS"+$0a$+"Web Site, No Link to BASIS"+$0a$
index=max(0,pos(tmm01.link_flag$="012")-1)
if tmm01.link_flag$="Y" then index=1;rem "switch from old YN format to lbtn code
link_list$=link_list$+$ff$+str(index)
win_partner.link_lbtn$=link_list$
win_partner.bblist_chk$=str(tmm01.bblist_flag$="Y")
win_partner.plan_chk$=str(tmm01.plan_flag$="Y")
win_partner.qstnr_chk$=str(tmm01.qstnr_flag$="Y")
win_partner.goals_chk$=str(tmm01.goals_flag$="Y")
win_partner.apdir_dt$=""
win_partner.qstnr_dt$=""
win_partner.goals_dt$=""
win_partner.plan_dt$=""
readrecord(arm10,key=tmm01.firm_id$+"I"+tmm01.disc_code$)arm10i$
win_partner.discount$=str(arm10i.disc_percent)+"%"
win_partner$=fngb__put_screen$(win_id_partner$,win_partner$)
if fnnum(tmm01.apdir_dt$) then apdir_dt!.setValue(fnjul(tmm01.apdir_dt$))
if fnnum(tmm01.plan_dt$) then plan_dt!.setValue(fnjul(tmm01.plan_dt$))
if fnnum(tmm01.qstnr_dt$) then qstnr_dt!.setValue(fnjul(tmm01.qstnr_dt$))
if fnnum(tmm01.goals_dt$) then goals_dt!.setValue(fnjul(tmm01.goals_dt$))

return

[Function (state_list)]
rem ' -----------------------------------------------------------------
rem ' state_list
rem ' -----------------------------------------------------------------

state_list:
index=-1
if state_list$<>"" then goto state_loop_end
state_list$="  "+$0a$
read(states,key="",dom=state_loop)
state_loop:
read record (states,end=state_loop_end) states$
state_list$=state_list$+states.state_code$+$0a$
goto state_loop
state_loop_end:
temp=pos(tmm03.state_code$=state_list$)
if temp then index=pos($0a$=state_list$(1,temp),1,0)
temp=pos($ff$=state_list$)
if temp then state_list$=state_list$(1,temp-1)
if index>=0 then state_list$=state_list$+$ff$+str(index)
return

